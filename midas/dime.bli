MODULE DIME(DOLLAR,ENTRIES=($DEXIT,$DEPDB,$DPNTR),FSAVE,TIMING,
                TIMER=EXTERNAL(SIX12))=
BEGIN
REQUIRE DDEFS.BLI[7,107355];
REQUIRE DVDEFS.BLI[7,107355];
REQUIRE UDEFS.BLI[7,107355];
REQUIRE GDEFS.BLI[7,107355];
REQUIRE SDEFS.BLI[7,107355];
REQUIRE RDEFS.BLI[7,107355];

MAP DR$ DREGS$;
MAP U$DYNARY DBASE$;
MAP U$DYNARY DPOOL$;

MAP U$DYNARY DRACK$;

UNDECLARE $DEXIT;

GLOBAL ROUTINE $DEXIT(EXTYPE,EXVAL)=
   BEGIN

   %$DEMIT PROCESSES THE GAMMA FILE TOKEN GV$SEXIT
     THE ROUTINE BEGINS BY SETTING DREGS$[1] TO 
   CONTAIN SYMBOLICALLY THE AC0;
   THEN , IF WE HAVE A $SEXIT WITH A RETURN VALUE OF TRUE

   DREGS$[2] IS SET TO SYMBOLLIC SHORT LITERAL 1;

   THE GENERATED CODE SHOULD LOOK LIKE THE FOLLOWING:

   (NO RETURNED VALUE): POPJ 0,

   (RETURN TRUE):       HRRZI 3,1
                        POPJ 0,

   (RETURN FALSE):      SETZ 3,
                        POPJ 0,

   /JS..............5/4/74
   %

   $DSETAC(1,0);
   $DSETAC(2,2);


   IF (.EXTYPE EQL GV$RESULT) AND (.EXVAL EQL GV$TRUE) THEN
        $DPCP(DTPV$SEXTT)

   ELSE IF (.EXTYPE EQL GV$RESULT) AND (.EXVAL EQL GV$FALSE) THEN
        $DPCP(DTPV$SEXTF)

   ELSE $DPCP(DTPV$PEXIT);

   %PROCESS JUMP AROUND%
   $DEM1W(DV$SET,DBV$LITBASE,0,.DXSAFE$);
   $DEUCE(DBV$LITBASE);
   $DEMIT(DV$DRELR,0,DBV$PROGBASE,0,.DBASE$[DBV$PROGBASE]);
   $DEM1W(DV$SET,DBV$LITBASE,0,.DBASE$[DBV$LITBASE]);

   END;


UNDECLARE $DEPDB;

GLOBAL ROUTINE $DEPDB(STP)=
   BEGIN
   %EMIT ONE EACH PROCEDURE DEFINITION BLOCK..
    AFTER EMITTING A POPJ TO RETURN TO OUTSIDE
   CALLER..

    SINCE THIS IS RELEASE "A" OF MIDAS, NO PARAMETERS WILL
   BE NOTED..
    
    THE PDB WILL BE FILLED AS INDICATED BY SECTION 2.9.11
    OF THE MIDAS PROJECT NOTEBOOK

    /JS............5/7/74
    %

   LOCAL A[2];
   LOCAL P,Q,R,T;
   LOCAL DBR,DFOR,DBS,DFOS;
   MAP S$ P;
   MAP PDW$I Q;
   MAP S$ R;
   LOCAL XNAME,DBN,DOFN;

   IF .DXLASTRSB$ NEQ -1 THEN
        BEGIN
        %EMIT A CALL TO $YPURGE TO CLEAN OUT THOSE TEMP RELATIONS%
        $DRNCL(RTRV$YPURG,0);
        END;

   %ZERO-TH, FORCE ADDRESS IF AN ABORT OCCURED%
   IF .DXEX$ EQL 0 THEN
        BEGIN
        %AN ABORT OCCURED..SO FIX UP THAT ADDRESS!%
        $DEM1W(DV$SET,DBV$LITBASE,0,.DXEXIT$);
        $DEUCE(DBV$LITBASE);
        $DEMIT(DV$DRELR,0,DBV$PROGBASE,0,.DBASE$[DBV$PROGBASE]);
        $DEM1W(DV$SET,DBV$LITBASE,0,.DBASE$[DBV$LITBASE]);
        END;

   %FIRST , FORCE THE RETURN USING A POPJ%
   $DSETAC(1,0);
   $DSETAC(2,2);

   $DPCP(DTPV$PEXIT);


   %NOW , GET THE NAME%
   P_$SGSTE(.STP);
   XNAME_$DNAME(.P[S$STP1]);
   %XNAME NOW HAS STP TO LIE FOR NAME%
   R_$SGSTE(.XNAME);
   %R NOW HAS LIE FOR NAME%
   DBN_.R[S$DB];
   DOFN _ .R[S$DFO];
   $SFRNCH(.R); %FREE R%

   $DEUCE(DBV$PROCBASE); %SET NOW FOR PDB%
   %THE FIRST WORD..STATUS,TRACE,PARAMETER COUNT, AND LAST RB%
   Q[PD$STATUS]_0;
   Q[PD$TRACE]_0;
   Q[PD$PARCT]_.P[S$NPARA];
        %THERE WERE NO RB'S..%
        Q[PD$RB]_0;
        DBR_0;
   DXPDBWD$_.Q;
   $DEMIT(DV$DRELR,0,.DBR,0,.Q);

   %THE SECOND WORD..NAME AND TFE CHAIN(POINTS TO FIRTS ENTRY)%
   A[0]_ (.P[S$STP1])^18;    %LEFT HALF HAS NAME POINTER%
   A[1]_ .DOFN ^ 18;   %LEFT HALF HAS REL ADDR OF NAME STR%
   $DEMVW(DV$DRELB,.DBN,DBV$LNEBASE,0,A[0],2);

   %THE THIRD WORD...PROGRAM ADDRESS%
   Q_0;
   Q[PD$PROG]_0;
   $DEMIT(DV$DRELR,0,DBV$PROGBASE,0,.Q);

   %THE FOURTH WORD..MYTHICAL UDT DATE%
   Q[PD$DATE]_-1;
   $DEMIT(DV$CNST,0,0,0,.Q);

   %THE FIFTH WORD...LAST RSB AND LAST SCB%
   A[0]_0;    %RSBF%
   IF .DXLASTRSB$ EQL -1 THEN
        BEGIN
        %THERE WERE NO RSB'S%
        A[1]_0;
        DBR_0;
        END
   ELSE
        BEGIN
        %THERE WAS AT LEAST ONE RSB%
        A[1]_(.DXLASTRSB$ ^ 18);
        DBR_DBV$RSBBASE;
        END;

   IF .DXLASTSCB$ EQL -1 THEN
        BEGIN
        %THERE WERE NO SCB'S%
        DBS_0;
        END
   ELSE
        BEGIN
        A[1]_.A[1]+.DXLASTSCB$;
        DBS_DBV$SCBBASE;
        END;

   $DEMVW(DV$DRELB,.DBR,.DBS,0,A[0],2);

   %SEVERAL UNCOUNTED WORDS OF PARAMETER ADDRESSES GO HERE%


   %NOW EMIT A WORD OF PARAMETER FOR EACH PARAM IN THE PROCEDURE%
   INCR I FROM 0 TO .P[S$NPARA]-1 DO
        BEGIN
        %GET THE PARAMETER%
        $DGFIE(.(P[S$PARASTP])[.I],1);
        %THEN EMIT IT OUT OF DREGS$[*,1]%
        $DEUCE(DBV$PROCBASE);
        $DEMIT(DV$DRELR,0,.DREGS$[DR$BASE,1],.DREGS$[DR$STP1,1],
                .DREGS$[DR$OFFSET,1]);
        END;

   $DEUCE(DBV$PROCBASE);
   %THE SIXTH WORD IS THE NUMBER OF RUN TIME ROUTINES%
   Q_0;
   Q[PD$RTCT]_.DXRACKCT$;
   $DEMIT(DV$CNST,0,0,0,.Q);

   DBASE$[DBV$PROCBASE]_.DBASE$[DBV$PROCBASE]+1+.DXPDBSZ$+2*.DXRACKCT$;

   INCR I FROM 1 TO .DXRACKCT$ DO
        BEGIN
        %EMIT RUN TIME ROUTINE NAMES AND SPACES%
        T_0;

        WHILE (.RTBL$[.T] NEQ 0) AND (.RTBL$[.T+1] NEQ .DRACK$[.I]) DO
           T_.T+2;

        %IF .RTBL[.T]=0, AN ERROR SHOULD BE EMITTED HERE%

        $DEMIT(DV$CNST,0,0,0,.RTBL$[.T]);
        $DEMIT(DV$CNST,0,0,0,#777777);

        END;

   $SFRNCH(.P);
    
   END;

UNDECLARE $DPNTR;

GLOBAL ROUTINE $DPNTR(STP)=
BEGIN
   %$DPNTR IS CALLED WHEN A GV$PROC   TOKEN
   IS SEEN ...
   THE FOLLOWING ACTIONS TAKE PLACE:
   (1) A DV$NOTE WITH THE PROC-NAME IS ISSUED;
   (2) $DPCP IS CALLED TO GENERATE A .ENT.5 ENTRY
       SEQUENCE.

   /JS .....    5/8/74
   %

   LOCAL P,T;
   MAP S$ P;
   LOCAL CT;

   P_$SGSTE(.STP);   %GET NAME ENTRY%
   CT_.P[S$NPARA];
   T_.P[S$STP1]; %AT LEAST THIS POINTS TO THE NAME ENTRY%
   $SFRNCH(.P); %FREE PROCEDURE DEFINITION ENTRY%
   P_$SGSTE(.T);  %SHOULD BE THE NAME%
%DEBUG%

%DEBUG%
   $DEMVW(DV$NOTE,0,0,0,(P[S$NAME])[1],.P[S$NCHAR]);

   $SFRNCH(.P);  %DONE WITH SYMBOL TABLE FOR NOW%

   $DSETAC(1,0);
   $DSETAC(2,2);
   
   INCR T FROM 11 TO 15 DO
        $DSETAC(.T,.T);

   $DEUCE(DBV$PROGBASE);
   $DPCP(DTPV$PENTR);

   $DEUCE(DBV$LITBASE);
   $DEMIT(DV$CNST,0,0,0,0);    %THIS WILL LATER BECOME THE ADDRESS
                                OF THE INITIALIZATION CODE%
   DREGS$[DR$BASE,1]_DBV$LITBASE;
   DXBACK$_DREGS$[DR$OFFSET,1]_.DBASE$[DBV$LITBASE];
   DREGS$[DR$STP1,1]_0;
   DREGS$[DR$DDTYPE,1]_DDV$1WD;
   $DPCP(DTPV$JRSTI);  %EMIT A JRST INIT,I%
   $DINCB(DBV$LITBASE);
   DXWHERE$_.DBASE$[DBV$PROGBASE]; %REMEMBER WHERE TO RETURN TO%

   IF .CT GTR 0 THEN
        BEGIN
        %A CALL TO $VCHK IS IN ORDER%
        %BUT FIRST FIX PDB SIZE%
        DXPDBSZ$_.DXPDBSZ$+.CT;
        %NOW THE CALL%
        $DGSAD(DBV$PROCBASE,0,3);
        $DRNCL(RTRV$VCHK,1);
        END; %EASY..WASN'T IT%
   %NOW SET UP RPDB$%
   $DGSAD(DBV$PROCBASE,0,3);
   $DRNCL(RTRV$YSTRT,1);
   %THIS SHOULD MAKE THE PDB VISIBLE TO SKIM ET AL..2/1/75%


   END;
END ELUDOM
