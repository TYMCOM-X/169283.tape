MODULE JOBSTA(DOLLAR, FSAVE, TIMING, TIMER=EXTERNAL(SIX12),
              ENTRIES=( $CPJOB,  %RETURN JOB NUMBER%
                        $CGPPN,  %RETURN USERS PPN %
                        $CTIME,  %RETURN TIME OF DAY %
                        $CDATE,  %RETURN DATE %
                        $COWNR,  %RETURN PROGRAM OWNER %
                        $CGLOG,  %RETURN LOGIN INFO FROM START%
                        $CUNAME  %RETURN POINTER TO USERS NAME %
                        )
              )=
BEGIN

REQUIRE CDEFS.BLI[7,107355];
UNDECLARE  $CPJOB,      $CGPPN,         $CTIME,
           $CDATE,      $COWNR,         $CGLOG,
           $CUNAME;


MACHOP CALLI= #047;


GLOBAL ROUTINE $CPJOB=  %RETURN JOB NUMBER, STORE IN CPJOB$ %
  BEGIN
  REGISTER Q;
  LOCAL A,B,C;
  CALLI(Q,#30,0,0);     %CALLI AC,30  RETURNS OCTAL NUMBER %
  A_ .Q<0,3> + 8*.Q<3,3> + 64*.Q<6,3>;  %CONVERT TO DECIMAL %
  CPJOB$_0;
  CPJOB$<12,6>_(B_ .A/100) + #20;       %BUILD SIXBIT REPRESENTATION %
  CPJOB$< 6,6>_(C_ .A/10 - .B*10) + #20; %OF THE DECIMAL JOB NUMBER   %
  CPJOB$< 0,6>_ .A - .B*100 - .C*10 + #20;
  RETURN .CPJOB$
  END;

GLOBAL ROUTINE $CGPPN=     %RETURN USERS PPN, STORE IN CPPN$ %
  BEGIN
  REGISTER Q;
  IFSKIP CALLI(Q,#24,0,0) THEN  %PRIVILAGED PROGRAM AND > 1 USER %
        RETURN CPPN$_.Q         % LOGGED IN WITH SAME PPN %
  ELSE RETURN CPPN$_.Q
  END;

GLOBAL ROUTINE $CTIME=0;

GLOBAL ROUTINE $CDATE=0;
 
GLOBAL ROUTINE $COWNR=0;

GLOBAL ROUTINE $CGLOG=0;

GLOBAL ROUTINE $CUNAME= %RETURN POINTER TO CUNAM$, STORE USERS NAME IN ASCII%
  BEGIN
  REGISTER Q,R,S;
  LOCAL    P6, P7;
  Q<18,18>_-1;
  Q< 0,18>_-#22;
  R<18,18>_-1;
  R< 0,18>_-#21;
        % Q  WILL RETURN FIRST SIX SIXBIT CHARACTERS OF USER'S NAME %
        % R  WILL RETURN SECOND SIX SIXBIT CHARACTERS OF USER'S NAME %
  IFSKIP CALLI(Q,#41,0,0) THEN .Q       %GETTAB%
  ELSE RETURN CUNAM$<36,7>;   %ERROR RETURN%
  IFSKIP CALLI(R,#41,0,0) THEN .R
  ELSE RETURN CUNAM$<36,7>;   %ERROR RETURN%
  P7_CUNAM$<36,7>;  %POINT TO DESTINATION FOR ASCII STORE%
  P6_Q<36,6>;     %POINT TO FIRST WORD%
  INCR CTR FROM 1 TO 6 BY 1 DO
        REPLACEI(P7,SCANI(P6)+#40);
  P6_R<36,6>;      %POINT TO SECOND WORD%
  INCR CTR FROM 1 TO 6 BY 1 DO
        REPLACEI(P7,SCANI(P6)+#40);
  RETURN CUNAM$<36,7>
  END;

END
ELUDOM
