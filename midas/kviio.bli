MODULE KVIIO(DOLLAR,ENTRIES=($KVIRD,$KVIWR),
                        FSAVE,TIMING,TIMER=EXTERNAL(SIX12))=

BEGIN

REQUIRE KDEFS.BLI[7,107355];
REQUIRE BTDEFS.BLI[7,107355];
REQUIRE FDEFS.BLI[7,107355];
REQUIRE UDEFS.BLI[7,107355];
UNDECLARE $KVIRD,$KVIWR;


GLOBAL ROUTINE $KVIRD(FCB,PN)=
   BEGIN 
   MAP KFCB$ FCB;
   LOCAL KBT$ BT;
   LOCAL BA,J;

   DECR I FROM .KBTIX$[BT$NBTS] TO 1     % SEARCH BTS FOR PN %
      DO
         BEGIN
         BT_.KBTIX$[BT$TABLE,.I];
         J_1;
         WHILE .J LEQ .BT[BT$SIZE]
            DO IF .PN EQL .BT[BT$PAGE,.J] AND .FCB[K$CHNL] EQL .BT[BT$CHAN,.J]
               THEN
                  BEGIN
                  BT[BT$AGE,.J]_1;
                  RETURN .BT[BT$FIRST]+(.J-1)*KV$PAGS;  % THE PAGE
                                                        IS IN CORE %
                  END
               ELSE J_.J+.BT[BT$NOP,.J];
         END;
   BA_$KGBUF(.FCB,.PN);        %  GET A BUFFER    %
   $KPHYR(.FCB,.BA,.PN);
   RETURN .BA
   END;


GLOBAL ROUTINE $KVIWR(FCB,BA,PN)=
   BEGIN
   MAP KFCB$ FCB;
   LOCAL KBT$ BT;
   LOCAL ANS,I;

   ANS_$KBTIN(.BA);   % SEARCH BTS FOR THIS ENTRY %
   BT_.ANS<18,18>;
   I_.ANS<0,18>;
   IF .BT EQL 0 THEN $KFATL(#726);  %  IF NO BT PANIC  %
   BT[BT$PAGE,.I]_.PN;
   BT[BT$CHAN,.I]_.FCB[K$CHNL];
   BT[BT$AGE,.I]_1;
   BT[BT$DIRTY,.I]_1;
   END;
END ELUDOM
