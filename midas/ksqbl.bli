MODULE KSQBL(DOLLAR,ENTRIES=($KRDBL,$KWRBL,$KSQBL),
                    FSAVE,TIMING,TIMER=EXTERNAL(SIX12))=
BEGIN

REQUIRE UDEFS.BLI[7,107355];
REQUIRE KDEFS.BLI[7,107355];

UNDECLARE $KRDBL,$KWRBL,$KSQBL;

MACRO MIN(A,B)=(IF A LEQ B THEN A ELSE B)&;

GLOBAL ROUTINE $KRDBL(FCB)=
   BEGIN
   MAP KFCB$ FCB;
   LOCAL CNT,ADR,QR;
   CNT_.FCB[K$IDCNT];
   ADR_.FCB[K$IDADR];
   WHILE .CNT GTR 0 DO
      BEGIN
      IF .FCB[K$EOFILE]
         THEN RETURN FCB[K$IDCNT]_.ADR-.FCB[K$IDADR];
      QR_MIN(.CNT,.FCB[K$ICOUNT]-.FCB[K$IDEX]);
      $UMOVE(.QR,(.FCB[K$IBUFL])[.FCB[K$IDEX]],.ADR);
      IF (FCB[K$IDEX]_.FCB[K$IDEX]+.QR) GEQ .FCB[K$ICOUNT]
         THEN $KSQIB(.FCB);
      ADR_.ADR+.QR;
      CNT_.CNT-.QR
      END
   END;


GLOBAL ROUTINE $KWRBL(FCB)=
   BEGIN
   MAP KFCB$ FCB;
   LOCAL CNT,ADR,QR;
   CNT_.FCB[K$ODCNT];
   ADR_.FCB[K$ODADR];
   WHILE .CNT GTR 0
   DO
      BEGIN
      QR_MIN(.CNT,.FCB[K$OCOUNT]-.FCB[K$ODEX]);
      $UMOVE(.QR,.ADR,(.FCB[K$OBUFL])[.FCB[K$ODEX]]);
      IF (FCB[K$ODEX]_.FCB[K$ODEX]+.QR) GEQ .FCB[K$OCOUNT]
         THEN $KSQOB(.FCB);
      ADR_.ADR+.QR;
      CNT_.CNT-.QR
      END
   END;

GLOBAL ROUTINE $KSQBL(FCB)=
   BEGIN
   MAP KFCB$ FCB;
   $KSQIZ(.FCB);
   FCB[K$MODE]_KV$SQBL;
   IF .FCB[K$DRCT] EQL KV$INPUT
      THEN FCB[K$RR]_$KRDBL
   ELSE IF .FCB[K$DRCT] EQL KV$OUTPUT
      THEN FCB[K$WR]_$KWRBL
   ELSE KV$ILMOD
   END;

END ELUDOM
