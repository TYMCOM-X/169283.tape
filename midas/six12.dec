

MODULE SIX12=
BEGIN


!			DEBUGGING  CONTROL  MODULE
!			--------------------------
!
!
!						C. B. WEINSTOCK
!						WM. A. WULF
!						JUNE 1, 1972
!
!
!
!	THIS MODULE  CONTAINS THE DRIVER TO ALLOW INTERACTIVE
!	DEBUGGING OF A BLISS PROGRAM. ESSENTIALLY THE SERVICE
!	PROVIDED BY THIS MODULE IS:
!
!
!		1. MONITOR THE TTY TO ALLOW THE USER TO 'INTERRUPT'
!		   HIS PROGRAM AND ENTER THE DEBUGGING MODE.
!		2. PROVIDE ANALYSIS OF THE SYNTAX OF LINES INPUT
!		   TO THE DEBUGGER, AND SWITCHING TO ANY OF SEVERAL
!		   SPECIFIC DEBUG ROUTINES. A FEW GENERALLY USEFUL
!		   DEBUG ROUTINES ARE INCLUDED IN THIS PACKAGE --
!		   THE INTENTION, HOWEVER, IS THAT THE USER WILL
!		   BUILD HIS OWN ROUTINES TAILORED TO THE PROBLEM
!		   AND USE THIS PACKAGE MERELY TO PROVIDE A STANDARD  
!		   INTERFACE.
!
!
!
!	THE SYNTAX OF INPUT LINES IS:
!
!!	<LINE> == <EMPTY>/<ROUTINE><PARM-LIST>
!	<ROUTINE> == <* ANY PRINT STRING WITHOUT EMBEDDED BLANKS *>
!	<PARM-LIST> == <EMPTY>/<PARM>/<PARM-LIST>,<PARM>
!	<PARM> == <EMPTY>/<ATOM>/<PARM>+<ATOM>/<PARM>-<ATOM>
!	<ATOM> == <NUMBER>/<DDT-NAME>/*/$/.<ATOM>/
!		  <ATOM>[<PARM>]/(<PARM>)
!	<NUMBER> == <INPUT-BASE-NUMBER>/#<OCTAL-NUMBER>/#<DECIMAL-NUMBER-IF-INPUT-BASE-OCTAL>
!	<DDT-NAME> == <IDENTIFIER>/<IDENTIFIER>(<NUMBER>)
!
!
!	FOR EXAMPLE, THE FOLLOWING ARE VALID LINES:
!
!		=  X,.X,.13
!		_  X(3),2
!		GLOP^ 3,#14,.X[.Y],*+1
!		THUD  .(Z[.X+1-.P]-.Y),46
!
!
!
!	THE INTENDED INTERPRETATION OF AN INPUT LINE IS THAT THE
!	NAMED DEBUGGING ROUTINE BE CALLED AND THE SPECIFIED
!	PARAMETER VALUES PASSED TO IT. THE INTERPRETATION
!	OF THE VARIOUS ATOMS IS:
!
!		1. DECIMAL AND OCTAL CONSTANTS REPRESENT THEMSELVES
!		2. DDT-NAMES ARE LOOKED UP IN THE DDT SYMBOL TABLE,
!		   THE INTERPRETATION OF SUCH NAMES IS AS IN BLISS -
!		   THAT IS, THEY ARE AN ADDRESS!
!		3. THE SYMBOL "*" IS THE VALUE OF THE POSITIONALLY
!		   CORRESPONDING PARAMETER ON THE IMMED. PREVIOUS
!		   DEBUG LINE.
!		4. THE SYMBOL "$" IS THE ADDRESS OF A SPECIAL
!		   "RESULT AREA" -- MAY BE USED BY SOME SPECIFIC
!		   DEBUG ROUTINES. DISPLACEMENTS FROM THE "RESULT AREA" MAY
!		   BE ADDRESSED FOR CONVENIENCE AS $N WHERE N IS A NUMBER.
!		5. THE "." OPERATOR IS INTERPRETED AS THE "@" IN BLISS.
!		6. THE SYMBOLS "[" AND "]" DENOTE INTEGRAL WORD 
!		   DISPLACEMENTS -- IE, E1[E2] = (E1+E2).
!
!
!	NOTE:
!	    IF, AT ANY TIME DURING EXECUTION, ANY CHARACTER IS
!	TYPED ON THE TTY THE DEBUG PACKAGE WILL SHORTLY INTERCEPT
!	CONTROL AND WAIT FOR THE USER TO COMPLETE THE DEBUG LINE.
!
!
!
!	USER-SPECIFIC DEBUGGING ROUTINES MAY BE EASILY ADDED TO
!	THE COLLECTION CURRENTLY PROVIDED AS FOLLOWS:
!		
!		1. WRITE THE ROUTINE TO ACCEPT ITS INPUT PARAMETERS
!		   FROM THE ARRAY "DEBUGPARMS". NOTE THENUMBER OF PARMS
!		   IS IN "NDEBUGPARMS".
!		2. ADD TO THE PLIT, "DEBUGROUTS", A PRINT-NAME AND
!		   THE ACTUAL ROUTINE NAME.
!
!
!	THE SET OF DEBUG ROUTINES PROVIDED IN THIS VERSION ARE:
!
!		= P1,...,PN	PRINT THE VALUES OF P1,...,PN
!		/ P1,P2		PRINT THE VALUES OF P1,...,P1+P2-1
!		_ P1,P2		ASSIGN P1_P2
!		<EMPTY>		NO ACTION
!		DDT		CALL DDT (IN CMU-DDT AN $P IN
!				DDT WILL CAUSE RESUMPTION IN DEBUG MODE)
!		GO		RESUME EXECUTING USER PROGRAM
!		BREAK P1,...,PN SET BREAK POINTS AT THE HEADS OF
!				ROUTINES P1,...,PN. THIS IS A BREAK
!				TO THIS DEBUG SYSTEM (NOT DDT).
!		DBREAK P1,..,PN REMOVE BREAK POINTS SET AS ABOVE.
!		ABREAK P1,,,PN	SET BREAK POINTS AT THE EXIT OF ROUTINES
!				P1,,,PN. THIS IS A BREAK TO THE DEBUG
!				SYSTEM (NOT DDT). THE VREG IS PRINTED.
!		DABREAK P1,,,PN	REMOVE ABREAK POINTS SET AS ABOVE.
!		CALLS		DISPLAY THE STACK OF ROUTINE CALLS
!		CALL+		DISPLAY CALL STACK AND LOCALS --
!				THE LATTER MAY NOT BE VERY USEFUL
!				UNLESS THE USER IS VERY FAMILIAR
!				WITH THE BLISS RUN-TIME ENVIRONMENT.
!		CALL N		PRINT THE LAST N CALLS ON STACK. IF
!				IF N IS OMITTED, PRINT LAST CALL.
!		LCALL N		SAME AS ABOVE BUT ALSO DISPLAYS LOCALS.
!		IBASE N		SETS THE INPUT NUMBER BASE TO N (N IS ALWAYS DECIMAL)
!				(1<N<11). NOTE THAT # IMMEDIATELY
!				PRECEEDING A NUMBER ALWAYS MEANS OCTAL
!				INPUT EXCEPT WHEN THE DEFAULT BASE IS
!				OCTAL, IN WHICH CASE # MEANS DECIMAL.
!				IF N IS OMITTED IT PRINTS THE CURRENT
!				INPUT BASE IN DECIMAL.
!		OBASE N		SETS THE OUTPUT NUMBER BASE TO N (N IS ALWAYS DECIMAL)
!				(1<N<11). IF N IS OMITTED IT PRINTS
!				THE OUTPUT BASE IN DECIMAL.
!		WBASE N		SETS THE MAXIMUM SIZE OF DISPLACEMENT
!				THAT WILL BE PRINTED IN 'BASE+DISPLACEMENT'
!				OUTPUT TO N. (W IS FOR WULF FOR HISTORICAL
!				REASONS.)  INITIALLY SET 1000(OCTAL).
!				IF N IS OMITTED, THE CURRENT WBASE IS
!				PRINTED IN THE CURRENT OUTPUT BASE.
!		SETTRACE	TURNS ON TRACE MODE. WILL DISPLAY
!				CALL STACK FOR ROUTINES AS THEY
!				ARE ENTERED AND LEFT.
!		TRACE		EQUIVALENT TO SETTRACE + GO.  EXECUTION
!				IS RESUMED IN TRACE MODE.  TRACE MODE
!				ENDS WHEN ANYTHING IS TYPED AT THE TERMINAL
!				OR A BREAK POINT IS REACHED.
!		EX P1,...,PN	CALL THE PROCEDURE NAMED P1 WITH
!				PARAMETERS P2,...,PN. (N<6)
!				RETURNS THE VALUE IN "$".
!		EV P1,...,PN	CALL THE PRECEDURE NAMED P2 WITH
!				PARAMETERS P3,...,PN. (N<7)
!				THE VALUE IS PLACED IN THE LOCATION 
!				SPECIFIED BY P1.
!		PRS P1,,,PN	PRINTS A LIST OF ALL DDT SYMBOL TABLE
!				ENTRIES FOR P1,,,PN.
!		DISABLE		TURNS OFF TYPEIN MONITORING, ALLOWS TYPE-AHEAD.
!				TO RESUME TYPEIN MONITORING ENTER
!				DDT AND DO PUSHJ SIXENABLE$X.
!		OPAQUE P1,..,PN	MAKES THE NAMED ROUTINES OPAQUE TO TRACING,
!				I.E., IF THE ROUTINE IS ENTERED WITH TRACING TURNED
!				ON, TRACING IS TURNED OFF UNTIL THE MATCHING ROUTINE
!				EXIT IS ENCOUNTERED.  ALL OPAQUE FLAGS ARE RESET IF
!				A BREAK OR TTY INTERUPT OCCURS.
!		DOPAQUE P1,.,PN	UNDOES THE EFFECT OF OPAQUE, I.E. THE
!				NAMED ROUTINES ARE NO LONGER OPAQUE.
!		MONITOR P1,,PN	MONITORS THE LOCATIONS P1,,PN. IF
!				VALUE CHANGES, TELLS WHERE AND HOW.
!		DMONITOR P1,,PN	TURNS OFF MONITORING.
!
!
!
!	NOTE:
!	    IN ORDER TO USE THIS PACKAGE THE MODULES TO BE
!	DEBUGGED MUST BE COMPILED WITH THE PROPER COMPILATION
!	CONTROL, NAMELY:
!
!		1. "TIMER=EXTERNAL(SIX12)" MUST APPEAR IN THE MODULE HEAD.
!		2. EITHER "/F/T" MUST BE USED IN THE COMMAND STRING,
!		   OR "FSAVE,TIMING" MUST APPEAR IN THE MODULE HEAD.
!
!	USE NOTES:
!
!	    1.  BECAUSE IT IS SOMETIMES USEFUL TO ENTER THIS PACKAGE 
!		IMMEDIATELY UPON THE START OF EXECUTION (AFTER STACK
!		INITIALIZATION), A FACILITY TO DO THIS HAS
!		BEEN PROVIDED.  ENTER DDT AND TYPE:
!
!			SIX1..$:	STARTFLG! 1 <CRLF>
!			$G  
!
!		IF YOUR PROGRAM DOES NOT USE THE NAME STARTFLG, THEN
!		THE SIX1..$: CAN BE ELIMINATED. (NOTE: $ IS ALTMODE)





!
!
!
!
!
! INPUT-OUTPUT FOR PDP-10 DEBUGGING (ALL TTY I/O)
! -----------------------------------------------

MACHOP TTCALL=#51, CALLI=#47;
MACRO	HALT=CALLI(0,#12)$;
BIND MODNAME=0;


BIND NDPARMS=20;

BIND BGOBASE=20,TRACEBASE=0;

OWN GOBASE;
EXTERNAL JOBSYM;

OWN	GOFLG,DCNT,CHAR,NC,ACCUM[2],PACCUM,VAL,ERRORFLG,PINDEX,STARTFLG,
	BUFF[30],PBUFF,NDEBUGPARMS,VVR,TRACEFLG,RTYPE,PNAME,ENABFLG,
	DEBUGPARMS:DEBUGRESULTS:OLDPARMS[NDPARMS];
OWN	ROUTS[50],NROUTS;
OWN	OLDVALS[50],NVALS;
OWN	OPQCNT;
OWN DBASE,IDBASE,WDBASE;

BIND CRLFSTR=PLIT '?M?J';	! STRING FOR CRLF MACRO

MACRO 	INC=(REGISTER Q; TTCALL(4,Q); .Q)$,
	OUTC(Z)=(REGISTER Q; Q_(Z); TTCALL(1,Q))$,
	OUTSA(Z)=TTCALL(3,Z)$,
	OUTS(Z)=OUTSA(PLIT ASCIZ Z)$,
	OUTM(C,N)=DECR I FROM (N)-1 TO 0 DO OUTC(C)$,
	CR=OUTC(#15)$,
	LF=OUTC(#12)$,
	NULL=OUTC(0)$,
	CRLF=OUTSA(CRLFSTR)$,
	TAB=OUTC(#11)$,
	OUTDEFAULT(Z)=OUTN(Z,.DBASE,1)$,
	OUTRDEF(Z,R)=OUTN(Z,.DBASE,R)$,
	OUTD(Z)=OUTN(Z,10,1)$,
	OUTO(Z)=OUTN(Z,8,1)$,
	OUTDR(Z,N)=OUTN(Z,10,N)$,
	OUTOR(Z,N)=OUTN(Z,8,N)$;

ROUTINE OUTN(NUM,BASE,REQD)=
    BEGIN OWN N,NNP,B,RD,T;
    ROUTINE XN=
	BEGIN LOCAL R;
	IF .N EQL 0 THEN RETURN(OUTM(" ",.RD-.T);IF .NNP THEN OUTC("-"));
	R_.N MOD .B; N_.N/.B; T_.T+1; XN(); OUTC(.R+"0")
	END;
    IF .NUM LSS 0 THEN NNP_T_1 ELSE NNP_T_0;
    B_.BASE; RD_.REQD; N_ABS(.NUM); 
    IF .N NEQ 0 THEN XN() ELSE
	(OUTM(" ",.RD-1); OUTC("0"));
    END;



!!!!!   INTERACTIVE DEBUGGING INTERFACE AND SUPPORT   !!!!!!
!       -------------------------------------------


FORWARD ERROR;


ROUTINE SDDTFA(X)=
    BEGIN REGISTER R; EXTERNAL JOBSYM;
    ! GIVEN A SYMBOL, SEARCH DDT SYMBOL-TABLE FOR ITS ADDRESS
    R_.JOBSYM;
    WHILE (R_.R+#2000002) LSS 0 DO
	IF .(.R)<0,32> EQL .X THEN RETURN @(.R+1);
    0
    END;


ROUTINE NSDDTFA(X,V)=
    BEGIN REGISTER R;
    ! GIVEN A SYMBOL SEARCH FOR THE ADDRESS OF ITS NEXT OCCURANCE
    ! STARTING AT .V
    R_.V;
    WHILE (R_.R+#2000002) LSS 0 DO
	BEGIN
	IF .(.R)<32,2> EQL MODNAME THEN PNAME_.R;
	IF .(.R)<0,32> EQL .X THEN RETURN .R
	END;
    0
    END;

ROUTINE SDDTFS(X)=
    BEGIN EXTERNAL JOBSYM; REGISTER R,N; OWN Z;
    ! GIVEN AN ADDRESS, SEARCH DDT SYMBOL-TABLE FOR THE SYMBOL
    Z_0; N_Z; R_.JOBSYM+1;
    WHILE (R_.R+#2000002) LSS 0 DO
	IF (@@R-.X) LEQ 0 THEN
	    IF (@@R-@@N) GEQ 0 THEN
		IF @(@R-1) GEQ 0 THEN N_.R;
    .N-1
    END;


ROUTINE MODDDT(X)=
    BEGIN
    REGISTER R;
    ! GIVEN A START ADDRESS X FIND FIRST MODULE NAME
    R_.X;
    WHILE (R_.R+#2000002) LSS 0
	DO IF .(.R)<32,2> EQL MODNAME THEN RETURN @.R;
    @(.R-2)
    END;


ROUTINE F50TO7(X)=
    BEGIN
    ! CONVERT BASE 50 CHARACTER TO ASCII CHARACTER
    IF .X EQL 0 THEN 0 ELSE
    #40+(IF .X LEQ #12 THEN .X+#17 ELSE
	IF .X LEQ #44 THEN .X+#26 ELSE
	IF .X EQL #45 THEN #16 ELSE
	IF .X EQL #46 THEN #04 ELSE #05)
    END;

ROUTINE F7TO50(X)=
    BEGIN
    ! CONVERT ASCII CHARACTER TO BASE 50 CHARACTER
    IF .X EQL 0 THEN RETURN 0;
    X_.X-#40;
    IF .X EQL #16 THEN #45 ELSE
    IF .X EQL #04 THEN #46 ELSE
    IF .X EQL #05 THEN #47 ELSE
    IF .X LEQ #31 THEN .X-#17 ELSE .X-#26
    END;


ROUTINE BASE50(X)=
    BEGIN OWN Q,N,Z;
    ! GET BASE 50 REPRESENTATION OF SYMBOL @.X
    Z_N_0; X_(.X<0,18>-1)<1,7>;
    WHILE (Q_SCANI(X)) NEQ 0 DO 
%1%	IF (N_.N+1) GTR 6 THEN RETURN .Z ELSE Z_#50*.Z+F7TO50(.Q);
    .Z
    END;

ROUTINE PRSYM50(X)=
    BEGIN LOCAL R;
    ! PRINT NAME GIVEN IN BASE50
    IF (X_.X AND #37777777777) NEQ 0 THEN
	(R_.X MOD #50; PRSYM50(.X /#50); OUTC(F50TO7(.R)));
    END;

ROUTINE PRDISP(X)=
%1%    BEGIN EXTERNAL DDTEND;
    ! PRINT BOTH HALVES OF .X IN "BASE+DISP" FORM
%1%	ROUTINE PRDISPH(Z)=
		    BEGIN
%1%		    REGISTER L,M;
%1%		    IF .Z LSS DDTEND<0,0> THEN RETURN OUTDEFAULT(.Z);
		    L_SDDTFS(.Z);  M_.Z<0,18>-@(.L+1);
%1%		    IF .M GTR .WDBASE THEN OUTDEFAULT(.Z)
			ELSE
			BEGIN
			PRSYM50(@@L);
			IF .M NEQ 0 THEN ( OUTC("+"); OUTDEFAULT(.M))
%1%			END
		    END;
%1%  IF .X<18,18> NEQ 0
%1%	THEN (PRDISPH(.X<18,18>); OUTS(',,'));
%1%  PRDISPH(.X<0,18>);
    END;

ROUTINE PRXDISP(X)=
    ! PRINT ONLY BASE OF .X<0,18>
%1%  PRSYM50(@SDDTFS(.X<0,18>));


ROUTINE PRG(B,T)=
    ! PRINT A CONT. SET OF WORDS FOR STACK DISPLAY
    INCR I FROM 1 TO .T DO
	BEGIN 
%1%	OUTDEFAULT(.I); OUTS(': '); PRDISP(@(.B+.I-1));
%1%	IF NOT(.I) AND .I LSS .T  THEN CRLF; TAB;
	END;

ROUTINE PRCALL(F,CALLED)=
	! PRINT A SINGLE ROUTINE CALL WITH ITS PARMS
	BEGIN LOCAL NP,LP,CALLER;
	CALLER_.(.F-1)<0,18>-1;
%1%	NP_IF .(@(.F-1))<23,13> NEQ (#274^4 OR SREG<0,0>) THEN 0 ELSE
	    .(@@(.F-1))<0,18>;
	LP_.F-1-.NP;
	PRXDISP(.CALLED); TAB; OUTC(#50);OUTC("_"); PRDISP(.CALLER); OUTC(#51);
	TAB; PRG(.LP,.NP);
	.CALLER<0,18>+.NP^18
	END;

ROUTINE PSTK(TOG)=
%1%	BEGIN LOCAL F,CALLED,NL;
	! DISPLAY THE CALL STACK, "TOG" CONTROLS LOCALS DISPLAY
%1%	F_@@@@@FREG; NL_@@@.FREG-.F-3;
	CALLED_.(@(.F-1)-1)<0,18>; CRLF;
	UNTIL (.CALLED<0,18> EQL #777777) OR ((DEBUGPARMS[1]_.DEBUGPARMS[1]-1) LSS 0) DO
	    BEGIN
	    CALLED_PRCALL(.F,.CALLED<0,18>);
%1%	    IF .TOG THEN (CRLF; TAB; PRG(.F+1,.NL));
%1%	    CRLF;
	    NL_.F-@@F-.CALLED<18,18>-2;
%1%	    IF NOT ((@@F LSS @F) AND (@@F GTR .BREG<0,18>)) THEN RETURN;
	    F_@@F;
%1%
	    END;
    CRLF;
    END;




! THE DEBUG PROCESSING ROUTINES
!-------------------------------





ROUTINE XNULL=.VREG;

ROUTINE EDDT=
	BEGIN EXTERNAL JOBOPC,JOBDDT; MACHOP JRST=#254;
	JOBOPC_XNULL<0,0>+1;
	JRST(0,@JOBDDT);
	END;

ROUTINE DISPLAY=
	BEGIN
	INCR I FROM 1 TO .NDEBUGPARMS DO 
	    ( CRLF; OUTRDEF(.DEBUGPARMS[.I],12); TAB; PRDISP(.DEBUGPARMS[.I]); );
	CRLF;
	END;

ROUTINE SLASH=
%1%	BEGIN
%1%	MACRO A=DEBUGPARMS[1]$;
%1%	CRLF;
%1%	DECR I FROM .DEBUGPARMS[2] TO 1 DO
	    BEGIN
%1%	    PRDISP(.A);OUTS('/');TAB;
%1%	    OUTDEFAULT(@(.A)); TAB; PRDISP(@(.A));
%1%	    A_.A+1;
%1%	    CRLF;
	    END;
%1%
	END;



!!  DEBUG INTEREST ROUTINES  !!
!   -----------------------

!	THE VECTOR ROUTS CONTAINS INFORMATION ABOUT EACH ROUTINE
!	IN WHICH THE DEGUG SYSTEM IS 'INTERESTED'.  THE VARIABLE
!	NROUTS (INITIALIZED TO -1) CONTAINS THE INDEX OF THE LAST
!	VALID ENTRY IN ROUTS.  THE STRUCTURE OF EACH ENTRY IN ROUTS
!	IS
!		!------------------+------------------!
!		! INTEREST BITS    !  ROUTINE ADDRESS !
!		!------------------+------------------!
!
!	TWO VALUES ARE ASSOCIATED WITH EACH BIT IN THE INTEREST BITS
!	FIELD.  IF BIT (35-N) INDICATES AN ABC TYPE INTEREST, THEN
!	ABCF IS A MACRO FOR THE BIT POSITION IN THE WORD,I.E. N,1.
!	ABCV IS A CONSTANT WITH A 1 IN THE CORRESPONDING BIT, I.E. 1^N.
!
!	THE MACRO SETBIT(VAL) PERFORMS THE FOLLOWING ACTIONS
!	FOR EACH ROUTINE IN DEBUGPARMS:
!		1) INSERT THE ROUTINE INTO ROUTS IF IT IS NOT
!		   ALREADY THERE. (CINSERT()).
!		2) TURNS ON THE INTEREST BITS INDICATED BY VAL.
!
!	THE MACRO UNSETBIT(VAL) DOES THE FOLLOWING FOR EACH ROUTINE
!	IN DEBUGPARMS:
!		1) TURN OF THE INTEREST BITS INDICATED BY VAL.
!		2) IF THE INTEREST BIT FIELD OF ANY ENTRY BECOMES
!		   ZERO, REMOVE THAT ENTRY FROM ROUTS.


STRUCTURE XVECTOR[I,J,K]=(.XVECTOR+.I)<.J,.K>;

MACRO BITFLD(N)=N,1$,
      BITVAL(N)=1^(N)$;

! BIT FIELD DEFINITIONS

MACRO
	BREAKF=BITFLD(18)$,
	ABREAKF=BITFLD(19)$,
	OPQF=BITFLD(20)$,
	IDIDITF=BITFLD(21)$,
	VCHGF=BITFLD(22)$;


BIND
	BREAKV=BITVAL(18),
	ABREAKV=BITVAL(19),
	OPQV=BITVAL(20),
	IDIDITV=BITVAL(21),
	VCHGV=BITVAL(22);


MACRO SETBIT(VAL)=
	BEGIN
	LOCAL L;
%1%	DECR I FROM .NDEBUGPARMS TO 1 DO
	    (L_CINSERT(.DEBUGPARMS[.I]);
	     ROUTS[.L]_.ROUTS[.L] OR (VAL);IF (VAL) EQL VCHGV THEN
		(MAP XVECTOR ROUTS;OLDVALS[.L]_.(.ROUTS[.L,0,18])<0,36>) );
	END$;

MACRO UNSETBIT(VAL)=
	BEGIN
%1%	DECR I FROM .NDEBUGPARMS TO 1 DO CREMOVE(.DEBUGPARMS[.I],(VAL));
	END$;

ROUTINE CFINDR(R)=	! CONDITIONAL FIND
	! RETURN THE INDEX OF ROUTINE R IN ROUTS.  -1 IF NOT FOUND
	BEGIN
	MAP XVECTOR ROUTS;
	R_.R<0,18>;
%1%	DECR I FROM .NROUTS TO 0 DO
	    IF .ROUTS[.I,0,18] EQL .R THEN RETURN .I;
	-1
	END;

ROUTINE CINSERT(R)=	! CONDITIONAL INSERT
	! RETURN THE INDEX OF R IN ROUTS. INSERT IF NECESSARY
	BEGIN
	LOCAL L;
	IF (L_CFINDR(.R)) GEQ 0 THEN RETURN .L;
	ROUTS[NROUTS_.NROUTS+1]_.R<0,18>;
	.NROUTS
	END;

ROUTINE CREMOVE(R,VAL)=	! CONDITIONAL REMOVE
	! TURN OF BITS SPECIFIED BY VAL IN THE ENTRY FOR R.
	! DELETE R IF POSSIBLE.
	BEGIN
	LOCAL L;
	MAP XVECTOR ROUTS;
	IF (L_CFINDR(.R)) LSS 0 THEN RETURN;
	ROUTS[.L,0,36]_.ROUTS[.L,0,36] AND NOT .VAL;
	IF .ROUTS[.L,18,18] EQL 0 THEN
	    (ROUTS[.L,0,36]_.ROUTS[.NROUTS,0,36];
	     NROUTS_.NROUTS-1  );
	END;


ROUTINE CKVALS(RTN,TOG)=
	BEGIN
%1%	MAP XVECTOR ROUTS;REGISTER X;
%1%	DECR I FROM .NROUTS TO 0 
	    DO IF .ROUTS[.I,VCHGF]
		THEN BEGIN
			X_.(.ROUTS[.I,0,18])<0,36>;
			IF .X NEQ .OLDVALS[.I]
			    THEN BEGIN
				IF .TOG GEQ 0
				    THEN BEGIN
					OUTS('*** ');
					IF .TOG THEN OUTS('DURING ')
						ELSE OUTS('BEFORE ');
					TOG_-1;
					PRXDISP(.RTN);
					CRLF;
					END;
				PRDISP(.ROUTS[.I,0,18]);
				TAB;
				OUTS('OLD: ');
				OUTDEFAULT(.OLDVALS[.I]);
				TAB;
				OUTS('NEW: ');
				OUTDEFAULT(.X);
				CRLF;
				OLDVALS[.I]_.X;
				END;
		END;
	END;



ROUTINE XBREAK=SETBIT(BREAKV);

ROUTINE XABREAK=SETBIT(ABREAKV);

ROUTINE DBREAK=UNSETBIT(BREAKV);

ROUTINE DABREAK=UNSETBIT(ABREAKV);

ROUTINE OPAQUE=SETBIT(OPQV);

ROUTINE DOPAQUE=UNSETBIT(OPQV);

ROUTINE XCHNG=(SETBIT(VCHGV);NVALS_.NVALS+.NDEBUGPARMS);
ROUTINE DCHNG=(UNSETBIT(VCHGV);NVALS_.NVALS-.NDEBUGPARMS);


!-----------------


ROUTINE DOOPQ(TOG,W)=
	BEGIN
	MAP XVECTOR ROUTS;
	IF .TOG EQL 0 THEN
	    BEGIN
	    IF .TRACEFLG THEN
		BEGIN
		ROUTS[.W,IDIDITF]_1;
		TRACEFLG_0;
		OPQCNT_1;
		OUTS('...'); CRLF
		END
	    ELSE
		IF .ROUTS[.W,IDIDITF] THEN OPQCNT_.OPQCNT+1
	    END
	ELSE
	    BEGIN
	    IF .ROUTS[.W,IDIDITF] THEN
		IF (OPQCNT_.OPQCNT-1) LEQ 0 THEN
		    (TRACEFLG_1; ROUTS[.W,IDIDITF]_0)
	    END
	END;



ROUTINE UNDOOPQ=
	BEGIN
	MAP XVECTOR ROUTS;
	IF .OPQCNT LEQ 0 THEN RETURN;
	OPQCNT_0;
%1%	DECR I FROM .NROUTS TO 1 DO
	    IF .ROUTS[.I,IDIDITF] THEN RETURN ROUTS[.I,IDIDITF]_0;
	END;

ROUTINE GOER= GOFLG_1;

ROUTINE DISAB=(ENABFLG_0;GOER());

ROUTINE XSTRACE=(DCNT_GOBASE_TRACEBASE;TRACEFLG_1);

ROUTINE XTRACE=(XSTRACE(); GOER());

ROUTINE SETIT=
	IF .NDEBUGPARMS LSS 2 THEN ERROR(6) ELSE
	    IF .DEBUGPARMS[1] GEQ #400000 THEN ERROR(7) ELSE (.DEBUGPARMS[1])<0,36>_.DEBUGPARMS[2];

ROUTINE SETBASE(TOG)=
    BEGIN
    IF .NDEBUGPARMS GTR 0 THEN
	IF (.DEBUGPARMS[1] LSS 2) OR (.DEBUGPARMS[1] GTR 10)
	    THEN RETURN ERROR(8)
	    ELSE IF .TOG THEN IDBASE_.DEBUGPARMS[1]
		ELSE DBASE_.DEBUGPARMS[1];
    OUTD(IF .TOG THEN .IDBASE ELSE .DBASE);
    CRLF;
    END;

ROUTINE M1=SETBASE(1);

ROUTINE M0=SETBASE(0);


ROUTINE XWBASE=
	BEGIN
	IF .NDEBUGPARMS GTR 0 THEN
	    WDBASE_.DEBUGPARMS[1];
	OUTDEFAULT(.WDBASE);
	CRLF;
	END;

ROUTINE C1=(DEBUGPARMS[1]_#377777777777;PSTK(0));

ROUTINE C2=(DEBUGPARMS[1]_#377777777777;PSTK(1));

ROUTINE XCALL=
    BEGIN
    IF .NDEBUGPARMS EQL 0 THEN DEBUGPARMS[1]_1;
    PSTK(0)
    END;

ROUTINE XLCALL=
    BEGIN
    IF .NDEBUGPARMS EQL 0 THEN DEBUGPARMS[1]_1;
    PSTK(1)
    END;



ROUTINE EXXX=
    BEGIN 
    MACRO P(X)=(.DEBUGPARMS[X])$;
%1%    REGISTER SAVEGO;
    SAVEGO_.GOFLG;
    GOFLG_1;
%1%    DEBUGRESULTS_CASE .NDEBUGPARMS OF
	SET
	0;
	P(1)();
	P(1)(P(2));
	P(1)(P(2),P(3));1)(P(2),P(3),P(4));
	P(1)(P(2),P(3),P(4),P(5));
	P(1)(P(2),P(3),P(4),P(5),P(6));
	TES;
    GOFLG_.SAVEGO;
%1%    .DEBUGRESULTS
    END;

ROUTINE EXV=
    BEGIN
    LOCAL SAVPARM;
    IF .NDEBUGPARMS LSS 1 THEN RETURN ERROR(6);
    SAVPARM_.DEBUGPARMS[1];
    INCR I FROM 1 TO NDEBUGPARMS_.NDEBUGPARMS-1 DO
	DEBUGPARMS[.I]_.DEBUGPARMS[.I+1];
    (.SAVPARM)<0,36>_EXXX()
    END;



ROUTINE PRS=
    BEGIN
    LOCAL X,V;
    INCR I FROM 1 TO .NDEBUGPARMS DO
	IF .DEBUGPARMS[.I] LSS 0 THEN (PRSYM50(MODDDT(SDDTFS(.DEBUGPARMS[.I]<0,18>)));TAB;
	    OUTRDEF(.DEBUGPARMS[.I]<0,18>,12);TAB;OUTS('MODULE');CRLF)
	  ELSE
	BEGIN
	V_.JOBSYM;X_SDDTFS(.DEBUGPARMS[.I]);
	INCR J FROM 1 DO
	    BEGIN
	    IF (V_NSDDTFA(.(.X)<0,32>,.V)) EQL 0 THEN EXITLOOP;
	    PRSYM50(@.X);OUTC("(");OUTDEFAULT(.J);OUTC(")");TAB;
	    OUTRDEF(.(.V+1)<0,18>,12);TAB;
	    IF .(.V)<32,2> EQL MODNAME
		THEN OUTS('MODULE')
		ELSE BEGIN
		    CASE .(.V)<32,2> OF
			SET
			0;
			OUTS('GLOBAL');
			OUTS('OWN');
			0;
			TES;
		    TAB;PRSYM50(MODDDT(.V));
		    END;
	    CRLF;
	    END;
	END;
    END;






!! NOTE !! THE PLIT BELOW MAPS PRINT NAME TO ROUTINES
!  ----    ------------------------------------------

BIND DEBUGROUTS= PLIT(
	'?M',	XNULL,
	'=',	DISPLAY,
	'_',	SETIT,
	'/',	SLASH,
	'DDT',	EDDT,
	'CALLS',C1,
	'CALL+',C2,
	'CALL',	XCALL,
	'LCALL',XLCALL,
	'EX',	EXXX,
	'EV',	EXV,
	'BREAK',XBREAK,
	'DBREA',DBREAK,
	'ABREA',XABREAK,
	'DABRE',DABREAK,
	'IBASE',M1,
	'OBASE',M0,
	'GO',	GOER,
	'SETTR',XSTRACE,
	'TRACE',XTRACE,
	'PRS',	PRS,
	'DISAB',DISAB,
	'OPAQU',OPAQUE,
	'DOPAQ',DOPAQUE,
	'MONIT',XCHNG,
	'DMONI',DCHNG,
	'WBASE',XWBASE,
	0,	XNULL);


! ANALYSIS OF DEBUG INPUT LINES
! -----------------------------

MACRO ADV=(NC_.NC+1; CHAR_SCANI(PBUFF))$;

ROUTINE TYPE=
	BEGIN
	IF .CHAR GEQ "0" THEN IF .CHAR LEQ "9" THEN RETURN 0;
	IF .CHAR GEQ "A" THEN IF .CHAR LEQ "Z" THEN RETURN 1;
	IF .CHAR EQL "#" THEN RETURN 3;
	RETURN 2
	END;

ROUTINE SCAN=
	BEGIN VAL_ACCUM[0]_ACCUM[1]_0; PACCUM_ACCUM[-1]<1,7>;
	WHILE .CHAR EQL " " DO ADV;
	CASE TYPE() OF
	    SET
	    (WHILE TYPE() EQL 0 DO(VAL_.VAL*
		(IF .RTYPE THEN 10 ELSE .IDBASE)+(.CHAR-"0");ADV);0);
	    (WHILE  (TYPE() LEQ 1) OR (.CHAR EQL ".") DO (REPLACEI(PACCUM,.CHAR);ADV);1);
	    2;
	    (ADV; WHILE TYPE() EQL 0 DO(VAL_.VAL*
			(IF .IDBASE EQL 8 THEN 10 ELSE 8)
		+(.CHAR-"0");ADV);0);
	    TES
	END;

ROUTINE ERROR(EN)=
	BEGIN ERRORFLG_1; OUTM(".",.NC); OUTC("^"); CRLF;
	CASE .EN OF
	SET
	OUTS('DDT-SYMBOL NOT FOUND');
	OUTS('INVALID ATOM');
	OUTS('INVALID PROCESS NAME');
	OUTS('SQ-BRACKETS NOT MATCHED');
	OUTS('PARENS NOT MATCHED');
	OUTS('ROUTINE NAME NOT FOUND');
	OUTS('INSUFFICIENT ARGUMENTS');
	OUTS('INVALID STORE ADDRESS');
	OUTS('MODE MUST BE BETWEEN 2 AND 10');
	OUTS('NUMERIC ARGUEMENT EXPECTED');
	TES;
	CRLF;
	END;


FORWARD PARM;


    ROUTINE STRNG=
	BEGIN
	LOCAL PTR,X[2];PTR_X<36,7>;X_0;
%1%	DECR I FROM 9 TO 0 DO
%1%	    IF .CHAR EQL "'" THEN RETURN .X
		ELSE (REPLACEI(PTR,.CHAR);ADV);
	.X
	END;



ROUTINE ATOM=
    BEGIN LOCAL X,Y,V,SACCUM[2]; X_0;
    WHILE 1 DO
	BEGIN
	Y_CASE SCAN() OF
	    SET
	    .VAL;
	    IF .CHAR NEQ "("
		THEN IF (V_SDDTFA(BASE50(ACCUM))) EQL 0 THEN ERROR(0) ELSE .V
		ELSE BEGIN
		    SACCUM[0]_.ACCUM;SACCUM[1]_.ACCUM[1];
		    ADV;
%1%		    IF SCAN() NEQ 0 THEN RETURN ERROR(9);
%1%		    IF .CHAR NEQ ")" THEN RETURN ERROR(4);
		    ADV;
		    IF .VAL LEQ 0 THEN VAL_1;
		    V_.JOBSYM;
		    Y_BASE50(SACCUM);
%1%		    DECR I FROM .VAL TO 1 
%1%			DO IF (V_NSDDTFA(.Y,.V)) EQL 0 THEN RETURN ERROR(0);
		    @(.V+1)
		    END;
	    SELECT .CHAR OF
		NSET
		".":(ADV;@ATOM());
		"+":(ADV;ATOM());
		"-":(ADV;-(ATOM()));
		"*":(ADV;.OLDPARMS[.PINDEX]);
		"$":(ADV;SCAN(); DEBUGRESULTS<0,0>+.VAL);
		"(":(ADV; V_PARM(); IF .CHAR EQL ")" THEN ADV ELSE ERROR(4); .V);
		"[":0;
		#15:0;
		#12:0;
		",":0;
		"'":(ADV;V_STRNG();IF .CHAR EQL "'" THEN ADV ELSE ERROR(1);.V);
	    OTHERWISE:(ERROR(1);ADV);
		TESN;
	    TES;
	IF .ERRORFLG NEQ 0 THEN RETURN 0;
	X_.X+.Y;
	IF .CHAR NEQ "[" THEN RETURN .X;
	ADV; X_.X+PARM();
	IF .CHAR EQL "]" THEN ADV ELSE ERROR(3);
	IF .ERRORFLG NEQ 0 THEN RETURN 0;
	END;
    END;

ROUTINE PARM=
    BEGIN LOCAL V; V_ATOM();
    WHILE 1 DO
	IF .ERRORFLG NEQ 0 THEN RETURN 0 ELSE
	    SELECT .CHAR OF
		NSET
		#15: RETURN .V;
		#12: RETURN .V;
		",": RETURN .V;
		")": RETURN .V;
		"]": RETURN .V;
		"+": (ADV; V_.V+ATOM());
		"-": (ADV; V_.V-ATOM());
	    OTHERWISE:ERROR(1);
		TESN;
    END;


ROUTINE INPUT=
    BEGIN OWN X;
    PBUFF_BUFF[-1]<1,7>; NC_0;
    DO (X_INC; REPLACEI(PBUFF,.X)) WHILE .X NEQ #12;
    PBUFF_BUFF[-1]<1,7>;
    END;

ROUTINE TINPUT=
%1%    BEGIN REGISTER X;
%1%    IFSKIP TTCALL(2,X)
%1%	THEN BEGIN
%1%	     PBUFF_BUFF[-1]<1,7>;
%1%	     WHILE .X NEQ #12 DO (REPLACEI(PBUFF,.X); X_INC);
	     REPLACEI(PBUFF,.X); NC_0; PBUFF_BUFF[-1]<1,7>;
	     1
	     END
    END;

ROUTINE GETDRNAME=
%1%    BEGIN
    ADV; ACCUM[0]_ACCUM[1]_0; PACCUM_ACCUM[-1]<1,7>;
%1%    UNTIL .CHAR EQL " " OR
%1%	.CHAR EQL #12 OR
%1%	.CHAR EQL #15 DO
%1%	(IF .NC LSS 10 THEN REPLACEI(PACCUM,.CHAR);ADV);
%1%    DECR I FROM .DEBUGROUTS[-1] TO 0 BY 2 DO
%1%	IF .ACCUM EQL .DEBUGROUTS[.I] THEN RETURN .DEBUGROUTS[.I+1];
%1%  ERROR(5)
    END;


ROUTINE XDEBUG=
%1%    BEGIN REGISTER R;
    PINDEX_0; ERRORFLG_0;RTYPE_0;
    DECR I FROM NDPARMS-1 TO 0 DO DEBUGPARMS[.I]_0;
    R_GETDRNAME();
    IF (.R<0,18> EQL M1<0,0>) OR (.R<0,18> EQL M0<0,0>) THEN RTYPE_1;
    IF .ERRORFLG NEQ 0 THEN RETURN;
%1%    WHILE .CHAR NEQ #15 AND .CHAR NEQ #12 DO
	    (PINDEX_.PINDEX+1;DEBUGPARMS[.PINDEX]_PARM();
	     IF .CHAR EQL "," THEN ADV);
    NDEBUGPARMS_.PINDEX;
    DECR I FROM NDPARMS-1 TO 0 DO OLDPARMS[.I]_.DEBUGPARMS[.I];
    IF .ERRORFLG EQL 0 THEN (.R)();
    END;

ROUTINE ISUB=(IF (DCNT_.DCNT-1) LSS 0 THEN
	(DCNT_.GOBASE; IF TINPUT() THEN (TRACEFLG_0;UNDOOPQ();GOFLG_0;DCNT_GOBASE_BGOBASE));
		UNTIL .GOFLG DO (IF .GOFLG GTR 0 THEN (OUTS('&');INPUT());GOFLG_2;XDEBUG();));

FORWARD INITSIX12;

ROUTINE SIXENABLE=ENABFLG_1;

GLOBAL ROUTINE SIX12(XP)=
    BEGIN
    MAP XVECTOR ROUTS;
    BIND ENTRY=3;LOCAL X,P,RNAME;
    EXTERNAL DDTEND;
    VVR_.VREG;
    P_.XP;
    ! IF ROUTINE ENTRY POINT, SIMULATE FREG SAVE
%1%
    IF .P EQL #400000000000 THEN (INITSIX12();IF .STARTFLG THEN (GOFLG_2;ISUB()))
	ELSE IF .P EQL 0 THEN BEGIN
%1%	    XP_.(.FREG)<0,18>;(.FREG)<0,36>_XP<0,0>;
	    RNAME_@(.FREG-1)-ENTRY;
	    IF .NVALS GTR 0 THEN CKVALS(.RNAME,0);
	    X_IF .NROUTS GEQ 0 THEN CFINDR(.RNAME) ELSE -1;
	    IF .X GEQ 0 THEN IF .ROUTS[.X,BREAKF]
		THEN (CRLF;OUTS('<-> AT: ');PRXDISP(@(.FREG-1));CRLF;ENABFLG_1;TRACEFLG_0;UNDOOPQ();GOFLG_2);
	    IF .TRACEFLG THEN (OUTS('--> ');
				PRCALL(.(@FREG)<0,18>,.RNAME);CRLF);
	    IF .X GEQ 0 THEN IF .ROUTS[.X,OPQF] THEN DOOPQ(0,.X);
%1%	    IF .ENABFLG THEN ISUB();
%1%	    (.FREG)<0,36>_.XP<0,18>;
	    END
	ELSE BEGIN
	    RNAME_.(@FREG-1)<0,18>;
	    IF .NVALS GTR 0 THEN CKVALS(.RNAME,1);
	    X_IF .NROUTS GEQ 0 THEN
		CFINDR(IF (X_.(@(@@FREG-1)-1)<0,18>) LEQ DDTEND<0,0> THEN @(SDDTFS(.RNAME)+1) ELSE .X) ELSE -1;
	    IF .X GEQ 0 THEN IF .ROUTS[.X,ABREAKF]
		THEN (CRLF;OUTS('<-> AFTER: ');PRXDISP(.RNAME);
			TAB;OUTS('VALUE: ');OUTDEFAULT(.VVR);TAB;
			PRDISP(.VVR);CRLF;ENABFLG_1;TRACEFLG_0;UNDOOPQ();GOFLG_2);
	    IF .OPQCNT GTR 0 THEN IF .X GEQ 0 THEN DOOPQ(1,.X);
	    IF .TRACEFLG THEN (OUTS('<-- ');
				PRXDISP(.RNAME);TAB;OUTS('VALUE: ');OUTDEFAULT(.VVR);CRLF);
	    IF .ENABFLG THEN ISUB()
	    END;
%1%
    .VVR
    END;

ROUTINE INITSIX12=
    BEGIN
    EXTERNAL DDT;
    GOFLG_1; NROUTS_-1; DCNT_GOBASE_BGOBASE;NVALS_0;
    TRACEFLG_0;ENABFLG_1;
    (.((.DDT<0,18>)+1)<0,18>)<0,36>_0;
    DBASE_IDBASE_8;
    WDBASE_#1000;
    END;





END ELUDOM

  5@/ï