	SUBROUTINE RGPOL
         DIMENSION X(1100),DUMMY(3000),IDUM(40),RESID(100)
         DIMENSION DI(100)
         DIMENSION D(66)
         DIMENSION B(10),E(10),SB(10),T(10)
         DIMENSION XBAR(11),STD(11),COE(11),SUMSQ(11),ISAVE(11)
	DIMENSION ANS(10)
         DIMENSION P(300)
    1 FORMAT(A4,A2,3I)
    2 FORMAT(2G)
    3 FORMAT(27H1POLYNOMIAL REGRESSION.....,'X= ',A5,1X,'Y= ',A5,/)
    4 FORMAT(23H0NUMBER OF OBSERVATIONS,I6//)
    5 FORMAT(/32H0POLYNOMIAL REGRESSION OF DEGREE,I3,/1X,35('-')/)
    6 FORMAT(12H0  INTERCEPT,G)
    7 FORMAT(26H0  REGRESSION COEFFICIENTS/(5G))
    8 FORMAT(1H0/24X,24HANALYSIS OF VARIANCE FOR,I4,19H  DEGREE POLYNOMI
     1AL/)
9	FORMAT(1X,'SOURCE OF VARIATION',2X,'D.F.',2X,'SUM OF',9X,
     1'MEAN',9X,'F',4X,'IMPROVEMENT',/28X,'SQUQRES',8X,
     1'SQUARE',7X,'VAL',4X,'IN SS'/)
10	FORMAT(1X,'DUE TO REGRESSION',3X,I3,2G,F8.2,F10.3)
11	FORMAT(1X,'DEV FROM REGRESSN',3X,I3,2G)
12	FORMAT(/1X,'TOTAL',15X,I3,G)
13	FORMAT(2X,'NO IMPROVEMENT IN REDUCING RESIDUAL SUM OF SQUARES.')
   14 FORMAT(1H0//27X,18HTABLE OF RESIDUALS//13H OBSERVATION ,5X,7HX
     1 VALUE,7X,7HY VALUE,5X,10HY ESTIMATE,5X,8HRESIDUAL/)
15	FORMAT(4X,I6,2X,4G)
16	FORMAT(1X,'ENTER THE NAME OF DATA FILE.?',$)
17	FORMAT(A5)
18	FORMAT(1X,'NAME OF DEPENDENT VARIABLE.?',$)
19	FORMAT(1X,'NAME OF INDEPENDENT VARIABLE.?',$)
24	FORMAT(1X,'VARIABLE ALREADY SPECIFIED.TRY AGAIN.'/)
28	FORMAT(1X,'HIGHEST DEGREE OF POLYNOMIAL TO BE TRIED.?',$)
29	FORMAT(I)
31	FORMAT(1X,'DEGREE EXCEEDS 10.TRY AGAIN.'/)
34	FORMAT(1X,'NO OF OBS.',I4,'  IS NOT GREATER THAN DEGREE+1.'/1X,
     +'EXECUTION ABORTED.'/)
104	FORMAT(1X,'NO SUCH VARIABLE.TRY AGAIN.'/)
260	FORMAT(/1X,'STANDARD ERROR OF FORECAST ',F12.3,/)
265	FORMAT(1X,'IF THE DEPENDENT VARIABLE LEADS/LAGS THE INDEPENDENT'
     +/1X,'VARIABLE ENTER PERIODS AS A POSITIVE/NEGATIVE NUMBER.'
     +/1X,'OTHERWISE TYPE 0.'/)
306	FORMAT(1X,'NEED RESIDUAL TABLE?.',/,1X,
     +'TYPE TTY,DSK,BOTH OR NONE.'/)
309	FORMAT(1X,'ILLEGAL OPTION.TRY AGAIN.'/)
310	FORMAT(1X,'OUTPUT FILENAME?.',$)
317	FORMAT(2I)
318	FORMAT(A5,25X,'RESIDUAL DATA',2X,A5)
319	FORMAT(6G)
324	FORMAT(1X,'NO SUCH FILE ',A5,'. TRY AGAIN.'/)
316	FORMAT(//1X,'RESIDUAL ARRAY NAME?.',$)
405	FORMAT(/1X,'NOTE THAT DEPENDENT VARIABLE IS A CONSTANT'/)
407	FORMAT(/1X,'INDEPENDENT VARIABLE IS A CONSTANT.CASE IGNORED'/)
	CALL ERRSET(0)
	ITRIV=0
300	DO 108 J=1,3000
108	DUMMY(J)=0.
	DO 109 J=1,40
109	IDUM(J)='     '
325	TYPE 16
	ACCEPT 17,IDATA
	CALL LOCATE(IDATA,IERR)
	IF(IERR)322,323,323
322	TYPE 324,IDATA
	GO TO 325
323	CALL INPUT(IND1,IND2,IDUM,DUMMY,IDATA)
	N=IND1
21	TYPE 18
	ACCEPT 17,NAME1
	DO 20 J=1,IND2
	IF(NAME1.EQ.IDUM(J)) GO TO 22
20	CONTINUE
	TYPE 104
	GO TO 21
22	ICOUN1=J
	IVAR1=J
25	TYPE 19
	ACCEPT 17,NAME2
	IF(NAME2.NE.NAME1) GO TO 23
	TYPE 24
	GO TO 25
23	DO 26 J=1,IND2
	IF(NAME2.EQ.IDUM(J)) GO TO 27
26	CONTINUE
	TYPE 104
	GO TO 25
27	ICOUN2=J
	IVAR2=J
30	TYPE 28
	ACCEPT 29,M
	IF(M.LE.10) GO TO 33
	TYPE 31
	GO TO 30
33	L=N*M
	NPLOT=1
	ICOUN1=(ICOUN1-1)*N
	ICOUN2=(ICOUN2-1)*N
	INOBS=0
	TYPE 265
	ACCEPT 29,IL
	ILAG=IABS(IL)
	IF(IL)37,38,38
37	DO 39 J=1,N
	IF(ILAG+J.GT.N) GO TO 40
	X(J)=DUMMY(ICOUN2+ILAG+J)
	X(L+J)=DUMMY(ICOUN1+J)
39	INOBS=INOBS+1
	GO TO 40
38	DO 35 J=1,N
	IF(ILAG+J.GT.N) GO TO 40
	X(J)=DUMMY(ICOUN2+J)
	X(L+J)=DUMMY(ICOUN1+ILAG+J)
35	INOBS=INOBS+1
40	N=INOBS
	ITEMP=N*M
	DO 41 J=1,N
41	X(ITEMP+J)=X(L+J)
	L=ITEMP
	IF(M+1.LT.N) GO TO 36
	TYPE 34,N
	GO TO 300
36      WRITE (5,4) N
C
C
      CALL GDATA (N,M,X,XBAR,STD,D,SUMSQ)
C
      MM=M+1
      SUM=0.0
      NT=N-1
C
      DO 200 I=1,M
      ISAVE(I)=I
C
C     FORM SUBSET OF CORRELATION COEFFICIENT MATRIX
C
      CALL ORDER (MM,D,MM,I,ISAVE,DI,E)
C
C     INVERT THE SUBMATRIX OF CORRELATION COEFFICIENTS
C
      CALL MINV (DI,I,DET,B,T)
C
      CALL MULTR (N,I,XBAR,STD,SUMSQ,DI,E,ISAVE,B,SB,T,ANS)
C
C     PRINT THE RESULT OF CALCULATION
C
      WRITE (5,5) I
      IF(ANS(7)) 140,130,130
  130 SUMIP=ANS(4)-SUM
      IF(SUMIP) 140, 140, 150
140	IF(I.EQ.1) ITRIV=1
	IF(ITRIV.EQ.1) GO TO  150
	WRITE(5,13)
      GO TO 210
150	IF(ITRIV.EQ.1.AND.B(1).NE.0.) GO TO 406
	WRITE(5,6) ANS(1)
      WRITE (5,7) (B(J),J=1,I)
	IF(ITRIV.EQ.1) WRITE(5,405)
	IF(ITRIV.EQ.1) GO TO 305
      WRITE (5,8) I
      WRITE (5,9)
      SUM=ANS(4)
      WRITE (5,10) I,ANS(4),ANS(6),ANS(10),SUMIP
      NI=ANS(8)
      WRITE (5,11) NI,ANS(7),ANS(9)
      WRITE (5,12) NT,SUMSQ(MM)
	WRITE(5,260) ANS(9)
C
C     SAVE COEFFICIENTS FOR CALCULATION OF Y ESTIMATES
C
      COE(1)=ANS(1)
      DO 160 J=1,I
  160 COE(J+1)=B(J)
      LA=I
C
  200 CONTINUE
	GO TO 210
406	WRITE(5,407)
	GO TO 305
C
C     TEST WHETHER PLOT IS DESIRED
C
  210 IF(NPLOT) 305, 305, 220
C
C        CALCULATE ESTIMATES
C
  220 NP3=N+N
      DO 230 I=1,N
      NP3=NP3+1
      P(NP3)=COE(1)
      L=I
      DO 230 J=1,LA
      P(NP3)=P(NP3)+X(L)*COE(J+1)
  230 L=L+N
C
C        COPY OBSERVED DATA
C
      N2=N
      L=N*M
      DO 240 I=1,N
      P(I)=X(I)
      N2=N2+1
      L=L+1
  240 P(N2)=X(L)
C
C     PRINT TABLE OF RESIDUALS
C
307	TYPE 306
	ACCEPT 17,NR
	IF(NR.EQ.'NONE ') GO TO 305
	IF(NR.EQ.'TTY  ') GO TO 242
	IF(NR.EQ.'DSK  '.OR.NR.EQ.'BOTH ') GO TO 241
	TYPE 309
	GO TO 307
241	TYPE 310
	ACCEPT 17,NOFIL
	CALL OFILE(1,NOFIL)
242      IF(NR.NE.'DSK  ')WRITE (5,3) IDUM(IVAR2),IDUM(IVAR1)
      IF(NR.NE.'DSK  ')WRITE (5,5) LA
      IF(NR.NE.'DSK  ')WRITE (5,14)
      NP2=N
      NP3=N+N
      DO 250 I=1,N
      NP2=NP2+1
      NP3=NP3+1
      RESID(I)=P(NP2)-P(NP3)
  250 IF(NR.NE.'DSK  ')WRITE (5,15) I,P(I),P(NP2),P(NP3),RESID(I)
	IF(NR.EQ.'TTY  ') GO TO 305
	K=1
	TYPE 316
	ACCEPT 17,IREASR
	WRITE(1,317)N,K
	WRITE(1,318)IREASR,IDUM(IVAR1)
	WRITE(1,319)(RESID(J),J=1,N)
	ENDFILE 1
305	RETURN
	END
C	STATPAK VERSION OF GDATA
      SUBROUTINE GDATA (N,M,X,XBAR,STD,D,SUMSQ)
      DIMENSION X(1),XBAR(1),STD(1),D(1),SUMSQ(1)
C     GENERATE INDEPENDENT VARIABLES
C
      IF(M-1) 105, 105, 90
   90 L1=0
      DO 100 I=2,M
      L1=L1+N
      DO 100 J=1,N
      L=L1+J
      K=L-N
  100 X(L)=X(K)*X(J)
C
C     CALCULATE MEANS
C
  105 MM=M+1
      DF=N
      L=0
      DO 115 I=1,MM
      XBAR(I)=0.0
      DO 110 J=1,N
      L=L+1
  110 XBAR(I)=XBAR(I)+X(L)
  115 XBAR(I)=XBAR(I)/DF
C
      DO 130 I=1,MM
  130 STD(I)=0.0
C
C     CALCULATE SUMS OF CROSS-PRODUCTS OF DEVIATIONS
C
      L=((MM+1)*MM)/2
      DO 150 I=1,L
  150 D(I)=0.0
      DO 170 K=1,N
      L=0
      DO 170 J=1,MM
      L2=N*(J-1)+K
      T2=X(L2)-XBAR(J)
      STD(J)=STD(J)+T2
      DO 170 I=1,J
      L1=N*(I-1)+K
      T1=X(L1)-XBAR(I)
      L=L+1
  170 D(L)=D(L)+T1*T2
      L=0
      DO 175 J=1,MM
      DO 175 I=1,J
      L=L+1
  175 D(L)=D(L)-STD(I)*STD(J)/DF
      L=0
      DO 180 I=1,MM
      L=L+I
      SUMSQ(I)=D(L)
  180 STD(I)= SQRT( ABS(D(L)))
C
C     CALCULATE CORRELATION COEFFICIENTS
C
      L=0
      DO 190 J=1,MM
      DO 190 I=1,J
      L=L+1
  190 D(L)=D(L)/(STD(I)*STD(J))
C
C     CALCULATE STANDARD DEVIATIONS
C
      DF=SQRT(DF-1.0)
      DO 200 I=1,MM
  200 STD(I)=STD(I)/DF
      RETURN
      END
C	STATPAK VERSION OF ORDER
      SUBROUTINE ORDER (M,R,NDEP,K,ISAVE,RX,RY)
      DIMENSION R(1),ISAVE(1),RX(1),RY(1)
C	COPY INTERCORRELATIONS OF INDEPENDENT VARIABLES
C	WITH DEPENDENT VARIABLE
      MM=0
      DO 130 J=1,K
      L2=ISAVE(J)
      IF(NDEP-L2) 122, 123, 123
  122 L=NDEP+(L2*L2-L2)/2
      GO TO 125
  123 L=L2+(NDEP*NDEP-NDEP)/2
  125 RY(J)=R(L)
C
C     COPY A SUBSET MATRIX OF INTERCORRELATIONS AMONG
C     INDEPENDENT VARIABLES
C
      DO 130 I=1,K
      L1=ISAVE(I)
      IF(L1-L2) 127, 128, 128
  127 L=L1+(L2*L2-L2)/2
      GO TO 129
  128 L=L2+(L1*L1-L1)/2
  129 MM=MM+1
  130 RX(MM)=R(L)
C
C     PLACE THE SUBSCRIPT NUMBER OF THE DEPENDENT
C     VARIABLE IN ISAVE(K+1)
C
      ISAVE(K+1)=NDEP
      RETURN
      END
C	STATPAK VERSION OF MINV
      SUBROUTINE MINV(A,N,D,L,M)
      DIMENSION A(1),L(1),M(1)
C	FIND LARGEST ELEMENT
      D=1.0
      NK=-N
      DO 80 K=1,N
      NK=NK+N
      L(K)=K
      M(K)=K
      KK=NK+K
      BIGA=A(KK)
      DO 20 J=K,N
      IZ=N*(J-1)
      DO 20 I=K,N
      IJ=IZ+I
   10 IF( ABS(BIGA)- ABS(A(IJ))) 15,20,20
   15 BIGA=A(IJ)
      L(K)=I
      M(K)=J
   20 CONTINUE
C
C        INTERCHANGE ROWS
C
      J=L(K)
      IF(J-K) 35,35,25
   25 KI=K-N
      DO 30 I=1,N
      KI=KI+N
      HOLD=-A(KI)
      JI=KI-K+J
      A(KI)=A(JI)
   30 A(JI) =HOLD
C
C        INTERCHANGE COLUMNS
C
   35 I=M(K)
      IF(I-K) 45,45,38
   38 JP=N*(I-1)
      DO 40 J=1,N
      JK=NK+J
      JI=JP+J
      HOLD=-A(JK)
      A(JK)=A(JI)
   40 A(JI) =HOLD
C
C        DIVIDE COLUMN BY MINUS PIVOT (VALUE OF PIVOT ELEMENT IS
C        CONTAINED IN BIGA)
C
   45 IF(BIGA) 48,46,48
   46 D=0.0
      RETURN
   48 DO 55 I=1,N
      IF(I-K) 50,55,50
   50 IK=NK+I
      A(IK)=A(IK)/(-BIGA)
   55 CONTINUE
C
C        REDUCE MATRIX
C
      DO 65 I=1,N
      IK=NK+I
      HOLD=A(IK)
      IJ=I-N
      DO 65 J=1,N
      IJ=IJ+N
      IF(I-K) 60,65,60
   60 IF(J-K) 62,65,62
   62 KJ=IJ-I+K
      A(IJ)=HOLD*A(KJ)+A(IJ)
   65 CONTINUE
C
C        DIVIDE ROW BY PIVOT
C
      KJ=K-N
      DO 75 J=1,N
      KJ=KJ+N
      IF(J-K) 70,75,70
   70 A(KJ)=A(KJ)/BIGA
   75 CONTINUE
C
C        PRODUCT OF PIVOTS
C
      D=D*BIGA
C
C        REPLACE PIVOT BY RECIPROCAL
C
      A(KK)=1.0/BIGA
   80 CONTINUE
C
C        FINAL ROW AND COLUMN INTERCHANGE
C
      K=N
  100 K=(K-1)
      IF(K) 150,150,105
  105 I=L(K)
      IF(I-K) 120,120,108
  108 JQ=N*(K-1)
      JR=N*(I-1)
      DO 110 J=1,N
      JK=JQ+J
      HOLD=A(JK)
      JI=JR+J
      A(JK)=-A(JI)
  110 A(JI) =HOLD
  120 J=M(K)
      IF(J-K) 100,100,125
  125 KI=K-N
      DO 130 I=1,N
      KI=KI+N
      HOLD=A(KI)
      JI=KI-K+J
      A(KI)=-A(JI)
  130 A(JI) =HOLD
      GO TO 100
  150 RETURN
      END
C	STATPAK VERSION OF MULTR
      SUBROUTINE MULTR (N,K,XBAR,STD,D,RX,RY,ISAVE,B,SB,T,ANS)
      DIMENSION XBAR(1),STD(1),D(1),RX(1),RY(1),ISAVE(1),B(1),SB(1),
     1          T(1),ANS(1)
      MM=K+1
C
C        BETA WEIGHTS
C
      DO 100 J=1,K
  100 B(J)=0.0
      DO 110 J=1,K
      L1=K*(J-1)
      DO 110 I=1,K
      L=L1+I
  110 B(J)=B(J)+RY(I)*RX(L)
      RM=0.0
      BO=0.0
      L1=ISAVE(MM)
C
C        COEFFICIENT OF DETERMINATION
C
      DO 120 I=1,K
      RM=RM+B(I)*RY(I)
C
C        REGRESSION COEFFICIENTS
C
      L=ISAVE(I)
      B(I)=B(I)*(STD(L1)/STD(L))
C
C        INTERCEPT
C
  120 BO=BO+B(I)*XBAR(L)
      BO=XBAR(L1)-BO
C
C        SUM OF SQUARES ATTRIBUTABLE TO REGRESSION
C
      SSAR=RM*D(L1)
C
C        MULTIPLE CORRELATION COEFFICIENT
C
  122 RM= SQRT( ABS(RM))
C
C        SUM OF SQUARES OF DEVIATIONS FROM REGRESSION
C
      SSDR=D(L1)-SSAR
C
C        VARIANCE OF ESTIMATE
C
      FN=N-K-1
      SY=SSDR/FN
C
C        STANDARD DEVIATIONS OF REGRESSION COEFFICIENTS
C
      DO 130 J=1,K
      L1=K*(J-1)+J
      L=ISAVE(J)
  125 SB(J)= SQRT( ABS((RX(L1)/D(L))*SY))
C
C        COMPUTED T-VALUES
C
  130 T(J)=B(J)/SB(J)
C
C        STANDARD ERROR OF ESTIMATE
C
  135 SY= SQRT( ABS(SY))
C
C        F VALUE
C
      FK=K
      SSARM=SSAR/FK
      SSDRM=SSDR/FN
      F=SSARM/SSDRM
C
      ANS(1)=BO
      ANS(2)=RM
      ANS(3)=SY
      ANS(4)=SSAR
      ANS(5)=FK
      ANS(6)=SSARM
      ANS(7)=SSDR
      ANS(8)=FN
      ANS(9)=SSDRM
      ANS(10)=F
      RETURN
      END
C	STATPAK VERSION OF RETRIEVAL ROUTINE INPUT
	SUBROUTINE INPUT(IND1,IND2,IDUM,DUMMY,IDATA)
	DIMENSION DUMMY(1),IDUM(1)
	CALL IFILE(1,IDATA)
	READ(1,100) IND1,IND2
	DO 200 J=1,IND2
	READ(1,105) IDUM(J)
	INDEX1=IND1*(J-1)+1
	INDEX2=INDEX1+IND1-1
200	READ(1,106) (DUMMY(L),L=INDEX1,INDEX2)
100	FORMAT(2I)
105	FORMAT(A5)
106	FORMAT(6F)
	RETURN
	END
    