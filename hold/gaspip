C     * * * * * * * * * *   HEAT PIPE VII   * * * * * * * * * *
C     AXIAL CONDUCTION, STEP CHANGE IN FIN PARAMETERS
      COMMON VV(1000),PP(1000),ZZ(1000),XX(1000),QQ(1000)
      COMMON /DATA/ GAS,DZ,TEV,DFT,Q,CEV,TZ,D,E,R,TR,VR,TS1,C1,F1,H1
     1              ,EMIS1,XL1,TS2,C2,F2,H2,EMIS2,XL2,TS,C,F,H,EMIS,HFG
     1              ,A11,B11,C11,NRES,MODEQM,NPRINT,NRUN,NERROR,DELTEE
     1              ,VRES
        CALL ERRSET(0)
        OPEN (7,'PIPOUT',OUTPUT)
        OPEN (6,'PIP.DAT',INPUT)
    1 NERROR=0
  500 CALL INPT
      IF (NERROR.EQ.0) GO TO 2
      IF (NRUN.EQ.1) GO TO 1
  600 CALL EXIT
    2 TI = 1.0
      EMIS = EMIS1
      H = H1
      F = F1
      TS = TS1
      CALL SURF(TI,S1,TC1)
      QI1 = S1*(TI-TC1)/(1.0+S1*R)
      EMIS = EMIS2
      H = H2
      F = F2
      TS = TS2
      CALL SURF(TI,S2,TC2)
    5 CONTINUE
      QI2 = S2*(TI-TC2)/(1.0+S2*R)
      VZ = QI1*XL1+QI2*XL2
      IF(VZ.GT.1.0) GO TO 100
      WRITE(7,10)
   10 FORMAT(/,' INPUT ERROR, HEAT FLUX TOO HIGH OR PIPE TOO SHORT')
      WRITE(7,60) S1,TC1,QI1,XL1,S2,TC2,QI2,XL2,VZ
   60 FORMAT(1X,9E12.4)
      GO TO 999
  100 CONTINUE
      DSUM = 0.0
      CALL MOLE (TS1,XS1)
      DELTEE=0.1
      TSTART=TS1+DELTEE/TEV
      ARG=TZ*(1.0/TSTART-1.0)
      SMAL=EXP(-ARG)
      PHIZ=ALOG(1.0/FEX(ARG))
      IF(SMAL.LT.0.0001) PHIZ=SMAL*(1.+SMAL/2.+SMAL*SMAL/3.)
      FAC=PHIZ
      ZGAS = 0.0
      DUMMY = XL1
      ITER = 0
      CALL SATUR(PHIZ,TR1)
      IF (NRES.EQ.1) TR1=TR
      IF (NRES.EQ.2) TR=TR1
      CALL MOLE(TR1,XSR)
      RSUM=VR*XSR/TR
  200 CONTINUE
  700 IF (MODEQM.EQ.0) GO TO 1000
  750 CALL PIPE(PHIZ,XSUM,QSUM,JEND)
      WRITE(7,40) ITER,PHIZ,XSUM,QSUM,JEND
   40 FORMAT(/,6H ITER=I3,7H  PHIZ=0PE12.4,7H  XSUM=0PE12.4,7H  QSUM=
     1       0PE12.4,7H  JEND=I4)
   41 FORMAT(/,6H ITER=I3,7H  ZGAS=0PE12.4,7H  XSUM=0PE12.4,7H  QSUM=
     1       0PE12.4,7H  JEND=I4)
      ITER = ITER+1
      XSUM = XSUM+DSUM+RSUM
      XX1=PHIZ
      YY1=XSUM-GAS
   50 IF(XSUM.GT.GAS) GO TO 104
   70 CONTINUE
      WRITE(7,30)
   30 FORMAT(/,' THE PROGRAM IS OPERATING IN LONG MODE')
  106 DZGAS = (GAS-XSUM)*TS1/XS1
      TEST=XL1/2.0
      IF(DZGAS.GT.TEST) DZGAS = TEST
      ZGAS = ZGAS+DZGAS
      XL1 = DUMMY-ZGAS
      DSUM = ZGAS*XS1/TS1
      CALL PIPE(PHIZ,XSUM,QSUM,JEND)
      XSUM = XSUM+DSUM+RSUM
      WRITE(7,41) ITER,ZGAS,XSUM,QSUM,JEND
      TEST = (XSUM-GAS)/GAS
   75 IF(ABS(TEST).LT.0.001) GO TO 900
      ITER = ITER+1
   85 IF(ITER.LE.16) GO TO 106
      WRITE(7,15)
   15 FORMAT(/,' SIXTEEN STEPS IN LONG MODE')
   95 CALL OUTPT(PHIZ,ZGAS,JEND)
      GO TO 999
   80 CONTINUE
  104 PHIZ = PHIZ+FAC
      WRITE(7,25)
   25 FORMAT(/,' THE PROGRAM IS OPERATING IN SHORT MODE')
      IF (NRES.EQ.1) GO TO 29
      CALL SATUR(PHIZ,TI)
      CALL MOLE (TI,XS)
      IF (NRES.EQ.2) TR=TI
      RSUM=VR*XS/TR
   29 CALL PIPE(PHIZ,XSUM,QSUM,JEND)
      XSUM = XSUM+DSUM+RSUM
      WRITE(7,40) ITER,PHIZ,XSUM,QSUM,JEND
      ITER = ITER+1
   89 TEST = XSUM-GAS
      XX2=PHIZ
      YY2=TEST
   90 IF(TEST) 103,900,102
  102 FAC=(XX2-XX1)*YY2/(YY1-YY2)
      XX1=XX2
      YY1=YY2
  110 IF(ITER.GE.32) GO TO 300
      GO TO 104
  103 FAC = FAC/2.0
      PHIZ = PHIZ-FAC
      IF (NRES.EQ.1) GO TO 109
      CALL SATUR(PHIZ,TI)
      CALL MOLE (TI,XS)
      IF (NRES.EQ.2) TR=TI
      RSUM=VR*XS/TR
  109 CALL PIPE(PHIZ,XSUM,QSUM,JEND)
      XSUM = XSUM+DSUM+RSUM
      WRITE(7,40) ITER,PHIZ,XSUM,QSUM,JEND
      ITER = ITER+1
      TEST = (XSUM-GAS)/GAS
  310 IF(ABS(TEST).LE.0.001) GO TO 900
  320 IF(ITER.GE.48) GO TO 300
  330 IF(TEST) 103,900,105
  105 FAC = 0.55*FAC
      PHIZ = PHIZ+FAC
      IF (NRES.EQ.1) GO TO 117
      CALL SATUR (PHIZ,TI)
      CALL MOLE (TI,XS)
      IF (NRES.EQ.2) TR=TI
      RSUM=VR*XS/TR
  117 CALL PIPE(PHIZ,XSUM,QSUM,JEND)
      XSUM = XSUM+DSUM+RSUM
      WRITE(7,40) ITER,PHIZ,XSUM,QSUM,JEND
      ITER = ITER+1
      TEST = (XSUM-GAS)/GAS
  410 IF(ABS(TEST).LE.0.001) GO TO 900
  420 IF(ITER.GE.48) GO TO 300
  430 IF(TEST) 103,900,105
  300 CONTINUE
      WRITE(7,20)
   20 FORMAT(/,' DID NOT CONVERGE')
  900 CONTINUE
      ZGAS = DUMMY-XL1
      CALL OUTPT(PHIZ,ZGAS,JEND)
  950 IF (NRUN.EQ.1) GO TO 1
      CALL EXIT
 1000 CALL PIPE(PHIZ,XSUM,QSUM,JEND)
      XX1=PHIZ
      YY1=1.0-QSUM
      WRITE(7,40) ITER,PHIZ,XSUM,QSUM,JEND
      ITER=ITER+1
  150 IF (QSUM.LT.1.0) GO TO 1104
  170 CONTINUE
      WRITE (7,30)
C     LONG MODE OPERATION
 1106 DZGAS=(QSUM-1.0)/QI1
      TEST=ZGAS+DZGAS
      QI=QI1
      IF (TEST.GT.DUMMY) QI=QI2
      DZGAS=(QSUM-1.0)/QI
      ZGAS=ZGAS+DZGAS
      XL1=DUMMY-ZGAS
      IF (XL1.GT.0.0) GO TO 1200
      XL1=0.
      DUMMY2=XL2
      XL2=DUMMY2-ZGAS
 1200 CONTINUE
      CALL PIPE(PHIZ,XSUM,QSUM,JEND)
      WRITE(7,41) ITER,ZGAS,XSUM,QSUM,JEND
      TEST=QSUM-1.0
  175 IF (ABS(TEST).LE.0.001) GO TO 1900
      ITER=ITER+1
  185 IF (ITER.LE.16) GO TO 1106
  115 WRITE (7,15)
  195 CALL OUTPT (PHIZ,ZGAS,JEND)
      GO TO 999
  180 CONTINUE
 1104 PHIZ=PHIZ+FAC
      WRITE (7,25)
C     SHORT MODE OPERATION
      CALL PIPE(PHIZ,XSUM,QSUM,JEND)
      XX2=PHIZ
      YY2=1.0-QSUM
      WRITE(7,40) ITER,PHIZ,XSUM,QSUM,JEND
      ITER=ITER+1
  189 TEST=QSUM-1.0
  190 IF (ABS(TEST).LE.0.001) GO TO 1900
  191 IF (QSUM.GT.1.0) GO TO 1103
 1102 FAC=(XX2-XX1)*YY2/(YY1-YY2)
      XX1=XX2
      YY1=YY2
 1110 IF (ITER.GE.32) GO TO 1300
      GO TO 1104
 1103 FAC=FAC/2.0
      PHIZ=PHIZ-FAC
      CALL SATUR(PHIZ,TI)
      CALL PIPE(PHIZ,XSUM,QSUM,JEND)
      WRITE(7,40) ITER,PHIZ,XSUM,QSUM,JEND
      ITER=ITER+1
      TEST=QSUM-1.0
 1310 IF (ABS(TEST).LE.0.001) GO TO 1900
 1320 IF (ITER.GT.48) GO TO 1300
 1330 IF (QSUM.GT.1.0) GO TO 1103
 1105 FAC=0.55*FAC
      PHIZ=PHIZ+FAC
      CALL SATUR(PHIZ,TI)
      CALL PIPE(PHIZ,XSUM,QSUM,JEND)
      WRITE(7,40) ITER,PHIZ,XSUM,QSUM,JEND
      ITER=ITER+1
      TEST=QSUM-1.0
 1410 IF (ABS(TEST).LE.0.001) GO TO 1900
 1420 IF (ITER.GT.48) GO TO 1300
 1430 IF (QSUM.GT.1.0) GO TO 1103
      GO TO 1105
 1300 CONTINUE
      WRITE (7,20)
 1900 CONTINUE
      ZGAS=DUMMY-XL1
      CALL OUTPT(PHIZ,ZGAS,JEND)
  999 IF (NRUN.EQ.1) GO TO 1
      CALL EXIT
      END
      SUBROUTINE INPT
      COMMON VV(1000),PP(1000),ZZ(1000),XX(1000),QQ(1000)
      COMMON /DATA/ GAS,DZ,TEV,DFT,Q,CEV,TZ,D,E,R,TR,VR,TS1,C1,F1,H1
     1              ,EMIS1,XL1,TS2,C2,F2,H2,EMIS2,XL2,TS,C,F,H,EMIS,HFG
     1              ,A11,B11,C11,NRES,MODEQM,NPRINT,NRUN,NERROR,DELTEE
     1              ,VRES
      READ (6,5)
    5 FORMAT (1X,//)
      WRITE (7,5)
C
C     FLUID CHARACTERISTICS
C
      READ (6,10) A11,B11,C11
   10 FORMAT (3F12.5)
C     A11,B11 AND C11 ARE VAPOR PRESSURE PARAMETERS IN THE LEAST SQUARES
C     FIT, P=EXP(A11-B11/T-C11/(T''2)), PSIA,T IN DEG-R.
      READ(6,15) XMC,DIF,E,HFG
   15 FORMAT(4F12.5)
C     XMC IS MOLECULAR WEIGHT OF THE CONDENSIBLE
C     DIF IS THE MASS DIFFUSIVITY AT ONE ATMOS AND 460R, FT**2/HR
C     E IS THE TEMPERATURE EXPONENT FOR DIFFUSIVITY MINUS ONE
C     HFG IS THE LATENT HEAT (BTU/LB) ( AT TEV )
      WRITE(7,20)
   20 FORMAT(/,26H VAPOR PRESSURE PARAMETERS)
      WRITE (7,21) A11,B11,C11
   21 FORMAT (/, 6H  A11=1PE12.5,7H   B11=1PE12.5,7H   C11=1PE12.5)
      WRITE(7,22)
   22 FORMAT(//,17H FLUID PROPERTIES)
      WRITE(7,23) HFG,DIF,XMC,E
   23 FORMAT(/,6H  HFG=F9.4,14H BTU/LB   DIF=F9.4,15H FTFT/HR   XMC=F9.4
     1       ,10H   CD EXP=F9.4)
C
C     CONDENSER PARAMETERS, SECTION 1, FARTHEST FROM EVAPORATOR
C
      READ(6,11) PF1,AF1,CF1,EF1,EMIS1,HF1
   11 FORMAT(6F12.5)
C     PF1 IS FIN PERIMETER PERPENDICULAR TO PIPE (INCHES)
C     AF1 IS FIN CROSS-SECTIONAL AREA (SQ. INCHES)
C     CF1 IS EFFECTIVE THERMAL CONDUCTIVITY OF FIN, ALLOWS FOR SLOTS
C     EF1 IS FIN EFFECTIVENESS (DIMENSIONLESS)
C     EMIS1 IS FIN TOTAL HEMISPHERICAL EMISSIVITY (DIMENSIONLESS)
C     HF1 IS FIN CONVECTIVE HEAT TRANSFER COEFFICIENT (BTU/HR-FT2-R)
      WRITE(7,24)
   24 FORMAT(//,' FIN PROPERTIES, CONDENSER SECTION NUMBER 1')
      WRITE(7,25) PF1,AF1,CF1,EF1,EMIS1,HF1
   25 FORMAT(/,5H  PF=F9.4,9H IN   AF=F9.4,12H SQ IN   CF=F9.4,
     1       16H BTU/HRFTR   EF=F9.4,8H   EMIS=F9.4,6H   HF=F9.4,
     1       11H BTU/HRFT2R)
      TEST=EF1*PF1*(EMIS1+HF1)
      IF (TEST.NE.0.) GO TO 996
      NERROR=1
      WRITE(7,96)
   96 FORMAT(/,36H CONDENSER SECTION NO. 1 INPUT WRONG)
  996 CONTINUE
C
C     CONDENSER PARAMETERS, SECTION 2, NEAREST TO EVAPORATOR
C
      READ(6,11) PF2,AF2,CF2,EF2,EMIS2,HF2
      WRITE(7,54)
   54 FORMAT(//,' FIN PROPERTIES, CONDENSER SECTION NUMBER 2')
      WRITE(7,25) PF2,AF2,CF2,EF2,EMIS2,HF2
      TEST=EMIS2+HF2
      IF (TEST.NE.0.) GO TO 997
      NERROR=1
      WRITE(7,97)
   97 FORMAT(/,36H CONDENSER SECTION NO. 2 INPUT WRONG)
  997 CONTINUE
C
C     WALL CHARACTERISTICS
C
      READ(6,12) DOUT,THKW,CW
   12 FORMAT(3F12.5)
C     DOUT IS OUTSIDE DIAMETER OF HEAT PIPE (INCHES)
C     THKW IS WALL THICKNESS (INCHES)
C     CW IS WALL THERMAL CONDUCTIVITY (BTU/HR-FT-R)
      WRITE(7,26)
   26 FORMAT(//,16H PIPE PROPERTIES)
      WRITE(7,27) DOUT,THKW,CW
   27 FORMAT(/,7H  DOUT=F9.4,15H IN   WALL THK=F9.4,16H IN   WALL COND=
     1       F9.4,10H BTU/HRFTR)
C
C     WICK CHARACTERISTICS
C
      READ(6,13) DEL,CONWK,DART
   13 FORMAT(3F12.5)
C     DEL IS THE HEAT PIPE WICK THICKNESS (INCHES)
C     CONWK IS THE EFFECTIVE THERMAL CONDUCTIVETY OF FILLED WICK
C     DART IS THE EFFECTIVE DIAMETER OF ARTERIES IN PIPE (INCHES)
C
C     ENVIRONMENTAL PARAMETERS AND LENGTHS
C
      READ(6,14) TF1,POW1,XLONG1
   14 FORMAT(3F12.5)
C     TF1 IS THE FLUID TEMPERATURE (DEGREES-R)
C     POW1 IS THE ABSORBED POWER PER UNIT AREA OF THE OUTER FIN SURFACE
C     (PERIMITER TIMES LENGTH)  THE POWER IS BOTH INSIDE (INTERNAL
C     POWER) AND OUTSIDE (SOLAR ABSORPTION, ETC.) BUT THE AREA IS THE
C     OUTSIDE AREA ONLY (BTU/HR-FT2)
C     XLONG1 IS THE LENGTH OF CONDENSER SECTION NUMBER 1 (FEET)
      READ(6,14) TF2,POW2,XLONG2
C     XLONG2 IS THE LENGTH OF CONDENSER SECTION NUMBER 2 (FEET)
      WRITE(7,32)
   32 FORMAT(//,' CONDENSER ENVIRONMENT, SECTION NUMBER 1')
      WRITE(7,33) TF1,POW1,XLONG1
   33 FORMAT(/,' TF=',0PE12.4,' R   POW=',0PE12.4,' BTU/HRFTFT   XLONG='
     1       ,0PE12.4,' FT')
      WRITE(7,34)
   34 FORMAT(//,' CONDENSER ENVIRONMENT, SECTION NUMBER 2')
      WRITE(7,33) TF2,POW2,XLONG2
C
C     OPERATING CONDITIONS
C
      READ(6,16) TEV,Q,ZGAS,AGAS
   16 FORMAT(4E12.5)
C     TEV IS THE EVAPORATOR TEMPERATURE (DEGREES-R)
C     Q IS THE HEAT PIPE POWER (BTU/HR)
C     ZGAS IS THE LENGTH OF CONDENSER FILLED WITH GAS IF SHARP FRONT, FT
C     AGAS IS THE AMOUNT OF GAS IN LB-MOLES
C     ONE, BUT NOT BOTH, OF ZGAS AND AGAS MUST BE EQUAL TO ZERO
      IF(ZGAS.EQ.0.0.AND.AGAS.EQ.0.0) GO TO 998
      IF(ZGAS.EQ.0.0.OR.AGAS.EQ.0.0) GO TO 200
  998 CONTINUE
      NERROR=1
      WRITE(7,98) ZGAS,AGAS
   98 FORMAT(/,' INPUT WRONG, ZGAS=',0PE12.5,' AGAS=',0PE12.5)
      CALL EXIT
  200 CONTINUE
C
C     RESERVOIR CHARACTERISTICS
C
      READ (6,17) VRES,TRES,NRES,MODEQM,NPRINT,NRUN
   17 FORMAT(2F12.5,4I12)
C     VRES IS THE RESERVOIR VOLUME IN CUBIC INCHES
C     TRES IS THE RESERVOIR TEMPERATURE IN DEGREES R
C     NRES REFERS TO A NON-WICKED (0), AN ACTIVE-WICKED (1) OR A
C     PASSIVE-WICKED (2) RESERVOIR.
C     MODEQM REFERS TO A PRESCRIBED Q (0) OR PRESCRIBED MOLES OF GAS (1).
C     IN THE LATTER CASE Q IS SIMPLY A GUESS, HOPEFULLY LOW BUT CORRECT
C     IN ORDER OF MAGNITUDE.  IN THE FORMER CASE AGAS IS SET EQUAL TO
C     ZERO AND ZGAS IS A NOMINAL GUESS.
C     NPRINT INDICATES THE NUMBER OF LINES TO BE SKIPPED IN OUTPUT.
C     NRUN EQUALS 1 IF ANOTHER SET OF DATA FOLLOWS.  OTHERWISE 0.
      WRITE(7,28)
   28 FORMAT(//,' WICK PROPERTIES AND RESERVOIR CONDITIONS')
      WRITE(7,29) DEL,CONWK,DART,VRES,TRES,NRES
   29 FORMAT(/,' WICK THK=',F9.4,' IN  WICK COND=',F9.4,' BTU/HRFTR  ',
     1' D-ART=',F9.4,' IN  VRES=',F9.4,' CU IN  TRES=',F9.4,' R  NRES='
     1,I2)
      SIGMA = 0.1714E-8
      RU = 1544.0
      PI = 3.14159265358979
      IF(EMIS1.EQ.0.0.AND.HF1.EQ.0.0) GO TO 999
      GO TO 100
  999 CONTINUE
      NERROR=1
      WRITE(7,99)
   99 FORMAT(/,12H INPUT WRONG)
      RETURN
C
C     CALCULATION OF DIMENSIONLESS PARAMETERS
C
  100 CONTINUE
      IF (NERROR.EQ.1) GO TO 999
      CALL SINK(EMIS1,POW1,HF1,TF1,TS1)
      TS2=TS1
      IF(PF2.NE.0.) CALL SINK(EMIS2,POW2,HF2,TF2,TS2)
      TS1 = TS1/TEV
      TS2 = TS2/TEV
      TR = TRES/TEV
      B11 = B11/TEV
      C11 = C11/TEV**2
      PEV=(EXP(A11-B11-C11))*144.0
      CEV = PEV/(RU*TEV)
      TZ=B11+C11*(1.0+1.0/TS1)
      PATMOS = PEV/(144.0*14.7)
      DEV=(DIF/PATMOS)*((TEV/460.0)**(E+1.0))
      CMDH = XMC*DEV*CEV*HFG
      DIN = DOUT-2.0*THKW
      DI = DIN-2.0*DEL
      FINKA1 = CF1*AF1/144.0
      FINKA2 = CF2*AF2/144.0
      WALKA = (PI/576.0)*(DOUT**2-DIN**2)*CW
      WICKA = (PI/576.0)*(DIN**2-DI**2)*CONWK
      SUMKA1 = FINKA1+WALKA+WICKA
      SUMKA2 = FINKA2+WALKA+WICKA
      IF(DI.GE.DART) GO TO 300
      WRITE(7,18) DI,DART
   18 FORMAT(/,' DART TOO BIG.   DI=',0PE12.4,'   DART=',0PE12.4)
      NERROR=1
      RETURN
  300 DFT = SQRT(DI**2-DART**2)/12.0
      VR = VRES/((PI/4.0)*1728.0*DFT**3)
      XL1 = XLONG1/DFT
      XL2 = XLONG2/DFT
      XLTOT=XL1+XL2
      IF(XLTOT.LT.250.) GO TO 600
      WRITE(7,60)
   60 FORMAT(//,27H PIPE EXCEEDS 250 DIAMETERS)
      CALL EXIT
  600 CONTINUE
      C1 = SUMKA1*TEV/(Q*DFT)
      C2 = SUMKA2*TEV/(Q*DFT)
      XK = 2.0*PI*CONWK*DFT*TEV/(Q*ALOG(DIN/DI))
      R = 1.0/XK
      D = (PI/4.0)*DFT*CMDH/Q
      F1 = EF1*(PF1/12.0)*DFT*SIGMA*TEV**4/Q
      F2 = EF2*(PF2/12.0)*DFT*SIGMA*TEV**4/Q
      H1 = HF1*TEV/(SIGMA*TEV**4)
      H2 = HF2*TEV/(SIGMA*TEV**4)
      COEF = PI       *CEV*DFT**3/4.0
      GAS = AGAS/COEF
      TS = TS1
      XS = 1.0-EXP(-TZ*(1.0/TS-1.0))
      RSUM = XS*VR/TR
      IF(ZGAS.EQ.0.0) GO TO 400
      GAS = (ZGAS*XS/(TS*DFT))+RSUM
  400 AMT = COEF*GAS
      XGAS = DFT*(GAS-RSUM)*TS/XS
      WRITE(7,30)
   30 FORMAT(//,21H OPERATING CONDITIONS)
      WRITE(7,31) TEV,Q,MODEQM,CEV
   31 FORMAT(/,'   TEV=',0PE12.4,' R   Q=',0PE12.4,' BTU/HR   MODEQM=',
     1I2,    '   CEV=',0PE12.4,' LBMOLE/FT3')
      WRITE(7,36)
   36 FORMAT(//,' DIMENSIONLESS TEMPERATURES')
      WRITE(7,37) TZ,TS1,TS2,TR
   37 FORMAT(/,' TZ=',0PE12.4,' TS1=',0PE12.4,' TS2=',0PE12.4,' TR=',
     1       0PE12.4)
      WRITE(7,38)
   38 FORMAT(//,' DIMENSIONLESS CONDENSER PARAMETERS, SECTION NO. 1')
      WRITE(7,39) EMIS1,C1,F1,H1,XL1
   39 FORMAT(/,' EMIS=',0PE12.4,' C=',0PE12.4,' F=',0PE12.4,' H=',
     1       0PE12.4,' XL=',0PE12.4)
      WRITE(7,40)
   40 FORMAT(//,' DIMENSIONLESS CONDENSER PARAMETERS, SECTION NO. 2')
      WRITE(7,39) EMIS2,C2,F2,H2,XL2
      WRITE(7,42)
   42 FORMAT(//,29H AMOUNT OF NONCONDENSIBLE GAS)
      WRITE(7,43) GAS,ZGAS,AGAS,COEF,AMT,XGAS
   43 FORMAT(/,5H GAS=0PE12.4,8H   ZGAS=0PE12.4,11H FT   AGAS=0PE12.4,
     1       15H LBMOLE   COEF=0PE12.4,9H 1/LBMOLE/,5H AMT=0PE12.4,
     1       14H LBMOLE  XGAS=0PE12.4,3H FT)
      WRITE(7,44)
   44 FORMAT(///)
      RETURN
      END
      SUBROUTINE SINK(EMIS,POW,HF,TF,TS)
      SIGMA = 0.1714E-8
      TS = TF
      IF(EMIS.EQ.0.0) GO TO 102
      TS = SQRT(SQRT(POW/(EMIS*SIGMA)))
      IF(HF.EQ.0.0) GO TO 102
      IF(TS.EQ.TF) GO TO 102
      TM = (TF+TS)/2.0
      DO 101 I = 1,10
      HR = 4.0*EMIS*SIGMA*TM**3
      TS2 = (POW+HF*TF+3.0*EMIS*SIGMA*TM**4)/(HF+HR)
      TS = (TS+TS2)/2.0
      TM = TS
  101 CONTINUE
  102 RETURN
      END
      SUBROUTINE SURF(TI,S,TC)
      COMMON VV(1000),PP(1000),ZZ(1000),XX(1000),QQ(1000)
      COMMON /DATA/ GAS,DZ,TEV,DFT,Q,CEV,TZ,D,E,R,TR,VR,TS1,C1,F1,H1
     1              ,EMIS1,XL1,TS2,C2,F2,H2,EMIS2,XL2,TS,C,F,H,EMIS,HFG
     1              ,A11,B11,C11,NRES,MODEQM,NPRINT,NRUN,NERROR,DELTEE
     1              ,VRES
      DENOM = 4.0*EMIS*TI**3+H
      S = F*DENOM
      TC = (EMIS*TS**4+H*TS+3.0*EMIS*TI**4)/DENOM
      RETURN
      END
      SUBROUTINE PIPE (PHIZ,XSUM,QSUM,JEND)
        DOUBLE PRECISION XSUM
      COMMON VV(1000),PP(1000),ZZ(1000),XX(1000),QQ(1000)
      COMMON /DATA/ GAS,DZ,TEV,DFT,Q,CEV,TZ,D,E,R,TR,VR,TS1,C1,F1,H1
     1              ,EMIS1,XL1,TS2,C2,F2,H2,EMIS2,XL2,TS,C,F,H,EMIS,HFG
     1              ,A11,B11,C11,NRES,MODEQM,NPRINT,NRUN,NERROR,DELTEE
     1              ,VRES
   99 CONTINUE
      DZ=0.25
      V=0.0
      P=PHIZ
      CALL SATUR(P,TI)
      CALL MOLE(TI,XS)
      Z=0.0
      J=0
      XSUM=0.0
      QSUM=0.0
      XL=XL1
      TS=TS1
      C=C1
      F=F1
      H=H1
      EMIS=EMIS1
      DO 100 I=1,1000
        I=I
      CALL RUNGE(V,P,DV,DP)
      IF (DV.LT.0.0) DV=0.0
      IF (DP.LT.0.0) DP=0.0
      IF (DV.LE.0.0.AND.DP.LE.0.0) GO TO 200
      IF (DV.GT.0.0625) GO TO 200
      GO TO 400
  200 DZ=DZ/2.0
      GO TO 98
  400 CONTINUE
      J=J+1
      Z=Z+DZ
      IF (Z.GT.XL) GO TO 150
      ZZ(J)=Z
      P1=P
      P=P+DP
      PP(J)=P
      V=V+DV
      VV(J)=V
      PM=(P1+P)/2.0
      CALL SATUR(PM,TM)
      CALL MOLE(TM,XM)
      XSUM=XSUM+XM*DZ/TM
      XX(J)=XSUM
      CALL SURF(TM,S,TC)
      QI=S*(TM-TC)/(1.0+S*R)
      QSUM=QSUM+QI*DZ
      QQ(J)=QSUM
      IF(DV.LT.0.015625) DZ=2.0*DZ
      IF (DZ.GT.0.25) DZ=0.25
      GO TO 100
  150 DZ1=Z-XL
      ZZ(J)=XL
      FRAC=(DZ-DZ1)/DZ
      TESTZ=Z
      Z=XL
      P1=P
      P=P+DP*FRAC
      PP(J)=P
      V=V+DV*FRAC
      VV(J)=V
      PM=(P1+P)/2.0
      CALL SATUR(PM,TM)
      CALL MOLE(TM,XM)
      XSUM=XSUM+XM*DZ*FRAC/TM
      XX(J)=XSUM
      CALL SURF(TM,S,TC)
      QI=S*(TM-TC)/(1.0+S*R)
      QSUM=QSUM+QI*DZ*FRAC
      QQ(J)=QSUM
      CALL SATUR(P,TI)
      DTEE=D*TI**E
      TERM=(TI**2/DTEE)*EXP(-P)/(FEX(P)*TZ)
      V=V*((1.0+C*TERM)/(1.0+C2*TERM))
      XL=XL1+XL2
      IF(TESTZ.GE.XL)GO TO 101
      TS=TS2
      C=C2
      F=F2
      H=H2
      EMIS=EMIS2
   98 CONTINUE
  100 CONTINUE
      WRITE(7,10) DELTEE,DV,DP,DZ,TI,TS1,TC
   10 FORMAT(/,28H 1000 STEPS IN  DELTEE=F10.4,5H  DV=E12.4,
     1       5H  DP=E12.4,5H  DZ=E12.4,/5H  TI=E12.4,6H  TS1=E12.4,
     1       5H  TC=E12.4)
      DELTEE=4.0*DELTEE
      TSTART=TS1+DELTEE/TEV
      IF (TSTART.GT.1.0) GO TO 101
      ARG=TZ*(1.0/TSTART-1.0)
      SMAL=EXP(-ARG)
      PHIZ=ALOG(1.0/FEX(ARG))
      IF (SMAL.LT.0.0001) PHIZ=SMAL*(1.+SMAL/2.+SMAL*SMAL/3.)
      GO TO 99
  101 CONTINUE
      JEND=J
      RETURN
      END
      SUBROUTINE RUNGE(V,P,DV,DP)
      COMMON VV(1000),PP(1000),ZZ(1000),XX(1000),QQ(1000)
      COMMON /DATA/ GAS,DZ,TEV,DFT,Q,CEV,TZ,D,E,R,TR,VR,TS1,C1,F1,H1
     1              ,EMIS1,XL1,TS2,C2,F2,H2,EMIS2,XL2,TS,C,F,H,EMIS,HFG
     1              ,A11,B11,C11,NRES,MODEQM,NPRINT,NRUN,NERROR,DELTEE
     1              ,VRES
      CALL DELTA(V,P,DV1,DP1)
      V1 = V+DV1/2.0
      P1 = P+DP1/2.0
      CALL DELTA(V1,P1,DV2,DP2)
      V1 = V+DV2/2.0
      P1 = P+DP2/2.0
      CALL DELTA(V1,P1,DV3,DP3)
      V1 = V+DV3
      P1 = P+DP3
      CALL DELTA(V1,P1,DV4,DP4)
      DV = (DV1+2.0*DV2+2.0*DV3+DV4)/6.0
      DP = (DP1+2.0*DP2+2.0*DP3+DP4)/6.0
      RETURN
      END
      SUBROUTINE DELTA(V,P,DV,DP)
      COMMON VV(1000),PP(1000),ZZ(1000),XX(1000),QQ(1000)
      COMMON /DATA/ GAS,DZ,TEV,DFT,Q,CEV,TZ,D,E,R,TR,VR,TS1,C1,F1,H1
     1              ,EMIS1,XL1,TS2,C2,F2,H2,EMIS2,XL2,TS,C,F,H,EMIS,HFG
     1              ,A11,B11,C11,NRES,MODEQM,NPRINT,NRUN,NERROR,DELTEE
     1              ,VRES
      CALL SATUR(P,T)
      TEE = T**E
      XC=FEX(P)
      CALL SURF(T,S,TC)
      IF(P.LT.600.) GO TO 1
      X=0.
      GO TO 2
    1 X=EXP(-P)
    2 QI = S*(T-TC)/(1.0+S*R)
      TERM1 = C*(X/XC)*(T**2/TEE)/(D*TZ)
      DP = DZ*V/(D*TEE)
      TERM2 = 1.0-(2.0-E)*(T/TZ)*X
      TERM3 = V**2/(D*TEE*XC)
      DV=DZ*TERM1*TERM2*TERM3/(1.+TERM1)
      IF (QI.LE.0.) RETURN
      DV=(DZ*QI/(1.0+TERM1))*(1.0+TERM1*TERM2*TERM3/QI)
      RETURN
      END
      SUBROUTINE SATUR(P,TI)
      COMMON VV(1000),PP(1000),ZZ(1000),XX(1000),QQ(1000)
      COMMON /DATA/ GAS,DZ,TEV,DFT,Q,CEV,TZ,D,E,R,TR,VR,TS1,C1,F1,H1
     1              ,EMIS1,XL1,TS2,C2,F2,H2,EMIS2,XL2,TS,C,F,H,EMIS,HFG
     1              ,A11,B11,C11,NRES,MODEQM,NPRINT,NRUN,NERROR,DELTEE
     1              ,VRES
C     FLAT RADIAL PROFILE
      TMIN=TS+DELTEE/TEV
      IF(P.GT.30.0) GO TO 100
      IF(P.LE.0.0) GO TO 101
      TI = 1.0/(1.0+(1.0/TZ)*ALOG(1.0/FEX(P)))
      IF (TI.LT.TMIN) TI=TMIN
      GO TO 102
  100 TI = 1.0
      GO TO 102
  101 TI=TMIN
  102 RETURN
      END
      SUBROUTINE MOLE (TI,XS)
      COMMON VV(1000),PP(1000),ZZ(1000),XX(1000),QQ(1000)
      COMMON /DATA/ GAS,DZ,TEV,DFT,Q,CEV,TZ,D,E,R,TR,VR,TS1,C1,F1,H1
     1              ,EMIS1,XL1,TS2,C2,F2,H2,EMIS2,XL2,TS,C,F,H,EMIS,HFG
     1              ,A11,B11,C11,NRES,MODEQM,NPRINT,NRUN,NERROR,DELTEE
     1              ,VRES
      TERM1 = B11*(1.0/TI-1.0)
      TERM2 = C11*(1.0/TI**2-1.0)
      ARG = TERM1+TERM2
      XS = FEX(ARG)
      RETURN
      END
      FUNCTION FEX(X)
      IF(X.LT.0.1) GO TO 100
      FEX=1.0
      IF (X.GT.30.) RETURN
      FEX=1.0-EXP(-X)
      RETURN
  100 XXX = X
      FEX = X
      XN = 1.0
      F = 1.0
      SIGN = 1.0
      DO 101 I = 1,7
      XN = XN+1.0
      SIGN = -SIGN
      F = F*XN
      XXX = X*XXX
      FEX = FEX+SIGN*XXX/F
  101 CONTINUE
      RETURN
      END
      SUBROUTINE OUTPT(PHIZ,ZGAS,JEND)
      DIMENSION XZIN(1000),YTWALL(1000)
      COMMON VV(1000),PP(1000),ZZ(1000),XX(1000),QQ(1000)
      COMMON /DATA/ GAS,DZ,TEV,DFT,Q,CEV,TZ,D,E,R,TR,VR,TS1,C1,F1,H1
     1              ,EMIS1,XL1,TS2,C2,F2,H2,EMIS2,XL2,TS,C,F,H,EMIS,HFG
     1              ,A11,B11,C11,NRES,MODEQM,NPRINT,NRUN,NERROR,DELTEE
     1              ,VRES
      PI=3.14159265358979
      COEF=PI       *CEV*DFT**3/4.0
      M=JEND+1
      NPRINT=NPRINT+1
      NP=0
      J=0
      Z=0.0
      P=PHIZ
      CALL SATUR(P,TI)
      CALL MOLE(TI,XS)
      DTEE=D*TI**E
      XL=XL1
      EMIS=EMIS1
      H=H1
      F=F1
      TS=TS1
      C=C1
      CALL SURF(TI,S,TC)
      TW=(TI+S*R*TC)/(1.0+S*R)
      IF (NRES.EQ.2) TR=TI
      CALL MOLE(TR,XR)
      IF (NRES.EQ.0) XR=XS
      RSUM=VR*XR/TR
      CALL MOLE(TS1,XS1)
      DSUM=ZGAS*XS1/TS1
      XSUM=DSUM
      ASTN=0.0
      V=0.0
      QSUM=0.0
      TWICK=TEV*TI
      TWALL=TEV*TW
      YTWALL(1)=TWALL
      TRES=TEV*TR
      ZFT=Z*DFT
      ZIN=12.*ZFT
      XZIN(1)=ZIN
      AMT=COEF*RSUM
      XMDOT=V*Q/HFG
      IF (VR.EQ.0.) GO TO 20
      WRITE(7,10) NRES,VRES,TRES,XR,AMT
   10 FORMAT(///,6H NRES=I2,8H   VRES=0PE12.4,12H IN3   TRES=0PE12.4,
     1           8H R   XR=0PE12.4,8H   MRES=0PE12.4,9H LB-MOLES///)
      AMT=0.
   20 WRITE(7,11) J,Z,ZIN,V,P,TI,XSUM,ASTN,XMDOT
   11 FORMAT(/,3H J=I4,4H  Z=0PE12.4,6H  ZIN=0PE12.4,3H IN/,11H           
     1V=0PE12.4,4H  P=0PE12.4,5H  TI=0PE12.4,7H  XSUM=0PE12.4,7H  ASTN=0
     2PE12.4,11H  MASSFLOW=0PE12.4,6H LB/HR)
      WRITE(7,21) QSUM,XS,TWICK,TWALL,AMT
   21 FORMAT(14H         QSUM=0PE12.4,12H BTU/HR  XS=0PE12.4,8H  TWICK=E
     112.4,10H R  TWALL=0PE12.4,10H R  MPIPE=0PE12.4,8H LBMOLES)
      Z=ZGAS
      CALL MOLE(TS1,XS1)
      DSUM=ZGAS*XS1/TS1
      XSUM=DSUM
      AMT=COEF*XSUM
      DO 100 I=1,M
      J=J+1
      NP=NP+1
      IF(M-I)12,15,12
   12 IF (NPRINT-NP) 30,15,30
   15 WRITE(7,11) J,Z,ZIN,V,P,TI,XSUM,ASTN,XMDOT
      WRITE(7,21) QSUM,XS,TWICK,TWALL,AMT
      IF(I.EQ.M) GO TO 101
      NP=0
   30 Z1=Z
      Z=ZZ(I)+ZGAS
      ZZ(I)=Z
      V1=V
      P=PP(I)
      V=VV(I)
      XSUM=XX(I)+DSUM
      QSUM=QQ(I)*Q
      CALL SATUR(P,TI)
      CALL MOLE(TI,XS)
      DTEE=D*TI**E
      DVDZ=(V-V1)/(Z-Z1)
      CALL SURF(TI,S,TC)
      TW=(TI+R*S*TC)/(1.0+R*S)
      ASTN=DVDZ/(16.0*DTEE)
      TWICK=TEV*TI
      TWALL=TW*TEV
      YTWALL(J+1)=TWALL
      ZFT=Z*DFT
      ZIN=ZFT*12.
      XZIN(J+1)= ZIN
      AMT=COEF*XSUM
      XMDOT=V*Q/HFG
      IF (Z.LT.XL) GO TO 100
      XL=XL1+XL2
      TS=TS2
      C=C2
      F=F2
      H=H2
      EMIS=EMIS2
  100 CONTINUE
  101 XSUM=XSUM+RSUM
      AMT=COEF*XSUM
      WRITE(7,25) AMT
   25 FORMAT(/,33H TOTAL GAS IN PIPE AND RESERVOIR=0PE12.5,9H LB-MOLES)
      CALL PLOT(XZIN,YTWALL,M)
      RETURN
      END
      SUBROUTINE PLOT(XZIN,YTWALL,M)
C     ROUTINE TO PLOT THE AXIAL TEMP PROFILE
C     ZIN VS TWALL
C     W. GIDEON   10-6-71
      DIMENSION IBOX(49,120),X(120),Y(120),YAXIS(49),XZIN(1000),
     *YTWALL(1000)
      DATA IBLANK /1H /
      DATA IDOT /1H*/
      K=M/120+1
      L=M/K
      DO 1 I=1,L
      II=1+K*(I-1)
      X(I)=XZIN(II)
    1 Y(I)=YTWALL(II)
C     INITIALIZE MATRIX
      DO 2 I=1,49
      DO 2 J=1,120
    2 IBOX(I,J)=IBLANK
C     DETERMINE SCALE FOR Y-AXIS
      YMAX=Y(1)
      YMIN=Y(1)
      DO 3 I=2,L
      IF(YMAX.LT.Y(I))YMAX=Y(I)
      IF(YMIN.GE.Y(I))YMIN=Y(I)
    3 CONTINUE
      DELTA=(YMAX-YMIN)/49.
C     DETERMINE POSITION OF POINTS
      DO 4 J=1,L
      I=1
      Y1=YMAX
      Y2=YMAX-DELTA
    5 IF(Y(J).GT.Y1.OR.Y(J).LE.Y2)GO TO 6
      IBOX(I,J)=IDOT
      GO TO 4
    6 I=I+1
      IF(I.GT.49)GO TO 4
      Y1=Y2
      Y2=Y2-DELTA
      GO TO 5
    4 CONTINUE
      DO 7 I=1,49
      XY=FLOAT(I-1)
      YAXIS(I)=YMAX-XY*(YMAX-YMIN)/48.
    7 CONTINUE
      WRITE(7,100)
C     PLOT POINTS AND PRINT Y-AXIS
      DO 8 I=1,49
      IF(MOD((I-1),3).EQ.0)GO TO 9
      WRITE(7,101) (IBOX(I,J),J=1,L)
      GO TO 8
    9 WRITE(7,102) YAXIS(I),(IBOX(I,J),J=1,L)
    8 CONTINUE
      WRITE(7,103)
      WRITE(7,104) (X(I),I=1,L,10)
      WRITE(7,105)
  100 FORMAT(1H1,42X,'AXIAL TEMPERATURE PROFILE'/2X,'TEMP(R)',38X,
     *'(ZIN  VS  TWALL)')
  101 FORMAT(8X,1H.,120A1)
  102 FORMAT(3X,F5.1,1H.,120A1)
  103 FORMAT(8X,1H.,12(1H+,9(1H.)))
  104 FORMAT(7X,F4.1,11(6X,F4.1))
  105 FORMAT(49X,'ZIN(INCHES)')
      RETURN
      END
  \;Z