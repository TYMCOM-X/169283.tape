IDENTIFICATION DIVISION.
ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
        SELECT  INFILE, ASSIGN TO DSK, RECORDING MODE IS ASCII.
        SELECT OUTFILE, ASSIGN TO DSK, RECORDING MODE IS ASCII.
DATA DIVISION.
FILE SECTION.
FD      INFILE, VALUE OF ID IS "RPT   RAW".
01      IN-REC.
        02 PAYEE, PIC X(6).
        02 DUE-DATE, PIC X(5).
        02 PAY-THIS, PIC 9(4)V99.
FD      OUTFILE, VALUE OF ID IS "RPT   OUT", REPORT IS PAYABLES.
REPORT SECTION.
RD      PAYABLES
        CONTROLS ARE FINAL, DUE-DATE
        PAGE LIMIT IS 50 LINES, FIRST DETAIL 4.
01      TYPE PH.
        02 LINE 1.
           03 COLUMN 8, PIC X(13), VALUE "PAYABLES PG.".
           03 COLUMN 21, PIC 99, SOURCE PAGE-COUNTER.
        02 LINE PLUS 1.
           03 COLUMN 1, PIC X(22), VALUE ".NAME...DUE....AMOUNT.".
01      RPT-LINE; TYPE DE; LINE PLUS 1.
        02 COLUMN  1, PIC X(6), SOURCE PAYEE.
        02 COLUMN  8, PIC X(5), SOURCE DUE-DATE.
        02 COLUMN 15, PIC Z,ZZZ.ZZ, SOURCE PAY-THIS.
01      TYPE CF DUE-DATE; LINE PLUS 2.
        02 COLUMN  6, PIC X, VALUE "*".
        02 COLUMN  8, PIC X(5), SOURCE DUE-DATE.
        02 AMT-TOTAL, COLUMN 14, PIC ZZ,ZZZ.ZZ, SUM PAY-THIS.
01      TYPE CF FINAL; LINE PLUS 2.
        02 COLUMN  5, PIC XX, VALUE "**".
        02 COLUMN 14, PIC ZZ,ZZZ.ZZ, SUM AMT-TOTAL.
PROCEDURE DIVISION.
START.
        OPEN INPUT INFILE, OUTPUT OUTFILE.
        INITIATE PAYABLES.
DO-REPORT.
        READ INFILE, AT END GO TO ALL-DONE.
        GENERATE RPT-LINE.
        GO TO DO-REPORT.
ALL-DONE.
        TERMINATE PAYABLES.
        CLOSE INFILE, OUTFILE.
        STOP RUN.
