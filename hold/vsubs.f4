C...FORTRAN SUBROUTINES DEALING WITH VEHICLES - LOAD BEFORE SUBS
C      BECAUSE THEY USE ROUTINES THEREIN.
C
C...CONTAINS:  VNAMES (SET UP VEHICLE NAMES LIST FROM DEMO, SCHEDULE
C                      FILE OR TERMINAL AND EDIT LIST)
C              VCOSTS (BRING IN VEHICLE COSTS FROM FILE OR TERMINAL
C                      AND EDIT THEM)
C              VEHDIC (TRANSLATE VEHICLE ABBREVIATIONS TO FULL-LENGTH
C                      NAMES)
C              TAPPER (TAPPER AND LIST OPTIONS FOR DISPLAYING SINGLES
C                      AND PAIRS FROM DEMO FILES)
C
C
C
C
C
C
C
      SUBROUTINE VNAMES (NAME,NUMV,MAXV,IEDIT,IEND)
C
C...ACCEPT LIST OF VEHICLE NAMES FROM TERMINAL, DEMO FILE OR
C      SCHEDULE FILE, AND/OR ALLOW EDITING OF LIST
C
C...NAME=VEHICLE NAMES ARRAY
C   NUMV=NUMBER OF VEHICLE NAMES
C   MAXV=MAXIMUM NUMBER OF VEHICLES ALLOWED BY CALLING PROGRAM
C   IEDIT='EDIT' TO GO DIRECTLY TO EDITING SECTION, 'ENTER' TO
C      ENTER NAMES FIRST
C   IEND=1 IF LAST COMMAND WITHIN ROUTINE IS "END", =0 IF NOT
C
C...SMALL STANDARD COMMON PACKAGE.............
      COMMON KPROJ,KPROG,KFILE(2),KACBLK,TCPU,ITCCV(30),ITCCU(30),
     1    ITCCL(30),ITCCF(30),ITRPSW,ITYMTR,ITRPAC,IMKUP,ICLI
C...END SMALL STANDARD COMMON PACKAGE.........
C
      DIMENSION NAME(MAXV)
      DIMENSION ICMDS(9),LINE(15),ITITLE(15)
      DOUBLE PRECISION IFNMEX
      DATA ICMDS /'ADD','DEL','REN','NAM','NUM','CLE','CON','END',
     1    'HEL'/
C
      IEND=0
      IF (IEDIT.EQ.'EDIT')   GO TO 510
 100    TYPE 101
  101 FORMAT(' VEHICLE NAMES FROM: ',$)
       ACCEPT 111,ICOM
 111    FORMAT (A3)
      IF (ICOM.EQ.3HTER)   GO TO 150
      IF (ICOM.EQ.3HSCH)   GO TO 300
      IF (ICOM.EQ.3HDEM)   GO TO 400
      TYPE 142
  142 FORMAT('+SOURCES ARE:  DEMO SCHEDULE TERMINAL')
       GO TO 100
C
C...VEHICLES FROM TERMINAL
C
  150 NVEH=0
  160 TYPE 161
  161 FORMAT('+NAMES: ',$)
      CALL RDLINE (LINE)
      IFIRST=1
  165 NAMEV=LDALPH(5)
      IF (NAMEV.EQ.1H )   GO TO 175
      IFIRST=0
      IF (NVEH.EQ.0)   GO TO 172
      DO 170 J=1,NVEH
      IF (NAMEV.NE.NAME(J))   GO TO 170
      TYPE 168,NAMEV
  168 FORMAT('+',A5,' ALREADY ENTERED'/)
      GO TO 165
  170 CONTINUE
  172 IF (NVEH.LT.MAXV)   GO TO 174
      TYPE 173,NAME(MAXV),MAXV
  173 FORMAT('+',A5,' IS VEHICLE NO.',I4/)
      GO TO 510
  174 NVEH=NVEH+1
      NAME(NVEH)=NAMEV
      GO TO 165
  175 IF (IFIRST)   160,160,510
C
C...VEHICLES FROM SCHEDULE (.S) FILE
C
 300    TYPE 301
 301    FORMAT ('+SCHEDULE: ',$)
      ACCEPT 311,IFNM
  311 FORMAT(A5)
      CALL NMFILE (IFNM,'S',IFNMEX,0)
  320 CALL FILNO (12,IFNM,'S',IFNMEX,2,IER)
      IF (IER.NE.0)   GO TO 100
      LOC=1
      CALL RECIN (12,NVEH,1,LOC,IER)
      LOCERR=50
      IF (IER.EQ.0)   GO TO 360
  350 TYPE 351,IER,LOCERR
  351 FORMAT(' VNAMES ERROR',I3,' AT LOC',I4,'.  PLEASE CALL TELMAR.'/)
      DO 355 J=11,15
  355 CALL CLOSE (J,IER)
      CALL ACCT (3)
      STOP
  360 IF (NVEH.LE.MAXV)   GO TO 370
      TYPE 365,NVEH,MAXV
  365 FORMAT('+FILE HAS ',I3,' VEHICLES; MAXIMUM IS',I4)
      GO TO 100
  370 LOC=18
      CALL RECIN (12,NAME,NVEH,LOC,IER)
      LOCERR=60
      IF (IER.NE.0)   GO TO 350
      CALL CLOSE (12,IER)
      GO TO 510
C
C...VEHICLE NAMES FROM DEMO (.D) FILE
C
 400    TYPE 401
 401    FORMAT ('+DEMO: ',$)
      ACCEPT 311,IFNM
      CALL NMFILE (IFNM,'D',IFNMEX,0)
      CALL RDDEM (IFNM,IFNMEX,ICLI,ITITLE,NVEH,NAME,IDUMMY,IDUMMY,
     1    IDUMMY,2,100,IDUMMY,1,3,IER)
      IF (IER.NE.0)   GO TO 100
      CALL CLOSE (12,IER)
C
C...VEHICLE NAMES EDITING
C
 510    TYPE 511
  511 FORMAT('+V: ',$)
      CALL RDLINE (LINE)
      ICOM=LDALPH(3)
      KVEH=LDALPH(5)
      IVPTR=0
      IF (KVEH.EQ.1H )   GO TO 525
      DO 520 J=1,NVEH
      IF (KVEH.NE.NAME(J))   GO TO 520
      IVPTR=J
      GO TO 525
  520 CONTINUE
  525 DO 530 ICMD=1,9
      IF (ICOM.EQ.ICMDS(ICMD))   GO TO 550
 530    CONTINUE
  540 TYPE 545
  545 FORMAT('+I BEG YOUR PARDON?'/)
      GO TO 510
  550 GO TO (160,650,610,750,730,100,780,770,560),ICMD
C
C...HELP
C
  560 TYPE 561
  561 FORMAT('+VEHICLE COMMANDS:'/
     1    ' ADD     DELETE VVVVV'/
     2    ' NAMES   RENAME VVVVV'/
     3    ' NUMBER  CLEAR'/
     4    ' END     CONTINUE'/)
      GO TO 510
C
C...RENAME A VEHICLE
C
  610 IF (IVPTR.NE.0)   GO TO 630
  621 TYPE 625
  625 FORMAT('+VEHICLE NAME IS MISSING OR INVALID'/)
       GO TO 510
  630 TYPE 631
  631 FORMAT('+NEW NAME: ',$)
      ACCEPT 311,KVEH
      IF (KVEH.NE.1H )   GO TO 635
      TYPE 625
      GO TO 630
  635  DO 640 J=1,NVEH
      IF (KVEH.NE.NAME(J))   GO TO 640
      TYPE 638
  638 FORMAT('+NEW NAME IS ALREADY IN USE'/)
      GO TO 630
 640    CONTINUE
      NAME(IVPTR)=KVEH
       GO TO 510
C
C...DELETE A VEHICLE
C
  650 IF (IVPTR.EQ.0)   GO TO 621
  680 IF (IVPTR.EQ.NVEH)   GO TO 710
      DO 700 J=IVPTR,NVEH-1
 700    NAME(J)=NAME(J+1)
 710    NVEH=NVEH-1
       GO TO 510
C
C...NUMBER OF VEHICLES
C
  730 TYPE 735,NVEH
  735 FORMAT('+',I3,' VEHICLES'/)
      GO TO 510
C
C...LIST VEHICLE NAMES
C
  750 DO 755 I=1,NVEH
  755 TYPE 760,I,NAME(I)
  760 FORMAT('+',I3,1X,A5/)
       GO TO 510
C
C...END
C
  770 IEND=1
C
C...CONTINUE
  780 NUMV=NVEH
      IF (NVEH.GE.1.OR.IEND.EQ.1)   RETURN
      TYPE 790
  790 FORMAT('+NO VEHICLE NAMES')
      GO TO 100
      END
      SUBROUTINE VCOSTS (NAMEV,NVEH,MAXV,NAMEC,NCOSTS,ICOSTS,JCHAN,
     1    IEDIT,IEND)
C
C...ACCEPT AND EDIT UP TO 10 SETS OF VEHICLE COSTS.  THE COSTS MAY
C      COME FROM A COSTS FILE - (P3SS)VCOSTS.COS - OR FROM THE
C      TERMINAL.  ALL COSTS NOT FOUND IN THE FILE ARE REQUESTED FROM THE
C      TERMINAL.
C
C...NAMEV=ARRAY OF VEHICLE NAMES BEING USED IN THIS RUN
C   NVEH=NUMBER OF VEHICLES IN NAMEV
C   MAXV=MAXIMUM VALUE OF NVEH; NOTE THAT THIS IS USED AS ONE
C      DIMENSION OF ARRAY ICOSTS
C   NAMEC=ARRAY OF COST NAMES REQUESTED BY CALLING PROGRAM.  THE
C      NAMES ARE USED ON FIRST 3 CHARACTERS ONLY.  TYPICAL ENTRIES
C      ARE:  P4C  HPB  ETC.
C   NCOSTS=NUMBER OF ENTRIES IN NAMEC
C   ICOSTS=COSTS ARRAY FILLED IN BY THIS ROUTINE;  ICOSTS(JV,JC)=
C      COST FOR VEHICLE JV, COST TYPE JC
C   JCHAN=CHANNEL ON WHICH TO OPEN AND READ COSTS FILE
C   IEDIT='EDIT' TO GO DIRECTLY TO COSTS EDITING SECTION, ='ENTER'
C      TO ENTER COSTS FIRST
C   IEND=1 IF LAST COMMAND WITHIN VCOSTS IS "END", =0 IF NOT
C
C
C...THE STRUCTURE OF THE COSTS FILE IS:
C      WORD 1                           NUMBER OF COST TYPES  NFCOST
C           2                           NUMBER OF VEHICLES  NFVEH
C           3                           NAME OF FIRST COST TYPE
C           4                           STARTING WORD NUMBER IN FILE
C                                         OF FIRST COST ARRAY
C           5-2*NFCOST+2                SIMILARLY FOR OTHER COST TYPES
C           NEXT-NEXT+NVEH-1            VEHICLE NAMES
C           LOC1 (SEE WORD 4).....      COST TYPE 1 FOR ALL VEHICLES
C           LOC2 (SEE WORD 6).....      COST TYPE 2, ETC.
C
C
C
      DIMENSION NAMEV(NVEH),NAMEC(NCOSTS),ICOSTS(MAXV,NCOSTS),
     1    IVPTR(100),ICPTR(10),LINE(15),NAMEVF(100),JCOSTS(100),IEQ(2)
      EQUIVALENCE (NAMEVF,JCOSTS),(NAMECF,IEQ(1)),(LOCOST,IEQ(2))
C
      IEND=0
      IF (IEDIT.EQ.'EDIT')   GO TO 300
   20 TYPE 8010
 8010 FORMAT(' COSTS FROM FILE? ',$)
      CALL BYESNO (IYN,1)
      IF (IYN.EQ.2)   GO TO 200
C
C...ACCEPT COSTS FROM CENTRAL COSTS FILE
C
      ICHAN=JCHAN
      CALL OPEN (ICHAN,'VCOSTS.COS',"3374,"41720,1,IER)
      LOCERR=10
      IF (IER.NE.0)   GO TO 900
C...NUMBER OF VEHICLES IN FILE
      LOC=2
      CALL RECIN (ICHAN,NFVEH,1,LOC,IER)
      LOCERR=20
      IF (IER.NE.0)   GO TO 900
C...NUMBER OF COST TYPES IN FILE
      LOC=1
      CALL RECIN (ICHAN,NFCOST,1,LOC,IER)
      LOCERR=30
      IF (IER.NE.0)   GO TO 900
C
C...MATCH REQUESTED VEHICLES AGAINST VEHICLES IN COSTS FILE
C
      DO 40 JV=1,NVEH
      IVPTR(JV)=0
      DO 40 JC=1,NCOSTS
   40 ICOSTS(JV,JC)=0
      LOC=2*NFCOST+3
      CALL RECIN (ICHAN,NAMEVF,NFVEH,LOC,IER)
      LOCERR=35
      IF (IER.NE.0)   GO TO 900
      DO 60 JV=1,NVEH
      NAMEVJ=NAMEV(JV)
      DECODE (5,7010,NAMEVJ)  INHOME,NAMEVX
 7010 FORMAT(A2,A3)
      IF (INHOME.EQ.'I/')   NAMEVJ=NAMEVX
      DO 50 JVF=1,NFVEH
      IF (NAMEVJ.NE.NAMEVF(JVF))   GO TO 50
      IVPTR(JV)=JVF
   50 CONTINUE
   60 CONTINUE
C
C...MATCH COST TYPES AGAINST TYPES IN COSTS FILE
C
   90 DO 110 JC=1,NCOSTS
      LOC=3
      DO 100 JCF=1,NFCOST
C...READ NAME OF COST TYPE AND FIRST WORD LOCATION OF ITS ARRAY
      CALL RECIN (ICHAN,IEQ,2,LOC,IER)
      LOCERR=50
      IF (IER.NE.0)   GO TO 900
      IF (NAMEC(JC).EQ.NAMECF)   GO TO 110
  100 CONTINUE
C...COST TYPE NOT IN FILE
      LOCOST=0
  110 ICPTR(JC)=LOCOST
C
C...BRING IN COSTS
C
      DO 140 JC=1,NCOSTS
      IF (ICPTR(JC).EQ.0)   GO TO 140
      LOC=ICPTR(JC)
      CALL RECIN (ICHAN,JCOSTS,NFVEH,LOC,IER)
      LOCERR=57
      IF (IER.NE.0)   GO TO 900
      DO 130 JV=1,NVEH
      IF (IVPTR(JV).NE.0)   ICOSTS(JV,JC)=JCOSTS(IVPTR(JV))
  130 CONTINUE
  140 CONTINUE
C
C...GET ANY MISSING COSTS FROM TERMINAL
C
      DO 160 JV=1,NVEH
      DO 150 JC=1,NCOSTS
      IF (ICOSTS(JV,JC).GT.0)   GO TO 150
      TYPE 8050,NAMEC(JC),NAMEV(JV)
 8050 FORMAT('+',A3,' IN ',A5,' = ',$)
      ACCEPT 7020,ICOSTS(JV,JC)
 7020 FORMAT(I)
  150 CONTINUE
  160 CONTINUE
      GO TO 300
C
C...ALL COSTS FROM TERMINAL
C
  200 TYPE 8060,(NAMEC(JC),JC=1,NCOSTS)
 8060 FORMAT('+',5X,11(3X,A3))
      TYPE 8070
 8070 FORMAT(1X)
      DO 210 JV=1,NVEH
      TYPE 8080,NAMEV(JV)
 8080 FORMAT('+',A5,1X,$)
  210 ACCEPT 7030,(ICOSTS(JV,JC),JC=1,NCOSTS)
 7030 FORMAT(10I)
C
C...COSTS EDITING
C
  300 TYPE 8090
 8090 FORMAT('+C: ',$)
      CALL RDLINE (LINE)
      IOPT=LDALPH (3)
      NAME=LDALPH (5)
      IVNUM=0
      IF (NAME.EQ.1H )   GO TO 308
      DO 302 JV=1,NVEH
      IF (NAME.EQ.NAMEV(JV))   GO TO 304
  302 CONTINUE
      TYPE 8095
 8095 FORMAT('+INVALID VEHICLE NAME'/)
      GO TO 300
  304 IVNUM=JV
  308 IF (IOPT.EQ.'LIS')   GO TO 310
      IF (IOPT.EQ.'REE')   GO TO 330
      IF (IOPT.EQ.'CLE')   GO TO 20
      IF (IOPT.EQ.'END')   GO TO 400
      IF (IOPT.EQ.'CON')   GO TO 410
      TYPE 8100
 8100 FORMAT('+COSTS COMMANDS:'/
     1    ' REENTER VVVVV CCC'/
     2    ' LIST (VVVVV OR RETURN)'/
     3    ' CLEAR'/
     4    ' CONTINUE'/
     5    ' END'/)
      GO TO 300
C
C...LIST COSTS
C
  310 TYPE 8060,(NAMEC(JC),JC=1,NCOSTS)
      JVA=1
      JVB=NVEH
      IF (IVNUM.EQ.0)   GO TO 315
      JVA=IVNUM
      JVB=IVNUM
  315 DO 320 JV=JVA,JVB
  320 TYPE 8110,NAMEV(JV),(ICOSTS(JV,JC),JC=1,NCOSTS)
 8110 FORMAT(1X,A5,5(1X,I5))
      TYPE 8070
      GO TO 300
C
C...REENTER
C
  330 IF (IVNUM.NE.0)   GO TO 350
      TYPE 8095
      GO TO 300
  350 NAMECA=LDALPH (3)
      DO 360 JC=1,NCOSTS
      IF (NAMECA.EQ.NAMEC(JC))   GO TO 370
  360 CONTINUE
      TYPE 8130
 8130 FORMAT('+INVALID COST TYPE'/)
      GO TO 300
  370 TYPE 8140,ICOSTS(IVNUM,JC)
 8140 FORMAT('+CHANGE FROM ',I5,' TO = ',$)
      ACCEPT 7020,ICOSTS(IVNUM,JC)
      GO TO 300
C
C...END
C
  400 IEND=1
C
C...CONTINUE
C
  410 CALL CLOSE (ICHAN,IER)
      RETURN
C
C...I/O ERROR
C
  900 TYPE 8900,IER,LOCERR
 8900 FORMAT(' VCOSTS ERROR ',I2,' AT LOC.',I2,'.  PLEASE CALL TELMAR.')
      DO 910 J=11,15
  910 CALL CLOSE (J,IER)
      CALL ACCT (3)
      STOP
      END
      SUBROUTINE VEHDIC (NAMEV,NVEH,IDICV,MAXV,JCHAN)
C
C...EXTRACT FROM VEHICLE DICTIONARY THE 20-CHARACTER NAMES OF
C      SPECIFIED VEHICLES
C
C...NAMEV=ARRAY OF SELECTED VEHICLE NAMES
C   NVEH=NUMBER OF NAMES IN NAMEV
C   IDICV=ARRAY IN WHICH LONG NAMES WILL BE STORED BY THIS
C      ROUTINE
C   MAXV=MAXIMUM VALUE OF NVEH; NOTE THAT IT IS USED TO DIMENSION
C      IDICV
C   JCHAN=CHANNEL FOR OPENING AND READING THE DICTIONARY FILE
C
C
C...THE STRUCTURE OF THE VEHICLE DICTIONARY FILE IS:
C      WORD 1                  NUMBER OF VEHICLES   NFVEH
C      WORDS 2-NFVEH+1         VEHICLE ABBREVIATIONS
C      WORDS NFVEH+2-NFVEH+6   VEHICLE 1: ABBREVIATION IN FIRST
C                              WORD, LONG NAME IN NEXT 4 WORDS
C      WORDS NFVEH+7-NFVEH+11  DITTO FOR SECOND VEHICLE, ETC.
C...THIS STRUCTURE ALLOWS ACCESS EITHER BY FINDING THE ABBREVIATION
C      IN THE COMPACT LIST AND GOING TO ITS NAME FURTHER DOWN THE
C      FILE, OR BY MOVING SEQUENTIALLY THROUGH THE ABBREVIATION/NAME
C      PAIRS.  THIS ROUTINE USES THE LATTER METHOD IN THE BELIEF
C      THAT IT WILL MINIMIZE DISK ACCESSES.
C...THE DICTIONARY FILE IS (P3SS)VDICT.DIC
C
      DIMENSION NAMEV(MAXV),IDICV(MAXV,4),IDENTR(5)
C
      ICHAN=JCHAN
      CALL CLOSE (ICHAN,IER)
      LOCERR=5
      IF (IER.EQ.0)   GO TO 100
      DO 20 JV=1,NVEH
      IDICV(JV,1)=NAMEV(JV)
      IDICV(JV,2)=1H 
      IDICV(JV,3)=1H 
   20 IDICV(JV,4)=1H 
      CALL OPEN (ICHAN,'VDICT.DIC',"3374,"41720,1,IER)
      LOCERR=10
      IF (IER.NE.0)   GO TO 100
      LOC=2
      CALL RECIN (ICHAN,IDUMMY,1,LOC,IER)
      LOCERR=20
      IF (IER.NE.0)   GO TO 100
      LOC=1
      CALL RECIN (ICHAN,NFVEH,1,LOC,IER)
      LOCERR=30
      IF (IER.NE.0)   GO TO 100
      IFOUND=0
      LOC=NFVEH+2
      DO 50 JVF=1,NFVEH
      CALL RECIN (ICHAN,IDENTR,5,LOC,IER)
      LOCERR=40
      IF (IER.NE.0)   GO TO 100
      ENCODE (5,8005,IHNAME)  IDENTR(1)
 8005 FORMAT('I/',A3)
      DO 40 JV=1,NVEH
      IF (NAMEV(JV).NE.IDENTR(1).AND.NAMEV(JV).NE.IHNAME)   GO TO 40
      DO 30 J=1,4
   30 IDICV(JV,J)=IDENTR(J+1)
      IFOUND=IFOUND+1
      IF (IFOUND-NVEH)   50,60,60
   40 CONTINUE
   50 CONTINUE
   60 CALL CLOSE (ICHAN,IER)
      RETURN
C
C...I/O ERROR
C
  100 TYPE 8010,IER,LOCERR
 8010 FORMAT(' VEHDIC ERROR ',I2,' AT LOC.',I3,
     1    '.  PLEASE CALL TELMAR.'/)
      DO 110 J=11,15
  110 CALL CLOSE (J,IER)
      CALL ACCT (3)
      STOP
      END
      SUBROUTINE TAPPER (INRCH,MSW,NUM,INM,IAUD,IBASE,IDUP)
C
C...TYPE AUDIENCES FROM A DEMO FILE
C
C...INRCH=1 IF DEMO HAS PAIRS, =2 IF NOT
C   MSW=1 FOR TAPPER, =2 FOR LIST (BRIEFER OUTPUT)
C   NUM=NUMBER OF VEHICLES IN DEMO
C   INM=VEHICLE NAMES VECTOR
C   IAUD=SINGLES VECTOR
C   IBASE=DEMO BASE POPULATION
C   IDUP=PAIRS & SELF-PAIRS VECTOR
C
        COMMON KPROJ,KPROG,KFILE,KACBLK,TCPU,ITCCV,ITCCU,ITCCL,
     1          ITCCF,ITRPSW,ITYMTR,ITRPAC,IMKUP,ICLI
      DIMENSION KFILE(2),ITCCV(30),ITCCU(30),ITCCL(30),ITCCF(30)
      DIMENSION IAUD(100),INM(100),IDUP(5050),NAME(100),COV(10)
      DIMENSION DUP(10),ITITLE(15),IAST(2),NAMERJ(100)
        DATA IAST/'*    ','     '/,ICRTN/"64324020100/
C
      ISW=MSW
      JSW=2*ISW+10
      ITCCV(JSW)=ITCCV(JSW)+1
      JSW=JSW+1
      IF (ISW.EQ.1)   CALL TITIN(ITITLE)
      CALL RJNAME (INM,NAMERJ,NUM)
      CALL FFVEH (NUM,INM,NVEH,NAME)
      RBASE=1./IBASE
      IF (ISW.EQ.1)   CALL PRITIT(ITITLE)
      TYPE 5430,IBASE
      ITCCV(JSW)=ITCCV(JSW)+2*NVEH+40
      IF (ISW.EQ.2)   GO TO 1307
      TYPE 1306
      ITCCV(JSW)=ITCCV(JSW)+NVEH+80
 1307 DO 1320 I=1,NVEH
      LI=NAME(I)
        TYPE 5505,ICRTN
      TYPE 5450,INM(LI),IAUD(LI)
      IF (ISW.EQ.2)   GO TO 1320
      COVER=IAUD(LI)*RBASE
      TYPE 5451,COVER
 1320 CONTINUE
      IF (INRCH.EQ.2)   GO TO 1800
      TYPE 5500
      CALL BYESNO(IYESNO,1)
      IF (IYESNO.EQ.2)   GO TO 1800
C
C...PAIRS
      IF (ISW.EQ.1)   TYPE 5510
      IF (ISW.EQ.2)   TYPE 5511
      IFAC=5
      IF (ISW.EQ.2)   GO TO 1420
      TYPE 5520
      IFAC=2
 1420 ITCCV(JSW)=ITCCV(JSW)+NVEH*NVEH/IFAC
      KVEH=1
 1500 LVEH=MIN0(KVEH+9,NVEH)
      TYPE 5670
      DO 1510 I=KVEH,LVEH
      LI=NAME(I)
 1510 TYPE 5600,NAMERJ(LI)
      DO 1700 I=KVEH,NVEH
      LI=NAME(I)
        TYPE 5505,ICRTN
      IF (ISW.EQ.1)   TYPE 1520
      TYPE 5610,INM(LI),IAST(ISW)
      AUDI=IAUD(LI)
      II=MIN0(I,LVEH)
 1540 DO 1550 J=KVEH,II
      LJ=NAME(J)
      LIJ=IJCAL(LI,LJ)
      TYPE 5620,IDUP(LIJ)
      K=J+1-KVEH
      IF (ISW.EQ.2)   GO TO 1550
      REACH=IDUP(LIJ)
      COV(K)=REACH*RBASE
      AUDJ=IAUD(LJ)
      DUP(K)=(AUDI+AUDJ-REACH)*RBASE
 1550 CONTINUE
      IF (ISW.EQ.2)   GO TO 1700
      TYPE 5505,ICRTN
      TYPE 5630
      DO 1560 J=KVEH,II
 1560 TYPE 5640,COV(J-KVEH+1)
      TYPE 5505,ICRTN
      TYPE 5650
      DO 1670 J=KVEH,II
 1670 TYPE 5640,DUP(J-KVEH+1)
 1700 CONTINUE
      KVEH=LVEH+1
      IF (KVEH.GT.NVEH)   GO TO 1800
      IF (ISW.EQ.1)   TYPE 1520
      GO TO 1500
 1800 CONTINUE
        TYPE 1301
      RETURN
C
C
 1301 FORMAT(//)
 1520 FORMAT(1X)
 5430 FORMAT('0BASE=',I6/'0VEHICLE AUDIENCE',$)
 5440 FORMAT(/1X'VEHICLE AUDIENCE',$)
1306   FORMAT('+ COVERAGE',$)
5450    FORMAT('+',A5,I11,$)
5451    FORMAT ('+',F9.3,$)
 5500 FORMAT(/1X'PAIRS? ',$)
5505    FORMAT(1X,A2)
 5510 FORMAT(1X'*  NET UNDUPLICATED AUDIENCE')
 5511 FORMAT(' NET UNDUPLICATED AUDIENCE')
5520    FORMAT(1X,'** NET UNDUPLICATED COVERAGE'/1X
     1,'***DUPLICATED COVERAGE')
5600    FORMAT('+',1X,A5,$)
5610  FORMAT ('+',A5,A3,$)
5620    FORMAT (1H+,I6,$)
5630    FORMAT ('+',5X,'** ',$)
5640    FORMAT ('+',2X,F4.3,$)
5650    FORMAT ('+',5X,'***',$)
 5670 FORMAT(/9X,$)
      END
    