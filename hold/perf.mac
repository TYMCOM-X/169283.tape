TITLE	PERF FOR LIBOL 5(223)		
SUBTTL	SET UP A PERFORM			AL BLACKINGTON/CAM

;COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORP, MAYNARD, MA.

	HISEG

ENTRY PERF.

PERF.:	SKIPE	0(PA)		;EXIT WORD IN USE?
	JRST	PERF.E		;YES--ERROR


	HRRZ	TA,0(PP)	;GET OUR RETURN
	ADDI	TA,1		;THAT + 1 IS RETURN FOR "EXIT."

	AOS	LEVEL.		;BUMP DEPTH OF PERFORM

	HRL	TA,LEVEL.	;STASH THAT IN EXIT WORD

	SKIPE	OVFLG.		;IS OVFLG. ZERO?
	TLO	TA,1B18		;NO--SET FLAG IN EXIT WORD

	MOVSM	TA,0(PA)	;SET EXIT WORD

	LDB	TA,OVCODE	;IF CODE IN AC-FIELD IS
	CAIN	TA,1		;  1,
	MOVEM	TA,OVFLG.	;RESET OVFLG.
	SKIPE	DPLOC.		;IF COMPILED BY COBOL V3 OR LATER,
	SKIPN	TRAC3.		;  RETURN OR PUSH-DOWN TRACE
	POPJ	PP,		;  ELSE SIMPLY RETURN
	JRST	@TRAC3.		;NOTE: TRAC3.=0 IF COMPILED WITH /P



;ERROR -- EXIT WORD IS BEING USED

PERF.E:	TTCALL	3,[ASCIZ "EXIT BEING USED BY TWO PERFORMS"]
	JRST	KILL.


OVCODE:	POINT 3,PA,12		;PART OF THE UUO'S AC-FIELD
SUBTTL EXIT FROM A PERFORMED ROUTINE	AL BLACKINGTON

ENTRY EXIT.

EXIT.:	SKIPN	TA,0(PA)	;ANY EXIT SET UP?
	POPJ	PP,		;NO--FALL THRU TO NEXT STATEMENT

	MOVE	TB,TA		;IS
	ANDI	TB,377777	;  LEVEL
	CAME	TB,LEVEL.	;    CORRECT?
	JRST	EXIT.E		;NO--ERROR

	HLRM	TA,0(PP)	;SET RETURN ADDRESS

	SETZM	0(PA)		;CLEAR EXIT WORD

	SOS	LEVEL.		;DECREMENT LEVEL OF PERFORM
	TRNN	TA,1B18		;SHOULD WE RESET OVFLG.?
	SETZM	OVFLG.		;YES--DO SO

	SKIPE	DPLOC.		;IF COMPILED BY COBOL V3 OR LATER,
	SKIPN	TRAC2.		;  RETURN OR POP-UP TRACE
	POPJ	PP,		;  ELSE SIMPLY RETURN
	JRST	@TRAC2.		;NOTE: TRAC2.=0 IF COMPILED WITH /P


;EXITING TO WRONG LEVEL

EXIT.E:	TTCALL	3,[ASCIZ "EXIT FAILURE"]
	JRST	KILL.

EXTERNAL KILL.	;ROUTINE WHICH WILL KILL THE RUN

EXTERNAL LEVEL.	;CURRENT DEPTH OF THE PERFORM
EXTERNAL OVFLG.	;OVERLAY CODE
EXTERNAL TRAC2.	;ADDRESS OF TRPOP.
EXTERNAL TRAC3.	;ADDRESS OF TRPD.
EXTERNAL DPLOC.	;ZERO IF COMPILED BY EARLIER THAN COBOL V3


	TA=10	;TEMPORARY
	TB=11	;TEMPORARY
	PA=16	;THE "EXIT." UUO
	PP=17	;PUSH-DOWN POINTER

	END
