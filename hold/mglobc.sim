
! PROGRAM TO TAKE A LOADER COMMAND FILE NAMED BY USER AND CREATE A
! COMMAND FILE FOR DEFERRED PROCESSING TO RUN GLOB.


!  THE FOLLOWING MACRO COMPUTES A GENERAL BYTE POINTER SO THAT NCHV ON
!  THE PDP-10 WILL DO ILDB'S FOR ANY BYTE SIZE.
DEF BYPT(Y,P,S) AS [6,6,6,18]*[IF P>=0 THEN 36-S*(P MOD(36/S)-1)&
        ELSE 36-S*(36/S-(-P)MOD(36/S)+1),S,0,&
        IF P>=0 THEN Y+P/(36/S) ELSE Y+(P+1)/(36/S)-1]
DEF NBYV AS NCHV
DEF BYV AS CHV

LOCAL A(100),F1,F2,IS,OS,OS1,OS2,C
LOCAL %WRSTR,%WRSTRL,EOF:,STRT:

STRT:  IOCS(0)
\\->STRT
RSTRT:  MSG('COMMAND FILE THAT LOADS PROGRAM: ')
  IS_GETLINE(TEL)
  F1_OPEN(IMAGE,SEQUEN+INPUT+CHARACTER,RSTRT,2)
  EXTARG(FIDP)_OCT 475443.000000  !SIXBIT GLC FOR EXTENSION
  F2_OPEN(-FIDP,SEQUEN+OUTPUT+CHARACTER,0,2)
  OS2_OS1_OS_CHPT(A,-1)

  WRSTRL(F2,':GLOB.DUP'); W(F2,CARRET)
  WRSTRL(F2,'PDP10'); W(F2,CARRET)
  WRSTRL(F2,'RUN (WALT)GLOB'); W(F2,CARRET)
  SETRWE(F1,EOF)
  IS_GETLINE(F1)
  LOOP DO
    LOOP DO
      C_NCHV IS
      WHILE C#$+ AND C#$, AND C#$; AND C#$= AND C#CARRET
      NCHV OS_C
    END
    EIF C=$+ THEN OS_OS1
    ORIF C=$; THEN IS_GETLINE(F1)
    ELSE DO
      NCHV OS_CARRET
      OS1_OS
      EIF C=$; OR C=CARRET THEN IS_GETLINE(F1)
      FIF C=$= THEN DO
        LOOP DO
          C_NCHV IS
          WHILE C#$, AND C#$; AND C#CARRET
        END
        IF C#$, THEN IS_GETLINE(F1)
      END
    END
  END

EOF:  IF ERRNUM # EOFERR THEN [MSG('?UNEXPLAINED ERROR$'); EXIT]
  NCHV OS_EOLIT
  WRSTR(F2,OS2)
  OS_OS2
  IS_BYPT(@NAMARG(FIDP),-1,6)
  EXTARG(FIDP)_0
  LOOP DO
    C_NBYV IS
    WHILE C#0
    NCHV OS_C+OCT 40
  END
  NCHV OS_$=; NCHV OS_OCT 33    !ALT MODE
  NCHV OS_OCT 3    !CONTROL C
  NCHV OS_EOLIT; WRSTR(F2,OS2)
  CLOSE.ALL
  EXIT

%WRSTR(F,S)
  WHILE NCHV S#EOLIT THEN W(F,CHV S)
  RETURN
END WRSTR

%WRSTRL(F,B)
  WRSTR(F,CHPT(B,-1))
  RETURN
END WRSTRL
