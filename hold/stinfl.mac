TITLE	STINFL FOR COBOL 5(61)		
SUBTTL	INITIALIZE AN INPUT FILE		AL BLACKINGTON/CAM

;COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORP, MAYNARD, MA

TWOSEG
RELOC	400000


	ENTRY	STINFL		;SET UP AN INPUT FILE
	ENTRY	OPENIT		;DO "OPEN"--SET UP FOR "LOOKUP" & "ENTER"

	EXTERNAL SIXOUT, RESTRT, ERATYP, FILOUT

STINFL:	HRRZ	DA,SRCEND	;GET ADDRESS OF NEXT SOURCE FILE DATA
	CAIE	DA,SRCEND	;ANY MORE ENTRIES?
	SKIPN	0(DA)		;YES--EMPTY?
	JRST	OPNIN7		;YES--NO MORE SOURCE FILES

	MOVE	TA,DEVSW(DA)	;IS IT LIBRARY FILE?
	TRNN	TA,1
	TLOA	DA,SRCDEV	;NO
	HRLI	DA,LIBDEV	;YES
	MOVS	TA,DA
	MOVEI	DA,(TA)
	PUSH	PP,DEVBUF(DA)
	BLT	TA,DEVSIZ-1(DA)
	POP	PP,DEVBUF(DA)

	MOVEI	TA,DEVSIZ
	ADDM	TA,SRCEND

	MOVE	TA,DEVDEV(DA)	;GET THE DEVICE
	CALLI	TA,$DEVCH	;GET CHARACTERISTICS

	MOVE	TB,DEVSW(DA)	;IS THIS THE LIBRARY?
	TRNE	TB,1
	JRST	STINFA		;YES

	MOVEI	DC,SRC		;NO--USE SOURCE CHANNEL
	JRST	OPENIN

STINFA:	TLNN	TA,$DSK		;IS DEVICE A DSK?
	JRST	NOTDSK		;NO--ERROR

	MOVEI	DC,LIB		;USE LIBRARY CHANNEL

	MOVE	TB,[SIXBIT /LIBARY/]	
	SKIPN	DEVFIL(DA)	;ANY FILE NAME?
	MOVEM	TB,DEVFIL(DA)	;NO--USE "LIBARY"
;INITIALIZE AN INPUT FILE

OPENIN:	MOVEI	I1,0		;ASCII MODE
	MOVEI	I3,DEVBH(DA)	;CREATE AN XWD
	PUSHJ	PP,OPENIT	;DO "OPEN", SET UP FOR "LOOKUP"

	MOVE	I0,LOOKOP	;CREATE A LOOKUP
	DPB	DC,I0CHAN
	MOVE	I4,DEVPP(DA)	;GET PROJ, PROG
	JUMPN	I2,OPNIN2	;ANY EXTENSION?

OPNIN1:	XCT	I0		;NO EXTENSION--DO LOOKUP
	SKIPA			;NOT FOUND
	JRST	OPNIN3

	MOVE	TA,DEVSW(DA)	;IS IT THE LIBRARY?
	TRNN	TA,1
	TLOA	I2,(SIXBIT "CBL")	;NO--USE "CBL"
	HRLZI	I2,(SIXBIT "LIB")	;YES--USE "LIB"
	XCT	I0
	JRST	NOLOOK		;DIDN'T FIND THAT EITHER--ERROR
	JRST	OPNIN3

OPNIN2:	XCT	I0		;DO LOOKUP
	JRST	NOLOOK		;ERROR

OPNIN3:	MOVSI	TA,I1		;SAVE SOURCE FILE INFO FOR LISTING
	HRRI	TA,SRCFIL##
	BLT	TA,SRCFIL+2
	HLLZ	TA,SRCFIL+1	;PUT EXT IN BYTES 2, 3, 4
	LSH	TA,-6		;  SO SIXIT OF COBOLF WORKS
	MOVEM	TA,SRCFIL+1
	MOVE	TA,DEVSW(DA)	;GET SWITCHES
	TRNE	TA,1		;LIBRARY?
	JRST	OPNIN4		;YES
	TRNE	TA,2		;NO--REWIND?
	MTAPE	SRC,$REW	;YES--REWIND MTA
;SET UP A BUFFER

OPNIN4:	SKIPN	TA,DEVBUF(DA)
	MOVE	TA,JOBFF

	MOVEM	TA,JOBFF
	MOVEM	TA,DEVBUF(DA)

	MOVE	I0,INBOP
	CAIN	DC,LIB		;IS THIS FOR LIBRARY?
	HRRI	I0,1		;YES--SINGLE BUFFERED

	DPB	DC,I0CHAN
	XCT	I0

	SETZM	DEVBLK(DA)

	CAIN	DC,LIB		;LIBRARY FILE?
	JRST	OPNIN6		;YES
	ADDI	TA,406		;NO--MAKE ROOM FOR TWO BUFFERS
	HRRM	TA,JOBFF
	POPJ	PP,

OPNIN6:	ADDI	TA,203		;MAKE ROOM FOR ONE BUFFER
	HRRM	TA,JOBFF
	JRST	STINFL

;NO MORE SOURCE FILES

OPNIN7:	SETZM	SRCDEV
	POPJ	PP,
;OPEN THE FILE AND SET UP PARAMETERS FOR ENTER OR LOOKUP

OPENIT:	MOVE	I2,DEVDEV(DA)	;GET DEVICE NAME
	MOVE	I0,OPENOP	;CREATE AN OPEN
	DPB	DC,I0CHAN
	XCT	I0		;OPEN
	JRST	CANTOP		;CANNOT--ERROR

	MOVE	I1,DEVFIL(DA)	;GET FILE NAME
	MOVE	I2,DEVEXT(DA)	;GET EXTENSION
	MOVEI	I3,0		;ZERO IN THIRD WORD
	POPJ	PP,
;ERRORS WHILE INITIALIZING THE DEVICE

;DEVICE UNAVAILABLE
CANTOP:	MOVEI	TB,MESS3


TYPEIT:	MOVEI	CH,"?"
	TTCALL	1,CH

	MOVE	TA,DEVDEV(DA)
	PUSHJ	PP,SIXOUT
TYPIT1:	TTCALL	3,(TB)
TYPIT2:	TSWT	FDSKC;
	SWOFF	FECOM;
	JRST	RESTRT

;LOOKUP FAILURE

NOLOOK:	TTCALL	3,[ASCIZ "?CANNOT FIND "]

	HRRZ	TA,I2
	JUMPN	TA,ERATYP
	PUSHJ	PP,FILOUT
	JRST	TYPIT2


;LIBRARY DEVICE NOT DSK

NOTDSK:	MOVEI	TB,MESS4
	JRST	TYPIT1
;ERROR MESSAGES

MESS3:	ASCIZ	": UNAVAILABLE"
MESS4:	ASCIZ	"?LIBRARY FILES MUST BE ON DISK"

EXTERNAL DEVDEV,DEVSW,DEVPP,DEVFIL,DEVEXT,DEVBH,DEVBUF,DEVBLK
EXTERNAL JOBFF,LOOKOP,OPENOP,INBOP,I0CHAN
EXTERNAL SRCEND,SRCDEV,LIBDEV,DEVSIZ


	END
