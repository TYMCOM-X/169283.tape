C!!! THIS IS A SPECIAL ESQUIRE VERSION OF RANK.  THERE ARE 2
C    DIFFERENCES BETWEEN THIS AND STANDARD RANK:  1. THIS VERSION
C    OMITS THE "TELMAR MEDIA REPORT" HEADING;  2. THE 1-DEMO STANDARD
C    FORMAT IS  AUD COMP SPA PBW SPA P4C
C          ANTHONY GRAY     3/12/73
C
C...PRETTY REPORTS PROGRAM     ANTHONY GRAY  FEB. 1973
C
C...THIS PROGRAM PRODUCES "PRETTY" REPORTS (A MATTER OF TASTE, TO BE
C      SURE).  IT IS INTENDED AS A LESS FLEXIBLE BUT LESS VERBOSE
C      SUBSTITUTE FOR THE 940 RANK PROGRAM.  THERE ARE TWO BASIC
C      REPORT TYPES - THE 1-DEMO AND 2-DEMO REPORTS.  A 1-DEMO REPORT
C      DISPLAYS SEVERAL ITEMS PER VEHICLE FOR ONE DEMO;  A 2-DEMO
C      REPORT DISPLAYS SEVERAL ITEMS PER VEHICLE FOR EACH OF TWO DEMOS,
C      SIDE BY SIDE, FOR COMPARISON PURPOSES.  THE REPORT CAN BE
C      RANKED ON ANY COLUMN.  THE AVAILABLE ITEMS (COLUMN DEFINITIONS)
C      ARE:
C          AVERAGE-ISSUE AUDIENCE       FIELD WIDTH 7 CHARS
C          % COVERAGE OF AVG-ISSUE                  6
C          % COMPOSITION                            6
C          CUME AUDIENCE (SELF-PAIR)                7
C          TURNOVER RATE (C2/C1)                    6
C          INDEX                                    5
C          SPACE (5 BLANKS)                         5
C          CPM                                      7
C
C...IF COMPOSITION IS REQUESTED, THE USER IS ASKED TO SUPPLY A BASE
C      DEMO.  INDEX IS DEFINED DIFFERENTLY FOR THE 1-DEMO AND
C      2-DEMO CASES.  IN A 1-DEMO REPORT, INDEX=COVERAGE WITHIN
C      TARGET DEMO DIVIDED BY COVERAGE WITHIN COMPOSITION BASE DEMO;
C      IN A 2-DEMO REPORT, INDEX=ANY COLUMN UNDER SECOND DEMO
C      DIVIDED BY CORRESPONDING COLUMN UNDER FIRST DEMO (THE USER
C      SPECIFIES WHICH COLUMN).
C
C
C...PRICING:  $1.00 PER RUN + $.30 PER DEMO READ + $.05 PER VEHICLE
C      (CHARGED EVERY TIME USER LEAVES VNAMES ROUTINE) + $.04 PER
C      NUMBER FOR FIRST 300 NUMBERS, + $.035 PER NUMBER FOR NUMBERS
C      301-500, + $.03 PER NUMBER FOR NUMBERS 501+.  THE MINIMUM
C      CHARGE PER TABLE IS $1.00;  THE MINIMUM PER RUN IS $3.00
C
C
C
C...SMALL STANDARD COMMON PACKAGE.............
      COMMON KPROJ,KPROG,KFILE(2),KACBLK,TCPU,ITCCV(30),ITCCU(30),
     1    ITCCL(30),ITCCF(30),ITRPSW,ITYMTR,ITRPAC,IMKUP,ICLI
C...END SMALL STANDARD COMMON PACKAGE.........
C
      COMMON /RANK/ NAMEV(100),NAMEVD(100),IAUD(100,2),IAUDD(100),
     1    AUDC(100),ICUME(100,2),IVPTR(100),NAMEC(4),ICOSTS(100,4),
     2    IBASE(2),BASE(2),BASEC,ITITLE(30,25),ITITLA(15),ITITLB(15),
     3    ITITLC(15),ICDEFA(25),ICDEFN(25),ICDEFC(25),NAMDEM(25,5,2),
     4    IDEMPM(25,5,2),LINE(15),IEDIT,NDEM,NCOL,NCTOT,ICOMSW,
     5    ICUMSW,IWIDTH,NCHAR,NCHAR1,IRNCOL,INDCOL,NREP,IEND,ITITSW,
     6    IFNM,IFNMEX,IRUN
      DOUBLE PRECISION IFNMEX
C
C
      DIMENSION IDICV(100,4),ITITCH(55),RANK(100),NAMCOL(16),NAMSC(9),
     1    IDBLSP(100),NUMD(2),IDATIM(3)
      DOUBLE PRECISION NAMCOL,NAMSC
      DATA NAMCOL /'AUDIENCE','COVERAGE','COMP  ','CUME  ','TURNOVER',
     1    'INDEX ','SPACE ','CPM P/4-C','CPM H/4-C','CPM Q/4-C',
     2    'CPM P/2-C','CPM H/2-C','CPM Q/2-C','CPM P/B&W','CPM H/B&W',
     3    'CPM Q/B&W'/
      DATA NAMSC /'  P/4-C','  H/4-C','  Q/4-C','  P/2-C','  H/2-C',
     1    '  Q/2-C','  P/B&W','  H/B&W','  Q/B&W'/
C
C
C     INITRU=1
C     CALL AGTRU(INITRU)
      ITRPSW=1
      IMKUP=150
      DO 12 J=17,20
   12 ITCCL(J)=J+94
      CALL LEGIT
      DO 15 J=17,20
   15 ITCCL(J)=J+94
      ITCCV(20)=1
      ITRPAC=667
      NUMNUM=0
      IWIDTH=0
      CALL DATE (IDATIM(1))
      CALL TIME (IDATIM(3))
   20 NUMDBL=0
      IFNMC=1H 
      NREP=0
C
C...REPORT FORMAT SPECS
C
      IEDIT='ENTER'
      CALL FORMAT
      IF (IEND.EQ.1)   GO TO 2000
C     CALL AGTRU(INITRU)
C
C...VEHICLES FOR THESE REPORTS
C
   25 CALL VNAMES (NAMEV,NVEH,100,IEDIT,IEND)
      IF (IEND.EQ.1)   GO TO 2000
      ITRPAC=ITRPAC+33*NVEH
C     CALL AGTRU(INITRU)
      IEDIT='ENTER'
C
C...ALLOW SELECTIVE DOUBLE SPACING
C
      IF (NUMDBL.NE.0)   GO TO 40
   28 DO 30 JV=1,NVEH
   30 IDBLSP(JV)=0
      TYPE 8020
 8020 FORMAT(' DOUBLE-SPACE AFTER: ',$)
      CALL RDLINE (LINE)
      NUMDBL=LDINT (IER)
      IF (IER.NE.0)   GO TO 31
      IF (NUMDBL-NVEH)   39,35,35
   31 CALL SETSCN (LINE,72)
   32 NAME=LDALPH (5)
      IF (IRNCOL.NE.0)   GO TO 37
      IF (NAME.EQ.1H )   GO TO 39
      DO 34 JV=1,NVEH
      IF (NAME.EQ.NAMEV(JV))   GO TO 36
   34 CONTINUE
      IF (NAME.NE.'HEL'.AND.NAME.NE.'HELP')   GO TO 35
      TYPE 8030
 8030 FORMAT('+TYPE RETURN FOR NO DOUBLE-SPACING'/
     1    4X,'OR TYPE A NUMBER N TO DOUBLE-SPACE AFTER EVERY N ',
     2    'VEHICLES'/)
      TYPE 8031
 8031 FORMAT('+',3X,'OR TYPE VEHICLE NAME(S) TO DOUBLE-SPACE AFTER ',
     1    'EACH SPECIFIED VEHICLE'/)
      GO TO 28
   35 TYPE 8080
      GO TO 28
   36 IDBLSP(JV)=1
      GO TO 32
   37 IF (NAME.NE.'HEL'.AND.NAME.NE.'HELP')   GO TO 38
      TYPE 8030
      GO TO 28
   38 TYPE 8080
      GO TO 28
   39 IF (IEDIT.EQ.'EDIT')   GO TO 70
C     CALL AGTRU(INITRU)
C
C...COSTS
C
   40 NCOSTS=0
      DO 45 JC=1,NCOL
      ICDEFC(JC)=0
      IDEFN=ICDEFN(JC)
      IF (IDEFN.LE.7)   GO TO 45
      NCOSTS=NCOSTS+1
      NAMEC(NCOSTS)=ICDEFA(JC)
      ICDEFC(JC)=NCOSTS
      IF (NDEM.EQ.2)   ICDEFC(JC+NCOL)=NCOSTS
   45 CONTINUE
      IF (NCOSTS.EQ.0)   GO TO 54
   47 CALL VCOSTS (NAMEV,NVEH,100,NAMEC,NCOSTS,ICOSTS,11,IEDIT,
     1    IEND)
      IF (IEND.EQ.1)   GO TO 2000
C     CALL AGTRU(INITRU)
      IF (IEDIT.EQ.'EDIT')   GO TO 70
C
C...COMPOSITION BASE DEMO
C
   54 IF (ICOMSW.EQ.0)   GO TO 62
      ICTISW=0
      IF (IFNMC.NE.1H )   GO TO 58
   56 TYPE 8050
 8050 FORMAT(' COMPOSITION BASE DEMO: ',$)
      ACCEPT 7050,IFNMC
 7050 FORMAT(A5)
      ICTISW=1
   58 ITRPAC=ITRPAC+200
      CALL ACCT (2)
      CALL RDDEM (IFNMC,IFNMEX,ICLI,ITITLC,NVDEM,NAMEVD,IBASEC,IAUDD,
     1    IDUMMY,2,100,LOCPR,ICTISW,1,IERCD)
      CALL CLOSE (12,IDUM)
      IF (IERCD.NE.0)   GO TO 56
      CALL MATCHV (NAMEV,NVEH,NAMEVD,NVDEM,IVPTR,IFNMC,IERCD)
      IF (IERCD.NE.0)   GO TO 70
      DO 60 JV=1,NVEH
      IVP=IVPTR(JV)
   60 AUDC(JV)=IAUDD(IVP)
      BASEC=IBASEC
C     CALL AGTRU(INITRU)
      IF (IEDIT.EQ.'EDIT')   GO TO 70
C
C...DEMOS FOR REPORTS
C
   62 IF (NREP.GE.1)   GO TO 70
   65 CALL REPDEM
C     CALL AGTRU(INITRU)
      IF (IEND.EQ.1)   GO TO 2000
      IF (IRUN.EQ.1)   GO TO 110
C
C...MAIN EDITING SECTION
C
   70 IEDIT='EDIT'
      TYPE 8060
 8060 FORMAT(' : ',$)
      ACCEPT 7060,IOPT
 7060 FORMAT(A3)
      IF (IOPT.EQ.'VEH')   GO TO 25
      IF (IOPT.EQ.'DOU')   GO TO 28
      IF (IOPT.EQ.'COM'.AND.ICOMSW.EQ.1)   GO TO 56
      IF (IOPT.EQ.'DEM')   GO TO 65
      IF (IOPT.EQ.'COS'.AND.NCOSTS.GE.1)   GO TO 47
      IF (IOPT.EQ.'RUN'.OR.IOPT.EQ.'CON')   GO TO 110
      IF (IOPT.EQ.'RES')   GO TO 20
      IF (IOPT.EQ.'END')   GO TO 2000
      IF (IOPT.EQ.'HEL')   TYPE 8070
 8070 FORMAT('+COMMANDS:  VEHICLES DOUBLE-SPACE COSTS COMPBASE ',
     1    'DEMOS RESTART'/4X,'RUN CONTINUE END'/)
      IF (IOPT.NE.'HEL')   TYPE 8080
 8080 FORMAT('+I BEG YOUR PARDON?'/)
      GO TO 70
C
C...RUN OR CONTINUE
C
  110 IF (IERCD.EQ.0)   GO TO 112
      TYPE 8085
 8085 FORMAT('+ERROR IN COMP. BASE DEMO MUST BE CORRECTED'/)
      GO TO 70
C
C...CHECK FOR PRESENCE OF AT LEAST ONE REPORT
C
  112 IF (NREP.GE.1)   GO TO 114
      IEDIT='ENTER'
      GO TO 65
C
C...READ IN LONG VEHICLE NAMES
C
  114 CALL VEHDIC (NAMEV,NVEH,IDICV,100,11)
C     CALL AGTRU(INITRU)
      CALL ACCT (2)
      DO 600 JR=1,NREP
      TYPE 8092,JR
 8092 FORMAT('1---REPORT',I3,'---')
      NUMOUT=0
      DO 190 JD=1,NDEM
      IBASE(JD)=0
      DO 120 JV=1,NVEH
      IAUD(JV,JD)=0
  120 ICUME(JV,JD)=0
      IF (JD.EQ.1)   TYPE 8100
 8100 FORMAT(' DEMO(S):'/)
      IF (JD.EQ.2)   TYPE 8120
 8120 FORMAT('+SECOND DEMO(S):'/)
      DO 170 JFILE=1,5
      IFNM=NAMDEM(JR,JFILE,JD)
      IF (IFNM.EQ.1H )   GO TO 180
      JSIGN=1H 
      IF (JFILE.GE.2)   JSIGN=IDEMPM(JR,JFILE,JD)
      TYPE 8130,JSIGN,IFNM
 8130 FORMAT('+',A1,1X,A5,3X,$)
      ITRPAC=ITRPAC+300
      CALL RDDEM (IFNM,IFNMEX,ICLI,ITITLB,NVDEM,NAMEVD,IBASED,IAUDD,
     1    IDUMMY,2,100,LOCPR,1,1,IER)
      IF (IER.EQ.0)   GO TO 140
  130 TYPE 8140
 8140 FORMAT('0REPORT SKIPPED!!')
      CALL CLOSE (12,IER)
      GO TO 590
  140 IF (JD.NE.1.OR.JFILE.NE.1)   GO TO 145
      DO 142 J=1,15
  142 ITITLA(J)=ITITLB(J)
  145 IF (LOCPR.GT.0.OR.ICUMSW.EQ.0)   GO TO 150
      TYPE 8150,IFNM
 8150 FORMAT('0NO PAIRS IN DEMO ',A5)
      GO TO 130
  150 KSIGN=1
      IF (JSIGN.EQ.1H-)   KSIGN=-1
      IBASE(JD)=IBASE(JD)+KSIGN*IBASED
      CALL MATCHV (NAMEV,NVEH,NAMEVD,NVDEM,IVPTR,IFNM,IER)
      IF (IER.NE.0)   GO TO 130
      DO 160 JV=1,NVEH
      IVP=IVPTR(JV)
      IAUD(JV,JD)=IAUD(JV,JD)+KSIGN*IAUDD(IVP)
      IF (ICUMSW.EQ.0)   GO TO 160
      LOC=LOCPR+IJCAL(IVP,IVP)-1
      CALL RECIN (12,ISELFP,1,LOC,IER)
      LOCERR=20
      IF (IER.NE.0)   GO TO 3000
      ICUME(JV,JD)=ICUME(JV,JD)+KSIGN*ISELFP
  160 CONTINUE
      CALL CLOSE (12,IER)
  170 CONTINUE
      JFILE=6
  180 NUMD(JD)=JFILE-1
  190 CONTINUE
      BASE(1)=IBASE(1)
      BASE(2)=IBASE(2)
C
C...REPORT HEADING
C
      IEXTBL=MIN0 (2,(IWIDTH-NCHAR)/NCTOT)
      NCHARA=NCHAR+IEXTBL*NCTOT
      NCH1A=NCHAR1+IEXTBL*NCOL
      TYPE 8152
 8152 FORMAT(//)
C     LEADBL=MAX0 (1,(NCHARA-58)/2)
C     CALL SPACER (LEADBL)
C     TYPE 8155,IDATIM
C8155 FORMAT('+','- - - - - TELMAR MEDIA REPORT',3X,2A5,2X,A5,
C    1    ' - - - - -'/)
      LEADBL=19
      IF (IRNCOL.NE.0)   LEADBL=24
      IF (NDEM.EQ.2)   GO TO 200
      IF (NUMD(1).EQ.1.AND.ITITSW.EQ.1)   CALL CENTER(ITITLB,15,NCHARA)
      IF (NUMD(1).GE.2.OR.ITITSW.EQ.2)   CALL CENTER(ITITLE(1,JR),26,
     1    NCHARA)
      GO TO 270
C...2-DEMO HEADING
  200 CALL SPACER (LEADBL)
      IF (NUMD(1).EQ.1.AND.ITITSW.EQ.1)   DECODE (55,7070,ITITLA) ITITCH
 7070 FORMAT(55A1)
      IF (NUMD(1).GE.2.OR.ITITSW.EQ.2)   DECODE (55,7070,ITITLE(1,JR))
     1    ITITCH
      DO 210 J=NCH1A,1,-1
      IF (ITITCH(J).NE.1H )   GO TO 220
  210 ITITCH(J)=1H-
  220 DO 230 J=1,NCH1A
  230 TYPE 8160,ITITCH(J)
 8160 FORMAT('+',A1,$)
      TYPE 8170
 8170 FORMAT('+  .  ',$)
      IF (NUMD(2).EQ.1.AND.ITITSW.EQ.1)   DECODE (55,7070,ITITLB) ITITCH
      IF (NUMD(2).GE.2.OR.ITITSW.EQ.2)   DECODE (55,7070,ITITLE(16,JR))
     1    ITITCH
      DO 240 J=NCH1A-2,1,-1
      IF (ITITCH(J).NE.1H )   GO TO 250
  240 ITITCH(J)=1H-
  250 DO 260 J=1,NCH1A-2
  260 TYPE 8160,ITITCH(J)
      TYPE 8210
C
C...COLUMN HEADINGS
C
  270 IF (NDEM.EQ.1)   GO TO 275
      CALL SPACER (LEADBL+NCH1A)
      TYPE 8180
      TYPE 8210
  275 CALL SPACER (LEADBL)
      DO 320 JC=1,NCTOT
      IF (NDEM.EQ.2.AND.JC.EQ.(NCOL+1))   TYPE 8180
 8180 FORMAT('+  .',$)
      IF (IEXTBL.EQ.1)   TYPE 8183
 8183 FORMAT('+ ',$)
      IF (IEXTBL.EQ.2)   TYPE 8185
 8185 FORMAT('+  ',$)
      IDEFN=ICDEFN(JC)
      IF (IDEFN.GE.8)   GO TO 292
      GO TO (280,282,282,284,286,288,290),IDEFN
C...AUD
  280 TYPE 8190
 8190 FORMAT('+   AUD ',$)
      GO TO 320
C...COVERAGE OR COMPOSITION
  282 TYPE 8192
 8192 FORMAT('+    % ',$)
      GO TO 320
C...CUME
  284 TYPE 8194
 8194 FORMAT('+   CUME',$)
      GO TO 320
C...TURNOVER
  286 TYPE 8196
 8196 FORMAT('+  TURN',$)
      GO TO 320
C...INDEX
  288 TYPE 8198
 8198 FORMAT('+  IN-',$)
      GO TO 320
C...SPACE
  290 TYPE 8200
 8200 FORMAT('+',5X,$)
      GO TO 320
C...CPM
  292 TYPE 8202
 8202 FORMAT('+   CPM ',$)
      GO TO 320
  320 CONTINUE
      TYPE 8210
 8210 FORMAT('+ ')
C
C...SECOND LINE OF HEADINGS
C
      IF (IRNCOL.EQ.0)   TYPE 8300
 8300 FORMAT(' VEHICLE NAME',6X,$)
      IF (IRNCOL.NE.0)   TYPE 8310
 8310 FORMAT(' RANK  VEHICLE NAME',5X,$)
      DO 350 JC=1,NCTOT
      IF (JC.EQ.(NCOL+1))   TYPE 8180
      IF (IEXTBL.EQ.1)   TYPE 8183
      IF (IEXTBL.EQ.2)   TYPE 8185
      IDEFN=ICDEFN(JC)
      IF (IDEFN.GE.8)   GO TO 334
      GO TO (322,324,326,322,328,330,332),IDEFN
C...AUD OR CUME
  322 TYPE 8322
 8322 FORMAT('+  (000)',$)
      GO TO 350
C...COVERAGE
  324 TYPE 8324
 8324 FORMAT('+   COV',$)
      GO TO 350
C...COMPOSITION
  326 TYPE 8326
 8326 FORMAT('+  COMP',$)
      GO TO 350
C...TURNOVER
  328 TYPE 8328
 8328 FORMAT('+  OVER',$)
      GO TO 350
C...INDEX
  330 TYPE 8330
 8330 FORMAT('+  DEX',$)
      GO TO 350
C...SPACE
  332 TYPE 8200
      GO TO 350
C...PAGE 4-COLOR
  334 TYPE 8334,NAMSC(IDEFN-7)
 8334 FORMAT('+',A7,$)
  350 CONTINUE
      TYPE 8210
C
C...UNDERLINING
C
      IF (IRNCOL.EQ.0)   TYPE 8350
 8350 FORMAT(' ------------',6X,$)
      IF (IRNCOL.NE.0)   TYPE 8355
 8355 FORMAT(' ----  ------------',5X,$)
      DO 370 JC=1,NCTOT
      IF (JC.EQ.(NCOL+1))   TYPE 8180
      IF (IEXTBL.EQ.1)   TYPE 8183
      IF (IEXTBL.EQ.2)   TYPE 8185
      IDEFN=ICDEFN(JC)
      IF (IDEFN.GE.8)   GO TO 361
      GO TO (361,362,362,361,362,363,364),IDEFN
C...AUD OR CUME OR CPM
  361 TYPE 8361
 8361 FORMAT('+  -----',$)
      GO TO 370
C...COV OR COMP OR TURNOVER
  362 TYPE 8362
 8362 FORMAT('+  ----',$)
      GO TO 370
C...INDEX
  363 TYPE 8363
 8363 FORMAT('+  ---',$)
      GO TO 370
C...SPACE
  364 TYPE 8200
  370 CONTINUE
      TYPE 8210
C
C...COMPUTE RANKING CRITERION
C
      IF (IRNCOL.EQ.0)   GO TO 410
      IORDER='DESC'
      IF (NDEM.EQ.2)   GO TO 385
C...RANKING FOR 1-DEMO REPORT
      IDEFN=ICDEFN(IRNCOL)
      IF (IDEFN.GE.8)   IORDER='ASC'
      DO 380 JV=1,NVEH
  380 RANK(JV)=VAL (IRNCOL,JV,1)
      GO TO 410
C...2-DEMO REPORT; RANK ON INDEX
  385 DO 390 JV=1,NVEH
      VAL2=VAL(INDCOL,JV,2)
      VAL1=AMAX1(VAL(INDCOL,JV,1),0.000001)
  390 RANK(JV)=VAL2/VAL1
      IF (ICDEFN(INDCOL).GE.8)   IORDER='ASC'
C
C..WRITE OUT THE LITTLE BASTARDS
C
  410 DO 510 JV=1,NVEH
      NEXTV=JV
      IF (IRNCOL.EQ.0)   GO TO 450
      IF (IORDER.EQ.'DESC')   GO TO 430
C
C...SORT IN ASCENDING ORDER (E.G. CPM)
C
      FLOW=1.E20
      DO 420 KV=1,NVEH
      IF (RANK(KV).GE.FLOW)   GO TO 420
      NEXTV=KV
      FLOW=RANK(KV)
  420 CONTINUE
      RANK(NEXTV)=1.E21
      GO TO 450
C
C...SORT IN DESCENDING ORDER
C
  430 HIGH=-5.
      DO 440 KV=1,NVEH
      IF (RANK(KV).LE.HIGH)   GO TO 440
      NEXTV=KV
      HIGH=RANK(KV)
  440 CONTINUE
      RANK(NEXTV)=-10.
C
C...TYPE EXPANDED VEHICLE NAME
C
  450 IF (IRNCOL.EQ.0)   TYPE 8450,(IDICV(NEXTV,J),J=1,4)
 8450 FORMAT(1X,3A5,A3,$)
      IF (IRNCOL.NE.0)   TYPE 8452,JV,(IDICV(NEXTV,J),J=1,4)
 8452 FORMAT(1X,I3,2X,3A5,A3,$)
      JDEM=1
C
C...TYPE COLUMN ENTRIES
C
      DO 500 JC=1,NCTOT
      IF (JC.NE.(NCOL+1))   GO TO 455
      JDEM=2
      TYPE 8180
  455 IF (IEXTBL.EQ.1)   TYPE 8183
      IF (IEXTBL.EQ.2)   TYPE 8185
      IDEFN=ICDEFN(JC)
      IF (NDEM.EQ.2.AND.ICDEFA(JC).EQ.'IND')   GO TO 474
      VALA=VAL (JC,NEXTV,JDEM)
  460 GO TO (462,464,464,462,466,468,470,472,472,472,472,472,472,
     1    472,472,472),IDEFN
C...AUD OR CUME
  462 IVAL=VALA+.5
      TYPE 8462,IVAL
 8462 FORMAT('+',I7,$)
      GO TO 500
C...COVERAGE OR COMPOSITION
  464 VALA=100.*VALA
      TYPE 8464,VALA
 8464 FORMAT('+',F6.1,$)
      GO TO 500
C...TURNOVER
  466 TYPE 8466,VALA
 8466 FORMAT('+',F6.2,$)
      GO TO 500
C...INDEX
  468 IVAL=AMIN1(100.*VALA,9999.)+.5
      TYPE 8468,IVAL
 8468 FORMAT('+',I5,$)
      GO TO 500
C...SPACE
  470 TYPE 8200
      GO TO 500
C...CPM
  472 VALMAX=999.99
      IF (IEXTBL.GE.1)   VALMAX=9999.99
      VALA=AMIN1(VALA,VALMAX)
      TYPE 8472,VALA
 8472 FORMAT('+',F7.2,$)
      GO TO 500
C...2-DEMO INDEX
  474 VAL2=VAL(INDCOL,NEXTV,2)
      VAL1=AMAX1(VAL(INDCOL,NEXTV,1),0.000001)
      VALA=VAL2/VAL1
      GO TO 468
  500 CONTINUE
      TYPE 8210
      IF (JV.EQ.NVEH)   GO TO 510
      IF (NUMDBL.EQ.0)   GO TO 502
      IF (MOD(JV,NUMDBL).NE.0)   GO TO 510
      GO TO 503
  502 IF (IDBLSP(JV).EQ.0)   GO TO 510
C...DOUBLE SPACE
  503 IF (NDEM.EQ.2)   GO TO 504
      TYPE 8503
 8503 FORMAT(1X)
      GO TO 510
  504 CALL SPACER (LEADBL+NCH1A)
      TYPE 8180
      TYPE 8210
  510 CONTINUE
C
C...DEMO BASE
C
      NUMOUT=NCTOT*NVEH
      IF (NDEM.EQ.2)   GO TO 512
      TYPE 8503
      GO TO 518
  512 CALL SPACER (LEADBL+NCH1A)
      TYPE 8180
      TYPE 8210
  518 IF (IRNCOL.EQ.0)   TYPE 8518
 8518 FORMAT(' BASE:',$)
      IF (IRNCOL.NE.0)   TYPE 8520
 8520 FORMAT(6X,'BASE:',$)
      DO 520 JC=1,NCOL
      IF (ICDEFA(JC).EQ.'AUD'.OR.ICDEFA(JC).EQ.'CUM')   GO TO 524
  520 CONTINUE
C...NO AUDIENCE COLUMN
      TYPE 8522,IBASE(1)
 8522 FORMAT('+',12X,I6,$)
      NUMOUT=NUMOUT+1
      IF (NDEM.EQ.1)   GO TO 550
      DO 523 J=1,NCH1A-5
  523 TYPE 8523
 8523 FORMAT('+ ',$)
      TYPE 8180
      TYPE 8462,IBASE(2)
      NUMOUT=NUMOUT+1
      GO TO 550
C...AUD COLUMN PRESENT
  524 JDEM=1
      TYPE 8525
 8525 FORMAT('+',13X,$)
      DO 540 JC=1,NCTOT
      IF (JC.NE.(NCOL+1))   GO TO 525
      JDEM=2
      TYPE 8180
  525 IF (IEXTBL.EQ.1)   TYPE 8183
      IF (IEXTBL.EQ.2)   TYPE 8185
      IDEFN=ICDEFN(JC)
      IF (IDEFN.GE.8)   GO TO 538
      GO TO (526,527,528,526,529,531,531),IDEFN
C...AUD OR CUME
  526 TYPE 8462,IBASE(JDEM)
      NUMOUT=NUMOUT+1
      GO TO 540
C...COV
  527 COV=100.
      TYPE 8464,COV
      NUMOUT=NUMOUT+1
      GO TO 540
C...COMP
  528 COMP=100.*BASE(JDEM)/BASEC
      TYPE 8464,COMP
      NUMOUT=NUMOUT+1
      GO TO 540
C...TURNOVER
  529 TYPE 8529
 8529 FORMAT('+',6X,$)
      GO TO 540
C...INDEX OR SPACE
  531 TYPE 8200
      GO TO 540
C...CPM
  538 TYPE 8538
 8538 FORMAT('+',7X,$)
  540 CONTINUE
C
C...FOOTNOTES
C
  550 TYPE 8210
      TYPE 8152
      IF (ICOMSW.EQ.0)   GO TO 555
      TYPE 8560
 8560 FORMAT('+COMPOSITIONS BASED ON:  ',$)
      CALL XPRITI (ITITLC,'+')
  555 IF (NDEM.EQ.1.OR.INDCOL.EQ.0)   GO TO 560
      IDEFN=ICDEFN(INDCOL)
      TYPE 8570,NAMCOL(IDEFN)
 8570 FORMAT('+INDEX BASED ON ',A10/)
  560 IF (IRNCOL.EQ.0)   GO TO 590
      IDEFN=ICDEFN(IRNCOL)
      TYPE 8580,NAMCOL(IDEFN)
 8580 FORMAT('+RANK BASED ON ',A10)
  590 NUM3A=MAX0 (0,NUMNUM-
      NUM35A=MAX0 (0,NUMNUM-NUM3A-300)
      NUM4A=MIN0 (NUMNUM,300)
      NUMNUM=NUMNUM+NUMOUT
      NUM3B=MAX0 (0,NUMNUM-500)
      NUM35B=MAX0 (0,NUMNUM-NUM3B-300)
      NUM4B=MIN0 (NUMNUM,300)
      PRICE=AMAX1 (1.00,0.04*(NUM4B-NUM4A)+0.035*(NUM35B-NUM35A)+
     1    0.03*(NUM3B-NUM3A))
      ITRPAC=ITRPAC+100.*(PRICE/0.15)+.5
      CALL ACCT (2)
C     CALL AGTRU(INITRU)
  600 CONTINUE
      TYPE 8600
 8600 FORMAT('1')
      GO TO 70
C
C...THAT'S ALL FOLKS!
C
 2000 ITRPAC=MAX0 (ITRPAC,2000)
      CALL ACCT (3)
      STOP
C
C...DISK ERROR
C
 3000 TYPE 83000,IER,LOCERR
83000 FORMAT(' ERROR ',I2,' AT LOC.',I3)
      GO TO 2000
      END
C
C
C
C
C
      FUNCTION VAL (ICOL,KV,KDEM)
C
C...COMPUTATION FUNCTION
C
C...ICOL=COLUMN NUMBER
C   KV=VEHICLE NUMBER
C   KDEM=DEMO NUMBER (1 OR 2)
C
      COMMON /RANK/ NAMEV(100),NAMEVD(100),IAUD(100,2),IAUDD(100),
     1    AUDC(100),ICUME(100,2),IVPTR(100),NAMEC(4),ICOSTS(100,4),
     2    IBASE(2),BASE(2),BASEC,ITITLE(30,25),ITITLA(15),ITITLB(15),
     3    ITITLC(15),ICDEFA(25),ICDEFN(25),ICDEFC(25),NAMDEM(25,5,2),
     4    IDEMPM(25,5,2),LINE(15),IEDIT,NDEM,NCOL,NCTOT,ICOMSW,
     5    ICUMSW,IWIDTH,NCHAR,NCHAR1,IRNCOL,INDCOL,NREP,IEND,ITITSW,
     6    IFNM,IFNMEX,IRUN
      DOUBLE PRECISION IFNMEX
C
C
      JV=KV
      JDEM=KDEM
      IDEFN=ICDEFN(ICOL)
      AUD=IAUD(JV,JDEM)
      IF (IDEFN.GE.8)   GO TO 100
      GO TO (20,30,40,50,60,70,80),IDEFN
C...AUD
   20 VAL=AUD
      GO TO 200
C...COV
   30 VAL=AUD/BASE(JDEM)
      GO TO 200
C...COMP
   40 VAL=AUD/AUDC(JV)
      GO TO 200
C...CUME
   50 VAL=ICUME(JV,JDEM)
      GO TO 200
C...TURNOVER
   60 VAL=0.
      IF (AUD.GT.0.)   VAL=ICUME(JV,JDEM)/AUD
      GO TO 200
C...INDEX (1-DEMO ONLY)
   70 VAL=(AUD/BASE(JDEM))/(AUDC(JV)/BASEC)
      GO TO 200
C...SPACE
   80 VAL=0.
      GO TO 200
C...CPM
  100 JCOST=ICDEFC(ICOL)
      VAL=0.
      IF (AUD.GT.0.)   VAL=ICOSTS(JV,JCOST)/AUD
  200 RETURN
      END
C
C
C
C
C
      SUBROUTINE FORMAT
C
C...COLUMN DEFINITIONS AND INDEXING AND RANKING CRITERIA FOR RANK
C      REPORTS
C
C
      COMMON /RANK/ NAMEV(100),NAMEVD(100),IAUD(100,2),IAUDD(100),
     1    AUDC(100),ICUME(100,2),IVPTR(100),NAMEC(4),ICOSTS(100,4),
     2    IBASE(2),BASE(2),BASEC,ITITLE(30,25),ITITLA(15),ITITLB(15),
     3    ITITLC(15),ICDEFA(25),ICDEFN(25),ICDEFC(25),NAMDEM(25,5,2),
     4    IDEMPM(25,5,2),LINE(15),IEDIT,NDEM,NCOL,NCTOT,ICOMSW,
     5    ICUMSW,IWIDTH,NCHAR,NCHAR1,IRNCOL,INDCOL,NREP,IEND,ITITSW,
     6    IFNM,IFNMEX,IRUN
      DOUBLE PRECISION IFNMEX
C
C
      DIMENSION IVALDF(16,2)
      DATA ((IVALDF(J,K),K=1,2),J=1,16) /'AUD',7,'COV',6,'COM',6,
     1    'CUM',7,'TUR',6,'IND',5,'SPA',5,'P4C',7,'H4C',7,'Q4C',7,
     2    'P2C',7,'H2C',7,'Q2C',7,'PBW',7,'HBW',7,'QBW',7/
C
C
      IEND=0
      IF (IWIDTH.NE.0)   GO TO 50
      IWIDTH=71
      TYPE 8010
 8010 FORMAT(' IS THIS A WIDE-CARRIAGE TERMINAL? ',$)
      CALL BYESNO (IYN,1)
      IF (IYN.EQ.1)   IWIDTH=131
   50 TYPE 8020
 8020 FORMAT('+1-DEMO OR 2-DEMO: ',$)
      ACCEPT 7010,NDEM
 7010 FORMAT(I1)
      IF (NDEM.EQ.1.OR.NDEM.EQ.2)   GO TO 60
      TYPE 8030
 8030 FORMAT('+I BEG YOUR PARDON?'/)
      GO TO 50
   60 TYPE 8040
 8040 FORMAT('+FORMAT: ',$)
      CALL RDLINE (LINE)
      IFIRST=1
      NCOL=0
      NCHAR=18
      IF (NDEM.EQ.2)   NCHAR=21
      NCHAR1=0
      ICUMSW=0
      ICOMSW=0
      NCOSTS=0
   70 IDEF=LDALPH (3)
      IF (IDEF.EQ.1H )   GO TO 110
      IF (IFIRST.EQ.1.AND.IDEF.EQ.'STA')   GO TO 102
      IFIRST=0
      DO 80 J=1,16
      IF (IDEF.EQ.IVALDF(J,1))   GO TO 90
   80 CONTINUE
      INDYN='INDEX'
      IF (NDEM.EQ.2)   INDYN=1H 
      IF (IDEF.EQ.'HEL')   TYPE 8050,INDYN
 8050 FORMAT('+OPTIONS:  STANDARD AUDIENCE COVERAGE COMPOSITION ',
     1    'CUME TURNOVER'/11X,A5,' SPACE P4C H4C Q4C P2C H2C Q2C ',
     2    'PBW HBW QBW'/)
      IF (IDEF.NE.'HEL')   TYPE 8030
      GO TO 60
   90 IF (IDEF.EQ.'IND'.AND.NDEM.EQ.2)   GO TO 70
      IF (J.GE.8)   NCOSTS=NCOSTS+1
      IF (NCOSTS.LE.4)   GO TO 95
      TYPE 8055
 8055 FORMAT('+NO MORE THAN 4 COST TYPES ALLOWED!'/)
      GO TO 60
   95 NCHAR=NCHAR+NDEM*IVALDF(J,2)
      NCHAR1=NCHAR1+IVALDF(J,2)
      IF (NCHAR.LE.IWIDTH)   GO TO 100
      TYPE 8060
 8060 FORMAT('+THE REPORT WOULD BE TOO WIDE FOR THIS TERMINAL!'/)
      GO TO 60
  100 NCOL=NCOL+1
      ICDEFA(NCOL)=IDEF
      IF (IDEF.EQ.'CUM'.OR.IDEF.EQ.'TUR')   ICUMSW=1
      IF (IDEF.EQ.'COM'.OR.(NDEM.EQ.1.AND.IDEF.EQ.'IND'))   ICOMSW=1
      GO TO 70
C...STANDARD FORMAT
  102 IF (NDEM.EQ.2)   GO TO 104
      ICDEFA(1)='AUD'
      ICDEFA(2)='COM'
      ICDEFA(3)='SPA'
      ICDEFA(4)='PBW'
      ICDEFA(5)='SPA'
      ICDEFA(6)='P4C'
      NCOL=6
      ICOMSW=1
      NCHAR=NCHAR+38
      GO TO 120
C...STANDARD FORMAT  2-DEMO
  104 ICDEFA(1)='AUD'
      ICDEFA(2)='COV'
      ICDEFA(3)='P4C'
      NCOL=3
      NCHAR=NCHAR+40
      NCHAR1=20
      GO TO 120
C
C...ALL COLUMN DEFINITIONS IN
C
  110 IF (IFIRST.EQ.0)   GO TO 120
      TYPE 8030
      GO TO 60
  120 NCTOT=NCOL
      IF (NDEM.EQ.1)   GO TO 160
      DO 125 JC=1,NCOL
  125 ICDEFA(JC+NCOL)=ICDEFA(JC)
      NCTOT=2*NCOL
  128 INDCOL=0
      IF (NCHAR.LE.(IWIDTH-5))   GO TO 130
      TYPE 8070
 8070 FORMAT('+NO ROOM FOR INDEX.  OK? ',$)
      CALL BYESNO (IYN,1)
      GO TO (160,60),IYN
  130 TYPE 8080
 8080 FORMAT('+INDEX BASED ON: ',$)
      ACCEPT 7020,INDBAS
 7020 FORMAT(A3)
      IF (INDBAS.EQ.'NON')   GO TO 160
      DO 140 JC=1,NCOL
      IF (INDBAS.EQ.ICDEFA(JC))   GO TO 150
  140 CONTINUE
      IF (INDBAS.NE.'HEL')   GO TO 145
      TYPE 8090,(ICDEFA(JC),JC=1,NCOL)
 8090 FORMAT('+BASES ARE:  NON',12(1X,A3))
      TYPE 8095
 8095 FORMAT(1X)
      GO TO 130
  145 TYPE 8030
      GO TO 130
  150 INDCOL=JC
      NCHAR=NCHAR+5
      NCTOT=NCTOT+1
      ICDEFA(NCTOT)='IND'
C
C...SET UP NUMERICAL POINTERS FOR COLUMNS FOR USE IN COMPUTED GO TO'S
C
  160 DO 180 JC=1,NCTOT
      IDEF=ICDEFA(JC)
      DO 170 J=1,16
      IF (IDEF.EQ.IVALDF(J,1))   GO TO 180
  170 CONTINUE
  180 ICDEFN(JC)=J
C
C...RANKING CRITERION
C
  190 IRNCOL=0
      IF (NCHAR.LE.(IWIDTH-5))   GO TO 195
      TYPE 8097
 8097 FORMAT('+NO ROOM FOR RANKING.  OK? ',$)
      CALL BYESNO (IYN,1)
      GO TO (240,60),IYN
  195 IF (NDEM.EQ.1)   GO TO 200
C...2-DEMO REPORT; RANKING MAY ONLY BE ON INDEX
      IF (ICDEFA(NCTOT).NE.'IND')   GO TO 240
      TYPE 8100
 8100 FORMAT('+RANKING? ',$)
      CALL BYESNO (IYN,1)
      IF (IYN.EQ.2)   GO TO 240
      IRNCOL=NCTOT
      NCHAR=NCHAR+5
      GO TO 240
C...1-DEMO REPORT; RANKING MAY BE ON ANY COLUMN
  200 TYPE 8110
 8110 FORMAT('+RANK BASED ON: ',$)
      ACCEPT 7020,IRNBAS
      IF (IRNBAS.EQ.'SPA')   GO TO 220
      IF (IRNBAS.EQ.'NON')   GO TO 240
      DO 210 JC=1,NCOL
      IF (IRNBAS.EQ.ICDEFA(JC))   GO TO 230
  210 CONTINUE
      IF (IRNBAS.NE.'HEL')   GO TO 220
      TYPE 8090,(ICDEFA(JC),JC=1,NCOL)
      TYPE 8095
      GO TO 200
  220 TYPE 8030
      GO TO 200
  230 IRNCOL=JC
      NCHAR=NCHAR+5
C
C...FORMAT EDITING SECTION
C
  240 TYPE 8120
 8120 FORMAT('+F: ',$)
      ACCEPT 7020,IOPT
      IF (IOPT.EQ.'REE')   GO TO 50
      IF (IOPT.EQ.'CLE')   GO TO 50
      IF (IOPT.EQ.'LIS')   GO TO 250
      IF (IOPT.EQ.'END')   GO TO 260
      IF (IOPT.EQ.'CON')   GO TO 270
      IF (IOPT.EQ.'HEL')   TYPE 8130
 8130 FORMAT('+FORMAT COMMANDS:  CLEAR LIST CONTINUE END'/)
      IF (IOPT.NE.'HEL')   TYPE 8030
      GO TO 240
C
C...LIST
C
  250 TYPE 8140,(ICDEFA(JC),JC=1,NCTOT)
 8140 FORMAT(1X,26(A3,2X))
      IF (INDCOL.NE.0)   TYPE 8150,ICDEFA(INDCOL)
 8150 FORMAT(' INDEX BASED ON ',A3/)
      IF (IRNCOL.NE.0)   TYPE 8160,ICDEFA(IRNCOL)
 8160 FORMAT(' RANK BASED ON ',A3/)
      GO TO 240
C
C...END
C
  260 IEND=1
C
C...CONTINUE
C
  270 RETURN
      END
C
C
C
C
C
      SUBROUTINE REPDEM
C
C...DEMO(S) FOR EACH RANK REPORT
C
C
C...STANDARD COMMON PACKAGE.............
      COMMON KPROJ,KPROG,KFILE(2),KACBLK,TCPU,ITCCV(30),ITCCU(30),
     1    ITCCL(30),ITCCF(30),ITRPSW,ITYMTR,ITRPAC,IMKUP,ICLI
C...END STANDARD COMMON PACKAGE.........
C
      COMMON /RANK/ NAMEV(100),NAMEVD(100),IAUD(100,2),IAUDD(100),
     1    AUDC(100),ICUME(100,2),IVPTR(100),NAMEC(4),ICOSTS(100,4),
     2    IBASE(2),BASE(2),BASEC,ITITLE(30,25),ITITLA(15),ITITLB(15),
     3    ITITLC(15),ICDEFA(25),ICDEFN(25),ICDEFC(25),NAMDEM(25,5,2),
     4    IDEMPM(25,5,2),LINE(15),IEDIT,NDEM,NCOL,NCTOT,ICOMSW,
     5    ICUMSW,IWIDTH,NCHAR,NCHAR1,IRNCOL,INDCOL,NREP,IEND,ITITSW,
     6    IFNM,IFNMEX,IRUN
      DOUBLE PRECISION IFNMEX
C
      IRUN=0
      IF (IEDIT.EQ.'EDIT')   GO TO 140
   70 IFROM=1
      TYPE 8060
 8060 FORMAT(' TAKE REPORT TITLES FROM DEMOS? ',$)
      CALL BYESNO (ITITSW,1)
      TYPE 8065
 8065 FORMAT(1X)
      JREP=0
   75 JREP=JREP+1
   77 TYPE 8070,JREP
 8070 FORMAT('+REPORT',I3,3X,$)
      DO 130 JD=1,NDEM
C...NDEM=1 FOR 1-DEMO REPORT, =2 FOR 2-DEMO REPORT
   80 IF (JD.EQ.1)   TYPE 8080
 8080 FORMAT('+DEMO(S): ',$)
      IF (JD.EQ.2)   TYPE 8100
 8100 FORMAT('+SECOND DEMO(S): ',$)
      CALL RDLINE (LINE)
      IFIRST=1
      NUMD=0
      NUMS=1
   85 IFNM=LDALPH (5)
      IF (IFNM.EQ.1H )   GO TO 110
      CALL NMFILE (IFNM,'D',IFNMEX,0)
      CALL FILNO (12,IFNM,'D',IFNMEX,2,IER)
      IF (IER.NE.0)   GO TO 80
      CALL CLOSE (12,IER)
      IFIRST=0
      NUMD=NUMD+1
      IF (NUMD.LE.5)   GO TO 90
      TYPE 8110
 8110 FORMAT('+NO MORE THAN 5 DEMOS MAY BE COMBINED!'/)
      GO TO 80
   90 NAMDEM(JREP,NUMD,JD)=IFNM
      JSIGN=LDALPH (1)
      IF (JSIGN.EQ.1H )   GO TO 120
      IF (JSIGN.EQ.1H+.OR.JSIGN.EQ.1H-)   GO TO 100
   95 TYPE 8120
 8120 FORMAT('+I BEG YOUR PARDON?'/)
      GO TO 80
  100 NUMS=NUMS+1
      IDEMPM(JREP,NUMS,JD)=JSIGN
      GO TO 85
  110 IF (IFIRST.EQ.0)   GO TO 95
      IF (JD.EQ.1)   GO TO 135
      TYPE 8130
 8130 FORMAT('+AT LEAST ONE DEMO IS NEEDED!'/)
      GO TO 80
  120 IF (NUMD.LT.5)   NAMDEM(JREP,NUMD+1,JD)=1H 
      IF (NUMD.EQ.1.AND.ITITSW.EQ.1)   GO TO 130
      TYPE 8140
 8140 FORMAT('+TITLE: ',$)
      IF (JD.EQ.1)   ACCEPT 7060,(ITITLE(J,JREP),J=1,30)
 7060 FORMAT(30A5)
      IF (JD.EQ.2)   ACCEPT 7060,(ITITLE(J,JREP),J=16,30)
  130 CONTINUE
      IF (IFROM.EQ.2)   GO TO 140
      IF (JREP.LT.25)   GO TO 75
      JREP=26
  135 NREP=JREP-1
C
C...EDITING SECTION
C
  140 IFROM=2
      TYPE 8150
 8150 FORMAT('+D: ',$)
      CALL RDLINE (LINE)
      IOPT=LDALPH (3)
      JREP=LDINT (IER)
      IF (IER.EQ.0)   GO TO 155
      TYPE 8120
      GO TO 140
  155 IF (IOPT.EQ.'CON')   GO TO 280
      IF (IOPT.EQ.'RUN')   GO TO 290
      IF (IOPT.EQ.'ADD')   GO TO 170
      IF (IOPT.EQ.'DEL')   GO TO 190
      IF (IOPT.EQ.'REE')   GO TO 160
      IF (IOPT.EQ.'CLE')   GO TO 70
      IF (IOPT.EQ.'LIS')   GO TO 230
      IF (IOPT.EQ.'END')   GO TO 270
      IF (IOPT.EQ.'HEL')   TYPE 8160
 8160 FORMAT('+DEMO COMMANDS:'/
     1    ' ADD     DELETE RR'/
     2    ' CLEAR   REENTER RR'/
     3    ' RUN     LIST (RR OR RETURN)'/
     4    ' END     CONTINUE'/)
      IF (IOPT.NE.'HEL')   TYPE 8120
      GO TO 140
C
C...REENTER
C
  160 IF (JREP.GE.1.AND.JREP.LE.NREP)   GO TO 77
  165 TYPE 8120
      GO TO 140
C
C...ADD
C
  170 JREP=NREP+1
      IF (JREP.LE.25)   GO TO 180
      TYPE 8170
 8170 FORMAT('+MAXIMUM OF 25 REPORTS ALREADY REACHED!'/)
      GO TO 140
  180 IFROM=1
      GO TO 77
C
C...DELETE
C
  190 IF (JREP.LT.1.OR.JREP.GT.NREP)   GO TO 165
      IF (JREP.EQ.NREP)   GO TO 210
      DO 200 JR=JREP+1,NREP
      DO 195 J=1,30
  195 ITITLE(J,JR-1)=ITITLE(J,JR)
      DO 200 JD=1,NDEM
      DO 200 JFILE=1,5
      NAMDEM(JR-1,JFILE,JD)=NAMDEM(JR,JFILE,JD)
  200 IDEMPM(JR-1,JFILE,JD)=IDEMPM(JR,JFILE,JD)
  210 NREP=NREP-1
      GO TO 140
C
C...LIST
C
  230 JRA=1
      JRB=NREP
      IF (JREP.EQ.0)   GO TO 235
      JRA=JREP
      JRB=JREP
  235 DO 260 JR=JRA,JRB
      TYPE 8180,JR
 8180 FORMAT(' ---REPORT',I3)
      DO 260 JD=1,NDEM
      IF (JD.EQ.1)   TYPE 8182
 8182 FORMAT(' DEMO(S):'/)
      IF (JD.EQ.2)   TYPE 8184
 8184 FORMAT('+SECOND DEMO(S):'/)
      DO 240 JFILE=1,5
      IFNM=NAMDEM(JR,JFILE,JD)
      IF (IFNM.EQ.1H )   GO TO 250
      JSIGN=1H 
      IF (JFILE.GE.2)   JSIGN=IDEMPM(JR,JFILE,JD)
      TYPE 8190,JSIGN,IFNM
 8190 FORMAT('+',A1,1X,A5,2X,$)
      CALL RDDEM (IFNM,IFNMEX,ICLI,ITITLB,IDUMMY,IDUMMY,IDUMMY,IDUMMY,
     1    IDUMMY,2,100,IDUMMY,1,2,IER)
      ITRPAC=ITRPAC+200
      IF (IER.NE.0)   GO TO 140
      CALL CLOSE (12,IER)
  240 CONTINUE
      JFILE=6
  250 IF (JFILE.GT.2.OR.ITITSW.EQ.1)   GO TO 260
      TYPE 8200
 8200 FORMAT('+TITLE: ',$)
      IF (JD.EQ.1)   CALL XPRITI(ITITLE(1,JR),'+')
      IF (JD.EQ.2)   CALL XPRITI(ITITLE(16,JR),'+')
  260 CONTINUE
      GO TO 140
C
C...END
C
  270 IEND=1
C
C...RUN OR CONTINUE
C
  280 RETURN
C
C...RUN
C
  290 IRUN=1
      GO TO 280
      END
C
C
C
C
C
      SUBROUTINE MATCHV (NAMEV,NVEH,NAMEVD,NVDEM,IVPTR,NAMDEM,IER)
C
C...FIND AND POINT TO ALL VEHICLES IN ARRAY NAMEV WHERE THEY APPEAR
C      IN ARRAY NAMEVD.  NORMALLY NAMEV WILL BE THE VEHICLE
C      LIST USED IN THE REPORT AND NAMEVD WILL BE THE VEHICLES IN
C      A DEMO.  NAMEVD MUST CONTAIN EVERY VEHICLE IN NAMEV.
C
C...NAMEV=SELECTED VEHICLE LIST
C   NVEH=NUMBER OF NAMES IN NAMEV
C   NAMEVD=SUPERSET LIST
C   NVDEM=NUMBER OF NAMES IN NAMEVD
C   IVPTR(I)=J IF VEHICLE I IN NAMEV IS VEHICLE J IN NAMEVD
C   NAMDEM=NAME OF DEMO FROM WHICH NAMEVD COMES
C   IER=0 IF ALL VEHICLES WERE FOUND, =1 IF NOT
C
      DIMENSION NAMEV(NVEH),NAMEVD(NVDEM),IVPTR(NVEH)
C
      IER=0
      DO 100 JV=1,NVEH
      NAME=NAMEV(JV)
      DO 50 JVD=1,NVDEM
      IF (NAMEVD(JVD).NE.NAME)   GO TO 50
      IVPTR(JV)=JVD
      GO TO 100
   50 CONTINUE
      TYPE 8010,NAME,NAMDEM
 8010 FORMAT(' VEHICLE ',A5,' MISSING IN DEMO ',A5)
      IER=1
  100 CONTINUE
      RETURN
      END
C
C
C
C
C
      SUBROUTINE SPACER (NUMSP)
C
C...START OFF A LINE WITH NUMSP SPACES (INCLUDING FIRST CHAR)
C
      DIMENSION FMT(2)
C
      ENCODE (7,8010,FMT) NUMSP
 8010 FORMAT('(',I2,'X,$)')
      TYPE FMT
      RETURN
      END
 ð