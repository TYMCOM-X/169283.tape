10 OPEN "XYZ",INPUT,4
20 OPEN "LP",INPUT,5
30 OPEN "EXP",INPUT,6
110 REM THIS PROGRAM DETERMINES THE RATE OF RETURN ON LEASED
120 REM EQUIPMENT. D.D.B. AND 1.5 D.B. WITH AUTOMATIC 
130 REM SWITCHOVER TO STRAIGHT LINE, AND STRAIGHT LINE IS
140 REM INCORPORATED. BORROWED MONEY IS HANDLED ON A MORTAGE
150 REM PAYMENT ARRANGEMENT. FEDERAL TAXES ARE PAID ANNUALLY
160 REM ANNUAL MISCELLANIOUS EXPENSES CAN BE INPUTED. EQUITY 
171 REM P=PURCHASE VALUE
172 REM O1= OPTIO^: 1= GIVEN I FIND L; 2= GIVEN L FIND I
173 REM               3= GIVEN L(Y) FIND I
174 REM 02= OPTION* 0= NO EXTENSION; 1= EXTENSION
175 REM 03= OPTION: 1= PAYMENTS IN ADVANCE; 2= IN ARREARS
176 REM I= PRETAX RATE
177 REM F= NUMBER OF LEASE PAYMENT PER YEAR
178 REM N= NUMBER OF LEASE YEARS
179 REM N3= NUMBER OF EXTENSION YEARS
180 REM L= LEASE PAYMENT PER YEAR
181  REM M= DEPRECIATION METHOD: 1=SLD; 2=DDB; 1.5= 150%DB
182 REM K= LIFE OF RQUIPMENT
183 REM R= RESIDUAL VALUE AT END OF LEASE
184 REM R3= RESIDUAL VALUE AT END OF EXTENSION
185 REM D= DEBT INCURRED
186 REM E= DEBT INTEREST
187 REM F2= NUMBER OF DEBT PAYMENTS PER YEAR
188 REM Q= NUMBER OF DEBT YEARS
189 REM T= TAX RATE IN PERCENT
190 REM C= ]ISC COST CODE:0=FILL ZEROES; 1=READ COSTS
191 REM C(Y)= COSTS
192 REM \8Y)- ANNUAL LEASE PAYMENTS
195 DIM C(51),D(51),E(51),P(51),T(51),F(51),L(51)
196 INPUT FROM 4:P,O1,O2,O3,I,F,N,N3,L,M,K,R,R3,R4,D,E,F2,Q,T,A1,
A2,I6,O5,X7,X8,C
198 Z8="^#DDDD,DDD,DDD.  ^*"
199 IF O2=1 THEN R=R3
200 I=I/100,E=E/100,T=T/100
203 IF A1=1 OR A1=3 THEN M=1.5
204 IF A1=2 OR A1=4 THEN M=2
207 IF A1=1 OR A1=2 OR A1=3 OR A1=4 THEN 213
211 A1=0
212 GOTO 215
213 IF A2>0 THEN IF A2<13 THEN 215
214 A2=1
215 GOTO 221
221 IF O2=0 THEN 279
222 IF O2=1 THEN 280 ELSE 9070
230 REM COMPUTE LEASE PAYMENT(L) ASSUMING I=ANNUAL INTEREST
240  REM RATE DESIRED, N YEARS LEASE, K YEARS EQUIP. LIFE
250 REM  F PAYMENTS PER YEAR, P= PURCHASE VALUE
260 W=(1+I/F)^(N*F)
261 L=P*I/F*W/(W-1)
263 IF O3=1 THEN 266
264 IF O3=2 THEN 460 ELSE 460
266 L=L/(1+I/F)
267 GOTO 460
279 N3=0
280 N5=INT(((N*12)+A2+11)/12)
283 N1=INT(((N*12)+(N3*12)+A2+11)/12)
285 N2=N1
287 N4=N1
289 IF N2<Q THEN N2=INT(((Q*12)+A2+11)/12)
320 REM REPORT COVERS N2 YEARS
325 REM MISC EXPENSES
330 IF C=0 THEN 390
332 IF C=1 THEN 340 ELSE 9010
340 FOR Y=1 TO N2
350 INPUT FROM 4:C(Y)
360 NEXT Y
370 GOTO 420
390 FOR Y= 1TO N2
400 C(Y)=0
410 NEXT Y
420 IF O1=1 THEN 230
421 IF O1=2 THEN 460
422 IF O1=3 THEN 511
423  PRINT "LEASE CODE ERROR"
424 GOTO 9999
450  REM FORM L(Y)
460 Y7=INT(N*F)
462 IF O3=Q THEN 468
464 Y9=INT (((12-A2+1)*F)/12)
466 GOTO 470
468  Y9=INT(((12-A2+12/F)*F)/12)
470 Y=1
471 L(Y)=L*Y9
472 Y8=Y9
473 Y=Y+1
474 IF (Y8+F)>Y7 THEN 480
475 Y8=Y8+F
476 L(Y)=L*F
477 GOTO 473
480 L(Y)=L*(Y7-Y8)
485 A=N5+1
505 IF O2= 0 THEN 520 ELSE 512
511 A=1
512 FOR Y=A TO N4
513 INPUT FROM 5:L(Y)
514 NEXT Y
520 FOR Y=N4+1 TO N2+1
530  L(Y)=0
540 NEXT Y
550  L(N2+2)=0
551 FOR Y=1 TO N2+1
552 L(N2+2)=L(N2+2)+L(Y)
553 NEXT Y
555 IF O1=1 THEN 600
556 I=12/100
557 U=0
558 U1=0
559 W=1+I/F
560 IF O1=3 THEN 566
561 Q2=L*F/I*(1-W^(-N*F))
562  Q1=Q2*(-1/I-N/W+N/W/(1-W^(-N*F)))
564 GOTO 590
566 FOR Y=1 TO N
570 Q2=L(Y)/I*(W^F-1)/W^(Y*F)
571 Q1=Q2*(-1/I-Y/W+W^(F-1)/(W^F-1))
574  IF O3=1 THEN Q1=Q2/F+Q1*W,Q2=Q2*W
578 U=U+Q2
580 U1=U1+Q1
582 NEXT Y
584 IF N=INT(N) THEN 598
586 Q2=L(Y+1)/I/(N-INT(N))*(W^((N-Y)*F)-1)/W^(N*F)
588 Q1=Q2*(-1/I-Y/W-(Y-N)*W^(F*(Y-N)-1)/(1-W^(F*(Y-N))))
590 IF O3=1THEN Q1=Q2/F+Q1*W,Q2=Q2*W
594 U=U+Q2
596 U1=U1+Q1
598 I=I-(U-P)/U1
599 IF (U-P)/U1*SGN((U-P)/U1)>.0000001 THEN 557
600 W=0
602 IF K>=N+N3 THEN 610
604 PRINT "LIFE OF EQUIPMENT LESS THAN LEASE LIFE"
610 IF R4-P/10>0 THEN 630 ELSE 640
630 W=R4-P/10
640 Z=(P-W)/K
650 B=P
660 IF A1>0 THEN IF A1<5 THEN 957
670 IF M=1 THEN 720
675 IF K<3 THEN PRINT "1.5DB AND DDB NOT ALLOWED, EQUIP LIFE <3YR"
676 IF K<3 THEN 9999
680 IF M=1.5 THEN 830
690 IF M=2 THEN 830 ELSE 9030
710 REM START ST. LINE  DEPR. COMPUTATION
720 FOR Y=1 TO N+N3
730 D(Y)=Z
735 IF Y=INT(K) THEN 785
740 NEXT Y
750 IF Y=N+N3 THEN 1010
760 Y=Y+1
770 D(Y)=(N+N3+1-Y)*Z
780 GO TO 1010
785 IF N+N3<K THEN 750
790 IF K>Y THEN D(Y+1)=Z*(K-Y),Y=Y+1
810 GOTO 1010
820 REM START DDB OR 1.5 DB CALCULATION
830 FOR Y=1 TO N+N3
835 D(Y)=B*M/K
838 Z=(B-W)/(K+1-Y)
840 IF  D(Y)<Z THEN 890
841 IF Y=K THEN D(Y)=B-W
850 B=B-D(Y)
855 IF Y=INT(K) THEN 883
860 NEXT Y
862 IF Y=N+N3 THEN 1010
864 Y=1+Y
866 D(Y)=B*M/K*(N+N3+1-Y)
868 Z=(B-W)/(K+1-N-N3)*(N+N3+1-Y)
870 IF D(Y)<Z THEN D(Y)=Z
880 GO TO 1010
881  IF N+N3<K THEN 930
882 GOTO 790
883 IF N+N3<K THEN 862
884 GOTO 790
890 FOR Y=Y TO N+N3
900 D(Y)=Z
905 IF Y=INT(K) THEN 881
910 NEXT Y
930 IF Y=N+N3 THEN 1010
940 Y=Y+1
950 D(Y)=Z*(N+N3+1-Y)
951 GOTO 1010
956 REM ADR DEPRECIATION
957 A5=((Y7/F)*4)+4
958 A6=INT((15-A2)/3)
959 REM ADR DEPRECIATION
960 B1=P-W
961 K1=INT(K)
962 IF K#K1 THEN K1=K1+1
963 A3=0
964 D5=0
965 FOR Y=1 TO N1
966 D4=0
967 K2=K-Y+1
968 K3=K2
969 IF K2<1 THEN K3=1
971 D1=B/K3
972 IF K<3 THEN 994
974 IF A3=1 THEN 994
978 H=INT(K2)
980 G=(H+2*(K2-H))*(H+1)/2
982 D2=(B*K2)/G
984 D3=(B*M)/K
988 IF D3>D2 THEN D4=D3 ELSE D4=D2
992 IF M#2 THEN D4=D3
994 IF D4<D1 THEN D4=D1
996 IF B1-(D5+D4)<0 THEN A3=1
998 IF A3=1 THEN D4=D1
999 IF A1<3 THEN IF Y=1 THEN D4=D4/2
1000 IF A1>=3 THEN IF A2>6 THEN IF Y=1 THEN D4=0
1001 IF A1>=3 THEN IF A2<=6 THEN IF Y=1 THEN D4=D4
1002 IF D4>B1-D5 THEN D4=(B1-D5)/K3
1003 A7=A5-(((Y-1)*4)+A6)
1004 IF K>(N+N3) THEN IF A7/4<1 THEN D4=D4*(A7/4)
1005 D5=D5+D4
1006 D(Y)=D4
1007 B=B-D4
1009 NEXT Y
1010 FOR Y=Y+1 TO N2
1020 D(Y)=0
1030  NEXT Y
1040 D(N2+1)=0
1050  FOR Y=1 TO N2
1060  D (N2+1)=D(N2+1)+D(Y)
1070 NEXT Y
1080 P1=0
1090  REM COMPUTE DEBT REPAYMENT.  MONTHLY PAYMENT UNIFORM=X,
1100  REM YEARS INTEREST=E(I), YEARS PRINCIPAL P(I), LIFE DEBT Q
1110 IF D#0 THEN 1170
1120  X=0
1130  FOR Y=1 TO N2
1140 P(Y)=0
1150 NEXT Y
1160 GOTO 1490
1170 W=(1+E/F2)^(Q*F2)
1180  X=D*E/F2*W/(W-1)
1190 Y=0,D1=D,F8=A2,F9=12/F2,F7=INT(12*Q)
1205 F6,P(N2+1),E(N2+1)=0
1230 Y=Y+1
1235 IF F6>=F7 THEN 1370
1240 P(Y)=0
1250 E(Y)=0
1270 P1=X-(D1*E/F2)
1280 P(Y)=P(Y)+P1
1290 E(Y)=X-P1+E(Y)
1300 D1=D1-P1
1310 F8=F8+F9
1320 F6=F6+F9
1325 IF F6>=F7 THEN 1350
1330 IF F8<=12 THEN 1270
1340 F8=F8-12
1350 P(N2+1)=P(Y)+P(N2+1)
1360 E(N2+1)=E(Y)+E(N2+1)
1365 GOTO 1230
1370 Z,Y=Y+1
1381 FOR Y=Z TO N2
1390 E(Y),P(Y)=0
1410 NEXT Y
1490  REM COMPUTE FEDERAL TAX T(Y)
1500  T(N2+1)=0
1501 IF O5=0 THEN 1519
1502 DIM G(51),H(51)
1503 FOR Y= 1 TO O5
1504 INPUT FROM 6:L8,L9
1505 G(Y)=L8
1506 H(Y)=L9
1507 L5=INT((L9+F-1)/F)
1508 IF L5=0 THEN L5=1
1509 I(L5)=I(L5)+L8
1510 NEXT Y
1518 DIM I(51)
1519 FOR Y=1 TO N5
1520 T(Y)=(L(Y)-D(Y)-E(Y)-C(Y)-I(Y))*T
1530  T(N2+1)=T(Y)+T(N2+1)
1540  NEXT Y
1541 Z=0
1542 IF O2=1 THEN IF N<INT(N) THEN Z=1
1543 IF Z=1 THEN T(Y)=T(Y)+T*L(Y+1)
1544 IF  Z=1 THEN T(N2+1)=T(N2+1)+T*L(Y+1)
1545 FOR Y=N5+1 TO N2
1546 T(Y)=T*(L(Y+Z)-D(Y)-E(Y)-C(Y)-I(Y))
1547 T(N2+1)=T(Y)+T(N2+1)
1548 NEXT Y
1550  REM COMPUTE AFTER TAX CASH FLOW F(Y)
1560  F(N2+1)=0
1570 FOR Y=1 TO N5
1580 F(Y)=L(Y)-E(Y)-P(Y)-T(Y)-C(Y)-I(Y)
1590  F(N2+1)=F(Y)+F(N2+1)
1591 NEXT Y
1592 IF Z=1 THEN F(Y)=F(Y)+L(Y+1),F(N2+1)=F(N2+1)+L(Y+1)
1594 FOR Y=N5+1 TO N2
1595 F(Y)=L(Y+Z)-E(Y)-P(Y)-T(Y)-C(Y)-I(Y)
1596  F(N2+1)=F(N2+1)+F(Y)
1600  NEXT Y
1601 IF X7=0 THEN 1605
1602 X9=INT((X8+F-1)/F)
1603 F(X9)=F(X9)+X7
1604  T(X9)=T(X9)-X7
1605 U4=T*(R-P+D(N2+1))
1610 REM FIND AFTER TAX RATE OF RETURN, V, USING THE NEWTON-
1620 REM RAPHSON METHOD: V(M+1)=V(M)-F[V(M)]/F'[V(M)]
1630  REM ASSUME THAT EQUIPMENT IS SOLD AFTER N YEARS FOR R DOLLARS
1639 GOTO 1650
1640 G7=INT(12/F)
1641 IF O3=1 THEN 1644
1642 G6=INT(((13-A2)*F)/12)
1643 GOTO 1645
1644 G6=INT(((12-A2+G7)*F)/12)
1645 G8=INT((N+N3)*F)
1646 G9=F-G6
1647 G5=INT((G9+G8)/F)
1648 G5=(G9+G8)-(G5*F)
1649 GOTO 1680
1650 IF P-D<=0 THEN 9050
1651 DEF FNA(F,A)=X1/V*(A^(F-F*Y)-A^(-Y*F))
1653 DEF FNC(F,A,N)=X1/(N+1-Y)/V*(A^(F-F*Y)-A^(-N*F))
1655 DEF FND(X2,F,A,G9,Y2)=(X2/F)*A^-(G9+Y2)
1656 DEF FNE(A,A2,G7,Z)=Z*A^-((A2-(G7*INT((A2-1)/G7))-1)/G7)
1659 IF I6=0 THEN I6=I/2
1660 V=I6,W=1,X,U,Z,J=0
1675 A=1+V/F,B=1+V/F2,C=1+V/4
1678 GOTO 1640
1680 P1=P*A^-G9
1681 Y1,Y2=1,M1=A2,F9=G6
1685 PRINT P1
1686 P1=FNE(A,A2,G7,P1)
1687 PRINT A,A2,G7,P1
1690 IF F9=0 THEN 1694
1691 X2=L(Y1)-C(Y1)
1692 Z=Z+FND(X2,F9,A,G9,Y2)
1694 IF Y2>=G8 THEN 1712
1695 Y2=Y2+1
1697 M1=M1+G7
1699 IF M1<=12 THEN 1690
1701 M1=M1-12
1702 Y1=Y1+1
1704 F9=F
1706 IF INT((G8+G9)/F)>(Y1-1) THEN 1690
1708 F9=G5
1710 GOTO 1690
1712 IF O3=1 THEN Z=Z*A
1713 Z=FNE(A,A2,G7,Z)
1716 G6=INT((15-A2)/3)
1718 G7=3
1720 G8 =INT((((N+N3)*12)+2)/3)
1722 G9=4-G6
1723 G5=INT ((G9+G8)/4)
1724 G5=G9+G8-G5*4
1728 F9=G6
1730 Y1,Y2=1
1734 M1=INT((G9+1)*G7)
1736 GO SUB 1757
1737  J=FND(X2,F9,C,G9,Y2)
1739 X=X+J
1740 IF Y2>=G8 THEN 1761
1741 M1=M1+G7
1742 Y2=Y2+1
1743 IF M1<=12 THEN 1736
1744 M1=M1-12
1746 Y1=Y1+1
1748 F9=4
1750 IF INT((G8+G9)/4)>(Y1-1) THEN 1736
1752 F9=G5
1754 GOTO 1736
1757 IF F9=0 THEN 1760
1758 X2=T*(L(Y1)-C(Y1)-I(Y1)-D(Y1)-E(Y1))
1759 Z=Z-FND(X2,F9,C,G9,Y2)
1760 RETURN
1761 IF D=0 THEN 1818
1762 G6=INT(((13-A2)*F2)/12)
1770 G7=12/F2
1772 G8=INT((N+N3)*F2)
1774 G9=F2-G6
1776 G5=INT((G9+G8)/F2)
1778 G5=(G9+G8)-(G5*F2)
1780 Y1,Y2=1
1784 M1=F2-G6+1
1786 F9=G6
1788 GO SUB 1810
1790 IF Y2>=G8 THEN 1816
1792 Y2=Y2+1
1794 M1=M1+G7
1796 IF M1<=12 THEN 1788
1798 M1=M1-12
1800 Y1=Y1+1
1802 F9=F2
1804 IF INT((G8+G9)/F2)>(Y1-1) THEN 1788
1806 F9=F5
1808 GOTO 1788
1810 X2=P(Y1)+E(Y1)
1811 IF F9=0 THEN 1814
1812 U=U-FND(X2,F9,B,G9,Y2)
1814 RETURN
1816 U=FNE(B,A2,G7,U)
1818 U=U+Z
1820 U=U+(R-U4)/A^((N+N3)*F)
1821 L6=0
1822 IF O5=0 THEN 1826
1823 FOR Y=1 TO O5
1824 L6=L6+G(Y)*A^(-H(Y))
1825 NEXT Y
1826 U=U+X7*A^(-X8)-L6
1828 REM PRINT W,V,U TO WATCH PROGRESS OF LOOP
1830 IF W=1 THEN 1850 ELSE 1920
1850 U1=U,U2=V
1870 W=2,V=1.001*V
1890 GOTO 1671
1900 REM F'[V(M)]=U3=DERIVATIVE OF FUNCTION[DISCOUNTED CASH FLOW
1910 REM MINUS PURCHASE VALUE MINUS DEBT VALUE]
1920 U3=(U1-U)/(U2-V)
1930 V=U2-(U1-P1+D)/U3
1940 IF (U2-V)*SGN(U2-V)>.0000001 THEN 1670
1950 REM  ADD EFFECT OF SALE AT RESIDUAL VALUE, R
1960 T(N2+1)=T(N2+1)+U4
1970 F(N2+1)=F(N2+1)+R-U4
5000 PRINT,,"LEASE ANALYSIS"
5010 PRINT
5020 PRINT
5022 IF O3=1 THEN B$="PAYMENTS IN ADVANCE"
5023 IF O3=2 THEN B$="PAYMENTS IN ARREARS"
5040 PRINT IN FORM "PURCHASE VALUE OF EQUIPMENT $999,999,999":P
5050  PRINT "PRETAX RATE: ":I*100:"%":B$
5060 PRINT IN FORM "RATE OF RETURN: 999.9999 %":V*100
5070 PRINT "ORIGINAL LEASE ":F*N:" PERIODS"
5075 IF O2=1 THEN PRINT "EXTENSION ":F*N3:" PERIODS"
5080 PRINT IN IMAGE "EQUIPMENT LIFE %%.%% YEARS  LEASE STARTS IN
MONTH %%%":K,A2
5085 IF D=0 THEN 5105
5090 PRINT "DEBT LIFE ":Q:" YEARS"
5105 IF D=0 THEN 5120
5110 PRINT "DEBT INTEREST ":E*100:"%"
5120 PRINT "TAX RATE ":T*100:"%"
5121 IF X7=0 THEN 5130
5122 PRINT IN FORM "ITC $999,999,999  IN PERIOD 999":X7,X8
5130 IF M=1 THENA$="S.L.D."
5140 IF M=1.5 THEN A$="1.5 D.B."
5150 IF M=2 THEN A$="D.D.B."
5160 PRINT
5170 PRINT TAB(7):"LEASE";TAB(17):A$;TAB(28):"---DEBT REPAYMENT---";
TAB(51):"FEDERAL":TAB(63):"AFTER TAX"
5190PRINT "YR";TAB(6):"PAYMENT";TAB(17):"DEPREC'N";TAB(28):"INTEREST";
"PRINCIPAL":TAB(53):"TAX":TAB(63):"CASH FLOW"
5210 FOR Y=1 TO N5
5220 Z8="%% 2B 9%. 1B 9%. 1B 9%. 1B 9%. 1B 9%. 1B 9%."
5230 PRINT IN FORM Z8:Y,L(Y),D(Y),E(Y),P(Y),T(Y),F(Y)
5250 NEXT Y
5251 IF N>INT(N) THEN X=1
5252 FOR Y=N5+1 TO N2
5253 PRINT IN FORM Z8:Y,L(Y+X),D(Y),E(Y),P(Y),T(Y),F(Y)
5254 NEXT Y
5260 Y=0
5270 PRINT IN FORM Z8:N1,Y,Y,Y,Y,U4,R-U4
5280 PRINT "    ---------- ---------- ---------- --------- ---------
 ---------"
5300 Y= N2 +1
5310 Z7="4B 9%. 1B 9%. 1B 9%. 1B 9%. 1B 9%. 1B 9%."
5320 PRINT IN FORM Z7:L(Y+1),D(Y),E(Y),P(Y),T(Y)-X7,F(Y)+X7
5340 PRINT
5350 PRINT
5360 PRINT IN IMAGE " RESIDUAL VALUE OF EQUIPMENT = $%%%,%%%,%%%.":R
5380 PRINT
5390 PRINT IN IMAGE " BOOK VALUE OF EQUIPMENT = $%%%,%%%,%%%.":P-D(N2+1)
5410 PRINT
5420 PRINT
5422 LINK "@RCCB"
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                              