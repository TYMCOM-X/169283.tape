DEF MXPS AS 3
LOCAL TRAN(0:200),WRUN(0:200),LRUN(0:200),%PN,K
LOCAL PFLG,LCNT
LOCAL DECK(1:13),DECK1(1:13),NDECK,%SHUFFLE
LOCAL SHFLG,%RAND,T(3),TK,NCRD
LOCAL %PLAY,%INIT,%TEST,%DEAL,%CSTN,%CNTSC
LOCAL NHANDS,NPLAY,NPP1,I,DSC,H(16),NTDSA,DSAF,DHVAL
LOCAL J,S(16),PS(8),SFLG,DFLG,ST(8),PST(8),DF(8)
LOCAL %FORT,%FDW,%FDL,%DISCARD,%STDECK
LOCAL URUN(8)
TABLE VAL(1:13)[11,2,3,4,5,6,7,8,9,10,10,10,10]

STRT: INIT

REPEAT NHANDS DO
   FOR I_1 TO NPP1*2 H(I)_DEAL
   IF DSC_H(NPP1*2)=1 THEN [INC NTDSA; DSAF_1] ELSE DSAF_0

   DHVAL_VAL(H(NPP1))+VAL(H(NPP1*2))

   FOR J_1 TO NPLAY [[S(J);S(NPP1+J)]_ &
      DOUBLE PLAY(H(J),H(NPP1+J),PS(J)); DF(J)_DFLG] !PLAY EACH HAND
   IF DHVAL#21 THEN DO
      SFLG_0
      FOR J_1 TO NPLAY IF S(J)<=21 OR S(NPP1+J)<=21 THEN INC SFLG
      IF SFLG#0 THEN [S(NPP1);S(NPP1*2)]_[DHVAL;]_ &
         DOUBLE PLAY(H(NPP1),H(NPP1*2),PS(NPP1))
   END

   FOR J_1 TO NPLAY DO
      SFLG_IF S(NPP1+J)=0 THEN 0 ELSE 1 !SPLIT FLAG
      ST(J)_0
      ST(J)+_TEST(S(J))+TEST(S(NPP1+J))

      IF DF(J)#0 THEN ST(J)_ST(J)*3

      FORT(J) !FIRST ORDER TRANSITIONS

         IF PST(J)*ST(J)>0 THEN  INC URUN(J) ELSE &
         [IF PST(J)>0 THEN FDW(J) ELSE IF PST(J)<0 THEN FDL(J); &
            URUN(J)_0]

      PST(J)_ST(J)
   END
   IF NCRD<NPP1*3 THEN SHUFFLE
END
MSG("$FIRST ORDER TRANSITION TABLE$$        1     2     3")
FOR J_1 TO NPLAY DO
   FOR K_1 TO 3 T(K)_0
   MSG("$$PL #"); PN(J,2)
   FOR I_1 TO 3 DO
      PUT(CARRET)
      PN(I,3)
      FOR K_1 TO 3 [PN(TK_TRAN((J-1)*10+(I-1)*3+K),6); T(K)+_TK]
   END
   MSG("$TOT")
   FOR K_1 TO 3 PN(T(K),6)
END

MSG("$$FREQUENCY DISTRIBUTION OF WIN AND LOSS RUNS$")
FOR I_0 TO 11 PN(I,6)
FOR J_1 TO NPLAY DO
   MSG("$PL #"); PN(J,2); PUT(CARRET)
   K_(J-1)*12
   MSG("WINS$")
   FOR I_0 TO 11 PN(WRUN(K+I),6)
   PUT(CARRET)
   MSG("LOSSES$")
   FOR I_0 TO 11 PN(LRUN(K+I),6)
   PUT(CARRET)
END

CLOSE.ALL
EXIT

%PLAY(C1,C2,PS)
LOCAL HIT, HV, HV1, HV2, N
LOCAL %HVAL
LOCAL H(20), S, B:, B1:, C:, D:
LOCAL B3:,B31:,C3:,D3:

   H(1)_C1; H(2)_C2; N_2
   HV_HV1_HVAL; HV2_0
   IF DHVAL=21 THEN RETURN [HV1;HV2]

   DO PS OF MXPS
!DEALER STRATEGY
   1: LOOP DO
      HIT_0
      IF HV<17 OR (HV=17 AND SFLG=0) THEN INC HIT
      WHILE HIT#0
      H(INC N)_DEAL
      HV_HVAL
   END
   HV1_HVAL
   HV2_0

!THORP BASIC STRATEGY
   2: S_1; DFLG_0
      B: IF C1=C2 THEN DO C1 OF 13
         1: 8: INC S
         2: 3: 6: IF 2<=DSC<=7 THEN INC S
         4: IF DSC=5 THEN INC S
         5: DEC S
         7: IF 2<=DSC<=8 THEN INC S
         9: IF 2<=DSC<=9 AND DSC#7 THEN INC S
         10: 11: 12: 13: DEC S
      END ELSE DEC S
      B1: IF S>0 THEN [H(2)_C2_DEAL; HV_HVAL; GO C]
      !ALT:IF H(2)_C2_DEAL=C1 THEN GO B

      IF SFLG=0 AND 8<=HV<=11 THEN DO HV OF 8:11
         8: DFLG_0 !IF 5<=DSC<=6 AND C1#6 AND C2#6 THEN DFLG_1
         9: DFLG_0 !IF 2<=DSC<=6 THEN DFLG_1
         10: IF 2<=DSC<=9 THEN DFLG_1
         11: DFLG_1
      END ELSE IF SFLG#0 AND 13<=HV<=18 THEN DO HV OF 13:18
         13: 14: 15: 16: DFLG_0 !IF 4<=DSC<=6 THEN DFLG_1
         17: DFLG_0 !IF 2<=DSC<=6 THEN DFLG_1
         18: DFLG_0 !IF 3<=DSC<=6 THEN DFLG_1
      END
      IF DFLG#0 THEN [H(INC N)_DEAL; HV_HVAL; GO D]

      C: IF S>0 AND C1=1 THEN GO D !ONLY ONE HIT ON ACE

      LOOP DO
         HIT_0
         IF SFLG=0 THEN DO
            EIF HV<12 THEN INC HIT
            ORIF HV<13 AND 2<=DSC<=3 THEN INC HIT
            FIF HV<17 AND (7<=DSC<=13 OR DSC=1) AND &
               (HV#16 OR N<3) AND (HV#14 OR C1#7#C2) THEN INC HIT
         END ELSE DO
            EIF HV<18 THEN INC HIT
            FIF HV=18 AND (9<=DSC<=13 OR DSC=1) THEN INC HIT
         END
         WHILE HIT#0
         H(INC N)_DEAL
         HV_HVAL
      END

      D: IF DEC S>0 THEN [HV2_HVAL; GO C] ELSE HV1_HVAL

!THORP BASIC STRATEGY, COMPLETE
   3: S_1; DFLG_0
      B3: IF C1=C2 THEN DO C1 OF 13
         1: 8: INC S
         2: 3: 6: IF 2<=DSC<=7 THEN INC S
         4: IF DSC=5 THEN INC S
         5: DEC S
         7: IF 2<=DSC<=8 THEN INC S
         9: IF 2<=DSC<=9 AND DSC#7 THEN INC S
         10: 11: 12: 13: DEC S
      END ELSE DEC S
      B31: IF S>0 THEN [H(2)_C2_DEAL; HV_HVAL; GO C]
      !ALT:IF H(2)_C2_DEAL=C1 THEN GO B3

      IF SFLG=0 AND 8<=HV<=11 THEN DO HV OF 8:11
         8: IF 5<=DSC<=6 AND C1#6 AND C2#6 THEN DFLG_1
         9: IF 2<=DSC<=6 THEN DFLG_1
         10: IF 2<=DSC<=9 THEN DFLG_1
         11: DFLG_1
      END ELSE IF SFLG#0 AND 13<=HV<=18 THEN DO HV OF 13:18
         13: 14: 15: 16: IF 4<=DSC<=6 THEN DFLG_1
         17: IF 2<=DSC<=6 THEN DFLG_1
         18: IF 3<=DSC<=6 THEN DFLG_1
      END
      IF DFLG#0 THEN [H(INC N)_DEAL; HV_HVAL; GO D3]

      C3: IF S>0 AND C1=1 THEN GO D3 !ONLY ONE HIT ON ACE

      LOOP DO
         HIT_0
         IF SFLG=0 THEN DO
            EIF HV<12 THEN INC HIT
            ORIF HV<13 AND 2<=DSC<=3 THEN INC HIT
            FIF HV<17 AND (7<=DSC<=13 OR DSC=1) AND &
               (HV#16 OR N<3) AND (HV#14 OR C1#7#C2) THEN INC HIT
         END ELSE DO
            EIF HV<18 THEN INC HIT
            FIF HV=18 AND (9<=DSC<=13 OR DSC=1) THEN INC HIT
         END
         WHILE HIT#0
         H(INC N)_DEAL
         HV_HVAL
      END

      D3: IF DEC S>0 THEN [HV2_HVAL; GO C3] ELSE HV1_HVAL
   END
   RETURN [HV1;HV2]

   %HVAL
      LOCAL V,HV(20),I
      V_0
      FOR I_1 TO N V+_HV(I)_VAL(H(I))
      I_0
      WHILE V>21 AND INC I<=N THEN IF HV(I)=11 THEN &
         [HV(I)-_10; V-_10]
      SFLG_0
      FOR I_1 TO N IF HV(I)=11 THEN INC SFLG
      RETURN V
   END HVAL
END PLAY

%TEST(S)
   IF S=0 THEN RETURN 0
   IF S>21 THEN RETURN -1
   IF DHVAL>21 THEN RETURN 1
   IF S=DHVAL THEN RETURN 0
   IF S>DHVAL THEN RETURN 1
   RETURN -1
END TEST

%DEAL
   LOCAL I,J
   SHFLG_0
   IF NCRD<=1 THEN SHUFFLE
   I_RAND(1,NCRD)
   J_1
   WHILE I-_DECK(J)>0 THEN INC J
   DEC DECK(J); DEC NCRD; INC DECK1(J)
   IF PFLG#0 THEN DO
      IF LCNT>68 THEN [PUT(CARRET); LCNT_0]
      PN(J,3)
      LCNT+_3
   END
   RETURN J

   %%DISCARD(N)
      INC DECK1(N)
      RETURN

   %%STDECK
      FOR I_1 TO 13 [DECK(I)_0; DECK1(I)_4]
      NCRD_0
      RETURN

END DEAL

%INIT
   LOCAL PTR,STR(0:9)
   LOCAL CHR,L,I
   IOCS(1)
LOOP DO
   MSG("$# OF DECKS: ")
   WHILE NDECK_CSTN(GETLINE(TEL),@CHR,10)<=0
END
   STDECK
   LOOP DO
      MSG("$# HANDS: ")
      WHILE NHANDS_CSTN(GETLINE(TEL),@CHR,10)<=0
   END
   LOOP DO
      MSG("# PLAYERS: ")
      WHILE NPLAY_CSTN(GETLINE(TEL),@CHR,10)<=0
   END
   FOR I_1 TO NPLAY DO
      PTR_CHPT(STR,-1)
      CNTSC(I,PTR,@L,0)
      LOOP DO
         MSG("PL # ")
         PRINT(STR)
         PRINT(": ")
         WHILE NOT(0<PS(I)_CSTN(GETLINE(TEL),@CHR,10)<=MXPS)
      END
   END
   PS(NPP1_NPLAY+1)_1 !DEALER STRATEGY
   FOR I_0 TO 200 TRAN(I)_WRUN(I)_LRUN(I)_0
   FOR I_1 TO 8 ST(I)_PST(I)_DF(I)_0
   NTDSA_SFLG_DFLG_0
   RETURN
END INIT

%CNTSC(N,PTR,@I,S)
   LOCAL A(0:9),APTR,Q1,Q2
   I_0; APTR_CHPT(A,-1); NCHV APTR_EOLIT

   IF N<0 THEN [N_-N; INC I; NCHV PTR_$-]

   IF S#0 THEN [N;Q2]_N DIVMOD S

   LOOP DO
      [N;Q1]_N DIVMOD 10
      NCHV APTR_Q1+$0
      WHILE N#0
   END

   NCHV APTR

   WHILE Q1_PCHV APTR#EOLIT THEN [INC I; NCHV PTR_Q1]

   IF S#0 THEN DO
      NCHV PTR_$.
      INC I
      LOOP DO
         WHILE S>1
         S_S/10
         [Q1;Q2]_Q2 DIVMOD S
         INC I
         NCHV PTR_Q1+$0
      END
   END

   NCHV PTR_EOLIT
   RETURN
END CNTSC

%CSTN(PTR,@TCH,B)
   LOCAL N
   N_0
   WHILE $0 <= TCH_NCHV PTR <= B+$0 THEN N_ N*B+TCH-$0
   RETURN N
END CSTN

%RAND(M,N)
   DEF B AS EXUC
   LOCAL A,C,%EXUC
   IF C_N-M=0 THEN RETURN M
   IF M*N<=0 THEN INC C
   [;A]_(B DMUL B)
   [;A]_(A DMUL B)
   RETURN ((OCT 77777777 BAND A) MOD C)+M
   %EXUC
      RETURN EXU(OCT 47,1,OCT 23)
   END EXUC
END RAND

%FORT(J)
   LOCAL I
   I_IF PST(J)<0 THEN 1 ELSE IF PST(J)=0 THEN 2 ELSE 3
   K_IF ST(J)<0 THEN 1 ELSE IF ST(J)=0 THEN 2 ELSE 3
   INC TRAN((J-1)*10+(I-1)*3+K)
   RETURN
END FORT

%FDW(J)
   LOCAL I
   I_IF URUN(J)>10 THEN 11 ELSE URUN(J)
   INC WRUN((J-1)*12+I)
   RETURN
END FDW

%FDL(J)
   LOCAL I
   I_IF URUN(J)>10 THEN 11 ELSE URUN(J)
   INC LRUN((J-1)*12+I)
   RETURN
END FDL
\\->STRT

%PN(I,J)
   LOCAL STR(0:9),PTR,L
   PTR_CHPT(STR,-1)
   CNTSC(I,PTR,@L,0)
   REPEAT J-L PUT($ )
   PRINT(STR)
   RETURN
END PN
%SHUFFLE
   LOCAL I,J
   FOR I_1 TO 13 [J_DECK1(I)<==0; NCRD+_J; DECK(I)+_J]
   SHFLG_J_1
   I_RAND(1,NCRD)
   WHILE I-_DECK(J)>0 THEN INC J
   DEC DECK(J); DEC NCRD; INC DECK1(J)
   RETURN
END SHUFFLE
                                                                                                                                                                                 