TITLE	PUTRES FOR LIBOL 6(235)		
SUBTTL	SET UP AND RESTORE USER PROGRAM'S FIXED PARAMETERS	 CHUCK MCCOMAS

;COPYRIGHT 1972, DIGITAL EQUIPMENT CORP, MAYNARD, MA

ENTRY	PUTF.
ENTRY	RESF.
ENTRY	GETNM.

;ASSIGNMENTS

TA=1		;TEMP
TB=2		;TEMP

PA=16		;ARGUMENT PTR
PP=17		;PDL PTR

	HISEG
;PUTF. IS USED TO SAVE THE FIXED PARAMETERS
;(I.E., USES. THRU PR.) OF THE CALLING PROGRAM
;AND SUBSTITUTE THE FIXED PARAMETERS OF THE CURRENT PROGRAM

;CALLING SEQUENCE:
;	MOVEI	16,ADDR OF LOCAL %FILES (I.E., %USES-1)
;	PUSHJ	17,PUTF.

PUTF.:	HRLI	TA,USES.##	;USES. TO %USES+FIXNUM
	HRRI	TA,FIXNUM+1(PA)	;  I.E., TO HOLDING AREA
	HRRZI	TB,FIXNUM##(PA)
	BLT	TA,FIXNUM-1(TB)

	HRLI	TA,1(PA)	;%USES TO USES.
	HRRI	TA,USES.
	HRRZI	TB,FILES.##
	BLT	TA,FIXNUM-1(TB)

	POPJ	PP,

;RESF. IS USED TO RESTORE THE FIXED PARAMETERS
;OF THE CALLING PROGRAM

;CALLING SEQUENCE:
;	MOVEI	16,ADDR OF LOCAL %FILES (I.E., %USES-1)
;	PUSHJ	17,RESF.

RESF.:	HRLI	TA,FIXNUM+1(PA)	;%USES+FIXNUM TO USES.
	HRRI	TA,USES.	;  I.E., FROM HOLDING AREA
	HRRZI	TB,FILES.
	BLT	TA,FIXNUM-1(TB)

	POPJ	PP,

;GETNM. IS USED BY COBDDT TO GET THE ADDRESSES OF %NM., %DT., %PR.

;CALLING SEQUENCE:
;	MOVEI	16,ADDR OF 3-WORD BLOCK TO RECEIVE ADDRESSES
;	PUSHJ	17,GETNM.

GETNM.:	HRLZI	TA,[EXP	%NM.##
			%DT.##
			%PR.##]
	HRRI	TA,(PA)
	BLT	TA,2(PA)
	POPJ	PP,

	END
