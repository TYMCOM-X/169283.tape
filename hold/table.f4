C...TABLE TAPPER PROGRAM   ANTHONY GRAY
C
C...THIS PROGRAM PRODUCES TABLES WITH DEMOS (MAX. 5) ACROSS THE
C      TOP AND VEHICLES DOWN THE SIDE.  OPTIONAL VEHICLE DATA
C      INCLUDE SINGLES, SELF-PAIRS AND PAIRS.  THE USER MAY
C      SELECT ALL PAIRS, ONE OR MORE SPECIFIC PAIRS, OR ALL PAIRS
C      INVOLVING A PARTICULAR VEHICLE.  TABLE ALLOWS UP TO
C      100 PAIR SPECIFICATIONS PER TABLE, WHERE EACH SPEC IS FOR
C      EITHER A SPECIFIC PAIR OR FOR ALL THE PAIRS INVOLVING A
C      VEHICLE.  ONE CAN, THEREFORE, GET,FOR EXAMPLE, SEVERAL
C      PARTICULAR PAIRS PLUS ALL THE PAIRS OF EACH OF SEVERAL VEHICLES
C      WITHIN ONE TABLE.  COVERAGES ARE ALSO AVAILABLE;
C      IF REQUESTED, THEY ARE PRODUCED FOR ALL NUMBERS.
C...ALL DEMOS IN A TABLE MUST HAVE IDENTICAL VEHICLE LISTS.  IF ANY
C      ONE OF THE DEMOS LACKS PAIRS, THE TABLE WILL LACK THEM.
C...THE PRICING IS BASICALLY STRAIGHT MARK-UP; THE MARKUP FACTOR IS
C      2.2 AND THERE IS AN ADDITIONAL CHARGE OF 2.4 TELMAR TRU.  THIS
C      IS TO PROVIDE A 2.2 MARKUP ON THE ESTIMATED 2 TYMSHARE TRU NOT
C      CAPTURED BY THE PROGRAM.
C...THE PROGRAM IS SELF-CONTAINED; NO CHAINING.  TO LOAD IT, USE:
C      -LOAD TABLE,SUBS/LIB,MSUBS/LIB.  THE .SAV PROGRAM GOES IN
C      DIRECTORY P3DM.
C...STANDARD COMMON PACKAGE................................
        COMMON KPROJ,KPROG,KFILE,KACBLK,TCPU,ITCCV,ITCCU,ITCCL,ITCCF,
     1    ITRPSW,ITYMTR,ITRPAC,IMKUP,ICLI
      DIMENSION KFILE(2),ITCCV(30),ITCCU(30),ITCCL(30),ITCCF(30)
C...END STANDARD COMMON PACKAGE................................
C
      COMMON /TICOM/ NAMEV(40),IPAIRS(820,5),BASE(5),
     1    COV(5),ICOVSW,ICSTSW,NDEM
      DIMENSION ITITLE(15),NAMDEM(5,3,10),IC1(40,5),
     1    IVPTR(40),IBASE(5),JTITLE(15),NAMEVA(40),IPROP1(100),
     2    IPROP2(100),IPRWD(3),LINEV(14)
      DOUBLE PRECISION IFILE
      DATA MAXV/40/
C
C
      ITRPSW=1
      IMKUP=220
      DO 10 J=17,20
   10 ITCCL(J)=J+99
      CALL LEGIT
      DO 12 J=17,20
   12 ITCCL(J)=J+99
C...ADD 1.2*2 TO TELMAR TRUS;  1.2 IS MARKUP (220-100) AND 2 IS
C...ABOUT THE NUMBER OF TYMSHARE TRU MISSED BY ACCOUNTING IN
C...RUNNING THIS PROGRAM (I.E. COST OF LOADING ETC.)
      ITCCU(20)=240
      ITCCV(20)=1
C
   15 CALL TITIN (ITITLE)
      TYPE 8005
 8005 FORMAT('+OK? ',$)
      CALL BYESNO (IYN,1)
      IF (IYN.EQ.2)   GO TO 15
      MAXLIN=0
      IPROPT=1
C
C...SELECT UP TO 5 DEMOS
C
      DO 80 JD=1,5
   30 TYPE 8010,JD
 8010 FORMAT(' DEMO',I2,': ',$)
      ACCEPT 7010,IDEMFL
 7010 FORMAT(A5)
      IF (IDEMFL.EQ.1H )   GO TO 100
C...OPEN AND READ DEMO FILE
      CALL RDDEM (IDEMFL,IFILE,ICLI,JTITLE,NVIN,NAMEVA,IBASE(JD),
     1    IC1(1,JD),IPAIRS(1,JD),1,MAXV,LOCPR,1,1,IER)
      CALL CLOSE (12,IDUM)
      IF (IER.NE.0)   GO TO 30
      IF (JD.GT.1)   GO TO 35
      NVDEM=NVIN
      DO 32 J=1,NVDEM
   32 NAMEV(J)=NAMEVA(J)
      GO TO 50
C
C...ALL DEMOS MUST HAVE IDENTICAL VEHICLE LISTS
C
   35 IF (NVIN.EQ.NVDEM)   GO TO 40
      TYPE 8020,NVIN,NVDEM
 8020 FORMAT('+',I2,' VEHICLES VERSUS',I3,' IN PRECEDING DEMO')
      GO TO 30
   40 DO 45 J=1,NVDEM
      IF (NAMEVA(J).EQ.NAMEV(J))   GO TO 45
      TYPE 8030,NAMEVA(J)
 8030 FORMAT('+VEHICLE ORDER MISMATCH AT VEHICLE ',A5)
      GO TO 30
   45 CONTINUE
C
C...DEMO IS OK - ACCEPT COLUMN HEADING (1-3 LINES) & RIGHT-JUSTIFY
C
   50 IF (LOCPR.EQ.0)   IPROPT=2
      BASE(JD)=IBASE(JD)
      IGOTO=1
   51 TYPE 8035
 8035 FORMAT('+COLUMN HEADING:'/)
      DO 512 J=2,3
      DO 512 K=1,10
  512 NAMDEM(JD,J,K)=1H 
      DO 562 J=1,3
      ACCEPT 7020,(NAMDEM(JD,J,K),K=1,10),IDUMMY
 7020 FORMAT(10A1,A5)
      DO 54 K=10,1,-1
      IF (NAMDEM(JD,J,K).NE.1H )   GO TO 55
   54 CONTINUE
      GO TO 565
   55 IF (K.EQ.10)   GO TO 562
      ISHIFT=10-K
      DO 56 K=10,1,-1
      IF (K.GT.ISHIFT)   NAMDEM(JD,J,K)=NAMDEM(JD,J,K-ISHIFT)
      IF (K.LE.ISHIFT)   NAMDEM(JD,J,K)=1H 
   56 CONTINUE
  562 CONTINUE
      J=4
  565 MAXLIN=MAX0(MAXLIN,J-1)
      GO TO (80,105),IGOTO
   80 CONTINUE
C
C...ALL DEMOS READ - ALLOW FOR EDITING OF COLUMN HEADINGS
C
      JD=6
  100 NDEM=JD-1
  105 TYPE 8052
 8052 FORMAT(' CHANGE COL HEADING: ',$)
      ACCEPT 7025,JD
 7025 FORMAT(I)
      IF (JD.EQ.0)   GO TO 108
      IGOTO=2
      IF (JD.GE.1.AND.JD.LE.NDEM)   GO TO 51
      TYPE 8054
 8054 FORMAT('+?'/)
      GO TO 105
C
C...LIST NAMES OF ALL VEHICLES IN DEMOS IF REQUESTED
C
  108 TYPE 8055
 8055 FORMAT(' LIST VEHICLES? ',$)
      CALL BYESNO (IYN,1)
      IF (IYN.EQ.2)   GO TO 1092
      TYPE 8056,(NAMEV(J),J=1,NVDEM)
 8056 FORMAT('+',A5/(1X,A5))
      TYPE 8057
 8057 FORMAT('0')
C
C...SELECT VEHICLES AND PRINT OPTIONS
C
 1092 CALL FFVEH (NVDEM,NAMEV,NV,IVPTR)
      TYPE 8085
 8085 FORMAT('+COVERAGE? ',$)
      CALL BYESNO (ICOVSW,1)
      IC1SW=1
      IC2SW=2
      IF (IPROPT.EQ.2)   GO TO 320
      TYPE 8090
 8090 FORMAT('+C1? ',$)
      CALL BYESNO (IC1SW,1)
      TYPE 8100
 8100 FORMAT('+C2? ',$)
      CALL BYESNO (IC2SW,1)
      IF (NV.EQ.1)   GO TO 230
C
C...ACCEPT PAIR OPTION(S)
C
      NPR=0
  200 NPR=NPR+1
      IF (NPR.GT.100)   GO TO 320
  210 TYPE 8110
 8110 FORMAT('+PAIRS: ',$)
      ACCEPT 7030,(IPRWD(L),L=1,3)
 7030 FORMAT(3A5)
      CALL SETSCN (IPRWD,12)
      IPRWD1=LDALPH(5)
      IPRWD2=LDALPH(5)
      IF (IPRWD1.NE.' ')   GO TO 215
C...BLANK ANSWER ENDS PAIR OPTIONS SECTION
      IPROP1(NPR)=-1
      GO TO 320
  215 IF (IPRWD1.NE.'NONE'.AND.IPRWD1.NE.'N')   GO TO 240
C...NO PAIRS - MUST BE FIRST REQUEST
      IF (NPR.EQ.1)   GO TO 230
  220 TYPE 8054
      GO TO 210
  230 IPROPT=2
      GO TO 320
  240 IF (IPRWD1.NE.'ALL'.AND.IPRWD1.NE.'Y')   GO TO 250
C...ALL PAIRS - MUST BE FIRST REQUEST
      IF (NPR.GT.1)   GO TO 220
      IPROP1(1)=0
      GO TO 320
C...FIRST WORD IS VEHICLE NAME
  250 DO 260 J=1,NV
      IF (IPRWD1.EQ.NAMEV(IVPTR(J)))   GO TO 270
  260 CONTINUE
      GO TO 220
  270 IPROP1(NPR)=IVPTR(J)
      IF (IPRWD2.NE.' ')   GO TO 280
C...ONE VEHICLE NAME ENTERED - ALL PAIRS INVOLVING THAT VEHICLE
      IPROP2(NPR)=0
      GO TO 200
  280 DO 290 J=1,NV
      IF (IPRWD2.EQ.NAMEV(IVPTR(J)))   GO TO 300
  290 CONTINUE
      GO TO 220
C...TWO VEHICLE NAMES ENTERED - THIS REQUESTS THEIR PAIR
  300 IPROP2(NPR)=IVPTR(J)
      GO TO 200
C
C...DIALOG FINISHED - TYPE TABLE
C
  320 TYPE 8120
 8120 FORMAT(///)
      CALL PRITIT (ITITLE)
C...COLUMN HEADINGS
      TYPE 8125
 8125 FORMAT('0')
      DO 325 K=1,MAXLIN
  325 TYPE 8130,((NAMDEM(J,K,L),L=1,10),J=1,NDEM)
 8130 FORMAT(10X,5(2X,10A1))
      TYPE 8140,(IBASE(J),J=1,NDEM)
 8140 FORMAT('0TOTAL',4X,5I12)
      IF (IC1SW.EQ.2.AND.IC2SW.EQ.2)   GO TO 400
C...SINGLES
      DO 395 J=1,NV
      IVPJ=IVPTR(J)
      IF (IC1SW.EQ.2)   GO TO 370
      TYPE 8150,NAMEV(IVPJ),(IC1(IVPJ,K),K=1,NDEM)
 8150 FORMAT('0',A5,' C1 ',5I12)
      IF (ICOVSW.EQ.2)   GO TO 360
      DO 350 K=1,NDEM
  350 COV(K)=100.*IC1(IVPJ,K)/BASE(K)
      TYPE 8160,(COV(K),K=1,NDEM)
 8160 FORMAT(8X,'% ',5F12.2)
C...SELF-PAIRS
  360 IF (IC2SW.EQ.2)   GO TO 395
  370 INDEX=(IVPJ*(IVPJ-1))/2+IVPJ
      TYPE 8170,NAMEV(IVPJ),(IPAIRS(INDEX,K),K=1,NDEM)
 8170 FORMAT('0',A5,' C2 ',5I12)
      IF (ICOVSW.EQ.2)   GO TO 395
      DO 390 K=1,NDEM
  390 COV(K)=100.*IPAIRS(INDEX,K)/BASE(K)
      TYPE 8160,(COV(K),K=1,NDEM)
  395 CONTINUE
C...PAIRS
  400 IF (IPROPT.EQ.2)   GO TO 600
      IF (IPROP1(1).NE.0)   GO TO 460
C...ALL PAIRS
      DO 450 JV=1,NV-1
      DO 450 KV=JV+1,NV
  450 CALL TYPEPR (IVPTR(JV),IVPTR(KV))
      GO TO 600
C...NOT ALL PAIRS
  460 NPR=0
  470 NPR=NPR+1
      IF (IPROP1(NPR).EQ.-1)   GO TO 600
      IF (IPROP2(NPR).EQ.0)   GO TO 500
C...ONE SPECIFIC PAIR
      CALL TYPEPR (IPROP1(NPR),IPROP2(NPR))
      GO TO 470
C..ALL PAIRS INVOLVING A PARTICULAR VEHICLE
  500 DO 530 JV=1,NV
      IF (IVPTR(JV).EQ.IPROP1(NPR))   GO TO 530
      CALL TYPEPR (IPROP1(NPR),IVPTR(JV))
  530 CONTINUE
      GO TO 470
C
C...TABLE FINISHED
C
  600 ITCCV(3)=NDEM
      CALL ACCT (2)
      TYPE 8175
 8175 FORMAT('0'/'0'/'0STOP? ',$)
      CALL BYESNO (IYN,1)
      IF (IYN.EQ.2)   GO TO 15
C...NO MORE TABLES WANTED - WRAP IT UP
      CALL ACCT (3)
      STOP
      END
C
C
C
C
C
      SUBROUTINE TYPEPR (IVP1,IVP2)
C
C...TYPE A LINE OF PAIRS
C
C...IVP1=ORDINAL OF FIRST VEHICLE
C   IVP2=ORDINAL OF SECOND VEHICLE
C
      COMMON /TICOM/ NAMEV(40),IPAIRS(820,5),BASE(5),
     1    COV(5),ICOVSW,ICSTSW,NDEM
C
      IVPHI=MAX0(IVP1,IVP2)
      IVPLOW=MIN0(IVP1,IVP2)
      INDEX=(IVPHI*(IVPHI-1))/2+IVPLOW
      TYPE 8010,NAMEV(IVP1),NAMEV(IVP2),(IPAIRS(INDEX,K),K=1,NDEM)
 8010 FORMAT('0',A5,1X,A5,I10,4I12)
      IF (ICOVSW.EQ.2)   GO TO 40
      DO 30 K=1,NDEM
   30 COV(K)=100.*IPAIRS(INDEX,K)/BASE(K)
      TYPE 8020,(COV(K),K=1,NDEM)
 8020 FORMAT(8X,'% ',5F12.2)
   40 RETURN
      END
