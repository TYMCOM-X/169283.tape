! TYMEX external inteface

ALT.ENTRY("$EIEXI",%EIEXIT)
ALT.ENTRY("$EIQUI",%EIQUIT)

GLOBAL ALT.NAME("$EIEXI",%EIEXIT)
GLOBAL ALT.NAME("$EIQUI",%EIQUIT)

LOCAL %READ.TMP,%WRITE.TMP,%DELETE.TMP,%RUN

%EIEXIT
EXU(OCT 47,0,OCT 12,0)
END EIEXIT

%EIQUIT
EXU(OCT 47,1,OCT 12,0)
END EIQUIT

!READ TMPCORE FILE 'CODE' OR DISK FILE <JOB>'CODE'.TMP INTO 'BUFF'
!
%READ.TMP(CODE,BUFF,N)
LOCAL T.BLK(0:1),FILE(0:1),FID.TBL(0:WPF),F,%GET.NAME,TMPERR:
DEF IOWD(B,N) AS [18,18]*[-N,B-1]
LOCAL R.TMP.EXIT:

GET.NAME

^OCT 16_[18,18]*[2,T.BLK]
EXU(OCT 47,OCT 16,OCT 44,R.TMP.EXIT)

F_OPEN(-FID.TBL,SEQUEN+INPUT+WORD,TMPERR,2)
RB(F,N,BUFF)
CLOSE(F)
DELETE(-FID.TBL,TMPERR)

R.TMP.EXIT: RETURN

!WRITE TMPCORE FILE 'CODE' OR DISK FILE <JOB>'CODE'.TMP FROM 'BUFF'
!
%%WRITE.TMP(CODE,BUFF,N)
LOCAL W.TMP.EXIT:

GET.NAME

^OCT 16_[18,18]*[3,T.BLK]
EXU(OCT 47,OCT 16,OCT 44,W.TMP.EXIT)

F_OPEN(-FID.TBL,SEQUEN+OUTPUT+WORD,TMPERR,2)
WB(F,N,BUFF)
CLOSE(F)

W.TMP.EXIT: RETURN

%%DELETE.TMP(CODE)
LOCAL D.TMP.EXIT:,TBUFF
BUFF_@TBUFF
N_1

GET.NAME

^OCT 16_[18,18]*[2,T.BLK]
EXU(OCT 47,OCT 16,OCT 44,D.TMP.EXIT)

DELETE(-FID.TBL,D.TMP.EXIT)

D.TMP.EXIT: RETURN

TMPERR: MSG('CANNOT READ/WRITE TEMPORARY FILE$') ; EXIT

%GETNAME
LOCAL JOB,I,J,P
TABLE TMP.EXT[OCT 64.55.60.00.00.00]

EXU(OCT 47,OCT 16,OCT 30)
JOB_^OCT 16 ; P_CHPT(FILE,-1) ; I_100
WHILE I>0 THEN DO
   [J;JOB]_JOB DIVMOD I
   I/_10
   NCHV P_J+$0
END
I_CHPT(CODE,-1)
WHILE NCHV P_NCHV I#EOLIT THEN NULL
P_CHPT(FILE,-1)
SETFID(FID.TBL,@P,TMP.EXT,TMPERR)
T.BLK(0)_NAMARG(FID.TBL) SHL 18
T.BLK(1)_IOWD(BUFF,N)
RETURN
END GET.NAME
END READ.TMP

!EXECUTE RUN UUO ON FILE 'NAME'
!
%RUN(NAME,START)
LOCAL RUN.BLK(0:5),FID.TBL(0:WPF),P,RUNERR:
RUN.BLK(0)_OCT 63.71.63.00.00.00  ! 'SYS' IN SIXBIT
P_CHPT(NAME,-1)
SETFID(FID.TBL,@P,0,RUNERR)
RUN.BLK(1)_NAMARG(FID.TBL)
MOVE 4 FROM ALL 0 TO @RUN.BLK(2)
^OCT 16_[18,18]*[START,RUN.BLK]
EXU(OCT 47,OCT 16,OCT 35)
RUNERR: MSG('CANNOT RUN ') ; MSG(NAME) ; EXIT
END RUN
