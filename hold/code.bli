MODULE CODE (MLIST) =
BEGIN
EXTERNAL OUTMSG;
OWN MHBCONTROL, MLBCONTROL, MPBCONTROL, CDIRSELVECTOR;
MACRO	RIGHT(A) = (A)<0,18>$,
	LEFT(A) = (A)<18,18>$;
MACRO
	LDS(OP,REG,ADDR,MODE,I) =
		IF .PC GTR .QUP THEN OUTMSG(PCERROR)
				ELSE ((.PC)<0,36>_OP OR (REG ^ 23) OR ADDR OR (MODE ^ 18) OR (I ^ 22);
				      PC_.PC+1
				     )$;
MACRO
	SETPC(QPC,QUPPER) =
		PC_(QPC)<0,0>;
		QUP_IF QUPPER NEQ 0 THEN (QUPPER)<0,0> ELSE -1 XOR (1^35)$;
BIND PCERROR = PLIT ASCIZ '?J?MPC OUT OF BOUNDS?J?M?W';
BIND
	LI	=	0,
	LOCLA	=	#300 ^ 27,
	RJMP	=	#201 ^ 24,
	JMP	=	1 ^ 24,
	POLIAA	=	#532 ^ 27,
	STOP	=	#260037 ^ 18;

BIND
	NAME	=	#10,
	RCR	=	#10,
	CDIR	=	#11,
	DIR	=	#12,
	VIEWLB	=	#2,
	WINDLB	=	#4,
	SELINT	=	#13;

BIND
	XQTM	=	#10,
	RPTM	=	#4;
GLOBAL ROUTINE MHBGEN (CONTROL,VIEWPORT,START,UPPER,WHERETO,HITLOC,CDIRSELINT) =
	BEGIN
	MACRO	HIT1(A) = (A)<33,1>$;
	REGISTER PC,QUP;
	BIND NOTYET = PLIT ASCIZ '?M?JDMB HIT NOT IMPLEMENTED YET?M?J?W';
	BIND PCERROR = PLIT ASCIZ '?M?JPC OUT OF BOUNDS IN MHBGEN?M?J?W';
	MHBCONTROL_.CONTROL;
	MLBCONTROL_0;
	CDIRSELVECTOR_.CDIRSELINT;
	SETPC(.START,.UPPER);
	IF .HIT1(CONTROL)
	   THEN	OUTMSG(NOTYET)
	   ELSE	(LDS(LI,DIR,.RIGHT(CONTROL),0,0);
		 LDS(LOCLA,VIEWLB,.RIGHT(VIEWPORT),4,0);
		 LDS(JMP,0,.WHERETO,0,0)
		);
	RETURN .PC-.START
	END;
GLOBAL ROUTINE MLBGEN (CONTROL,POINTCONTROL,START,UPPER,NAMELOC,FLAG) =
	BEGIN
	MACRO	HIT2(A) = (A)<33,1>$,
		REG(A) = (A)<0,18>$,
		CDIRPOSITION(A) = (A)<9,9>$,
		SELINTPOSITION(A) = (A)<0,9>$,
		REFFLAG = (-2)<0,36,FREG,1>$;
	REGISTER  PC, QUP, TEMP;
	BIND PCERROR = PLIT ASCIZ '?M?JPC OUT OF BOUNDS IN MLBGEN?M?J?W';
	BIND HIT1MSG = PLIT ASCIZ '?M?JMLB HIT NOT IMPLEMENTED?M?J?W';
	BIND MPBMSG = PLIT ASCIZ '?M?JMPB CONTROLWORD CANNOT BE HANDLED WITH MLB CONTROLWORD?M?J?W';
	REFFLAG_0;
	TEMP_.CONTROL XOR .MLBCONTROL;
	SETPC(.START,.UPPER);
	IF .POINTCONTROL EQL 0
	  THEN	(IF .REG(TEMP) NEQ 0
		  THEN	(IF .CDIRPOSITION(TEMP) NEQ 0
			  THEN	LDS(LOCLA,CDIR,.RIGHT(CDIRSELVECTOR)+.CDIRPOSITION(CONTROL),0,0);
			 IF .SELINTPOSITION(TEMP) NEQ 0
			  THEN	LDS(LOCLA,SELINT,.RIGHT(CDIRSELVECTOR)+.SELINTPOSITION(CONTROL),0,0)
			);
		 IF .HIT2(CONTROL)
		  THEN	OUTMSG(HIT1MSG)
		)
	  ELSE
		OUTMSG(MPBMSG);
	LDS(RJMP,0,0,XQTM,0);
	LDS(POLIAA,RCR,0,RPTM,0);
	MPBCONTROL_.POINTCONTROL;
	MLBCONTROL_.CONTROL;
	RETURN .PC-.START
	END;
GLOBAL ROUTINE MPBGEN (CONTROL,START,UPPER,NAMELOC,FLAG) =
	BEGIN
	BIND PCERROR = PLIT ASCIZ '?M?JPC OUT OF BOUNDS IN MPBGEN?M?J?W';
	REGISTER  PC, QUP, TEMP;
	MACRO	CLIPREG(A) = (A)<0,18>$,
		HIT3(A) = (A)<33,1>$,
		CDIRPOS(A) = (A)<9,9>$,
		SELINTPOS(A) = (A)<0,9>$,
		REFLAG = (-2)<0,36,FREG,1>$;
	BIND NOHITMSG = PLIT ASCIZ '?M?JHIT IN MPB NOT IMPLEMENTED?M?J?W';
	REFLAG_0;
	TEMP_.CONTROL XOR .MPBCONTROL;
	SETPC(.START,.UPPER);
	IF .CLIPREG(TEMP) NEQ 0
	  THEN	(IF .CDIRPOS(TEMP) NEQ 0
		  THEN	LDS(LOCLA,CDIR,.RIGHT(CDIRSELVECTOR)+.CDIRPOS(CONTROL),0,0);
		 IF .SELINTPOS(TEMP) NEQ 0
		  THEN	LDS(LOCLA,SELINT,.RIGHT(CDIRSELVECTOR)+.SELINTPOS(CONTROL),0,0)
		);
	IF .HIT3(CONTROL)
	  THEN	OUTMSG(NOHITMSG);
	LDS(RJMP,0,0,XQTM,0);
	LDS(POLIAA,RCR,0,RPTM,0);
	MPBCONTROL_.CONTROL;
	RETURN .PC-.START
	END;
GLOBAL ROUTINE ENDCODE (START,UPPER) =
	BEGIN
	REGISTER  PC, QUP;
	SETPC(.START,.UPPER);
	LDS(LOCLA,NAME,17,0,0);
	RETURN  .PC-.START
	END;
END
ELUDOM
