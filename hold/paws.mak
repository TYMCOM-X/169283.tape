TITLE FORPSE %2.(102)
SUBTTL	D. TODD /DRT/ 15-JAN-1973
;***COPYRIGHT 1972,1973 DIGITAL EQUIPMENT CORP., MAYNARD, MASS.***
;FROM	20 MARCH 1966		ED YOURDON


;THE PAUSE SUBROUTINE MAY BE CALLED FOR ANY OF THE THREE
;FOLLOWING FORTRAN STATEMENTS:
;	PAUSE
;	PAUSE N
;	PAUSE "MESSAGE"
;WHERE N IS AN OCTAL NUMBER, AND "MESSAGE" IS AN ASCII
;MESSAGE.
;THE CALLING SEQUENCES FOR THE THREE TYPES OF PAUSE STATEMENTS
;ARE AS FOLLOWS:
;	PAUSE	GENERATES
;	MOVEI	1, 0
;	PUSHJ	P, PAUSE.

;	PAUSE N		GENERATES
;	HRROI	1, CONST
;	PUSHJ	P, PAUSE.	;CONST HAS THE OCTAL NUMBER IN IT

;	PAUSE "MESSAGE"	GENERATES
;	HRLI	1, N		;NUMBER OR WORDS IN ASCII MESSAGE
;	HRRI	1, CONST	;ASCII MESSAGE STARTS AT CONST
;	PUSHJ	P, PAUSE.

;AFTER TYPING PAUSE, AN OCTAL NUMBER AND/OR A MESSAGE, THE
;ROUTINE ALLOWS THE USER TO CONTINUE BY TYPING A 'G', FOLLOWED
;BY A CARRIAGE RETURN, AND ALLOWS HIM TO EXIT BY TYPING AN
;'X', FOLLOWED BY A CARRIAGE RETURN.

	SEARCH	FORPRM

	P=	17
	A=	0
	B=	1


IFN F10LIB,<
	HELLO	(PAUS.)
	SETOM	PSEFLG		;SET FLAG FOR PAUSE ENTRY
	JRST	PSEARG

	HELLO	(STOP.)
	SETZM	PSEFLG		;SET STOP FLAG
PSEARG:
	PUSH	P,T0
	PUSH	P,T1
	SETZ	T1,		;ASSUME NO ARG
	SKIPL	-1(L)	;IS THERE AN ARG
	JRST	PAUSEZ
	LDB	T1,[POINT 4,(L),12]	;GET THE ARG TYPE
	CAIE	T1,TP%LIT	;LITERAL STRING
	JRST	PAUSEN		;NO, A CONSTANT
	MOVSI	T1,(POINT 7)	;GET A BYTE POINTER
	HRRI	T1,@(L)		;GET THE ADDRESS
	ILDB	T0,T1		;GET A CHARACTER
	JUMPN	T0,.-1		;COUNT THE NUMBER OF WORDS
	SUBI	T1,@(16)	;NUMBER OF WORDS IN THE STING
	MOVSI	T1,1(T1)	;BUILD THE AOB POINTER
	HRRI	T1,@(L)		;GET THE STRING ADDRESS
	JRST	PAUSEZ		;COMMON ROUTINE
PAUSEN:
	HRRO	T1,(L)		;GET THE ADDRESS OF THE CONSTANT
>
IFN F40LIB,<
	JRST	PAUSEZ
	HELLO	(PAUSE.)
	PUSH	P,A
	PUSH	P,B
	SETOM	PSEFLG		;SET PAUSE FLAG
	JRST	PAUSEZ
>
PAUSEZ:
	PUSH	P,L
	TTCALL	13,0		;TURN OFF ^O
	  JFCL
	HLRE	A, B		;GET WORD COUNT OF MESSAGE
	JUMPLE	A, PAUSE1	;CONSTANT, OR NO MESSAGE?
	MOVNS	A		;MESSAGE - FORM AOBJN WORD
	HRLM	A, B		;IN ACCUMULATOR B
        MOVEI   L,MSBLK3                ;OUTPUT THE MSG ON TTY.
	PUSHJ	P,OUT.##
	MOVEI	L,[XWD 001000,(B)
		XWD 0,0]
PRLOOP:	PUSHJ	P,IOLST.##
	AOBJN	B, PRLOOP	;LOOP BACK FOR MORE WORDS
	JRST	PAUSE3	;ALL DONE, TYPE G AND X STUFF

PAUSE1: MOVEI   L,MSBLK4
	PUSHJ	P,OUT.##
	MOVEI	L,[XWD 001000,MESS5
		XWD 0,0]
	SKIPN	PSEFLG		;SKIP IF PAUSE CALL
	MOVEI	L,[XWD 001000,MESS6
		XWD 0,0]
	PUSHJ	P,IOLST.##
	HRRZ	A, (B)		;GET THE NUMBER IF THERE IS ONE
	JUMPGE	B,PAUSE3	;IS THERE REALLY A NUMBER?
	MOVEI	L,[XWD 001000,A
		XWD 0,0]
	PUSHJ	P,IOLST.##
PAUSE3:	PUSHJ	P,FIN.##	;FINISH PREVIOUS IO
	SKIPN	PSEFLG		;PAUSE/STOP
	JRST	EXIT.##	;STOP EXIT
        MOVEI   L,MSBLK1
	PUSHJ	P,OUT.##
	PUSHJ	P,FIN.##
        MOVEI   L,MSBLK2
	PUSHJ	P,IN.##
	MOVEI	L,[XWD 001000,A
		XWD 004000,0]
	PUSHJ	P,IOLST.##	;GET THE ANSWER
	LSH	A, -35		;MAKE CHARACTER RIGHT-JUSTIFIED
	TRZ	A,40		;ACCEPT LOWER CASE ALSO
	CAIN	A, "X"		;IS IT AN X?
	FUNCT	(EXIT.)	;YES, EXIT
	CAIN	A, "G"		;IS IT A G?
	JRST	PAUSE4		;YES, CONTINUE
	CAIE	A, "T"		;T FOR TRACE
	JRST	PAUSE3+1	;NO, TRY AGAIN
	FUNCT	TRACE,<0>	;YES DO A TRACE
	JRST	PAUSE3+1	;GET THE NEW RESPONCE
PAUSE4:	POP	P,B
	POP	P, A		;YES, RESTORE AC A
	POP	P,L
	POPJ	P,		;EXIT


MESS1:	ASCII	"(' Type G to Continue, X to Exit, T To trace.'/2H *,$)"
MESS2:	ASCII	"(A1)"
MESS3:	ASCII	"(1H 14A5/)"
MESS4:	ASCII	"(1H A5,1X,O6)"
MESS5:	ASCII	"PAUSE"
MESS6:	ASCII	"STOP "
PSEFLG:	BLOCK	1
        XWD     -5,0            ; ARG BLOCK.
MSBLK1: XWD     0,1
        Z
        Z
        MESS1
        EXP     MESS2-MESS1
        
        XWD     -5,0
MSBLK2: XWD     0,0
        Z
        Z
        MESS2
        EXP     MESS3-MESS2
        XWD     -5,0
MSBLK3: XWD     0,1
        Z
        Z
        MESS3
        EXP     MESS4-MESS3
        
        XWD     -5,0
MSBLK4: XWD     0,1
        Z
        Z
        MESS4
        EXP     MESS5-MESS4
        
	END

