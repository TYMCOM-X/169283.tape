TITLE	GETITM FOR COBOL 5(107)		
SUBTTL	GET NEXT SOURCE WORD		AL BLACKINGTON/CAM

;COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORP., MAYNARD,MA

TWOSEG
RELOC	400000

;TYPE VALUES RETURNED IN "CT":

;	1000	USER NAME, AS YET UNDEFINED
;	1001	FILE NAME
;	1002	CONSOLE MNEMONIC NAME
;	1003	NON-CONSOLE MNEMONIC NAME
;	1004	CONDITION NAME
;	1005	EXTERNAL NAME
;	1006	PROCEDURE NAME

;	1010	FIGURATIVE CONSTANT
;	1011	INTEGER
;	1012	NUMERIC LITERAL, NOT AN INTEGER
;	1013	NON-NUMERIC LITERAL

;	1017	DATA NAME
;	1020	REPORT NAME

;IN ADDITION, BIT 25 OF "CT" WILL BE A 1-BIT IF THE
;DATUM WAS STARTED IN AREA A.

	ENTRY GETITM	;GET NEXT WORD A DETERMINE TYPE
	ENTRY GETKAR	;GET NEXT CHARACTER FOR A WORD OR NUMERIC LITERAL.
	ENTRY GETCH	;GET NEXT CHARACTER FOR A NON-NUMERIC LITERAL.
	ENTRY GETFCH	;GET VERY FIRST CHARACTER IN THE SOURCE FILE.
	ENTRY GETWRD	;GET NEXT WORD OR LITERAL FROM SOURCE
	ENTRY FINSKP	;COMPLETE SKIPPING OF PARAGRAPH
	ENTRY CLRCPY	;CLEAR OUT CPYTAB
	ENTRY SETLIB	;SET UP FOR READING LIBRARY FILE
	ENTRY SKPSRC	;READ ONE SOURCE CHARACTER
	ENTRY PUTCRF	;PUT ITEM INTO CREF FILE

	EXTERNAL FATAL, WARN, DEVDED, STINFL, TRYNAM
	EXTERNAL PUTCPY, PUTCIF, PUTFEL, LNKSET
	EXTERNAL KILL
GETITM:	TSWF	FPERWD		;YES--GET PERIOD?
	JRST	GTITM1		;YES
	TSWF	FREGWD		;NO--REGET PREVIOUS WORD?
	JRST	GETWRD		;YES--DO SO, AND RETURN

GTITM0:	SKIPE	W2,SAVEWD+1	;ANYTHING SAVED?
	JRST	GITM20		;YES

GTITM1:	PUSHJ	PP,GETWRD	;GET NEXT WORD

GITM1A:	TLNN	W1,GWLIT	;LITERAL?
	TLNN	W1,GWRESV	;NO--RESERVED WORD?
	JRST	GTITM3		;NO

	LDB	CT,GWVAL	;YES--FIGURATIVE CONSTANT?
	CAIL	CT,700
	CAILE	CT,707
	JRST	GTITM2		;NO

	CAIN	CT,706		;MAYBE--"ALL"?
	JRST	GITM30		;YES

	CAIN	CT,704		;NO--TALLY?
	JRST	GTITM5		;YES

	MOVEI	CT,1010		;IT IS A FIGURATIVE CONSTANT
	TLO	W1,GWFIGC

GTITM2:	SETZM	WASERC		;ASSUME IT WAS NOT 'SEARCH'
	CAIN	CT,30		;IS IT 'SEARCH'?
	SETOM	WASERC		;YES--SET FLAG

	LDB	TE,GWCP		;GET CHARACTER POSITION
	CAIGE	TE,^D12		;IN A-MARGIN?
	IORI	CT,2000		;YES--SET FLAG
GITM2A:	MOVEM	CT,ITEMCT
	POPJ	PP,		;RETURN


;NOT A RESERVED WORD

GTITM3:	TLNE	W1,GWLIT	;IS IT A LITERAL?
	JRST	GITM10		;YES

	SKIPE	CREFSW		;IF WE ARE CREFFING,
	PUSHJ	PP,PTCRF0	;  WRITE OUT CREF ITEM

	TLNE	W1,GWNOT	;NO--IN NAMTAB?
	JRST	GITM3A		;NO

	HRRZ	CT,(TA)		;YES
	JUMPN	CT,GTITM4	;DEFINED?
GITM3A:	MOVEI	CT,1000		;NO
	JRST	GTITM2		;RETURN
;IT IS A DEFINED USER WORD

GTITM4:	PUSH	PP,TA
	MOVE	TA,0(TA)	;GET TABLE LINK
	LDB	CT,[POINT 3,TA,20]	;GET CODE
	EXCH	DT,0(PP)	;SAVE DT, DT_NAMTAB ADDRESS
	JUMPE	CT,GITM12	;IF CODE ZERO, SPECIAL
	PUSHJ	PP,LNKSET	;GET TABLE ADDRESS INTO TA
GITM4B:	EXCH	TA,DT		;DT_TABLE ADDRESS, TA_NAMTAB ADDRESS

	MOVE	CT,CODTAB(CT)

	CAIN	CT,1017		;DATAB?
	JRST	GITM4A		;YES

	CAIN	CT,1002		;NO--MNEMONIC?
	JRST	GTITM9		;YES

GITM4A:	POP	PP,DT		;RESTORE DT
	JRST	GTITM2		;RETURN


;TALLY
GTITM5:	MOVEI	CT,1017		;PRETEND IT'S A DATA-NAME
	TLO	W1,GWFIGC	;BUT TURN ON "FIG. CONST." ANYWAY
	JRST	GTITM2

;MNEMONIC NAME
GTITM9:	MOVE	TE,1(DT)	;DEVICE-NAME?
	TLNN	TE,MTCONS
	MOVEI	CT,1003		;NO
	JRST	GITM4A		;RETURN

;IT IS A LITERAL
GITM10:	TLNE	W1,GWNLIT	;NUMERIC?
	JRST	GITM11		;YES
	MOVEI	CT,1013		;NO
	JRST	GTITM2

GITM11:	MOVEI	CT,1011
	TLNE	W1,GWDP		;DECIMAL-POINT?
	AOJA	CT,GTITM2	;YES
	JRST	GTITM2		;NO--INTEGER
;ITEM IS CODE ZERO.
;IT IS EITHER A FILE-NAME OR A REPORT-NAME.

GITM12:	MOVE	TE,FILNXT	;GET SIZE OF
	SUB	TE,FILLOC	;  FILTAB
	CAILE	TA,(TE)		;IS ITEM WITHIN FILTAB?
	JRST	GITM13		;NO

	ADD	TA,FILLOC	;MAYBE--GET ABSOLUTE ADDRESS
	LDB	TE,[POINT 15,0(TA),17]; GET FILTAB'S NAMTAB LINK
	HLRZ	TD,DT		;GET RELATIVE NAMTAB ADDRESS
	CAIN	TE,(TD)		;IS IT SAME AS THIS ONE?
	JRST	GITM4B		;YES

GITM13:	MOVE	TA,DT		;TA_NAMTAB ADDRESS
	MOVEI	CT,1020		;REPORT-NAME CODE
	JRST	GITM4A
;SOMETHING WAS SAVED AT A PREVIOUS TIME, BY "FPERWD".

GITM20:	MOVE	W1,SAVEWD
	MOVE	CT,SAVEWD+2
	MOVEM	CT,ITEMCT

	SETZM	SAVEWD+1
	LDB	CP,GWCP
	LDB	LN,GWLN

	TSWF	FEOF		;END OF SOURCE?
	JRST	GITM21		;YES

	TLNE	W1,GWLIT	;WAS IT A LITERAL?
	POPJ	PP,		;YES

	TLZ	W1,GWNOT	;NO--GO LOOK IN NAMTAB
	PUSHJ	PP,TRYNAM
	TLOA	W1,GWNOT
	HRR	W1,0(TA)

	POPJ	PP,


GITM21:	MOVSI	W1,ENDIT	;RETURN AN "END OF PROG"
	LDB	CT,GWVAL
	POPJ	PP,
;THE WORD "ALL" WAS SEEN

GITM30:	SKIPE	WASERC		;WAS PREVIOUS WORD 'SEARCH'?
	JRST	GTITM2		;YES--THEN NO LITERAL IS COMING

	PUSHJ	PP,GETWRD	;GET NEXT WORD

	TLNN	W1,GWLIT	;LITERAL?
	TLNN	W1,GWRESV	;NO--IS NEW WORD RESERVED?
	JRST	GITM35		;NO

	LDB	CT,GWVAL	;IS NEW WORD A FIG. CONST.?
	CAIL	CT,700
	CAILE	CT,707
	JRST	GITM33		;NO--ERROR

	CAIN	CT,706		;MAYBE--"ALL" AGAIN?
	JRST	GITM33		;YES--ERROR

	MOVEI	CT,1010		;NO--IT IS A FIG. CONST.
	TLO	W1,GWFIGC!GWALL
	JRST	GITM2A

;ERROR WITH "ALL"

GITM33:	MOVEI	DW,^D273
	PUSHJ	PP,FATAL
	JRST	GITM1A


GITM35:	TLO	W1,GWALL	;NO--SET "ALL" FLAG
	JRST	GTITM3		;RETURN TO SET UP "CT"

;CODE TABLE FOR USER NAMES

CODTAB:	1001	;FILTAB
	1017	;DATAB
	1004	;CONTAB
	0	;INVALID
	1006	;PROTAB
	1005	;EXTAB
	0	;INVALID
	1002	;MNETAB
;PUT ITEM ONTO CREF FILE

PUTCRF:	SKIPN	CREFSW		;IF NO CREF FILE,
	POPJ	PP,		;  FORGET IT

PTCRF0:	MOVE	TE,[POINT 6,NAMWRD]

PTCRF1:	MOVE	TD,[POINT 6,CH]
PTCRF2:	ILDB	TC,TE
	CAIN	TC,":"-40
	MOVEI	TC,"-"-40
	CAIN	TC,";"-40
	MOVEI	TC,"."-40
	IDPB	TC,TD
	TLNE	TD,770000
	JRST	PTCRF2

	SOSG	CRFBHO+2
	PUSHJ	PP,PTCRF9
	IDPB	CH,CRFBHO+1

	CAME	TE,[POINT 6,NAMWRD+4,35]
	JRST	PTCRF1

	MOVE	CH,W2
	TLZ	CH,377774
	SOSG	CRFBHO+2
	PUSHJ	PP,PTCRF9
	IDPB	CH,CRFBHO+1

	POPJ	PP,

PTCRF9:	OUT	CRF,
	POPJ	PP,
	MOVEI	CH,CRFDEV
	JRST	DEVDED
;SCAN THE NEXT WORD.
;SET UP WORD DESCRIPTORS.

GETWRD:	SETZM	SAVECH
	TSWFZ	FPERWD;		;RETURN A PERIOD AND A WORD?
	JRST	TESTWD		;YES
	TSWFZ	FREGWD;		;NO--RETURN SAME WORD?
	JRST	REGWRD		;YES
	TSWFZ	FGTPER;		;RETURN A PERIOD?
	JRST	SETPER		;YES
	TSWFZ	FGTMIN;		;NO--RETURN A "-"?
	JRST	SETMIN		;YES

	SETZM	SAVEWD+1	;INSURE NOTHING SAVED
	SETZM	NAMWRD		;CLEAR NAMWRD
	MOVE	TA,[XWD NAMWRD,NAMWRD+1]
	BLT	TA,NAMWRD+4

GTWRD1:	SOSG	CPYCNT		;MORE ENTRIES TO COPY?
	JRST	GETWD0		;NO

	MOVE	TD,CPYST	;YES--GET NEXT ONE
	JRST	ENDWD7


GETWD0:	SWOFF	FLETTR!FALIT	;TURN OFF "ALPHA" FLAGS
	JRST	GTWD1B
;SCAN THE NEXT WORD  (CONT'D).
;INITIALIZE.

GETWD1:	TSWTZ	FNEEDS		;TURN OFF "SPACE NEEDED"
	JRST	GTWD1B
	MOVEM	CP,SAVBCP
	MOVEM	LN,SAVBLN

GTWD1B:	TSWF	FEOF;		;END-OF-FILE?
	JRST	SETEND		;YES

	TSWFZ	FECOPY		;ANY LIBRARY TO FINISH UP?
	PUSHJ	PP,ENDCPY	;YES--DO SO

	PUSHJ	PP,GETCH	;GET NEXT CHARACTER

	CAIE	CH," "
	CAIN	CH,$TAB
	JRST	GETWD1

GTWD1A:	MOVEM	LN,WORDLN
	MOVEM	CP,WORDCP
	SWOFF	FNEEDS;

	MOVE	TE,SAVBLN
	JUMPE	TE,GTWD1C
	MOVEM	TE,BLNKLN
	MOVE	TE,SAVBCP
	MOVEM	TE,BLNKCP

GTWD1C:	MOVE	CT,INPTCP
	MOVEM	CT,INPTST

	SETZB	CT,LC		;CLEAR COUNTERS
	SETZM	SAVBLN
	MOVE	PA,[POINT 6,NAMWRD]
	MOVE	PB,[POINT 7,LITVAL]
	SETZB	W1,W2
	SETZM	NOCONT		;SET "CONTINUATIONS LEGAL" INDICATION

	TSWF	FCOPY		;ARE WE COPYING
	SKIPN	EOLKAR		;YES--END-OF-LINE TO GO OUT?
	JRST	GETWD2	;NO

	SWON	FREGCH		;YES--SET "REGET CHARACTER"
	MOVE	CH,EOLKAR
	PUSHJ	PP,PUTCPY
	PUSHJ	PP,GETSRC
;FIRST NON-BLANK CHARACTER SEEN
GETWD2:	CAIL	CH,141		;CONVERT LC TO UC
	CAILE	CH,172
	JRST	.+2		;NOT LC
	TRZ	CH,40
	CAIG	CH,"Z"		;LETTER?
	CAIGE	CH,"A"
	JRST	GETWD5		;NO

	TLNE	W1,GWLIT	;YES--IS THIS A LITERAL?
	JRST	GTWD5A		;YES--ERROR

GETWD3:	SWON	FLETTR;		;NO--SET FLETTR
GTWD3A:	CAIGE	CT,^D30		;30 CHARACTERS YET?
	AOJA	CT,GTWD3B	;NO--INCREMENT AND JUMP

	MOVEI	DW,BIGWRD	;YES
	CAIN	CT,^D30		;HAVE WE PUT OUT DIAG?
	PUSHJ	PP,WARN		;NO--PUT IT OUT
	AOJA	CT,GETWD4

GTWD3B:	IDPB	CH,PB
	CAIN	CH,"-"		;IS IT "-"?
	MOVEI	CH,":"		;YES--SUBSTITUTE ":"
	SUBI	CH,40		;CONVERT TO SIXBIT
	IDPB	CH,PA		;STASH IT

GETWD4:	PUSHJ	PP,GETKAR	;GET NEXT CHARACTER
	JRST	GETWD2

GETWD5:	CAIG	CH,"9"		;NOT LETTER--DIGIT?
	CAIGE	CH,"0"
	JRST	GTWD10		;NO

GTWD5B:	AOJA	LC,GTWD3A	;YES--STASH IT

GTWD5A:	MOVEI	DW,BADLIT
	PUSHJ	PP,FATAL
	JRST	GETWD4
;TRY FOR NON-NUMERIC LITERAL

GETWD6:	CAIN	CH,$QUOTE	;QUOTE?
	JRST	GTWD6A
	CAIE	CH,"'"
	JRST	GTWD11		;NO

GTWD6A:	JUMPE	CT,GTWD6C	;YES--ANYTHING SEEN?
	SWON	FREGCH;		;YES--SET "REGET CHARACTER"
	JRST	ENDWRD		;FINISH UP

GTWD6C:	TLO	W1,GWLIT	;TURN ON "LITERAL" FLAG
	SWON	FLETTR!FALIT;	;TURN ON "ALPHA" FLAGS
	MOVEM	CH,TERMQ	;SAVE DELIMITER

GETWD7:	PUSHJ	PP,GETCH	;GET NEXT CHARACTER
	MOVE	TE,SRCCOL	;COLUMN 7?
	CAIN	TE,7
	JRST	GETWD9		;YES

	CAMN	CH,TERMQ	;NO--CLOSING QUOTE?
	JRST	GETWD8		;YES

GTWD7A:	CAIE	CT,^D120	;NO--TOO BIG?
	AOJA	CT,GTWD7B	;NO--INCREMENT AND JUMP

	MOVEI	DW,BIGLIT	;YES
	CAIN	CT,^D120	;HAVE WE PUT OUT DIAG?
	PUSHJ	PP,FATAL	;NO--PUT IT OUT
	JRST	GETWD7

GTWD7B:	CAIGE	CH,140
	CAIGE	CH,40
	TLO	W1,GWASCI
	IDPB	CH,PB
	JRST	GETWD7

GETWD8:	SWOFF	FALIT		;CLOSING QUOTE SEEN
	MOVEM	LN,SAVBLN	;UPDATE PTRS TO END OF LITERAL
	MOVEM	CP,SAVBCP
	PUSHJ	PP,GTWD18
	JRST	ENDLIT
;CONTINUATION COLUMN WHEN SCANNING NON-NUMERIC LITERAL

GETWD9:	CAIE	CH," "		;SPACE?
	JRST	GTWD9B		;NO
	MOVE	CP,WORDCP
	MOVE	LN,WORDLN
	MOVEI	DW,NOQUOT	;YES--ERROR
GTWD9A:	PUSHJ	PP,FATAL
	JRST	ENDWRD

GTWD9B:	CAIN	CH,"-"		;HYPHEN?
	JRST	GTWD9C		;YES
	MOVEI	DW,BADCC7	;NO--ERROR
	JRST	GTWD9A		;QUIT

GTWD9C:	PUSHJ	PP,GETCH	;GET NEXT CHARACTER
	MOVE	TE,SRCCOL	;COLUMN 7 AGAIN?
	CAIN	TE,7
	JRST	GETWD9		;YES

	CAIE	CH,$TAB		;NO--TAB?
	CAIN	CH," "		;NO--SPACE?
	JRST	GTWD9C		;YES--LOOP
	CAMN	CH,TERMQ	;NO--QUOTE?
	JRST	GETWD7		;YES--EVERYTHING OK
	PUSH	PP,CH
	MOVEI	DW,CONLIT	;NO--WARN HIM
	PUSHJ	PP,WARN
	POP	PP,CH
	JRST	GTWD7A		;GO STASH IT
;TRY A SPACE OR TAB

GTWD10:	CAIE	CH," "	
	CAIN	CH,$TAB
	JRST	ENDWRD		;YES--IT IS A SPACE OR A TAB


	MOVEM	LN,SAVLN1	;SAVE LINE NUMBER AND
	MOVEM	CP,SAVCP1	;	CHARACTER POSITION
	MOVEM	CH,SAVECH	;	AND CHARACTER

;TRY A HYPHEN

	CAIE	CH,"-"		;HYPHEN?
	JRST	GETWD6		;NO

	JUMPE	CT,GTW10C	;YES--FIRST CHARACTER?
	PUSHJ	PP,GETKAR	;NO--GET NEXT ONE
	CAIN	CH," "		;SPACE?
	JRST	GTW10A		;YES

	SWON	FREGCH;		;NO--SET "REGET CHARACTER"
	TLNE	W1,GWSIGN!GWDP	;IS IT A SIGNED LITERAL?
	JRST	GTW10D		;YES--ERROR

	MOVEI	CH,"-"		;GET HYPHEN BACK
	TLO	W1,GWHYF	;TURN ON "THERE IS A HYPHEN"
	JRST	GETWD3

;SPACE AFTER "-"

GTW10A:	TSWT	FARITH		;EXPRESSION?
	JRST	GW10AA		;NO
	SWON	FGTMIN;		;YES--SET "REGET MINUS"
	JRST	ENDWRD

GW10AA:	MOVE	LN,SAVLN1
	MOVE	CP,SAVCP1
	MOVEI	DW,ENDHYF	;NOT EXPRESSION--ERROR
	PUSHJ	PP,FATAL
	JRST	ENDWRD
;INPUT CHARACTER IS A HYPHEN  (CONT'D)

;WORD STARTED WITH A HYPHEN

;HAS TO BE A LITERAL

GTW10C:	IDPB	CH,PB
	TLO	W1,GWLIT!GWSIGN
	AOJA	CT,GETWD4


;IMPROPER LITERAL--SIGN AFTER EITHER SIGN OR DECIMAL POINT

GTW10D:	MOVE	LN,SAVLN1
	MOVE	CP,SAVCP1
	MOVEI	DW,BADLIT
	PUSHJ	PP,FATAL
	JRST	GETWD4
;TRY A PERIOD

GTWD11:	CAIE	CH,"."		;PERIOD?
	JRST	GTWD12		;NO

	TSWT	FLETTR;		;ANY LETTERS SO FAR?
	CAME	CH,DCPNT.##	;NO--ALSO DECIMAL POINT?
	JRST	GTW11C		;NO

	PUSHJ	PP,GETKAR	;YES
	JUMPE	CT,GTW11H	;WAS DECIMAL POINT THE FIRST CHARACTER?
	CAIG	CH,"9"		;NO--IS THIS A DIGIT?
	CAIGE	CH,"0"
	JRST	GTW11B		;NO

GTW11A:	TLOE	W1,GWDP		;YES--SET "DECIMAL-POINT" INDICATION
	JRST	GTW11G		;ALREADY SET--ERROR

	TLO	W1,GWLIT
	MOVEI	TC,"."
	IDPB	TC,PB		;STASH PERIOD
	SETZM	SAVECH
	AOJA	CT,GTWD5B	;KICK UP COUNT--GO TO LITERAL

GTW11B:	SWON	FGTPER;
	CAIE	CH," "		;IS IT A SPACE?	
	JRST	GTW11F		;NO--ERROR
	JRST	GTW11D		;YES

GTW11C:	SWON	FGTPER!FNEEDS;	;SET "GET A PERIOD" AND "SPACE NEEDED"

GTW11D:	MOVE	CP,SAVCP1
	MOVE	LN,SAVLN1

	JUMPN	CT,ENDWRD	;ANYTHING SO FAR?
	JRST	GETWRD
;FIRST CHARACTER WAS DECIMAL POINT

GTW11H:	CAIE	CH," "		;WAS IT FOLLOWED BY A SPACE?
	JRST	GTW11J		;NO--MUST BE A LITERAL
	SWON	FGTPER;		;YES--WARN HIM
	JRST	GTW11D

GTW11J:	TLO	W1,GWLIT!GWDP
	SWON	FREGCH;
	MOVEI	CH,"."
	IDPB	CH,PB
	SETZM	SAVECH
	AOJA	CT,GETWD4

GTW11F:	JUMPN	CT,GTW11E	;ANYTHING BEFORE THE PERIOD?
	SWON	FNEEDS;		;NO--SET "SPACE REQUIRED"
	JRST	GTW11D

GTW11E:	MOVEI	DW,BADLIT
	PUSHJ	PP,FATAL
	JRST	GTW11D

;TWO DECIMAL POINTS IN LITERAL

GTW11G:	PUSH	PP,CH
	MOVEI	DW,TWOPER
	MOVE	CP,SAVCP1
	MOVE	LN,SAVLN1
	PUSHJ	PP,FATAL
	POP	PP,CH
	JRST	GTWD3A
;TRY COMMA AND SEMI-COLON

GTWD12:	CAIE	CH,","		;IS IT A COMMA?
	JRST	GTW12B		;NO
	CAME	CH,DCPNT.	;YES--ALSO DECIMAL POINT?
	JRST	GTW12C		;NO

	PUSHJ	PP,GETKAR	;YES--LOOK AT NEXT CHARACTER
	CAIG	CH,"9"		;DIGIT?
	CAIGE	CH,"0"
	JRST	GTW12D
	JRST	GTW11A		;YES--TREAT AS DECIMAL POINT

GTW12B:	CAIE	CH,";"		;SEMI-COLON?
	JRST	GTWD13		;NO

GTW12C:	PUSHJ	PP,GETKAR
GTW12D:	CAIE	CH," "
	SWON	FREGCH;
	JRST	GTW11D


;TRY PLUS

GTWD13:	CAIE	CH,"+"		;NO--IS IT "+"?
	JRST	GTWD14		;NO

	JUMPE	CT,GTW10C	;YES--FIRST CHARACTER?

	SWON	FREGCH;
	JRST	ENDWRD

GTW13A:	MOVEI	DW,BADLIT
	PUSHJ	PP,FATAL
	JRST	GETWD4
;TRY SPECIAL CHARACTERS

GTWD14:	JUMPE	CT,GTWD15	;FIRST CHARACTER?
	SWON	FREGCH;		;NO--SET "REGET CHARACTER"
	JRST	ENDWRD

GTWD15:	PUSHJ	PP,CPYPUN	;PUT OUT THE CHARACTER (IF COPYING)

	CAIE	CH,"*"		;CHECK FOR "*"
	JRST	GTW15A		;NO

	MOVSI	W1,MULWD	;YES--IS NEXT "*" ALSO?
	PUSHJ	PP,GETKAR
	CAIN	CH,"*"
	JRST	GTW15D		;YES--IT'S "**"

	SWON	FREGCH;		;NO
	CAIN	CH," "		;IS NEXT CHARACTER SPACE?
	JRST	SETPN1		;YES
	JRST	GTW17A		;NO

GTW15D:	TSWF	FCOPY		;ARE WE COPYING?
	PUSHJ	PP,PUTCPY	;YES--PUT OUT SECOND "*"
	MOVSI	W1,EXPWD
	JRST	GTW17A

GTW15A:	MOVEI	W1,0
	CAIN	CH,"-"
	MOVSI	W1,MINWD
	CAIN	CH,"+"
	MOVSI	W1,PLUSWD
	CAIN	CH,"("
	MOVSI	W1,LPARWD
	JUMPN	W1,SETPN1

	MOVE	TA,PUNPTR
GTW15B:	HRRZ	TB,0(TA)
	CAMN	CH,TB
	JRST	GTWD17
	AOBJN	TA,GTW15B
;BAD CHARACTER
GTWD16:	MOVEI	DW,BADKAR	;PUT OUT DIAG
	PUSHJ	PP,FATAL
	JUMPE	CT,GETWRD	;ANTHING SO FAR?
	JRST	ENDWRD		;YES--FINISH UP
;A SPECIAL CHARACTER WAS SEEN. CHECK FOR FOLLOWING PUNCTUATION.

GTWD17:	HLLZ	W1,0(TA)
GTW17A:	PUSHJ	PP,SETPN1

	PUSH	PP,LN
	PUSH	PP,CP
	PUSHJ	PP,GTWD18
	SKIPE	SAVECH		;WAS PUNCTUATION SEEN?
	PUSHJ	PP,CHKPUN	;YES--PUT IT OUT

	POP	PP,CP
	POP	PP,LN
	POPJ	PP,

GTWD18:	SETZM	SAVECH
	PUSHJ	PP,GETKAR	;GET NEXT CHARACTER
	CAIN	CH," "		;SPACE?
	POPJ	PP,		;YES
	CAIN	CH,"."		;NO--PERIOD?
	SWON	FGTPER!FNEEDS	;YES--SET FLAGS
	CAIE	CH,";"		;SEMICOLON OR
	CAIN	CH,","		;COMMA?
	SWON	FNEEDS		;YES--SET "SPACE NEEDED"

	TSWT	FNEEDS		;ANY PUNCTUATION SEEN?
	JRST	GTW18C		;NO
	MOVEM	CH,SAVECH	;YES--SAVE IT
	MOVEM	LN,SAVLN1	;SAVE
	MOVEM	CP,SAVCP1	;  LOCATION
	POPJ	PP,

GTW18C:	SWON	FREGCH		;CHARACTER NOT PUNCTUATION--GET IT NEXT TIME
	POPJ	PP,

;RETURN A PERIOD
TESTWD:	TSWTZ	FREGWD;		;ALSO "REGET WORD"?
	JRST	TSTWD1		;NO--SIMPLY RETURN A PERIOD

	MOVEM	W1,SAVEWD	;SAVE THAT WORD
	MOVEM	W2,SAVEWD+1
	MOVE	CT,ITEMCT
	MOVEM	CT,SAVEWD+2

TSTWD1:	MOVSI	W1,PERWD
	POPJ	PP,
;PUNCTUATION OF SOME KIND

SETPUN:	PUSHJ	PP,CPYPUN

SETPN1:	MOVE	LN,SAVLN1
	MOVE	CP,SAVCP1

SETPN2:	DPB	LN,GWLN
	MOVEM	LN,WORDLN
	DPB	CP,GWCP
	MOVEM	CP,WORDCP
	POPJ	PP,		;RETURN


SETPLS:	MOVSI	W1,PLUSWD	;PLUS
	MOVEI	CH,"+"
	JRST	SETPUN

SETMIN:	MOVSI	W1,MINWD	;MINUS
	MOVEI	CH,"-"
	JRST	SETPUN

SETPER:	MOVSI	W1,PERWD	;PERIOD
	MOVEI	CH,"."
	JRST	SETPN1
;END-OF-FILE HAD BEEN SEEN BEFORE

SETEND:	MOVSI	W1,ENDIT
	MOVE	LN,SAVELN
	MOVEI	CP,7
	JRST	SETPN2


;REGET SAME WORD

REGWRD:	MOVE	CT,ITEMCT
	TLNE	W1,GWLIT	;LITERAL?
	POPJ	PP,		;YES--RETURN

	TLZ	W1,GWNOT
	PUSHJ	PP,TRYNAM	;NO--GET NAMTAB ENTRY
	TLOA	W1,GWNOT	;THERE ISN'T ONE
	HRR	W1,0(TA)
	POPJ	PP,
;WORD HAS BEEN SCANNED

ENDWRD:	TLNN	W1,GWLIT	;LITERAL?
	TSWT	FLETTR;		;NO--ALL DIGITS?
	JRST	ENDLIT		;YES

	PUSHJ	PP,TRYNAM	;NO--FIND WORD IN NAMTAB
	JRST	ENDWD2		;NOT FOUND

	MOVE	TB,0(TA)	;GET FLAG WORD
	LDB	TD,NAMVAL	;GET RESERVED WORD VALUE OR CPYTAB POINTER
	TLNN	TB,NAMRSV/1000000; RESERVED WORD?
	JRST	ENDWD6		;NO

ENDWD3:	DPB	TD,GWVAL	;YES--SET VALUE IN W1
	TLO	W1,GWRESV	;SET "RESERVED" FLAG

ENDWD4:	HRR	W1,TB		;GET TABLE ADDRESS
	HLRZ	TB,TA		;SET NAMTAB POINTER
	DPB	TB,GWNAMP

ENDWD5:	TSWF	FCOPY		;ARE WE COPYING?
	PUSHJ	PP,CPYNAM	;YES

ENDWD1:	TSWF	FCOPY		;ARE WE COPYING?
	PUSHJ	PP,CHKPUN	;APPEND ANY TERMINATING PUNCTUATION
	MOVE	LN,WORDLN	;SET "LN"
	DPB	LN,GWLN
	MOVE	CP,WORDCP	;SET "CP"
	DPB	CP,GWCP
	POPJ	PP,		;RETURN

ENDWD2:	TLO	W1,GWNOT
	JRST	ENDWD5
;A WORD MAY HAVE TO BE REPLACED  (FOR COPY).

ENDWD6:	JUMPE	TD,ENDWD4	;ANY TABLE LINK?

	TSWT	FCOPY		;YES--ARE WE COPYING?
	JRST	ENDW15		;NO--TROUBLE


	ADD	TD,CPYLOC	;YES--GET CPYTAB ADDRESS

	HRRZ	TE,0(TD)	;SET "NUMBER OF WORD TO REPLACE WITH"
	MOVEM	TE,CPYCNT

ENDWD7:	MOVE	W1,1(TD)	;RESET W1
	TLNE	W1,GWLIT	;IS IT A LITERAL?
	JRST	ENDWD9		;YES

	HRLZ	W2,W1

	MOVEI	TE,2(TD)	;RESET "START OF COPY ENTRY"
	HRRZM	TE,CPYST

	HLRZ	TE,W1
	CAIN	TE,LPARWD
	JRST	ENDW17
	CAIN	TE,RPARWD
	JRST	ENDW18

	HLRZ	TE,W2		;GET NAMTAB LINK OF REPLACING ITEM
	LSH	TE,-2
	ANDI	TE,77777
	MOVE	TA,TE
	ADD	TA,NAMLOC
	HRL	TA,TE

	MOVE	TE,[POINT 6,1(TA)]

	SETZM	NAMWRD		;GET NAMTAB LINK
	MOVE	TD,[XWD NAMWRD,NAMWRD+1]	;COPY NAMTAB ENTRY TO NAMWRD
	BLT	TD,NAMWRD+4

	SKIPA	TD,[POINT 6,NAMWRD]
ENDWD8:	IDPB	CH,TD
	ILDB	CH,TE
	TRNE	CH,60
	JRST	ENDWD8

	MOVE	TB,0(TA)	;IS IT RESERVED?
	LDB	TD,NAMVAL
	TLNN	TB,NAMRSV/1000000
	JRST	ENDWD4		;NO
	JRST	ENDWD3		;YES
;A WORD IS BEING REPLACED BY A LITERAL


ENDWD9:	SETZB	W2,CPYCNT	;THIS MUST BE THE ONLY ENTRY
	LDB	CT,GWVAL	;GET SIZE

	MOVE	TB,[POINT 7,LITVAL]
	HRLI	TD,440700
	ADDI	TD,2

	MOVEI	LC,0

ENDW10:	ILDB	CH,TD
	TLNN	W1,GWNLIT
	JRST	ENDW11

	CAIG	CH,"9"
	CAIGE	CH,"0"
	SKIPA
	ADDI	LC,1

ENDW11:	IDPB	CH,TB
	SOJG	CT,ENDW10

	LDB	CT,GWVAL
	JRST	ENDL1A


ENDW15:	TTCALL	3,[ASCIZ "COPY LINK WHEN NOT COPYING
"]
	JRST	KILL


;REPLACING WORD IS LEFT PARENTHESIS

ENDW17:	CAIG	CP,ENDCL-2
	PUSHJ	PP,EOLCH0
	MOVEI	CH," "
	PUSHJ	PP,PUTCPY
	MOVEI	CH,"("
	PUSHJ	PP,PUTCPY
	JRST	SETPN2

;REPLACING WORD IS RIGHT PARENTHESIS

ENDW18:	MOVEI	CH,")"
	PUSHJ	PP,PUTCPY
	JRST	SETPN2
;LITERAL HAS BEEN SCANNED

ENDLIT:	TLO	W1,GWLIT	;SET "LITERAL" FLAG
	TSWT	FLETTR;		;NON-NUMERIC?
	JRST	ENDL2		;NO
ENDL1:	DPB	CT,GWVAL	;SET SIZE
ENDL1A:	TSWF	FCOPY		;ARE WE COPYING?
	PUSHJ	PP,CPYLIT	;YES--COPY THE LITERAL
	JRST	ENDWD1

ENDL2:	JUMPE	LC,ENDL3	;ANY SIZE?
	TLO	W1,GWNLIT	;YES--SET "NUMERIC" FLAGIG	LC,^D18		;TOO BIG?
	JRST	ENDL1

	MOVEI	DW,BIGLIT	;YES--PUT OUT DIAG
	PUSHJ	PP,FATAL
	MOVEI	CT,^D18		;REDUCE SIZE
	JRST	ENDL1

ENDL3:	LDB	CH,[POINT 7,LITVAL,6];NO SIZE--IS IT "+"?
	CAIE	CH,"+"
	JRST	SETMIN		;NO--MUST BE "-"
	JRST	SETPLS		;YES
;COPY CONTENTS OF NAMWRD ONTO CPYFIL

CPYNAM:	PUSHJ	PP,EOLCHK

	MOVE	TB,[POINT 6,NAMWRD]

CPYN2:	ILDB	CH,TB
	TRNN	CH,60
	POPJ	PP,

	ADDI	CH,40
	CAIN	CH,":"
	MOVEI	CH,"-"
	PUSHJ	PP,PUTCPY

	CAME	TB,[POINT 6,NAMWRD+4,35]
	JRST	CPYN2

	POPJ	PP,

CPYN3:	MOVE	CP,SAVECP	;SET CP
	MOVE	LN,SAVELN	;SET LN
	POPJ	PP,
;COPY CONTENTS OF LITVAL ONTO CPYFIL

CPYLIT:	PUSHJ	PP,EOLCHK

	MOVE	CH,TERMQ
	TLNN	W1,GWNLIT
	PUSHJ	PP,PUTCPY

	MOVE	TB,[POINT 7,LITVAL]

CPYL1:	ILDB	CH,TB
	PUSHJ	PP,PUTCPY

	CAIGE	CP,CPMAXN
	JRST	CPYL2

	MOVEI	CH,$LF
	PUSHJ	PP,PUTCPY

	MOVEI	CH,"-"
	PUSHJ	PP,PUTCPY
	MOVEI	CH,$TAB
	PUSHJ	PP,PUTCPY

	MOVE	CH,TERMQ
	TLNN	W1,GWNLIT
	PUSHJ	PP,PUTCPY

	MOVEI	CP,^D12

CPYL2:	SOJG	CT,CPYL1

	MOVE	CH,TERMQ
	TLNN	W1,GWNLIT
	PUSHJ	PP,PUTCPY

	LDB	CT,GWVAL

	POPJ	PP,
;PUT OUT SOME PUNCTUATION ONTO CPYFIL.

CPYPUN:	TSWF	FCOPY		;ARE WE COPYING?
	TSWF	FNOCPY!FNOLST	;YES--ANY LIST FILE?
	JRST	CPYN3		;NO--FORGET IT

	CAIN	CH,")"		;IS THIS RIGHT-PAREN?
	JRST	CPYPN3		;YES

	PUSH	PP,CH		;YES--SAVE THE CHARACTER
	PUSHJ	PP,EOLCHK
	MOVE	CH,0(PP)

CPYPN1:	PUSHJ	PP,PUTCPY
	POP	PP,CH
CPYPN2:	MOVEM	CP,SAVCP1
	MOVEM	LN,SAVLN1
	POPJ	PP,

CPYPN3:	LDB	TE,CPYBHO+1
	CAIE	TE," "
	JRST	CPYPN4
	DPB	CH,CPYBHO+1
	MOVE	CP,SAVECP
	MOVE	LN,SAVELN
	JRST	CPYPN2

CPYPN4:	PUSHJ	PP,PUTCPY
	JRST	CPYPN2


;IF THERE WAS A TERMINATING PUNCTUATION, PUT IT OUT.
;OTHERWISE PUT OUT A SPACE.

CHKPUN:	TSWT	FCOPY		;ARE WE COPYING?
	JRST	CPYN3		;NO--FORGET IT
	MOVE	CH,SAVECH
	SETZM	SAVECH
	CAIN	CH,"."
	JRST	PUTCPY

	CAIE	CH,";"
	CAIN	CH,","
	JRST	PUTCPY

	LDB	CH,CPYBHO+1	;WAS LAST OUTPUT CHARACTER
	CAIN	CH,"("		;  A LEFT-PAREN?
	POPJ	PP,		;YES--FORGET IT

	MOVEI	CH," "
	JRST	PUTCPY
;GET READY TO COPY SOMETHING TO CPYFIL.
;IF AT END-OF-LINE, SKIP TO START OF NEXT LINE.
;BE SURE THAT WORD IS AT LEAST AS FAR INTO LISTING AS IT WAS IN SOURCE LINE.

EOLCHK:	CAIG	CP,ENDCL	;AT END OF LISTING LINE?
	JRST	EOLCH1		;NO

EOLCH0:	MOVEI	CH,12		;YES--START NEW LINE
	PUSHJ	PP,PUTCPY

	MOVEI	CH,$TAB
	PUSHJ	PP,PUTCPY
	MOVEI	CH,$TAB
	PUSHJ	PP,PUTCPY

	MOVEI	CP,^D19
	TSWT	FNOCPY;
	MOVEM	CP,SAVECP
	SETOM	SKPCPY
	JRST	EOLCH3

EOLCH1:	MOVE	CH,SAVECP
	ADDI	CH,1
	CAMGE	CH,INPTST
	SKIPE	SKPCPY
	JRST	EOLCH2

	MOVEI	CH," "
	PUSHJ	PP,PUTCPY
	JRST	EOLCH1

EOLCH2:	LDB	CH,CPYBHO+1
	CAIE	CH," "
	CAIN	CH,$TAB
	JRST	EOLCH3

	CAIN	CH,"("
	JRST	EOLCH3

	MOVEI	CH," "
	PUSHJ	PP,PUTCPY

EOLCH3:	MOVE	CH,SAVECP
	ADDI	CH,1
	MOVEM	CH,WORDCP
	MOVE	LN,SAVELN
	MOVEM	LN,WORDLN

	POPJ	PP,
;GET A CHARACTER FOR A WORD OR NUMERIC LITERAL.

;IF A SPACE IS SEEN, THE REMAINDER OF THE LINE IS SCANNED. IF NOTHING
;	IS LEFT ON THE LINE, THE CONTINUATION COLUMN OF THE NEXT LINE
;	IS CHECKED.  IF HYPHEN, THAT LINE IS SCANNED UNTIL A NON-BLANK
;	CHARACTER IS FOUND.

;IF THERE IS NO CONTINUATION, AND A SPACE IS FOUND, THE SPACE IS RETURNED.
;IF THERE IS A CONTINUATION, THE FIRST NON-SPACE ON THE LINE IS RETURNED.

GETKAR:	PUSHJ	PP,GETK9

	CAIN	TE,7		;IS IT CONTINUATION COLUMN?
	JRST	GETK2		;YES

	CAIE	CH," "
	JRST	GETK4A

	MOVEM	LN,SAVBLN	;SAVE LOCATION OF THE BLANK
	MOVEM	CP,SAVBCP

GETK1:	PUSHJ	PP,GETK9	;YES--GET NEXT CHARACTER
	CAIN	TE,7		;IS IT CONTINUATION COLUMN?
	JRST	GETK2		;YES

	CAIN	CH," "		;NO--IS IT SPACE?
	JRST	GETK1		;YES--CONTINUE SCANNING

	SWON	FREGCH;		;NO--SET "REGET CHARACTER" FLAG
	MOVEI	CH," "		;RETURN A SPACE
	POPJ	PP,
;GET A CHARACTER FOR WORD OR NUMERIC LITERAL (CONT'D).

;CONTINUATION COLUMN SEEN.

GETK2:	CAIN	CH," "		;IS CONTINUATION COLUMN  A SPACE?
	JRST	GETK6		;YES--RETURN

GETK3:	CAIE	CH,"-"		;IS CONTINUATION COLUMN A HYPHEN?
	JRST	GETK5		;NO--ERROR

GETK4:	PUSHJ	PP,GETK9	;YES--SCAN THIS NEW LINE
	CAIN	TE,7		;CONTINUATION COLUMN AGAIN?
	JRST	GETK2		;YES

	CAIN	CH," "		;NO--STIL SPACE?
	JRST	GETK4		;YES--LOOP
	SETZM	SAVBLN		;NO--CLEAR "SAVBLN"

GETK4A:	SETZM	NOCONT
	POPJ	PP,		;RETURN

GETK5:	MOVEI	DW,BADCC7	;CONTINUATION NOT SPACE OR HYPHEN
	PUSHJ	PP,FATAL
	MOVEI	CH," "
GETK6:	SETOM	NOCONT
	POPJ	PP,


GETK9:	PUSHJ	PP,GETCH
	MOVE	TE,SRCCOL	;REMEMBER SOURCE COLUMN
	TSWF	FEOF;
	POPJ	PP,

	CAIN	CH,$TAB
	MOVEI	CH," "
	CAIL	CH,140
	SUBI	CH,40
	POPJ	PP,
;GET THE NEXT CHARACTER FROM THE SOURCE LINE, EVEN IF SPACE.

GETCH:	MOVE	CP,SAVECP	;RESET CHARACTER POSITION
	TSWF	FREGCH;		;RE-GET SAME CHARACTER?
	JRST	GETCH5		;YES

	AOS	CH,INPTCP

	TSWF	FSEQ;		;SEQUENCED INPUT?
	JRST	GETCH3		;YES

GETCH0:	CAIGE	CP,CPMAXN	;NO--TOO MANY CHARACTERS?
	JRST	GETCH5		;NO

	MOVE	LN,SAVELN
	MOVEI	CP,7
	MOVEI	DW,LNGLIN
	TSWT	FNOCPY		;NO ERROR IF NOT LISTING
	PUSHJ	PP,FATAL
	MOVE	CP,SAVECP

;END OF SOURCE LINE--TOO MANY CHARACTERS
GETCH1:	PUSHJ	PP,GETSRC	;GET NEXT ONE
	CAIE	CH,$LF		;END OF LINE?
	CAIN	CH,$FF
	JRST	FINLIN		;YES

	JRST	GETCH1		;NO--LOOP

;SEQUENCED INPUT
GETCH3:	TSWF	FRLIB		;READING LIBRARY?
	JRST	GETCH0		;YES

	CAIGE	CH,^D73		;NO--COLUMN 72 BEEN PASSED YET?
	JRST	GETCH5		;NO

GETCH8:	PUSHJ	PP,GETSRC	;YES, IGNORE REST OF LINE
	CAIE	CH,$LF
	CAIN	CH,$FF
	JRST	FINLIN
	JRST	GETCH8

;STILL SOME SOURCE ON THIS LINE
GETCH5:	PUSHJ	PP,GETSRC	;GET NEXT CHARACTER
	CAIE	CH,$LF		;END OF LINE?
	CAIN	CH,$FF
	JRST	FINLIN		;YES

GETCH6:	MOVE	LN,INPTCP	;SAVE SOURCE COLUMN
	MOVEM	LN,SRCCOL
	CAIN	CH,$TAB		;IS IT A TAB?
	JRST	GTCH10		;YES

;CHARACTER OK--LEAVE
GETCH9:	MOVE	LN,SAVELN	;GET CURRENT LINE
	POPJ	PP,		;LEAVE
;INPUT CHARACTER WAS TAB--BUMP INPTCP

GTCH10:	MOVE	CH,INPTCP
	ADDI	CH,4
	ANDCMI	CH,7
	ADDI	CH,3
	MOVEM	CH,INPTCP

	MOVEI	CH,$TAB
	JRST	GETCH9


;END OF SOURCE LINE--PRINTER CONTROL HAS BEEN SEEN

FINLIN:	TSWFZ	FNOCPY		;CLR FNOCPY SWITCH
	SWON	FNCOFF		;  BUT REMEMBER IF IT WAS TURNED OFF
FINLN0:	MOVEM	CH,EOLKAR
	PUSHJ	PP,PUTCIF
	SETZM	SKPCPY

	TSWT	FRLIB		;READING LIBRARY?
	TSWT	FEOF		;NO--END OF INPUT?
	JRST	GETSEQ		;NO--START NEW LINE

;END OF SOURCE

	TSWFZ	FNCOFF		;WAS FNOCPY TURNED OFF ABOVE?
	SWON	FNOCPY		;YES, TURN IT BACK ON
	MOVEI	CH," "		;RETURN A SPACE
	MOVEI	CP,7		;	FOR COLUMN 7

	AOS	SAVELN		;KICK UP LINE COUNT
	JRST	GETCH9		;LEAVE
;PARAGRAPH HAS BEEN DELETED AND FIRST CHARACTER OF NEXT PARAGRAPH NAME SEEN.
;PUT OUT THAT FIRST CHARACTER.

FINSKP:	SWOFF	FNOCPY;
	MOVEI	CH,$LF
	PUSHJ	PP,PUTCPY
	TSWF	FEOF;
	POPJ	PP,

FINSK3:	MOVEI	CH,1(CP)
	CAML	CH,INPTCP
	JRST	FINSK4
	MOVEI	CH," "
	PUSHJ	PP,PUTCPY
	JRST	FINSK3

FINSK4:	SWON	FREGCH;		;TURN ON "REGET CHARACTER"
	LDB	CH,SRCBH+1	;PUT OUT LAST CHARACTER
	JRST	PUTCPY		;   AND RETURN
;GET THE VERY FIRST CHARACTER FROM THE SOURCE FILE

GETFCH:	SETZM	SAVELN		;SET LINE COUNT TO ZERO
	SETOM	NOCONT
	SETZM	CPYCNT
	MOVEI	CH,$FF
	PUSHJ	PP,PUTFEL
	SETZM	EOLKAR
	SETZM	SEQIN
;START A NEW SOURCE LINE

GETSEQ:	TSWF	FRLIB		;ARE WE READING LIBRARY?
	JRST	GETSQ2		;YES

	SETZM	SAVECP		;STARTING AT COLUMN 1

	TSWF	FSEQ		;SEQUENCED INPUT?
	JRST	GETSQ7		;YES

	PUSHJ	PP,GETSRC	;GET A CHARACTER
	CAIE	CH,$LF		;END OF
	CAIN	CH,$FF		;  LINE?
	JRST	GETSQ4		;YES

	MOVE	TD,@SRCBH+1	;NO--IS IT
	TRNN	TD,1		;  A SEQUENCE NUMBER?
	JRST	GETSQ9		;NO

	MOVEI	TD,6		;YES
	JRST	GETSQ8

GETSQ2:	PUSHJ	PP,GETSRC	;GET COLUMN 7
GTSQ2A:	CAIE	CH,"-"		;CONTINUATION?
	JRST	GETSQ3		;NO
	SKIPN	NOCONT		;YES--ARE THEY LEGAL?
	JRST	GETSQ5		;YES

	MOVE	LN,SAVELN	;NO--
	MOVEI	DW,^D279	;  PUT OUT
	PUSHJ	PP,FATAL	;  DIAG
	MOVEI	CH," "		;REPLACE WITH SPACE

GETSQ3:	CAIE	CH," "		;SPACE OR
	CAIN	CH,$TAB		;  TAB?
	JRST	GETSQ5		;YES
	CAIN	CH,"*"		;NO--COMMENT?
	JRST	GTSQ10		;YES

GETSQ4:	TSWT	FEOF!FECOPY	;END OF FILE OR END OF COPY?
	SWON	FREGCH		;NO--REGET LATER
	MOVEI	CH," "		;REPLACE WITH SPACE

GETSQ5:	MOVEI	CP,7		;RESET CP AND
	MOVEM	CP,INPTCP	;  INPUT COLUMN NUMBER
	TSWTZ	FNCOFF		;WAS FNOCPY TURNED OFF AT FINLIN?
	JRST	GETCH6		;NO
	SWON	FNOCPY		;YES, TURN IT BACK ON NOW
	SWON	FNEEDS		;ALSO REQUEST A DUMMY PERIOD
	JRST	GETCH6		;LEAVE
;START A NEW SOURCE LINE (CONT'D)

;SEQUENCED INPUT -- COPY COLUMNS 1-6, GET COLUMN 7

GETSQ7:	MOVEI	TD,7

GETSQ8:	SETOM	SEQIN

GTSQ8A:	PUSHJ	PP,GETSRC
	CAIE	CH,$LF
	CAIN	CH,$FF
	JRST	GETSQ4

	SOJG	TD,GTSQ8A
	JRST	GTSQ2A

;PUT OUT 6 SPACES IN PLACE OF SEQUENCE NUMBER

GETSQ9:	TSWF	FCOPY		;IGNORE IF COPYING
	JRST	GTSQ2A

	MOVE	TE,CH		;SAVE CH
	MOVEI	CH," "		;REPLACE THAT SOURCE CHARACTER
	DPB	CH,CPYBHO+1	;  WITH SPACE
	MOVEI	TD,5
	PUSHJ	PP,PUTCPY
	SOJG	TD,.-1

	MOVE	CH,TE		;RESTORE CH
	PUSHJ	PP,PUTCPY
	JRST	GTSQ2A

;"*" IN COLUMN 7

GTSQ10:	SETOM	NOCONT
	TSWT	FRLIB		;LIBRARY MODE?
	JRST	GTSQ12		;NO
	PUSH	PP,CH
	MOVEI	CH,12
	PUSHJ	PP,PUTCPY
	POP	PP,CH

GTSQ11:	TSWF	FRLIB		;LIBRARY MODE?
	PUSHJ	PP,PUTCPY	;YES, COMMENT TO LISTING
GTSQ12:	PUSHJ	PP,GETSRC
	CAIE	CH,$LF
	CAIN	CH,$FF
	JRST	GETSQ4
	JRST	GTSQ11
;GET A CHARACTER FROM THE SOURCE FILE BUFFER

GETSRC:	TSWF	FECOPY;
	JRST	GTBLNK

	TSWF	FRLIB		;DO WE READ LIBFIL?
	JRST	GETLIB

	TSWF	FEOF;		;END OF FILE?
	JRST	GTBLNK		;YES

	TSWFZ	FREGCH;		;REGET PREVIOUS CHARACTER?
	JRST	REGETS		;YES

	SOSG	SRCBH+2
	JRST	GETSR3

GETSR0:	ILDB	CH,SRCBH+1

GETSL:	CAIGE	CH,40		;CONTROL CHARACTER?
	JRST	GETSR2		;YES
	CAIL	CH,140		;NO--SIXBIT CHARACTER?
	JRST	GETSR1
	JRST	PUTCIF


;REGET PREVIOUS CHARACTER

REGETS:	LDB	CH,SRCBH+1
	POPJ	PP,


;RETURN A LINE-FEED

GTBLNK:	MOVEI	CH,$LF
	POPJ	PP,
;SPECIAL CHARACTER PROCESSING

;CHARACTERS ABOVE CODE 137

GETSR1:	CAIG	CH,172		;LOWER CASE?
	CAIGE	CH,141
	JRST	GTSR1B		;NO
	JRST	PUTCIF

GTSR1B:	PUSH	PP,CH		;SAVE CHARACTER
	MOVEI	CH," "		;PUT SPACE IN CPYFIL
GTSR1D:	PUSHJ	PP,PUTCIF
	POP	PP,CH		;RESTORE CHARACTER
	POPJ	PP,		;RETURN
;SPECIAL CHARACTER PROCESSING  (CONT'D)

;CHARACTERS BELOW CODE 040

GETSR2:	JUMPE	CH,GETSRC	;IGNORE NULLS
	CAIE	CH,15		;	AND CARRIAGE-RETURNS
	CAIN	CH,32		;	AND END-FILES
	JRST	GETSRC

	CAIE	CH,$TAB		;TAB?
	JRST	GTSR2B		;NO

	MOVEI	CH," "		;YES--REPLACE WITH SPACE
	PUSHJ	PP,GTSR2D
	MOVEI	CH,$TAB

	JRST	PUTCIF

GTSR2B:	CAIE	CH,$LF		;LINE-FEED?
	CAIN	CH,$FF		;NO--FORM-FEED?
	POPJ	PP,		;YES--RETURN

	CAIG	CH,24		;NO--OTHER PRINTER CONTROL?
	CAIGE	CH,20
	CAIN	CH,13
	JRST	GTSR2C
	PUSH	PP,CH		;YES
	MOVEI	CH,"^"
	PUSHJ	PP,PUTCIF
	MOVE	CH,(PP)
	ADDI	CH,100
	JRST	GTSR1D

GTSR2C:	MOVEI	CH,$LF		;YES--FORCE LINE-FEED
GTSR2D:	TSWF	FRLIB;
	JRST	GTSR2E
	DPB	CH,SRCBH+1	;RESTORE FOR POSSIBLE REGET
	POPJ	PP,		;RETURN
GTSR2E:	DPB	CH,LIBBH+1
	POPJ	PP,
;NEW BUFFER REQUIRED

GETSR3:	IN	SRC,		;FILL BUFFER
	JRST	GETSR0

	GETSTS	SRC,CH		;ERROR--SEE IF END-FILE
	TRNE	CH,$ERAS
	JRST	GETSR4		;NO

	RELEASE	SRC,		;RELEASE THIS DEVICE

	PUSHJ	PP,STINFL	;SET UP NEXT SOURCE FILE
	SKIPE	SRCDEV		;WAS THERE ANY?
	JRST	GETSR3		;YES

;NO MORE SOURCE

GTSR3A:	SWON	FEOF;		;SET "END-FILE" SWITCH
	RELEASE	LIB,
	SKIPE	CREFSW
	CLOSE	CRF,
	MOVEI	CH,7
	MOVEM	CH,SRCCOL
	MOVEI	CH,$LF
	POPJ	PP,


;ERROR ON SOURCE DEVICE

GETSR4:	MOVEI	CH,SRCDEV
	JRST	DEVDED
;SYNTAX SCANNER HAS DETECTED AN ERROR AND IS SKIPPING PAST DATA.
;GET NEXT SOURCE CHARACTER.

SKPSRC:	TSWFZ	FGTPER;
	JRST	SKPSR6
	TSWFZ	FGTMIN;
	JRST	SKPSR8

	TSWF	FCOPY		;ARE WE COPYING?
	JRST	SKPSR3		;YES

	PUSHJ	PP,GETCH	;NO--GET A CHARACTER

SKPSR1:	CAIN	CH,$TAB		;CHANGE TAB TO SPACE
	MOVEI	CH," "
	CAIN	CH," "
	SWOFF	FNEEDS;

SKPSR2:	TSWF	FNOCPY;
	MOVE	CP,SRCCOL
	POPJ	PP,

SKPSR3:	MOVE	CH,INPTCP
	TSWF	FREGCH;
	SUBI	CH,1
	CAMLE	CH,SAVECP
	JRST	SKPSR9

	TSWFZ	FECOPY		;SHOULD WE CLEAN UP LIBRARY?
	JRST	SKPSR5		;YES

	SKIPE	CH,EOLKAR	;NO--ANY LINE TO TERMINATE?
	PUSHJ	PP,PUTCPY	;YES--DO SO

	PUSHJ	PP,GETCH	;GET A CHARACTER

	TSWFZ	FECOPY		;DONE WITH LIBRARY NOW?
	JRST	SKPSR5		;YES
	SKIPN	EOLKAR
	JRST	SKPSR4

	PUSH	PP,CH
	MOVE	CH,EOLKAR
	PUSHJ	PP,PUTCPY
	POP	PP,CH

SKPSR4:	PUSHJ	PP,PUTCPY
	JRST	SKPSR1

SKPSR5:	PUSHJ	PP,ENDCPY
	JRST	SKPSRC

SKPSR6:	MOVEI	CH,"."
	JRST	SKPSR2

SKPSR8:	MOVEI	CH,"-"
	TSWF	FCOPY;
	PUSHJ	PP,PUTCPY
	JRST	SKPSR2

SKPSR9:	MOVEI	CH," "
	PUSHJ	PP,PUTCPY
	JRST	SKPSR3
;GET A CHARACTER FROM THE LIBRARY FILE

GETLIB:	TSWFZ	FREGCH		;REGET A CHARACTER?
	JRST	REGETL		;YES

GETLB1:	SOSG	LIBBH+2		;NO--GET NEXT CHARACTER
	JRST	GETLB4

GETLB2:	ILDB	CH,LIBBH+1
	MOVE	CH,@LIBBH+1	;IS THIS A LINE-NUMBER WORD?
	TRNN	CH,1
	JRST	REGETL		;NO

	MOVE	CH,@LIBBH+1	;YES--END OF PROGRAM?
	JUMPL	CH,GETLB5

	MOVNI	CH,5		;NO--JUMP OVER 5 CHARACTERS
	ADDB	CH,LIBBH+2
	JUMPLE	CH,GETLB1	;JUMP IF BUFFER NOW EMPTY
	AOS	LIBBH+1		;IT ISN'T--BUMP BYTE POINTER

REGETL:	LDB	CH,LIBBH+1
	JUMPE	CH,GETLB1
	JRST	GETSL

GETLB4:	IN	LIB,		;GET ANOTHER BUFFER FULL
	JRST	GETLB2

	GETSTS	LIB,CH		;END-FILE?
	TRNE	CH,$ERAS
	JRST	GTLB10		;NO--TROUBLE

GETLB5:	SWON	FECOPY		;SET "CLEAN UP COPY" INDICATOR
	SWOFF	FRLIB;
	JRST	GTBLNK

GTLB10:	MOVEI	CH,LIBDEV	;ERROR ON LIBRARY DEVICE
	JRST	DEVDED
;GET A LIBRARY PROGRAM.

;ENTER WITH CP SET TO PERIOD TERMINATING THE COPY CLAUSE, 
;	CPYW2 POINTING TO "COPY", AND PROGRAM-NAME IN LIBNAM.

SETLIB:	SETZM	REGKAR
	TSWTZ	FREGCH		;CHARACTER TO REGET?
	JRST	SETLB2		;NO

	LDB	TE,SRCBH+1	;YES--PICK IT UP
	MOVEM	TE,REGKAR
	MOVSI	TE,7B<^D18+5>
	ADDM	TE,CPYBHO+1
	AOS	CPYBHO+2

SETLB2:	MOVE	CP,SAVECP
	MOVEM	CP,CPYCP	;SAVE CHARACTER POSITION
	SKIPN	LIBDEV		;IS THERE A LIBRARY FILE?
	JRST	STLB20		;NO--WE LOSE

	USETI	LIB,1		;GET READY TO READ ROUGH TABLE
	IN	LIB,		;READ ROUGH TABLE
	SKIPA
	JRST	GTLB10		;ERROR

	MOVE	TC,LIBNAM	;PICK UP LIBRARY ROUTINE NAME
	MOVE	TB,LIBNAM+1
	LSHC	TC,-6
	TRZ	TB,-1
	LSH	TB,-1

	MOVE	TA,LIBBH+1	;SET TA TO POINT TO ROUGH-TABLE
	MOVE	TE,1(TA)	;COMPARE LIBNAM TO FIRST ENTRY
	MOVE	TD,2(TA)
	PUSHJ	PP,SETLBC
	SKIPA			;EQUAL
	JRST	SETLB4		;GREATER
	MOVEI	TE,2		;LESS
	JRST	SETL5A


;SEARCH  ROUGH-TABLE

SETLB4:	ADDI	TA,2
	MOVE	TE,1(TA)
	MOVE	TD,2(TA)
	PUSHJ	PP,SETLBC
	JRST	SETLB5
	JRST	SETLB4
	SKIPA	TE,0(TA)	;ITEM FOUND--GET ADDRESS

SETLB5:	MOVE	TE,2(TA)
	TLZ	TE,777700
	LSH	TE,-7
	ADDI	TE,1
;GET A LIBRARY PROGRAM  (CONT'D).

;AN APPROPRIATE ROUGH-TABLE ENTRY HAS BEEN LOCATED.

SETL5A:	USETI	LIB,(TE)	;READ IN FINE-TABLE
	IN	LIB,
	SKIPA
	JRST	GTLB10		;ERROR

	MOVE	TA,LIBBH+1	;SET UP AN IOWD TO FINE TABLE
	HRLI	TA,-^D64

;SEARCH THE FINE TABLE

SETLB6:	MOVE	TE,1(TA)
	MOVE	TD,2(TA)
	PUSHJ	PP,SETLBC
	JRST	SETLB8
	AOJA	TA,SETLB7
	JRST	STLB21

SETLB7:	AOBJN	TA,SETLB6
	JRST	STLB21

;PROGRAM FOUND

SETLB8:	MOVE	TE,2(TA)
	TLZ	TE,777700
	LSHC	TE,-7
	ADDI	TE,1
	USETI	LIB,(TE)
	IN	LIB,
	SKIPA
	JRST	GTLB10

	AOS	LIBBH+2
	MOVEI	TE,0
	LSHC	TE,7
	ADDM	TE,LIBBH+1
	IMULI	TE,5
	MOVNS	TE
	ADDM	TE,LIBBH+2

	SWON	FCOPY!FRLIB;

	MOVEI	CH,$LF
	PUSHJ	PP,PUTCPY
	MOVEI	CH,6
	MOVEM	CH,INPTCP

	POPJ	PP,
;GET A LIBRARY PROGRAM (CONT'D).

;ERRORS

;NO LIBRARY FILE

STLB20:	TTCALL	3,[ASCIZ "%LIBRARY FILE NOT FOUND - CONTINUING
"]
	MOVEI	DW,^D75
	JRST	STLB29

;PROGRAM NOT FOUND

STLB21:	TTCALL	3,[ASCIZ "%LIBRARY ROUTINE "]
	MOVE	TA,LIBNAM
	PUSHJ	PP,SIXOUT##
	MOVE	TA,LIBNAM+1
	PUSHJ	PP,SIXOUT
	TTCALL	3,[ASCIZ " NOT FOUND - CONTINUING
"]
	MOVEI	DW,^D74

STLB29:	MOVE	CP,CPYW2
	LDB	LN,[POINT 13,CPYW2,28]
	JRST	FATAL


;COMPARE LIBNAM AGAINST CONTENTS OF TE & TD.
;IF EQUAL, RETURN TO CALL + 1.
;IF LIBNAM > TE,TD RETURN TO CALL+2.
;IF LIBNAM < TE,TD RETURN TO CALL+3.

SETLBC:	LSHC	TE,-6
	TRZ	TD,-1
	LSH	TD,-1

	CAME	TC,TE
	JRST	SETLC1
	CAMN	TB,TD
	POPJ	PP,

	AOS	(PP)
	CAMG	TB,TD
	AOS	(PP)
	POPJ	PP,

SETLC1:	AOS	(PP)
	CAMG	TC,TE
	AOS	(PP)
	POPJ	PP,
;SPACE OVER UNTIL PREVIOUS SOURCE CHARACTER POSITION REACHED

ENDCPY:	MOVEI	CH,$LF
ENDCP1:	PUSHJ	PP,PUTCPY
	MOVEI	CH,1(CP)
	CAML	CH,CPYCP
	JRST	ENDCP2
	MOVEI	CH," "
	JRST	ENDCP1


ENDCP2:	SKIPE	CH,REGKAR
	SWONS	FREGCH;
	MOVEI	CH," "
	PUSHJ	PP,PUTCPY

;CLEAR OUT CPYTAB AFTER END-OF-PROGRAM IN CPYFIL.

CLRCPY:	MOVE	TE,CPYLOC
	CAMN	TE,CPYNXT
	JRST	CLCPY8

CLCPY1:	HLRZ	TD,1(TE)	;GET NAMTAB LINK FOR REPLACED ITEM
	ADD	TD,NAMLOC
	MOVEI	CH,0		;CLEAR CPYTAB LINK IN NAMTAB ENTRY
	DPB	CH,[POINT 15,(TD),17]

	HRRZ	TD,1(TE)	;GET SUB-ITEM COUNT
	AOBJP	TE,CLCPY9	;SKIP ONE WORD

CLCPY2:	MOVE	CH,1(TE)	;IS ITEM A LITERAL?
	TLNN	CH,GWLIT
	JRST	CLCPY3		;NO

	HRLS	CH		;YES--BUMP TE BY NUMBER OF WORDS
	ADD	TE,CH

CLCPY3:	AOBJP	TE,CLCPY9	;SKIP TO NEXT ENTRY
	SOJG	TD,CLCPY2	;IF MORE SUB-ITEMS, LOOP

	CAME	TE,CPYNXT	;CPYTAB NOW EMPTY?
	JRST	CLCPY1		;NO--LOOP UNTIL IT IS

	MOVE	TE,CPYLOC	;YES--RESET CPYNXT
	MOVEM	TE,CPYNXT

CLCPY8:	SWOFF	FCOPY!FECOPY!FRLIB	;TURN OFF "WE ARE COPYING"
	POPJ	PP,		;RETURN

CLCPY9:	TTCALL	3,[ASCIZ "CPYTAB IMPROPERLY SET UP
"]
	JRST	KILL
;CHARACTERS USED

	$TAB==11
	$LF==12
	$FF==14
	$CR==15
	$ALT==33
	$QUOTE==42

;DIAGNOSTICS

	BADCC7==^D73	;COLUMN 7 IS NEITHER SPACE, TAB NOR HYPHEN
	BADKAR==^D57	;IMPROPER CHARACTER
	BADLIT==^D76	;IMPROPER CHARACTER IN NUMERIC LITERAL
	BADPUN==^D79	;IMPROPER PUNCTUATION--NOT FOLLOWED BY SPACE
	SPACED==^D78	;IMPROPER PUNCTUATION--PRECEDED BY SPACE
	NEEDSP==^D81	;IMPROPER PUNCTUATION--PRECEDING SPACE REQUIRED
	BIGWRD==^D55	;WORD > 30 CHARACTERS
	BIGLIT==^D56	;LITERAL TOO LONG
	CONLIT==^D71	;IMPROPER CONTINUATION OF LITERAL
	NOQUOT==^D70	;NO QUOTE ON PRECEDING LINE
	TWOPER==^D77	;MORE THAN ONE DECIMAL POINT
	ENDHYF==^D54	;WORD ENDS WITH A HYPHEN
	LNGLIN==^D82	;LINE TOO LONG


	EXTERNAL CPYLOC,CPYNXT,CPYBUF,FILLOC,FILNXT
	EXTERNAL SAVECP,SAVELN,SAVECH,WORDCP,WORDLN,SAVCP1,SAVLN1,SAVEWD,ITEMCT
	EXTERNAL CPYST,CPYCNT,EOLKAR,ENDCL,SKPCPY,CPYCP,CPYW2,LIBBH,LIBNAM
	EXTERNAL BLNKLN,BLNKCP,SAVBLN,SAVBCP,SRCCOL
	EXTERNAL CPYBHO,CPYBLK,CPYDEV,SRCBH,SRCBLK,SRCDEV,TTYBHI
	EXTERNAL CRFBHO,CRFDEV,CREFSW,WASERC
	EXTERNAL PLUSWD,MINWD,MULWD
	EXTERNAL LPARWD,RPARWD,EXPWD,PERWD,COMWD,SEMIWD,ENDIT
	EXTERNAL NAMWRD,LITVAL,NAMLOC,NAMVAL,INPTCP,INPTST
	EXTERNAL GWNAMP,GWVAL,GWLN,GWCP,CPMAXN,SEQIN,TERMQ
	EXTERNAL SRCEND,SRCDEV,LIBDEV,DEVSIZ,DEVBUF,DEVSW,PUNPTR,NOCONT,REGKAR


	END
    D L‰