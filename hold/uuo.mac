TITLE	UUO FOR LIBOL 5(223)		
SUBTTL	UUO HANDLER FOR COBOL OP-SYS	AL BLACKINGTON/CAM

;COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORP, MAYNARD, MA.

	HISEG


ENTRY UUO.
EXTERNAL JOBUUO,UOCAL.
EXTERNAL UUO1., UUO2., UUO3., UUO4., UUO5., UUO6.


;THIS ROUTINE IS CALLED BY A PUSHJ PP, UUO.  WHEN OP-CODES 001-037 ARE USED

; THE OP-CODE AND AC FIELDS OF THE UUO ARE USED TO DETERMINE WHICH
;	SUBROUTINE IS DESIRED.  THAT SUBROUTINE IS THEN ENTERED.

UUO.:	MOVE	PA,JOBUUO	;PICK UP THE UUO
	HRRZ	TA,(PP)		;GET THE UUO CALL RETURN ADDRESS
	MOVEM	TA,UOCAL.	;SAVE IT IN UOCAL.
	LDB	TA,UUO		;GET THE OP-CODE FIELD

	CAIL	TA,HI.OP	;IS IT ONE OF THE HIGHER UUOS?
	JRST	@UUO6.-HI.OP(TA) ;YES--DISPATCH

	CAIG	TA,1		;IS IT "OPEN" OR "CLOSE"?
	TRZA	TB,-1		;YES--CLEAR RH
	LDB	TB,UUOAC	;NO--GET FIRST 3 BITS OF AC FIELD
	TLNE	PA,40		;IS LOW BIT OF AC FIELD ZERO?
	SKIPA	TA,@UUOTAB-1(TA) ;NO--USE ADDRESS IN RH OF A TABLE ENTRY
	HLRZ	TA,@UUOTAB-1(TA) ;YES--USE ADDRESS IN LH OF A TABLE ENTRY
	JRST	(TA)		;GO TO THE ROUTINE
; MISCELLANEOUS CONSTANTS

UUO:	POINT 9,PA,8	;THE OP-CODE FIELD OF THE UUO
UUOAC:	POINT 3,PA,11	;THE 3 HI-ORDER BITS OF THE AC FIELD OF THE UUO


PP=17			;PUSH-DOWN POINTER (SET BEFORE ENTRY)
PA=16			;HOLDS THE UUO
TA=15			;TEMPORARY
TB=14			;TEMPORARY

HI.OP==6		;FIRST OP-CODE WHICH PASSES ON AC-FIELD


; A TABLE OF POINTERS TO DISPATCH TABLES

UUOTAB:	Z	UUO1.(TB)
	Z	UUO2.(TB)
	Z	UUO3.(TB)
	Z	UUO4.(TB)
	Z	UUO5.(TB)


	END
