C... EL CHEAPO UNITAB
C
C... UNITAB LINK   ANTHONY GRAY
C
C... START OF COMMAND VERSION STANDARD COMMON .....................
        COMMON KPROJ,KPROG,KFILE,KACBLK,TCPU,ITCCV,ITCCU,ITCCL,ITCCF,
     A    ITRPSW,ITYMTR,ITRPAC,IMKUP,
     1          ICLI,NWDS,NRESP,NMEN,LOCMEN,NWOM,LOCWOM,IWGT,LOCWGT,
     2          IFPTR,IFNAME,NPROJ,NPROG,IBSSW,IVSW,IBNO,IBAT,IUNISW,
     3          IBSVEC,MENWOM,IBTITL,IPRSW,IRCNT,IOPT,KVEH,NISS,IVNAME,
     4          IVMEAS,LOCISS,PX,IBFNAM,IPRGSW,JVNAME,ISRCE,ITABTI
      DOUBLE PRECISION IFPTR,IBFNAM
      DIMENSION KFILE(2),ITCCV(30),ITCCU(30),ITCCL(30),ITCCF(30),
     1 IFNAME(2),IBSVEC(430,0/6),MENWOM(2,0/6),IBTITL(15,0/6),
     2 IOPT(10),IVNAME(25),IVMEAS(25),LOCISS(250),PX(25),IBFNAM(6),
     3 JVNAME(25),ITABTI(15)
C... END OF COMMAND VERSION STANDARD COMMON ...................
C
C
C
C...IN THIS ECONOMY VERSION, ALL BASES ARE TABBED SIMULTANEOUSLY
C   WHETHER OR NOT THERE ARE ANY VEHICLES
C
C...ICELL=MAIN WORKING SPACE, ALLOCATED AS FOLLOWS:
C     VEHICLE POPPING POINTERS (NISS OF THEM)
C     SINGLES FOR THIS RESPONDENT (KVEH OF THEM)
C     RESPONSE COUNTS FOR THIS RESPONDENT, IF REQUESTED (KVEH)
C     PAIRS FOR THIS RESPONDENT, IF REQUESTED (KVEH*(KVEH+1))/2
C     SINGLES - BASE 1
C     RESPONSE COUNTS - BASE 1 (IF REQUESTED)
C     PAIRS - BASE 1 (IF REQUESTED)
C     SINGLES - BASE 2
C     .........
C     PAIRS - BASE IBNO (IF REQUESTED)
C     WEIGHTS
C     VEHICLE VECTORS (NISS VECTORS)
C
C
C...COMMON VARIABLES:
C   NWDS=# WORDS PER VECTOR IN DATA FILE
C   NRESP=# RESPINDENTS IN DATA FILE
C   NMEN=# MEN IN DATA FILE
C   LOCMEN=ORDINAL OF FIRST MALE RESPONDENT
C   NWOM=# WOMEN IN DATA FILE
C   LOCWOM=ORDINAL OF FIRST FEMALE RESPONDENT
C   IWGT=# BITS IN RESPONDENT WEIGHT
C   LOCWGT=FIRST WORD # OF WEIGHTS VECTOR IN DATA FILE
C   IFPTR=POINTER FILE NAME
C   IFNAME=DATA FILE NAME
C   IBNO=# BASES
C   IBSVEC=ARRAY OF BASE QUAL VECTORS
C   MENWOM(1,J)=# MALE RESPONDENTS IN BASE J
C   MENWOM(2,J)=# FEMALE RESPONDENTS IN BASE J
C   IBTITL=ARRAY OF BASE TITLES
C   ITABTI=TITLE FOR TABLE OUTPUT
C   IPRSW=1 IF PAIRS REQUESTED, =2 IF NOT
C   IRCNT=1 IF VEHICLE RESPONSE COUNTS REQUESTED, =2 IF NOT
C   IOPT=OPTIONS ARRAY - RELEVANCE FOR THIS PROGRAM IS:
C      IOPT(10)=0 FOR NO TABULAR OUTPUT
C              =1 FOR AUDIENCES ONLY
C              =2 FOR AUDS & COVERAGES
C              =3 FOR AUDS & COMPOSITIONS
C              =4 FOR AUDS, COVS & COMPS
C              =5 FOR AUDS & CUMES
C              =6 FOR AUDS, COVS & CUMES
C              =7 FOR AUDS, COMPS & CUMES
C              =8 FOR THE WORKS
C   KVEH=# VEHICLES
C   NISS=TOTAL # ISSUES ACROSS ALL VEHICLES
C   IVNAME=VEHICLE NAMES ARRAY
C   IVMEAS(J)=# ISSUES SURVEYED OF VEHICLE J
C   LOCISS=ARRAY OF POINTERS TO VEHICLE ISSUE VECTORS
C   PX=ADJUSTMENT FACTORS ARRAY
C   IBFNAM=DEMO NAMES ARRAY
C   JVNAME=ARRAY OF VEHICLE NAMES BEFORE ANY RENAMING
C   ISRCE=DATA SOURCE CODE
C
C
C
C...NON-COMMON VARIABLES:
C   IBPTR(J)=POPPING POINTER FOR BASE J
C   IBRC(J)=RESPONDENT COUNT, BASE J
C   IBPT(J)=PROJECTED TOTAL, BASE J
C   VADJ(J)=AUDIENCE ADJUSTMENT FACTOR FOR VEHICLE J
C   ISNADJ(J)=ADJUSTED SINGLE ISSUE AUDIENCE OF VEHICLE J
C   IWMEAS(J)=#ISSUES SURVEYED PER WEEK OF VEHICLE J
C   NAMRN(1,J)=NAME OF A REGIONAL EDITION
C   NAMRN(2,J)=NAME OF CORRESPONDING NATIONAL MAGAZINE
C   LINE, COV AND COMPBS ARE USED FOR TABULAR OUTPUT LINES
C   NAMERJ=ARRAY OF RIGHT-JUSTIFIED VEHICLE NAMES FOR TABULAR OUTPUT
C
C
      DIMENSION ICELL(10000),IBPTR(0/6),IBRC(0/6),IBPT(0/6),VADJ(25),
     1    ISNADJ(25),IDATE(2),IWMEAS(25),NAMRN(2,20),LINE(7),COV(7),
     2    COMPBS(7),NAMERJ(25)
      EQUIVALENCE (NAMERJ,ISNADJ)
      DOUBLE PRECISION IDPBLK
      DATA IDPBLK/'          '/
C
      DATA NREGED/12/
      DATA NAMRN(1,1),NAMRN(2,1)/'RII','RD'/
      DATA NAMRN(1,2),NAMRN(2,2)/'BHS','BHG'/
      DATA NAMRN(1,3),NAMRN(2,3)/'BHT','BHG'/
      DATA NAMRN(1,4),NAMRN(2,4)/'BHE','BHG'/
      DATA NAMRN(1,5),NAMRN(2,5)/'BHW','BHG'/
      DATA NAMRN(1,6),NAMRN(2,6)/'PSC','LHJ'/
      DATA NAMRN(1,7),NAMRN(2,7)/'I/RII','I/RD'/
      DATA NAMRN(1,8),NAMRN(2,8)/'I/BHS','I/BHG'/
      DATA NAMRN(1,9),NAMRN(2,9)/'I/BHT','I/BHG'/
      DATA NAMRN(1,10),NAMRN(2,10)/'I/BHE','I/BHG'/
      DATA NAMRN(1,11),NAMRN(2,11)/'I/BHW','I/BHG'/
      DATA NAMRN(1,12),NAMRN(2,12)/'I/PSC','I/LHJ'/
C
      NCELLS=10000
      CALL ACCT (4)
      ITCCL(17)=185
      ITCCL(18)=186
      ITCCL(19)=187
      CALL DATE (IDATE)
      CALL TIME (ITIME)
      TYPE 8002,IDATE,ITIME
 8002 FORMAT(/' TELMAR UNITAB SYSTEM',3X,2A5,1X,A5)
      TYPE 8005
 8005 FORMAT(//' ---UNIVERSE---')
      LENWT=IWGT
      IWTDIV=10
      IF (ISRCE.EQ.'ORC')   IWTDIV=1
      GO TO 150
  100 TYPE 8010,IERR,LOCERR
 8010 FORMAT('0FILE ERROR TYPE',I3,' AT LOC.',I4/' PLEASE CALL TELMAR')
      CALL ACCT (3)
      STOP
C...TYPE UNIVERSE TITLE
  150 CALL PRITIT (IBTITL(1,0))
C...OPEN DATA FILE
      IDPROJ="3374
      IDPROG="41720
      CALL OPEN (12,IFNAME,IDPROJ,IDPROG,1,IERR)
      LOCERR=35
      IF (IERR.NE.0)   GO TO 100
C
  400 CONTINUE
  460 IPRYN=IPRSW
      IF (IPRYN.NE.1)   IPRYN=0
      IVRESC=IRCNT
      IF (IVRESC.NE.1)   IVRESC=0
      IF (KVEH.GE.1)   GO TO 480
      IPWT=1
      INCORE=NCELLS/LENWT
      GO TO 497
C...SET UP POINTERS WITHIN ICELL
C...POINTER TO FIRST VEHICLE POPPING POINTER-1
  480 IPPOP=0
C...POINTER TO FIRST SINGLE-1
      IPSING=IPPOP+NISS
      NPAIRS=IPRYN*(KVEH*(KVEH+1))/2
C...# WORDS PER BASE FOR SINGLES, RESPONSE COUNTS & PAIRS
      NWPERB=KVEH+IVRESC*KVEH+NPAIRS
      NWACC=(IBNO+1)*NWPERB
C...POINTER TO FIRST WORD OF WEIGHTS VECTOR
      IPWT=IPSING+NWACC+1
      NUMVEC=NISS+LENWT
      INCORE=(NCELLS+1-IPWT)/NUMVEC
C...POINTER TO FIRST WORD OF FIRST VEHICLE VECTOR
      IPVEH=IPWT+INCORE*LENWT
      NOTV=1
      DO 490 J=1,KVEH
      IWMEAS(J)=IVMEAS(J)/2
      IF (IVMEAS(J).GT.2)   NOTV=0
      VADJ(J)=PX(J)
      IF (ISRCE.EQ.'ORC')   GO TO 490
      NAMEJ=JVNAME(J)
      IF (NAMEJ.EQ.'PAR'.OR.NAMEJ.EQ.'I/PAR')   VADJ(J)=2.*VADJ(J)
      IF (NAMEJ.EQ.'RII'.OR.NAMEJ.EQ.'I/RII')   VADJ(J)=VADJ(J)/2.
      IF (NAMEJ.EQ.'FW*'.OR.NAMEJ.EQ.'I/FW*')   VADJ(J)=1.175*VADJ(J)
  490 CONTINUE
C
  497 DO 510 J=0,IBNO
      IBRC(J)=0
  510 IBPT(J)=0
      IF (KVEH.EQ.0)   GO TO 516
      DO 515 J=1,NWACC
  515 ICELL(IPSING+J)=0
  516 IF (MENWOM(1,0).EQ.0)   GO TO 530
      IF (MENWOM(2,0).EQ.0)   GO TO 540
C...MEN AND WOMEN PRESENT IN UNIVERSE
      IF (NMEN+NWOM.LT.NRESP)   GO TO 520
C...PROCESS ALL RESPONDENTS
      IRESPA=1
      NR=NRESP
      NRB=0
      GO TO 550
C...OMIT SOME (E.G. NON-TV) RESPONDENTS
  520 IRESPA=LOCMEN
      IRESPB=LOCWOM
      NR=NMEN
      NRB=NWOM
      GO TO 550
C...ONLY WOMEN IN UNIVERSE
  530 IRESPA=LOCWOM
      NR=NWOM
      NRB=0
      GO TO 550
C...ONLY MEN IN UNIVERSE
  540 IRESPA=LOCMEN
      NR=NMEN
      NRB=0
  550 NRPOP=NR+NRB
C...POINT TO FIRST RESPONDENT TO BE PROCESSED
  560 IWDA=(IRESPA-1)/36
      NR=NR+(IRESPA-36*IWDA-1)
      IWRDWD=LOCWGT+LENWT*IWDA
C...READ A CHUNK OF RESPONDENTS
  600 IWDSIN=MIN0(INCORE,(NR+35)/36)
C...READ WEIGHTS VECTOR
      CALL RECIN (12,ICELL(IPWT),LENWT*IWDSIN,IWRDWD,IERR)
      LOCERR=170
      IF (IERR.NE.0)   GO TO 100
      IF (KVEH.EQ.0)   GO TO 670
C...READ VEHICLE VECTORS AND SET UP POP POINTERS
      INDEX=IPVEH
      DO 620 J=1,NISS
      IWD=LOCISS(J)+IWDA
      CALL RECIN (12,ICELL(INDEX),IWDSIN,IWD,IERR)
      LOCERR=180
      IF (IERR.NE.0)   GO TO 100
      CALL SETPTR (ICELL(INDEX),1,ICELL(IPPOP+J))
  620 INDEX=INDEX+INCORE
C...SET UP WEIGHT & BASE POPPING POINTERS
  670 CALL SETPTR (ICELL(IPWT),LENWT,IWTPTR)
      DO 672 J=0,IBNO
  672 CALL SETPTR (IBSVEC(IWDA+1,J),1,IBPTR(J))
      CALL ACCT (2)
      NNR=MIN0(NR,36*IWDSIN)
C...PROCESS NEXT NNR RESPONDENTS
      CALL CMACBV (NNR,IWTPTR,IBPTR(0),KVEH,ICELL(IPPOP),IBRC(0),
     1    IBPT(0),ICELL(IPSING),NISS,IVRESC,IPRYN,IBNO,NWPERB,NWACC,
     2    IVMEAS(1),IWMEAS(1),NOTV)
      NR=NR-NNR
      IWDA=IWDA+IWDSIN
      IF (NR.GE.1)   GO TO 600
C...CHECK IF THERE ARE MEN OR WOMEN YET TO BE PROCESSED
      IF (NRB.EQ.0)   GO TO 680
      NR=NRB
      NRB=0
      IRESPA=IRESPB
      GO TO 560
C...FINISHED TABBING RESPONDENTS - TYPE & WRITE RESULTS
  680 CALL CLOSE (12,IERR)
      CALL ACCT (2)
C
      DO 860 JB=0,IBNO
      IBPT(JB)=(IBPT(JB)+IWTDIV/2)/IWTDIV
      IF (JB.EQ.0)   GO TO 750
      IF (IOPT(10).EQ.0)   TYPE 8094
 8094 FORMAT(1X)
      TYPE 8095,JB
 8095 FORMAT('0---BASE',I2,'---')
      CALL PRITIT (IBTITL(1,JB))
  750 IF (JB.GE.1.AND.IOPT(10).NE.0)   GO TO 755
      IF (IWTDIV.NE.1)   TYPE 8097,IBPT(JB),IBRC(JB)
 8097 FORMAT(4X,'PROJECTED TOTAL=',I6,' (000)',7X,
     1    'RESPONDENT COUNT=',I5)
      IF (IWTDIV.EQ.1)   TYPE 8098,IBRC(JB)
 8098 FORMAT(4X,'RESPONDENT COUNT=',I5)
  755 IF (JB.EQ.0.OR.KVEH.EQ.0)   GO TO 860
C...RESPONSE COUNTS
      IF (IVRESC.NE.1)   GO TO 770
      TYPE 8100
 8100 FORMAT(4X,'VEHICLE RESPONSE COUNTS:')
      DO 765 JV=1,KVEH
      INDEX=IPSING+KVEH+JB*NWPERB+JV
      IRSCNT=ICELL(INDEX)
      IAST='  '
      IF (ISRCE.NE.'ORC')   GO TO 760
      IRSCNT=IRSCNT/2
      GO TO 765
  760 IF (IRSCNT.LE.60)   IAST='* '
      IF (IRSCNT.LE.30)   IAST='**'
  765 TYPE 8105,IVNAME(JV),IRSCNT,IAST
 8105 FORMAT(4X,A5,I6,1X,A2)
C...SCALE VEHICLE FIGURES
  770 INDEX=IPSING+KVEH+IVRESC*KVEH+JB*NWPERB
      IPPAIR=INDEX
      DO 805 JV=1,KVEH
      INDEXS=IPSING+JB*NWPERB+JV
      IVDIV=IWTDIV*IVMEAS(JV)
      ISINGL=(ICELL(INDEXS)+IVDIV/2)/IVDIV
      ICELL(INDEXS)=ISINGL
      ISINGL=VADJ(JV)*ISINGL+.5
      ISNADJ(JV)=MIN0(ISINGL,IBPT(JB))
      IF (IPRYN.NE.1)   GO TO 805
      DO 800 KV=1,JV
      INDEX=INDEX+1
      IF (JV.NE.KV)   GO TO 795
C...SELF-PAIR
      IVDIV=IWTDIV*IWMEAS(JV)
      IPR=(ICELL(INDEX)+IVDIV/2)/IVDIV
      IF (ISRCE.NE.'ORC')   GO TO 794
  793 IPR=0
      GO TO 800
  794 NAMEJ=JVNAME(JV)
      IF (NAMEJ.EQ.'HOL'.OR.NAMEJ.EQ.'I/HOL'.OR.NAMEJ.EQ.'TL'.OR.
     1    NAMEJ.EQ.'I/TL'.OR.NAMEJ.EQ.'PB'.OR.NAMEJ.EQ.'I/PB')
     2    GO TO 793
      IPR=VADJ(JV)*IPR+.5
      GO TO 798
C...PAIR (NOT SELF-PAIR)
  795 IVDIV=2*IWTDIV*IWMEAS(JV)*IWMEAS(KV)
      IPR=(ICELL(INDEX)+IVDIV/2)/IVDIV
  796 DUP=ICELL(INDEXS)+ICELL(IPSING+JB*NWPERB+KV)-IPR
      NEWDUP=DUP*VADJ(JV)*VADJ(KV)
      IPR=ISNADJ(JV)+ISNADJ(KV)-NEWDUP
  798 IPR=MAX0(ISNADJ(JV),ISNADJ(KV),MIN0(IPR,IBPT(JB),ISNADJ(JV)+
     1    ISNADJ(KV)))
  800 ICELL(INDEX)=IPR
  805 CONTINUE
C...MOVE ADJUSTED SINGLES INTO ICELL
      INDEXS=IPSING+JB*NWPERB
      DO 806 JV=1,KVEH
  806 ICELL(INDEXS+JV)=ISNADJ(JV)
      IF (ISRCE.EQ.'ORC')   GO TO 809
C...ADJUST REGIONAL-NATIONAL PAIRS TO BE = NATIONAL C1+
C   (REGIONAL C2-REGIONAL C1)
      DO 808 JV=1,KVEH
      DO 8068 L=1,NREGED
      IF (JVNAME(JV).NE.NAMRN(1,L))   GO TO 8064
C...VEHICLE JV IS A REGIONAL
      NAMNAT=NAMRN(2,L)
      DO 8062 KV=1,JV-1
      IF (JVNAME(KV).NE.NAMNAT)   GO TO 8062
C...VEHICLE KV IS THE CORRESPONDING NATIONAL
      IPR=ISNADJ(KV)+ICELL(IPPAIR+IJCAL(JV,JV))-ISNADJ(JV)
      ICELL(IPPAIR+IJCAL(JV,KV))=MAX0(ISNADJ(JV),ISNADJ(KV),
     1    MIN0(IPR,IBPT(JB),ISNADJ(JV)+ISNADJ(KV)))
 8062 CONTINUE
      GO TO 808
 8064 IF (JVNAME(JV).NE.NAMRN(2,L))   GO TO 8068
C...VEHICLE JV IS A NATIONAL WITH CORRESPONDING REGIONAL
      NAMREG=NAMRN(1,L)
      DO 8066 KV=1,JV-1
      IF (JVNAME(KV).NE.NAMREG)   GO TO 8066
C...VEHICLE KV IS A CORRESPONDING REGIONAL
      IPR=ISNADJ(JV)+ICELL(IPPAIR+IJCAL(KV,KV))-ISNADJ(KV)
      ICELL(IPPAIR+IJCAL(JV,KV))=MAX0(ISNADJ(JV),ISNADJ(KV),
     1    MIN0(IPR,IBPT(JB),ISNADJ(JV)+ISNADJ(KV)))
 8066 CONTINUE
 8068 CONTINUE
  808 CONTINUE
  809 IF (IBFNAM(JB).EQ.IDPBLK)   GO TO 860
C...WRITE DEMO FILE
      TYPE 8110,IBFNAM(JB)
 8110 FORMAT(4X,'DEMO NAME: ',A10)
      CALL OPEN (11,IBFNAM(JB),0,0,-1,IERR)
      IF (IERR.EQ.0)   GO TO 820
      CALL OPEN (11,IBFNAM(JB),0,0,2,IERR)
      LOCERR=200
      IF (IERR.NE.0)   GO TO 100
  820 CALL WRDEM (11,IBTITL(1,JB),KVEH,ICLI,IVNAME,IBPT(JB),ISNADJ,
     1    ICELL(IPPAIR+1),IPRSW)
  860 CONTINUE
C...EXVEC=EXCESS VEHICLE VECTORS OVER 2 PER VEHICLE
      EXVEC=MAX0 (NISS-2*KVEH,0)
      VEH=KVEH
      FNPR=NPAIRS
      BM1=IBNO-1
      PRICE=9.25+0.30*VEH+0.05*FNPR+0.10*EXVEC+BM1*(1.25+0.10*VEH+
     1    0.04*FNPR+0.03*EXVEC)
      PRICE=AMAX1 (PRICE,10.00)
      ITRPAC=ITRPAC+100.*(PRICE/0.15)+.5
      IF (IOPT(10).EQ.0)   GO TO 1100
      CALL ACCT (2)
C
C...TABULAR OUTPUT
C
C...CENTER AND TYPE TABLE TITLE
      CALL CENTER (ITABTI,15,72)
C...RIGHT-JUSTIFY VEHICLE NAMES FOR PRINTING
      CALL RJNAME (IVNAME,NAMERJ,KVEH)
      PRICE=0.00
      DO 1030 JV=0,KVEH,7
      LASTV=MIN0 (JV+6,KVEH)
      NUMLIN=(LASTV-JV)+1
      FNUMLN=NUMLIN
      IF (JV.EQ.0)   TYPE 8150,(NAMERJ(J),J=1,LASTV)
 8150 FORMAT('0',18X,'TOTAL',6(3X,A5))
      IF (JV.NE.0)   TYPE 8155,(NAMERJ(J),J=JV,LASTV)
 8155 FORMAT('0',15X,7(3X,A5))
      DO 1020 JB=1,IBNO
      INDEXS=IPSING+JB*NWPERB
      INDEXP=INDEXS+KVEH+IVRESC*KVEH
      IF (JV.NE.0)   GO TO 920
      LINE(1)=IBPT(JB)
      DO 910 J=1,LASTV
  910 LINE(J+1)=ICELL(INDEXS+J)
      GO TO 940
  920 DO 930 J=1,NUMLIN
  930 LINE(J)=ICELL(INDEXS+JV+J-1)
  940 TYPE 8160,(IBTITL(J,JB),J=1,2),(LINE(J),J=1,NUMLIN)
 8160 FORMAT('0',2A5,'  AUD',7I8)
      PRICE=PRICE+0.04*FNUMLN
      IF (JB.GT.1)   GO TO 945
C...SAVE COMPOSITION BASE
      DO 942 J=1,NUMLIN
  942 COMPBS(J)=LINE(J)
  945 GO TO (960,947,960,947,960,947,960,947),IOPT(10)
C...COVERAGE
  947 DO 950 J=1,NUMLIN
  950 COV(J)=100.*LINE(J)/IBPT(JB)
      TYPE 8170,(COV(J),J=1,NUMLIN)
 8170 FORMAT(13X,'COV',7(F7.1,'%'))
      PRICE=PRICE+0.01*FNUMLN
  960 GO TO (965,965,962,962,965,965,962,962),IOPT(10)
C...COMPOSITION
  962 DO 963 J=1,NUMLIN
  963 COV(J)=100.*LINE(J)/COMPBS(J)
      TYPE 8175,(COV(J),J=1,NUMLIN)
 8175 FORMAT(12X,'COMP',7(F7.1,'%'))
      PRICE=PRICE+0.01*FNUMLN
  965 IF (IOPT(10).LE.4)   GO TO 1020
C...SELF-PAIRS
      IF (JV.NE.0)   GO TO 980
      LINE(1)=IBPT(JB)
      DO 970 J=1,LASTV
  970 LINE(J+1)=ICELL(INDEXP+IJCAL(J,J))
      GO TO 1000
  980 DO 990 J=1,NUMLIN
  990 LINE(J)=ICELL(INDEXP+IJCAL(JV+J-1,JV+J-1))
 1000 TYPE 8180,(LINE(J),J=1,NUMLIN)
 8180 FORMAT(12X,'CUME',7I8)
      PRICE=PRICE+0.04*FNUMLN
      IF (IOPT(10).NE.6.AND.IOPT(10).NE.8)   GO TO 1020
      DO 1010 J=1,NUMLIN
 1010 COV(J)=100.*LINE(J)/IBPT(JB)
      TYPE 8190,(COV(J),J=1,NUMLIN)
 8190 FORMAT(8X,'CUME COV',7(F7.1,'%'))
      PRICE=PRICE+0.01*FNUMLN
 1020 CONTINUE
 1030 CONTINUE
      PRICE=AMAX1 (PRICE,3.00)
      ITRPAC=ITRPAC+100.*(PRICE/0.15)+.5
C
C...END OF LINK
C
 1100 TYPE 8200
 8200 FORMAT(////)
      CALL ACCT (5)
      NPACK=IPACK2 ("3374,"63014)
      CALL CHAIN (0,1,'CMNCH',NPACK)
      END
   