TITLE	COBOLC	FOR COBOL 5(107)	
SUBTTL	DATA DIV. SYNTAX SCAN		W.NEELY/CAM

;COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORP, MAYNARD, MA.

TWOSEG
RELOC	400000

ENTRY	COBOLC

COBOLC:	JRST	1,.+1		;ENABLE CONCEALED MODE
	SETFAZ	C;
	SETZM	FILSEC		;CLR FILE-SECTION-SEEN FLAG
IFN RPW,<
	SETZM	CURRPW		;CLR RPWTAB PTR
	SETZM	LASTYP##	;CLR LAST RPW TYPE SEEN STORAGE
	MOVE	TA,['000000']	;INIT 6-DIGIT SIXBIT #
	MOVEM	TA,SIXHLD
	>
	MOVE	SAVPTR,ISVPTR##
	MOVE	NODPTR,INDPTR##
	IFN	DEBUG,<
	SWOFF	FNDTRC;
	MOVE	TE,CORESW##
	TRNE	TE,TRACED##
	SWON	FNDTRC;		;TRACE DD NODES
	>

	HRRZI	TA,DD1.
	PUSH	NODPTR,TA
	PUSHJ	PP,SQURL.##
	TTCALL	3,[ASCIZ /COBOLC--LOST; TOO MANY POPJ'S
/]
	JRST	KILL
SUBTTL	ACTIONS FOR DD SYNTAX PROCESSING

	INTER.	DA0.
DA0.:	POP	NODPTR,NODE	;POP UP CURRENT NODE
IFN DEBUG,<
	TSWT	FNDTRC;
	JRST	DA0.1
	HRRZI	CH,11
	HRRZI	TB,6
	PUSHJ	PP,PUTLST
	SOJG	TB,.-1
	MOVE	NODE,(NODPTR)
	PUSHJ	PP,PTNOD.##
	HRRZI	TB,6
	HRRZI	CH,136
	PUSHJ	PP,PUTLST##
	SOJG	TB,.-1
	PUSHJ	PP,LCRLF##
	MOVE	NODE,(NODPTR)
	PUSHJ	PP,PTNOD.
	HRRZI	CH,11
	PUSHJ	PP,PUTLST
DA0.1:	>
	POPJ	PP,

	INTER.	DA1.
DA1.:	SWOFF	FFILSC;		;'FILE SECTION' FLAG
	MOVE	SAVPTR,ISVPTR	;SAVE LIST POINTER
	HRRZI	TA,1
	PUSH	SAVPTR,TA
	SETZM	RDFLVL		;CLR REDEFINES NESTING LEVEL
	SETZM	CURFIL
	SETZM	CURDAT
	SETZM	CURCON
	SETZM	CURVAL
	SETZM	CURNAM
	SETZM	EAS1PC
	SETZM	EAS2PC
	SETOM	PCHOLD##
	SETZM	SVDADR
	SETZM	CFLM
	SETZM	WSAS1P
	SETZM	IDXLST##
IFN RPW,<
	SETZM	LASTRD##	;INIT LAST RD PTR
	>
	POPJ	PP,
	INTER.	DA2.
DA2.:	SWON	FFILSC;
	SETOM	FILSEC##	;SET FILE-SECTION-SEEN FLAG
IFN RPW,<SETZM	REPSEC>		;CLR REPORT SECTION FLAG
	SETZM	LNKSEC		;CLR LINKAGE SECTION FLAG
	SKIPL	TA,PCHOLD	;RESET EAS1PC TO PREVIOUS
	MOVEM	TA,EAS1PC	;  IF CHANGED BY LINKAGE SECTION
	SETOM	PCHOLD
	MOVE	TA,EAS1PC
	MOVEM	TA,WSAS1P##
	SETZM	EAS1PC
	SETZM	EAS2PC
	SETZM	CFLM
	POPJ	PP,

	INTER.	DA3.
DA3.:
	SETZM	LNKSEC		;CLR LINKAGE SECTION FLAG
	SKIPL	TA,PCHOLD	;RESET EAS1PC TO PREVIOUS
	MOVEM	TA,EAS1PC	;  IF CHANGED BY LINKAGE SECTION
	SETOM	PCHOLD
DA3.0:	SWOFF	FFILSC;
IFN RPW,<SETZM	REPSEC>		;CLR REPORT SECTION FLAG
	MOVE	TA,WSAS1P
	MOVEM	TA,EAS1PC
	SETZM	EAS2PC
	SETZM	CFLM
	SETZM	LAST01##
	POPJ	PP,
	INTER.	DA4.
DA4.:	PUSHJ	PP,DA10.
	SKIPN	SVDADR
	JRST	D4.1
	MOVE	CH,SVDWRD##
	PUSHJ	PP,PUTAS1
	SETZM	SVDADR
D4.1:	SETZM	EAS2PC
	TSWT	FFILSC;
	JRST	D4.11
	MOVE	TA,WSAS1P
	MOVEM	TA,EAS1PC
D4.11:	SKIPN	EAS1PC
	JRST	DA4.A2
	HLRZ	TA,EAS1PC
	JUMPE	TA,DA4.A2
	AOS	TA,EAS1PC
	HRRZM	TA,EAS1PC
DA4.A2:	PUSHJ	PP,CLEANC	;DO CLEANC HERE SO SUM-CTRS GET ALLOCATED
	HRLZI	CH,AS.REL##
	HRRI	CH,1+AS.DAT##
	PUSHJ	PP,PUTAS1
	HRRZ	CH,EAS1PC
DA4.A:	HRRZM	CH,TBLOCK
	MOVE	CH,[XWD	AS.REL+1,AS.MSC##]
	PUSHJ	PP,PUTAS1
	HRRZ	CH,TBLOCK
	CAILE	CH,077777
	HRRZI	CH,077777
	IORI	CH,AS.DOT##
	PUSHJ	PP,PUTAS1
	HRRZ	CH,TBLOCK
	SUBI	CH,077777
	JUMPG	CH,DA4.A
	PUSHJ	PP,CLRNAM##	;DELETE UNNECESSARY RESERVED WORDS
	ENDFAZ	C;
	INTER.	DA5.
DA5.:	MOVEM	LN,TBLOCK
DA5.0:	MOVEM	TYPE,TBLOCK+1
	PUSHJ	PP,GETITM
DA5.A:	CAIE	TYPE,2000+ENDIT.
	CAIN	TYPE,ENDIT.	;EOF?
	POPJ	PP,		;YES
	CAMN	LN,TBLOCK
	JRST	DA5.B
	MOVEM	LN,TBLOCK
	CAIE	TYPE,2000+LINKG.
	CAIN	TYPE,2000+FILE.
	JRST	DA5.X
	CAIN	TYPE,2000+WORKI.
	JRST	DA5.X
	CAIN	TYPE,2000+PROC.
	JRST	DA5.X
	CAIN	TYPE,2000+FD.
	JRST	DA5.X
	CAIN	TYPE,PRIOD.
	JRST	DA5.0
	CAIE	TYPE,INTGR.
	CAIN	TYPE,2000+INTGR.
	JRST	DA5.X
DA5.B:	CAIN	TYPE,PIC.
	PUSHJ	PP,PSCAN##
	JRST	DA5.0
DA5.X:	MOVE	TA,TBLOCK+1
	CAIE	TA,PRIOD.
	PUSHJ	PP,CE125.
	SWON	FREGWD;
	POPJ	PP,

	INTER.	DA6.
DA6.:	SWOFF	FREGWD;
	POPJ	PP,

	INTER.	DA7.
DA7.:	SWON	FREGWD;
	POPJ	PP,
	INTER.	DA8.
DA8.:	HLRZ	TB,CURDAT
	JUMPE	TB,DA8.X
	PUSHJ	PP,DA54.
IFN RPW,<SETZM	LASTYP>		;CLR LAST RPW TYPE SEEN STORAGE
	HLRZ	TB,CURDAT
	PUSHJ	PP,FNDPOP
	JRST	DA8.X
	LDB	TC,[POINT	3,TB,20]
	CAIE	TC,CD.DAT
	JRST	DA8.X		;FATHER NOT DATTAB
	HRRZ	TA,TB
	HRLZM	TB,CURDAT
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	HRRM	TA,CURDAT
	JRST	DA8.
DA8.X:	SETZM	CURDAT
	POPJ	PP,
	INTER.	DA9.
DA9.:	PUSHJ	PP,TRYNAM##
	PUSHJ	PP,BLDNAM##
	HLRZS	TA
	DPB	TA,[POINT 15,W2,15]
	TLZ	W1,GWNOT
	LDB	TA,[POINT	15,W2,15]
	HRRZI	TB,CD.FIL
	PUSHJ	PP,FNDLNK##	;FIND A FILTAB ENTRY
	JRST	DA9.E		;NONE FOUND
	MOVEM	TB,CURFIL	;SAVE POINTER
	MOVE	TA,TB
	LDB	TB,FI.FDD##	;FD ALREADY SEEN?
	JUMPN	TB,DA9.E2	;YES
DA9.1:	SETO	TB,
	DPB	TB,FI.FDD
	DPB	W2,FI.LNC##
	SETZM	EAS1PC##
	SETZM	EAS2PC
	SETZM	CFLM
	POPJ	PP,
DA9.E:	EWARNW	CE20.
DA9.E1:	MOVE	TA,[XWD CD.FIL,SZ.FIL]
	PUSHJ	PP,GETENT
	MOVEM	TA,CURFIL
	HRRZI	TB,CD.FIL
	DPB	TB,[POINT 3,0(TA),2]
	LDB	TB,[POINT 15,W2,15]
	DPB	TB,FI.NAM##
	DPB	W2,FI.LNC
	HRRZI	TC,%%RM
	LDB	TE,FI.RM2##	;RECORDING MODE CLAUSE SEEN?
	SKIPN	TE		;YES, DON'T CHANGE
	DPB	TC,FI.ERM##
	DPB	TC,FI.IRM##
	HRRZI	TC,%%LBL
	DPB	TC,FI.LBL##
	HRRZI	TC,%%ACC
	DPB	TC,FI.ACC##
	HRRZI	TC,1
	DPB	TC,FI.NDV##
	MOVE	TA,[XWD CD.VAL,1]
	PUSHJ	PP,GETENT
	MOVE	TB,[ASCII /*****/]
	HRRZI	TC,4
	DPB	TC,[POINT 7,TB,6]
	MOVEM	TB,(TA)
	HLRZ	TB,TA
	MOVE	TA,CURFIL
	DPB	TB,FI.VAL##
	LDB	TB,[POINT 15,W2,15]
	HRRI	TA,(TB)
	PUSHJ	PP,PUTLNK
	MOVE	TA,CURFIL
	JRST	DA9.1
DA9.E2:	EWARNW	CE34.
	JRST	DA9.E1

	INTER.	DA10.
DA10.:	PUSHJ	PP,DA8.
	MOVE	TA,CURFIL
	JUMPE	TA,DA10.X
	HRRZ	TB,CFLM
	HRRZI	TD,(TB)
	HRRZ	TC,EAS2PC
	SUBI	TD,(TC)
	CAIGE	TD,5
	HRRZI	TB,5(TC)
	MOVE	TE,EAS1PC
	TLNE	TE,777777
	HRRZI	TE,1(TE)
	CAIGE	TB,(TE)
	HRRZI	TB,(TE)
	HRRZM	TB,EAS1PC
	SETZM	EAS2PC
	SETZM	CFLM
DA10.A:	HRRZ	TA,CURFIL
	SETZM	TBLOCK
	LDB	TA,FI.DRL
	JUMPE	TA,DA10.B	;NO DATA RECORDS
D10A.1:	HRLZM	TA,CURDAT
	PUSHJ	PP,LNKSET
	HRRM	TA,CURDAT
	LDB	TB,DA.DEF
	JUMPN	TB,.+3		;THIS RECORD IS DEFINED
	PUSHJ	PP,D10E.1
	JRST	D10A.2
	LDB	TB,DA.EXS
	SKIPN	TC,TBLOCK
	HRRZM	TB,TBLOCK
	CAMN	TB,TBLOCK	;SAME SIZE RECORDS?
	JRST	D10A.3		;YES
	CAMLE	TB,TBLOCK
	HRRZM	TB,TBLOCK
	SETO	TB,
	HRRZ	TA,CURFIL
	DPB	TB,FI.VLR##	;RECORDS ARE VARIABLE LENGTH
D10A.2:	HRRZ	TA,CURDAT
D10A.3:	LDB	TB,DA.FAL
	JUMPN	TB,DA10.B	;NO MORE DATA RECORDS
	LDB	TA,DA.BRO##
	JUMPN	TA,D10A.1	;CHECK THIS RECORD
DA10.B:	HRRZ	TA,CURFIL
	HRRZ	TB,TBLOCK
	DPB	TB,FI.MRS
	SETZ	TB,
	LDB	TC,FI.LBL
	CAIN	TC,%LBL.S	;STANDARD LABELS?
	HRRZI	TB,^D80		;SIZE OF STANDARD LABEL
	HRRZM	TB,TBLOCK
	LDB	TA,FI.LRL##	;LABEL RECORD LINK
	JUMPE	TA,DA10.C	;NO NON-STANDARD LABELS
D10B.1:	HRLZM	TA,CURDAT
	PUSHJ	PP,LNKSET
	HRRM	TA,CURDAT
	LDB	TB,DA.DEF
	JUMPN	TB,.+3
	PUSHJ	PP,D10E.1
	JRST	D10B.2
	LDB	TB,DA.EXS
	SKIPN	TC,TBLOCK
	HRRZM	TB,TBLOCK
	CAMN	TB,TBLOCK	;SAME SIZE RECORDS?
	JRST	D10B.3		;YES
	CAMLE	TB,TBLOCK
	HRRZM	TB,TBLOCK
	SETO	TB,
	HRRZ	TA,CURFIL
	DPB	TB,FI.VLR
D10B.2:	HRRZ	TA,CURDAT
D10B.3:	LDB	TB,DA.FAL
	JUMPN	TB,DA10.C	;NO MORE LABEL RECORDS
	LDB	TA,DA.BRO
	JUMPN	TA,D10B.1
DA10.C:	HRRZ	TA,CURFIL
	HRRZ	TB,TBLOCK
	DPB	TB,FI.LRS##	;MAXIMUM LABEL RECORD SIZE
	JRST	DA10.X
D10E.1:	HRRZ	TA,CURDAT
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	HRRZI	DW,150		;'NOT DEFINED'
	JRST	FATAL
DA10.X:	SETZM	CURFIL
	SETZM	CURDAT
	SETZM	DATLVL		;INIT LAST DATA LEVEL HOLD
	SETZM	LSTDAT##	;& CLR LAST DATA-ITEM-NOT-A-REDEF TABLE
	MOVE	TA,[LSTDAT,,LSTDAT+1]
	BLT	TA,LSTDAT+^D49
	POPJ	PP,
	INTER.	DA11.
DA11.:	TLNN	W1,GWNLIT;		;IS ITEM NUMERIC LITERAL?
	JRST	DA11.E		;NO
	TLNE	W1,GWDP		;IS IT INTEGER?
	JRST	DA11.E		;NO
	HLRZ	TB,W1
	ANDI	TB,177		;NO. OF CHARACTERS
	MOVEM	TB,CTR##
	HRRZI	TA,LITVAL##
	PUSHJ	PP,GETVAL
DA11.A:	MOVEM	TC,0(SAVPTR)
	POPJ	PP,
DA11.E:	EWARNW	CE25.
	SETZ	TC,
	JRST	DA11.A


	INTER.	DA12.
DA12.:	SETZ	TC,
	PUSH	SAVPTR,TC
	PUSHJ	PP,DA11.
	POP	SAVPTR,TC
	CAML	TC,0(SAVPTR)
	MOVEM	TC,0(SAVPTR)
	POPJ	PP,


	INTER.	DA13.
DA13.:	SETZ	TB,
	EXCH	TB,0(SAVPTR)
	CAIL	TB,^D4096	;REQUIRE BLK FACTOR .LE. 4095
	EWARNJ	CE2.		;IT ISN'T
	SKIPE	TA,CURFIL
	DPB	TB,FI.BLF##
DA13.X:	POPJ	PP,


	INTER.	DA14.
DA14.:	SETZ	TB,
	EXCH	TB,(SAVPTR)
	SKIPE	TA,CURFIL
	DPB	TB,FI.FBS##	;BUFFER SIZE
DA14.X:	POPJ	PP,

	INTER.	DA15.
DA15.:	SETZ	TB,
	EXCH	TB,0(SAVPTR)
	SKIPN	TA,CURFIL
	POPJ	PP,
	LDB	TC,FI.MRS##	;DATA RECORD SIZE
	CAIGE	TC,(TB)
	DPB	TB,FI.MRS
DA15.X:	POPJ	PP,

	INTER.	DA16.
DA16.:	MOVE	TA,FI.DRL	;DATA RECORD LINK
	MOVEM	TA,PNTS
	MOVE	TA,DA.DRC##
	MOVEM	TA,PNTS2##
	SKIPN	TA,CURFIL
	POPJ	PP,
	SETO	TB,
	DPB	TB,FI.DRC##
	POPJ	PP,

	INTER.	DA17.
DA17.:	MOVE	TA,FI.LRL	;LABEL RECORD LINK
	MOVEM	TA,PNTS
	MOVE	TA,DA.LRC##
	MOVEM	TA,PNTS2
	POPJ	PP,
	INTER.	DA18.
DA18.:	PUSHJ	PP,TRYNAM
	PUSHJ	PP,BLDNAM
	MOVEM	TA,CURNAM
	TLZ	W1,GWNOT
	HLRS	TA
	DPB	TA,[POINT	15,W2,15]
	JRST	DA18.P
DA18.C:	LDB	TA,[POINT	15,W2,15]	;NAMTAB POINTER
	HRRZI	TB,CD.DAT
	PUSHJ	PP,FNDLNK	;ANY DATAB ENTRIES FOR NAME?
	JRST	DA18.P		;NO
DA18.D:	LDB	TC,DA.LVL##	;LEVEL OF ITEM
	CAIE	TC,1		;RECORD
	JRST	DA18.T		;NO
	MOVEM	TA,TBLOCK
	MOVEM	TB,TBLOCK+1
	HLRS	TB		;GET RELATIVE ADDRESS IN R. H.
	PUSHJ	PP,FNDPOP	;FIND FATHER LINK
	HLRZ	TC,CURFIL
	CAMN	TB,TC		;CURRENT FILE?
	EWARNJ	CE35.		;YES
	MOVE	TA,TBLOCK
	MOVE	TB,TBLOCK+1

DA18.T:	PUSHJ	PP,FNDNXT##
	JRST	DA18.P
	JRST	DA18.D
DA18.P:	MOVE	TA,[XWD CD.DAT,SZ.DAT+SZ.DOC+SZ.MSK]
	PUSHJ	PP,GETENT##
	MOVEM	TA,CURDAT
	LDB	TB,[POINT	15,W2,15]
	IORI	TB,CD.DAT*1B20
	MOVSM	TB,0(TA)
	DPB	W2,DA.LNC##
	SETZ	TB,		;LEVEL 0 IS USED FOR 'PRE-NAMED' DATA ITEMS
	DPB	TB,DA.LVL
	SETO	TB,
	DPB	TB,DA.CLA	;CLASS
	DPB	TB,DA.PWA
	TSWF	FFILSC;
	DPB	TB,DA.DFS##
	MOVE	TA,CURFIL
	JUMPE	TA,DA18.X
	MOVE	TA,CURDAT
	DPB	TB,PNTS2	;SET 'LABEL RECORD' OR 'DATA RECORD' BIT
	MOVE	TA,CURFIL
	LDB	TB,PNTS		;GET 'DATA RECORD' OR 'LABEL RECORD' PTR
	JUMPE	TB,DA18.V	;THIS IS FIRST SUCH RECORD
	HLRZ	TC,CURDAT
	DPB	TC,PNTS
	MOVE	TA,CURDAT
DA18.Q:	DPB	TB,DA.BRO	;BROTHER LINK
DA18.W:	LDB	TB,[POINT	15,W2,15]
	HRRI	TA,(TB)
	JRST	PUTLNK
DA18.V:	HLRZ	TB,CURDAT
	DPB	TB,PNTS
	SETO	TB,
	MOVE	TA,CURDAT
	DPB	TB,DA.FAL	;'FATHER LINK' FLAG
	HLRZ	TB,CURFIL
	JRST	DA18.Q
DA18.X:	MOVE	TA,CURDAT
	JRST	DA18.W
	INTER.	DA19.
DA19.:	HRRZI	TC,%LBL.S	;'STANDARD LABELS' CODE
CHKLBL:	MOVE	TA,CURFIL
	JUMPE	TA,DA19.X
	LDB	TB,FI.LBL
	CAIE	TB,%%LBL	;INITIAL STATE?
	EWARNJ	CE16.		;NO--ERROR
	DPB	TC,FI.LBL
DA19.X:	POPJ	PP,

	INTER.	DA20.
DA20.:	HRRZI	TC,%LBL.O	;'OMITTED LABELS' CODE
	JRST	CHKLBL

	INTER.	DA21.
DA21.:	HRRZI	TC,%LBL.N	;'NON-STANDARD LABELS' CODE
	MOVE	TA,FI.LRL
	MOVEM	TA,PNTS
	MOVE	TA,DA.LRC
	MOVEM	TA,PNTS2
	JRST	CHKLBL

	INTER.	DA22.
DA22.:	MOVEI	TA,%HL.VI	;'VALUE OF IDENTIFICATION' FLAG
	MOVEM	TA,PNTS
	POPJ	PP,

	INTER.	DA23.
DA23.:	MOVEI	TA,%HL.VD	;'VALUE OF DATE-WRITTEN' FLAG
	MOVEM	TA,PNTS
	POPJ	PP,
;GET LITERAL VALUE OF IDENTIFICATION

	INTER.	DA24I.
DA24I.:	HLRZ	TB,W1		;GET LENGTH OF LITERAL
	ANDI	TB,777
	CAIG	TB,^D9		;9 CHARS OR LESS?
	JRST	DA24I1		;YES
	MOVEI	TB,^D9		;NO, TRUNCATE
	HRRZI	DW,^D238	;& WARN
	PUSHJ	PP,DA24X.
	MOVEM	TB,TBLOCK+2
	JRST	DA24I2
DA24I1:	MOVEM	TB,TBLOCK+2	;SAVE TRUE SIZE
	CAIL	TB,^D9		;LESS THAN 9 CHARS?
	JRST	DA24I2		;NO
	HRRZI	DW,CE334.	;YES, WARN
	PUSHJ	PP,DA24X.
	MOVEI	TB,^D9
DA24I2:	PUSHJ	PP,DA24S.	;SET PTRS & CTR
	SETZM	TBLOCK+1	;CLR NON-STANDARD CHAR FLAG
	MOVE	TD,TBLOCK+2	;GET TRUE SIZE
DA24I3:	SOJGE	TD,DA24I4	;SKIP IF NOT FINISHED WITH REAL CHARS
	MOVEI	TE,40		;GET A SPACE TO PAD OUT TO 9 CHARS
	JRST	DA24I6
DA24I4:	ILDB	TE,TB		;GET LITERAL CHAR
	CAIN	TE,40		;MAKE SURE CHAR IS A-Z OR 0-9 OR SPACE
	JRST	DA24I6		;IT'S SPACE
	CAIL	TE,"0"
	CAILE	TE,"Z"
	JRST	DA24I5		;NON-STANDARD CHAR
	CAILE	TE,"9"
	CAIL	TE,"A"
	JRST	DA24I6		;CHAR IS OK
DA24I5:	AOS	TBLOCK+1	;REQUEST NON-STD CHAR WARNING
DA24I6:	IDPB	TE,TC		;STORE LITERAL CHAR
	SOSLE	TBLOCK		;COUNT CHARS INCLUDING PADDING
	JRST	DA24I3		;DO NEXT CHAR
	SKIPE	TBLOCK+1	;NEED A NON-STD CHAR WARNING?
	PUSHJ	PP,DA24W.	;YES
	MOVE	TD,FI.VID	;GET PTR TO VAL-OF-ID
DA24I8:	HRRZ	TA,CURFIL	;FILTAB ADDR
	HLRZ	TB,CURVAL	;VALTAB REL ADDR
	LDB	TC,TD		;VALUE SEEN BEFORE?
	SKIPE	TC
	EWARNJ	CE16.		;YES, DUPLICATE CLAUSE
	DPB	TB,TD		;NO, STORE VALTAB LINK
	POPJ	PP,
;GET LITERAL VALUE OF DATE-WRITTEN

	INTER.	DA24D.
DA24D.:	HLRZ	TB,W1		;GET LENGTH OF LITERAL
	ANDI	TB,777
	CAIGE	TB,6		;FEWER THAN 6 CHARS?
	EWARNJ	CE333.		;YES, THAT'S ILLEGAL
	CAIG	TB,6		;MORE THAN 6?
	JRST	DA24D1		;NO, OK
	MOVEI	TB,6		;YES, TRUNCATE
	HRRZI	DW,^D238	;& WARN
	PUSHJ	PP,DA24X.
DA24D1:	PUSHJ	PP,DA24S.	;SET PTRS & CTR
	SETZM	TBLOCK		;CLR NON-STANDARD CHAR FLAG
DA24D2:	SOJL	TD,DA24D3	;SKIP IF FINISHED
	ILDB	TE,TB		;GET LITERAL CHAR
	CAIL	TE,"0"		;IS IT A DIGIT?
	CAILE	TE,"9"
	AOS	TBLOCK		;NO, REQUEST FLAG
	IDPB	TE,TC		;STORE LITERAL CHAR
	JRST	DA24D2		;DO NEXT CHAR
DA24D3:	SKIPE	TBLOCK		;NEED A NON-STD CHAR WARNING?
	PUSHJ	PP,DA24W.	;YES
	MOVE	TD,FI.VDW##	;GET PTR TO VAL-OF-DATE-WRITTEN
	JRST	DA24I8
;GET LITERAL VALUE OF PROJECT-PROGRAMMER

	INTER.	DA24P.
DA24P.:	HLRZ	TB,W1		;GET LENGTH OF LITERAL
	ANDI	TB,777
	TLNE	W1,GWNLIT	;IS IT A NUMERIC LITERAL?
	TLNE	W1,GWDP		;DOES IT HAVE A DECIMAL POINT
	EWARNJ	CE336.		;NOT AN INTEGER
	CAILE	TB,6		;MORE THAN 6 CHARS?
	EWARNJ	CE336.		;YES
	PUSHJ	PP,DA24S.	;SET PTRS & CTR
DA24P2:	SOJL	TD,DA24P3	;SKIP IF FINISHED
	ILDB	TE,TB		;GET LITERAL CHAR
	CAIL	TE,"0"		;IS IT AN OCTAL DIGIT?
	CAILE	TE,"7"
	EWARNJ	CE336.		;ILLEGAL CHARACTER
	IDPB	TE,TC		;STORE LITERAL CHAR
	JRST	DA24P2		;DO NEXT CHAR
DA24P3:	HRRZ	TA,CURFIL	;FILTAB ADDR
	HLRZ	TB,CURVAL	;VALTAB REL ADDR
	LDB	TC,FI.VPP	;1ST HALF OF PPN ALREADY IN?
	SKIPN	TC		;IF SO, 2ND HALF ASSUMED IN NEXT VALTAB ENTRY
	DPB	TB,FI.VPP	;NO, STORE 1ST HALF VALTAB LINK
	POPJ	PP,
;SUBROUTINE TO SET UP PTRS AND CTR FOR TRANSFERRING LITERAL TO VALTAB

DA24S.:	MOVEM	TB,TBLOCK	;SAVE LENGTH OF LITERAL
	ADDI	TB,5
	IDIVI	TB,5		;NUMBER OF WORDS
	HRRZ	TA,TB
	HRLI	TA,CD.VAL
	PUSHJ	PP,GETENT
	MOVEM	TA,CURVAL	;SAVE VALTAB ADDR
	HLR	W1,TA		;PUT POINTER IN W1
	MOVE	TB,[POINT 7,LITVAL]	;'GET' POINTER
	MOVE	TC,[POINT 7,(TA),6]	;'PUT' POINTER
	MOVE	TD,TBLOCK	;SIZE
	DPB	TD,TC
	POPJ	PP,

DA24.E:	EWARNJ	CE16.

;ISSUE A WARNING FOR NON-STD CHAR IN VALUE ITEM

DA24W.:	HRRZI	DW,^D242	;NON-STD CHAR
DA24X.:	LDB	LN,[POINT 13,W2,28]	;GET LINE POSITION
	LDB	CP,[POINT 7,W2,35]
	JRST	WARN

;2ND HALF OF PROJ-PROGRAMMER NUMBER MISSING

	INTER.	DA24PE
DA24PE:	MOVE	TA,[XWD CD.VAL,1]	;GET 1-WORD VALTAB ENTRY
	PUSHJ	PP,GETENT
	MOVSI	TB,5400		;PUT A "0" IN VALTAB
	MOVEM	TB,(TA)
	EWARNJ	CE335.		;FATAL ERROR
	INTER.	DA25.
DA25.:	PUSHJ	PP,DA60S.	;SAVE NAMTAB ADDR
	PUSHJ	PP,DA25S.	;SET UP HLDTAB ENTRY
	HRRZ	TB,PNTS		;STORE 'VALUE OF XXX' FLAG
	DPB	TB,HL.COD##
	HLRZ	TB,CURFIL	;STORE FILTAB LINK IN HLDTAB
DA25.X:	DPB	TB,HL.LNK
	POPJ	PP,

;SET UP HLDTAB ENTRY

DA25S.:	MOVE	TA,[XWD CD.HLD,SZ.HLD]	;GET A HLDTAB ENTRY
	PUSHJ	PP,GETENT
	MOVEM	TA,CURHLD	;SAVE ADDR
	HLRZ	TB,CURNAM	;PUT LINK TO NAMTAB IN HLDTAB
	DPB	TB,HL.NAM##
	DPB	W2,HL.LNC##	;ALSO POSITION OF ITEM IN SOURCE
	SETZ	TB,		;CLR # OF QUALIFIERS
	DPB	TB,HL.QAL##
	POPJ	PP,
;CHECK LEVEL NUMBER FOR 01 LEVEL ITEMS

	INTER.	DA26.
DA26.:	PUSHJ	PP,DA11.	;GET VALUE OF INTEGER
DA26N.:	MOVE	TC,0(SAVPTR)
	JUMPLE	TC,DA26.E
IFN RPW,<
	SKIPN	REPSEC		;IN REPORT SECTION?
	JRST	DA26.1		;NO
	CAIG	TC,^D49		;YES, ONLY 1-49 LEGAL
	JRST	DA26.A		;OK
	JRST	DA26.E		;WON'T DO
	>
DA26.1:	CAIN	TC,102
	JRST	DA26.R		;LEVEL 66
	CAILE	TC,61		;49. IS MAX. LEVEL NUMBER
	CAIN	TC,115		;EXCEPT FOR 77
	JRST	DA26.A
DA26.E:	EWARNW	CE64.
	HRRZ	TC,LEVEL
	CAILE	TC,0
	CAILE	TC,61
	HRRZI	TC,1
	MOVEM	TC,0(SAVPTR)
DA26.X:	PUSHJ	PP,SAVLVL	;LEAVE TRACKS FOR REDEFINES
	SETZM	RUSAGE##	;INIT GROUP ITEM USAGE CHECK
	POPJ	PP,

DA26.A:	CAIN	TC,115		;77.
	HRRZI	TC,77
	MOVEM	TC,LEVEL
	CAIE	TC,1
	CAIN	TC,77		;77-LEVEL ITEM
	JRST	DA26.X
	HRRZI	TC,1
	MOVEM	TC,LEVEL
	EWARNJ	CE48.
DA26.R:	HRRZI	TC,76
	HRRZM	TC,LEVEL
	HRRZI	NODE,DD165.
	HRRZM	NODE,0(NODPTR)
	POPJ	PP,
;REMEMBER THIS DATA LEVEL FOR REDEFINES
;ALSO IF LEVEL INCREASING, CLEAN UP PART OF TABLE BELOW THIS

SAVLVL:
IFN RPW,<
	SKIPE	REPSEC		;NOT NEEDED BY REPORT SECTION
	POPJ	PP,
	>
	MOVE	TA,(SAVPTR)	;NEW LEVEL
	CAIN	TA,^D77		;CONVERT 77 TO 1
	MOVEI	TA,1
	CAML	TA,DATLVL##	;ARE WE GOING UP A LEVEL?
	JRST	SAVLV1		;NO
	HRLZI	TB,LSTDAT+1(TA)	;YES, CLEAR BELOW THIS LEVEL
	HRRI	TB,LSTDAT+2(TA)
	SETZM	LSTDAT+1(TA)
	BLT	TB,LSTDAT+^D49
SAVLV1:	MOVEM	TA,DATLVL	;REMEMBER THIS LEVEL
	POPJ	PP,
;SET UP DATAB ENTRY FOR 01 LEVEL ITEMS

	INTER.	DA27.
DA27.:
IFN RPW,<
	SKIPL	REPSEC		;IN REPORT SECTION AND NOT PAGE- OR LINE-CTR?
	JRST	.+3		;NO
	SKIPN	NAMWRD		;YES, DOES ITEM HAVE A NAME?
	JRST	DA27.S		;NO
	>
	TLO	W2,GWDEF	;PUT DEFINING REFERENCE ON CREF FILE
	HRRZI	TB,(TYPE)	;(EXCEPT FILLERS)
	ANDI	TB,1777
	CAIE	TB,FILLE.
	PUSHJ	PP,PUTCRF##
	PUSHJ	PP,TRYNAM
	PUSHJ	PP,BLDNAM
	MOVEM	TA,CURNAM
	HLRZS	TA		;NAMTAB POINTER
	DPB	TA,[POINT	15,W2,15]
	TLZ	W1,GWNOT;
	HRRZI	TB,CD.DAT
	PUSHJ	PP,FNDLNK	;FIND DATTAB LINK
	JRST	DA27.N		;NONE
DA27.0:	MOVE	TA,TB		;GET LEVEL
	LDB	TC,DA.LVL
D27.01:	CAIE	TC,1
	CAIN	TC,77
	JRST	.+2
	JUMPN	TC,DA27.B	;NOT AN INDEPENDENT ITEM
	HRRZI	TB,(TYPE)
	ANDI	TB,1777
	CAIN	TB,FILLE.
	JRST	DA27.B		;FILLER
	MOVEM	TA,TBLOCK+15
	MOVEM	TB,TBLOCK+16
	HLRZ	TB,TA		;REL. ADDR. OF ITEM
	PUSHJ	PP,FNDPOP
	SETZ	TB,
IFN RPW,<
	SKIPLE	TD,REPSEC	;DOING REPORT SECTION PAGE- OR LINE-CTR?
	JUMPE	TB,D27.E3	;YES, IF OTHER ITEM HAS NO FATHER, ERROR
	JUMPN	TD,D27.B0	;ALL REPORT ITEMS NOW SKIP AHEAD
	>
	HLRZ	TD,CURFIL
	CAIE	TB,(TD)
	JRST	D27.B0
	TSWT	FFILSC;
	JRST	D27.E1		;IN W-S==ERROR
	MOVE	TA,TBLOCK+15
	HLRZM	TA,TBLOCK	;REL. ADDR. OF RECORD
	HRRZ	TA,CURFIL
	LDB	TB,FI.DRL	;DATA RECORDS CHAIN
DA27.A:	JUMPE	TB,DA27.C
	CAMN	TB,TBLOCK
	JRST	DA27.F		;IT IS A DATA RECORD
	PUSHJ	PP,FNDBRO	;FIND BROTHER LINK
	JRST	DA27.C		;NONE
	JRST	DA27.A
DA27.C:	HRRZ	TA,CURFIL
	LDB	TB,FI.LRL	;LABEL RECORDS CHAIN
D27.C1:	JUMPE	TB,D27.E2
	CAMN	TB,TBLOCK
	JRST	DA27.F		;IT IS A LABEL RECORD
	PUSHJ	PP,FNDBRO
	JRST	D27.E2
	JRST	D27.C1
D27.B0:	MOVE	TA,TBLOCK+15
	MOVE	TB,TBLOCK+16
DA27.B:	PUSHJ	PP,FNDNXT
	JRST	DA27.N
	JRST	DA27.0
DA27.N:	TSWF	FFILSC;
	JRST	D27.E2		;IN FILE SECTION---ERROR
DA27.S:	MOVE	TA,[XWD	CD.DAT,SZ.DAT]
D27N.1:	PUSHJ	PP,GETENT
	MOVEM	TA,CURDAT
	LDB	TB,[POINT	15,W2,15]
	DPB	TB,DA.NAM##
	HRRZI	TB,CD.DAT
	DPB	TB,[POINT	3,(TA),2]
	HRRZ	TB,LEVEL##
	DPB	TB,DA.LVL
	SETO	TB,
	DPB	TB,DA.CLA
	DPB	TB,DA.DEF
	SKIPN	LNKSEC		;LINKAGE SECTION?
	JRST	.+3		;NO
	SETZM	EAS1PC		;YES, RESTA PC
	DPB	TB,DA.LKS##	;  SET LINKAGE FLAG IN ENTRY
	TSWF	FFILSC;
	DPB	TB,DA.DFS
	DPB	W2,DA.LNC
	LDB	TB,DA.NAM
IFN RPW,<
	SKIPL	REPSEC		;RPW SECTION AND NOT PAGE- OR LINE-CTR?
	JRST	D29XIT		;NO
	SETZM	THSCTL##	;CLR STORE FOR CURRENT CF CONTROL
	HRRZ	TB,RPWRDL##	;LINK ITEM TO FATHER REPORT
	HRRZ	TA,CURDAT
	DPB	TB,DA.POP
	SETO	TB,
	DPB	TB,DA.FAL
	PUSHJ	PP,GETRDL	;MAKE PTR TO RD ENTRY
	LDB	TB,RW.FGP##	;GET LINK TO LAST GROUP ITEM SEEN
	HLRZ	TC,CURDAT	;GET LINK TO NEW GROUP
	DPB	TC,RW.FGP	;STORE LINK TO NEW GROUP IN RD ENTRY
	JUMPE	TB,D27XIT	;EXIT IF THIS WAS THE 1ST GROUP ITEM
	HRRZ	TA,CURDAT	;LINK NEW GROUP BACK TO LAST AS A BROTHER
	DPB	TB,DA.BRO
	SETZ	TB,
	DPB	TB,DA.FAL
D27XIT:	MOVE	TA,CURDAT	;SET UP FOR PUTLNK
	LDB	TB,DA.NAM
	JRST	D29XIT
	>

D27.E1:	SETO	TB,
	DPB	TB,DA.ERR
	EWARNJ	CE60.
D27.E2:	MOVE	TA,[XWD	CD.DAT,SZ.DAT+SZ.DOC+SZ.MSK]
	PUSHJ	PP,D27N.1
	HLRZ	TB,CURFIL
	JUMPE	TB,JCE13.
	HRRZ	TA,CURDAT
	DPB	TB,DA.POP##
	SETO	TB,
	DPB	TB,DA.FAL
	DPB	TB,DA.DRC
	DPB	TB,DA.PWA
	HRRZ	TA,CURFIL
	LDB	TB,FI.DRL
	HLRZ	TC,CURDAT
	DPB	TC,FI.DRL
	JUMPE	TB,D27E22
	HRRZ	TA,CURDAT
	DPB	TB,DA.BRO
	SETZ	TB,
	DPB	TB,DA.FAL
	HRRZ	TA,CURFIL
D27E22:	LDB	TB,FI.DRC
	JUMPE	TB,D27F.1
	EWARNW	CE228.
	JRST	D27F.1

DA27.F:	HRLZM	TB,CURDAT
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET##
	HRRM	TA,CURDAT
	LDB	TC,DA.DEF
	SETO	TB,
	DPB	TB,DA.DEF
	HRRZ	TB,LEVEL
	DPB	TB,DA.LVL
	DPB	W2,DA.LNC
	JUMPE	TC,D27F.1
	EWARNW	CE16.
D27F.1:	HRRZ	TB,EAS2PC##
	EXCH	TB,EAS1PC
	TLZE	TB,777777
	HRRZI	TB,1(TB)
	CAMLE	TB,CFLM##
	HRRZM	TB,CFLM
	POPJ	PP,


JCE13.:	EWARNJ	CE13.
JCE268:	EWARNJ	CE268.
JCE269:	EWARNJ	CE269.
JCE183:	EWARNJ	CE183.

;PAGE-COUNTER OR LINE-COUNTER INDEPENDENTLY DEFINED IN WORKING-STORAGE

D27.E3:	LDB	LN,DA.LN	;GET POSITION OF W-S ITEM
	LDB	CP,DA.CP
	HRRZI	DW,CE399.
	JRST	FATAL
;CHECK LEVEL NUMBER FOR ALL ITEMS BELOW 01 LEVEL

	INTER. DA28.
DA28.:	MOVE	TA,0(SAVPTR)
	JUMPLE	TA,DA28.E	;ERROR IF .LE. 0
	CAIN	TA,1
	JRST	DA28.A
IFN RPW,<
	SKIPN	REPSEC		;IN REPORT SECTION?
	JRST	DA28.0		;NO
	CAIG	TA,^D49		;YES, ONLY 01-49 ALLOWED
	POPJ	PP,		;OK
	JRST	DA28.E		;TOO BIG
	>
DA28.0:	PUSHJ	PP,SAVLVL	;LEAVE TRACKS FOR REDEFINES
	MOVE	TA,(SAVPTR)	;RESTORE TRUE LEVEL #
	CAIE	TA,115		;LEVEL 77
	JRST	DA28.B
	SETZM	RUSAGE		;77'S DON'T HAVE TO AGREE WITH PREVIOUS
	TSWF	FFILSC;
	EWARNW	CE46.
DA28.A:	PUSHJ	PP,DA7.
	JRST	DA0.
DA28.B:	CAIE	TA,130		;88-LEVEL?
	JRST	DA28.R
	HRRZI	NODE,DD93A.	;YES
	HRRZM	NODE,(NODPTR)
	POPJ	PP,
DA28.R:	CAIN	TA,102
	JRST	DA28.S		;LEVEL 66
	CAIG	TA,61
	POPJ	PP,
DA28.E:	EWARNW	CE64.		;LEVEL NUMBER NOT LEGAL
	HRRZ	TA,LEVEL
	CAILE	TA,0
	CAILE	TA,61
	HRRZI	TA,1
	MOVEM	TA,0(SAVPTR)
	JRST	DA28.
DA28.S:	HRRZI	NODE,DD86A.
	HRRZM	NODE,0(NODPTR)
	JRST	DA7.
;SET UP DATAB ENTRY FOR ALL ITEMS BELOW 01 LEVEL

	INTER.	DA29.
DA29.:
IFN RPW,<
	SKIPN	REPSEC		;IN REPORT SECTION?
	JRST	.+3		;NO
	SKIPN	NAMWRD		;YES, DOES ITEM HAVE A NAME?
	JRST	DA29.0		;NO
	>
	TLNN	W1,GWNOT
	JRST	DA29.0
	TLO	W2,GWDEF	;PUT DEFINING REFERENCE ON CREF FILE
	HRRZI	TB,(TYPE)	;IF ITEM IS FILLER, SKIP PUTCRF
	ANDI	TB,1777
	CAIE	TB,FILLE.
	PUSHJ	PP,PUTCRF
	PUSHJ	PP,TRYNAM
	PUSHJ	PP,BLDNAM
	TLZ	W1,GWNOT
	HLRZ	TB,TA
	DPB	TB,[POINT	15,W2,15]
DA29.0:	HRRZ	TC,0(SAVPTR)
	CAIN	TC,102		;LEVEL 66
	JRST	DA29.R
	CAIN	TC,115		;LEVEL 77
	HRRZI	TC,1
	HRRZ	TA,CURDAT
	JUMPE	TA,DA29.A	;NO CURRENT ITEM
	LDB	TB,DA.LVL
	CAIL	TB,(TC)
	JRST	DA29.B		;NEW ITEM SAME OR LOWER LEVEL NUMBER
DA29.A:	MOVE	TA,CURDAT
	JUMPE	TA,D29.A0
	LDB	TB,DA.USG
	CAIE	TB,%US.IN	;USAGE INDEX?
	JRST	D29.A0		;NO
	HRRZI	DW,342
	HRRZI	TB,%%US
	DPB	TB,DA.USG
	SETO	TB,
	MOVE	TA,CURDAT
	DPB	TB,DA.ERR
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	PUSHJ	PP,WARN##
D29.A0:	MOVE	TA,[XWD	CD.DAT,SZ.DAT]
	PUSHJ	PP,GETENT
	LDB	TB,[POINT	15,W2,15]
	DPB	TB,DA.NAM
	HRRZI	TB,CD.DAT
	DPB	TB,[POINT	3,(TA),2]
	HRRZ	TB,0(SAVPTR)
	CAIN	TB,115
	HRRZI	TB,77		;LEVEL 77
	HRRZM	TB,LEVEL
	DPB	TB,DA.LVL
	SETO	TB,
	DPB	TB,DA.CLA	;CLASS NOT YET KNOWN
	DPB	W2,DA.LNC
	DPB	TB,DA.DEF
	SKIPN	LNKSEC		;LINKAGE SECTION?
	JRST	D29.A2		;NO
	DPB	TB,DA.LKS	;YES, SET LINKAGE FLAG IN ENTRY
	LDB	TC,DA.LVL	;LEVEL 01 OR 77?
	CAIE	TC,77
	CAIN	TC,1
	SETZM	EAS1PC		;YES, RESET DATA PC
D29.A2:	TSWF	FFILSC;
	DPB	TB,DA.DFS
	SKIPN	CURDAT
	JRST	D29.A1
	DPB	TB,DA.FAL	;FATHER/BROTHER BIT
	HLRZ	TB,CURDAT	;SET TO INDICATE
	DPB	TB,DA.POP	;FATHER
D29.A1:	EXCH	TA,CURDAT	;TA==FATHER-TO-BE
	MOVE	TB,CURDAT	;TB==SON-TO-BE
	PUSHJ	PP,PUTSON	;SET UP SON CHAIN
	MOVE	TA,CURDAT
	JRST	D29.B1		;PUT IN SAME NAME CHAIN AND SET UP SUBSCRIPTS

DA29.B:	CAIE	TB,(TC)
	JRST	DA29.C		;NEW ITEM IS LOWER LEVEL NUMBER
	PUSHJ	PP,DA54.
	MOVE	TA,[XWD	CD.DAT,SZ.DAT]
	PUSHJ	PP,GETENT
	LDB	TB,[POINT	15,W2,15]
	DPB	TB,DA.NAM
	HRRZI	TB,CD.DAT
	DPB	TB,[POINT	3,(TA),2]
	HRRZ	TB,0(SAVPTR)
	CAIN	TB,115
	HRRZI	TB,77		;LEVEL 77
	HRRZM	TB,LEVEL
	DPB	TB,DA.LVL
	SETO	TB,
	DPB	TB,DA.CLA
	DPB	TB,DA.DEF
	SKIPN	LNKSEC		;LINKAGE SECTION?
	JRST	D29.B0		;NO
	DPB	TB,DA.LKS	;YES, SET LINKAGE FLAG IN ENTRY
	LDB	TC,DA.LVL	;01 OR 77 LEVEL?
	CAIE	TC,77
	CAIN	TC,1
	SETZM	EAS1PC		;YES, RESET DATA PC
D29.B0:	TSWF	FFILSC;
	DPB	TB,DA.DFS
	DPB	TB,DA.FAL
	DPB	W2,DA.LNC
	EXCH	TA,CURDAT
	HLRZ	TB,CURDAT
	LDB	TC,DA.POP
	DPB	TB,DA.BRO
	SETZ	TB,		;OLD ENTRY BECOMES BROTHER OF
	DPB	TB,DA.FAL	;NEW AND FATHER OF OLD IS FATHER
	MOVE	TA,CURDAT	;OF NEW
	DPB	TC,DA.POP
D29.B1:	LDB	TB,DA.NAM
	HRR	TA,TB
	PUSHJ	PP,PUTLNK
	HLRZ	TB,CURDAT
	PUSHJ	PP,FNDPOP
	JRST	RPWPOP		;EXIT
	LDB	TC,[POINT	3,TB,20]
	CAIE	TC,CD.DAT
	JRST	RPWPOP		;EXIT
	HRLZM	TB,TBLOCK+13
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	HRRM	TA,TBLOCK+13	;FATHER
	LDB	TD,DA.RDF##
	CAIN	TD,0
	LDB	TD,DA.RDH##	;SET TD TO 1 IF REDEFINES AT ANY HIGHER LEVEL
	LDB	TB,DA.VAL
	LDB	TC,DA.VHL
	CAIE	TB,0
	SETO	TC,
	HRRZ	TA,CURDAT
	DPB	TC,DA.VHL
	DPB	TD,DA.RDH	;REDEFINES AT HIGHER LEVEL FLAG
	MOVE	TA,TBLOCK+13
	LDB	TB,DA.SUB
	CAIN	TB,0
	JRST	RPWPOP		;EXIT
	LDB	TD,DA.OCH##
	LDB	TE,DA.OCC
	CAIE	TE,0
	HLRZ	TD,TBLOCK+13
	PUSH	PP,TD		;SAVE TD
	MOVE	TA,[CD.DAT,,SZ.DOC]	;MAKE DATAB WORDS 8,9
	PUSHJ	PP,GETENT
	POP	PP,TD		;RESTORE TD
	HRRZ	TA,CURDAT
D29.B2:	DPB	TD,DA.OCH
	SETO	TE,
	DPB	TE,DA.SUB
	JRST	RPWPOP		;EXIT

DA29.C:	PUSHJ	PP,DA54.
	HLRZ	TB,CURDAT
	SETZM	CURDAT
	PUSHJ	PP,FNDPOP
	JRST	DA29.
	LDB	TA,[POINT 3,TB,20]
	CAIE	TA,CD.DAT
	JRST	DA29.
	HRLZM	TB,CURDAT
	HRRZ	TA,TB
	PUSHJ	PP,LNKSET
	HRRM	TA,CURDAT
	JRST	DA29.
DA29.R:	SKIPE	CURDAT
	JRST	DA29.C
	MOVE	TA,[XWD CD.DAT,SZ.DAT]
	PUSHJ	PP,GETENT
	MOVEM	TA,CURDAT
	DPB	W2,DA.LNC
	LDB	TB,[POINT 15,W2,15]
	DPB	TB,DA.NAM
	HRRZI	TC,CD.DAT
	DPB	TC,[POINT 3,(TA),2]
	HRRZI	TC,76
	DPB	TC,DA.LVL
	SETO	TC,
	DPB	TC,DA.DEF
	DPB	TC,DA.CLA
	TSWF	FFILSC;
	DPB	TC,DA.DFS
D29XIT:	HRRI	TA,(TB)
	PUSHJ	PP,PUTLNK
RPWPOP:
IFN RPW,<
	SKIPL	REPSEC		;RPW SECTION?
	POPJ	PP,		;NO
	MOVE	TA,[CD.RPW,,SZ.RPG]	;GET A REPORT GROUP ENTRY IN RPWTAB
	PUSHJ	PP,GETENT
	MOVE	TB,RPWRDL	;STORE LINK TO RD ENTRY
	DPB	TB,RW.RDL
	MOVEM	TA,CURRPW##	;SAVE PTR
	MOVEI	TB,4		;SET RPG BIT
	DPB	TB,[POINT 3,(TA),2]
	HLRZ	TB,CURDAT	;STORE DATAB LINK IN RPWTAB
	DPB	TB,RW.DAT
	MOVE	TB,LASTYP	;COPY LAST TYPE SEEN INTO THIS ENTRY
	DPB	TB,RW.TYP
	HRRZ	TA,CURDAT	;STORE RPWTAB LINK IN DATAB
	HLRZ	TB,CURRPW
	DPB	TB,DA.RPW
	>
	POPJ	PP,
PUTSON:	SKIPE	TA
	SKIPN	TB
	POPJ	PP,
	LDB	TC,DA.SON##
	JUMPN	TC,PS.2
	HLRZ	TC,TB
	DPB	TC,DA.SON	;SON LINK
	EXCH	TA,TB
	HLRZ	TC,TB
	DPB	TC,DA.POP
	SETO	TC,
	DPB	TC,DA.FAL
	POPJ	PP,
PS.2:	MOVEM	TA,TBLOCK+3
	MOVEM	TB,TBLOCK+1
	HRRZM	TC,TBLOCK+2
	HRRZ	TB,TBLOCK+2
PS.3:	PUSHJ	PP,FNDBRO
	JRST	PS.4
	HRRZM	TB,TBLOCK+2
	JRST	PS.3
PS.4:	HRRZ	TA,TBLOCK+2
	PUSHJ	PP,LNKSET
	SETZ	TB,
	DPB	TB,DA.FAL
	HLRZ	TB,TBLOCK+1	;NEW SON
	DPB	TB,DA.BRO
	HRRZ	TA,TBLOCK+1
	HLRZ	TB,TBLOCK+3
	DPB	TB,DA.POP
	SETO	TB,
	DPB	TB,DA.FAL
	POPJ	PP,
;SET UP "REDEFINES"

	INTER.	DA30.
DA30.:	JUMPL	W1,JCE104	;EXIT IF NOT DEFINED
	MOVE	TC,DATLVL	;CURRENT LEVEL
	CAIE	TC,1		;DISALLOW REDEF AT 01 LEVEL IN FILE SECT.
	JRST	DA30.0
	TSWF	FFILSC;
	EWARNJ	CE66.
	HRRZ	TA,LAST01	;GET LINK TO LAST 01 ITEM
	JUMPE	TA,CPOPJ	;IF ITEM REDEFINES DUPLICATE,
				;  TREAT THIS AS AN ORDINARY DEFN
	PUSHJ	PP,LNKSET
	HLRZ	TB,CURDAT	;MAKE CURRENT ITEM LAST 01'S BROTHER
	DPB	TB,DA.BRO
	SETZ	TB,
	DPB	TB,DA.FAL
DA30.0:	LDB	TB,[POINT 15,W2,15]  ;GET NAMTAB LINK
	HRRZ	TA,LSTDAT(TC)	;LAST ITEM AT THIS LEVEL NOT A REDEF
DA30.1:	CAIN	TA,0		;GOOD LINK?
	HRRI	TA,<CD.DAT>B20+1	;NO, AIM AT DUMMY ENTRY
	PUSHJ	PP,LNKSET	;MAKE PTR
	LDB	TD,DA.NAM	;GET NAMTAB LINK
	CAIN	TB,(TD)		;THIS THE ONE WE ARE REDEFINING?
	JRST	DA30.2		;YES
	LDB	TD,DA.FAL	;FATHER BIT ON?
	JUMPN	TD,JCE266	;YES, NO MORE BROTHERS
	LDB	TD,DA.BRO	;TRY BROTHER
	HRRZI	TA,(TD)
	JUMPN	TD,DA30.1
JCE266:	EWARNJ	CE266.		;ILLEGAL REDEFINITION

DA30.2:	HRRZ	TA,CURDAT	;GET PTR TO CURRENT ITEM
	LDB	TB,DA.SUB##
	JUMPN	TB,JCE269	;NOT PERMITTED ON OCCURS ITEM
	MOVE	TB,EAS1PC
	MOVE	TE,RDFLVL##
	MOVEM	TB,RDEFPC##(TE)
	AOS	TE,RDFLVL	;UPDATE LVL COUNT
	CAIL	TE,RDFSIZ##	;SEE IF TOO DEEP
	JRST	[SOS RDFLVL
		 EWARNJ CE268.]
	MOVE	TC,DATLVL
	HRRZ	TA,LSTDAT(TC)
	PUSHJ	PP,LNKSET
	HRRZI	TB,44
	LDB	TC,DA.RES
	SUBI	TB,(TC)
	LDB	TC,DA.LOC##
	HRLI	TC,(TB)
	MOVEM	TC,EAS1PC
	MOVE	TA,CURDAT
	SETO	TB,
	DPB	TB,DA.RDF
	MOVE	TB,DATLVL	;IF 01 LEVEL
	CAIN	TB,1		;SAVE LINK
	HLRZM	TA,LAST01
	POPJ	PP,

;ITEM IS NOT A REDEFINITION -- REMEMBER THIS

	INTER.	DA30N.
DA30N.:
IFN RPW,<
	SKIPE	REPSEC		;NOT NEEDED IN REPORT SECTION
	JRST	DA7.
	>
	MOVE	TC,DATLVL
	HLRZ	TB,CURDAT
	MOVEM	TB,LSTDAT(TC)
	MOVE	TA,CURDAT	;PTR & LINK TO ITEM
	CAIN	TC,1		;01 LEVEL?
	HLRZM	TA,LAST01	;YES, STORE LINK
	JRST	DA7.		;WANT TO REGET THIS WORD
	INTER.	DA31.
DA31.:	LDB	TB,[POINT 9,W1,17]
	CAIE	TB,ZERO.
JCE18.:	EWARNJ	CE18.
	MOVE	TA,CURDAT
	JUMPE	TA,DA31.X
	LDB	TB,DA.CLA	;CLASS
	CAIE	TB,%%CL		;UNKNOWN
	CAIN	TB,%CL.NU	;NUMERIC
	JRST	DA31.A
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	HRRZI	DW,^D223
	JRST	FATAL
DA31.A:	LDB	TB,DA.BWZ##
	JUMPN	TB,DA24.E	;DUPLICATED
	SETO	TB,
	DPB	TB,DA.BWZ	;SET FLAG
DA31.X:	POPJ	PP,

	INTER. DA32.
DA32.:	MOVE 	TA,CURDAT
	JUMPE	TA,DA32.X
	LDB	TB,DA.JST
	JUMPN	TB,DA24.E	;DUPLICATED
	SETO	TB,
	DPB	TB,DA.JST
DA32.X:	POPJ	PP,
	INTER.	DA33.
DA33.:	PUSHJ	PP,DA11.	;GET NUMBER OF OCCURRENCES
	HRRZ	TC,0(SAVPTR)
	CAIGE	TC,1
	EWARNJ	CE25.
	CAIG	TC,77777
	JRST	DA33.A
	EWARNW	CE229.
	HRRZI	TC,77777		;ONLY 32K OCCURRENCES ALLOWED
	HRRZM	TC,0(SAVPTR)
DA33.A:	HRRZ	TA,CURDAT
	LDB	TB,DA.OCC
	JUMPN	TB,DA24.E
	HRRZ	TC,0(SAVPTR)
	DPB	TC,DA.NOC
	LDB	TB,DA.PWA	;DATAB WORDS 8,9 CREATED YET?
	LDB	TC,DA.SUB
	IORI	TB,(TC)
	JUMPN	TB,.+4		;YES
	MOVE	TA,[CD.DAT,,SZ.DOC]	;NO, DO IT
	PUSHJ	PP,GETENT
	HRRZ	TA,CURDAT	;RESTORE TA
	SETO	TB,
	DPB	TB,DA.OCC
	DPB	TB,DA.SUB
	POPJ	PP,
;SET UP INDEX FOR "INDEXED BY" CLAUSE

	INTER.	DA34.
DA34.:	PUSHJ	PP,DA60S.	;SAVE NAMTAB LINK IN CURNAM
	HRRZI	TB,CD.DAT	;(USING TA LEFT BY DA60S.)
	PUSHJ	PP,FNDLNK
	JRST	DA34.B		;NO LINK
	EWARNJ	CE297.		;BAD NAME
DA34.B:	TLO	W2,GWDEF	;PUT DEFINING REFERENCE ON CREF FILE
	PUSHJ	PP,PUTCRF
	PUSHJ	PP,DA25S.	;SET UP HLDTAB ENTRY
	HRRZI	TB,%HL.IX	;  BECAUSE PUTTING INDEX IN DATAB NOW
	DPB	TB,HL.COD	;  MIGHT PUT IT BETWEEN THE ITEM INDEXED
	HLRZ	TB,CURDAT	;  AND ITS SEARCH KEYS
	DPB	TB,HL.LNK
	HRRZ	TA,CURDAT	;PUT HLDTAB LINK IN DATAB ENTRY
	HLRZ	TB,CURHLD	;  "INDEXED BY" FIELD
	LDB	TC,DA.XBY##	;  UNLESS ONE HAS BEEN STORED ALREADY
	JUMPN	TC,.+2
	DPB	TB,DA.XBY
	POPJ	PP,
	INTER.	DA35.
DA35.:	SKIPN	TA,CURDAT
	JRST	DA35.1
	LDB	TB,DA.PIC
	JUMPE	TB,DA35.1
	EWARNW	CE16.	;CLAUSE DUPLICATED
DA35.1:	PUSHJ	PP,PSCAN
	HRRZ	TA,CURDAT
	JUMPE	TA,DA35.X
	LDB	TB,DA.PIC
	JUMPN	TB,DA35.X	;PICTURE SEEN BEFORE
	SETO	TB,
	DPB	TB,DA.PIC
	LDB	TB,DA.USG
	CAIN	TB,%US.IN	;USAGE INDEX?
	JRST	DA35.E		;YES
	CAIN	TB,%US.C1	;COMP-1?
	JRST	DA35.E		;YES
	DPB	SW,DA.CLA	;BITS 34-35 OF SW ARE CLASS
	SETZ	TB,
	TSWF	FSIGN;		;SIGNED?
	SETO	TB,		;YES
	DPB	TB,DA.SGN
	HRRZ	TB,INSIZE##
	DPB	TB,DA.INS##	;INTERNAL SIZE
	HRRZ	TB,EXSIZE##
	DPB	TB,DA.EXS	;EXTERNAL SIZE
	SKIPL	TB,DPSIZE##
	JRST	DA35.A
	SETO	TB,
	DPB	TB,DA.DPR##	;DECIMAL POINT TO RIGHT OF ITEM
	MOVN	TB,DPSIZE
DA35.A:	DPB	TB,DA.NDP##	;NUMBER OF DECIMAL PLACES
	TSWT	FEDIT;
	JRST	DA35.X		;NOT EDITED
	SETO	TB,
	DPB	TB,DA.EDT##
	SKIPN	FLOTBZ##	;PICTURE ALL FLOAT CHARS & NO 9'S?
	TSWF	FBWZ;
	DPB	TB,DA.BWZ
	SKIPG	MSKSIZ##
	JRST	DA35.X		;NO MASK
DA35.B:	HRRZ	TB,MSKSIZ
	CAILE	TB,SZ.MSK
	HRRZI	TB,SZ.MSK
	HRRZM	TB,MSKSIZ
	LDB	TB,DA.PWA
	JUMPE	TB,D35B.1
	ADDI	TA,SZ.DAT+SZ.DOC
	JRST	D35B.0
D35B.1:	LDB	TB,DA.SUB	;WORDS 8&9 ALLOCATED YET?
	JUMPN	TB,.+4		;YES
	MOVE	TA,[CD.DAT,,SZ.DOC]	;NO, DO IT
	PUSHJ	PP,GETENT
	HRRZ	TA,CURDAT
	SETO	TB,
	DPB	TB,DA.PWA
	MOVE	TA,[CD.DAT,,SZ.MSK]
	PUSHJ	PP,GETENT	;GET ENTRY FOR MASK
D35B.0:	HRRZ	TC,MSKSIZ
	ADDI	TC,-1(TA)	;LAST WORD FOR STORING MASK
	HRLI	TA,MSKWRD##
	BLT	TA,(TC)		;MOVE MASK
DA35.X:	POPJ	PP,
DA35.E:	HRRZI	DW,335
	LDB	LN,DA.LN
	LDB	CP,DA.CP##
	JRST	WARN
D35E.1:	EWARNJ	CE16.		;CLAUSE DUPLICATED

	INTER.	DA36.
DA36.:	MOVE	TA,CURDAT
	JUMPE	TA,DA36.X	;NO DATTAB LINK
	LDB	TB,DA.SYL##	;SYNC LEFT?
	JUMPN	TB,JCE18.	;YES--ERROR
	LDB	TB,DA.SYR##	;ALREADY SYNC RIGHT?
	JUMPN	TB,DA24.E	;YES
	SETO	TB,
	DPB	TB,DA.SYR
DA36.X:	POPJ	PP,

	INTER.	DA37.
DA37.:	MOVE 	TA,CURDAT
	JUMPE	TA,DA37.X
	LDB	TB,DA.SYR
	JUMPN	TB,JCE18.
	LDB	TB,DA.SYL
	JUMPN	TB,DA24.E
	SETO	TB,
	DPB	TB,DA.SYL
DA37.X:	POPJ	PP,
	INTER.	DA38.
DA38.:	HRRZI	TC,%US.D7	;USAGE CODE 'DISPLAY-7'
SETUSG:	HRRZ	TA,CURDAT
	JUMPE	TA,DA38.X
	LDB	TB,DA.USG	;GET USAGE FIELD
	CAIE	TB,%%US
	EWARNJ	CE16.
	HRRZM	TC,TBLOCK	;USAGE OF SON
	HLRZ	TB,CURDAT
DA38.A:	PUSHJ	PP,FNDPOP
	JRST	DA38KL		;NO FATHER
	LDB	TC,[POINT	3,TB,20]
	CAIE	TC,CD.DAT
	JRST	DA38KL		;FATHER NOT DATTAB
	HRRZM	TB,TBLOCK+1	;FATHER LINK
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	LDB	TC,DA.USG
	HRRZ	TB,TBLOCK+1
	CAIN	TC,%%US
	JRST	DA38.A		;USAGE OF FATHER NOT KNOWN
	HRRZ	TB,TBLOCK	;USAGE OF SON
	CAIN	TB,(TC)		;SAME USAGE FOR BOTH FATHER AND SON?
	JRST	DA38.K		;YES
	CAIN	TC,%US.D6	;IS FATHER DISPLAY-6?
	JRST	DA38.B		;YES
	CAIN	TC,%US.D7	;IS FATHER DISPLAY-7?
	JRST	DA38.C		;YES
DA38.D:	HRRZI	DW,CE41.	;CONFLICT WITH HIGHER-LEVEL USAGE
DA38.E:	HRRZ	TA,CURDAT
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	PUSHJ	PP,FATAL##
	POPJ	PP,
DA38.B:	CAIN	TB,%US.D7	;SON'S USAGE DISPLAY-7?
	JRST	D38.E2		;YES--BAD NEWS
	JRST	DA38.K
DA38.C:	CAIN	TB,%US.D6	;SON'S USAGE DISPLAY-6?
	JRST	D38.E2		;YES--BAD NEWS
DA38.N:	HLRZ	TB,CURDAT
	PUSHJ	PP,FNDPOP
	JRST	DA38.K		;NO FATHER
	LDB	TC,[POINT	3,TB,20]
	CAIE	TC,CD.DAT
	JRST	DA38.K		;FATHER NOT DATTAB
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	LDB	TB,DA.SON
	JUMPE	TB,DA38.K	;NO SON
D38.N1:	HRRZM	TB,TBLOCK+1	;SON LINK
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	LDB	TB,DA.USG	;USAGE OF SON
	CAIN	TB,%%US
	JRST	D38.NL		;NOT SPECIFIED
	CAMN	TB,TBLOCK
	JRST	DA38.K		;SAME USAGES
	CAIN	TB,%US.D7
	JRST	D38.E2		;ONE IS DISPLAY-7
	HRRZ	TB,TBLOCK
	CAIN	TB,%US.D7
	JRST	D38.E2		;ONE IS DISPLAY-7
	JRST	DA38.K		;NEITHER IS DISPLAY-7
D38.NL:	HRRZ	TB,TBLOCK+1
	PUSHJ	PP,FNDBRO	;FIND BROTHER
	JRST	DA38.K		;NO MORE
	JRST	D38.N1
DA38.K:	HRRZ	TB,TBLOCK
	SKIPN	TC,RUSAGE	;SEEN A USAGE ITEM YET?
	JRST	DA38KK		;NO
	CAIN	TB,(TC)		;YES, CURRENT USAGE = RUSAGE?
	JRST	DA38KL		;YES, OK
	CAIE	TC,%US.D7	;NO, IS EITHER DISPLAY-7?
	CAIN	TB,%US.D7
	JRST	DA38.D		;YES, NO FAIR
	JRST	DA38KL
DA38KK:	MOVEI	TC,%US.D6	;PREPARE FOR DISPLAY-6
	CAIN	TB,%US.D7	;IS THIS ITEM DISPLAY-7?
	MOVEI	TC,%US.D7	;THEN MAKE IT THAT
	MOVEM	TC,RUSAGE
DA38KL:	HRRZ	TA,CURDAT
	HRRZ	TB,TBLOCK
	DPB	TB,DA.USG
DA38.X:	POPJ	PP,
D38.E1:	HRRZI	DW,342		;USAGE INDEX INVALID FOR GROUP
	JRST	DA38.E
D38.E2:	JRST	DA38.D		;CAN'T MIX DISPLAY-7 WITH ANYTHING


	INTER.	DA39.
DA39.:	HRRZI	TC,%US.D6	;USAGE 'DISPLAY-6'
	JRST	SETUSG

	INTER.	DA40.
DA40.:	HRRZI	TC,%US.1C	;USAGE 'COMP'
	JRST	SETUSC
	
INTER.	DA41.
DA41.:	HRRZI	TC,%US.C1	;USAGE 'COMP-1'
	JRST	SETUSC

	INTER.	DA43.
DA43.:	HRRZI	TC,%US.IN
SETUSC:
IFE RPW,<
	JRST	SETUSG
	>
IFN RPW,<
	SKIPN	REPSEC		;IN REPORT SECTION?
	JRST	SETUSG		;NO
	EWARNW	CE349.		;?ILLEGAL USAGE IN REPORT GROUP
	JRST	DA73.X		;NEXT NODE IS DD144.
	>

	INTER.	DA44.
DA44.:	MOVE	TA,DA.VAL##
	MOVEM	TA,PNTS##
	POPJ	PP,

	INTER.	DA45.
DA45.:	MOVE	TA,LEVEL
	CAIN	TA,1
	POPJ	PP,
	CAIN	TA,77		;77
	POPJ	PP,
	HRRZI	TA,1
	MOVEM	TA,LEVEL
	EWARNJ	CE64.

FNDPOP:	JUMPN	TB,.+2
NOPOP:	POPJ	PP,
	LDB	TC,[POINT	3,TB,20]
	CAIE	TC,CD.DAT
	POPJ	PP,		;NOT DATTAB ENTRY
POP.0:	HRRZM	TB,TBLOCK+13
	HRRZ	TA,TB
	PUSHJ	PP,LNKSET
	LDB	TB,DA.FAL
	JUMPN	TB,POP.1
	HRRZ	TB,TBLOCK+13
	PUSHJ	PP,FNDBRO
	JRST	NOPOP
	JRST	POP.0
POP.1:	LDB	TB,DA.POP
	JUMPN	TB,.+2
	POPJ	PP,		;NULL FATHER LINK
	POP	PP,TA
	JRST	1(TA)


FNDBRO:	JUMPN	TB,.+2
NOBRO:	POPJ	PP,
	LDB	TC,[POINT	3,TB,20]
	CAIE	TC,CD.DAT
	POPJ	PP,
	HRRZ	TA,TB
	PUSHJ	PP,LNKSET
	LDB	TC,DA.FAL
	JUMPE	TC,BRO.1
	POPJ	PP,
BRO.1:	LDB	TB,DA.BRO
	JUMPN	TB,.+2
	POPJ	PP,
	POP	PP,TA
	JRST	1(TA)


	INTER.	DA46.
DA46.:	TLO	W2,GWDEF	;PUT DEFINING REFERENCE ON CREF FILE
	PUSHJ	PP,PUTCRF
	PUSHJ	PP,TRYNAM
	PUSHJ	PP,BLDNAM
	MOVEM	TA,CURNAM
	HLRZS	TA
	DPB	TA,[POINT	15,W2,15]
	TLZ	W1,GWNOT
	HRRZI	TB,CD.CON
	PUSHJ	PP,FNDLNK
	JRST	DA46.B		;NO CONDITION OF THIS NAME
DA46.A:	MOVE	TA,TB
	LDB	TC,CO.DAT
	HLRZ	TD,CURDAT
	CAIN	TC,(TD)
	EWARNJ	CE230.		;DUPLICATE CONDITION FOR THIS DATTAB ITEM
	PUSHJ	PP,FNDNXT
	JRST	DA46.B		;NO MORE
	JRST	DA46.A
DA46.B:	MOVE	TA,[XWD	CD.CON,SZ.CON]
	PUSHJ	PP,GETENT
	MOVEM	TA,CURCON##
	HLRZ	TB,CURNAM
	ANDI	TB,77777
	IORI	TB,CD.CON*1B20
	MOVSM	TB,(TA)
	HLRZ	TB,CURDAT##
	DPB	TB,CO.DAT
	LDB	TB,CO.NAM##
	HRRI	TA,(TB)
	JRST	PUTLNK##
;STORE VALUE ON CONTAB ENTRY FOR 88 ITEM

	INTER.	DA47.
DA47.:	TLNE	W1,GWFIGC	;FIGURATIVE CONSTANT?
	JRST	DA47.C		;YES
	TLNN	W1,GWLIT
	EWARNJ	CE45.		;LITERAL EXPECTED
	HLRZ	TC,W1
	ANDI	TC,177		;SIZE
	HRLM	TC,TBLOCK+13	;SAVE NO. OF CHARACTERS
	IDIVI	TC,5
	JUMPE	TB,.+2
	HRRZI	TC,1(TC)
	HRRM	TC,TBLOCK+13	;SAVE NO. OF WORDS
	HRRZI	TA,SZ.LIT(TC)
	HRLI	TA,CD.LIT
	PUSHJ	PP,GETENT
	HLR	W1,TA
	MOVEM	TA,CURLIT
	HLRZ	TC,TBLOCK+13
	DPB	TC,LI.NCH
	SETO	TD,
	TLNE	W1,GWASCI	;ANY PURE ASCII CHARACTERS?
	DPB	TD,LI.PUR##
	TLNE	W1,GWALL
	DPB	TD,LI.ALL
	TLNN	W1,GWNLIT	;NUMERIC?
	JRST	DA47.B		;NO
	DPB	TD,LI.NLT
	TLNN	W1,GWDP
	DPB	TD,LI.INT##
DA47.B:	HRRZ	TC,TBLOCK+13	;NO. OF WORDS
	JUMPE	TC,JCE183	;NULL LITERAL
	ADDI	TC,SZ.LIT-1(TA)
	HRRZI	TB,SZ.LIT(TA)
	HRLI	TB,LITVAL
	BLT	TB,(TC)
	POPJ	PP,
DA47.C:	MOVE	TA,[XWD CD.LIT,SZ.LIT]
	PUSHJ	PP,GETENT
	HLR	W1,TA
	MOVEM	TA,CURLIT
	LDB	TC,[POINT	9,W1,17]
	DPB	TC,LI.FCC
	SETO	TC,
	DPB	TC,LI.FGC
	POPJ	PP,

;ILLEGAL VALUE FOR CONDITION

	INTER.	DA47E.
DA47E.:	SWOFF	FREGWD		;CLEAR REGET ITEM BIT
	TLO	W1,GWFIGC	;SET FIG. CON. FLAG
	MOVEI	TB,SPACE.	;ASSUME "SPACES"
	DPB	TB,[POINT 9,W1,17]
	PUSHJ	PP,DA47.	;PUT ASSUMED VALUE IN CONTAB
	EWARNJ	CE258.		;"?LITERAL OR FIG. CON. REQUIRED"
	INTER.	DA48.
DA48.:	SKIPN	CURCON
	POPJ	PP,
	MOVE	TA,[XWD	CD.CON,1]
	PUSHJ	PP,GETENT
	HLRZ	TB,CURLIT
	ANDI	TB,077777
	MOVSM	TB,(TA)
	SETZM	CURLIT
	HRRZ	TA,CURCON
	LDB	TB,CO.NVL
	HRRZI	TB,1(TB)
	DPB	TB,CO.NVL
	POPJ	PP,


	INTER.	DA49.
DA49.:	PUSHJ	PP,DA48.
	PUSHJ	PP,DA47.
	SKIPN	TA,CURCON
	POPJ	PP,
	LDB	TB,CO.NVL
	ADDI	TB,SZ.CON-1(TA)
	HLRZ	TC,CURLIT
	ANDI	TC,077777
	HLL	TC,(TB)
	TLO	TC,400000
	MOVEM	TC,(TB)
	SETZM	CURLIT
	POPJ	PP,


	INTER.	DA50.
DA50.:	POPJ	PP,


	INTER.	DA51.
DA51.:	SETZM	RENAM1##
	SETZM	RENAM2##
	TLNE	W1,GWNOT
	EWARNJ	CE17.
	LDB	TA,[POINT 15,W2,15]
	HRRZI	TB,CD.DAT
	PUSHJ	PP,FNDLNK
	EWARNJ	CE17.
	MOVEM	TB,RENAM1
	POPJ	PP,
	INTER.	DA52.
DA52.:	TLNE	W1,GWNOT
	EWARNJ	CE17.
	LDB	TA,[POINT 15,W2,15]
	HRRZI	TB,CD.DAT
	PUSHJ	PP,FNDLNK
	EWARNJ	CE17.
	MOVEM	TB,RENAM2
	POPJ	PP,
	INTER.	DA53.
DA53.:	SKIPE	RENAM2
	JRST	DA53.2
	SKIPN	TA,RENAM1
	JRST	DA53.X
	SETZ	TB,		;CK RENAMED ITEM FOR
	LDB	TC,DA.PWA##	;  PICTURE WORDS ALLOCATED
	CAIN	TC,0
	LDB	TB,DA.SUB	;  OR SUBSCRIPTING
	IMULI	TB,SZ.DOC	;IF SO, ALLOCATE EXTRA WORDS
	IMULI	TC,SZ.DOC+SZ.MSK	;FOR RENAMING ITEM
	ADDI	TC,(TB)
	JUMPE	TC,DA53.1	;NO EXTRAS NEEDED
	PUSH	PP,TC
	MOVEI	TA,(TC)
	HRLI	TA,CD.DAT
	PUSHJ	PP,GETENT
	HLRZ	TA,RENAM1
	PUSHJ	PP,LNKSET
	HRRM	TA,RENAM1
	POP	PP,TC
	ADDI	TC,SZ.DAT
DA53.1:	SKIPN	TB,CURDAT
	POPJ	PP,
	HRRZI	TB,1(TB)		;WORD 2 OF 66 ENTRY
	HRLI	TB,1(TA)		;WORD 2 OF RENAMED ENTRY
	ADDI	TC,-2(TB)		;LAST WORD OF 66 ENTRY
	BLT	TB,(TC)
	HRRZ	TA,CURDAT
	SETZ	TB,
	DPB	TB,DA.POP
	DPB	TB,DA.SON
	DPB	TB,DA.VAL
	HRRZI	TC,76		;LEVEL 66
	DPB	TC,DA.LVL
	SETO	TC,
	DPB	TC,DA.FAL
D53.11:	HLRZ	TB,RENAM1
	PUSHJ	PP,FNDPOP
	JRST	DA53.X
	HRLM	TB,RENAM1
	LDB	TC,[POINT 3,TB,20]
	CAIE	TC,CD.DAT
	JRST	D53.12
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	LDB	TC,DA.LVL
	CAIE	TC,1
	JRST	D53.11
D53.12:	HLRZ	TB,RENAM1
	HRRZ	TA,CURDAT
	DPB	TB,DA.POP
DA53.X:	SETZM	CURDAT
	POPJ	PP,
DA53.2:	HLRZ	TA,RENAM1
	PUSHJ	PP,LNKSET
	HRRM	TA,RENAM1
	LDB	TB,DA.LVL
	CAIN	TB,1
	JRST	D53E.1		;ILLEGAL LEVEL
	CAIE	TB,77
	CAIN	TB,76
	JRST	D53E.1
	LDB	TB,DA.SUB
	JUMPN	TB,D53E.2	;MAY NOT RENAME ITEMS WITH OCCURS
	HLRZ	TB,RENAM1
D53R.1:	PUSHJ	PP,FNDPOP
	JRST	D53E.3		;NO RECORD FOUND
	HLRZ	TC,RENAM2
	CAIN	TC,(TB)
	JRST	D53E.4		;FIRST ITEM SUBSIDIARY TO SECOND
	HRLZM	TB,RNREC1##
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	HRRM	TA,RNREC1
	LDB	TC,DA.LVL
	HLRZ	TB,RNREC1
	CAIE	TC,1
	JRST	D53R.1		;NOT YET UP TO RECORD
	HLRZ	TA,RENAM2
	PUSHJ	PP,LNKSET
	HRRM	TA,RENAM2
	LDB	TB,DA.LVL
	CAIN	TB,1
	JRST	D53E.1
	CAIN	TB,77
	JRST	D53E.1
	CAIN	TB,76
	JRST	D53E.1
	LDB	TB,DA.SUB
	JUMPN	TB,D53E.2
	HLRZ	TB,RENAM2
D53R.2:	PUSHJ	PP,FNDPOP
	JRST	D53E.3
	HLRZ	TC,RENAM1
	CAIN	TC,(TB)
	JRST	D53E.4	;SECOND ITEM SUBSIDIARY TO FIRST
	HRLZM	TB,RNREC2##
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	HRRM	TA,RNREC2
	LDB	TC,DA.LVL
	HLRZ	TB,RNREC2
	CAIE	TC,1
	JRST	D53R.2
	HLRZ	TC,RNREC1
	CAIE	TC,(TB)
	JRST	D53E.5		;ITEMS NOT IN SAME RECORD
	HRRZ	TA,RENAM1
	LDB	TB,DA.LOC
	LDB	TC,DA.RES
	HRRZ	TA,RENAM2
	LDB	TD,DA.LOC
	LDB	TE,DA.RES
	SUBI	TD,(TB)		;L2-L1
	SUBI	TC,(TE)		;R1-R2
	IMULI	TD,44
	ADD	TD,TC
	JUMPLE	TD,D53E.6	;SECOND ITEM IS BEFORE FIRST
	HRRZI	TE,6
	LDB	TB,DA.USG
	CAIN	TB,%US.D7
	HRRZI	TE,5		;DISPLAY-7
	IDIVI	TD,44
	IMULI	TD,(TE)		;NO. CHARACTERS IN WHOLE WORDS
	CAIE	TE,6
	HRRZI	TE,7		;SIZE OF DISPLAY-7 CHARACTER IN BITS
	IDIVI	TC,(TE)
	ADDI	TD,(TC)
	HRRZ	TA,RENAM2
	LDB	TB,DA.INS
	ADDI	TD,(TB)
	HRRZ	TA,RENAM1
	LDB	TB,DA.LOC
	LDB	TC,DA.RES
	HRRZ	TA,CURDAT
	DPB	TB,DA.LOC
	DPB	TC,DA.RES
	CAILE	TD,MAXWSS##
	JRST	D53E.7
	DPB	TD,DA.INS
	DPB	TD,DA.EXS
	CAIE	TE,6
	SKIPA	TE,[EXP %US.D7]
	HRRZI	TE,%US.D6	;DISPLAY-6
	DPB	TE,DA.USG
	HLRZ	TB,RNREC1
	DPB	TB,DA.POP
	SETO	TB,
	DPB	TB,DA.FAL
	HRRZI	TB,%CL.AN
	DPB	TB,DA.CLA	;CLASS IS ALPHANUMERIC
	SETZM	CURDAT
	POPJ	PP,
D53E.1:	HRRZI	DW,375
	JRST	DA53.E
D53E.2:	HRRZI	DW,376
	JRST	DA53.E
D53E.3:	HRRZI	DW,377
	JRST	DA53.E
D53E.4:	HRRZI	DW,400
	JRST	DA53.E
D53E.5:	HRRZI	DW,377
	JRST	DA53.E
D53E.6:	HRRZI	DW,401
	JRST	DA53.E
D53E.7:	HRRZI	DW,^D316
DA53.E:	SKIPN	TA,CURDAT
	JRST	D53E.X
	LDB	LN,DA.LN
	LDB	CP,DA.CP
D53E.X:	SETZM	CURDAT
	JRST	FATAL
	INTER.	DA54.
DA54.:	SKIPN	TA,CURDAT
	JRST	DA54.X
	LDB	TB,[POINT 3,TA,2]
	CAIE	TB,CD.DAT
	SETZB	TA,CURDAT
	JUMPE	TA,DA54.X
	LDB	TB,DA.LVL
	CAIN	TB,76		;LEVEL 66?
	JRST	D54XXX		;YES
	LDB	TB,DA.DEF##
	JUMPE	TB,DA54.X
	LDB	TB,DA.SON
	JUMPN	TB,DA54.A	;GROUP ITEM
	SWON	ELITEM;
	JRST	DA54.B
D54E.E:	LDB	LN,DA.LN
	LDB	CP,DA.CP
	HRRZI	DW,334		;PICTURE REQUIRED
	SETO	TB,
	DPB	TB,DA.ERR##
	PUSHJ	PP,FATAL
	JRST	D54C.0
DA54.Z:	JUMPE	TB,DA54.Y	;PICTURE MUST NOT HAVE APPEARED FOR INDEX OR COMP-1
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	HRRZI	DW,335		;PICTURE NOT ALLOWED
	PUSHJ	PP,WARN
	HRRZ	TA,CURDAT
	LDB	TC,DA.USG
DA54.Y:	CAIN	TC,%US.C1
	SKIPA	TB,[EXP ^D8]	;SIZE OF COMP-1
	HRRZI	TB,5		;SIZE OF INDEX
	DPB	TB,DA.EXS##
	DPB	TB,DA.INS
	HRRZI	TB,%CL.NU
	DPB	TB,DA.CLA	;CLASS IS NUMERIC
	SETO	TB,
	DPB	TB,DA.SGN	;SIGNED
	JRST	D54C.0
DA54.A:	SWOFF	ELITEM;
	HRRZI	TB,%CL.AN
	DPB	TB,DA.CLA	;CLASS IS ALPHANUMERIC FOR GROUP ITEMS
	LDB	TB,DA.USG
	CAIE	TB,%US.IN	;USAGE INDEX?
	JRST	D54A.1		;NO
	HRRZI	DW,342		;USAGE INDEX INVALID FOR GROUPS
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	IFN	DEBUG,<
	PUSHJ	PP,WARN
>
	IFE	DEBUG,<
	PUSHJ	PP,FATAL
>
	HRRZ	TA,CURDAT
D54A.1:	LDB	TB,DA.PIC##	;PICTURE SEEN?
	JUMPE	TB,DA54.B	;NO
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	HRRZI	DW,335		;PICTURE NOT ALLOWED
	PUSHJ	PP,WARN
	HRRZ	TA,CURDAT
	SETZ	TB,
	DPB	TB,DA.EXS
	DPB	TB,DA.INS
	DPB	TB,DA.EDT
	DPB	TB,DA.NDP
	DPB	TB,DA.DPR
DA54.B:	HRRZ	TA,CURDAT
	LDB	TB,DA.USG	;USAGE
	CAIE	TB,%%US
	JRST	DA54.C		;USAGE KNOWN
	HLRZ	TB,CURDAT
D54B.L:	PUSHJ	PP,FNDPOP
	JRST	D54.B0		;NO FATHER
	LDB	TC,[POINT	3,TB,20]
	CAIE	TC,CD.DAT
	JRST	D54.B0
	HRRZM	TB,TBLOCK	;FATHER LINK
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	LDB	TC,DA.USG	;USAGE OF FATHER
	HRRZ	TB,TBLOCK	;FATHER LINK
	JUMPE	TC,D54B.L	;NO USAGE AT THIS LEVEL
	HRRZ	TA,CURDAT
	DPB	TC,DA.USG	;USAGE OF FATHER IS USAGE OF SON
	JRST	DA54.C
D54.B0:	HRRZ	TA,CURDAT
	LDB	TA,DA.SON	;SON LINK
	JUMPE	TA,D54B06	;NO SON--ASSUME DISPLAY-6
	PUSHJ	PP,LNKSET
	LDB	TB,DA.USG	;USAGE OF SON
	CAIE	TB,%US.D7	;DISPLAY-7?
	JRST	D54B06		;NO
	SKIPN	RUSAGE		;IF RUSAGE NOT SET, MAKE IT = SON
	MOVEM	TB,RUSAGE
	MOVE	TC,RUSAGE	;YES, CK IF RUSAGE HAS BEEN SET TO D-6
	CAIE	TC,%US.D7	;IF SO, ERROR MSG HAS ALREADY BEEN SENT
D54B06:	HRRZI	TB,%US.D6	;NO -- ASSUME DISPLAY-6
	SKIPE	RUSAGE		;USAGE SEEN ON BROTHER?
	CAMN	TB,RUSAGE	;YES, IS BROTHER THE SAME?
	JRST	D54B07		;YES, OK AS IT IS
	HRRZ	TA,CURDAT	;NO
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	HRRZI	DW,CE41.	;SIXBIT AFTER EARLIER ASCII IS ILLEGAL
	PUSHJ	PP,WARN
	HRRZI	TB,%US.D6	;HOPE D6 IS OK
D54B07:	MOVEM	TB,RUSAGE	;REMEMBER IN CASE MORE BROTHERS
	HRRZ	TA,CURDAT
	DPB	TB,DA.USG
DA54.C:	TSWT	ELITEM;
	JRST	D54C.2		;GROUP ITEM
	HRRZ	TA,CURDAT
	LDB	TB,DA.PIC
	LDB	TC,DA.USG
	CAIE	TC,%US.IN
	CAIN	TC,%US.C1
	JRST	DA54.Z		;INDEX OR COMP-1
	JUMPE	TB,D54E.E	;PICTURE REQUIRED FOR OTHERS
D54C.0:	HRRZ	TA,CURDAT
	LDB	TB,DA.CLA
	CAIN	TB,%%CL
	JRST	D54C.1		;CLASS UNKNOWN
	LDB	TC,DA.USG
	CAIN	TC,%US.D6
	JRST	D54C.1		;DISPLAY-6 --- OK
	CAIN	TC,%US.D7
	JRST	D54C.1		;DISPLAY-7 --- OK
	CAIE	TB,%CL.NU	;NUMERIC?
	JRST	D54C.E		;NO --- ERROR
	LDB	TD,DA.EDT
	JUMPE	TD,D54C.1	;OK IF NOT EDITED
D54C.E:	LDB	LN,DA.LN
	LDB	CP,DA.CP
	HRRZI	DW,364		;PICTURE AND USAGE CONFLICT
	SETO	TC,
	DPB	TC,DA.ERR
	PUSHJ	PP,FATAL
D54C.1:	JRST	DA54.D		;ELEMENTARY ITEM
D54C.2:	HRRZ	TA,CURDAT
	LDB	TA,DA.SON	;SON LINK
	JUMPE	TA,DA54.D	;NO SON
	PUSHJ	PP,LNKSET
	LDB	TB,DA.LOC	;LOCATION OF LEFTMOST SON
	LDB	TC,DA.RES	;RESIDUE OF LEFTMOST SON
	LDB	TD,DA.SYR
	JUMPE	TD,.+2
	HRRZI	TC,44		;IF ITEM WAS SYNC RIGHT, HE STARTED A NEW WORD
	HRRZ	TA,CURDAT
	DPB	TB,DA.LOC
	DPB	TC,DA.RES
	HLRZ	TD,EAS1PC	;NO. BITS USED IN CURRENT WORD
	HRRZ	TE,EAS1PC	;CURRENT LOCATION
	ADDI	TC,(TD)		;NO. BITS USED AS PARTIAL WORDS
	SUBI	TE,1(TB)	;NO. FULL WORDS USED
	IMULI	TE,44		;NO. BITS IN FULL WORDS USED
	ADDI	TE,(TC)		;TOTAL NO. BITS USED
	IDIVI	TE,44		;NO. OF FULL WORDS USED
	LDB	TB,DA.USG
	IDIV	TD,BYTSIZ(TB)	;NO. BYTES IN PART WORD
	IMUL	TE,NCW(TB)	;NO. BYTES IN FULL WORDS
	ADDI	TE,(TD)		;TOTAL NO. BYTES
	CAIG	TE,MAXWSS
	JRST	D54C21
	HRRZ	TA,CURDAT
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	HRRZI	DW,^D316
	PUSHJ	PP,FATAL
	HRRZI	TE,MAXWSS
D54C21:	DPB	TE,DA.EXS
	DPB	TE,DA.INS
	HRRZ	TA,CURDAT	;CHECK FOR SYNC AT NEXT LEVEL IN
	LDB	TA,DA.SON	;GET LINK TO 1ST SON
D54C22:	PUSHJ	PP,LNKSET	;SEE WHERE HE REALLY IS
	LDB	TB,DA.SYL	;THIS SON HAVE A SYNC?
	LDB	TC,DA.SYR
	IOR	TB,TC
	LDB	TC,DA.SLL##
	IOR	TB,TC
	LDB	TC,DA.FAL##	;THIS SON HAVE A BROTHER?
	LDB	TD,DA.BRO
	HRRZ	TA,CURDAT	;AIM AT CURRENT
	LDB	TE,DA.SLL	;GET FATHER'S SLL BIT
	IOR	TB,TE		;MERGE WITH SON'S SYNC
	DPB	TB,DA.SLL	;STORE SON'S SYNC IN FATHER'S SLL BIT
	JUMPN	TB,DA54.D	;QUIT IF BIT IS SET
	JUMPN	TC,DA54.D	;CK BROTHER BIT
	MOVE	TA,TD		;YES, REPEAT PROCESS WITH BROTHER
	JRST	D54C22
DA54.D:	HRRZ	TA,CURDAT
	LDB	TB,DA.BWZ	;BLANK WHEN ZERO?
	JUMPE	TB,DA54.E	;NO
	TSWF	ELITEM;
	JRST	D54D.0		;ELEMENTARY ITEM
	HRRZI	DW,336		;BWZ NOT ALLOWED AT GROUP LEVEL
	JRST	D54D.E
D54D.0:	LDB	TB,DA.CLA
	CAIE	TB,%CL.NU	;NUMERIC?
	JRST	D54DE0		;NO---ERROR
	LDB	TB,DA.USG
	CAIN	TB,%US.D6	;DISPLAY-6?
	JRST	DA54.E		;YES--OK
	CAIN	TB,%US.D7	;DISPLAY-7?
	JRST	DA54.E		;YES--OK
D54DE0:	HRRZI	DW,337		;BWZ ALLOWED ONLY FOR NUMERIC DISPLAY ITEMS
D54D.E:	HRRZ	TA,CURDAT
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	PUSHJ	PP,WARN
	HRRZ	TA,CURDAT
	SETZ	TB,
	DPB	TB,DA.BWZ
DA54.E:	HRRZ	TA,CURDAT
	LDB	TB,DA.JST	;JUSTIFIED RIGHT?
	JUMPE	TB,DA54.F	;NO
	TSWT	ELITEM;
	JRST	D54E.0		;GROUP ITEM
	LDB	TB,DA.CLA
	CAIE	TB,%CL.NU
	JRST	DA54.F		;NON-NUMERIC--OK
	HRRZI	DW,105		;NOT PERMITTED FOR NUMERIC ITEMS
	JRST	D54E.1
D54E.0:	HRRZI	DW,340		;NOT PERMITTED FOR GROUP
D54E.1:	LDB	LN,DA.LN
	LDB	CP,DA.CP
	PUSHJ	PP,WARN
	HRRZ	TA,CURDAT
	SETZ	TB,
	DPB	TB,DA.JST
DA54.F:	HRRZ	TA,CURDAT
	LDB	TB,DA.SYL
	LDB	TC,DA.SYR
	IORI	TB,(TC)
	JUMPN	TB,D54F.0
	LDB	TD,DA.USG
	CAIE	TD,%US.D6
	CAIN	TD,%US.D7
	TDCA	TC,TC
	SETO	TC,
	DPB	TC,DA.SYR
	JUMPE	TB,DA54.G	;NO SYNC CLAUSE
D54F.0:	TSWF	ELITEM;
	JRST	DA54.G		;OK FOR ELEMENTARY ITEM
	HRRZI	DW,341
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	PUSHJ	PP,WARN
	HRRZ	TA,CURDAT
	SETZ	TB,
	DPB	TB,DA.SYL
	DPB	TB,DA.SYR
DA54.G:	SKIPN	TA,CURDAT
	JRST	DA54.H
	LDB	TB,DA.VHL##
	JUMPE	TB,D54G.0
	LDB	TC,DA.SYR
	JUMPN	TC,D54GE0	;SYNC NOT ALLOWED
	LDB	TC,DA.SYL
	JUMPN	TC,D54GE0
	LDB	TC,DA.JST
	JUMPN	TC,D54GE0	;JUSTIFICATION NOT ALLOWED
	LDB	TC,DA.USG
	JUMPE	TC,D54G.0
	CAIN	TC,1		;DISPLAY-6?
	JRST	D54G.0		;YES
	CAIN	TC,2
	JRST	D54G.0		;DISPLAY-7
D54GE0:	LDB	LN,DA.LN
	LDB	CP,DA.CP
	SETZ	TB,
	DPB	TB,DA.SYR
	DPB	TB,DA.SYL
	DPB	TB,DA.JST
	HRRZI	DW,367
	PUSHJ	PP,FATAL
	HRRZ	TA,CURDAT
D54G.0:	LDB	TB,DA.VAL
	JUMPE	TB,DA54.H	;NO VALUE
	LDB	TC,DA.DFS	;DEFINED IN FILE SECTION?
	JUMPN	TC,D54GE4	;YES --- ERROR
	LDB	TC,DA.VHL
	JUMPN	TC,D54GE1	;VALUE AT HIGHER LEVEL WINS
	LDB	TC,DA.SUB
	JUMPN	TC,D54GE2	;OCCURS AT THIS OR HIGHER LEVEL NOT ALLOWED
	LDB	TC,DA.RDF
	JUMPN	TC,D54GE6
	LDB	TC,DA.RDH
	JUMPN	TC,D54GE6
	HRLZM	TB,CURLIT
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	HRRM	TA,CURLIT
	LDB	TB,LI.PUR	;NON-SIXBIT CHARS IN LITERAL?
	JUMPE	TB,D54G.5	;NO
	HRRZ	TA,CURDAT	;YES, BETTER CHK THE DATA ITEM
	LDB	TB,DA.USG
	CAIN	TB,%US.D7	;DISPLAY-7?
	JRST	D54G.4		;YES, ALL IS WELL
	LDB	LN,DA.LN	;NO, WE HAVE AN ERROR
	LDB	CP,DA.CP
	HRRZI	DW,^D329	;'NON-SIXBIT IN ITEM' MSG
	PUSHJ	PP,FATAL
D54G.4:	HRRZ	TA,CURLIT	;GO BACK TO THE LITERAL
D54G.5:	TSWF	ELITEM;
	JRST	D54G.1		;ELEMENTARY ITEM
	LDB	TB,LI.NLT##	;NUMERIC LITERAL?
	JUMPN	TB,D54GE3	;YES --- NOT ALLOWED FOR GROUPS
D54G.1:	LDB	TB,LI.FGC	;FIGURATIVE CONSTANT?
	JUMPN	TB,D54G.3	;YES
	HRRZ	TA,CURDAT
	LDB	TC,DA.CLA
	CAIN	TC,%%CL
	JRST	DA54.H		;CLASS NOT KNOWN
	LDB	TD,DA.EDT
	HRRZ	TA,CURLIT
	LDB	TB,LI.NLT	;NUMERIC LITERAL?
	JUMPE	TB,D54G.2	;NON-NUMERIC
	CAIN	TC,%CL.NU
	JUMPE	TD,DA54.H	;OK IF NOT EDITED
	JRST	D54GE5
D54G.2:	CAIN	TC,%CL.NU
	JUMPE	TD,D54GE5
	JRST	DA54.H
D54G.3:	LDB	TB,LI.FCC
	SUBI	TB,HIVAL.
	CAILE	TB,5
	JRST	D54GE7
	HRRZ	TA,CURDAT
	LDB	TD,DA.CLA
	LDB	TC,DA.EDT
	CAIE	TD,%CL.NU
	JRST	DA54.H
	JUMPN	TC,DA54.H
	CAIN	TB,ZERO.-HIVAL.
	JRST	DA54.H
	JRST	D54GE5
D54GE1:	HRRZI	DW,352		;HIGHER LEVEL VALUE TAKES PRECEDENCE
	JRST	D54G.E
D54GE2:	HRRZI	DW,353		;OCCURS --- VALUE NOT ALLOWED
	JRST	D54G.E
D54GE3:	HRRZI	DW,354		;NUMERIC VALUE NOT ALLOWED FOR GROUP
	JRST	D54G.E
D54GE4:	HRRZI	DW,355		;VALUE NOT ALLOWED IN FILE SECTION
	JRST	D54G.E
D54GE5:	HRRZI	DW,361		;CLASS OF ITEM CONFLICTS WITH LITERAL TYPE
	JRST	D54G.E
D54GE6:	HRRZI	DW,^D270	;REDEFINITION ENTRY
	JRST	D54G.E
D54GE7:	HRRZI	DW,^D298	;IMPROPER FIGURATIVE CONSTANT
D54G.E:	HRRZ	TA,CURDAT
	SETZ	TB,
	DPB	TB,DA.VAL	;REMOVE VALUE LINK
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	PUSHJ	PP,WARN
DA54.H:	TSWT	ELITEM;
	JRST	DA54.S		;GROUP ITEM
	HRRZ	TA,CURDAT
	LDB	TB,DA.LVL
	CAIN	TB,1
	JRST	DA54.0
	CAIE	TB,77
	JRST	DA54.1
DA54.0:	HLRZ	TB,EAS1PC
	JUMPE	TB,DA54.1
	AOS	TB,EAS1PC
	HRRZM	TB,EAS1PC
DA54.1:	LDB	TC,DA.USG
	JRST	.+1(TC)
	HRRZI	TC,1		;NOT SPECIFIED---ASSUME DISPLAY-6
	JRST	D54.A6		;DISPLAY-6
	JRST	D54.A7		;DISPLAY-7
	JRST	D54.A0		;MEANINGLESS
	JRST	D54.A1		;1-WORD COMP
	JRST	D54.A2		;2-WORD COMP
	JRST	D54.A3		;COMP-1
	JRST	D54.A4		;INDEX
D54.A6:	DPB	TC,DA.USG
	LDB	TB,DA.EXS	;NUMBER OF CHARACTERS
	HRRZI	TC,6		;BYTE SIZE
	JRST	ADV1PC
D54.A7:	LDB	TB,DA.EXS
	HRRZI	TC,7
	JRST	ADV1PC
D54.A0:	JRST	DA54.X
D54.A1:	LDB	TB,DA.EXS
	CAIG	TB,12
	JRST	D54.A4		;1 WORD
	HRRZI	TB,5		;CHANGE USAGE TO 2-WORD COMP
	DPB	TB,DA.USG
D54.A2:	HRRZI	TB,2
	HRRZI	TC,44
	JRST	ADV1PC

D54.A3:
D54.A4:	HRRZI	TB,1
	HRRZI	TC,44
ADV1PC:	CAILE	TC,44		;36. IS MAXIMUM BYTE SIZE
	HRRZI	TC,44
	HRRZ	TA,CURDAT
	LDB	TD,DA.SYL
	LDB	TE,DA.SYR
	IORI	TD,(TE)
	JUMPE	TD,ADV1.0
	HLRZ	TD,EAS1PC
	JUMPE	TD,.+3
	AOS	TD,EAS1PC
	HRRZM	TD,EAS1PC
	JUMPE	TE,ADV1.0	;NOT SYNC RIGHT
	HRRZI	TE,44
	IDIVI	TE,(TC)		;NO. BYTES PER WORD
	HRRZI	TA,(TE)		;SAVE BYTES PER WORD
	HRRZI	TD,(TE)
	HRRZI	TE,(TB)		;NO. BYTES IN ITEM
	IDIVI	TE,(TD)		;TD==NO. BYTES FOR FIRST WORD
	JUMPE	TD,ADV1.0
	SUBI	TA,(TD)		;NO. BYTES TO WASTE IN FIRST WORD
	IMULI	TA,(TC)		;NO. BITS TO WASTE IN FIRST WORD
	HRLM	TA,EAS1PC
ADV1.0:	HLRZ	TD,EAS1PC	;NO. BITS ALREADY USED IN CURRENT WORD
	HRRZI	TE,44
	SUBI	TE,(TD)		;NO. BITS REMAINING IN CURRENT WORD
	IDIVI	TE,(TC)		;NO. BYTES WHICH FIT IN REMAINDER
	CAIGE	TE,(TB)
	JRST	ADV.1		;NOT ALL OF ITEM FITS IN REMAINDER
	HRRZI	TE,(TB)
	HRRZ	TA,CURDAT
	HRRZ	TD,EAS1PC
	DPB	TD,DA.LOC	;RUN-TIME LOCATION OF ITEM
	HLRZ	TD,EAS1PC
	HRRZI	TE,44
	SUBI	TE,(TD)
	DPB	TE,DA.RES	;RESIDUE
	IMULI	TB,(TC)		;NO. OF BITS USED FOR ITEM
	HLRZ	TD,EAS1PC
	ADDI	TD,(TB)		;NO. OF BITS USED IN CURRENT WORD
	HRLM	TD,EAS1PC
	CAIGE	TD,44
	JRST	DA54.X
	AOS	TD,EAS1PC
	HRRZM	TD,EAS1PC
	JRST	DA54.X
ADV.1:	JUMPG	TE,ADV.11
	AOS	TD,EAS1PC
	HRRZM	TD,EAS1PC
ADV.11:	HRRZ	TA,CURDAT
	HRRZ	TD,EAS1PC
	DPB	TD,DA.LOC
	HLRZ	TA,EAS1PC
	HRRZI	TD,44
	SUBI	TD,(TA)
	HRRZ	TA,CURDAT
	DPB	TD,DA.RES
	SUBI	TB,(TE)		;NO. OF BYTES REMAINING IN ITEM
	IMULI	TE,(TC)	;NO. OF BITS USED FOR ITEM
	SUBI	TD,(TE)		;NO. OF BITS REMAINING IN WORD
	HRRZI	TA,44
	SUBI	TA,(TD)		;NUMBER OF BITS USED IN WORD
	HRLM	TA,EAS1PC
	CAIGE	TA,44
	JRST	ADV.12
	AOS	TD,EAS1PC
	HRRZM	TD,EAS1PC
ADV.12:	JUMPLE	TB,DA54.X	;NO MORE TO ALLOCATE
	HLRZ	TD,EAS1PC
	JUMPE	TD,.+3
	AOS	TD,EAS1PC
	HRRZM	TD,EAS1PC
	HRRZI	TE,44
	IDIVI	TE,(TC)		;NO. BYTES PER WORD
	MOVEI	TA,(TE)
	MOVEI	TE,(TB)
	IDIVI	TE,(TA)		;NO. WORDS REQUIRED
	ADDM	TE,EAS1PC
	IMULI	TE,(TA)
	SUBI	TB,(TE)		;NUMBER OF BYTES REMAINING
	IMULI	TB,(TC)		;NUMBER OF BITS FOR REMAINDER
	HLRZ	TE,EAS1PC
	ADDI	TE,(TB)		;NUMBER OF BITS USED IN CURRENT WORD
	HRLM	TE,EAS1PC
	CAIGE	TE,44
	JRST	DA54.X
	AOS	TE,EAS1PC
	HRRZM	TE,EAS1PC
	JRST	DA54.X
DA54.S:	LDB	TB,DA.USG
	CAIN	TB,%US.D6	;DISPLAY-6?
	JRST	DA54.X		;YES
	CAIN	TB,%US.D7	;DISPLAY-7?
	JRST	DA54.X		;YES
	HRRZI	TB,%US.D6	;ASSUME DISPLAY-6
	DPB	TB,DA.USG	;SINCE GROUPS MUST BE DISPLAY
DA54.X:	SKIPN	TA,CURDAT
	JRST	D54XXX
	LDB	TA,DA.VAL
	PUSHJ	PP,PUTVLU
	HRRZ	TA,CURDAT
	LDB	TB,DA.SYL
	LDB	TC,DA.SYR
	IORI	TB,(TC)
	JUMPE	TB,D54X.1
	MOVE	TB,EAS1PC
	TLZE	TB,777777
	HRRZI	TB,1(TB)	;SYNC ITEM
	HRRZM	TB,EAS1PC
D54X.1:	LDB	TC,DA.OCC##	;OCCURS FOR THIS ITEM?
	JUMPE	TC,D54.X0	;NO
	LDB	TC,DA.NOC##
	CAIG	TC,1
	JRST	D54.X0		;ALREADY TAKEN CARE OF
	LDB	TB,DA.USG
	CAIN	TB,%US.D7
	SKIPA	TE,[XWD 5,7]
	MOVE	TE,[XWD 6,6]
	LDB	TD,DA.RES
	IDIVI	TD,(TE)		;NO. CHAR. IN FIRST WORD
	HLRZ	TC,EAS1PC
	IDIVI	TC,(TE)		;NO. CHAR. IN LAST WORD
	ADDI	TD,(TC)
	HRRZ	TC,EAS1PC
	LDB	TB,DA.LOC
	SUBI	TC,1(TB)
	MOVSS	TE		;XWD BYTE SIZE,NO. CHAR./WORD
	IMULI	TC,(TE)		;NO. CHAR. IN FULL WORDS USED
	ADD	TD,TC		;TOTAL NO. CHARACTERS IN FIELD
	LDB	TB,DA.RES	;# OF BITS IN 1ST WORD
	HLRZ	TC,EAS1PC	;# OF BITS IN LAST WORD
	ADDI	TB,(TC)
	CAILE	TB,44
	SUBI	TB,44		;IF SUM IS > 36, SUBTRACT 36
	MOVEI	TC,44		;NOW SUBTRACT THIS FROM 36
	SUBI	TC,(TB)		;= # OF BITS WASTED IF SYNC IS ON
	LDB	TB,DA.SLL	;SYNC AT LOWER LEVEL?
	CAIN	TB,0
	SETZ	TC,		;NO, NO BITS WASTED
	MOVSS	TE		;CONVERT # BITS TO # OF CHARS
	IDIVI	TC,(TE)
	ADDI	TD,(TC)		;ADD ON # OF WASTED BITS
	LDB	TB,DA.NOC	;NO. OF OCCURRENCES
	IMULI	TD,-1(TB)	;NO. ADDITIONAL CHAR. TO ALLOCATE
	HRRZI	TC,44
	HLRZ	TB,EAS1PC
	SUBI	TC,(TB)		;NO. BITS LEFT IN CURRENT WORD
	IDIVI	TC,(TE)		;NO. BYTES LEFT IN CURRENT WORD
	CAIGE	TC,(TD)
	JRST	.+3
	MOVE	TC,TD
	JRST	D54X.2		;ALL FIT IN THIS WORD
	SUBI	TD,(TC)		;NO. MORE NEEDED
	AOS	TB,EAS1PC
	HRRZM	TB,EAS1PC
	MOVSS	TE		;XWD BYTE SIZE,NO. CHAR./WORD
	IDIVI	TD,(TE)		;NO. FULL WORDS NEEDED
	MOVSS	TE		;XWD NO. CHAR./WORD,BYTE SIZE
	ADDM	TD,EAS1PC	;TC==NO. BYTES FOR NEXT WORD
D54X.2:	IMULI	TC,(TE)		;NO. BITS REQUIRED
	HLRZ	TD,EAS1PC
	ADDI	TD,(TC)
	HRLM	TD,EAS1PC
D54.X0:	LDB	TB,DA.LVL
	CAIN	TB,1
	JRST	D54.X1
	CAIN	TB,77
	JRST	D54.X1
	JRST	DA54XX
D54.X1:	HLRZ	TB,EAS1PC
	JUMPE	TB,D54.X2
	AOS	TB,EAS1PC
	HRRZM	TB,EAS1PC
D54.X2:	LDB	TC,DA.DFS
	JUMPE	TC,DA54XX
	LDB	TB,DA.USG
	HRRZ	TA,CURFIL##
	JUMPE	TA,DA54XX
	HRRZI	TE,%RM.6B	;SIXBIT
	CAIE	TB,%US.D7
	JRST	D54.X3		;RECORD NOT ASCII
	HRRZI	TE,%RM.7B	;ASCII
D54.X3:	DPB	TE,FI.IRM
	LDB	TB,FI.RM2	;RECORDING MODE CLAUSE SEEN?
	SKIPN	TB		;YES, DON'T CHANGE ERM
	DPB	TE,FI.ERM
DA54XX:	MOVE	TA,CURDAT
	JUMPE	TA,D54XXX
	LDB	TB,DA.RDF
	JUMPE	TB,D54XXX
	HRRZ	TB,EAS1PC
	HLRZ	TD,EAS1PC	;BE SURE TO MOVE POINTER OVER BIT 35
	CAIN	TD,^D35		;  WHEN TESTING REDEFINES FOR
	AOJ	TB,		;  SAME SIZE
	SOSGE	TE,RDFLVL	;COME UP ONE LEVEL & CK IF OK
	JRST	[SETZM RDFLVL
		 EWARNW	CE380.
		 JRST D54XXX]
	MOVE	TC,RDEFPC(TE)
	HLRZ	TD,RDEFPC(TE)	;MOVE POINTER OVER BIT 35
	CAIE	TD,^D35		;  IF NECESSARY
	JRST	.+3		;NOT ON BIT 35
	HRLI	TC,0		;YES, CLR BIT COUNT
	AOJ	TC,		;& BUMP WORD COUNT
	CAILE	TB,(TC)
	JRST	D54XXE
	MOVE	TD,EAS1PC
	MOVEM	TC,EAS1PC
	CAIE	TB,(TC)
	JRST	D54XXE
	HLRZ	TB,TC
	HLRZ	TE,TD
	CAIN	TE,^D35
	SETZ	TE,
	CAIL	TB,(TE)
	JRST	D54XXX
	MOVEM	TD,EAS1PC
D54XXE:	LDB	LN,DA.LN
	LDB	CP,DA.CP
	HRRZI	DW,CE271.
	PUSHJ	PP,WARN

;ALL EXITS FROM DA54. MUST PASS THRU HERE

D54XXX:	POPJ	PP,

BYTSIZ:	XWD	0,6
	XWD	0,6		;DISPLAY-6
	XWD	0,7		;DISPLAY-7
	XWD	0,0
	XWD	0,6		;1-WORD COMP
	XWD	0,6		;2-WORD COMP
	XWD	0,6		;COMP-1
	XWD	0,6		;INDEX
NCW:	XWD	0,6
	XWD	0,6
	XWD	0,5
	XWD	0,1
	XWD	0,6
	XWD	0,14
	XWD	0,6
	XWD	0,6
PUTVLU:	JUMPE	TA,PV.X
	HRLZM	TA,CURLIT
	PUSHJ	PP,LNKSET
	HRRM	TA,CURLIT
	PUSHJ	PP,ADJUST
	SKIPN	TA,CURDAT
	JRST	PV.X
	LDB	TB,DA.ERR
	JUMPN	TB,PV.X		;DD ERROR --- IGNORE VALUE
	LDB	TB,DA.USG
	JRST	.+1(TB)
	POPJ	PP,		;USAGE NOT SPECIFIED
	JRST	PUTDSP		;DISPLAY-6
	JRST	PUTDSP		;DISPLAY-7
	POPJ	PP,		;NOT DEFINED
	JRST	PUT1WC		;1-WORD COMP
	JRST	PUT2WC		;2-WORD COMP
	JRST	PUTC1		;COMP-1
	JRST	PUT1WC		;INDEX
PUTC1:	SKIPN	SVDADR
	JRST	PUTC11
	MOVE	CH,SVDWRD
	PUSHJ	PP,PUTAS1##
	SETZM	SVDADR
PUTC11:	HLRZ	CH,CURDAT
	ANDI	CH,077777
	IORI	CH,1B20
	HRLI	CH,710000
	PUSHJ	PP,PUTAS1	;RELOC TO ITEM
	MOVE	CH,[XWD 602000,2]	;FLOATING POINT NUMBER HEADER
	JRST	PUT21A
PUT2WC:	SKIPN	SVDADR
	JRST	PUT2W1		;NOTHING SAVED
	MOVE	CH,SVDWRD
	PUSHJ	PP,PUTAS1
	SETZM	SVDADR
PUT2W1:	HLRZ	CH,CURDAT
	ANDI	CH,077777
	IORI	CH,1B20
	HRLI	CH,710000	;RELOC TO ITEM
	PUSHJ	PP,PUTAS1
	MOVE	CH,[XWD 604000,2]	;2-WORD COMP HEADER
PUT21A:	PUSHJ	PP,PUTAS1	;PUT OUT HEADER
	MOVE	CH,VALUE1
	PUSHJ	PP,PUTAS1
	MOVE	CH,VALUE2
	JRST	PUTAS1
PUT1WC:	SKIPN	SVDADR
	JRST	PUT1W1		;NOTHING SAVED
	MOVE	CH,SVDWRD
	PUSHJ	PP,PUTAS1
	SETZM	SVDADR
PUT1W1:	HLRZ	CH,CURDAT
	ANDI	CH,077777
	IORI	CH,1B20
	HRLI	CH,710000	;RELOC TO ITEM
	PUSHJ	PP,PUTAS1
	MOVE	CH,[XWD 610000,1]
	PUSHJ	PP,PUTAS1	;1-WORD COMP HEADER
	MOVE	CH,VALUE2
	JRST	PUTAS1
PUTDSP:	SKIPN	TA,SVDADR
	JRST	P6
	HRRZ	TB,ITMLOC
	CAIE	TB,(TA)
	JRST	P5		;DIFFERENT LOCATION
	MOVE	CH,SVDWRD
	MOVE	TE,[POINT 6,CH]
	TSWT	CONVRT;
	MOVE	TE,[POINT 7,CH]
	HRRZ	TB,ITMRES
	CAILE	TB,44
	HRRZI	TB,44
	DPB	TB,[POINT 6,TE,5]	;RESIDUE
	HRRZI	TC,44		;NEXT WORD, IF ANY, WILL START
	HRRZM	TC,ITMRES	;IN BIT 0
P1:	SOSGE	NCHITM##
	JRST	P4
	PUSHJ	PP,GETCHR
	IDPB	TC,TE
	LDB	TB,[POINT 6,TE,5]	;RESIDUE
	LDB	TC,[POINT 6,TE,11]	;BYTE SIZE
	CAIL	TB,(TC)
	JRST	P1		;ROOM FOR MORE IN THIS WORD
	PUSHJ	PP,PUTAS1
	SETZ	CH,
	AOS	SVDADR
P1.5:	SKIPLE	NCHITM
	JRST	P1.6
	SETZM	SVDADR
PV.X:	POPJ	PP,
P1.6:	HRRZI	TC,44
	SUB	TC,ITMRES
	CAIGE	TC,0
	SETZ	TC,
	MOVE	TB,NCHWRD##	;GET BYTES PER WORD
	CAIE	TB,6		;SIXBIT?
	MOVEI	TB,7		;NO--THEN 7 BITS PER BYTE
	IDIV	TC,TB		;DIVIDE BY BITS PER BYTE
	ADD	TC,NCHITM
	IDIV	TC,NCHWRD
	JUMPE	TB,.+2
	HRRZI	TC,1(TC)
	HRLZI	CH,620000		;DISPLAY-6
	TSWT	CONVRT;
	HRLZI	CH,640000		;DISPLAY-7
	HRRI	CH,(TC)
	PUSHJ	PP,PUTAS1
	SETZ	CH,
	TSWT	CONVRT;
	MOVE	CH,[ASCII /     /]
	MOVE	TE,[POINT 6,CH]
	TSWT	CONVRT;
	MOVE	TE,[POINT 7,CH]
	HRRZ	TB,ITMRES
	DPB	TB,[POINT 6,TE,5]
	JRST	P3
P2:	AOS	SVDADR
	PUSHJ	PP,PUTAS1
	SETZ	CH,
	TSWT	CONVRT;
	MOVE	CH,[ASCII /     /]
	MOVE	TE,[POINT 6,CH]
	TSWT	CONVRT;
	MOVE	TE,[POINT 7,CH]
P3:	SOSGE	NCHITM
	JRST	P4
	PUSHJ	PP,GETCHR
	IDPB	TC,TE
	LDB	TB,[POINT 6,TE,5]	;RESIDUE
	LDB	TC,[POINT 6,TE,11]	;BYTE SIZE
	CAIL	TB,(TC)
	JRST	P3		;ROOM FOR MORE IN THIS WORD
	JRST	P2		;WORD IS FULL
P4:	LDB	TB,[POINT 6,TE,5]
	CAIE	TB,44
	JRST	P4.1
	SETZM	SVDADR
	POPJ	PP,
P4.1:	MOVEM	CH,SVDWRD
	HRLM	TB,SVDADR
	POPJ	PP,
P5:	MOVE	CH,SVDWRD
	PUSHJ	PP,PUTAS1
P6:	HLRZ	CH,CURDAT
	ANDI	CH,077777
	IORI	CH,1B20
	HRLI	CH,710000
	PUSHJ	PP,PUTAS1
	HRRZ	TB,ITMLOC##
	HRRZM	TB,SVDADR
	HRRZ	TB,ITMRES##
	HRLM	TB,SVDADR##
	JRST	P1.5

	INTER.	DA55.
DA55.:	PUSHJ	PP,DA47.
	SKIPN	TA,CURDAT
	JRST	DA55.X
	LDB	TB,DA.VAL
	JUMPN	TB,DA24.E
	HLRZ	TB,CURLIT
	DPB	TB,DA.VAL
IFN RPW,<
	SKIPN	REPSEC		;DOING A REPORT ITEM?
	JRST	DA55.X		;NO
	HRRZ	TA,CURRPW	;GET RPWTAB PTR
	HRRZI	TB,%RG.VL	;GET VALUE CODE
	DPB	TB,RW.SCD	;PUT IN SOURCE CODE FIELD
	>
DA55.X:
	SKIPE	LNKSEC		;LINKAGE SECTION?
	EWARNJ	CE89.		;?VALUES ILLEGAL IN LINKAGE SECTION
	POPJ	PP,


	INTER.	DA56.
DA56.:	PUSHJ	PP,DA11.
	HRRZ	TC,0(SAVPTR)
	CAIGE	TC,1
	EWARNJ	CE25.
	CAIGE	TC,7777
	JRST	DA56.A
	EWARNW	CE229.
	HRRZI	TC,7777
	HRRZM	TC,0(SAVPTR)
DA56.A:	HRRZ	TA,CURDAT
	LDB	TB,DA.NOC
	CAIG	TC,(TB)
	EWARNJ	CE272.
	DPB	TC,DA.NOC
	POPJ	PP,


	INTER.	DA57.
DA57.:	PUSHJ	PP,DA60S.	;SAVE NAMTAB ADDR
	PUSHJ	PP,DA25S.	;SET UP HLDTAB ENTRY
	HRRZI	TB,%HL.DP	;DEPENDING-FOR-OCCURS CODE
	DPB	TB,HL.COD
	HLRZ	TB,CURDAT	;STORE DATAB LINK IN HLDTAB
	DPB	TB,HL.LNK##
	POPJ	PP,
	INTER.	DA58.
DA58.:	SKIPN	TA,CURFIL
	JRST	DA58.X
	SETO	TB,
	DPB	TB,FI.DSD##	;DEFINED IN AN SD
DA58.X:	POPJ	PP,


	INTER.	DA59.
DA59.:	SETZM	CURCPY##
	MOVE	TA,CPYLOC##
	MOVEM	TA,CPYNXT##	;RESET CPYTAB POINTER
	MOVEM	W2,CPYW2##	;SAVE W2 OF 'COPY'
	PUSHJ	PP,GETITM##
	TLNN	W1,GWLIT	;LITERAL?
	JRST	DA59.A		;NO
	EWARNW	CE285.	;YES--IMPROPER LIBRARY-NAME
	JRST	DA5.
DA59.A:	MOVE	TA,NAMWRD##
	MOVEM	TA,LIBNAM##
	HLRZ	TA,NAMWRD+1
	ANDI	TA,777700
	HRLZM	TA,LIBNAM+1
	PUSHJ	PP,GETITM
	CAIN	TYPE,PRIOD.	;IS IT A PERIOD?
	JRST	DA59.X		;YES--NO REPLACING

	CAIN	TYPE,REPLA.
	JRST	DA59.B		;WITH 'REPLACING'
	EWARNW	CE286.
	JRST	DA5.
DA59.B:	PUSHJ	PP,GETITM
DA59.C:	TLNE	W1,GWLIT
	JRST	DA59E1		;LITERAL ILLEGAL
	TLNE	W1,GWRESV
	JRST	DA59E1		;RESERVED WORD ILLEGAL
	PUSHJ	PP,TRYNAM
	PUSHJ	PP,BLDNAM
	MOVEM	TA,CURNAM##
	PUSHJ	PP,GETITM
	CAIE	TYPE,BY.
	JRST	DA59E2		;'BY' EXPECTED
	PUSHJ	PP,GETITM
	TLNE	W1,GWLIT
	JRST	DA59.D		;LITERAL
	TLNE	W1,GWRESV
	JRST	DA59E3		;RESERVED WORD ILLEGAL
	PUSHJ	PP,TRYNAM
	PUSHJ	PP,BLDNAM
	TLZ	W1,GWNOT
	HLRS	TA
	DPB	TA,[POINT 15,W2,15]	;NAMTAB POINTER
DA59.D:	MOVE	TA,[XWD CD.CPY,SZ.CPY]
	PUSHJ	PP,GETENT
	MOVEM	TA,CURCPY
	HLLZ	TB,CURNAM
	HRRI	TB,1
	MOVEM	TB,(TA)
	HLLZM	W1,1(TA)
	HLRM	W2,1(TA)
	HLRZ	TB,TA
	TLZ	TB,1B20
	HRRZ	TA,CURNAM
	DPB	TB,[POINT 16,(TA),17]	;NAMTAB LINK TO CPYTAB
	TLNN	W1,GWLIT
	JRST	DA59.E		;NOT A LITERAL
	LDB	TB,[POINT 9,W1,17]
	HRRZI	TB,4(TB)
	IDIVI	TB,5
	HRRZ	TA,CURCPY
	HRRM	TB,1(TA)
	HRRZI	TA,(TB)
	HRLI	TA,CD.CPY
	PUSHJ	PP,GETENT
	HRRZI	TB,(TA)
	HRRZ	TA,CURCPY
	HRRZ	TC,1(TA)
	ANDI	TC,000777
	ADDI	TC,-1(TB)
	HRLI	TB,LITVAL
	BLT	TB,(TC)		;MOVE LITERAL TO CPYTAB
DA59.E:	PUSHJ	PP,GETITM
	CAIE	TYPE,PRIOD.
	JRST	DA59.C
DA59.X:	PUSHJ	PP,SETLIB##	;SET UP COPY
	PUSHJ	PP,GETITM	;GET NEXT ITEM
	CAIE	TYPE,INTGR.	;IS IT INTEGER OR
	CAIN	TYPE,INTGR.+2000;  OR INTEGER IN MARGIN?
	SWONS	FPERWD!FREGWD	;YES--KEEP PERIOD
	SWON	FREGWD		;NO--FORGET PERIOD, SAVE WORD
	POPJ	PP,

DA59E1:	EWARNW	CE314.
CLROUT:	PUSHJ	PP,CLRCPY##
	JRST	DA5.
DA59E2:	EWARNW	CE124.
	JRST	CLROUT
DA59E3:	EWARNW	CE315.
	JRST	CLROUT
;PUT DATA-NAME QUALIFIER IN NEXT WORD OF HLDTAB

	INTER.	DA60.
DA60.:	PUSHJ	PP,DA60S.	;SAVE NAMTAB ADDR
	MOVE	TA,CURHLD	;GET # OF QUALIFIERS BEFORE THIS
	LDB	TB,HL.QAL
	AOJ	TB,		;INCREMENT COUNT
	DPB	TB,HL.QAL	;& PUT BACK
	ROT	TB,-1		;DIV BY 2
	HLRZ	TC,CURNAM	;GET NAMTAB LINK
	JUMPL	TB,DA60.A	;IF BIT0 ON, USE ODD HALF-WORD
	ADDI	TA,1(TB)	;PTR TO EVEN HALF-WORD
	HRRM	TC,(TA)		;STORE IN EVEN HALF
	POPJ	PP,
DA60.A:	PUSH	PP,CURHLD	;SAVE PTR TO HLDTAB ENTRY
	MOVE	TA,[XWD CD.HLD,1]	;GET ONE MORE WORD FOR THE ENTRY
	PUSHJ	PP,GETENT
	HLRZ	TC,CURNAM	;GET NAMTAB LINK
	HRLZM	TC,(TA)		;STORE NAMTAB LINK IN ODD HALF
	POP	PP,CURHLD	;RESTORE HLDTAB PTR
	POPJ	PP,

;STORE NAMTAB RELATIVE ADDRESS FOR NEW NAME

DA60S.:	TLNN	W1,GWNOT	;NAME IN NAMTAB?
	JRST	DA60SA		;YES
	PUSHJ	PP,BLDNAM	;NO, BUILD NAMTAB ENTRY
	MOVEM	TA,CURNAM	;SAVE ADDR
	HLRZS	TA		;LINK TO RIGHT HALF
	DPB	TA,[POINT 15,W2,15]	;& TO W2 IN CASE ANYBODY WANTS IT
	POPJ	PP,
DA60SA:	LDB	TA,[POINT 15,W2,15]	;GET NAMTAB REL ADDR
	HRLZM	TA,CURNAM	;& SAVE
	POPJ	PP,

	INTER.	DA61.
DA61.:	MOVEI	TA,%HL.VP	;'VALUE OF PROJECT-PROGRAMMER' FLAG
	MOVEM	TA,PNTS
	POPJ	PP,
;BUILD REPORT TABLE ENTRY & LINK FILE TO REPORT

IFN RPW,<
	INTER.	DA62.
DA62.:	PUSHJ	PP,DA62S.
	HLRZ	TB,CURFIL	;STORE FILTAB LINK IN RPWTAB
	DPB	TB,RW.FIL##
	HRRZ	TA,CURFIL	;GET FILTAB PTR
	HLRZ	TB,CURRPW	;STORE RPWTAB LINK IN FILTAB
	DPB	TB,FI.RPG##
	SETO	TB,		;FORCE ASCII MODE FOR FILE
	DPB	TB,FI.ADV##	;BY SETTING WRITE-ADVANCING FLAG
	POPJ	PP,

DA62S.:	PUSHJ	PP,BLDNAM	;PUT USERN. IN NAMTAB
	HLRZ	TB,TA		;SAVE NAMTAB LINK
	DPB	TB,[POINT 15,W2,15]
	MOVE	TA,[XWD CD.RPW,SZ.RPD]	;GET AN RPWTAB ENTRY
	PUSHJ	PP,GETENT
	MOVEM	TA,CURRPW	;SAVE RPWTAB PTR
	LDB	TB,[POINT 15,W2,15]	;GET BACK NAMTAB LINK
	DPB	TB,RW.NAM##	;STORE NAMTAB LINK IN RPWTAB
	ADD	TB,NAMLOC##	;MAKE ABS. NAMTAB PTR
	HLRM	TA,(TB)		;STORE RPWTAB LINK IN NAMTAB
	HLRZ	TB,CURRPW	;GET LINK TO CURRENT RPWTAB ENTRY
	SKIPN	TC,LASTRD	;GET LINK TO PREVIOUS RD
	JRST	DA62SX		;NO PREVIOUS
	HRRZ	TA,RPWLOC	;MAKE ABS. PTR TO PREV. ENTRY
	ADDI	TA,(TC)
	DPB	TB,RW.BRO##	;LINK OLD ENTRY TO NEW
DA62SX:	MOVEM	TB,LASTRD	;REMEMBER CURR. ENTRY FOR NEXT TIME
	MOVE	TA,CURRPW	;RESTORE PTR TO CURRENT
	POPJ	PP,
	>

;INIT REPORT SECTION

IFN RPW,<
	INTER.	DA63.
DA63.:
	SETZM	LNKSEC		;CLR LINKAGE SECTION FLAG
	SKIPL	TA,PCHOLD	;RESET EAS1PC TO PREVIOUS
	MOVEM	TA,EAS1PC	;  IF CHANGED BY LINKAGE SECTION
	SETOM	PCHOLD
	SKIPN	FILSEC		;FILE SECTION SEEN?
	JRST	DA63E.		;NO
	SWOFF	FFILSC		;CLR FILE SECTION FLAG
	SETOM	REPSEC##	;SET REPORT SECTION FLAG
	SETZM	RPWRDL		;CLR RD RPWTAB LINK STORAGE
	POPJ	PP,

DA63E.:	HRRZI	NODE,DD204E	;NEXT SYNTAX NODE IS DD204E
	HRRZM	NODE,(NODPTR)	;TO SKIP TO NEXT SECTION
	EWARNJ	CE339.		;FILE SECTION NOT SEEN MSG
	>
;INIT RD

IFN RPW,<
	INTER.	DA64.
DA64.:	LDB	TB,[POINT 15,W2,15]	;GET BACK NAMTAB LINK
	ADD	TB,NAMLOC	;MAKE ABS NAMTAB PTR
	HRRZ	TA,(TB)		;GET RPWTAB LINK TO RD ENTRY
	HRLI	TA,(TA)		;IN BOTH HALVES
	TRZE	TA,700000	;TABLE CODE = RPWTAB?
	EWARNJ	CE359.		;?REPORT-NAME EXPECTED
	MOVE	TB,RPWLOC##	;MAKE FULL RPWTAB PTR
	ADDI	TA,(TB)
	MOVEM	TA,CURRPW	;SAVE PTR TO CURRENT RPWTAB ENTRY
DA64.A:	TLO	W2,GWDEF	;PUT DEFINING REF. IN CREF TABLE
	PUSHJ	PP,PUTCRF
	MOVE	TA,CURRPW	;RESTORE PTR TO NEW ENTRY
	HLRZM	TA,RPWRDL	;& SAVE LINK FOR GROUP ITEMS
	LDB	TB,[POINT 20,W2,35]	;STORE LINE POSITION IN RPWTAB
	DPB	TB,RW.LNC##
	MOVE	TB,[XWD [SIXBIT /PAGE:COUNTER/],NAMWRD]
	PUSHJ	PP,DA64S.	;MAKE PAGE-COUNTER ENTRY IN DATAB
	DPB	TB,RW.PC##	;PUT DATAB LINK IN RPWTAB
	HRRZ	TA,CURDAT	;DEFAULT PAGE-CTR SIZE IS 10
	HRRZI	TB,^D10
	DPB	TB,DA.EXS
	DPB	TB,DA.INS
	MOVE	TB,[XWD [SIXBIT /LINE:COUNTER/],NAMWRD]
	PUSHJ	PP,DA64S.	;MAKE LINE-COUNTER ENTRY IN DATAB
	DPB	TB,RW.LC##	;PUT DATAB LINK IN RPWTAB
	HRRZ	TA,CURDAT	;GET DATAB PTR
	HRRZI	TB,2		;LINE-COUNTER IS SIZE 2
	DPB	TB,DA.EXS
	DPB	TB,DA.INS
	MOVE	TA,[CD.LIT,,SZ.LIT+1]	;PUT A -1 IN LITAB
	PUSHJ	PP,GETENT
	MOVEM	TA,CURLIT
	MOVE	TB,[001002,,1]	;SET UP CODE WORD
	MOVEM	TB,(TA)
	MOVE	TB,[ASCII /-1/]	;"-1" TO 2ND WORD OF ENTRY
	MOVEM	TB,1(TA)
	HLRZ	TB,CURLIT	;PUT LITAB LINK IN DATAB ENTRY
	HRRZ	TA,CURDAT
	DPB	TB,DA.VAL	;AS A VALUE OF -1 TO MEAN NO INITIATE YET
	JRST	DA8.		;ALLOCATE LINE-COUNTER
	>
;MAKE PAGE/LINE-COUNTER ENTRY IN DATAB

IFN RPW,<
DA64S.:	BLT	TB,NAMWRD+1	;PUT 'XXXX-COUNTER' IN NAME STORE
	SETZM	NAMWRD+2	;CLR REST OF NAMWRD
	MOVE	TA,[NAMWRD+2,,NAMWRD+3]
	BLT	TA,NAMWRD+5
	PUSHJ	PP,DA8.		;ALLOCATE PREVIOUS ITEM
	HRRZI	TB,1		;MAKE COUNTER AN 01 LEVEL ITEM
	MOVEM	TB,(SAVPTR)
	PUSHJ	PP,DA26N.
	HRRZS	REPSEC		;PRETEND ITS AN ORDINARY W-S ITEM
	PUSHJ	PP,DA27.	;CREATE DATAB ENTRY
	SETOM	REPSEC		;RESET REPORT SECTION FLAG
	HRRZ	TA,CURDAT	;PTR TO COUNTER DATAB ENTRY
	HRRZI	TB,%CL.NU	;SET NUMERIC CLASS IN DATAB
	DPB	TB,DA.CLA
	HRRZI	TB,%US.1C	;& 1-WORD COMP USAGE
	DPB	TB,DA.USG
	SETO	TB,
	DPB	TB,DA.PIC	;& PIC SEEN BIT
	DPB	TB,DA.SGN	;& SIGNED BIT
	DPB	TB,DA.FAL	;& FATHER BIT
	DPB	TB,DA.LPC##	;THIS IS A LINE- OR PAGE-COUNTER
	HRRZ	TB,RPWRDL	;RD ENTRY IS THE FATHER LINK
	DPB	TB,DA.POP
	PUSHJ	PP,GETRDL	;GET RPWTAB PTR
	HLRZ	TB,CURDAT	;PUT PAGE/LINE-CTR DATAB LINK IN RPWTAB ENTRY
	POPJ	PP,
	>

;GET RPWRDL & CONVERT IT TO AN ABSOLUTE PTR

IFN RPW,<
GETRDL:	HRRZ	TA,RPWLOC	;TABLE BASE
	ADD	TA,RPWRDL	;PLUS RELATIVE ADDR
	POPJ	PP,
	>

;SET UP REPORT NAME FOR RD, WHERE REPORT NAME NOT
;SPECIFIED IN A REPORT CLAUSE OF THE FILE SECTION

IFN RPW,<
	INTER.	DA64E.
DA64E.:	EWARNW	CE342.		;?NOT NAMED IN FILE SECTION
	JRST	DA62S.
	>
;GET PAGE LIMIT

IFN RPW,<
	INTER.	DA66.
DA66.:	PUSHJ	PP,DA11.	;GET THE INTEGER
	PUSHJ	PP,GETRDL	;GET RPWTAB PTR
	LDB	TC,RW.PAG	;PAGE LIMIT CLAUSE SEEN ALREADY?
	JUMPN	TC,JCE16.	;YES, DUPLICATE CLAUSE
	MOVE	TC,(SAVPTR)	;GET VALUE OF PAGE LIMIT
	JUMPLE	TC,DA66E	;MUST BE .GT. 0
	CAILE	TC,777		;MUST BE .LT. 512
DA66E:	EWARNJ	CE344.		;?PAGE-LIMIT MUST BE LESS THAN 512
	DPB	TC,RW.PAG	;STORE PAGE LIMIT
	MOVEI	TD,2		;MAKE LINE-CTR SIZE AGREE WITH PG-LIM
	CAIL	TC,^D100
	MOVEI	TD,3
	CAIGE	TC,^D10
	MOVEI	TD,1
	LDB	TC,RW.LC
	HRRZI	TA,(TC)
	PUSH	PP,TD
	PUSHJ	PP,LNKSET
	POP	PP,TD
	DPB	TD,DA.EXS
	DPB	TD,DA.INS
	POPJ	PP,
	>

JCE16.:	EWARNJ	CE16.		;DUPLICATE CLAUSE
;GET PAGE HEADING LINE NUMBER

IFN RPW,<
	INTER.	DA67.
DA67.:	MOVE	TB,RW.PHL##	;PTR TO HEADING-LINE FIELD IN RPWTAB ENTRY
DA67X.:	MOVEM	TB,PNTS		;SAVE FIELD PTR
	PUSHJ	PP,DA11.	;GET VALUE OF INTEGER
	MOVE	TC,(SAVPTR)
	PUSHJ	PP,GETRDL	;GET RPWTAB PTR
	LDB	TB,RW.PAG	;GET PAGE LIMIT
	CAIGE	TB,(TC)		;INDICATED LINE .LE. PAGE LIMIT?
	EWARNJ	CE343.		;NO
	DPB	TC,PNTS		;YES, STORE NUMBER IN INDICATED FIELD
	POPJ	PP,
	>

;GET FIRST DETAIL LINE NUMBER

IFN RPW,<
	INTER.	DA68.
DA68.:	MOVE	TB,RW.FDE##	;PTR TO FIRST DETAIL FIELD IN RPWTAB ENTRY
	JRST	DA67X.
	>

;GET LAST DETAIL LINE NUMBER

IFN RPW,<
	INTER.	DA69.
DA69.:	MOVE	TB,RW.LDE	;PTR TO LAST DETAIL FEILD
	JRST	DA67X.
	>

;GET PAGE FOOTING LINE NUMBER

IFN RPW,<
	INTER.	DA70.
DA70.:	MOVE	TB,RW.CFL##	;PTR TO FOOTING-LINE FIELD IN RPWTAB
	JRST	DA67X.
	>
;CONTROL 'FINAL'

IFN	RPW,<
	INTER.	DA71.
DA71.:	MOVE	TA,[CD.RPW,,SZ.RPC]	;GET A CONTROL ENTRY IN RPWTAB
	PUSHJ	PP,GETENT
	MOVEM	TA,CURRPW	;SAVE RPWTAB PTR
	HLRZ	TC,TA		;PUT LINK TO CONTROL ENTRY INTO RD ENTRY
	PUSHJ	PP,GETRDL	;GET RPWTAB PTR
	LDB	TB,RW.NCI	;IS THIS THE FIRST CONTROL?
	JUMPE	TB,.+2		;YES
	EWARNW	CE360.		;NO, FINAL MUST BE FIRST
	DPB	TC,RW.CID##
	HRRZI	TC,1		;INDICATE 1ST CONTROL ENTRY
DA71.X:	DPB	TC,RW.NCI##
	POPJ	PP,
	>

;CONTROL <DATA-NAME>

IFN RPW,<
	INTER.	DA72.
DA72.:	PUSHJ	PP,DA96.	;READ IDENTIFIER WITH ALL QUALS
	PUSH	PP,TE		;SAVE DATAB LINK
	MOVE	TA,[CD.RPW,,SZ.RPC]	;GET A CONTROL ENTRY IN RPWTAB
	PUSHJ	PP,GETENT
	MOVEM	TA,CURRPW	;SAVE RPWTAB PTR
	POP	PP,TE		;STORE DATAB LINK
	HRLZM	TE,(TA)		;IN CONTROL ENTRY
	HLRZ	TB,TA		;GET CNTRL ENTRY LINK
	PUSHJ	PP,GETRDL	;GET RD RPWTAB PTR
	LDB	TC,RW.NCI	;# OF CTRL IDENTIFIERS SEEN
	SKIPN	TC		;THIS THE 1ST CONTROL ID?
	DPB	TB,RW.CID	;YES, STORE LINK TO 1ST CTRL ID IN RPWTAB
	AOJA	TC,DA71.X	;INCREMENT CTRL-ID CTR
	>
;CHECK FOR ILLEGAL CLAUSE IN REPORT SECTION

	INTER.	DA73.
DA73.:
IFN RPW,<
	SKIPN	REPSEC		;IN REPORT SECTION?
	POPJ	PP,		;NO
	EWARNW	CE348.		;CLAUSE ILLEGAL IN REPORT SECT.
DA73.X:	HRRZI	NODE,DD115.	;CONTINUE AT NODE DD115.
	HRRZM	NODE,(NODPTR)
	>
	POPJ	PP,

;CHECK FOR ILLEGAL CLAUSE OUTSIDE REPORT SECTION

IFN RPW,<
	INTER.	DA74.
DA74.:	SKIPE	REPSEC		;IN REPORT SECTION?
	POPJ	PP,		;YES, CLAUSE IS OK
DA74.X:	EWARNW	CE350.		;NO, ILLEGAL CLAUSE
	JRST	DA73.X		;GO TO SYNTAX NODE DD144.
	>
IFN RPW,<

;IF REPORT ITEM HAS NO NAME,
;PUT NAME 'RWITM.######' ON REPORT GROUP ITEM

FAKNAM:	HRRZ	TA,CURDAT	;DATAB ADDR
	LDB	TC,DA.NAM	;HAVE A REAL NAME?
	SKIPE	TC
	POPJ	PP,		;YES
	PUSHJ	PP,RPWNAM	;MAKE NAMTAB ENTRY "RWITM.######"
	HLRZS	TA		;LINK DATAB ENTRY TO NAMTAB
	HLL	TA,CURDAT
	PUSHJ	PP,PUTLNK
	HRRZ	TA,CURDAT	;PUT NAMTAB LINK IN DATAB
	HRRZ	TB,NAMADR##
	HRRZ	TC,NAMLOC
	SUBI	TB,(TC)
	DPB	TB,DA.NAM
	LDB	TB,DA.SNL##	;REMOVE ITEM FROM NO-NAME CHAIN
	HRRZM	TB,(TC)
	SETZ	TB,		;CLR SAME NAME LINK
	DPB	15,DA.SNL
	SETO	TB,		;SET FAKE NAME BIT
	DPB	TB,DA.FAK
	POPJ	PP,

;MAKE A "RWITM.######" ENTRY IN NAMTAB

RPWNAM:	MOVE	TA,['RWITM;']	;FIRST WORD OF SIXBIT NAME
	MOVEM	TA,NAMWRD
	PUSHJ	PP,SIXDIG	;SECOND WORD OF NAME
	MOVEM	TA,NAMWRD+1
	SETZM	NAMWRD+2	;CLR REST OF NAMWRD
	MOVE	TA,[NAMWRD+2,,NAMWRD+3]
	BLT	TA,NAMWRD+5
	PUSHJ	PP,TRYNAM
	PUSHJ	PP,BLDNAM	;PUT NAME IN NAMTAB
	MOVEM	TA,NAMADR	;SAVE NAMTAB PTR
	POPJ	PP,

;GENERATE A 6-DIGIT SIXBIT NUMBER ONE LARGER THAN THE LAST

SIXDIG:	MOVE	TA,SIXHLD##	;GET LAST NUMBER RETURNED
	ADD	TA,[464646464647]
	MOVE	TB,TA
	TDZ	TB,[171717171717]
	MOVE	TC,TB
	LSH	TC,-3
	OR	TB,TC
	SUB	TA,TB
	ADD	TA,[202020202020]
	MOVEM	TA,SIXHLD	;STORE NEW NUMBER
	POPJ	PP,

	>
;REPORT LINE IS NEXT PAGE

IFN RPW,<
	INTER.	DA75.
DA75.:	MOVEI	TB,%RG.NP	;GET NEXT PAGE CODE
DA75.X:	HRRZ	TA,CURRPW	;PTR TO REPORT GROUP ENTRY
	LDB	TC,RW.LCD##	;LINE CODE SEEN BEFORE?
	JUMPN	TC,JCE16.	;YES, DUPLICATE CLAUSE
	DPB	TB,RW.LCD	;NO, STORE IT
	POPJ	PP,
	>

;REPORT LINE IS <INTEGER>

IFN RPW,<
	INTER.	DA76.
DA76.:	PUSHJ	PP,DA11.	;GET THE INTEGER
	MOVE	TC,(SAVPTR)
	JUMPLE	TC,JCE25.	;MUST BE POSITIVE
	HRRZ	TA,CURRPW	;GET RPWTAB PTR
	LDB	TB,RW.LIN##	;LINE # THERE ALREADY?
	JUMPN	TB,JCE16.	;YES, DUP. CLAUSE
	PUSHJ	PP,GETRDL	;MAKE PTR TO RD ENTRY
	LDB	TB,RW.PAG##	;LINE# MUST BE .LE. PAGE LIMIT
	HRRZ	TA,CURRPW	;RESTORE PTR TO GROUP ITEM
	JUMPE	TB,.+3
	CAILE	TC,(TB)
	EWARNJ	CE352.		;IT'S NOT
	DPB	TC,RW.LIN	;OK, STORE IT
	HRRZI	TB,%RG.LN	;GET THE LINE # CODE
	JRST	DA75.X
	>

;REPORT LINE IS PLUS <INTEGER>

IFN RPW,<
	INTER.	DA77.
DA77.:	PUSHJ	PP,DA11.	;GET THE INTEGER
	MOVE	TC,(SAVPTR)
	JUMPLE	TC,JCE25.	;MUST BE POS.
	HRRZ	TA,CURRPW	;GET RPWTAB PTR
	LDB	TB,RW.LIN	;LINE # GIVEN ALREADY?
	JUMPN	TB,JCE16.	;YES
	DPB	TC,RW.LIN	;NO, STORE IT
	HRRZI	TB,%RG.PI	;PLUS INTEGER CODE
	JRST	DA75.X
	>


JCE25.:	EWARNJ	CE25.		;?POSITIVE INTEGER REQUIRED
;NEXT GROUP IS NEXT PAGE

IFN RPW,<
	INTER.	DA80.
DA80.:	MOVEI	TB,%RG.NP	;GET NEXT PAGE CODE
DA80.X:	HRRZ	TA,CURRPW	;PTR TO REPORT GROUP ENTRY
	LDB	TC,RW.NLC##	;NEXT GROUP CODE SEEN BEFORE?
	JUMPN	TC,JCE16.	;YES, CLAUSE DUPLICATED
	DPB	TB,RW.NLC	;NO, STORE IT
	POPJ	PP,
	>

;NEXT GROUP IS <INTEGER>

IFN RPW,<
	INTER.	DA81.
DA81.:	PUSHJ	PP,DA11.	;GET THE INTEGER
	MOVE	TC,(SAVPTR)
	JUMPLE	TC,JCE25.	;MUST BE .GT. 0
	HRRZ	TA,CURRPW	;GET RPWTAB PTR
	LDB	TB,RW.NXT##	;NEXT GROUP CLAUSE SEEN ALREADY?
	JUMPN	TB,JCE16.	;YES
	LDB	TB,RW.PAG	;INTEGER MUST BE .LE. PAGE LIMIT
	JUMPE	TB,.+3
	CAILE	TC,(TB)
	EWARNJ	CE352.		;TOO BIG
	DPB	TC,RW.NXT	;OK, STORE IT
	HRRZI	TB,%RG.LN	;LINE # CODE
	JRST	DA80.X
	>

;NEXT GROUP IS PLUS <INTEGER>

IFN RPW,<
	INTER.	DA82.
DA82.:	PUSHJ	PP,DA11.	;GET THE INTEGER
	MOVE	TC,(SAVPTR)
	JUMPLE	TC,JCE25.	;MUST BE .GT. 0
	HRRZ	TA,CURRPW	;GET RPWTAB PTR
	LDB	TB,RW.NXT	;NEXT GROUP CLAUSE SEEN ALREADY?
	JUMPN	TB,JCE16.	;YES
	DPB	TC,RW.NXT	;STORE INTEGER
	HRRZI	TB,%RG.PI	;GET PLUS INTEGER CODE
	JRST	DA80.X
	>
;SET GROUP INDICATE BIT

IFN RPW,<
	INTER.	DA83.
DA83.:	SKIPN	REPSEC		;IN REPORT SECTION?
	JRST	DA74.X		;NO, SHOULDN'T BE HERE
	HRRZ	TA,CURRPW	;REPORT GROUP RPWTAB PTR
	SETO	TB,		;SET GROUP INDICATE BIT
	DPB	TB,RW.GPI##
	POPJ	PP,
	>
;RESET ON FINAL

IFN RPW,<
	INTER.	DA84.
DA84.:	HRRZ	TA,CURRPW	;GET RPWTAB PTR
	LDB	TB,RW.RSF##	;RESET ON FINAL SEEN BEFORE?
	JUMPN	TB,JCE16.	;YES
	LDB	TB,RW.RSI##	;RESET ON IDENTIFIER SEEN?
	JUMPN	TB,JCE16.	;YES
	LDB	TB,RW.TYP	;MAKE SURE IT'S CF (OR NOT YET SPECIFIED)
	CAIE	TB,0
	CAIN	TB,%RG.CF
	JRST	.+2		;OK
	EWARNJ	CE368.		;?RESET ON ITEM OTHER THAN CF
	SETO	TB,		;NO, SET RESET ON FINAL BIT
	DPB	TB,RW.RSF
	POPJ	PP,
	>

;RESET ON <IDENTIFIER>

IFN RPW,<
	INTER.	DA85.
DA85.:	PUSHJ	PP,DA96.	;READ FULL IDENTIFIER
	HRRZ	TA,CURRPW	;GET RPWTAB PTR
	LDB	TB,RW.RSF	;RESET ON FINAL SEEN BEFORE?
	JUMPN	TB,JCE16.	;YES
	LDB	TB,RW.RSI	;RESET ON IDENTIFIER SEEN?
	JUMPN	TB,JCE16.	;YES
	LDB	TB,RW.TYP	;MAKE SURE IT'S CF (OR NOT YET SPECIFIED)
	CAIN	TB,0
	JRST	DA85.X		;NOT YET SPECIFIED, CK IT AT DA94.
	CAIE	TB,%RG.CF	;CF?
	JRST	JCE368		;?RESET ON ITEM OTHER THAN CF
	PUSHJ	PP,FNDCNT	;LOOK FOR MATCHING CONTROL ENTRY
	JRST	JCE369		;?NOT A CONTROL
	CAML	TB,THSCTL	;IS RESET CONTROL HIGHER THAN CURRENT ITEMS CONTROL?
	JRST	JCE370		;NO
DA85.X:	HRRZ	TA,CURRPW	;GET BACK PTR TO CURRENT RPW ITEM
	SETO	TB,		;SET RESET FLAG
	DPB	TB,RW.RSI
	DPB	TE,RW.RES##	;STORE DATAB LINK TO RESET IDENTIFIER
	POPJ	PP,
	>
IFN RPW,<

JCE357:	HRRZI	DW,CE357.
	JRST	FATAL
JCE367:	HRRZI	DW,CE367.
	JRST	FATAL
JCE368:	HRRZI	DW,CE368.
	JRST	FATAL
JCE369:	HRRZI	DW,CE369.
	JRST	FATAL
JCE370:	HRRZI	DW,CE370.
	JRST	FATAL

	>
;LOCATE CONTROL ENTRY FOR DATAB ITEM IN TE
;SKIP RETURNS WITH LINK TO CONTROL ENTRY IN TB IF FOUND

IFN RPW,<
FNDCNT:	HRRZ	TA,CURRPW	;PTR TO CURRENT GROUP ENTRY
	LDB	TB,RW.RDL	;MAKE PTR TO RD ENTRY
	HRRZ	TA,RPWLOC
	ADDI	TA,(TB)
	LDB	TC,RW.NCI	;GET NUMBER OF CONTROLS
	LDB	TB,RW.CID	;GET LINK TO 1ST CONTROL
	ADD	TB,RPWLOC
	HRRZS	TB		;CLR LEFT HALF
FNDCN1:	HLRZ	TD,(TB)		;GET DATAB LINK FROM CONTROL ENTRY
	CAIN	TD,(TE)		;IS IT THE ONE?
	JRST	CPOPJ1		;YES
	ADDI	TB,3		;NO, ADVANCE TO NEXT CONTROL
	SOJG	TC,FNDCN1	;GO BACK TO TRY NEXT ONE
	HRRZ	TC,RPWLOC	;REDUCE RPW ADDR TO RPW LINK
	SUBI	TB,(TC)
	POPJ	PP,		;NO MORE -- TAKE ERROR RETURN
	>

CPOPJ1:	AOS	(PP)		;SKIP RETURN
CPOPJ:	POPJ	PP,
;GET COLUMN NUMBER

IFN RPW,<
	INTER.	DA86.
DA86.:	PUSHJ	PP,DA11.	;GET THE INTEGER
	MOVE	TC,(SAVPTR)
	JUMPLE	TC,JCE25.	;MUST BE POSITIVE
	HRRZ	TA,CURRPW	;GET RPWTAB PTR
	LDB	TB,RW.COL##	;COLUMN # ALREADY GIVEN?
	JUMPN	TB,JCE16.	;YES
	DPB	TC,RW.COL	;YES, STORE IT IN RPWTAB
	POPJ	PP,
	>
IFN RPW,<

;SET REPORT ITEM TYPE

	INTER.	DA87.
DA87.:	MOVEI	TC,%RG.RH	;REPORT HEADING TYPE CODE
DA87.X:	HRRZ	TA,CURRPW	;PTR TO RPWTAB ENTRY
	LDB	TB,RW.TYP##	;TYPE STORED ALREADY?
	JUMPN	TB,JCE16.	;YES
	DPB	TC,RW.TYP	;NO, STORE IT
	MOVEM	TC,LASTYP	;REMEMBER LAST TYPE SEEN
	POPJ	PP,

	INTER.	DA88.
DA88.:	MOVEI	TC,%RG.PH	;PAGE HEADING TYPE
	JRST	DA87.X

	INTER.	DA89.
DA89.:	MOVEI	TC,%RG.CH	;CONTROL HEADING TYPE
	JRST	DA87.X

	INTER.	DA90.
DA90.:	MOVEI	TC,%RG.DE	;DETAIL TYPE
	JRST	DA87.X

	INTER.	DA91.
DA91.:	MOVEI	TC,%RG.CF	;CONTROL FOOTING TYPE
	JRST	DA87.X

	INTER.	DA92.
DA92.:	MOVEI	TC,%RG.PF	;PAGE FOOTING
	JRST	DA87.X

	INTER.	DA93.
DA93.:	MOVEI	TC,%RG.RF	;REPORT FOOTING
	JRST	DA87.X

	>
IFN RPW,<

;CH/CF IDENTIFIER

	INTER.	DA94.
DA94.:	PUSHJ	PP,DA96.	;GET FULL IDENTIFIER
DA94.1:	PUSHJ	PP,FNDCNT	;FIND CORRESPONDING CONTROL ENTRY
	JRST	JCE357		;?THIS DATA ITEM IS NOT A CONTROL
	HRRZM	TB,THSCTL	;SAVE ADDR OF CORRESP CONTROL ENTRY
	MOVE	TA,CURRPW	;GET BACK PTR TO RPW ITEM ENTRY
	LDB	TC,RW.TYP	;CH OR CF?
	CAIE	TC,3
	JRST	DA94.A		;CF
	HLLM	TA,1(TB)	;STORE RPWTAB LINK TO CH ITEM
	POPJ	PP,
DA94.A:	HLRM	TA,1(TB)	;STORE RPWTAB LINK TO CF
	LDB	TC,[POINT 2,(TA),10]	;GET RW.RSF+RW.RSI
	JUMPE	TC,CPOPJ	;NO RESET FLAGS
	MOVEM	TE,(SAVPTR)	;SAVE LINK TO CURRENT ITEMS CONTROL
	LDB	TE,RW.RES	;GET RESET LINK TO DATAB
	PUSHJ	PP,FNDCNT	;LOCATE CONTROL ENTRY MATCHING RESET
	JRST	JCE369		;NO SUCH CONTROL (CHECKED AT DA85.)
	CAML	TB,THSCTL	;IS RESET CONTROL HIGHER THAN CURRENT CONTROL
	JRST	JCE370		;?NOT HIGHER
	POPJ	PP,

;CH/CF FINAL

	INTER.	DA95.
DA95.:	HRRZ	TA,CURRPW	;PTR TO RPWTAB
	SETO	TB,		;SET FINAL CONTROL FLAG
	DPB	TB,RW.FNC##
	SETZ	TE,		;FINAL HAS "DATAB LINK" OF 0
	JRST	DA94.1

	>
;READ & FIND A DEFINED DATA-NAME WITH ALL QUALIFIERS
;RETURNS TE=DATAB LINK

	INTER.	DA96.
DA96.:	SETZM	TBLOCK##	;CLR TBLOCK
	MOVE	TA,[TBLOCK,,TBLOCK+1]
	BLT	TA,TBLOCK+24
	MOVEM	W2,TBLOCK+4	;FACTS ABOUT DATA-NAME TO TBLOCK SETUP
	MOVEM	LN,(SAVPTR)	;SAVE LINE POSITION
	MOVEM	CP,1(SAVPTR)
DA96.1:	PUSHJ	PP,GETITM	;READ NEXT SOURCE WORD
	CAIE	TYPE,OF.	;IS IT "OF" OR "IN"?
	JRST	DA96.2		;NO, TIME TO EXIT
	PUSHJ	PP,GETITM	;YES, QUALIFIER SHOULD FOLLOW
	CAILE	TYPE,ENDIT.	;IS IT A RESERVED WORD?
	JRST	DA96.0		;NO, IT'S OK
	SWON	FREGWD		;YES, PREPARE TO REGET THAT ITEM
	EWARNJ	CE101.		;& FLAG THIS AS ILLEGAL QUALIFIER

DA96.0:	AOS	TA,TBLOCK+1	;COUNT THE QUALIFIER
	LDB	TB,[POINT 15,W2,15]	;GET NAMTAB LINK
	JUMPL	W1,JCE104	;QUALIFIER MUST BE DEFINED
	MOVEM	TB,TBLOCK+4(TA)	;STORE NAMTAB LINK OF QUAL IN TBLOCK
	JRST	DA96.1		;ANY MORE QUALS?

DA96.2:	MOVE	LN,(SAVPTR)	;RESTORE LINE POSITION OF ITEM IN CASE ERROR
	MOVE	CP,1(SAVPTR)
	SWON	FREGWD		;REGET THIS LAST WORD THAT WASN'T "OF"
	PUSHJ	PP,FINDAT##	;FIND A DATAB MATCH FOR THE ITEM
	JUMPN	DW,.+2		;SKIP IF ERROR
	POPJ	PP,		;RETURN WITH DATAB LINK IN TE
	PUSHJ	PP,FATAL	;GIVE MESSAGE
	MOVEI	TE,<CD.DAT>B20+1	;AIM AT DUMMY ENTRY
	POPJ	PP,

JCE104:	EWARNJ	CE104.		;NAME IS UNDEFINED
;LINK REPORT ITEM TO SOURCE

IFN RPW,<
	INTER.	DA97.
DA97.:	PUSHJ	PP,DA96.	;GET FULL SOURCE IDENTIFIER
	PUSH	PP,TE		;SAVE IT
	HRRZI	TA,(TE)		;GET PTR TO DATAB ENTRY
	PUSHJ	PP,LNKSET
	POP	PP,TE
	LDB	TB,DA.RPW##	;RPW LINK SHOULD BE 0 (I.E. W-S OR FILE)
	SKIPE	TB		;OK
	JRST	JCE367		;?SOURCE ITEM MUST BE IN FILE OR W-S SECTION
	SETO	TB,		;SET SOURCE FOR DETAIL BIT
	DPB	TB,DA.RDS
	HRRZ	TA,CURRPW	;PTR TO RPWTAB ENTRY
	LDB	TB,RW.SCD##	;SEEN SOURCE, ETC YET?
	JUMPN	TB,JCE16.	;YES, DUPLICATE ITEM
	HRRZI	TB,%RG.SR	;SAY IT HAS A SOURCE CLAUSE
	DPB	TB,RW.SCD
	DPB	TE,RW.SLK##	;STORE SOURCE LINK TO DATAB
	LDB	TD,RW.DAT##	;GET LINK TO CORRESP DATAB ENTRY
	LDB	TB,RW.RDL##	;& LINK TO CORRESP RD ENTRY
	HRRZ	TA,RPWLOC	;MAKE ABS. PTR TO RD ENTRY
	ADDI	TA,(TB)
	LDB	TB,RW.PC	;GET LINK TO THIS REPORT'S PAGE-CTR
	CAIE	TB,(TE)		;IS THIS SOURCE THE PAGE-CTR?
	POPJ	PP,		;NO
	PUSH	PP,TE		;SAVE LINK TO PAGE-CTR
	HRRZI	TA,(TD)		;MAKE PTR TO DATAB ENTRY
	PUSHJ	PP,LNKSET
	LDB	TB,DA.INS	;GET ITS SIZE
	POP	PP,TE
	PUSH	PP,TB		;SAVE SIZE OF DATAB ENTRY
	HRRZI	TA,(TE)		;MAKE ABS PTR TO PAGE-CTR
	PUSHJ	PP,LNKSET
	LDB	TC,DA.INS	;GET PAGE-CTR'S SIZE
	CAIN	TC,^D10		;1ST TIME THRU?
	SETZ	TC,		;YES, PAGE-CTR IS 0
	POP	PP,TB		;GET BACK SIZE OF DATAB ENTRY
	CAIG	TB,(TC)		;PAGE CTR ALREADY BIGGER?
	POPJ	PP,		;YES
	DPB	TB,DA.EXS	;NO, PAGE-CTR MUST GROW
	DPB	TB,DA.INS
	POPJ	PP,
	>
;ADVANCE TO NEXT ITEM

	INTER.	DA98.
DA98.:	SWOFF	FREGWD		;CLR REGET BIT
	JRST	GETITM		;GET NEXT ITEM

;PROCESS MISSING DATA-NAME FOR ITEM

	INTER.	DA99.
DA99.:
IFN RPW,<
	SKIPE	REPSEC		;REPORT SECTION?
	JRST	DA99.R		;YES
	>
	EWARNW	CE283.		;NO, ?DATA-NAME EXPECTED
	HRRZI	NODE,DD91P.	;MAKE DD91. THE NEXT NODE IN TREE
DA99.X:	HRRZM	NODE,(NODPTR)
	JRST	DA7.		;SET TO REGET WORD
IFN RPW,<
DA99.R:	SETZB	TA,NAMWRD	;INDICATE REPORT ITME HAS NO NAME
	DPB	TA,[POINT 15,W2,15]
	HRRZI	NODE,DD89P.	;CONTINUE AT NODE DD89.
	JRST	DA99.X
	>

	INTER.	DA99A.
DA99A.:
IFN RPW,<
	SKIPE	REPSEC		;IN REPORT SECTION?
	JRST	DA99.Q		;YES
	>
	EWARNW	CE17.		;NO, ?DATA-NAME EXPECTED
	HRRZI	NODE,DD98P.	;NEXT NODE IS DD98.
	JRST	DA99.X
IFN RPW,<
DA99.Q:	SETZB	TA,NAMWRD	;INDICATE ITEM HAS NO NAME
	DPB	TA,[POINT 15,W2,15]
	HRRZI	NODE,DD95P.	;CONTINUE AT NODE DD95.
	JRST	DA99.X
	>

;CHECK REDEFINES CLAUSE FOR REPORT SECTION ITEM

	INTER.	DA100.
DA100.:
IFN RPW,<
	SKIPE	REPSEC		;IN REPORT SECTION?
	EWARNJ	CE348.		;YES, ILLEGAL
	>
	POPJ	PP,
;PROCESS SUM IDENTIFIER

IFN RPW,<
	INTER.	DA101.
DA101.:	HRRZ	TA,CURRPW	;GET RPWTAB PTR
	LDB	TB,RW.NSI##	;GET NUMBER OF SUM IDENTIFIERS SEEN
	AOJ	TB,		;INCREMENT
	DPB	TB,RW.NSI	;& REPLACE
	ROT	TB,-1		;MOVE BIT 35 TO BIT 0
	MOVEM	TB,CTR		;SAVE LEFT/RIGHT FLAG
	JUMPGE	TB,DA101A	;IF EVEN, USE RT HF OF CURRENT ENTRY
	MOVE	TA,[XWD CD.RPW,1]	;IF ODD, GET ANOTHER RPWTAB WORD
	PUSHJ	PP,GETENT
	HLRZM	TA,(SAVPTR)	;SAVE RPWTAB LINK
DA101A:	PUSHJ	PP,DA60S.	;SAVE NAMTAB ENTRY ADDR
	PUSHJ	PP,DA25S.	;SET UP HLDTAB ENTRY
	HRRZ	TB,(SAVPTR)	;STORE RPWTAB LINK IN HLDTAB
	DPB	TB,HL.LNK
	HRRZI	TB,%HL.SL	;GET SUM ID (LH) FLAG
	SKIPL	CTR		;LEFT OR RIGHT HALF STORE?
	HRRZI	TB,%HL.SR	;RT., GET SUM ID (RH) FLAG
	DPB	TB,HL.COD	;STORE HLDTAB CODE
	POPJ	PP,
	>

;SUM UPON CLAUSE

IFN RPW,<
	INTER.	DA102.
DA102.:	PUSHJ	PP,DA60S.	;SAVE NAMTAB ENTRY ADDR
	PUSHJ	PP,DA25S.	;SET UP HLDTAB ENTRY
	HRRZI	TB,%HL.UP	;SET 'SUM UPON' CODE
	DPB	TB,HL.COD
	HLRZ	TB,CURRPW	;STORE RPWTAB LINK IN HLDTAB
	DPB	TB,HL.LNK
	POPJ	PP,
	>
;SET UP FOR SUM CLAUSE

IFN RPW,<
	INTER.	DA103.
DA103.:	HRRZ	TA,CURRPW	;PTR TO RPWTAB
	LDB	TB,RW.SCD	;SEEN SUM, ETC YET?
	JUMPN	TB,JCE16.	;YES, DUP. CLAUSE
	HRRZI	TB,%RG.SM	;NO, INDICATE ITEM HAS A SUM CLAUSE
	DPB	TB,RW.SCD
	LDB	TE,RW.DAT	;GET LINK TO CORRESP. DATAB ITEM
	PUSH	PP,TE		;SAVE
	MOVE	TA,[CD.HLD,,SZ.HLD]	;GET A HLDTAB ENTRY
	PUSHJ	PP,GETENT
	HRRZI	TB,%HL.SC	;SET "BUILD-SUM-CTR" CODE
	DPB	TB,HL.COD
	POP	PP,TE
	DPB	TE,HL.LNK	;& PUT IN LINK TO DATAB ITEM
	POPJ	PP,
	>

;SET DEFAULTS FOR PAGE LIMIT CLAUSE

IFN RPW,<
	INTER.	DA104.
DA104.:	PUSHJ	PP,GETRDL	;GET PTR TO RPWTAB GROUP ENTRY
	LDB	TB,RW.PHL	;GET PAGE HEADING LINE #
	JUMPN	TB,.+3		;SET?
	MOVEI	TB,1		;NO, MAKE IT 1
	DPB	TB,RW.PHL
	LDB	TC,RW.FDE	;GET FIRST DETAIL LINE #
	JUMPN	TC,.+3		;SET?
	MOVEI	TC,(TB)		;NO, DEFAULT = PHL
	DPB	TC,RW.FDE
	LDB	TB,RW.PAG	;PAGE LIMIT
	LDB	TC,RW.LDE##	;LAST DETAIL
	LDB	TD,RW.CFL	;FOOTING
	JUMPN	TC,DA104A	;LDE SET?
	JUMPN	TD,.+2		;NO, CFL SET?
	MOVEI	TD,(TB)		;NO, NEITHER SET. MAKE BOTH = PAGE LIMIT
	MOVEI	TC,(TD)		;MAKE LDE = CFL
	JRST	.+3
DA104A:	JUMPN	TD,CPOPJ	;LDE SET. IS CFL SET?
	MOVEI	TD,(TC)		;NO, MAKE CFL = LDE
	DPB	TC,RW.LDE	;STORE VALUES
	DPB	TD,RW.CFL
	POPJ	PP,
	>
;CK FOR MISSING PERIOD ON DATA ITEM

	INTER.	DA105.
DA105.:	CAIE	TYPE,INTGR.
	CAIN	TYPE,2000+INTGR.
	JRST	DA105B
	CAIE	TYPE,2000+PROC.
	CAIN	TYPE,2000+WORKI.
	JRST	DA105A
	CAIE	TYPE,2000+REPOR.
	CAIN	TYPE,2000+LINKG.
	JRST	DA105A
	EWARNJ	CE18.		;IMPROPER CLAUSE
DA105A:	SWON	FREGWD;
	PUSHJ	PP,CE125.	;PERIOD ASSUMED
	JRST	DA107.		;POP UP A LEVEL IN TREE
DA105B:	HLRZ	TB,W1		;GET THE INTEGER
	ANDI	TB,177
	MOVEM	TB,CTR
	HRRZI	TA,LITVAL
	PUSHJ	PP,GETVAL##
	CAIN	TC,^D77
	JRST	DA105A
	CAIL	TC,1
	CAILE	TC,^D49
	EWARNJ	CE18.		;IMPROPER CLAUSE
	JRST	DA105A
;STORE CODE LINK

IFN RPW,<
	INTER.	DA106.
DA106.:	LDB	TA,[POINT 15,W2,15]	;NAMTAB LINK
	HRRZI	TB,CD.MNE
	PUSHJ	PP,FNDLNK
	JFCL
	PUSHJ	PP,GETRDL	;GET PTR TO RD ENTRY
	HLRZS	TB
	DPB	TB,RW.COD##
	POPJ	PP,
	>

;END OF DATA ITEM
;IF IN REPORT SECTION AND ITEM HAS NO NAME, GIVE IT ONE

	INTER.	DA107.
DA107.:
IFN RPW,<
	SKIPE	REPSEC		;IN REPORT SECTION?
	PUSHJ	PP,FAKNAM	;YES
	>
	JRST	DA0.		;NOW POP UP A LEVEL IN TREE
IFN SERCH,<

;ASCENDING KEY FOR OCCURS

	INTER.	DA108.
DA108.:	HRRZI	TB,%HL.KY	;ASC. KEY CODE
DA108X:	MOVEM	TB,TBLOCK	;SAVE CODE UNTIL NAME SEEN
	POPJ	PP,

;DESCENDING KEY FOR OCCURS

	INTER.	DA109.
DA109.:	HRRZI	TB,%HL.DY	;DESC. KEY CODE
	JRST	DA108X

;ASCENDING/DESCENDING KEY FOR OCCURS

	INTER.	DA110.
DA110.:	PUSHJ	PP,DA60S.	;SAVE NAMTAB ADDR
	PUSHJ	PP,DA25S.	;SET UP HLDTAB ENTRY
	HRRZ	TB,TBLOCK	;STORE KEY CODE
	DPB	TB,HL.COD
	HRRZ	TA,CURDAT##	;PTR TO DATAB ENTRY
	LDB	TB,DA.PWA	;PIC WORDS ALLOCATED?
	JUMPN	TB,DA110A	;YES
	MOVE	TA,[CD.DAT,,SZ.MSK]	;NO, DO IT
	PUSHJ	PP,GETENT
	HRRZ	TA,CURDAT
	SETO	TB,		;& SAY SO
	DPB	TB,DA.PWA
DA110A:	LDB	TB,DA.KEY##	;INCREMENT KEY CTR
	AOJ	TB,
	DPB	TB,DA.KEY
	MOVE	TA,[CD.DAT,,1]	;GET A WORD ON DATAB ENTRY FOR KEY
	PUSHJ	PP,GETENT
	HLRZ	TB,TA		;SAVE DATAB ADDR
	HLRZ	TC,CURHLD
	HRRZ	TA,HLDLOC	;MAKE ABS. PTR TO HLDTAB ENTRY
	ADDI	TA,(TC)
	DPB	TB,HL.LNK	;STORE DATAB PTR IN HLDTAB
	POPJ	PP,

	>

;INITIALIZE LINKAGE SECTION

	INTER.	DA112.
DA112.:	SETOM	LNKSEC##	;SET LINKAGE SECTION FLAG
	SETOM	SUBPRG##	;THIS IS A SUBPROGRAM
	MOVE	TB,EAS1PC	;SAVE DATA PC WHILE DOING
	MOVEM	TB,PCHOLD	;  LINKAGE SECTION
	JRST	DA3.0		;REST IS LIKE WORKING-STORAGE

	INTER.	DCA1.
DCA1.:	PUSHJ	PP,DA10.
	JRST	DA2.


	INTER.	DCA2.
DCA2.:	PUSHJ	PP,DA10.
	JRST	DA3.


	INTER.	DCA3.
DCA3.:	PUSHJ	PP,DA5.
	JRST	DA0.


	INTER.	DCA4.
DCA4.:	PUSHJ	PP,DA7.
	JRST	DA0.


	INTER.	DCA5.
DCA5.:	PUSHJ	PP,DA11.
	JRST	DA28.

IFN RPW,<
	INTER.	DCA6.
DCA6.:	PUSHJ	PP,DA10.
	JRST	DA63.
	>

	INTER.	DCA7.
DCA7.:	PUSHJ	PP,DA10.
	JRST	DA112.


	INTER.	DCA10.
DCA10.:	PUSHJ	PP,DA9.
	JRST	DA58.
SUBTTL	GENERATE LITERALS FOR FILE TABLES

CLEANC:	HRRZ	TA,HLDLOC##	;START OF HLDTAB
	AOJ	TA,
	MOVEM	TA,CURHLD##	;PTR TO 1ST ENTRY
CLNHLD:	HRRZ	TA,CURHLD	;NEXT HLDTAB ENTRY
	HRRZ	TB,HLDNXT##	;PTR TO END
	CAILE	TA,(TB)		;PAST THE END YET?
	JRST	CLNFIL		;YES, ALL DONE
	LDB	TC,HL.COD
	CAIN	TC,%HL.IX	;20: CK FOR "MAKE INDEX" CODE
	JRST	CLHIDX
IFN RPW,<
	CAIN	TC,%HL.SC	;14: CK FOR "BUILD-SUM-CTR" CODE
	JRST	CLHSCT		;YES
	>
	SETZM	TBLOCK		;CLR TBLOCK
	MOVE	TC,[TBLOCK,,TBLOCK+1]
	BLT	TC,TBLOCK+24
	MOVE	TC,(TA)		;GET 1ST WORD OF HLDTAB ENTRY (ESSENTIALLY W2)
	MOVEM	TC,TBLOCK+4	;& PUT IT IN TBLOCK SETUP
	LDB	TC,HL.QAL	;GET # OF QUALS
	JUMPE	TC,CLNHL8	;THERE AREN'T ANY
	MOVEM	TC,CTR
CLNHL5:	AOS	TB,TBLOCK+1	;INDEX TO NEXT QUALIFIER
	CAMLE	TB,CTR		;DONE ALL?
	JRST	CLNHL3		;YES
	ADDI	TB,3		;AIM AT THAT QUALIFIER
	ROT	TB,-1
	ADDI	TB,(TA)
	HLRZ	TD,(TB)		;GET QUALIFIER (ASSUMING INDEX ODD)
	TLNE	TB,400000	;IF INDEX EVEN, GET RIGHT QUALIFIER
	HRRZ	TD,(TB)		;TD=NAMTAB LINK FOR QUALIFIER
	MOVE	TB,TBLOCK+1	;CTR IS INDEX TO QUALIFIER STORAGE
	MOVEM	TD,TBLOCK+4(TB)	;STORE QUALIFIER
	JRST	CLNHL5		;GET NEXT QUALIFIER

CLNHL3:	SOS	TBLOCK+1	;ADJUST QUAL CTR
CLNHL8:	PUSHJ	PP,FINDAT	;FIND DATAB LINK FOR ITEM
	JUMPE	DW,CLNHL7	;NO ERROR
	PUSHJ	PP,CLHBA1	;GIVE ERROR MESSAGE
	JRST	CLNHL4		;LEAVE TBLOCK+4 NON-ZERO
CLNHL7:	SETZM	TBLOCK+4	;CLR UNDEFINED FLAG
CLNHL4:	HRRZ	TA,CURHLD	;GET HLDTAB PTR
	LDB	TB,HL.LNK	;LINK TO PLACE WHERE DATA LINK WANTED
	ANDI	TB,077777	;MASK OFF TABLE BITS
	LDB	TC,HL.COD	;TYPE OF THIS HLDTAB ITEM
	MOVEI	TA,(TB)
	ADD	TA,FILLOC##	;ASSUME IT'S A FILTAB REFERENCE
	XCT	.(TC)		;DO ACTION FOR TYPE
	JRST	CLHACK		;1: STORE ACTUAL KEY DATAB LINK
	JRST	CLHVID		;2: STORE VALUE OF IDENTIFICATION DATAB LINK
	JRST	CLHVDW		;3: STORE VALUE OF DATE-WRITTEN DATAB LINK
	JRST	CLHVPP		;4: STORE VALUE OF PROJ-PROG # DATAB LINK
	JRST	CLHLFL		;5: STORE LOW FILE-LIMIT DATAB LINK
	JRST	CLHHFL		;6: STORE HIGH FILE-LIMIT DATAB LINK
	JRST	CLHDOC		;7: STORE DEPENDING FOR OCCURS DATAB LINK
IFN SERCH,<
	JRST	CLHKOC		;10: STORE ASCN. KEY FOR OCCURS DATAB LINK
	>
IFE SERCH,<0>
IFN RPW,<
	JRST	CLHSML		;11: STORE SUM ID DATAB LINK IN LEFT HALF
	JRST	CLHSMR		;12: STORE SUM ID DATAB LINK IN RIGHT HALF
	JRST	CLHUPN		;13: STORE SUM-UPON DATAB LINK
	0			;14: (EXECUTED 1ST IF SEEN)
	>
IFE RPW,<REPEAT 4,<0>>
IFN ISAM,<
	JRST	CLHSKY		;15: STORE SYMBOLIC KEY DATAB LINK
	JRST	CLHRKY		;16: STORE RECORD KEY DATAB LINK
	>
IFE ISAM,<REPEAT 2,<0>>
IFN SERCH,<
	JRST	CLHDKY		;17: STORE DESC. KEY FOR OCC. DATAB LINK
	>
IFE SERCH,<0>
	0			;20: (EXECUTED 1ST IF SEEN)

CLNHL9:	MOVE	TA,CURHLD	;GET HLDTAB PTR
	LDB	TB,HL.QAL	;NUMBER OF QUALIFIERS
	MOVEM	TB,CTR
	ADDI	TB,5		;ROUND UP + STANDARD # OF HALFWORDS
	LSH	TB,-1		;DIVIDED BY 2
	ADDM	TB,CURHLD	;AIM AT NEXT HLDTAB ENTRY
	JRST	CLNHLD
;STORE INDIVIDUAL LINKS IN APPROPRIATE TABLES

IFN RPW,<

CLHSML:	SUB	TA,FILLOC
	ADD	TA,RPWLOC
	PUSHJ	PP,SUMCK	;SEE THAT ITEM IS A SUM CTR
	PUSHJ	PP,CLHSME	;ERROR
	HRLM	TE,(TA)		;STORE DATAB LINK TO SUM ADDEND IN LEFT HALF
	JRST	CLNHL9

CLHSMR:	SUB	TA,FILLOC
	ADD	TA,RPWLOC
	PUSHJ	PP,SUMCK	;SEE THAT ITEM IS A SUM CTR
	PUSHJ	PP,CLHSME	;ERROR
	HRRM	TE,(TA)		;STORE DATAB LINK TO SUM ADDEND IN RIGHT HALF
	JRST	CLNHL9

CLHSME:	HRRZI	DW,CE358.	;NOT A TYPE DETAIL OR CONTROL-FOOTING
	JRST	CLHBA1

CLHUPN:	SUB	TA,FILLOC	;NOT FILTAB REF. BUT RPWTAB
	ADD	TA,RPWLOC
	PUSH	PP,TE		;SAVE AC'S
	PUSH	PP,TA		;SAVE RPWTAB PTR
	HRRZI	TA,(TE)		;MAKE PTR TO DATA ITEM
	PUSHJ	PP,LNKSET
	LDB	TB,DA.RPW	;GET LINK TO CORRESP RPW ITEM
	HRRZ	TA,RPWLOC	;MAKE PTR TO RPWTAB ENTRY
	ADDI	TA,(TB)
	LDB	TB,RW.TYP	;MUST BE TYPE DETAIL
	CAIE	TB,%RG.DE
	PUSHJ	PP,CLHUPE	;IT'S NOT
	SETO	TB,		;SET REFERENCED-BY-SUM-UPON BIT
	DPB	TB,RW.RSU##
	POP	PP,TA		;GET BACK AC'S
	POP	PP,TE
	MOVE	TC,RW.UPN##	;GET PTR TO UPON CLAUSE LINK
	JRST	CLNST1		;PUT LINK IN RPWTAB 'UPON'

CLHUPE:	HRRZI	DW,CE364.	;?SUM UPON MUST REF. TYPE DETAIL
	JRST	CLHBA1

	>

;MAKE A SUM COUNTER FOR DATAB ITEM

IFN RPW,<
CLHSCT:	LDB	TE,HL.LNK	;GET DATAB LINK
	PUSHJ	PP,SUMCTR	;GIVE ITEM A SUM-CTR
	JRST	CLNHL9
	>
;CK THAT SUM-ID IS A SUM-CTR
;IF NOT, MAKE ONE FOR THE ITEM

IFN RPW,<
SUMCK:	PUSH	PP,TA		;SAVE CRUCIAL AC'S
	MOVEM	TE,(SAVPTR)
	HRRZI	TA,(TE)		;MAKE PTR TO DATA ITEM
	PUSHJ	PP,LNKSET
	LDB	TB,DA.SCT##	;IS THIS A SUM-CTR?
	JUMPE	TB,SUMCKB	;NO
	DPB	TB,DA.RBS##	;YES, SET REF'D BY SUM BIT
	JRST	SUMCKX
SUMCKB:	LDB	TB,DA.RPW	;NO, GET PTR TO CORRESP RPW ENTRY
	JUMPE	TB,SUMCKA	;MUST BE A SOURCE ITEM
	HRRZ	TA,RPWLOC
	ADDI	TA,(TB)
	LDB	TB,RW.TYP	;GET TYPE
	CAIN	TB,%RG.CF	;CONTROL-FOOTING?
	JRST	SUMCKS		;YES, MUST MAKE A SUM-CTR FOR IT
	JRST	SUMCKE		;NOT A CF & NOT A SOURCE FOR DETAIL
SUMCKA:	LDB	TB,DA.RDS##	;GET SOURCE FOR DETAIL BIT
	JUMPE	TB,SUMCKE	;NOT ON
	DPB	TB,DA.RBS	;SET REFERENCED BY SUM BIT
	JRST	SUMCKX		;EXIT
SUMCKS:	HRRZ	TE,(SAVPTR)	;MAKE A SUM-CTR FOR CF ITEM
	PUSHJ	PP,SUMCTR
	HRRZI	TA,(TB)		;GET PTR TO SUM CTR
	SETO	TB,		;SET REF'D BY SUM BIT
	DPB	TB,DA.RBS
	JRST	SUMCKY
SUMCKX:	MOVE	TE,(SAVPTR)	;RESTORE TE
SUMCKY:	AOS	-1(PP)		;SKIP RETURN
SUMCKE:	POP	PP,TA		;RESTORE TA
	POPJ	PP,
	>
;MAKE A SUM-CTR FOR A DATA ITEM (WHOSE LINK IS IN TE)

IFN RPW,<
SUMCTR:	HRRZ	TA,DATLOC	;MAKE LINK & PTR TO ITEM'S DATAB ENTRY
	ADDI	TA,(TE)
	HRLI	TA,(TE)
	TRZ	TA,100000
	MOVEM	TA,CURDAT##
	PUSHJ	PP,RPWNAM	;MAKE A NAME FOR THE GROUP ITEM ("RWITM...")
	MOVE	TA,[CD.DAT,,SZ.DAT]	;MAKE A DATAB ENTRY FOR SUM CTR
	PUSHJ	PP,GETENT
	MOVEM	TA,(SAVPTR)	;SAVE LINK & PTR TO SUM CTR DATAB ENTRY
	HRRZ	TA,CURDAT	;MAKE PTR TO CORRESP RPWTAB ENTRY
	LDB	TB,DA.RPW
	HRRZ	TA,RPWLOC
	ADDI	TA,(TB)
	HLRZ	TB,(SAVPTR)	;PUT SUM CTR DATAB LINK IN RPWTAB
	DPB	TB,RW.SLK
	HRRZ	TA,CURDAT	;GET GROUP ITEMS SAME NAME LINK
	LDB	TB,DA.SNL
	SETZ	TC,		;& PUT 0 WHERE IT WAS
	DPB	TC,DA.SNL
	SETO	TC,		;& SET FAKE NAME BIT ON GP ITEM
	DPB	TC,DA.FAK##
	LDB	TD,DA.LNC	;ALSO GET GROUP ITEM'S LINE POSITION
	LDB	TE,DA.INS	;& SIZE
	LDB	TC,DA.NDP	;& # OF DECIMAL PLACES
	HRRZ	TA,(SAVPTR)	;MOVE GROUP ITEMS SNL TO SUM CTRS SNL SLOT
	DPB	TB,DA.SNL
	DPB	TD,DA.LNC	;PUT GP ITEM'S LINE POS. IN SUM CTR ENTRY
	DPB	TE,DA.INS	; " SIZE
	DPB	TE,DA.EXS
	DPB	TC,DA.NDP	; " # DEC. PLACES
	HRRZ	TD,EAS1PC	;GET NEXT FREE DATA LOC
	DPB	TD,DA.LOC	;STORE ASSIGNED LOC
	AOJ	TD,		;INCREMENT FOR 1-WORD SUM-CTR
	CAILE	TE,^D10		;SUM CTR MORE THAN 10 DIGITS?
	AOJ	TD,		;YES, NEED A 2-WORD CTR
	MOVEM	TD,EAS1PC	;& SAVE LAST USED
	HRRZI	TB,%CL.NU	;SET NUMERIC CLASS IN DATAB
	DPB	TB,DA.CLA
	HRRZI	TB,%US.1C	;& 1-WORD COMP USAGE
	DPB	TB,DA.USG
	HRRZI	TB,CD.DAT	;SET DATAB CODE
	DPB	TB,[POINT 3,(TA),2]
	HRRZI	TB,77		;MAKE SUM-CTR LEVEL 77
	DPB	TB,DA.LVL
	DPB	TB,DA.PIC	;SET PIC SEEN BIT
	DPB	TB,DA.SGN	;& SIGNED BIT
	DPB	TB,DA.SCT	;& SUM-CTR BIT
	DPB	TB,DA.DEF	;& DEFINED BIT
	HRRZ	TB,NAMADR	;PUT GROUP ITEMS DATAB LINK IN 'RWITM' NAMTAB ENTRY
	MOVE	TA,CURDAT
	HLRM	TA,(TB)
	LDB	TC,DA.NAM	;GET GP ITEMS NAMTAB LINK
	HLRZ	TD,NAMADR	;& PUT LINK TO 'RWITM' IN ITS PLACE
	DPB	TD,DA.NAM
	HRRZ	TA,(SAVPTR)	;MOVE GP ITEMS NAMTAB LINK TO SUM CTR
	DPB	TC,DA.NAM
	HLRZ	TE,CURDAT	;NOW SUBSTITUTE SUM CTR FOR GP ITEM IN SNL CHAIN
	HRRZ	TA,NAMLOC
	ADDI	TA,(TC)
	HRRZ	TB,(TA)
	CAIN	TB,(TE)
	JRST	DA103X
DA103L:	HRRZI	TC,(TB)
	ANDI	TC,077777
	HRRZ	TA,DATLOC
	ADDI	TA,(TC)
	HRRZ	TB,(TA)
	CAIE	TB,(TE)
	JRST	DA103L
DA103X:	MOVE	TB,(SAVPTR)
	HLRM	TB,(TA)
	POPJ	PP,
	>
CLHDOC:	SUB	TA,FILLOC	;NOT A FILTAB REFERENCE
	ADD	TA,DATLOC	;BUT A DATAB REFERENCE
	PUSHJ	PP,CLHSUB	;GET PTR TO DATA ITEM
	SKIPE	TBLOCK+4	;UNDEFINED?
	JRST	CLHDOX		;YES
	LDB	TB,DA.CLA	;CK CLASS OF ITEM
	CAIE	TB,2		;NUMERIC?
	PUSHJ	PP,CLE264	;NO
	LDB	TB,DA.NDP	;NUMBER OF DECIMAL PLACES
	LDB	TC,DA.DPR	;DECIMAL PT. TO RIGHT?
	JUMPN	TC,CLHDOX	;YES, OK
	JUMPE	TB,CLHDOX
	PUSHJ	PP,CLE264	;NOT AN INTEGER
CLHDOX:	MOVE	TC,DA.DEP##	;GET DEP-FOR-OCC PTR
	JRST	CLNSTO

IFN SERCH,<
CLHKOC:	HRLI	TE,0		;ASC. KEY FLAG
CLHKOX:	SUB	TA,FILLOC
	ADD	TA,DATLOC
	MOVEM	TE,(TA)		;PUT KEY LINK & FLAG IN DATAB ENTRY WORD
	JRST	CLNHL9
	>

IFN SERCH,<
CLHDKY:	HRLI	TE,400000	;DESC. KEY FLAG
	JRST	CLHKOX
	>
IFN ISAM,<

CLHSKY:	MOVE	TC,FI.SKY##	;GET FILTAB SYMBOLIC KEY PTR
	JRST	CLNST1

CLHRKY:	MOVE	TC,FI.RKY##	;GET FILTAB RECORD KEY PTR
	JRST	CLNST1

	>

CLHACK:	SKIPE	TBLOCK+4	;UNDEFINED?
	JRST	CLHACX		;YES
	PUSHJ	PP,CLHSUB	;GET USAGE OF DATA ITEM
	CAIE	TB,%US.1C	;1-WORD COMP?
	PUSHJ	PP,CLHBAD	;NO
	CAILE	TC,^D10		;10 DIGITS OR LESS?
	PUSHJ	PP,CLHSIZ	;NO
CLHACX:	MOVE	TC,FI.ACK##	;ACT-KEY PTR
	JRST	CLNSTO
;MAKE INDEX ENTRY IN DATAB & LINK ITEM INDEXED TO IT

CLHIDX:	LDB	TB,HL.NAM	;STASH HLDTAB INFO IN TBLOCK
	MOVEM	TB,TBLOCK	;NAMTAB LINK OF INDEX
	LDB	TB,HL.LNK
	MOVEM	TB,TBLOCK+1	;DATAB LINK OF INDEXED ITEM
	LDB	TB,HL.LNC
	MOVEM	TB,TBLOCK+2	;LINE&CHAR POS. OF INDEX

	HRRZ	TA,TBLOCK	;LOOK FOR ITEM ALREADY IN DATAB
	HRRZI	TB,CD.DAT
	PUSHJ	PP,FNDLNK
	JRST	CLHID2		;NONE -- MAKE ONE

	HRRZI	TA,(TB)		;GET DATAB LINK
	HLRM	TB,TBLOCK+4	;SAVE A COPY
	LDB	TB,DA.IDX##	;IS THIS AN INDEX?
	JUMPN	CLHID3		;YES

	HRRZI	DW,CE297.	;ILLEGAL INDEX
	LDB	LN,[POINT 13,TBLOCK+2,28]
	LDB	CP,[POINT 7,TBLOCK+2,35]
	PUSHJ	PP,FATAL
	JRST	CLNHL9

CLHID2:	MOVE	TA,[XWD CD.DAT,SZ.DAT]	;MAKE INDEX
	PUSHJ	PP,GETENT
	HLRZM	TA,TBLOCK+4	;SAVE LINK TO INDEX
	MOVE	TB,TBLOCK	;GET NAMTAB LINK OF INDEX
	DPB	TB,DA.NAM
	HRRZI	TB,CD.DAT
	DPB	TB,[POINT 3,(TA),2]
	HRRZI	TB,77
	DPB	TB,DA.LVL
	HRRZI	TB,%CL.NU
	DPB	TB,DA.CLA##
	HRRZI	TB,5
	DPB	TB,DA.INS
	DPB	TB,DA.EXS
	SETO	TB,
	DPB	TB,DA.SGN
	DPB	TB,DA.PIC
	DPB	TB,DA.IDX	;SET INDEX BIT
	HRRZI	TB,%US.IN
	DPB	TB,DA.USG##
	MOVE	TB,TBLOCK+2	;SAVE LINE & CHAR POS.
	DPB	TB,DA.LNC
	HRRZ	TB,EAS1PC	;ALLOCATE WORD IN AS1FIL FOR INDEX
	DPB	TB,DA.LOC
	HRRZI	TB,44
	DPB	TB,DA.RES
	SETO	TB,
	DPB	TB,DA.DEF
	AOS	EAS1PC
	LDB	TB,DA.NAM
	HRRI	TA,(TB)
	PUSHJ	PP,PUTLNK

CLHID3:	HRRZ	TA,CURHLD	;CHANGE NAMTAB LINK OF INDEX ITEM
	MOVE	TB,TBLOCK+4	;  IN HLDTAB TO DATAB LINK
	DPB	TB,HL.NAM
	JRST	CLNHL9
CLHVID:	SKIPE	TBLOCK+4	;UNDEFINED?
	JRST	CLHVIX		;YES
	PUSHJ	PP,CLHSUB	;GET USAGE OF DATA ITEM
	CAIL	TB,%US.D6	;MUST BE DISPLAY
	CAILE	TB,%US.D7
	PUSHJ	PP,CLHBAD	;WRONG USAGE
	CAIE	TC,^D9		;9 CHARS?
	PUSHJ	PP,CLHSIZ	;NO
CLHVIX:	MOVE	TC,FI.VID##	;VAL-OF-ID PTR
	JRST	CLNSTO

CLHVDW:	SKIPE	TBLOCK+4	;UNDEFINED?
	JRST	CLHVDX		;YES
	PUSHJ	PP,CLHSUB	;GET USAGE OF DATA ITEM
	CAIL	TB,%US.D6	;MUST BE DISPLAY
	CAILE	TB,%US.D7
	PUSHJ	PP,CLHBAD	;WRONG USAGE
	CAIE	TC,6		;6 DIGITS?
	PUSHJ	PP,CLHSIZ	;NO
CLHVDX:	MOVE	TC,FI.VDW	;VAL-OF-DATE-WRITTEN PTR
	JRST	CLNSTO

CLHVPP:	SKIPE	TBLOCK+4	;UNDEFINED?
	JRST	CLHVPX		;YES
	PUSHJ	PP,CLHSUB	;GET USAGE OF DATA ITEM
	CAIE	TB,%US.1C	;1-WORD COMP?
	PUSHJ	PP,CLHBAD	;NO
	CAILE	TC,^D10		;10 DIGITS OR LESS?
	PUSHJ	PP,CLHSIZ	;NO
CLHVPX:	MOVE	TC,FI.VPP##	;VAL-OF-PROJ-PROG PTR
CLNSTO:	MOVE	TA,TBLOCK+1	;GET BACK STORAGE PTR
CLNST1:	LDB	TB,TC		;SEE IF ITEM ALREADY STORED
	JUMPE	TB,CLNST2
	PUSHJ	PP,CLHDUP	;YES, DUPLICATE CLAUSE
CLNST2:	DPB	TE,TC		;NO, STORE ITEM
	JRST	CLNHL9		;GO BACK FOR NEXT HLDTAB ENTRY

CLHLFL:	SKIPE	TBLOCK+4	;UNDEFINED?
	JRST	CLHLFX		;YES
	PUSHJ	PP,CLHSUB	;GET USAGE OF DATA ITEM
	CAIE	TB,%US.1C	;1-WORD COMP?
	PUSHJ	PP,CLHBAD	;NO
	CAILE	TC,^D10		;10 DIGITS OR LESS?
	PUSHJ	PP,CLHSIZ	;NO
	MOVE	TA,TBLOCK+1	;GET BACK STORAGE PTR
CLHLFX:	HRLM	TE,(TA)		;STORE DATAB LINK
	JRST	CLNHL9		;GO BACK FOR NEXT HLDTAB ENTRY

CLHHFL:	SKIPE	TBLOCK+4	;UNDEFINED?
	JRST	CLHHFX		;YES
	PUSHJ	PP,CLHSUB	;GET USAGE OF DATA ITEM
	CAIE	TB,%US.1C	;1-WORD COMP?
	PUSHJ	PP,CLHBAD	;NO
	CAILE	TC,^D10		;10 DIGITS OR LESS?
	PUSHJ	PP,CLHSIZ	;NO
	MOVE	TA,TBLOCK+1	;GET BACK STORAGE PTR
CLHHFX:	HRRM	TE,(TA)		;STORE DATAB LINK
	JRST	CLNHL9		;GO BACK FOR NEXT HLDTAB ENTRY

CLHSUB:	MOVEM	TA,TBLOCK+1	;SAVE STORAGE PTR
	MOVE	TA,TE		;GET ITEM DATAB LINK
	ANDI	TA,77777	;MAKE DATAB PTR
	ADD	TA,DATLOC##
	LDB	TB,DA.USG	;GET USAGE
	LDB	TC,DA.EXS	;& SIZE
	POPJ	PP,

CLHBAD:	HRRZI	DW,^D373	;WRONG USAGE
CLHBA1:	HRRZ	TA,CURHLD	;HLDTAB ADDR
	LDB	LN,HL.LN##	;GET LINE POSITION
	LDB	CP,HL.CP##
	PUSHJ	PP,FATAL	;GIVE ERROR MESSAGE
	MOVEI	TE,<CD.DAT>B20+1	;AIM AT DUMMY DATAB ENTRY
	POPJ	PP,

CLHDUP:	HRRZI	DW,^D16		;DUPLICATE CLAUSE MESSAGE
	JRST	CLHBA1

CLHSIZ:	HRRZI	DW,^D340	;WRONG SIZE
	JRST	CLHBA1

CLE264:	HRRZI	DW,^D264	;WRONG CLASS
	JRST	CLHBA1
CLNFIL:	SKIPL	TA,PCHOLD	;RESET EAS1PC IF IT WAS CHANGED
	MOVEM	TA,EAS1PC	;  FOR LINKAGE SECTION
	MOVE	TA,AS2BUF##	;SET
	MOVEM	TA,JOBFF##	;  UP
	OUTBUF	AS2,2		;  AS2FIL
	MOVE	TA,FILLOC
	CAMN	TA,FILNXT##
	JRST	CLNCON		;CLEAN UP CONTAB LITERALS
	HRRZI	TA,CD.FIL*1B20+1
CLNC:	HRLZM	TA,CURFIL
	PUSHJ	PP,LNKSET
	HRRM	TA,CURFIL
	LDB	TB,FI.DRL	;ANY DATA RECORD?
	JUMPN	TB,CLNC0	;YES
IFN RPW,<
	LDB	TB,FI.RPG	;NO - REPORT FILE?
	JUMPN	TB,CLNCRP	;YES
	>
	MOVEI	TB,<CD.DAT>B20+1	;NO, AIM AT DUMMY ENTRY
	DPB	TB,FI.DRL
	HRRZI	DW,CE38.	;?NO DATA RECORD
	LDB	LN,FI.LN
	LDB	CP,FI.CP
	PUSHJ	PP,FATAL
	JRST	CLNC0		;CONTINUE
CLNCRP:
IFN RPW,<
	PUSHJ	PP,RPWNAM	;MAKE A NAME FOR REPORT RECORD
	MOVE	TA,[CD.DAT,,SZ.DAT]
	PUSHJ	PP,GETENT
	MOVEI	TB,%US.D7	;DISPLAY-7 USAGE IN CORE
	DPB	TB,DA.USG
	MOVEI	TB,^D132	;RECORD SIZE = 132 CHARS.
	DPB	TB,DA.INS
	DPB	TB,DA.EXS
	MOVEI	TB,^D36		;BYTE RESIDUE
	DPB	TB,DA.RES
	MOVEI	TB,1		;LEVEL 01
	DPB	TB,DA.LVL
	DPB	TB,DA.PIC	;SAY PIC SEEN
	DPB	TB,DA.DFS	;DEFINED IN FILE SECTION
	DPB	TB,DA.DRC	;DATA RECORD
	DPB	TB,DA.DEF	;SAY DEFINED
	DPB	TB,DA.FAK	;SAY FAKE NAME
	DPB	TB,DA.FAL	;SET FATHER LINK BIT (FILE IS FATHER)
	HLRZ	TB,NAMADR	;STORE NAMTAB LINK TO DATA ENTRY
	DPB	TB,DA.NAM
	MOVEI	TB,%CL.AN	;ALPHANUMERIC CLASS
	DPB	TB,DA.CLA
	HLRZ	TB,CURFIL	;FILE = FATHER LINK
	DPB	TB,DA.BRO
	HRRZ	TB,NAMADR	;PUT LINK TO DATA ENTRY IN NAMTAB
	HLRM	TA,(TB)
	MOVS	TC,TA		;LINK FILE TO NEW DATA RECORD
	HRRZ	TA,CURFIL
	DPB	TC,FI.DRL
	MOVEI	TB,^D132	;RECORD SIZE
	DPB	TB,FI.MRS
	MOVEI	TB,%RM.7B	;ASCII RECORDING MODE
	DPB	TB,FI.ERM
	DPB	TB,FI.IRM
	MOVEI	TB,1		;RECORDING MODE DECLARED
	DPB	TB,FI.RM2
	LDB	TB,FI.LNC	;GET FILE DEFINITION LINE POSITION
	MOVS	TA,TC		;GET BACK PTR TO DATAB ENTRY
	DPB	TB,DA.LNC	;RECORD DEFN POSITION = FILE DEF. POS.
	>
CLNC0:	HRRZ	TA,CURFIL
	LDB	TB,FI.NDV		;NUMBER OF DEVICES
	JUMPLE	TB,PFL		;NONE
	MOVEM	TB,NDEV##
	SETZM	(SAVPTR)	;CLR FLAG
	CAIE	TB,1		;ONLY ONE DEVICE NAMED?
	JRST	CLNC2		;NO, FORGET THIS STUFF
	LDB	TC,FI.ACC	;INDEXED?
	CAIE	TC,%ACC.I
	JRST	CLNC2		;NO
	SETOM	(SAVPTR)	;YES, SET FLAG
	DPB	TC,FI.NDV	;SAY 2 DEVICES
CLNC2:	LDB	TB,FI.VAL
	JUMPE	TB,PFL		;LINK IS NULL
	HRLZM	TB,CURVAL##
	HRRZ	TA,TB
	PUSHJ	PP,LNKSET
	HRRM	TA,CURVAL
	PUSHJ	PP,GETTAG##
	HRRZ	TA,CURFIL
	DPB	CH,FI.VAL
	PUSHJ	PP,PUTTAG
	MOVE	TA,CURVAL	;SAVE CURVAL IN CASE WE NEED SAME DEV TWICE
	MOVEM	TA,1(SAVPTR)
PDEV:	SETZ	TA,
	PUSHJ	PP,PUTVAL
	AOSE	(SAVPTR)	;INDEXED FILE WITH ONLY ONE DEVICE?
	JRST	PDEV2		;NO
	MOVE	TA,1(SAVPTR)	;YES, REPEAT SAME DEVICE
	MOVEM	TA,CURVAL
	JRST	PDEV
PDEV2:	SOSLE	NDEV
	JRST	PDEV
	HRRZ	TA,CURFIL
PFL:	LDB	TB,FI.NFL##
	JUMPLE	TB,CHID		;NO FILE LIMITS
	ASH	TB,1
	MOVEM	TB,CFLM
	ADDI	TA,SZ.FIL
	HRLI	TA,442200
	MOVEM	TA,PNTS
PFL1:	ILDB	TA,PNTS
	SETZ	TE,
	JUMPE	TA,PFCNT
	LDB	TB,[POINT	3,TA,20]
	CAIE	TB,CD.VAL
	JRST	PFC1
	PUSHJ	PP,GETTAG
	MOVEM	CH,TBLOCK+12
	PUSHJ	PP,PUTTAG
	HRLZM	TA,CURVAL
	PUSHJ	PP,LNKSET
	HRRM	TA,CURVAL
	HRRZI	TA,1
	PUSHJ	PP,PUTVAL
	MOVE	TE,TBLOCK+12
PFCNT:	DPB	TE,PNTS
PFC1:	SOSLE	CFLM
	JRST	PFL1

CHID:	HRRZ	TA,CURFIL
	LDB	TB,FI.VID
	JUMPE	TB,CHID2		;NO VALUE-OF-ID
	LDB	TC,[POINT	3,TB,20]
	CAIE	TC,CD.VAL
	JRST	CHID.1		;NOT A LITERAL
	HRLZM	TB,CURVAL
	HRRZ	TA,TB
	PUSHJ	PP,LNKSET
	HRRM	TA,CURVAL
	PUSHJ	PP,GETTAG
	HRRZ	TA,CURFIL
	DPB	CH,FI.VID
	PUSHJ	PP,PUTTAG
	SETZ	TA,
	PUSHJ	PP,PUTVAL
	JRST	CHID2
CHID.1:	CAIE	TC,CD.DAT
	JRST	CHID2
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	LDB	TB,DA.DEF
	JUMPE	TB,CHIDE1	;NOT DEFINED
	LDB	TB,DA.USG
	CAIN	TB,1
	JRST	CHID.3		;DISPLAY-6
	CAIE	TB,2
	JRST	CHIDE2		;NOT DISPLAY-7
CHID.3:	LDB	TB,DA.EXS
	CAIN	TB,11
	JRST	CHID2		;SIZE 9
CHIDE2:	HRRZI	DW,CE62.
	LDB	LN,DA.LN
	LDB	CP,DA.CP
CHEE:	PUSHJ	PP,FATAL
	JRST	CHID2
CHIDE1:	HRRZI	DW,150
CHIDE:	HRRZ	TA,CURFIL
	LDB	LN,FI.LN
	LDB	CP,FI.CP##
	JRST	CHEE
CHID2:	HRRZ	TA,CURFIL
	LDB	TB,FI.VPP
	JUMPE	TB,CHID3
	LDB	TC,[POINT 3,TB,20]
	CAIE	TC,CD.VAL
	JRST	CHID.5
	HRLZM	TB,CURVAL
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	HRRM	TA,CURVAL
CHID.6:	PUSHJ	PP,GETTAG
	HRRZ	TA,CURFIL
	DPB	CH,FI.VPP
	PUSHJ	PP,PUTTAG
	HRLZI	CH,AS.XWD##
	HRRI	CH,1
	PUSHJ	PP,PUTAS2##
	AOS	EAS2PC
	PUSHJ	PP,PUTOCT
	PUSHJ	PP,PUTOCT
	JRST	CHID3
CHID.5:	CAIE	TC,CD.DAT
	JRST	CHID3
	HRRZI	TA,(TB)
	PUSHJ	PP,LNKSET
	LDB	TB,DA.USG
	CAIN	TB,%US.1C
	JRST	CHID3
	HRRZI	DW,CE366.	;?PPN MUST BE 1-WORD COMP
	HRRZ	TA,CURFIL
	LDB	LN,FI.LN##
	LDB	CP,FI.CP
	PUSHJ	PP,WARN
	JRST	CHID.6
CHID3:	HRRZ	TA,CURFIL
	LDB	TB,FI.VDW
	JUMPE	TB,CHDATS	;NO VALUE-OF-DATE-WRITTEN
	LDB	TC,[POINT	3,TB,20]
	CAIE	TC,CD.VAL
	JRST	CHDATS		;NOT A LITERAL
	HRLZM	TB,CURVAL
	HRRZ	TA,TB
	PUSHJ	PP,LNKSET
	HRRM	TA,CURVAL
	LDB	TB,[POINT	7,(TA),6]
	CAIN	TB,6
	JRST	CH2.1
	CAILE	TB,6
	HRRZI	TB,6
	DPB	TB,[POINT	7,(TA),6]
	HRRZI	DW,77
	HRRZ	TA,CURFIL
	LDB	LN,FI.LN
	LDB	CP,FI.CP
	PUSHJ	PP,WARN
CH2.1:	PUSHJ	PP,GETTAG
	HRRZ	TA,CURFIL
	DPB	CH,FI.VDW
	PUSHJ	PP,PUTTAG
	SETZ	TA,
	PUSHJ	PP,PUTVAL
;CHECK TO INSURE ALL RECORDS FOR THIS FILE ARE < 4096 CHARACTERS
;AND THAT OCCURS DOES NOT APPEAR AT 01 LEVEL

CHDATS:	SETZM	RUSAGE		;CLEAR USAGE FLAG
	HRRZ	TA,CURFIL	;GET ADDRESS OF FILTAB ENTRY
	LDB	TA,FI.DRL##	;GET RECORD LINK
	JUMPE	TA,CLNCUP	;IF ZERO--FORGET IT

CHDA.1:	PUSHJ	PP,LNKSET	;GET RECORD ADDRESS
	LDB	TE,DA.USG	;GET USAGE OF RECORD
	CAIE	TE,%US.D7	;DISPLAY-7?
	HRRZI	TE,1		;NO, ASSUME DISPLAY-6

	SKIPN	RUSAGE		;SEEN 1ST RECORD BEFORE THIS?
	MOVEM	TE,RUSAGE	;NO, SAVE USAGE OF 1ST
	CAMN	TE,RUSAGE	;YES, THIS USAGE CONFLICT WITH 1ST RECORD?
	JRST	CHDA.4		;NO

	MOVEI	DW,CE33.	;YES, ERROR
	HRRM	TA,CURDAT	;SAVE DATAB PTR
	HRRZ	TA,CURFIL	;GET FILTAB PTR
	LDB	LN,FI.LN
	LDB	CP,FI.CP
	PUSHJ	PP,FATAL
	HRRZ	TA,CURDAT	;GET BACK DATAB PTR

CHDA.4:	LDB	TE,DA.EXS	;GET EXTERNAL SIZE
	CAIG	TE,MAXFSS##	;GREATER THAN LARGEST ALLOWED SIZE?
	JRST	CHDA.3		;NO

	LDB	LN,DA.LN	;YES--
	LDB	CP,DA.CP	;  PUT
	MOVEI	DW,^D322	;  OUT
	PUSHJ	PP,FATAL	;  DIAGNOSTIC

CHDA.3:	LDB	TE,DA.OCC	;OCCURS AT 01 LEVEL?
	JUMPE	TE,CHDA.2	;NO
	LDB	LN,DA.LN	;YES, FLAG IT
	LDB	CP,DA.CP
	MOVEI	DW,^D325
	PUSHJ	PP,FATAL

CHDA.2:	LDB	TE,DA.FAL	;IS BROTHER/FATHER LINK
	JUMPN	TE,CLNCUP	;  A FATHER?
	LDB	TA,DA.BRO	;NO--LOOK AT NEXT RECORD
	JUMPN	TA,CHDA.1

CLNCUP:	HRRZ	TA,CURFIL
	LDB	TA,FI.NXT##
	JUMPN	TA,CLNC		;NEXT FILE TABLE
CLNCON:	MOVE	TA,CONLOC##
	CAMN	TA,CONNXT##
	POPJ	PP,
	HRRZI	TA,CD.CON*1B20+1
	HRLZM	TA,CURCON
	PUSHJ	PP,LNKSET
C0.:	HRRM	TA,CURCON
	LDB	TB,CO.NVL
	LDB	TA,CO.DAT##
	LSH	TB,1
	HRRZM	TB,TBLOCK+1
	JUMPE	TA,C3.
	HRLZM	TA,CURDAT
	PUSHJ	PP,LNKSET
	HRRM	TA,CURDAT
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	MOVE	TE,[POINT	18,SZ.CON(TA)]
	MOVEM	TE,PNTS
C1.:	HRRZ	TA,CURCON
	ILDB	TA,PNTS
	ANDI	TA,077777
	JUMPE	TA,C2.
	IORI	TA,CD.LIT*1B20
	HRLZM	TA,CURLIT
	PUSHJ	PP,LNKSET
	HRRM	TA,CURLIT
	PUSHJ	PP,ADJUST
	HRRZ	TA,CURLIT
	LDB	TB,LI.FGC
	JUMPE	TB,C1.5
	LDB	TC,LI.FCC
	HRRZ	TA,CURCON
	LDB	TB,PNTS
	ANDI	TB,400000
	TRO	TB,200000
	CAIN	TC,SPACE.
	TRO	TB,040000
	CAIN	TC,ZERO.
	TRO	TB,20000
	CAIN	TC,QUOTE.
	TRO	TB,10000
	CAIN	TC,HIVAL.
	TRO	TB,4000
	CAIN	TC,LOVAL.
	TRO	TB,2000
	DPB	TB,PNTS
	JRST	C2.
C1.5:	PUSHJ	PP,GETTAG
	HRRZ	TA,CURCON
	LDB	TB,PNTS
	ANDI	TB,7B20
	HRRZI	TC,(CH)
	ANDI	TC,077777
	IORI	TB,(TC)
	DPB	TB,PNTS
	PUSHJ	PP,PUTTAG
	PUSHJ	PP,PUTLIT
C2.:	SOSLE	TBLOCK+1
	JRST	C1.
C3.:	HRRZ	TA,CURCON
	LDB	TB,CO.NVL##
	ADDI	TA,SZ.CON(TB)
	HRRZ	TC,CONNXT
	CAIG	TA,(TC)
	JRST	C0.
	POPJ	PP,
PUTLIT:	HRRZ	TA,CURDAT
	LDB	TB,DA.USG
	JRST	.+1(TB)
	POPJ	PP,
	JRST	PTLDSP		;DISPLAY-6
	JRST	PTLDSP		;DISPLAY-7
	POPJ	PP,
	JRST	PTL1WC		;1-WORD COMP
	JRST	PTL2WC		;2-WORD COMP
	JRST	PTLC1		;COMP-1
	JRST	PTL1WC		;INDEX

PTL1WC:	MOVE	CH,[XWD 610000,1]
	PUSHJ	PP,PUTAS2
	MOVE	CH,VALUE2
	AOS	EAS2PC
	JRST	PUTAS2
PTL2WC:	MOVE	CH,[XWD 604000,2]
	PUSHJ	PP,PUTAS2
	MOVE	CH,VALUE1##
	AOS	EAS2PC
	PUSHJ	PP,PUTAS2
	MOVE	CH,VALUE2##
	AOS	EAS2PC
	JRST	PUTAS2
PTLC1:	MOVE	CH,[XWD 602000,2]
	PUSHJ	PP,PUTAS2
	MOVE	CH,VALUE1
	PUSHJ	PP,PUTAS2
	MOVE	CH,VALUE2
	AOS	EAS2PC
	JRST	PUTAS2
PTLDSP:	SKIPG	TC,NCHITM
	POPJ	PP,
	ADD	TC,NCHWRD
	HRRZI	TC,-1(TC)
	IDIV	TC,NCHWRD
	HRLZI	CH,620000
	TSWT		CONVRT;
	HRLZI	CH,640000
	HRRI	CH,(TC)
	SKIPA
P2.:	AOS	EAS2PC
	PUSHJ	PP,PUTAS2
	SETZ	CH,
P0:	HRRZ	TA,CURLIT##
	TSWT	CONVRT;
	SKIPA	TE,[POINT	7,CH]
	MOVE	TE,[POINT	6,CH]
P1.:	SOSGE	NCHITM
	JRST	P3.
	PUSHJ	PP,GETCHR
	IDPB	TC,TE
	TLNE	TE,760000
	JRST	P1.
	JRST	P2.
P3.:	LDB	TB,[POINT	6,TE,5]
	CAIN	TB,44
	POPJ	PP,
	AOS	EAS2PC
	JRST	PUTAS2
;PUT ASCII # ON AS2 AS A BINARY CONSTANT

PUTOCT:	HRRZ	TA,CURVAL
	HRLI	TA,440700
	ILDB	TD,TA
	MOVEM	TD,CTR
	SETZ	TC,
PUTOC1:	SOJL	TD,PUTOC2
	ILDB	TB,TA
	IMULI	TC,10
	ADDI	TC,-60(TB)
	JRST	PUTOC1
PUTOC2:	AOJ	TA,
	HRRM	TA,CURVAL
	HRLZI	CH,(TC)
	HRRI	CH,AS.CNB##
	JRST	PUTAS2
PUTVAL:	JUMPL	TA,PVOUT
	CAILE	TA,1
	JRST	PVOUT
	HRLZI	TB,440700
	HRR	TB,CURVAL
	MOVEM	TB,PNTR##
	ILDB	TC,PNTR
	MOVEM	TC,CTR
	JRST	.+1(TA)
	JRST	PV.SXB
PV.INT:	MOVE	CH,[XWD	610000,1]
	PUSHJ	PP,PUTAS2
	MOVE	TD,PNTR
	PUSHJ	PP,GETV2##
	MOVE	CH,TC
	PUSHJ	PP,PUTAS2
	AOS	EAS2PC
	POPJ	PP,

PV.SXB:	ADDI	TC,5
	IDIVI	TC,6
	MOVEM	TC,TBLOCK+1
	HRLZI	CH,620000
	HRR	CH,TC
PV.1:	PUSHJ	PP,PUTAS2
	MOVE	TA,[POINT	6,CH]
	SETZ	CH,
	HRRZI	TE,6
PV.2:	ILDB	TB,PNTR
	CAIN	TB,":"
	HRRZI	TB,"-"
	CAIN	TB,";"
	HRRZI	TB,"."
	SUBI	TB,40
	IDPB	TB,TA
	SOSG	CTR
	JRST	PV.3
	SOJG	TE,PV.2
	JRST	PV.1
PV.3:	PUSHJ	PP,PUTAS2
	MOVE	TA,CURVAL
	LDB	TC,[POINT	7,(TA),6]
	ADDI	TC,5
	IDIVI	TC,5
	HLRZS	TA
	ADD	TA,TC
	HRLZM	TA,CURVAL
	PUSHJ	PP,LNKSET
	HRRM	TA,CURVAL
	MOVE	TB,EAS2PC
	ADD	TB,TBLOCK+1
	MOVEM	TB,EAS2PC
	POPJ	PP,


PVOUT:	MOVE	CH,[XWD	610000,1]
	PUSHJ	PP,PUTAS2
	SETZ	CH,
	JRST	PUTAS2


PUTTAG:	ANDI	CH,077777
	IORI	CH,CD.TAG*1B20
	HRLI	CH,720000
	PUSHJ	PP,PUTAS2
	MOVE	TE,EAS2PC
	TRZ	TE,1B18
	ANDI	CH,077777
	ROT	CH,-1
	JUMPL	CH,PTAG1
	ADD	CH,TAGLOC##
	HRLM	TE,(CH)
	POPJ	PP,
PTAG1:	ADD	CH,TAGLOC
	HRRM	TE,(CH)
	POPJ	PP,
SUBTTL	ADJUST NON-NUMERIC LITERALS

ADJUST:	SETZM	NPADL
	SETZM	NPADR
	SETZM	NCHLIT##
	SETZM	NCHLI2
	SETZM	NCHITM
	SETZM	ITMLOC
	SETZM	ITMRES
	SETZM	SIGNED
	SETZM	VALUE1
	SETZM	VALUE2
	MOVE	TB,[POINT 7,SZ.LIT(TA)]
	MOVEM	TB,BYTEPT##
	MOVEM	TB,BYTEP2##
	SKIPN	TA,CURDAT
	POPJ	PP,
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	SKIPN	TA,CURLIT
	POPJ	PP,
	LDB	TB,LI.NLT
	JUMPN	TB,ADJNUM
ADJNN.:	SKIPN	TA,CURDAT
	POPJ	PP,
	LDB	TB,DA.LOC
	HRRZM	TB,ITMLOC
	LDB	TB,DA.RES##
	HRRZM	TB,ITMRES
	LDB	TB,DA.EXS
	HRRZM	TB,NCHITM
	LDB	TC,DA.JST##
	LDB	TD,DA.USG
	CAIN	TD,%US.D6	;DISPLAY-6?
	JRST	AJNN.1		;YES
	CAIE	TD,%US.D7	;DISPLAY-7?
	POPJ	PP,		;NO
	HRRZI	TE," "
	HRRZM	TE,PADCHR
	SWOFF	CONVRT;
	HRRZI	TE,5
	HRRZM	TE,NCHWRD
	JRST	AJNN.2
AJNN.1:	SETZM	PADCHR
	SWON	CONVRT;
	HRRZI	TE,6
	HRRZM	TE,NCHWRD
AJNN.2:	SKIPN	TA,CURLIT
	POPJ	PP,
	LDB	TD,LI.FGC##
	JUMPN	TD,AJNN.6
	LDB	TD,LI.NCH##
	HRRZM	TD,NCHLIT
	CAIN	TD,(TB)
	JRST	AJNN.X
	CAIL	TD,(TB)
	JRST	AJNN.4		;LITERAL LARGER THAN DD ITEM
	SUBI	TB,(TD)
	JUMPE	TB,AJNN.X	;SAME LENGTH
	LDB	TD,LI.ALL##
	SETOM	NPADR
	JUMPN	TD,AJNN.X	;'ALL'
	JUMPE	TC,AJNN.3	;NOT JUSTIFIED RIGHT
	HRRZM	TB,NPADL##
	JRST	AJNN.X
AJNN.3:	HRRZM	TB,NPADR##
	JRST	AJNN.X
AJNN.4:	HRRZM	TB,NCHLIT
	JUMPE	TC,AJNN.5
	SUBI	TD,(TB)
	IBP	BYTEPT
	SOJG	TD,.-1
AJNN.5:	HRRZI	DW,356
	PUSHJ	PP,WARN
	JRST	AJNN.X
AJNN.6:	HRRZM	TB,NPADL
	LDB	TD,LI.FCC##
	SETZ	TC,
	SUBI	TD,HIVAL.
	CAILE	TD,5
	JRST	AJNN.7
	LSH	TD,1
	TSWT	CONVRT;
	HRRZI	TD,1(TD)
	HRRZ	TC,FIGC(TD)
AJNN.7:	HRRZM	TC,PADCHR##
AJNN.X:	SKIPN	TA,CURDAT
	POPJ	PP,
	LDB	TB,DA.CLA
	CAIE	TB,%CL.AB	;ALPHABETIC?
	JRST	P0.8		;NO
	SETZM	TBLOCK
	SKIPG	TD,NCHLIT
	JRST	P0.6		;MIGHT BE FIGURATIVE CONSTANT
	HRRZ	TA,CURLIT
	MOVE	TE,BYTEPT
P0.5:	ILDB	TB,TE
	CAIN	TB," "
	JRST	P0.55		;SPACE
	CAIL	TB,"A"
	CAILE	TB,"Z"
	JRST	P0.E		;NOT A LETTER
P0.55:	SOJG	TD,P0.5
	JRST	P0.7		;DONE
P0.6:	TSWT	CONVRT;
	JRST	P0.61		;DISPLAY-7
	SKIPN	PADCHR
	JRST	P0.8		;VALUE IS SPACES --- OKAY
	SETZM	PADCHR
	JRST	P0.71
P0.61:	HRRZ	TB,PADCHR
	CAIN	TB," "
	JRST	P0.8		;VALUE IS SPACES --- OKAY
	HRRZI	TB," "
	HRRZM	TB,PADCHR
	JRST	P0.71
P0.E:	HRRZI	TB," "
	DPB	TB,TE
	SETOM	TBLOCK
	JRST	P0.55
P0.7:	SKIPN	TBLOCK
	JRST	P0.8		;NO ILLEGAL CHARACTERS
P0.71:	HRRZI	DW,362		;ONLY ALPHABETIC CHARACTERS ALLOWED
	PUSHJ	PP,WARN
P0.8:	MOVE	TA,BYTEPT
	MOVEM	TA,BYTEP2
	MOVE	TA,NCHLIT
	MOVEM	TA,NCHLI2##
	POPJ	PP,


FIGC:	OCT	77
	OCT	177
	OCT	0
	OCT	0
	SIXBIT /""""""/
	OCT	42
	SIXBIT	/      /
	EXP	" "
	OCT	0
	OCT	0
	SIXBIT	/000000/
	EXP	"0"
GETCHR:	SOSL	NPADL
	JRST	GETPAD
GETIT0:	SOSL	NCHLIT
	JRST	GETIT1
	SKIPL	NPADR
	JRST	GETPAD
	MOVE	TA,BYTEP2
	MOVEM	TA,BYTEPT
	SKIPG	TA,NCHLI2
	JRST	GETPAD
	MOVEM	TA,NCHLIT
	JRST	GETIT0
GETIT1:	HRRZ	TA,CURLIT
	ILDB	TC,BYTEPT
	SKIPL	SIGNED
	JRST	GETCH1
	SKIPLE	NCHITM
	JRST	GETCH1
	CAIN	TC,"0"
	JRST	GETCH0
	ADDI	TC,"J"-"1"
	JRST	GETCH1
GETCH0:	HRRZI	TC,72
GETCH1:	TSWF	CONVRT;
	SUBI	TC,40
	POPJ	PP,
GETPAD:	HRRZ	TC,PADCHR
	SKIPL	SIGNED
	POPJ	PP,
	SKIPLE	NCHITM
	POPJ	PP,
	HRRZI	TC,72
	TSWF	CONVRT;
	HRRZI	TC,32
	POPJ	PP,
ADJNUM:	LDB	TB,LI.FGC
	JUMPN	TB,ADJNM5	;FIGURATIVE CONSTANT
	LDB	TB,LI.NCH
	HRRZM	TB,NCHLIT
	PUSHJ	PP,EXALIT
	SKIPN	TA,CURDAT
	POPJ	PP,
	LDB	TB,DA.USG
	CAIN	TB,%US.C1
	JRST	AJUC1		;COMP-1
	LDB	TB,DA.INS
	LDB	TC,DA.NDP
	LDB	TD,DA.DPR
	HRRZM	TB,NCHITM
	SKIPN	NINTGD
	SKIPE	NFRACD##
	JRST	ADJNM0
	HRRZM	TB,NPADL
	SETZM	NCHLIT
	SETZM	NPADR
	JRST	ADJNM3
ADJNM0:	JUMPN	TD,ADJNM2	;NDP<0
	CAIGE	TB,(TC)
	JRST	ADJNM1		;NDP>INSIZE --- P(>0)9(N)
	SUBI	TB,(TC)
	CAMGE	TB,NINTGD##	;INS-NDP>=NINTGD?
	JRST	AJUE.1		;NO---NUMBER TOO BIG
	SUB	TB,NINTGD
	MOVEM	TB,NPADL
	CAMGE	TC,NFRACD
	JRST	AJUE.2		;NDP<NFRACD --- ERROR
	SUB	TC,NFRACD
	MOVEM	TC,NPADR
	JRST	ADJNM3
ADJNM1:	SKIPLE	NINTGD
	JRST	AJUE.1		;NUMBER TOO BIG
	HRRZ	TA,CURLIT
	SUBI	TC,(TB)		;PIC IS P(NDP-INS)9(INS)
	ILDB	TE,BYTEPT
	CAIE	TE,"0"
	JRST	AJUE.3		;NUMBER TOO BIG
	SOJG	TC,.-3
	SETZM	NPADL
	HRRZ	TA,CURDAT
	LDB	TC,DA.NDP
	MOVE	TD,TB
	ADD	TB,NCHLIT
	SUB	TB,TC		;INS-NDP+NCHLIT
	MOVEM	TB,NCHLIT
	SUB	TD,TB
	MOVEM	TD,NPADR
	JRST	ADJNM3
ADJNM2:	SKIPE	NFRACD
	JRST	AJUE.3
	CAML	TC,NINTGD
	JRST	AJUE.3
	MOVE	TA,CURLIT
	MOVE	TE,NINTGD
	SUB	TE,TC		;PIC IS 9(INS)P(NDP)
	MOVE	TD,BYTEPT
	IBP	TD
	SOJG	TE,.-1
	ILDB	TE,TD
	CAIE	TE,"0"
	JRST	AJUE.3
	SOJG	TC,.-3
	HRRZ	TA,CURDAT
	LDB	TC,DA.NDP
	MOVE	TD,TB
	SUB	TD,NINTGD
	ADD	TD,TC
	JUMPL	TD,AJUE.3	;VALUE TOO LARGE
	MOVEM	TD,NPADL	;INS+NDP-NINTGD
	SETZM	NPADR
	SUBI	TB,(TD)
	MOVEM	TB,NCHLIT
ADJNM3:	HRRZ	TA,CURDAT
	LDB	TB,DA.SGN##
	JUMPN	TB,ADJNM4
	SKIPN	SIGNED##
	JRST	ADJNM4
	LDB	LN,DA.LN##
	LDB	CP,DA.CP
	HRRZI	DW,371		;VALUE SHOULD BE UNSIGNED
	PUSHJ	PP,WARN
	SETZM	SIGNED
ADJNM4:	HRRZ	TA,CURDAT
	LDB	TE,DA.USG
	JRST	.+1(TE)
	POPJ	PP,		;NOT DECLARED
	JRST	AJUDS6		;DISPLAY-6
	JRST	AJUDS7		;DISPLAY-7
	POPJ	PP,		;UNKNOWN
	JRST	AJU1WC		;1-WORD COMP
	JRST	AJU2WC		;2-WORD COMP
	JRST	AJUC1		;COMP-1
	JRST	AJU1WC		;INDEX

ADJNM5:	LDB	TB,LI.FCC
	CAIE	TB,ZERO.
	JRST	AJUE.4		;ONLY ZERO ALLOWED
	HRRZ	TA,CURDAT
	LDB	TB,DA.INS
	HRRZM	TB,NPADR
	HRRZM	TB,NCHITM
	SETZM	NINTGD
	SETZM	NFRACD
	JRST	ADJNM4
AJU2WC:
AJU1WC:	HRRZ	TA,CURLIT
	SETZB	TD,TC
AJU1W1:	SOSGE	NCHLIT
	JRST	AJU1WX
	ILDB	TE,BYTEPT
	CAIL	TE,"0"
	CAILE	TE,"9"
	JRST	AJU1W1
	IMULI	TD,12
	MULI	TC,12
	ADD	TD,TC
	MOVE	TC,TB
	ADDI	TC,-"0"(TE)
	TLZN	TC,1B18
	JRST	AJU1W1
	AOJA	TD,AJU1W1
AJU1WX:	SOSGE	NPADR
	JRST	AJ1WX1
	IMULI	TD,12
	MULI	TC,12
	ADD	TD,TC
	MOVE	TC,TB
	TLZN	TC,1B18
	JRST	AJU1WX
	AOJA	TD,AJU1WX
AJ1WX1:	SKIPL	SIGNED
	JRST	AJU1X2
	SETCA	TD,
	MOVNS	TC
	JUMPN	TC,AJU1X2
	TLO	TC,1B18
	AOJA	TD,AJU1X2
AJU1X2:	MOVEM	TD,VALUE1
	MOVEM	TC,VALUE2
	POPJ	PP,


AJUDS6:	SWON	CONVRT;
	HRRZI	TA,6
	HRRZM	TA,NCHWRD
	JRST	AJUDSP
AJUDS7:	SWOFF	CONVRT;
	HRRZI	TA,5
	HRRZM	TA,NCHWRD
AJUDSP:	HRRZI	TB,"0"-40	;DISPLAY-6 0
	TSWT	CONVRT;
	HRRZI	TB,"0"
	HRRZM	TB,PADCHR
	HRRZ	TA,CURDAT
	LDB	TB,DA.LOC
	HRRZM	TB,ITMLOC
	LDB	TB,DA.RES
	HRRZM	TB,ITMRES
	POPJ	PP,
AJUC1:	SKIPN	TA,NINTGD
	SKIPE	NFRACD
	SKIPA			;NON-ZERO DIGITS IN SOME PART
	POPJ	PP,
	CAIGE	TA,0
	SETZ	TA,
	MOVEM	TA,VALUE1	;EXPONENT
	SETZ	TA,
	SKIPGE	SIGNED
	HRLZI	TA,740000
	MOVEM	TA,VALUE2
	MOVE	TA,CURLIT
	HRRZI	TB,^D8		;NUMBER OF SIGNIFICANT DIGITS ALLOWED
	MOVE	TD,[POINT 4,VALUE2,3]
AJUC11:	ILDB	TE,BYTEPT
	CAIE	TE,"0"
	JRST	AJC121		;NON-ZERO DIGIT
	SOS	VALUE1		;ZERO HERE IS A ZERO FOLLOWING THE DECIMAL
				;POINT --- HENCE, DECREMENT EXPONENT
	SOSLE	NCHLIT
	JRST	AJUC11
	POPJ	PP,
AJUC12:	ILDB	TE,BYTEPT
AJC121:	IDPB	TE,TD
	SOS	TC,NCHLIT
	SOJLE	TB,AJUC13	;NO MORE SIGNIFICANT DIGITS ALLOWED
	JUMPG	TC,AJUC12	;MORE DIGITS
AJUC13:	SKIPG	NCHLIT
	POPJ	PP,
AJUC14:	ILDB	TE,BYTEPT
	CAIE	TE,"0"
	JRST	AJUC15		;NON-ZERO DIGIT AFTER 8 PLACES USED
	SOSLE	NCHLIT
	JRST	AJUC14
	POPJ	PP,
AJUC15:	HRRZ	TA,CURDAT
	LDB	LN,DA.LN
	LDB	CP,DA.CP
	HRRZI	DW,^D302	;TOO MANY DIGITS
	JRST	WARN
AJUE.1:	HRRZI	DW,365		;HIGH-PART TRUNCATION
AJUE.E:	SETZM	NCHITM
	SETZM	NPADL
	SETZM	NPADR
	SETZM	NCHLIT
	JRST	FATAL
AJUE.2:	HRRZI	DW,366		;LOW-PART TRUNCATION
	JRST	AJUE.E
AJUE.3:	HRRZI	DW,370		;VALUE OUT OF RANGE
	JRST	AJUE.E
AJUE.4:	HRRZI	DW,^D298
	JRST	AJUE.E




EXALIT:	SETZM	SIGNED
	SETZM	NLEADZ
	SETZM	NTRALZ##
	SETZM	NINTGD
	SETZM	NFRACD
	SKIPN	TA,CURLIT
	POPJ	PP,
	LDB	TB,LI.NCH
	MOVE	TE,[POINT	7,SZ.LIT(TA)]
	MOVE	TD,TE
	ILDB	TC,TE
	CAIN	TC,"+"
	JRST	EXL.1
	CAIE	TC,"-"
	JRST	EXL.2
	SETOM	SIGNED
	JRST	EXL.3
EXL.1:	HRRZI	TC,1
	HRRZM	TC,SIGNED
	JRST	EXL.3
EXL.2:	CAIE	TC,"0"
	JRST	EXL.4
	AOS	NLEADZ##
EXL.3:	SOS	NCHLIT
	SOJLE	TB,EXL.DN
	ILDB	TC,TE
	JRST	EXL.2
EXL.4:	CAIN	TC,"."
	JRST	EXL.45
	IDPB	TC,TD
	AOS	NINTGD
	SOJLE	TB,EXL.DN
	ILDB	TC,TE
	JRST	EXL.4
EXL.45:	SOS	NCHLIT
EXL.5:	SOJLE	TB,EXL.DN
	ILDB	TC,TE
	IDPB	TC,TD
	CAIE	TC,"0"
	JRST	EXL.6
	AOS	NTRALZ
	SOS	NCHLIT
	JRST	EXL.5
EXL.6:	AOS	TC,NTRALZ
	ADDM	TC,NFRACD
	ADDM	TC,NCHLIT
	SOS	NCHLIT
	SETZM	NTRALZ
	JRST	EXL.5
EXL.DN:	POPJ	PP,
SUBTTL	DATA DIVISION SYNTAX TREE

$ DD1.,0,0,0,0,<DD1A,DD9.,DD10.,DD1B>
$ DD1A,0,DATA.,0,1,<DD2.,DD3.>
$ DD1B,CE203.,0,0,0,<DD2.,DD3.>
$ DD2.,DA1.,DIVIS.,0,0,<DD4.,DD5.>
$ DD3.,CE137.,0,0,0,DD2.
$ DD4.,0,PRIOD.,0,0,DD6.
$ DD5.,CE125.,0,0,0,DD6.
$ DD6.,0,0,0,0,<DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD11.>
$ DD7.,DCA1.,FILE.,0,1,<DD14.,DD15.>
$ DD7A.,DCA7.,LINKG.,0,1,<DD24A,DD24B>
$ DD8.,DCA2.,WORKI.,0,1,<DD24A,DD24B>
$ DD9.,DA4.,PROC.,0,1
$ DD10.,DA4.,ENDIT.,0,0
$ DD11.,CE43.,0,0,0,DD12.
$ DD12.,DA5.,0,0,0,<DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD13.>
$ DD13.,DA6.,0,0,0,DD12.
$ DD14.,0,SECT.,0,0,<DD16.,DD17.>
$ DD15.,CE140.,0,0,0,<DD16.,DD17.>
$ DD16.,0,PRIOD.,0,0,<DD18.,DD18A.,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD19.>
$ DD17.,CE125.,0,0,0,<DD18.,DD18A.,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD19.>
$ DD18.,DA10.,FD.,0,1,DD22.
$ DD18A.,DA10.,SD.,0,1,DD18B.
$ DD18B.,DD174A,0,1,0,<DD24.,DD18.,DD18A.,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD21.>
$ DD19.,CE176.,0,0,0,DD20.
$ DD20.,DA5.,0,0,0,<DD18.,DD18A.,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD21.>
$ DD21.,DA5.,0,0,0,<DD18.,DD18A.,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD21.>
$ DD22.,DD36.,0,1,0,<DD24.,DD18.,DD18A.,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD19.>
IFN RPW,<
$ DD23.,DA8.,0,0,0,<DD24.,DD18.,DD18A.,DD205.,DD205E,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD19.>
>
IFE RPW,<
$ DD23.,DA8.,0,0,0,<DD24.,DD18.,DD18A.,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD19.>
>
$ DD24.,DA7.,INTGR.,0,0,DD25.
$ DD24A,0,SECT.,0,0,<DD24C,DD24D>
$ DD24B,CE140.,0,0,0,<DD24C,DD24D>
$ DD24C,0,PRIOD.,0,0,<DD24E,DD24K>
$ DD24D,CE125.,0,0,0,DD24C
$ DD24E,DA7.,INTGR.,0,0,DD24F
$ DD24F,DD86A.,0,1,0,DD24G
$ DD24G,DA8.,0,0,0,<DD24E,DD24K>
$ DD24H,CE25.,0,0,0,DD24I
$ DD24I,DA5.,0,0,0,<DD24E,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD24J>
$ DD24J,DA6.,0,0,0,DD24I
$ DD24K,0,0,0,0,<DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD24H>
$ DD25.,DD86A.,0,1,0,DD23.
$ DD36.,0,0,0,0,<DD37.,DD37A.,DD38.>
$ DD37.,DA9.,FILEN.,0,0,<DD40.,DD41.,DD42.>
$ DD37A.,DA9.,USERN.,0,0,<DD40.,DD41.,DD42.>
$ DD38.,CE20.,0,0,0,DD39.
$ DD39.,DCA3.,0,0,0
$ DD40.,DA0.,PRIOD.,0,0
$ DD41.,DA59.,COPY.,0,0,<DD40.,DD42.>
$ DD42.,0,0,0,0,<DD43.,DD43A.,DD44.,DD45.,DD46.,DD47.,DD48.,DD49.>
$ DD43.,0,BLOCK.,0,0,<DD51.,DD52.,DD53.>
$ DD44.,DA16.,DATA.,0,0,<DD67A.,DD67.>
$ DD45.,DA17.,LABEL.,0,0,<DD73.,DD74.,DD75.,DD76.,DD77.>
$ DD46.,0,REC.,0,0,<DD60.,DD61.,DD62.>
$ DD47.,0,VALUE.,0,0,<DD78.,DD79.,DD79A.,DD80.,DD81.>
$ DD48.,CE125.,INTGR.,0,0,DD50.
$ DD49.,CE18.,0,0,0,DD39.
$ DD50.,DCA4.,0,0,0
$ DD51.,0,CONTA.,0,0,<DD52.,DD53.>
$ DD52.,DA11.,INTGR.,0,0,<DD54.,DD57.,DD58.,DD59.>

$ DD53.,CE25.,0,0,0,DD39.
$ DD54.,0,TO.,0,0,<DD55.,DD56.>
$ DD55.,DA12.,INTGR.,0,0,<DD57.,DD58.,DD59.>
$ DD56.,CE25.,0,0,0,<DD57.,DD58.,DD59.>
$ DD57.,DA13.,REC.,0,0,<DD40.,DD42.>
$ DD58.,DA14.,CHARA.,0,0,<DD40.,DD42.>
$ DD59.,CE395.,0,0,0,<DD40.,DD42.>
$ DD60.,0,CONTA.,0,0,<DD61.,DD62.>
$ DD61.,DA11.,INTGR.,0,0,<DD63.,DD65.,DD66.>
$ DD62.,CE25.,0,0,0,DD39.
$ DD63.,0,TO.,0,0,<DD64.,DD62.>
$ DD64.,DA12.,INTGR.,0,0,<DD65.,DD66.>
$ DD65.,DA15.,CHARA.,0,0,<DD40.,DD42.>
$ DD66.,DA15.,0,0,0,<DD40.,DD42.>
$ DD67.,0,0,0,0,<DD69.,DD70.,DD70C.,DD71.>
$ DD67A.,0,REC.,0,0,DD67.
$ DD68.,CE18.,0,0,0,DD39.
$ DD69.,0,ARE.,0,0,<DD70.,DD70C.,DD71.>
$ DD70.,DA18.,USERN.,0,0,<DD72.,DD70.,DD40.,DD42.>
$ DD71.,CE17.,0,0,0,DD39.
$ DD70C.,DA18.,DATAN.,0,0,<DD72.,DD70.,DD40.,DD42.>
$ DD72.,0,.COMMA,0,0,<DD70.,DD40.,DD42.>
$ DD73.,0,REC.,0,0,<DD74.,DD75.,DD76.,DD77.>
$ DD74.,0,ARE.,0,0,<DD75.,DD76.,DD77.>
$ DD75.,DA19.,STAND.,0,0,<DD40.,DD42.>
$ DD76.,DA20.,OMITT.,0,0,<DD40.,DD42.>
$ DD77.,DA21.,0,0,0,<DD70.,DD70C.,DD71.>
$ DD78.,0,OF.,0,0,<DD79.,DD79A.,DD80.,DD81.>
$ DD79.,DA22.,ID.,0,0,<DD82I.,DD83I.,DD84.,DD84E.,DD85.>
$ DD79A.,DA61.,PPN.,0,0,<DD82P.,DD83P.,DD84.,DD84E.,DD85.>
$ DD80.,DA23.,DATEW.,0,0,<DD82D.,DD83D.,DD83N.,DD84.,DD84E.,DD85.>
$ DD81.,CE18.,0,0,0,DD39.
$ DD82D.,0,IS.,0,0,<DD83D.,DD84.,DD85.>
$ DD82I.,0,IS.,0,0,<DD83I.,DD84.,DD85.>
$ DD82P.,0,IS.,0,0,<DD83P.,DD84.,DD85.>
$ DD83I.,DA24I.,LITER.,0,0,<DD79.,DD79A.,DD80.,DD40.,DD42.>
$ DD83D.,DA24D.,LITER.,0,0,<DD79.,DD79A.,DD80.,DD40.,DD42.>
$ DD83N.,DA24D.,INTGR.,0,0,<DD79.,DD79A.,DD80.,DD40.,DD42.>
$ DD83P.,DA24P.,INTGR.,0,0,<DD83PP,DD83PE>
$ DD83PP,DA24P.,INTGR.,0,0,<DD79.,DD79A.,DD80.,DD40.,DD42.>
$ DD83PE,DA24PE,0,0,0,<DD79.,DD79A.,DD80.,DD40.,DD42.>
$ DD84.,DA25.,USERN.,0,0,<DD84A.,DD79A.,DD79.,DD80.,DD40.,DD42.>
$ DD84A.,0,OF.,0,0,<DD84C.,DD84D.,DD85.>
$ DD84C.,DA60.,USERN.,0,0,<DD84A.,DD79.,DD79A.,DD80.,DD40.,DD42.>
$ DD84D.,0,DATAN.,0,0,DD84C.
$ DD84E.,0,DATAN.,0,0,DD84.
$ DD85.,CE18.,0,0,0,DD39.
$ DD86A.,0,0,0,0,<DD86.,DD87.>
$ DD86.,DA26.,INTGR.,0,0,<DD89.,DD89A.,DD89F.,DD90.>
$ DD87.,CE25.,0,0,0,DD88.
$ DD88.,DCA3.,0,0,0
$ DD89P.,0,0,0,0,DD89.
$ DD89.,DA27.,DATAN.,0,0,DD92.
$ DD89A.,DA27.,USERN.,0,0,DD92.
$ DD89F.,DA27.,FILLE.,0,0,DD92.
$ DD90.,DA99.,0,0,0,DD91.
$ DD91P.,0,0,0,0,DD91.
$ DD91.,DCA3.,0,0,0
$ DD92.,DD99A.,0,1,0,<DD93.,DD94.>
$ DD93.,DCA5.,INTGR.,0,0,<DD95.,DD95A.,DD96.,DD97.>
$ DD93A.,0,0,0,0,DD93B.
$ DD93B.,DD153A,0,1,0,<DD93.,DD94.>
$ DD94.,DCA4.,0,0,0
$ DD95P.,0,0,0,0,DD95.
$ DD95.,DA29.,USERN.,0,0,DD92.
$ DD95A.,DA29.,DATAN.,0,0,DD92.
$ DD96.,DA29.,FILLE.,0,0,DD92.
$ DD97.,DA99A.,0,0,0,DD98.
$ DD98P.,0,0,0,0,DD98.
$ DD98.,DA5.,0,0,0,<DD93.,DD94.>
$ DD99A.,0,0,0,0,<DD101.,DD99B.>
$ DD99B.,DA30N.,0,0,0,<DD99.,DD100.,DD107.>
$ DD99.,DA107.,PRIOD.,0,0

$ DD100.,DA59.,COPY.,0,0,DD99A.
$ DD101.,DA100.,REDEF.,0,0,<DD102.,DD103.>
$ DD102.,DA30.,DATAN.,0,0,<DD105.,DD106.,DD107.>
$ DD103.,CE111.,0,0,0,DD104.
$ DD104.,DCA3.,0,0,0
$ DD105.,0,SEMIC.,0,0,<DD106.,DD107.>
$ DD106.,DA107.,PRIOD.,0,0
$ DD107.,0,0,0,0,<DD108.,DD109.,DD110.,DD111.,DD112.,DD113.,DD114.,
DD136.,DD137.,DD138.,DD139.,DD140.,DD142.,DD247.,DD257.,DD265.,DD267.,DD277.,DD282.,DD304.,DD314.,DD115.>
$ DD108.,0,BLANK.,0,0,<DD117.,DD118.,DD119.>
$ DD109.,0,JUST.,0,0,<DD120.,DD120A,DD120.>
$ DD110.,DA73.,OCCUR.,0,0,<DD122.,DD123.>
$ DD111.,0,PIC.,0,0,DD121.
$ DD112.,DA73.,SYNCH.,0,0,<DD131.,DD132.,DD133.>
$ DD113.,0,USAGE.,0,0,<DD134.,DD135.>
$ DD114.,0,VALUE.,0,0,<DD145.,DD146.,DD146A,DD146B,DD146C,DD147.>
$ DD115.,DA105.,0,0,0,DD116.
$ DD116.,DCA3.,0,0,0
$ DD117.,0,WHEN.,0,0,<DD118.,DD119.>
$ DD118.,DA31.,FIGCN.,0,0,<DD105.,DD106.,DD107.>
$ DD119.,CE18.,0,0,0,<DD105.,DD106.,DD107.>
$ DD120.,DA32.,RIGHT.,0,0,<DD105.,DD106.,DD107.>
$ DD120A,0,LEFT.,0,0,<DD106.,DD107.>
$ DD121.,DA35.,0,0,0,<DD105.,DD106.,DD107.>
$ DD122.,DA33.,INTGR.,0,0,<DD125.,DD122A,DD122L>
$ DD122A,0,TO.,0,0,<DD122B,DD123.>
$ DD122B,DA56.,INTGR.,0,0,<DD122C,DD122D,DD122E>
$ DD122C,0,TIMES.,0,0,<DD122D,DD122E>
$ DD122D,0,DEPEN.,0,0,<DD122F,DD122G>
$ DD122E,CE126.,0,0,0,DD122L
$ DD122F,0,ON.,0,0,DD122G
$ DD122G,0,0,0,0,<DD122H,DD122M,DD122K>
$ DD122H,DA57.,DATAN.,0,0,<DD122N,DD122L>
$ DD122K,CE111.,0,0,0,DD122L
$ DD122L,0,0,0,0,<DD126.,DD335.,DD336.,DD105.,DD106.,DD107.>
$ DD122M,0,USERN.,0,0,DD122H
$ DD122N,0,OF.,0,0,<DD122P,DD122Q,DD122K>
$ DD122P,DA60.,DATAN.,0,0,<DD122N,DD122L>
$ DD122Q,0,USERN.,0,0,DD122P
$ DD123.,CE25.,0,0,0,DD124.
$ DD124.,DCA3.,0,0,0
$ DD125.,0,TIMES.,0,0,DD122L
$ DD126.,0,INDXD.,0,0,<DD127.,DD128.,DD128A,DD129.>
$ DD127.,0,BY.,0,0,<DD128.,DD128A,DD129.>
$ DD128.,DA34.,USERN.,0,0,<DD130.,DD128.,DD128A,DD335.,DD336.,DD105.,
DD106.,DD107.>
$ DD128A,DA34.,DATAN.,0,0,<DD130.,DD128.,DD128A,DD335.,DD336.,DD105.,
DD106.DD107.>
$ DD129.,CE58.,0,0,0,<DD335.,DD336.,DD105.,DD106.,DD107.>
$ DD130.,0,.COMMA,0,0,<DD128.,DD128A,DD105.,DD106.,DD107.>
$ DD131.,DA36.,RIGHT.,0,0,<DD105.,DD106.,DD107.>
$ DD132.,DA37.,LEFT.,0,0,<DD105.,DD106.,DD107.>
$ DD133.,DA37.,0,0,0,<DD105.,DD106.,DD107.>
$ DD134.,0,IS.,0,0,DD135.
$ DD135.,0,0,0,0,<DD136.,DD137.,DD138.,DD139.,DD140.,DD142.,DD143.>
$ DD136.,DA39.,DISPL.,0,0,<DD105.,DD106.,DD107.>
$ DD137.,DA39.,DSPL6.,0,0,<DD105.,DD106.,DD107.>
$ DD138.,DA38.,DSPL7.,0,0,<DD105.,DD106.,DD107.>
$ DD139.,DA40.,COMP.,0,0,<DD105.,DD106.,DD107.>
$ DD140.,DA41.,COMP1.,0,0,<DD105.,DD106.,DD107.>
$ DD142.,DA43.,INDEX.,0,0,<DD105.,DD106.,DD107.>
$ DD143.,CE18.,0,0,0,DD144.
$ DD144.,DCA3.,0,0,0
$ DD145.,0,IS.,0,0,<DD146.,DD146A,DD146B,DD146C,DD147.>
$ DD146.,DA55.,LITER.,0,0,<DD105.,DD106.,DD107.>
$ DD146A,DA55.,NLIT.,0,0,<DD105.,DD106.,DD107.>
$ DD146B,DA55.,INTGR.,0,0,<DD105.,DD106.,DD107.>
$ DD146C,DA55.,FIGCN.,0,0,<DD105.,DD106.,DD107.>

$ DD147.,CE258.,0,0,0,DD148.
$ DD148.,DCA3.,0,0,0
$ DD152.,DCA3.,0,0,0
$ DD153.,DA46.,USERN.,0,0,<DD154.,DD155.,DD156.,DD159.>
$ DD153A,0,0,0,0,<DD153.,DD153B,DD153E,DD153F>
$ DD153B,DA46.,DATAN.,0,0,DD153G
$ DD153E,DA46.,CONDI.,0,0,DD153G
$ DD153F,CE17.,0,0,0,DD153H
$ DD153G,0,0,0,0,<DD154.,DD155.,DD156.,DD159.>
$ DD153H,DCA3.,0,0,0
$ DD154.,DA0.,PRIOD.,0,0
$ DD155.,0,SEMIC.,0,0,<DD156.,DD155A>
$ DD155A,CE296.,0,0,0,DD152.
$ DD156.,0,VALUE.,0,0,<DD157.,DD157A>
$ DD157.,0,IS.,0,0,DD157A
$ DD157A,0,0,0,0,<DD158.,DD158A,DD158B,DD158C,DD159.>
$ DD158.,DA47.,LITER.,0,0,<DD160.,DD161.>
$ DD158A,DA47.,NLIT.,0,0,<DD160.,DD161.>
$ DD158B,DA47.,INTGR.,0,0,<DD160.,DD161.>
$ DD158C,DA47.,FIGCN.,0,0,<DD160.,DD161.>
$ DD159.,DA47E.,0,0,0,<DD160.,DD161.>
$ DD160.,0,THRU.,0,0,<DD162.,DD162A,DD162B,DD162C,DD159.>
$ DD161.,DA48.,0,0,0,<DD164.,DD154.,DD164A,DD156.,DD157A>
$ DD162.,DA49.,LITER.,0,0,<DD164.,DD154.,DD164A,DD156.,DD157A>
$ DD162A,DA49.,NLIT.,0,0,<DD164.,DD154.,DD164A,DD156.,DD157A>
$ DD162B,DA49.,INTGR.,0,0,<DD164.,DD154.,DD164A,DD156.,DD157A>
$ DD162C,DA49.,FIGCN.,0,0,<DD164.,DD154.,DD164A,DD156.,DD157A>
$ DD164.,0,.COMMA,0,0,<DD154.,DD164A,DD157A>
$ DD164A,0,SEMIC.,0,0,DD157A
$ DD165.,0,0,0,0,<DD165A,DD165B,DD165E>
$ DD165A,DA29.,USERN.,0,0,<DD166.,DD167.>
$ DD165B,DA29.,DATAN.,0,0,<DD166.,DD167.>
$ DD165E,CE17.,0,0,0,DD169.
$ DD166.,0,RENAM.,0,0,<DD168.,DD167A>
$ DD167.,CE281.,0,0,0,DD169.
$ DD167A,CE111.,0,0,0,DD169.
$ DD168.,DA51.,DATAN.,0,0,<DD168A,DD172.>
$ DD168A,0,THRU.,0,0,<DD170.,DD167A>
$ DD169.,DCA3.,0,0,0
$ DD170.,DA52.,DATAN.,0,0,DD172.
$ DD172.,DA53.,0,0,0,<DD173.,DD173A>
$ DD173.,DA0.,PRIOD.,0,0
$ DD173A,CE125.,0,0,0,DD173B
$ DD173B,DA5.,0,0,0,DD173.
$ DD174A,0,0,0,0,<DD174.,DD175.,DD176.>
$ DD174.,DCA10.,FILEN.,0,0,<DD177A,DD178.>
$ DD175.,DCA10.,USERN.,0,0,<DD177A,DD178.>
$ DD176.,CE20.,0,0,0,DD177.
$ DD177.,DCA3.,0,0,0
$ DD177A,DA59.,COPY.,0,0,DD178.
$ DD178.,0,0,0,0,<DD179.,DD180.,DD181.,DD182.>
$ DD179.,DA16.,DATA.,0,0,<DD183A,DD183.>
$ DD180.,0,REC.,0,0,<DD188.,DD189.,DD190.>
$ DD181.,DA0.,PRIOD.,0,0
$ DD182.,CE18.,0,0,0,DD177.
$ DD183.,0,0,0,0,<DD185.,DD186.,DD186A,DD187.>
$ DD183A,0,REC.,0,0,DD183.
$ DD184.,CE18.,0,0,0,DD177.
$ DD185.,0,ARE.,0,0,<DD186.,DD186A,DD187.>
$ DD186.,DA18.,USERN.,0,0,DD186D
$ DD186A,DA18.,DATAN.,0,0,DD186D
$ DD186D,0,0,0,0,<DD186.,DD186A,DD178.>
$ DD187.,CE17.,0,0,0,DD177.
$ DD188.,0,CONTA.,0,0,<DD189.,DD190.>
$ DD189.,DA11.,INTGR.,0,0,<DD191.,DD194.,DD195.>
$ DD190.,CE25.,0,0,0,DD177.
$ DD191.,0,TO.,0,0,<DD192.,DD193.>
$ DD192.,DA12.,INTGR.,0,0,<DD194.,DD195.>
$ DD193.,CE25.,0,0,0,DD177.
$ DD194.,0,CHARA.,0,0,DD195.
$ DD195.,DA15.,0,0,0,DD178.
;REPORT(S) IS/ARE <REPORT-NAME-1, REPORT-NAME-2, ...>
IFN RPW,<
$ DD43A.,0,REPOR.,0,0,<DD196.,DD197.,DD199.>	;REPORT
$ DD196.,0,IS.,0,0,<DD197.,DD199.>		;IS
$ DD197.,DA62.,USERN.,0,0,<DD197.,DD40.,DD42.>	;REP-NAME: LINK FILE TO REP
$ DD199.,CE338.,0,0,0,<DD40.,DD42.>		;?ILLEGAL REP-NAME
>
IFE RPW,<
$ DD43A.,CE91.,REPOR.,0,0,DD196.		;?NOT IMPLEMENTED
$ DD196.,DA98.,0,0,0,<DD43.,DD43A.,DD44.,DD45.,DD46.,DD47.,DD48.,DD42.,DD196.>  ;SKP
>
;REPORT SECTION.
IFN RPW,<
$ DD8A.,DCA6.,REPOR.,0,1,<DD200.,DD201.>	;REPORT: INIT SECTION
$ DD8AE.,CE341.,REPOR.,0,0,DD8A.		;REPORT: ?NOT AT A-MARG
$ DD200.,0,SECT.,0,0,<DD202.,DD203.>		;SECTION
$ DD201.,CE140.,0,0,0,<DD202.,DD203.>		;?SECTION EXPECTED
$ DD202.,0,PRIOD.,0,0,DD204.			;PERIOD
$ DD203.,CE125.,0,0,0,DD204.			;?PERIOD NOT SEEN
$ DD204.,0,0,0,0,<DD202.,DD205.,DD205E.,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD204A>
$ DD204A,DA98.,0,0,0,DD204.
>
$ DD204E,0,0,0,0,<DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD204F>
$ DD204F,DA98.,0,0,0,DD204E			;SKIP CLAUSE
IFE RPW,<
$ DD8A.,CE91.,REPOR.,0,1,DD204E			;?NOT IMPLEMENTED
$ DD8AE.,CE91.,REPOR.,0,0,DD204E		;?NOT IMPLEMENTED
>

IFN RPW,<

;RD <REPORT-NAME>
$ DD205.,DA10.,RD.,0,1,DD205G			;RD: CLEAN UP PREV. GROUP
$ DD205E,CE341.,RD.,0,0,DD205.			;RD: ?NOT AT A-MARG
$ DD205G,DD205S,0,1,0,<DD24.,DD205.,DD205E,DD7.,DD7A.,DD8.,DD8A.,DD8AE.,DD9.,DD10.,DD21.>
$ DD205S,0,0,0,0,<DD206.,DD206E,DD207.>
$ DD206.,DA64.,RPNAM.,0,0,<DD40.,DD41.,DD209.>	;REP-NAM: INIT RD
$ DD206E,DA64E.,USERN.,0,0,DD206.
$ DD207.,CE342.,0,0,0,DD39.			;?NOT DEFINED IN FILE SECT.

;OPTIONS IN RD PARAGRAPH
$ DD209.,0,0,0,0,<DD327.,DD210.,DD211.,DD48.,DD49.>

;CODE IS <MNEMONIC-NAME>
$ DD327.,0,CODE.,0,0,<DD328.,DD328.>
$ DD328.,0,IS.,0,0,<DD329.,DD330.,DD331.>
$ DD329.,DA106.,SPECN.,0,0,<DD40.,DD209.>
$ DD330.,CE104.,USERN.,0,0,<DD40.,DD209.>
$ DD331.,CE18.,0,0,0,DD240.

;CONTROL IS <FINAL, DATA-NAME>
$ DD210.,0,CONTR.,0,0,<DD230.,DD231.,DD232.,DD233.,DD239.>  ;CONTROL
$ DD230.,0,IS.,0,0,<DD231.,DD232.,DD233.,DD239.>;IS
$ DD231.,DA71.,FINAL.,0,0,<DD232.,DD233.,DD234.,DD40.,DD209.>  ;FINAL: MAKE CTRL RPW ENTRY
$ DD232.,0,USERN.,0,0,DD233.			;U-NAME: WILL CAUSE UNDEF FLAG
$ DD233.,DA72.,DATAN.,0,0,<DD234.,DD232.,DD233.,DD40.,DD209.>  ;D-NAME: MAKE CTRL ENTRY
$ DD234.,CE346.,FINAL.,0,0,<DD232.,DD233.,DD234.,DD40.,DD209.>  ;FINAL: ?SHOULD BE 1ST
$ DD239.,CE345.,0,0,0,DD240.			;?CONTROL IDENT EXPECTED
$ DD240.,DA7.,0,0,0,<DD40.,DD209.>		;SET TO REGET LAST WORD

>
IFN RPW,<

;PAGE LIMIT IS <INTEGER> LINES
$ DD211.,0,PAGE.,0,0,<DD212.,DD213.,DD214.,DD215.>  ;PAGE: INIT LINE-CTR
$ DD212.,0,LIM.,0,0,<DD213.,DD214.,DD215.>	;LIMIT
$ DD213.,0,IS.,0,0,<DD214.,DD215.>		;IS
$ DD214.,DA66.,INTGR.,0,0,<DD216.,DD217.>	;INTEGER: GET #LINES/PAGE
$ DD216.,0,LINE.,0,0,DD217.			;LINES
$ DD215.,CE18.,0,0,0,<DD40.,DD209.>		;?IMPROPER CLAUSE

;OPTIONS IN PAGE LIMIT CLAUSE
$ DD217.,0,0,0,0,<DD218.,DD219.,DD220.,DD221.,DD325.,DD326.>
$ DD325.,DA104.,PRIOD.,0,0,DD40.		;PERIOD: SET DEFAULTS
$ DD326.,DA104.,0,0,0,DD209.			;SET DEFAULTS

;HEADING <INTEGER>
$ DD218.,0,HEADG.,0,0,<DD222.,DD223.>		;HEADING
$ DD222.,DA67.,INTGR.,0,0,DD217.		;INTEGER: STORE HEADING LINE#
$ DD223.,CE18.,0,0,0,DD217.			;?IMPROPER CLAUSE

;FIRST DETAIL <INTEGER>
$ DD219.,0,FIRST.,0,0,<DD224.,DD223.>		;FIRST
$ DD224.,0,DE.,0,0,<DD225.,DD223.>		;DETAIL
$ DD225.,DA68.,INTGR.,0,0,DD217.		;INTEGER: STORE 1ST DE LINE#

;LAST DETAIL <INTEGER>
$ DD220.,0,LAST.,0,0,<DD226.,DD223.>		;LAST
$ DD226.,0,DE.,0,0,<DD227.,DD223.>		;DETAIL
$ DD227.,DA69.,INTGR.,0,0,DD217.		;INTEGER: STORE LAST DE LINE#

;FOOTING <INTEGER>
$ DD221.,0,FOOT.,0,0,<DD228.,DD223.>		;FOOTING
$ DD228.,DA70.,INTGR.,0,0,DD217.		;INTEGER: STORE FOOTING LINE#

>
;LINE NUMBER IS <INTEGER, PLUS INTEGER, NEXT PAGE>
IFN RPW,<
$ DD247.,DA74.,LINE.,0,0,<DD248.,DD249.,DD250.,DD251.,DD254.,DD143.>  ;LINE:
$ DD248.,0,NUMBR.,0,0,<DD249.,DD250.,DD251.,DD254.,DD143.>  ;NUMBER
$ DD249.,0,IS.,0,0,<DD250.,DD251.,DD254.,DD143.>;IS
$ DD250.,DA76.,INTGR.,0,0,DD99A.		;INTEGER:
$ DD251.,0,PLS.,0,0,<DD252.,DD253.>		;PLUS
$ DD252.,DA77.,INTGR.,0,0,DD99A.		;INTEGER:
$ DD253.,CE25.,0,0,0,DD144.			;?POS INTEGER EXPECTED
$ DD254.,0,NEXT.,0,0,<DD255.,DD256.>		;NEXT
$ DD255.,DA75.,PAGE.,0,0,DD99A.			;PAGE:
$ DD256.,CE351.,0,0,0,DD144.			;?PAGE EXPECTED
>
IFE RPW,<
$ DD247.,CE350.,LINE.,0,0,DD115.
>

;NEXT GROUP IS <INTEGER, PLUS INTEGER, NEXT PAGE>
IFN RPW,<
$ DD257.,DA74.,NEXT.,0,0,<DD258.,DD259.,DD260.,DD261.,DD263.,DD143.>  ;NEXT:
$ DD258.,0,GROUP.,0,0,<DD259.,DD260.,DD261.,DD263.,DD143.>  ;GROUP
$ DD259.,0,IS.,0,0,<DD260.,DD261.,DD263.,DD143.>;IS
$ DD260.,DA81.,INTGR.,0,0,DD99A.		;INTEGER:
$ DD261.,0, PLS.,0,0,<DD262.,DD253.>		;PLUS
$ DD262.,DA82.,INTGR.,0,0,DD99A.		;INTEGER:
$ DD263.,0,NEXT.,0,0,<DD264.,DD256.>		;NEXT
$ DD264.,DA80.,PAGE.,0,0,DD99A.			;PAGE:
>
IFE RPW,<
$ DD257.,CE350.,NEXT.,0,0,DD115.
>
;GROUP INDICATE
IFN RPW,<
$ DD265.,DA83.,GROUP.,0,0,<DD266.,DD266.>	;GROUP: SET GROUP INDIC. FLAG
$ DD266.,0,INDIC.,0,0,DD99A.			;INDICATE: POPJ
>
IFE RPW,<
$ DD265.,CE350.,GROUP.,0,0,DD115.
>

;RESET ON <CONTROL-IDENTIFIER, FINAL>
IFN RPW,<
$ DD267.,DA74.,RESET.,0,0,<DD268.,DD269.,DD270.,DD271.,DD272.>  ;RESET
$ DD268.,0,ON.,0,0,<DD269.,DD270.,DD271.,DD272.>;ON
$ DD269.,DA84.,FINAL.,0,0,DD99A.		;FINAL:
$ DD270.,0,USERN.,0,0,DD271.			;U-NAME: WILL CAUSE UNDEF FLAG
$ DD271.,DA85.,DATAN.,0,0,DD99A.		;D-NAME: PUT LINK IN RPWTAB
$ DD272.,CE345.,0,0,0,DD144.			;?CONTROL IDENTIFIER EXPECTED
>
IFE RPW,<
$ DD267.,CE350.,RESET.,0,0,DD115.
>

;COLUMN NUMBER IS <INTEGER>
IFN RPW,<
$ DD277.,DA74.,COL.,0,0,<DD278.,DD279.,DD280.,DD281.>  ;COLUMN
$ DD278.,0,NUMBR.,0,0,<DD279.,DD280.,DD281.>	;NUMBER
$ DD279.,0,IS.,0,0,<DD280.,DD281.>		;IS
$ DD280.,DA86.,INTGR.,0,0,DD99A.		;INTEGER:
$ DD281.,CE25.,0,0,0,DD144.			;?POS. INTEGER REQUIRED
>
IFE RPW,<
$ DD277.,CE350.,COL.,0,0,DD115.
>
;TYPE IS <RH,PH,CH,DE,CF,PF,RF>
IFN RPW,<
$ DD282.,DA74.,TYPE.,0,0,<DD283.,DD283A>	;TYPE
$ DD283.,0,IS.,0,0,DD283A			;IS
$ DD283A,0,0,0,0,<DD284.,DD285.,DD286.,DD287.,DD288.,DD289.,DD290.,DD291.,DD292.,DD293.,DD295.>
$ DD284.,0,REPOR.,0,0,<DD284A,DD284B,DD294.>	;REPORT
$ DD284A,DA87.,HEADG.,0,0,DD99A.		;HEADING:
$ DD284B,DA93.,FOOT.,0,0,DD99A.			;FOOTING:
$ DD285.,0,PAGE.,0,0,<DD285A,DD285B,DD294.>	;PAGE
$ DD285A,DA88.,HEADG.,0,0,DD99A.		;HEADING:
$ DD285B,DA92.,FOOT.,0,0,DD99A.			;FOOTING:
$ DD286.,0,CONTR.,0,0,<DD286A,DD286B,DD294.>	;CONTROL
$ DD286A,DA89.,HEADG.,0,0,<DD296.,DD297.,DD298.,DD303.>  ;HEADING:
$ DD286B,DA91.,FOOT.,0,0,<DD296.,DD297.,DD298.,DD303.>  ;FOOTING:
$ DD287.,DA87.,RH.,0,0,DD99A.			;RH:
$ DD288.,DA88.,PH.,0,0,DD99A.			;PH:
$ DD289.,DA89.,CH.,0,0,<DD296.,DD297.,DD298.,DD303.>  ;CH:
$ DD290.,DA90.,DE.,0,0,DD99A.			;DE:
$ DD291.,DA91.,CF.,0,0,<DD296.,DD297.,DD298.,DD303.>  ;CF:
$ DD292.,DA92.,PF.,0,0,DD99A.			;PF:
$ DD293.,DA93.,RF.,0,0,DD99A.			;RF:
$ DD294.,CE355.,0,0,0,DD144.			;?HEAD OR FOOT REQ'D
$ DD295.,CE356.,0,0,0,DD144.			;?TYPE REQUIRED
$ DD296.,0,USERN.,0,0,DD297.			;U-NAME: WILL CAUSE UNDEF FLAG
$ DD297.,DA94.,DATAN.,0,0,DD99A.		;D-NAME: LINK ITEM TO CTRL ENTRY
$ DD298.,DA95.,FINAL.,0,0,DD99A.		;FINAL:
$ DD303.,CE231.,0,0,0,DD302.			;?CONTROL ID OR FINAL EXPECTED
$ DD302.,DA7.,0,0,0,DD99A.			;SET TO REGET WORD
>
IFE RPW,<
$ DD282.,CE350.,TYPE.,0,0,DD115.
>
;SOURCE IS <IDENTIFIER>

IFN RPW,<
$ DD304.,DA74.,SOUR.,0,0,<DD305.,DD306.,DD307.,DD308.>  ;SOURCE: CK FOR REP SEC
$ DD305.,0,IS.,0,0,<DD306.,DD307.,DD308.>	;IS
$ DD306.,0,USERN.,0,0,DD307.			;U-NAME: WILL CAUSE UNDEF FLAG
$ DD307.,DA97.,DATAN.,0,0,DD99A.		;D-NAME: STORE SOURCE LINK
$ DD308.,CE101.,0,0,0,DD99A.			;?IDENTIFIER EXPECTED
>
IFE RPW,<
$ DD304.,CE350.,SOURC.,0,0,DD115.
>

;SUM <IDENTIFIER>,<IDENTIFIER>,...UPON <DATA-NAME>
IFN RPW,<
$ DD314.,DA103.,SUM.,0,0,<DD315.,DD316.,DD317.>
$ DD315.,DA101.,USERN.,0,0,<DD315.,DD316.,DD318.,DD322.,DD99A.>
$ DD316.,DA101.,DATAN.,0,0,<DD315.,DD316.,DD318.,DD322.,DD99A.>
$ DD317.,CE101.,0,0,0,DD99A.
$ DD318.,0,OF.,0,0,<DD319.,DD320.,DD321.>
$ DD319.,DA60.,USERN.,0,0,<DD318.,DD315.,DD316.,DD322.,DD99A.>
$ DD320.,DA60.,DATAN.,0,0,<DD318.,DD315.,DD316.,DD322.,DD99A.>
$ DD321.,CE347.,0,0,0,DD144.
$ DD322.,0,UPON.,0,0,<DD323.,DD324.,DD317.>
$ DD323.,DA102.,DATAN.,0,0,DD99A.
$ DD324.,DA102.,USERN.,0,0,DD99A.
>
IFE RPW,<
$ DD314.,CE350.,SUM.,0,0,DD115.
>
;ASCENDING/DESCENDING KEY CLAUSE FOR OCCURS
IFN SERCH,<
$ DD335.,DA108.,ASCND.,0,0,<DD337.,DD337.>
$ DD336.,DA109.,DESCN.,0,0,<DD337.,DD337.>
$ DD337.,0,KEY,0,0,<DD338.,DD338.>
$ DD338.,0,IS.,0,0,<DD339.,DD340.,DD341.>
$ DD339.,0,DATAN.,0,0,DD340.
$ DD340.,DA110.,USERN.,0,0,<DD342.,DD339.,DD340.,DD335.,DD336.,DD126.,DD105.,DD106.,DD107.>
$ DD341.,CE18.,0,0,0,<DD126.,DD105.,DD106.,DD107.>
$ DD342.,0,OF.,0,0,<DD343.,DD344.,DD123.>
$ DD343.,0,DATAN.,0,0,DD344.
$ DD344.,DA60.,USERN.,0,0,<DD342.,DD339.,DD340.,DD335.,DD336.,DD105.,DD126.,DD106.,DD107.>
>
IFE SERCH,<
$ DD335.,0,ASCND.,0,0,DD336.
$ DD336.,CE91.,DESCN.,0,0,<DD126.,DD105.,DD106.,DD107.>
>
SUBTTL	ERROR ROUTINES FOR DD SYNTAX SCAN

	RADIX	10

	WERRS	C,0,110

	INTER.	CE111.
CE111.:	TLNE	W1,GWRESV
	EWARNJ	CE103.
	EWARNJ	CE104.

	WERRS	C,112,124

	INTER.	CE125.
CE125.:	MOVE	CP,BLNKCP##
	MOVE	LN,BLNKLN##
	HRRZI	DW,125
	JRST	WARN

	WERRS	C,126,400

	END	COBOLC
   d@D