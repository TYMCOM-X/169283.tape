TITLE COMPILE -- CCL CONTROL CUSP
;PROGRAM TO COMPILE, LOAD, EXECUTE AND DEBUG USER PROGRAMS
SUBTTL WEIHER/CLEMENTS/PRIVITERA /DRP - 15 DEC 69 - V2011

VCOMPIL=2011

;THE ORIGINAL VERSION OF
;THIS PROGRAM WAS WRITTEN AT THE STANFORD UNIVERSITY
;ARTIFICIAL INTELLIGENCE LABORATORY BY WILLIAM F. WEIHER.
;MR. WEIHER'S COOPERATION, AND THAT OF THE A-I LABORATORY,
;ARE GRATEFULLY ACKNOWLEDGED.
;
;CONVERTED TO MACRO SOURCE LANGUAGE FROM FAIL ON
;1 NOVEMBER 68 BY R CLEMENTS
;
;ADDED NEW CCL COMMANDS TO AID PDP-10 TIME SHARING
;USERS.
;SEPT 69 BY D.R. PRIVITERA
;
;ADDED BBN FEATURES DEC 69  /DRP  GRAPHICS

INTERNAL VCOMPILE	;FOR LOADER MAP AND LIBRARY

LOC 137
EXP VCOMPILE
RELOC

IFNDEF RUNSW,<RUNSW=1>		;NON-ZERO TO USE THE RUN UUO
IFNDEF PURESW,<PURESW=1>	;NON-ZERO FOR A SHARED VERSION OF COMPIL
IFNDEF SYSSW,<SYSSW=0>		;NON ZERO USES DSK [1,1] FOR RUNS
	IFNDEF BBNSW,<BBNSW=1>	;NON-ZERO TO INCLUDE BBN FEATURES:
					;SQUEZED PPN'S,LONG CREF FILES

IFNDEF STANSW,<STANSW=0>	;NON-ZERO TO INCLUDE STANFORD FEATURES
IFNDEF LSTRSW,<LSTRSW=0>	;NON-ZERO TO USE "LISTER" INSTEAD OF PIP
				;FOR TYPE AND LIST COMMANDS
IFNDEF SAVEXT,<SAVEXT=<SIXBIT /   SAV/>>	;USE DMP FOR PDP6'S
IFE SYSSW,<SYSDEV=<SIXBIT /SYS/>>
IFN SYSSW,<SYSDEV=<SIXBIT /DSK/>>	;USE DSK [1,1]

MLON
;ACS
P=17		;PUSHDOWN POINTER
C=16		;CHARACTERS RETURNED HERE
CS=15		;CHARACTER STATUS BITS HERE
SVPT=14		;POINTER TO CURRENT FILE IN LIST OF FILES (AOBJN)
SWPT=13		;BYTE POINTER INTO SWITCH STORAGE AREA
SWCNT=12	;NUMBER OF BYTES LEFT FOR SWITCH STORAGE
FL3=11		;FLAG REGISTER (LEFT HALF IS GLOBAL SWITCHES)
FL2=10		;FLAG REGISTER (LEFT HALF INDICATES PROCESSOR)
IOP==7		;PDL FOR INPUT NESTING
IOPNT==6	;POINTER TO CURRENT INPUT FILE

T4=4		;TEMPORARY ACCUMS
T3=3
T2=2
T1=1
FL=0		;FLAG REGISTER (LEFT HALF LOCAL SWITCHES)
		;(RIGHT HALF MISC BITS)
IFN PURESW,<IFE RUNSW,<
PRINTX ;ASSEMBLY SWITCHES CONFLICT>>
;FLAGS (RH OF FL)

PROCS==1	;PROCESSOR SWITCHES SEEN
DOLOD==2	;WE WANT TO DO LOADING
PCM1==4		;FIRST COMMA SEEN IN PROCESSOR SWITCHES
PCM2==10	;SECOND COMMA SEEN
IDF==20		;SCAN SAW AN IDENTIFIER
LODOUT==40	;SOME OUTPUT HAS BEEN DONE TO LOADER
PERF==200	;PERMANENT TYPE FLAGS
MAPST==400	;A MAP DEVICE HAS BEN GENERATED FOR LOADER
CMDSN==1000	;THE COMMAND SHOULD BE WRITTEN AS SVC OR EDS
INCRF==2000	;WE ARE FINISHING CREF OUTPUT
INPRNT==4000	;WE ARE PRINTING A CHARACTER STRING IN ERROR MSG
PIPF==10000	;DOING SOMETHING FOR PIP
EDITF==20000	;IN EDIT OR CREATE
CREATF==40000	;CREATE
FFLG==PCM1	;/F FLAG IN DIRECTORY COMMAND
LPTFG==PCM2	;/L FLAG IN DIRECTORY COMMAND
NODAT==PCM1	;FILE FROM OTHER THAN DSK
NOLOOK==PCM2	;LOOKUP FAILED
TECOF==100000	;WE WANT TECO
RECALF==200000	;WE ARE READING A COMMAND SAVE FILE


;FLAGS (SWITCH TYPE)
LISTSW==1	;DO LISTING
CRSW==2		;DO A CREF
LIBSW==4	;DO A LIBRARY SEARCH OF THIS FILE
RELSW==10	;DO A LOAD ONLY ON THIS FILE
COMPLS==20	;COMPILE REGARDLESS OF DATES
MAP1==40	;MAP SWITCH SEEN MAP_ NOT GENERATED
MAP2==100	;MAP SWITCH SEEN MAP_ GENERATED

PDL==40	;LENGTH OF PDL
SWBK==5	;NUMBER OF WORDS FOR SWITCHES TO PROCESSOR
LODSCT==10	;NUMBER OF LOADER SWITCHES PER FILE ALLOWED
REPEAT 0,< THE MACRO PROCESS DEFINES DETAILS ABOUT THE VARIOUS
	PROCESSORS WHICH COMPILE IS EXPECTED TO HANDLE BY CALLING
	THE MACRO X WHICH IS REDEFINED TO PRODUCE THE INFORMATION REQUIRED.
	THE ARGUMENTS ARE SWITCH NAME,EXTENSION,PROCESSOR NAME
	<OPTIONAL>EXTENSION OF NEXT PROCESSOR IF MUST BE PROCESSED MORE
	THAN ONCE (AS COBOL),EXTENSION PRODUCED

	USE F4 OR F40 AS APPROPRIATE FOR THE SYSTEM
>

DEFINE PROCESS<
X FORTRAN,F4,F40
X MACRO,MAC,MACRO
X COBOL,CBL,COBOL
IFN STANSW,<X FAIL,FAI,FAIL>
>

;DEFINE VARIOUS BITS

ALPROC==RELSW	;OR OF BITS FOR ALL THE PROCESSORS
NPROCS==0	;NUMBER OF PROCESSORS
FCHLOD==400000	;LOADER CHANNEL HAS BEEN INITED
CHN==17		;USED TO DEFINE CHANNEL NUMBER
SPRC==0		;BITS FOR THOSE PROCESSORS WHICH OUTPUT TO ANOTHER

DEFINE X (A,B,C,D,E)<
B'SW==FCHLOD	;PROCESSOR BIT
IFDIF <D>,<>,<SPRC==SPRC!FCHLOD>
ALPROC==ALPROC!FCHLOD
NPROCS==NPROCS+1
FCH'B==FCHLOD
CHN'B==CHN	;CHANNEL NUMBER
CHN==CHN-1
FCHLOD==FCHLOD_<-1>>
PROCESS
CHNLOD==CHN
CHNLST==CHN
FCHLST==FCHLOD
CHNEDT==CHN
FCHEDT==FCHLOD
CHNPIP==CHN
FCHPIP==FCHLOD
CHNCRF==CHN-1		;CREF OUTPUT CHANNEL
FCHCRF==FCHLOD_<-1>
LOOK==0		;CHANNEL FOR DOING LOOKUPS FOR INFORMATION
NFILE==^D40	;NUMBER OF FILES PERMITTED IN A + CONSTRUCTION
MAXNST==17	;MAXIMUM NESTING DEPTH TO PERMIT
NESTDP==CHN-3	;MAXIMUM NESTING DEPTH POSSIBLE WITH THESE PROCESSORS
IFG NESTDP-MAXNST,<NESTDP==MAXNST>	;PICK SMALLER

EXTERNAL JOBFF,JOBREL,JOBSA,JOBSYM

;DEFINE FOLLOWING BECAUSE MACRO WON'T DO ADR ARITH PROPERLY

JOBERR=42
JOBDDT=74
JOBS41=122
JOBCOR=133

INTERN	JOBDDT,JOBS41,JOBCOR,JOBERR	;IN CASE JOBDAT CHANGES

IFN PURESW,<	HISEG>		;SIGNAL LOADER FOR HIGH SEGMENT

DEFINE STRING <CALLI T1,3>

;MACROS FOR THE DATA STORAGE IN PURE AND IMPURE VERSIONS

.ZZ=140		;PSEUDO-PC

DEFINE WORDS(A)<
IRP A,<
U(A,1)>>

IFE PURESW,<
DEFINE U(A,B)<
A:	BLOCK B>>

IFN PURESW,<
DEFINE U(A,B)<
A=.ZZ
.ZZ=.ZZ+B>>
NUNPNT==6
NUNTOP==7

IFE RUNSW,<
OFFSET==INHERE-74

NUNCOM:	IOWD 0,INHERE
	0
NUNGO2:	CALLI 15,11	;GET PROPER CORE SIZE
	JRST NOCOR	;LOSE
	IN 1,NUNCOM	;GET IT
	JRST NUNGO3	;OK
NUNERR:	CALLI NUNPNT,3	;WE LOSE, PRINT ERROR
	CALLI 12
NUNERM:	ASCIZ #?LINKAGE ERROR - I/O#
NUNGO3:	SKIPE 12,OFFSET+JOBCOR	;GET JOBCOR
	CAMG 12,JOBREL	;AND SEE IF WE SHOULD EXPAND
	JRST NUNGO4	;NO, START THE BLT
	MOVEI NUNPNT,NUNCER
	CALLI 12,11	;YES, DO IT
	JRST NUNERR	;LOSE
	MOVE 12,OFFSET+JOBS41	;RESET 41
	MOVEM 12,41
	JRST NUNGO4	;WIN
NUNCER:	ASCIZ /?CORE NEEDED/
INHERE:

NUNAC:	PHASE 0	;THE CODE TO GO IN THE ACS
NUNGO4:	MOVE 12,OFFSET+JOBDDT	;SET JOBDDT
	CALLI 12,2
NUNBLT:	BLT NUNTOP,0
	CALLI	;RESET THE WORLD
	AOS 1,JOBSA	;GET STARTING ADDRESS
	JRST (1)
	NUNERM
	XWD INHERE+1,75	;THE BLT WORD
DEPHASE
>
CREFIT:	MOVE T1,[SIXBIT /CREF/]
NUNDO:	MOVEM T1,NAME	;SET NAME
	CALLI		;RESET THINGS
IFE SYSSW,<	SETZM	NAME+3>
IFN SYSSW,<	MOVE T1,[XWD 1,1]
	MOVEM	T1,NAME+3		;CUSPS FROM DSK [1,1]
>
IFN RUNSW,<
	SETZM	NAME+1	;LET MONITOR SUPPLY SAV OR SHR
	MOVSI	T1,1	;START ADDR PLUS ONE
NUNDOT:		HRRI	T1,RUNBLK	;ARGUMENTS
	CALLI	T1,35	;RUN UUO
	MOVEI	FL,0		;ACS ARE GONE
	MOVE	P,PDP
	MOVEI	T1,[ASCIZ /?LINKAGE ERROR - RUN UUO
/]
	STRING
	JRST	NOFIL		;TRY TO GIVE FILE-NAME
>
IFE RUNSW,<
NORUN:	INIT 1,16	;GET A DSK IN DUMP MODE
	EXP SYSDEV	;SIXBIT SYS OR DSK
	0
	JRST DSKNA
	MOVSI T1,SAVEXT		;SIXBIT FOR SAVE OR DMP.
	MOVEM T1,NAME+1
	LOOKUP 1,NAME
	JRST NOFIL
	MOVE T1,NAME	;SET NAME OF NEW PROCESSOR
	CALL T1,[SIXBIT /SETNAM/]
	HLRO 15,NAME+3	;GET COUNT
	HRLM 15,NUNCOM
	MOVNS 15	;MAKE POSITIVE
	MOVEI 16,73(15)	;GET END
	ADDI 15,INHERE	;CHECK CORE SIZE
	IORI 15,1777
	MOVSI NUNTOP,NUNAC
	BLT NUNTOP,NUNTOP	;GET ACS LOADER
	HRR NUNBLT,16	;AND SET END OF BLT
	JRST NUNGO2
>
TERMF==200000
NUMF==100000
SPCF==400000
SPACT==40000	;SPECIAL ACTION TO BE TAKEN ON CHAR

GETNAM:	SETZM SVNAM(SVPT)	;ZERO OUT CELLS IN CASE NOTHING
	SETZM SVEXT(SVPT)	;GETS PUT THERE
	SETZM SVPPN(SVPT)
	SETZM SWBKS(SVPT)
	SETZM SVDEV(SVPT)
	TRNN FL,IDF		;WAS THE THING SCANNED AN IDENT
	JRST SYNERR	;NO, LOSE
	PUSH P,ACCUM
	PUSHJ P,SCANS	;CHECK FOR EXT OR PPN
	CAIE C,":"	;IS IT A DEVICE NAME
	JRST NODEV	;NO
	POP P,SVDEV(SVPT)	;WE WERE HIDING IT IN THE STACK
	PUSHJ P,SCAN	;BYPASS
	PUSHJ P,SCAN	;AND GET NEXT
;************GC PATCH 9/5/69******************
;
	TRNE	FL,PIPF		;IF IN PIP MODE ALLOW FOR NON-DIRECTORY DEVICE
	JRST	[CAIN  C,"_"
		 POPJ  P,  
		TLNE	CS,TERMF
		POPJ	P,
		JRST	NODEV-4]
;
;***************END OF PATCH*******************
	TRNN FL,IDF	;MUST BE AN IDENT
	JRST SYNERR
	PUSH P,ACCUM
	PUSHJ P,SCANS
NODEV:	POP P,SVNAM(SVPT)
	CAIN C,"["	;IS IT PPN
	JRST GETPPN
	CAIE C,"."	;NO, EXT?
	POPJ P,		;NEITHER, RETURN
	PUSHJ P,SCAN	;NO. GO OVER DOT
	PUSHJ P,SCANS	;PEEK AT NEXT CHAR
	SKIPG SAVCHR	;ALPHANUMERIC?
	JRST	GETN1	;NO. IT MAY BE A STAR IN PIP MODE
GETN2:	PUSHJ P,SCAN	;GET EXT
	TRNN FL,IDF
	JRST SYNERR
	MOVE T1,ACCUM
	HLLZM T1,SVEXT(SVPT)
	PUSHJ P,SCANS
	CAIE C,"["	;CHECK FOR PPN AGAIN
	POPJ P,
	;**********GC PATCH TO FIX DEC BUG  9/10/69
	JRST	GETPPN		;GET AND SAVE PPN
	;**********END OF PATCH******

GETN1:	TRNE	FL,PIPF	;PIP MODE?
	CAIE	C,52	;YES. ASTERISK?
	POPJ	P,0	;NO. NULL EXTENSION.
	JRST	GETN2	;WILD EXTENSION. GO GET IT.
GETPPN:	PUSHJ P,SCAN
GETPP1:	PUSHJ P,SCAN
	TRNN FL,IDF
	JRST SYNERR
	MOVE T1,ACCUM
IFE BBNSW,<
	PUSHJ P,RJUST	;THIS NEED TO BE RIGHT JUSTIFIED
	HRLM T1,SVPPN(SVPT)	;STORE LEFT HALF
	PUSHJ P,SCAN
	CAIE C,","
	JRST SYNERR
	PUSHJ P,SCAN
	TRNN FL,IDF
	JRST SYNERR
	MOVE T1,ACCUM
	PUSHJ P,RJUST
	HRRM T1,SVPPN(SVPT)
>
IFN BBNSW,<
	CALL	T1,[SIXBIT .SQUEZE.]
	MOVEM	T1,SVPPN(SVPT)
>
	PUSHJ P,SCAN
	CAIE C,"]"
	JRST SYNERR
	POPJ P,		;ALL DONE

	IFN STANSW,<
RJUST:	TRNE T1,77
	POPJ P,	;GET IT OVER THERE
	LSH T1,-6
	JRST RJUST>

IFE STANSW!BBNSW,<
RJUST:	PUSH	P,T3
	MOVE T3,T1	;CONVERT SIXBIT TO OCTAL
	MOVEI T1,0
CONVOC:	MOVEI T2,0
	LSHC T2,6
	CAIL T2,20
	CAILE T2,27
	JRST SYNERR
	LSH T1,3
	IORI T1,-20(T2)
	JUMPN T3,CONVOC
	POP	P,T3
	POPJ P,0
>
SCANS:	MOVNI T1,1	;FLAG AS NOTHING SEEN YET
	SKIPN CS,SAVCHR	;CHARACTER WAITING?
SCNS2:	PUSHJ P,GETCH
	JUMPN CS,SCNS1	;FOUND SOMETHING
	MOVEI T1,0
	JRST SCNS2
SCNS1:	JUMPL CS,SCNS4	;SPECIAL CHARACTER
	MOVEM CS,SAVCHR	;SAVE THAT CHARACTER
	JUMPL T1,SCNS3	;DO NOTHING ELSE IF NO BLANKS SEEN
	MOVEM T1,SAVCHR	;IF BLANKS SEEN, SAVE ONE
	MOVSI T1,70000
	ADDM T1,@GETB3(IOPNT)	;AND BACK UP POINTER
SCNS3:	TDZA CS,CS	;IN EITHER CASE, RETURN 0
SCNS4:	MOVEM CS,SAVCHR	;SAVE SPECIAL CHARACTER
	HRRZ C,CS		;GET A CHARACTER TO RETURN
	POPJ P,
SCAN:	TRZ FL,IDF	;RESET IN CASE NOT
	SKIPN CS,SAVCHR	;WAS THERE SOMETHING LEFT OVER
CONSN:	PUSHJ P,GETCH	;NO, GET ANOTHER
	JUMPE CS,.-1	;IGNORE BLANKS
	JUMPL CS,SPCHR	;IS IT A SPECIAL CHARACTER
	SETZM ACCUM	;PREPARE TO STORE IT
	MOVE T1,[POINT 6,ACCUM]
SCAN1:	TLNE T1,770000	;ALL SIX STORED?
	IDPB CS,T1	;NO, STORE ANOTHER
	PUSHJ P,GETCH	;GET NEXT
	JUMPG CS,SCAN1	;ANOTHER ALPHA
	TRO FL,IDF	;IT SURE IS
	MOVEM CS,SAVCHR
	MOVEI C,0	;TO AVOID CONFUSION
	MOVEI CS,0
	POPJ P,
SPCHR:	HRRZ C,CS	;RETURN HIM THE HALF OF IT
	CAIN C,";"	;SKIP REST OF LINE
	JRST SEMIC
	SETZM SAVCHR	;NOTHING SAVED BY NOW
	CAIN C,"*"
	TRNN FL,PIPF
	JRST SPCHR1
	PUSH P,[SIXBIT /*/]	;IN PIP MODE * IS AN IDENT
	POP P,ACCUM
	TRO FL,IDF
	POPJ P,
SPCHR1:	TLNN CS,SPACT	;DO WE WANT SPECIAL ACTION?
	POPJ P,		;NO
	JRST (CS)	;YES, RH IS DISPATCH
;GETCH RETURNS 7-BIT ASCII CHAR IN C, TABLE ENTRY IN CS

GETCH:	SOSLE @GETB1(IOPNT)	;USE CORRECT BUFFER HEADER
	JRST OKPICK
	XCT GETB2(IOPNT)	;AN IN UUO
	SKIPA
	JRST ERREOF	;WAS AN ERROR OR EOF, GO CHECK
OKPICK:	IBP @GETB3(IOPNT)
	MOVE C,@GETB3(IOPNT)	;PICK UP THE NEW BYTE POINTER
	MOVE CS,(C)	;GET THE WORD IT CAME FROM
	TRNE CS,1	;AND CHECK FOR SEQ NUM
	JRST 	[AOS @GETB3(IOPNT)	;ADVANCE POINTER
		MOVNI CS,5	;AND ADJUST COUNT
		ADDB CS,@GETB1(IOPNT)
		SKIPG CS	;CHECK FOR BUFFER OVERRUN
		PUSHJ P,GETCH	;GET RID OF TAB
		JRST GETCH]
	LDB C,@GETB3(IOPNT)
	JUMPE C,GETCH	;IGNORE NULLS
EOFRT:	MOVE CS,CTBL(C)	;GET STATUS BITS
EOFRT1:	TRNN FL,INPRNT	;IF PRINTING ERROR, DO NOT NEST
	CAIE C,100	;IS IT @
	POPJ P,
	JRST NEST	;SPECIAL

ERREOF:	XCT GETB4(IOPNT)	;TO A STATZ
	JRST READER	;AN INPUT ERROR
	JRST POPFIL	;GO GET PREVIOUS FILE
SEMIC:	PUSHJ P,GETCH	;READ CHRS
	CAIE C,12	;UNTIL A LINE FEED
	JRST SEMIC
	JRST CONSN
CTBL:	0
REPEAT 10,<XWD SPCF,.-CTBL>
	0		;TAB
	XWD SPCF!SPACT!TERMF+12,CHKTRM
XWD SPCF!SPACT!TERMF+13,CHKTRM	;VTAB
XWD SPCF!SPACT!TERMF+14,CHKTRM	;FORM
	0		;CARRET
REPEAT 15,<XWD SPCF,.-CTBL>
	XWD SPCF!TERMF!SPACT+44,CHKTRM
REPEAT 4,<XWD SPCF,.-CTBL>
	0		;SPACE
REPEAT 17,<XWD SPCF,.-CTBL>
REPEAT 12,<XWD NUMF,.-CTBL-40		;DIGIT>
REPEAT 6,<XWD SPCF,.-CTBL>
	XWD SPCF,100
REPEAT 32,<EXP .-CTBL-40		;UPPER CASE LETTERS>
REPEAT 7,<XWD SPCF,.-CTBL>
REPEAT 31,<EXP .-CTBL-100		;LOWER CASE LETTERS>
	XWD SPCF,.-CTBL
	XWD SPCF,.-CTBL
	XWD SPCF!TERMF!SPACT+44,CHKTRM
	XWD SPCF!TERMF!SPACT+44,CHKTRM
	XWD SPCF!SPACT,POPFIL
COMMA==CTBL+","

CHKTRM:	PUSH P,CS	;SAVE MAGIC BITS
TERMC1:	PUSHJ P,GETCH
	JUMPE CS,TERMC1	;ALSO IGNORE TABS AND SPACES
	TLNE CS,TERMF
	JRST TERMC1	;BYPASS TERMINATORS
	CAMN CS,COMMA	;CHECK FOR , AFTER CRET
	JRST	[POP P,(P)	;GET STACK IN SYNC
		POPJ P,]	;RETURN THE COMMA
	MOVEM CS,SAVCHR	;SAVE FOR LATER
	POP P,CS
	MOVEI C,0	;AS GOOD AS ANYTHING ELSE
	POPJ P,

DEFINE QQ<
N=1
REPEAT NESTDP+1,<MAC(\N)
	N=N+1>>

GETB1:	DINCT
DEFINE MAC(X)<IBUF'X+2>
QQ
GETB2:	HALT
DEFINE MAC(X)<IN X,0>
QQ
GETB3:	DINPT
DEFINE MAC(X)<IBUF'X+1>
QQ
GETB4:	HALT
DEFINE MAC(X)<STATZ X,740000>
QQ

NEST:	PUSH P,ACCUM	;SAVE STATE OF SCANNER
	PUSH P,FL	;SAVE THE FLAGS (AS IDF?)
	PUSH P,T1
	SETZM SAVCHR
	PUSH P,NAME	;AND THIS OTHER STUFF
	PUSH P,NAME+1
	PUSH P,NAME+2
	PUSH P,NAME+3
	AOBJP SVPT,TMNER	;GET A CLEAR SPACE FOR NAME
	PUSHJ P,SCAN
	PUSHJ P,GETNAM	;GET ONE TO USE
	AOBJP IOPNT,NESTTD	;TOO DEEP?
	PUSH IOP,SAVCHR
	SETZM	OPENB
	MOVSI	C,(SIXBIT /DSK/)
	MOVEM	C,OPENB+1
	MOVE C,NESTB(IOPNT)	;GET BUFFER POINTER
	MOVEM C,OPENB+2
	MOVE	C,[OPEN .-.,OPENB]
	DPB IOPNT,[POINT 4,C,12]
	XCT	C
	JRST DSKNA	;LOSE BIG
	PUSHJ P,CHKRM	;FIND A PLACE TO PUT BUFFERS
	MOVE C,JOBFF
	MOVEM C,BUFTAB(IOPNT)	;SAVE THE PLACE PUT
	XCT INTAB(IOPNT)	;DO AN INBUFF
	MOVE C,SVNAM(SVPT)
	MOVEM C,LNAM	;SET UP FOR LOOKUP
	MOVE C,SVEXT(SVPT)
NEST1:	MOVEM C,LEXT
	MOVE C,SVPPN(SVPT)
	MOVEM C,LPPN
	XCT LKTAB(IOPNT)
	JRST	[TRNE FL,INCRF	;SPECIAL IF TRYING TO READ QQCREF
		JRST DNCRF
		HLLZ C,LEXT	;SEE IF BLANK USED
		JUMPN C,NOFIL	;NO, NOT THERE
		MOVSI C,SIXBIT '   CMD'	;TRY .CMD
		JRST NEST1]
	SUB SVPT,[XWD 1,1]	;GET HIM POINTED BACK RIGHT
	POP P,NAME+3	;RESTORE THINGS
	POP P,NAME+2
	POP P,NAME+1
	POP P,NAME
	POP P,T1
	POP P,FL
	POP P,ACCUM
	TRZ FL,RECALF	;WE HAVE DONE THE FIND
	JRST GETCH	;AND CONTINUE TO GET THAT CHR

POPFIL:
	XCT RELTAB(IOPNT)	;RELEASE HIM
	MOVE C,BUFTAB(IOPNT)
	MOVEM C,FREBUF(IOPNT)	;MARK BUFFER FREE
	POP IOP,CS
	HRRZ C,CS
	SUB IOPNT,[XWD 1,1]	;POINT IT BACK
	JRST EOFRT1	;AND GIVE BACK THE CHARACTER

NESTB:	0
DEFINE MAC(X)<IBUF'X>
QQ
DEFINE MAC(X)<U (IBUF'X,3)>
QQ
INTAB:	HALT	;INBUFS
DEFINE MAC(X)<INBUF X,2>
QQ

LKTAB:	HALT
DEFINE MAC(X)<LOOKUP X,NAME>
QQ
RELTAB:	JRST ALLDON
DEFINE MAC(X)<RELEAS X,0>
QQ
;ERROR ROUTINES

ETMS:	MOVEI T1,[ASCIZ /TOO MANY SWITCHES: /]
ERRCOM:	STRING		;DDTOUT OR FACSIMILE
	MOVEI T1,20	;SET TO TYPE SOME CHRS TO TELL WHERE ERROR
	MOVE T2,[POINT 7,ERRBUF]	;IS FROM
	TRO FL,INPRNT	;IN CASE EOF WHILE READING CHRS TO TYPE
	SKIPN C,SAVCHR	;FIND THE ONE LEFT
	JRST PUTER
	TLNE C,SPACT	;IS IT SPECIAL
	MOVSS C	;YES. GET IT
	SKIPA
PUTER:	PUSHJ P,GETCH
	CAIN C,177	;THIS IS EOF
	JRST FINER
	IDPB C,T2
	SOJGE T1,PUTER
NOFIL0:	MOVE	T1,T2
	JRST NOFIL1	;PRINT WITH CR/LF
TMNER:	MOVEI T1,[ASCIZ /TOO MANY NAMES: /]
	JRST ERRCOM
DSKNA:	MOVEI T1,[ASCIZ /DISK NOT AVAILABLE: /]
	JRST ERRCOM
OUTER:	MOVEI T1,[ASCIZ /OUTPUT ERROR: /]
	JRST ERRCOM
PROCON:	MOVEI T1,[ASCIZ /PROCESSOR CONFLICT: /]
	JRST ERRCOM
NOCOR:	MOVEI T1,[ASCIZ /NOT ENOUGH CORE: /]
	JRST ERRCOM
READER:	MOVEI T1,[ASCIZ /INPUT ERROR: /]
	JRST ERRCOM
SYNRR1:	SUB IOPNT,[XWD 1,1]	;GET HIM BACK TO RIGHT PLACE
SYNERR:	MOVEI T1,[ASCIZ /WHAT?

 /]
	JRST ERRCOM
NESTTD:	MOVEI T1,[ASCIZ /NESTING TOO DEEP: /]
	JRST ERRCOM
UNRECS:	MOVEI T1,[ASCIZ /UNRECOGNIZABLE SWITCH: /]
	JRST ERRCOM
DEVERR:	MOVEI	T1,[ASCIZ /TTY IS ILLEGAL TRANSFER DEVICE:/]
	JRST 	ERRCLN
DEVER1:	MOVEI	T1,[ASCIZ /LPT IS ILLEGAL TRANSFER DEVICE:/]
ERRCLN:	STRING
	MOVE	T1,[POINT 7,ERRBUF]
	JRST	NOFIL1
NOFIL:	TRNE FL,RECALF	;WE WERE LOOKING UP A SVC FILE
	JRST SYNRR1	;SO GIVE SPECIAL MESSAGE
	MOVEI T1,[ASCIZ /NO SUCH FILE - /]
NAMCOM:	STRING			;DDTOUT OR FACSIMILE
	MOVE T1,[POINT 7,ERRBUF]
	MOVE T3,NAME
	PUSHJ P,SIXOUT
	HLLZ T3,NAME+1
	JUMPE T3,NOFIL1
	MOVEI T2,"."
	IDPB T2,T1
NOFIL2:	PUSHJ P,SIXOUT
NOFIL1:	MOVEI T2,15
	IDPB T2,T1
	MOVEI T2,12
	IDPB T2,T1
	MOVEI T2,0
	IDPB T2,T1
	MOVEI T1,ERRBUF
	STRING
	CALLI 0
	CALLI 12	;GO AWAY
FIU:	MOVEI T1,[ASCIZ /FILE IN USE OR PROTECTED - /]
	JRST NAMCOM

SIXOUT:	MOVEI T2,0
	LSHC T2,6
	ADDI T2,40
	IDPB T2,T1
	JUMPN T3,SIXOUT
	POPJ P,
DEVNA:	MOVEI T1,[ASCIZ /DEVICE NOT AVAILABLE - /]
	CALLI T1,3
	MOVE T1,[POINT 7,ERRBUF]
	MOVE T3,LOKNAM
	JRST NOFIL2
FINER:	CAIGE T1,10	;HAVE WE PRINTED ENOUGH??
	JRST NOFIL0	;YES
	MOVEI C,11	;NO, PRINT A TAB
	IDPB C,T2
	JRST NOFIL0	;AND GO AWAY
ALLDON:	TRNE FL,INCRF	;JUST FOUND END OF QQCREF FILE
	JRST DNCRF
	TRNE FL,INPRNT
	JRST FINER	;IF PRINTENG AND EOF THEN FIINSH UP
	HRRZ T1,(P)	;GET THE ADDRESS WE WANT TO RETURN TO
	CAIE T1,NXFIL+1	;THIS SHOULD BE HERE
	JRST SYNERR	;ELSE ERROR
	SETZM PCNAM	;NO LINK NAME TO START WITH
	MOVEI T3,OUTLOD
	MOVSI T2,SIXBIT '   /G '		;SET UP FOR TERMINATE LOADING
	TRNE FL,DOLOD		;ARE WE LOADING?
	PUSHJ P,OUTSIX		;YES, PUT IT OUT
	MOVEI T4,FCHLOD	;START WITH LOADER
	MOVEI T3,NPROCS	;WHICH IS HERE
	SKIPA
ALDN1:	LSH T4,1	;LOOK FOR NEXT PROCESSOR
	TRNN FL2,(T4)	;HAS THAT PROCESSOR BEEN SET UP FOR OUTPUT?
	SOJGE T3,ALDN1	;NO, TRY NEXT (BUT NOT TOO MANY)
	JUMPL T3,DONE	;IF OUT OF PROCESSORS THEN DONE
	PUSH P,T3	;SAVE IT FOR LATER
	MOVE T3,PRCTAB(T3)	;GET PROCESSOR OUTPUT ROUTINE
	SKIPN T2,PCNAM	;IS THERE A PROCESSOR FOR IT TO CALL?
	JRST NONAM	;NO
	PUSHJ P,OUTSIX	;YES, PUT OUT ITS NAME
	MOVEI T1,"!"	;AND THE LOAD SYMBOL
	PUSHJ P,(T3)
	PUSHJ	P,OUCRLF
NONAM:	POP P,T3	;GET BACK PROCESSOR NUMBER
	XCT RLSTAB(T3)	;CLOSE UP ITS OUTPUT
	MOVE T1,PRCNAM(T3)	;GET THE NAME OF THAT PROCESSOR
	MOVEM T1,PCNAM	;AND SET AS THE ONE TO LINK TO
	SOJGE T3,ALDN1	;GO BACK IF MORE TO LOOK AT
DONE:	TRNE FL,CMDSN	;DID WE SEE COMMAND FROM TTY?
	JRST DONE1	;NO, DO NOT WRITE FILE
	MOVE T1,JOBNAM
	HRRI T1,(SIXBIT /SVC/)
	TRNE FL,EDITF
	HRRI T1,(SIXBIT /EDS/)
	MOVEM T1,LNAM	;SET UP OUTPUT FILE
	MOVSI T1,SIXBIT '   TMP'
	MOVEM T1,LEXT
	SETZM LDAT
	SETZM LPPN
	CLOSE LOOK,0	;MAKE SURE NOTHING USING THIS CHANEL
	PUSHJ P,CHKRM	;GET ROOM FOR BUFFERS
	ENTER LOOK,LNAM	;GET SET TO WRITE
	JRST FIU	;TREAT THIS AS A FATAL ERROR
	OUTBUF LOOK,2
	MOVE T1,[POINDTBUF]	;WRITE ALL OF COMMAND
DONE3:	ILDB T2,T1
	CAIN T2,177	;DONE?
	JRST DONE2
	SOSG LOOKBF+2
	OUTPUT LOOK,0
	IDPB T2,LOOKBF+1
	JRST DONE3
DONE2:	RELEASE LOOK,0	;LET IT GO
DONE1:	TRNE FL2,FCHCRF	;DID WE DO ANY CREF?
	PUSHJ P,FINCRF	;YES, FINISH OFF CREF
	SKIPN T1,PCNAM	;IS THERE ONE TO LOAD?
	CALLI 12	;NO, EXIT
	JRST NUNDO	;GO LOAD IT
	DEFINE X (A,B,C,D,E)
<RELEASE CHN'B,0>

RLSTAB:	PROCESS
	RELEASE CHNLOD,0	;DON'T FORGET LOADER

	DEFINE X (A,B,C,D,E)
<<SIXBIT /C/>>

PRCNAM:	PROCESS
	SIXBIT /LOADER/
CHKRM:	PUSH P,T1	;SAVE THE REGISTERS WE ARE USING
	PUSH P,T2
	MOVSI T1,-<NESTDP+2>	;LOOK TO SEE IF ANY FREED BUFFERS
	SKIPN T2,FREBUF(T1)
	AOBJN T1,.-1	;TRY AGAIN
	JUMPGE T1,USTOP	;NO, GET IT FROMTOP OF STORAGE
	MOVEM T2,JOBFF	;YES, SET JOBFF THERE
	SETZM FREBUF(T1)	;AND MARK IT USED
	JRST TPOPJ	;THATS ALL FOR NOW
USTOP:	MOVE T1,SVJFF	;GET THE CURRENT TOP OF BUFFER AREA
	MOVEM T1,JOBFF
	ADDI T1,<203*2>+1	;LEAVE THIS MUCH ROOM
	MOVEM T1,SVJFF	;THATS THE NEW TOP
	CAMGE T1,CORTOP	;WILL THAT RUN US OUT OF CORE?
	JRST TPOPJ	;NO, LEAVE
	PUSH P,CTPOPJ
XPAND:	MOVEI T1,2000	;GET SET TO EXPAND
	ADDM T1,JOBSYM	;UPDATE IMPORTANT LOCATIONS
	ADDM T1,CORTOP
	ADDM T1,CORT1
	ADD T1,JOBREL	;NEW TOP DESIRED
	CALLI T1,11	;ASK FOR IT
	JRST NOCOR	;LOSE BIG
	MOVE T1,JOBREL
MVCR:	MOVE T2,-2000(T1)	;MVOVE CORE UP
	MOVEM T2,(T1)
	CAMLE T1,CORTOP	;ARE WE DONE?
	SOJA T1,MVCR
CTPOPJ:	POPJ	P,TPOPJ
TPOPJ:	POP P,T2	;ALL FINISHED
	POP P,T1
	POPJ P,
DEFINE TABLE
<COMAND COMPILE,<TRZ FL,DOLOD>
COMAND LOAD,JFCL
COMAND DEBUG,<PUSHJ P,DEBUG>
COMAND EXECUTE,<PUSHJ P,XCTR>
COMAND EDIT,<TRO FL,EDITF>
COMAND CREATE,<TRO FL,EDITF!CREATF>
COMAND LIST,<JRST LISTR>
COMAND DIRECTORY,<JRST DODIR>
COMAND CREF,<JRST CREFIT>
COMAND CROSS,<JRST CREFIT>
COMAND DELETE,<JRST DODEL>
COMAND TECO,<TRO FL,EDITF!TECOF>
COMAND MAKE,<TRO FL,EDITF!TECOF!CREATF>
COMAND RENAME,<JRST DOREN>
COMAND TYPE,<JRST TYPR>
COMAND TTY,<JRST TELE>
COMAND BASIC,<JRST BASCUS>
COMAND PIP,<JRST PIPCUS>
COMAND MACRO,<JRST MACCUS>
COMAND LINED,<JRST LINCUS>
COMAND EDITOR,<JRST EDICUS>
COMAND REWIND,<JRST REWIND>
COMAND COPY,<JRST TRANSF>
COMAND TRANSFER,<JRST TRANSF>
COMAND PROTECT,<JRST PROTEC>
COMAND UNPROTECT,<JRST UNPROT>
COMAND LOCK,<JRST LOCK>
COMAND UNLOCK,<JRST UNLOCK>
>
DEFINE COMAND (A,B)
<<SIXBIT /A/>>
COMTAB:	TABLE
COMTLG==.-COMTAB
DEFINE COMAND (A,B)
<B>
COMT2:	TABLE

DEBUG:	MOVSI  T2,SIXBIT '   /T '
	SKIPA
XCTR:	MOVSI T2,SIXBIT '   /E '
	MOVEI T3,OUTLOD
	JRST OUTSIX

RPGSET:	MOVE T1,[POINT 7,FCOMD]
	MOVEM T1,DINPT
	MOVEI FL,RECALF!CMDSN
	JRST RPGRET
;HERE WE GO TO START THE WORLD TURNING

STPT:	TDZA	T4,T4		;NORMAL ENTRY
	MOVNI	T4,1		;REENTRY FROM AN EDITOR
IFN PURESW,<	SETZM	140		;MUST CLEAR LOW CORE
	MOVE	T1,[XWD 140,141]
	BLT	T1,LOWTOP
>
	JUMPL	T4,RPGSET
	TTCALL 10,0	;RESET POINTER TO START OF COMMAND
	MOVEI FL,0
	MOVE T1,[POINT 7,DDTBUF]	;SET POINTER
	MOVEM T1,DINPT
START1:	TTCALL 5,T2	;READ A COMMAND CHAR INTO T2
	MOVEI	T2,33		;NON-SKIP MEANS END
	IDPB	T2,T1		;STORE IN DDTINBUF
	MOVE	T3,CTBL(T2)	;GET CHARACTER DESCRIPTOR
	TLNN	T3,TERMF	;IS IT A BREAK CHAR?
	JRST	START1		;NO. GO GET MORE.
	MOVEI	T2,177		;MARK END WITH AN EOF FLAG
	IDPB T2,T1
	IDPB T2,T1	;MAKE SURE
RPGRET:
IFN PURESW,<
	MOVE T1,[XWD INIDAT,INILOW]
	BLT T1,INILOW+INILEN
	MOVEI	T1,LOWTOP+1
	HRLM	T1,JOBSA
>
	CALLI 	;AND RESET
	MOVEI T1,3
	CALLI T2,30	;PJOB
	IDIVI T2,12
	ADDI T3,20	;TO SIXBIT
	LSHC T3,-6
	SOJG T1,.-3	;THREE DIGITS
	HLLZM T4,JOBNAM	;SAVE TO MAKE UNIQUE NAMES
	TLO T4,404040	;NOW TO ASCII FOR ASCIZ'S
	MOVEI T1,3	;THREE CHARS
	LSH T3,1
	LSHC T3,6	;BRING IN A CHAR
	SOJG T1,.-2
	DPB T3,[POINT 21,CRFRDR,27]	;SAVE IN ASCIZ
	DPB T3,[POINT 21,FCOMD,27]
	DPB T3,[POINT 21,FCOMD2,27]
	MOVSI T1,377777	;SET COUNT TO A LARGE NUMBER
	MOVEM T1,DINCT
	MOVE P,PDP		;SET UP PDL
	MOVE IOP,[IOWD <NESTDP+2>*3,IOPD]	;AND IO PDL
	MOVSI IOPNT,-<NESTDP+1>	;SET NEXT LIMIT
	MOVEI FL2,0	;NO OUTPUT FILES OPEN
	TRO FL,DOLOD		;WE WANT TO LOAD
	HRLI FL,F4SW	;ASSUME F4
	SETZM LOKNAM	;NO ALTERNATE DEVICE YET
	OPEN LOOK,DSKLK	;GET THE DSK
	JRST DSKNA
	INBUF LOOK,2
	SETZM FREBUF	;MARK NO FREED BUFFERS
	MOVE T1,[XWD FREBUF,FREBUF+1]
	BLT T1,FREBUF+NESTDP+1
	MOVE T1,JOBFF
	MOVEM T1,SVJFF
	MOVE T1,JOBREL
	SKIPE JOBDDT	;SEE IF WE SHOULD LEAVE SYMBOL SPACE
	HRRZ T1,JOBSYM
	MOVEM T1,CORTOP
	MOVEM T1,CORT1
	SETZM SAVCHR	;TO START THINGS
	HLLZS JOBERR	;RESET ERROR COUNT
	MOVSI SVPT,-NFILE
	PUSHJ P,SCAN	;SCAN PAST THE COMPILE ETC
	MOVE T1,ACCUM	;FIND OUT WHICH COMMAND
	MOVNI T2,1
STPT1:	LSH T1,6
	LSH T2,-6
	JUMPN T1,STPT1
	MOVSI T1,-COMTLG
STPT2:	MOVE T3,COMTAB(T1)
	ANDCM T3,T2
	CAMN T3,ACCUM
	JRST COMATC
	AOBJN T1,STPT2
	JRST SYNERR
COMATC:	XCT COMT2(T1)	;DO THE APPROPRIATE THING
	PUSHJ P,SCAN	;GET NEXT THING
	TRNN IOPNT,-1	;IF DOWN A LEVEL ITS OK
	TLNN CS,TERMF	;OR IF NOTHING SEEN
	SKIPA
	JRST COMAT1
	TRNE FL,EDITF
	JRST DOEDT1
	JRST NXFIL1
COMAT1:	MOVE T1,[POINT 7,FCOMD]	;GENERATE FAKE COMMAND TO READ
	TRNE FL,EDITF
	MOVE T1,[POINT 7,FCOMD2]
	MOVEM T1,DINPT	;SAVE FILE
	TRO FL,RECALF+CMDSN	;MARK RECALLING FILE, DONT WRITE
	SETZM SAVCHR	;CLEAR OUT SCANNER
	MOVSI IOPNT,-<NESTDP+2>	;ALLOW EXTRA NESTING
	PUSHJ P,SCAN
	TRNE FL,EDITF
	JRST DOEDIT
NXFIL:	PUSHJ P,SCAN
NXFIL1:	MOVSI SVPT,-NFILE	;SET UP FOR NUMBER OF FILES
	MOVEI SWCNT,SWBK*5	;SET UP FOR SWITCHES
	MOVE SWPT,[POINT 7,SWBLK]	;AND POINTER
	SETZM SWBKL
	SETZM SWBKB
	SETZM ONAM
	HLL FL3,FL	;SET TEMP FLAGS FROM PERM FLAGS
	MOVE T1,[POINT 7,LODSBK]	;SET POINTER TO LOADER
	MOVEM T1,LODSP		;SWITCH BLOCK
	MOVEI T1,LODSCT
	MOVEM T1,LODCTR
	MOVE T1,LODCT2	;AND SET COUNT FOR AFTER FILE NAME SWITCHES
	MOVE T1,[POINT 7,LODSB2]
	MOVEM T1,LODSP2
	SETZM BROCNT	;CLEAR OUT THE <> COUNT
	JRST	ILP0A
;MAIN LOOP FOR READING INPUT

ILP0:	PUSHJ P,SCAN	;GET FIRST "THING"
ILP0A:	CAIN C,"/"	;CHECK FOR PERM COMPILE SWITCHES
	JRST COMPS1
	CAIN C,"%"	;CHECK FOR PERM LOADER FLAGS
	JRST LOADS1
ILP1A:	TRZ FL,PROCS	;NO PROCESSOR SWITCHES SEEN YET
ILP1:	PUSHJ P,GETNAM	;GO GET A FILE NAME
	MOVE C,LODSP	;EXCHANGE POINTERS
	EXCH C,LODSP2
	MOVEM C,LODSP
	MOVE C,LODCTR
	EXCH C,LODCT2
	MOVEM C,LODCTR
ILP2A:	PUSHJ P,SCAN	;GET THE SPECIAL CHR OR WHATEVER
ILP2:	CAIE C,","	;DONE WITH THIS SET OF NAMES?
	TLNE CS,TERMF	;WILL ACCEPT A TERMINATOR
	JRST SETUP	;GO SET UP THE FILES FOR PROCESSORS
	CAIN C,"("	;MAYBE SWITCHES TO BE PASSED TO PROCESSORS
	JRST PROCSW
	CAIN C,"/"	;OR FOR US
	JRST COMPSW
	CAIN C,"%"
	JRST LOADS2
	CAIN C,">"
	JRST ENDBRO	;THIS IS THE END OF A BROKET STRING
	CAIN C,"="	;MAYBE HE IS SETTING THE OUTPUT NAME
	JRST SETONM
	CAIE C,"+"	;IS THIS A SECOND FILE
	JRST SYNERR	;IT SHOULD HAVE BEEN ONE OF THOSE
	AOBJP SVPT,TMNER	;MAYBE TOO MANY FILES
	MOVE C,LODSP	;EXCHANGE POINTERS AGAIN
	EXCH C,LODSP2
	MOVEM C,LODSP
	MOVE C,LODCTR
	EXCH C,LODCT2
	MOVEM C,LODCTR
	PUSHJ P,SCAN	;GET NEXT
	CAIE C,"<"	;IS THIS THE <> CONSTRUCTION
	JRST ILP0A	;NO
	AOS BROCNT	;WE ARE ONE DEEPER IN BROKETS
	PUSH P,SVPT	;SAVE AWAY ALL THE IMPORTANT INFORMATION
	PUSH P,SWPT
	PUSH P,SWCNT
	PUSH P,LODSP
	PUSH P,LODSP2
	PUSH P,LODCTR
	PUSH P,LODCT2
	PUSH P,SWBKL
	PUSH P,SWBKB
	PUSH P,ONAM
	JRST ILP0	;GO FINISH THINGS UP

ENDBRO:	PUSHJ P,SCAN	;GO GET NEST THING (SHOULD BE A ",")
	TLNN CS,TERMF
	CAIN C,","
	SOSGE BROCNT	;ALSO ERROR IF NO < WAS SEEN
	JRST SYNERR
	SUB P,[XWD 6,6]	;RESET PDL
	JRST SETUP	;GO TAKE CARE OF THINGS

NXFILP:	SKIPG BROCNT	;ARE WE DONING BROKETS?
	JRST NXFIL	;NO, JUST CONTINUE
	MOVE T1,(P)
	MOVEM T1,ONAM
	MOVE T1,-1(P)
	MOVEM T1,SWBKB
	MOVE T1,-2(P)
	MOVEM T1,SWBKL
	MOVE T1,-3(P)
	MOVEM T1,LODCT2
	MOVE T1,-4(P)
	MOVEM T1,LODCTR
	MOVE T1,-5(P)
	MOVEM T1,LODSP2
	MOVE T1,-6(P)
	MOVEM T1,LODSP
	MOVE SWCNT,-7(P)
	MOVE SWPT,-10(P)
	MOVE SVPT,-11(P)
	JRST ILP0
COMPS:	PUSHJ P,SCAN	;GET THE NAME OF THE SWITCH
	TRNN FL,IDF	;WAS THERE REALLY AN IDENTIFIER THERE?
	JRST SYNERR	;LOSE
	MOVE T1,ACCUM	;GET ITS SIXBIT
	MOVNI T2,1	;SET UP MASK
CMP1:	LSH T1,6
	LSH T2,-6
	JUMPN T1,CMP1	;WHEN DONE T2 HAS 0'S FOR ALL CHRS IN T1
	MOVSI T1,-TBLG	;GET SET TO SCAN FOR NAME
	SETOM NUMAT	;-1 TO NUMBER FOUND
CMP3:	MOVE T3,SWTAB(T1)	;GET A SWITCH
	CAMN T3,ACCUM	;EXACT MATCH?
	JRST MATCH	;YES, ALL DONE
	ANDCM T3,T2	;0 OUT UNNECESSARY CHRS
	CAME T3,ACCUM
	JRST CMP2	;NO MATCH
	AOS NUMAT	;POSSIBLE MATCH
	MOVEM T1,SVIND	;SAVE POINTER
CMP2:	AOBJN T1,CMP3
	SKIPE NUMAT	;WAS THERE AT LEAST ONE BUT NO MORE?
	JRST UNRECS
MATCH:	MOVE T1,SWTAB2(T1)	;GET ACTION
	TLZ FL3,(T1)	;TURN OFF SWITCHES AS NEEDED
	TRNE FL,PERF	;PERMANENT?
	TLZ FL,(T1)	;SET THAT TOO
	MOVSS T1
	TLO FL3,(T1)	;AND TURN ON OTHERS
	TRNE FL,PERF
	TLO FL,(T1)
	TLZE FL3,MAP1	;ARE WE DOING MAPING?
	JRST SETMAP	;YES, SET IT UP
	JRST SCAN	;GET SOMETHING ELSE

DEFINE X (A,B,C,D,E)
<SWITCH A,<XWD B'SW,ALPROC>>
	DEFINE TABLE
<SWITCH LIST,<XWD LISTSW,CRSW>
SWITCH CREF,<XWD CRSW!LISTSW,0>
SWITCH M,<XWD MACSW,ALPROC>
SWITCH C,<XWD CRSW!LISTSW,0>
SWITCH LIBRARY,<XWD LIBSW,0>
SWITCH REL,<XWD RELSW,ALPROC>
SWITCH NOLIST,LISTSW
SWITCH NOSEARCH,LIBSW
SWITCH L,<XWD LISTSW,CRSW>
SWITCH F,<XWD F4SW,ALPROC>
SWITCH N,LISTSW
SWITCH COMPILE,<XWD COMPLS,0>
SWITCH NOCOMPILE,COMPLS
SWITCH MAP,<XWD  MAP1!MAP2,0>
PROCESS
>

DEFINE SWITCH (A,B)
<<SIXBIT /A/>>
SWTAB:	TABLE
TBLG==.-SWTAB
DEFINE SWITCH (A,B)
<B>
SWTAB2:	TABLE
;HERE ON "/" AFTER A FILE NAME

COMPSW:	TRZ FL,PERF	;DOING TEMP
	PUSHJ P,COMPS
	JRST ILP2

;HERE ON "/" AS FIRST CHAR OF IDENT, I.E. PERM SW

COMPS1:	TRO FL,PERF
	PUSHJ P,COMPS
	JRST ILP0A

SETMAP:	TLZ FL,MAP1	;IN CASE WAS A PERM TYPE FLAG
	TRON FL,MAPST	;IS IT SET?
	TRNN FL,DOLOD	;OR NOT LOADING?
	JRST SCAN
	MOVEI T3,OUTLOD
	PUSHJ	P,OUCRLF
	MOVE T2,[SIXBIT /MAP_/]
	PUSHJ P,OUTSIX
	TRZ FL,LODOUT	;DO NOT NEED A COMMA FOR NEXT FILE
	JRST SCAN
PROCSW:	TROE FL,PROCS	;HAVE WE ALREADY SEEN SOME?
	JRST SYNERR	;YES, I DEFINE THIS AS ILLEGAL
	MOVEM SWPT,SWBKS(SVPT)	;SAVE BYTE POINTER TO NEW ONES
	TRZ FL,PCM1!PCM2	;NO COMMAS YET
PROCS1:	PUSHJ P,GETCH	;GIVE ME A CHARACTER
	CAIN C,")"	;DONE?
	JRST ESTR	;YES
	CAIN C,","	;POSSIBLY COMMA
	JRST PCCOM	;GO TAKE GOOD CARE OF IT
	JUMPLE CS,SYNERR	;NOT ANUMBER OR LETTER, HE LOSES
	IDPB C,SWPT	;SAVE IT AWAY
	SOJG SWCNT,PROCS1	;NEXT PLEASE
	JRST ETMS	;TOO MANY SWITCHES FOR SPACE RESERVED

PCCOM:	TROE FL,PCM1	;IS THIS THE FIRST OR SECOND COMMA
	JRST NOTBIN	;NOT FIRST, TRY FOR SECOND
	CAMN SWPT,SWBKS(SVPT)	;WAS ANYTHING STORED?
	JRST PROCS1	;NO, JUST IGNORE
	SKIPE SWBKB	;ARE THERE ALREADY BINARY SWITCHES
	JRST SYNERR	;YES, MORE NOT ALLOWED
	MOVE T1,SWBKS(SVPT)	;GIVE THIS TO BINARY
	MOVEM T1,SWBKB
COMCOM:	MOVEI C,0	;MARK END OF STRING
	IDPB C,SWPT
	SOJLE SWCNT,ETMS	;HAVE WE RUN OUT?
	MOVEM SWPT,SWBKS(SVPT)	;AND A NEW ONE FOR SRC
	JRST PROCS1
NOTBIN:	TROE FL,PCM2	;SECOND ALREADY SEEN?
	JRST SYNERR	;YES, THREE NOT PERMITTED
	CAMN SWPT,SWBKS(SVPT)	;ANYTHING THERE?
	JRST PROCS1	;HE WOULD HAVE BEEN JUST AS WELL WITHOUT IT
	SKIPE SWBKL	;ALREADY LIST SWITCHES?
	JRST SYNERR	;YES, HE LOSES
	MOVE T1,SWBKS(SVPT)	;AND GIVE TO CORRECT PERSON
	MOVEM T1,SWBKL
	JRST COMCOM
ESTR:	CAMN SWPT,SWBKS(SVPT)	;WAS ANYTHING STORED?
	JRST	[SETZM SWBKS(SVPT)	;NO, ZERO IT
		JRST ESTR1]
	MOVEI C,0
	IDPB C,SWPT	;MARK
	SOJLE SWCNT,ETMS
ESTR1:	PUSHJ P,SCAN
	JRST ILP2	;NEXT
SETONM:	TRNE FL,PROCS	;PROCESSOR SWITCHES NOT PERMITTED HERE
	JRST SYNERR
	MOVE T1,SVNAM(SVPT)	;GET THE NAME
	MOVEM T1,ONAM	;AND SAVE IT AWAY
	PUSHJ P,SCAN
	JRST ILP1

LOADS1:	PUSHJ P,LODS1
	JRST ILP0
LOADS2:	PUSH P,[ILP2A]	;SET RETURN POINT
LODS1:	PUSHJ P,GETCH	;NEXT CHR
	JUMPLE CS,SYNERR	;SPECIAL CHARACTERS NOT PERMITTED
	IDPB C,LODSP	;SAVE IT
	SOSG LODCTR	;CHECK SIZE
	JRST ETMS
	TLNN CS,NUMF	;A NUMBER
	POPJ P,		;NO, DONE
	JRST LODS1	;YES, THEY GET PASSED ON
SETUP:	SKIPE ONAM	;SET ONAM IF NOT ALREADY
	JRST ONSET
	MOVE T1,SVNAM(SVPT)	;AS LAST FILE NAME
	MOVEM T1,ONAM
ONSET:	MOVSI T1,-NFILE	;NUMBER OF FILES
	TRZ FL,NODAT	;WE HAVE NOT SEEN A DIFFERENT DEVICE
	SETZM SDAT	;LATEST DATE
	SETZM STIM	;AND LATEST TIME
	TLZ FL2,-1	;NO PROCESSOR YET
	PUSHJ P,GETPRO	;GO FIND DATE AND PROCESSOR
	TLNN FL2,RELSW	;IF A REL FILE
	TLNE FL3,LIBSW	;OR LIBRARY SEARCH
	JRST LDREL	;GO LOAD IT NOW
	TRNN FL,NODAT	;NO DATES ON OTHER DEVICES
	TLNE FL3,COMPLS	;DO WE ALWAYS WANT TO COMPILE?
	JRST DOCOMP	;YES, IGNORE DATES
	MOVE T1,SVPPN	;LOOK ON THIS AREA FOR REL
REREL:	MOVEM T1,LPPN	;BUT ONLY FIRST TIME
	MOVEM T1,SVRPP	;SO WE KNOW IF SECOND TIME
	MOVE T1,ONAM	;SEE IF REL IS THERE
	MOVEM T1,LNAM
	MOVSI T1,SIXBIT '   REL'
	MOVEM T1,LEXT
	LOOKUP LOOK,LNAM	;IS IT THERE
	JRST DOCOMP	;NO, WE MUST RECOMPILE
	LDB T2,[POINT 12,LDAT,35]	;GET DATE
	CAMGE T2,SDAT	;EARLIER
	JRST DOCOMP	;YES, COMPILE
	CAME T2,SDAT	;SAME?
	JRST NOCOM1	;NO, ALL OK
	LDB T2,[POINT 11,LDAT,23]	;YES, GET TIME
	CAMLE T2,STIM	;LATER?
	JRST NOCOM1	;YES
DOCOMP:	SKIPE SVRPP	;DID WE LOOK ON THIS AREA?
	JRST	[MOVEI T1,0
		JRST REREL]	;NO, TRY IT
	MOVEI T3,0	;GET THE PROCESSOR
	SKIPA T1,FL2
DC1:	LSH T1,1	;TRY NEXT BIT
	TLNN T1,400000	;IS HIGH BIT ON YET?
	AOJA T3,DC1	;TURNS IT INTO A NUMBER
DC2:	MOVEM T3,PCNUM	;SAVE IT FOR LATER
	MOVE T3,PRCTAB(T3)	;GET THE NAME OF THE OUTPUT ROUTINE
	MOVE T2,ONAM	;START PUTTING OUT
	PUSHJ P,OUTSIX
	SKIPE T2,SWBKB	;ARE THERE SWITCHES
	PUSHJ P,OUTSW	;YES, OUTPUT THEM
	TLNN FL3,LISTSW	;LISTING REQUESTED?
	JRST NOLST
	MOVEI T1,","	;YES, NEED A COMMA
	PUSHJ P,(T3)
	MOVE T2,ONAM	;SET IT UP
	PUSHJ P,OUTSIX
	TLNN FL3,CRSW	;CREF MAYBE
	JRST NOLST1
	MOVSI T2,SIXBIT '   /C '
	PUSHJ P,OUTSIX
	PUSH P,T3
	PUSHJ P,ENTCRF	;PUT IT IN THE ###CREF FILE
	POP P,T3
NOLST1:	SKIPE T2,SWBKL	;SWITCHES?
	PUSHJ P,OUTSW
NOLST:	MOVEI T1,"_"
	PUSHJ P,(T3)
	MOVSI T4,-NFILE
PRCLP:	SKIPN T2,SVDEV(T4)	;IS THERE A DEVICE THERE
	JRST PRCLP2	;NO
	PUSHJ P,OUTSIX	;YES, PRINT IT
	MOVEI T1,":"
	PUSHJ P,(T3)
PRCLP2:	MOVE T2,SVNAM(T4)	;PUT OUT NAME
	PUSHJ P,OUTSIX
	SKIPN T2,SVEXT(T4)	;AND EXT IF NECESSARY
	JRST NOEXT
	MOVEI T1,"."
	PUSHJ P,(T3)
	PUSHJ P,OUTSIX
NOEXT:	SKIPE T2,SVPPN(T4)	;NEED PPN?
	PUSHJ P,OUTPPN	;PUT THEM OUT
	SKIPE T2,SWBKS(T4)	;AND SWITCHES
	PUSHJ P,OUTSW
	CAMN T4,SVPT	;ALL DONE?
	JRST ENDPRC	;YES, GO FINISH UP AND CONSIDER LOADING
	MOVEI T1,","
	PUSHJ P,(T3)	;NEXT FILE
	AOBJN T4,PRCLP
ENDPRC:	PUSHJ	P,OUCRLF
IFN SPRC,<
	TLNN FL2,SPRC>
	JRST NOCOMP	;GO LOAD
IFN SPRC,<
	MOVSI SVPT,-NFILE	;RESET POINTER
	MOVE T1,ONAM	;AND FAKE WORLD
	MOVEM T1,SVNAM
	MOVE T3,PCNUM	;GET BACK PROCESSOR NUMBER
	MOVE T1,INTEXT(T3)	;GET EXTENSION
	MOVEM T1,SVEXT
	SETZM SVPPN
	SETZM SWBKS
	SETZM SWBKB
	SETZM SWBKL
	HRL FL2,NXPC(T3)	;SET FOR NEXT PROCESSOR
	JRST DOCOMP	;AND GO EMIT CALLS
>
LDREL:	TRNE SVPT,-1	;CHECK FOR ONLY ONE FILE
	JRST NOFIL		;IF MORE THAN ONE, THERE IS AN ERROR
NOCOMP:	TRNN FL,DOLOD	;DO WE WANT TO LOAD?
	JRST NXFILP	;NO, GO TO NEXT
	MOVEI C,0
	IDPB C,LODSP	;END SECOND SET OF SWITCHES
	IDPB C,LODSP2
	MOVEI T3,OUTLOD	;SET FOR LOADER
	TRZE FL,LODOUT	;IS THERE ALREADY OUTPUT THERE?
	PUSHJ P,OUCRLF	;YES, OUTPUT A CRLF AS SEPARATOR
	MOVE T2,[POINT 7,LODSBK]	;OUTPUT FIRST SWITCHES
	PUSHJ P,OUTSW
	MOVSI T2,SIXBIT '   DSK'
	TLNN FL3,LIBSW
	TLNE FL2,RELSW	;USING A REL FILE?
	JRST LODR0	;A SPECIFIED REL FILE
	JRST LODR3	;A GENERAL REL FILE
LODR0:	SKIPN T2,LOKNAM	;ON NON-DISK DEVICE?
	JRST LODR1	;NO
LODR3:	PUSHJ P,OUTSIX	;YES. OUTPUT DEVICE
	MOVEI	T1,":"	;AND A COLON
	PUSHJ	P,(T3)	; ..
LODR1:	MOVE T2,ONAM	;NOW FILE NAME
	PUSHJ P,OUTSIX
	TLNN FL3,LIBSW	;IF LIBRARY
	TLNE FL2,RELSW	;OR REL
	SKIPA
	JRST ELOD
	SKIPN T2,SVEXT	;ALSO USE EXT IF GIVEN
	JRST LODR2
	MOVEI T1,"."
	PUSHJ P,OUTLOD
	PUSHJ P,OUTSIX
LODR2:	SKIPE T2,SVPPN	;THEN THINK ABOUT PPN
	PUSHJ P,OUTPPN
	TLNN FL3,LIBSW	;LIBRARY?
	JRST ELOD
	MOVSI T2,SIXBIT '   /L '	;TELL LOADER
	PUSHJ P,OUTSIX
ELOD:	MOVE T2,[POINT 7,LODSB2]	;OUTPUT SECOND SET OF SWITCHES
	PUSHJ P,OUTSW
	TLZN FL3,MAP2	;DO A MAP?
	JRST ELOD2	;NO
	MOVSI T2,SIXBIT '   /M '
	PUSHJ P,OUTSIX
	TLZ FL,MAP2	;TURN OFF PERM FLAG
ELOD2:	TRO FL,LODOUT	;MARK AS HAVING OUTPUT THERE
	JRST NXFILP
NOCOM1:	SKIPN SVRPP	;DID WE FIND THE REL FILE SOMEWHERE ELSE?
	JRST NOCOMP	;NO
	SETZM SVEXT	;ZERO SOURCE EXTENSION
	TLO FL2,RELSW	;AND PRETEND HE SAID .REL
	JRST NOCOMP

GETPRO:	MOVEI T3,NPROCS+1	;SET UP LOOK OF EXTENSION POINTER
NFIL:	MOVE T2,SVNAM(T1)	;SET UP NAME AND PPN
	MOVEM T2,LNAM
	MOVE T2,SVEXT(T1)
NXEXT:	MOVEM T2,LEXT	;START WITH ORIGINAL EXT
	MOVEM T2,OLDEXT	;SAVE FOR RAS SYSTEM
	MOVE T2,SVPPN(T1)
	MOVEM T2,LPPN
	SKIPN T2,SVDEV(T1)	;A DEVICE?
	SKIPE T2,LOKNAM	;OR SAVING ONE UP
	JRST ALTDEV
OKLOOK:	LOOKUP LOOK,LNAM
	JRST NOTYET	;HAVE NOT FOUND IT YET
DNLOK:	HLLZ T2,LEXT	;GET THE EXTENSION
	CAME T2,OLDEXT	;WAS IT WHAT WE ASKED FOR
	JRST NOTYET	;TREAT AS IF LOOKUP FAILED
	MOVEM T2,SVEXT(T1)	;SAVE EXT (WILL HELP <> CODE)
	LDB T2,[POINT 12,LDAT,35]	;GET DATE
	CAMLE T2,SDAT	;AND CHECK TO SEE IF LATEST
	JRST SETDT
	CAME T2,SDAT
	JRST OLDAT
	LDB T2,[POINT 11,LDAT,23]
	CAMLE T2,STIM
SETTM:	MOVEM T2,STIM	;MARK WITH LATER ONE
OLDAT:	HLLZ T2,LEXT	;GET THE EXTENSION WE FOUND
	JUMPE T2,SETCP	;SET TO CURRENT PROCESSOR
	MOVSI T3,-<NPROCS+1>	;LOOK AT EXTENSION TO FIND PROCESSOR
	CAME T2,PXTAB(T3)
	AOBJN T3,.-1
	JUMPGE T3,SETCP	;NOT THERE
	TLNE FL2,@ISPTAB(T3)	;IS THAT ONE ALREADY SET?
	JRST NFIL2
	TLNE FL2,ALPROC	;IS ANY SET?
	JRST PROCON	;YES, WE HAVE A CONFLICT
	TLO FL2,@ISPTAB(T3)	;SET UP FOR THIS ONE
NFIL2:	CAME T1,SVPT	;ARE WE DONE?
NFIL1:	AOBJN T1,GETPRO	;NO, GO ON
	POPJ P,
SETDT:	MOVEM T2,SDAT
	LDB T2,[POINT 11,LDAT,23]	;AND TIME
	JRST SETTM

SETCP:	CAME T1,SVPT	;AT END?
	JRST NFIL1	;NO, DO NOT SET
	TLNN FL2,ALPROC	;SOMETHING ALREDY SET?
	HLL FL2,FL3	;NO, SET TO CURRENT PROCESSOR
	POPJ P,		;AND DONE

NOTYET:	MOVE T2,SVEXT(T1)	;GET THE CURRENT EXT
	JUMPN T2,NOFIL	;IF HE SPECIFIED AN EXT WE LOSE
	MOVE T2,PXTAB-1(T3)	;ELSE PICK UP ONE
	SOJGE T3,NXEXT
	MOVE T2,SVEXT(T1)	;GET THE ORIGINAL EXT
	MOVEM T2,LEXT
	JRST NOFIL	;ARE OUT OF THINGS TO TRY
ALTDEV:	MOVEM T2,LOKNAM	;SAVE FOR LATER
	MOVEM T2,SVDEV(T1)	;AND IN DEVICE FOR OUTPUT
	CALLI T2,4	;GET CHARACTERISTICS
	TLNE T2,200000	;A DSK?
	JRST	[SETZM LOKNAM	;NO MORE NEED
		JRST OKLOOK]
	TRO FL,NODAT	;NO DATES ON OTHER DEVICES
	TLNN T2,4	;A DECTAPE?
	JRST OLDAT	;DON'T BOTHER WITH LOOKUP
	TRZ FL,NOLOOK	;NOT FAILED YET
	OPEN LOOK,LOKINT	;OPEN FOR INPUT
	JRST DEVNA	;NOT THERE
	LOOKUP LOOK,LNAM	;SEE IF FILE IS
	TRO FL,NOLOOK	;NO
	OPEN LOOK,DSKLK	;GET THE DSK BACK
	JRST DSKNA	;I HOPE THIS NEVER HAPPENES
	TRZE FL,NOLOOK	;SEE IF FAILED
	JRST NOTYET	;IT DID
	JRST DNLOK	;LOOKUP HAPPENED

IFE STANSW!BBNSW,<
OUTPPN:	HRRZM T2,SAVPPN	;CONVERT TO SIXBIT FOR OUTPUT
	MOVEI T1,"["	;START OUT
	PUSHJ P,(T3)
	HLRZ T1,T2	;GET NUMBER
	PUSHJ P,OUTOCT
	MOVEI T1,","
	PUSHJ P,(T3)
	HRRZ T1,SAVPPN
	PUSHJ P,OUTOCT
	MOVEI T1,"]"
	JRST (T3)

OUTOCT:	IDIVI T1,10	;OCTAL OUTPUT
	HRLM T2,(P)
	SKIPE T1
	PUSHJ P,OUTOCT
	HLRZ T1,(P)
	ADDI T1,"0"
	JRST (T3)
>
DEFINE X (A,B,C,D,E)
<SIXBIT /B/>

PXTAB:	SIXBIT /REL/
	PROCESS

DEFINE X (A,B,C,D,E)
<B'SW>

ISPTAB:	RELSW
	PROCESS

OUTSIX:	MOVEI T1,0
	LSHC T1,6
	ADDI T1,40
	PUSHJ P,(T3)
	JUMPN T2,OUTSIX
CPOPJ:	POPJ P,

IFN STANSW,<
OUTPPN:	MOVEM T1,SAVPPN	;SAVE IT AWAY
	ANDCMI T2,-1
	MOVEI T1,"["
	PUSHJ P,(T3)
	PUSHJ P,OUTSIX	;PRINT IT
	MOVEI T1,","	;AND A COMMA
	PUSHJ P,(T3)
	HRLZ T2,SVPPN
	PUSHJ P,OUTSIX
	MOVEI T1,"]"
	JRST (T3)
>

IFN BBNSW,<
OUTPPN:	MOVEI	T1,"["
	PUSHJ	P,(T3)
	CALL	T2,[SIXBIT .UNSQZE.]
	PUSHJ	P,OUTSIX
	MOVEI	T1,"]"
	JRST	(T3)
>
OUTSW:	MOVEM T2,SVSWP	;SAVE THE POINTER
	ILDB T1,T2	;PICK UP THE FIRST CHR
	JUMPE T1,CPOPJ	;AND CHECK FOR NULL AS A PRECAUTION
	MOVEI T1,"("
	PUSHJ P,(T3)	;SWITCHES ARE IN () TO PROCESSOR
OUTSW1:	ILDB T1,SVSWP
	JUMPE T1,LPAR
	PUSHJ P,(T3)
	JRST OUTSW1	;A NULL WILL MARK THE END
LPAR:	MOVEI T1,")"
	JRST (T3)
ENTCRF:	MOVE T1,CORTOP	;CHECK TO SEE IF NAME ALREADY THERE
	MOVE T2,ONAM
ENTC1:	CAMN T1,CORT1
	JRST ENTC2
	CAMN T2,1(T1)
	POPJ P,	;NAME THERE, EXIT
	AOJA T1,ENTC1	;CHECK ANOTHERE
ENTC2:	MOVEM T2,@CORTOP	;SAVE IT
	SOS T1,CORTOP
	CAMG T1,SVJFF	;CHECK TO SEE IF CORE EXCEEDED
	PUSHJ	P,XPAND
IFN BBNSW,<
	MOVEI	T3,OUTCRF
	MOVE	T2,[SIXBIT ?LPT:?]
	PUSHJ	P,OUTSIX
	MOVE	T2,ONAM
	PUSHJ	P,OUTSIX
	MOVE	T2,[SIXBIT ?.LST_?]
	PUSHJ	P,OUTSIX
	MOVE	T2,ONAM
	PUSHJ	P,OUTSIX
	JRST	OUCRLF
>
IFE BBNSW,<
	MOVEI T1,"_"
	PUSHJ P,OUTCRF
	MOVE T2,ONAM
	MOVEI T3,OUTCRF
	PUSHJ P,OUTSIX
	JRST	OUCRLF
>
FINCRF:	MOVSI IOPNT,-2	;PERMIT ONLY THIS ONE LEVEL
	TRO FL,INCRF	;SAY WE ARE FINISHING
	MOVEM P,SVPDL	;SAVE THE PDL FOR LATER
	MOVE T1,[POINT 7,CRFRDR]
	MOVEM T1,DINPT
FINC1:	PUSHJ P,SCAN	;GET SOMETHING
IFN BBNSW,<CAIN C,"."	;IGNORE ALL IDENTIFIERS EXCEPT NAME
				;IN "LPT:NAME.LST_NAME">
	TRNN FL,IDF	;IGNORE ALL BUT IDENTIFIERS
	JRST FINC1
	MOVE T1,ACCUM
	MOVEM T1,ONAM	;SET AS NAME
	PUSHJ P,ENTCRF	;ENTER IT
	JRST FINC1
DNCRF:	RELEASE CHNCRF,0	;LET GO OF THE CHANNEL
	MOVE P,SVPDL	;GET THE ENTERING PDL BACK
	TRZ FL,INCRF	;NO LONGER THERE
	POPJ P,
	DEFINE X (A,B,C,D,E)
<OUT'B>
PRCTAB:	PROCESS
	OUTLOD

	DEFINE X (A,B,C,D,E)<
IFNB <E>,<SIXBIT /E/>>
INTEXT:	PROCESS

	DEFINE X (A,B,C,D,E)
	<D'SW>
NXPC:	PROCESS

SW==0
DODIR:	SETZM SVDEV		;NO DEVICE YET
	SETZB	SVPT,SVPPN
DODIR1:	PUSHJ P,SCAN	;FIND OUT IF HE WANTS /L OR /F SWITCH
DODIR2:	TRNE FL,IDF	;WAS IT AN IDENT?
	JRST DEVNAM	;CHECK FOR NAME
	CAIN	C,"["
	JRST	DODIP
	CAIE C,"/"
	JRST NOSLSH
	PUSHJ P,SCAN	;WHICH ONE
	TRNN FL,IDF	;MUST SEEN AN IDENTIFIER
	JRST SYNERR
	MOVS T1,ACCUM
	CAIN T1,SIXBIT '   F  '
	JRST SETF
	CAIE T1,SIXBIT '   L  '
	JRST SYNERR	;DO NOT RECOGNIZE THIS SWITCH
	TROE FL,LPTFG	;HE WANTS IT ON THE LINE PRINTER
	JRST SYNERR	;SECOND TIME IS LOSAGE
	JRST DODIR1	;BACK FOR MORE
DEVNAM:	SKIPE SVDEV	;SECOND TIME?
	JRST SYNERR	;LOSE
	MOVE T1,ACCUM
	MOVEM T1,SVDEV
	PUSHJ P,SCAN
	CAIN C,":"	;IGNORE A COLON ON DEVICE
	PUSHJ P,SCAN	;BY SCANNING AGAIN
	JRST DODIR2	;AND SEE IF MORE
SETF:	TROE FL,FFLG
	JRST SYNERR	;HE WANTS A SHORT FORM BUT HE SAID IT TWICE
	JRST DODIR1
NOSLSH:	TLNN CS,TERMF	;THIS MUST BE A TERMINATOR
	JRST SYNERR	;NOPE, HE LOSES
	MOVE T2,[SIXBIT !TTY:_!]
	TRNE FL,LPTFG	;ON LINE PRINTER INSTEAD?
	HRLI T2,SIXBIT '   LPT'	;YES
	MOVEI T3,OUTPIP
	PUSHJ P,OUTSIX
	SKIPN T2,SVDEV	;SEE IF DEVICE SPECIFIED
	MOVSI T2,SIXBIT '   DSK'
	PUSHJ P,OUTSIX
	MOVSI T2,SIXBIT '   :/L'
	TRNE FL,FFLG
	MOVSI T2,SIXBIT '   :/F'
	PUSHJ P,OUTSIX
	SKIPE	T2,SVPPN
	PUSHJ	P,OUTPPN
OPIP1:	PUSHJ	P,OUCRLF
OPIP2:	RELEASE CHNPIP,0
	MOVE	T1,[SIXBIT /SPIP/]
	MOVEM T1,PCNAM	;LOAD THIS ONE
	JRST DONE1
DODIP:	PUSHJ	P,GETPP1
	JRST	DODIR1
DODEL:	TRO FL,PIPF	;SET TO ALLOW * AS AN IDENT
	PUSHJ P,SCAN
	PUSHJ P,GETNAM
	MOVSI T1,SIXBIT '   DSK'
	SKIPN SVDEV	;FORCE TO DSK IF NONE
	MOVEM T1,SVDEV
	JRST DEL2
DEL3:	PUSHJ P,SCAN
	PUSHJ P,GETNAM
DEL2:	MOVEI T3,OUTPIP
	SKIPN T2,SVDEV	;DEVICE?
	JRST NODVC
	PUSHJ	P,OUCRLF
	PUSHJ P,OUTSIX	;DUMP NAME
	MOVE T2,[SIXBIT !:/D_!]
	PUSHJ P,OUTSIX
	JRST DIDDEV
NODVC:	MOVEI T1,","	;IF NO DEV, JUST A ,
	PUSHJ P,OUTPIP
DIDDEV:	SKIPE SVPPN	;DO NOT LET HIM TOUCH OTHERS
	JRST SYNERR
	PUSHJ P,OUTNAM	;WRITE THE NAME
	PUSHJ P,SCAN	;SEE IF MORE THERE
	CAIN C,","
	JRST DEL3	;GO ON
	TLNN CS,TERMF	;MAKE SURE THAT LINE ENDS PROPERLY
	JRST SYNERR
	JRST OPIP1	;COMMON END ROUTINE
DOREN:	TRO FL,PIPF	;PERMIT * IN FILES
	MOVSI T2,SIXBIT '   DSK'
	MOVEM T2,LOKNAM	;SALT NAME TABLE
NXTNAM:	PUSHJ P,SCAN	;GET A FILE NAME
	PUSHJ P,GETNAM
	MOVEI T3,OUTPIP
	SKIPN T2,SVDEV	;SEE IF DEVICE SPECIFIED
	MOVE T2,LOKNAM	;OR SAVED
	MOVEM T2,LOKNAM
	PUSHJ P,OUTSIX	;PUT IT OUT
	MOVEI T1,":"
	PUSHJ P,OUTPIP
	SKIPE SVPPN	;PROJECT-PROGRAMMER NUMBER ILLEGAL
	JRST SYNERR
	PUSHJ P,OUTNAM
	MOVSI T2,SIXBIT '   /R_'
	PUSHJ P,OUTSIX
	PUSHJ P,SCAN
	CAIE C,"="	;= MUST BE THERE
	JRST SYNERR
	PUSHJ P,SCAN
	PUSHJ P,GETNAM
	SKIPN SVDEV	;SEEEIF DEVICE OR PPN
	SKIPE SVPPN
	JRST SYNERR	;EITHER IS WRONG
	MOVEI T3,OUTPIP	;RSET
	PUSHJ P,OUTNAM
	PUSHJ	P,OUCRLF
	PUSHJ P,SCAN	;CHECK FOR MORE
	CAIN C,","
	JRST NXTNAM	;YES
	TLNN CS,TERMF	;NO MORE, SEE IF END
	JRST SYNERR
	JRST OPIP2
DOEDIT:	PUSHJ P,SCAN	;START ON THE FILE NAME
DOEDT1:	PUSHJ P,GETNAM
	SKIPE SVDEV	;DEVICE IS ILLEGAL
	JRST SYNERR
	TLNN CS,TERMF	;SHOULD END
	JRST SYNERR
	MOVEI	T3,OUTEDT
	MOVEI	T1,"S"		;COMMAND FOR DREDIT
	PUSHJ	P,OUTEDT	;OUTPUT THE S
	SKIPE	SVPPN		;CANT EDIT SOMEONE ELSES FILES
	JRST	SYNERR
	PUSHJ	P,OUTNAM	;OUTPUT THE NAME & EXT
	TRNE	FL,CREATF	;EDIT OR CREATE?
	JRST	DOEDT3		;CREATE (OR MAKE)
	PUSHJ	P,OUCRLF	;EDIT (OR TECO) - OUTPUT CRLF
DOEDT2:	RELEAS	CHNEDT,0	;END OF COMMAND
	MOVE T1,[SIXBIT /LINED/]
	TRNE FL,TECOF
	JRST	ISTECO		;TECO OR MAKE COMMAND
ENDED:	MOVEM T1,PCNAM
	JRST DONE	;GO GET IT LOADED

DOEDT3:	MOVEI	T1,175		;OLD ALTMODE
	PUSHJ	P,OUTEDT	;ENDS CREATE OR MAKE COMMAND
	JRST	DOEDT2

ISTECO:	MOVE	14,SVNAM	;EDITING THIS PROGRAM
	TRNE	FL,CREATF	;CHECK FOR MAKE COMMAND
	CAME	14,[SIXBIT /LOVE/]	;WITH ARGUMENT OF LOVE
	JRST	ISTEC1		;NO SUCH HACK
	MOVEI	T2,2		;YES. PAUSE THOUGHTFULLY
	CALLI	T2,31		;BY SLEEPING
	MOVEI	T1,[ASCIZ /NOT WAR?
/]
	STRING		;DDTOUT OR FACSIMILE
ISTEC1:	MOVE	T1,[SIXBIT /TECO/]	;NAME OF CUSP
	JRST	ENDED

;NOTE: LEAVE THE ABOVE HACK IN FOR SALES DEMOS
IFE LSTRSW,<
OUTLST=OUTPIP		;USE PIP FOR A LISTER
CHNLST=CHNPIP>

TYPR:	SKIPA T2,[SIXBIT /TTY:_/]
LISTR:	MOVE T2,[SIXBIT /LPT:_/]
	MOVEI T3,OUTLST
	PUSHJ P,OUTSIX
	MOVSI T2,(SIXBIT /DSK/)
	MOVEM T2,LOKNAM
IFE LSTRSW,<	TRO	FL,PIPF>	;IF IT'S PIP, ALLOW *.MAC, ETC.
LSTLP:	PUSHJ P,SCAN	;GET NAME
	PUSHJ P,GETNAM
	SKIPN T2,SVDEV
	MOVE T2,LOKNAM	;USE PREV NAME IF NO NEW NAME
	PUSHJ P,OUTSIX	;OUTPIT IT
	MOVEI T1,":"	;COLON AFTER DEV
	PUSHJ P,OUTLST
	PUSHJ P,OUTNAM	;FILE NAME
	PUSHJ P,SCAN
IFN LSTRSW,<
	CAIE C,"("	;PAGE SPEC?
	JRST ENDLST	;NO
	MOVEI T1,"("	;OUTPUT THE ( TO FILE
	PUSHJ P,OUTLST
LST1:	PUSHJ P,GETCH	;COPY PAGE SPEC
	MOVE T1,C	;TO OUTPUT AC
	PUSHJ P,OUTLST	;THENCE TO FILE
	CAIE C,")"	;THROUGH END OF ARG
	JRST LST1	;MORE
	PUSHJ P,SCAN	;NOW WHAT?
>
ENDLST:	CAIN C,","	;SHOULD BE COMMA OR CR
	JRST [MOVEI T1,","
		PUSHJ P,OUTLST
		JRST LSTLP]
	TLNN CS,TERMF	;SHOULD BE TERMINATOR
	JRST SYNERR	;WASNT
	PUSHJ	P,OUCRLF	;ADD CRLF TO COMMAND
	RELEASE CHNLST,0
	MOVE T1,LSTCSP	;CUSP NAME
	MOVEM T1,PCNAM
	JRST DONE1

LSTCSP:	IFE LSTRSW,<SIXBIT /PIP/>
	IFN LSTRSW,<SIXBIT /LISTER/>
OUTNAM:	MOVE T2,SVNAM
	PUSHJ P,OUTSIX
	SKIPN T2,SVEXT
	JRST OUTPP
	MOVEI T1,"."
	PUSHJ P,(T3)
	PUSHJ P,OUTSIX
OUTPP:	SKIPN T2,SVPPN
	POPJ P,
	JRST OUTPPN

OUCRLF:	MOVEI	T1,15	;CARRIAGE RETURN
	PUSHJ	P,0(T3)		;TO CURRENT OUTPUT FILE
	MOVEI	T1,12		;LINE FEED
	JRST	0(T3)		;TO OUTPUT FILE
;
;
;
;       CODE TO CALL IN VARIOUS CUSPS FOR THE USER...
;
;
BASCUS:	MOVE	T1,[SIXBIT/BASIC/]
	JRST	SETRUN
PIPCUS:	MOVE	T1,[SIXBIT/PIP/]
	JRST	SETRUN
MACCUS:	MOVE	T1,[SIXBIT/MACRO/]
	JRST	SETRUN
EDICUS:	MOVE	T1,[SIXBIT/EDITOR/]
	JRST	SETRUN
LINCUS:	MOVE	T1,[SIXBIT/LINED/]
	JRST	SETRUN
TELE:	MOVE	T1,[SIXBIT/TTY/]
;
;
;		CODE TO SET UP RUN BLOCK FOR RUN UUO...
;
SETRUN:	MOVEM	T1,NAME
	SETZM	NAME+1
	SETZM	NAME+2
	SETZM	NAME+3
	SETZM	NAME+4
	SETZM	T1
	CALLI
	JRST	NUNDOT		;START THE CUSP
;
;		CODE TO REWIND A MOAGTAPE TO LOAD POINT......
;
;
REWIND:	PUSHJ	P,SCAN		;GET MAGTPE DEV TO REWIND
	MOVEI	T3,OUTLST	;PUT IN PIPTMP
	MOVE	T2,ACCUM
	PUSHJ	P,OUTSIX
	MOVE	T2,RWCODE	;SET UP COMMAND
	PUSHJ	P,OUTSIX	;PUT IT IN PIP.TMP
	;
	PUSHJ	P,SCAN		;GET EXPECTED TERMINATOR
	TLNN	CS,TERMF	;IS IT A CR
	JRST	SYNERR		;TO BAD IT WASNT..YOU LOSE
	PUSHJ	P,OUCRLF	;PUT A CR IN PIP.TMP
	RELEASE	CHNLST,0
	MOVE	T1,LSTCSP
	MOVEM	T1,PCNAM	;SET UP TO CALL CUSP
	JRST	DONE1		;MOVE OUT..
RWCODE:	SIXBIT	/:(MW)_/
;
;
;
;***   CODE TO TRANSFER SOURCE OR REL FILES BETWEEN DEVICES.
;
;         COMMAND FORMAT:
;
;         TRANSFER DESTINATION-DEV:FILE.EXT_SOURSE-DEV:FILE.EXT
;
;         DEFAULT DEVICE IS DISK  'DSK'
;
;
TRANSF:	MOVSI	T2,(SIXBIT /DSK/)	;LOAD DEFAULT DEV NAME
	MOVEM	T2,LOKNAM
TRNSF1:	TRO	FL,PIPF			;ALLOW FOR *.MAC  IN PIP MODE
	MOVEI	T3,OUTLST		;SET RETURN ADDRESS FOR OUTSIX CALL
	PUSHJ	P,SCAN			;GET FIRST PARAMMETER
	PUSHJ	P,GETNAM		;EVALUATE FIRT ARGUMENT
	SKIPN	T2,SVDEV		;SPECIFIED DEV NAME
	MOVE	T2,LOKNAM		;NEW ONE..SO LOAD IT
	MOVEM	T2,LOKNAM		;LOAD DEFAULT DEVICE NAME
	CAMN	T2,[SIXBIT /TTY/]	;TRANSFER COMMAND DOES NOT ALLOW
	JRST	DEVERR			;    TTY  OR  LPT
	CAMN	T2,[SIXBIT /LPT/]
	JRST	DEVER1			; TRY EITHER ONE AND YOU LOSE BIG
	PUSHJ	P,OUTSIX		;USE DEFAULT DEV NAME..OUTPUT IT
	MOVEI	T1,":"			;COLON AFTER DEV NAME
	PUSHJ	P,OUTLST		;OUTPUT IT
	PUSHJ	P,OUTNAM		;OUTPUT FILE NAME SPECIFIED
	CAIN	C,"_"			;IS THIS END OF PASS ONE FOR COMMAND
	JRST	[ MOVSI  T2,(SIXBIT /_/)
			SKIPN	SVNAM
			MOVSI	T2,(SIXBIT '/X_')
			PUSHJ P,OUTSIX
			SETZM SAVCHR
			JRST  TRANSF]
	MOVSI	T1,(SIXBIT/CDR/)
	CAMN	T1,SVDEV		;WHEN DESTINATION DEV IS CDR
	JRST	MTRAN			; ASSUM ASCII MODE OF TRANSIT
	MOVEI	T1,"/"		;OTHERWISE PUT IN /B TO HELP TRANSFER IN CASE REL FILE
	PUSHJ	P,OUTLST
	MOVSI	T2,(SIXBIT/B/)
	PUSHJ	P,OUTSIX
MTRAN:	CAIN	C,","		;MORE FILES TO GO?
	JRST 	[  MOVEI	T1,","
			PUSHJ	P,OUTLST
			PUSHJ	P,SCAN
			JRST	TRNSF1  ]
	JRST	ENDLST+4		;LAST PASS...SO FINISH UP
;
;
;
;	****CO;
;
UNPROT:	MOVSI	T4,(SIXBIT /055/)	;ASSIGN STANDARD PROTECTION
	JRST	.+2
PROTEC:	MOVSI	T4,(SIXBIT /477/)	;SET STANDARD PROTECTION
	TRO	FL,PIPF			;ALLOW FOR * IN PIP MODE
	MOVEI	T3,OUTLST		;SET UP PIP OUTPUT ADDRESS
	PUSHJ	P,SCAN			;GET FIRST ARGUMENT
	PUSHJ	P,GETNAM		;SAVE PARAMS IN APPROPRIATE CELLS
	TLNE	CS,TERMF		;IF LAST SCAN IS A TERMINATOR
	JRST	OUTPRO			;FINISH UP
	CAMN	T4,[SIXBIT /055/]	;IF YOU GET THIS FAR WITH THE UNPROTECT
	JRST	SYNERR			;COMMAND  YOU GOOFED. 
	PUSHJ	P,SCAN			;OTHERWISE CHECK FOR PROTECTION
	PUSHJ	P,SCAN			;SPECIFIED BY USER
	SETZM	T2
	MOVE	T1,ACCUM
	ROTC	T1,6			;ISOLATE FIRST CHAR OF PARAMETER
	CAIN	T2,62			;IS IT READ PROTECTION HE WANTS
	JRST	[MOVSI	T4,(SIXBIT /467/)
		 JRST	OUTPRO]
	CAIE	T2,67			;NO...PROBABLY WRITE PROTECTION
	JRST	SYNERR			;IF NOT ,HE DOESNT KNOW WHAT HE WANTS
	MOVSI	T4,(SIXBIT /457/)	;SET UP FOR WRITE PROTECT
OUTPRO:	MOVE	T2,PROCD1
	PUSHJ	P,OUTSIX		;START TO FILL UP PIP TMP FILE
	MOVE	T2,T4			;SET THE PROTECTION VALUE
	PUSHJ	P,OUTSIX
	MOVE	T2,PROCD2
	PUSHJ	P,OUTSIX
	MOVE	T2,PROCD3
	PUSHJ	P,OUTSIX
	PUSHJ	P,OUTNAM
	JRST	ENDLST+4		;THATS ALL FOLKS..
;
PROCD1:	SIXBIT /DSK:</
PROCD2:	SIXBIT '>/XR_D'
PROCD3:	SIXBIT /SK:/
;
;
;	****CODE TO UNLOCK A USER WRITE PROTECTED FILE***
;
;
LOCK:	MOVSI	T4,500000		;SET WRITE LOCK AGAINST SELF
	JRST	.+2
UNLOCK:	MOVSI	T4,400000		;LET YOURSELF WRITE OVER FILE IF YOU LIKE
	TRO	FL,PIPF			;INIT PIP MODE FLAG FOR *
	PUSHJ	P,SCAN			;SCAN FIRST ARGUMENT
	PUSHJ	P,GETNAM		;SET UP APPROPRIATE CELLS
	TLNN	CS,TERMF		;BETTER BE A TERMINATOR
	JRST	CMDERR			;IF NOT YOUR IN TROUBLE
	MOVE	T1,SVNAM		;GET FILE NAME
	MOVEM	T1,NAME
	MOVE	T1,SVEXT		;GET EXT IF THERE IS ONE
	MOVEM	T1,NAME+1
	INIT	LOOK,0			;INITIALIZE FOR CHANGING
	SIXBIT	/DSK/
	Z
	JRST	RNERR			;IF CANT DO IT LET USER KNOW
	LOOKUP	LOOK,NAME
	JRST	LKERR
	MOVE	T1,NAME+2
	TLZ	T1,700000		;CLEAR OUT USER PREVIOUS PROTECTION
	TDO	T1,T4		;SET NEW TYPE PROTECTION
	MOVEM	T1,NAME+2
	SETZM	NAME+3
	RENAME	LOOK,NAME		;RESET PROTECTION
	JRST	RNERR		;DIDNT DO IT LET USER KNOW
	RELEASE LOOK,		;RELEASE CHANNEL
	CALLI	1,12			;GO AWAY
;
;MESSAGES FOR UNLOCK ERRORS**
;
LKERR:	TTCALL	3,[ASCIZ  /NO SUCH FILE/]
	CALLI	1,12
RNERR:	TTCALL	3,[ASCIZ  /FILE ALREADY LOCKED/]
	CALLI	1,12
CMDERR:	TTCALL	3,[ASCIZ /TOO MANY ARGUMENTS/]
	CALLI	1,12
;
;
;HERE IS THE SET OF OUTPUT ROUTINES

DEFINE X (A,B,C,D,E)<
OUT'B:	TRON FL2,FCH'B	;IS IT INITTED
	JRST B'INITCH
B'DNINIT:	SOSLE B'OBUF+2
	JRST B'OU1	;SOME LEFT
	OUT CHN'B,0
	SKIPA
	JRST OUTER	;OUTPUT ERROR
B'OU1:	IDPB T1,B'OBUF+1
	POPJ P,
B'INITCH:	INIT CHN'B,1
	SIXBIT /DSK/
	XWD B'OBUF,0
	CALLI 12
	PUSHJ P,CHKRM
	OUTBUF CHN'B,2	;SET UP BUFFERS
	PUSH P,T1	;SAVE IT
	HLLZ T1,JOBNAM	;JOB NUMBER IN SIXBIT
	HRRI T1,(<SIXBIT /A/>&777777000000)	;AND CUSP NAME
	MOVEM T1,NAME
	MOVSI T1,SIXBIT /   TMP/
	MOVEM T1,NAME+1
	SETZM NAME+2
	SETZM NAME+3
	ENTER CHN'B,NAME
	JRST FIU	;LOSE BIG
	POP P,T1
	JRST B'DNINIT	;WE HAVE INITED IT

U (B'OBUF,3)

>
PROCESS
;ALSO THESE
X LOADER,LOD
IFN LSTRSW,<X LIST,LST>
X PIP,PIP
X CREF,CRF
X EDT,EDT
;DATA STORAGE ASSIGNMENTS

PDP:	IOWD	PDL,PDLB		;INITIAL PDL POINTER

WORDS <PCNAM,SAVPPN,SVSWP,PCNUM,OLDEXT,SDAT,STIM,SAVCHR>
WORDS <ACCUM,DINPT,DINCT,SVJFF,CORTOP,CORT1,SVRPP,NUMAT>
WORDS <SVIND,SVPDL,JOBNAM,BROCNT,LODSP2,LODCT2,LODSP,LODCTR>

U (LODSBK,<LODSCT/5+1>)
U (LODSB2,<LODSCT/5+1>)

WORDS <SWBKB,SWBKL>

U (SWBKS,NFILE)
U (ONAM,1)

U (SVDEV,NFILE)
U (SVNAM,NFILE)
U (SVEXT,NFILE)
U (SVPPN,NFILE)

U (DDTBUF,^D30)
U (LOOKBF,3)
U (PDLB,PDL+1)
U (SWBLK,SWBK+1)
U (OPENB,3)
U (IOPD,<<NESTDP+2>*3+1>)
U ERRBUF,22		;FOR TYPEOUTS
U BUFTAB,<NESTDP+2>		;WHERE THE BUFFERS ARE FOR FILES
U FREBUF,<NESTDP+2>		;FREED BUFFERS
IFE PURESW,<

;THIS IS THE PART THAT MUST BE INITIALIZED
;IF PURE, JUST USED AS IS IF IMPURE

RUNBLK:	EXP SYSDEV
NAME: LNAM:	0
LEXT:	0
LDAT:	0
LPPN:	0
	0		;FOR RUN UUO CORE ARG
LOKINT:	1
LOKNAM:	0
	XWD LOOKBF,LOOKBF
DSKLK:	1
	SIXBIT /DSK/
	XWD LOOKBF,LOOKBF

FCOMD:	ASCII /@***SVC.TMP
/
	BYTE (7) 177,177	;MARK EOF

FCOMD2:	ASCII /@***EDS.TMP
/
	BYTE (7) 177,177

CRFRDR:	ASCII /@***CRE.TMP/
	BYTE (7) 177,177
>

IFN PURESW,<

;THIS IS THE ASSIGNMENT OF SPACE FOR THE INITIALIZED DATA
INILOW=.ZZ		;WHERE IT GOES IN LOW PAGE

WORDS <RUNBLK,NAME,LEXT,LDAT,LPPN,ZZZ>

LNAM=NAME
WORDS <LOKINT,LOKNAM,ZZZ>
U (DSKLK,3)
U (FCOMD,4)
U (FCOMD2,4)
U (CRFRDR,4)

INILEN=.ZZ-INILOW

;AND HERE IS THE DATA, WHICH MUST MATCH THE STORAGE
;ASSIGNMENT ABOVE

INIDAT:
;RUNBLK:
	EXP SYSDEV
	0
	0
	0
	0
	0		;CORE ARG FOR RUN

;LOKINT:
	1
	0
	XWD LOOKBF,LOOKBF

;DSKLK:
	1
	SIXBIT /DSK/
	XWD LOOKBF,LOOKBF

;FCOMD:
	ASCII /@***SVC.TMP
/
	BYTE (7) 177,177

;FCOMD2:
	ASCII /@***EDS.TMP
/
	BYTE (7) 177,177

;CRFRDR:
	ASCII /@***CRE.TMP/

	BYTE (7) 177,177

IFN <.-INIDAT-INILEN>,<PRINTX <STORAGE ERROR>>
>
	LOWTOP=.ZZ
	XLIST	;THE LITERALS
	LIT
	LIST	;THE LITERALS

CMPEND:	END STPT
                                                                                                                                                                                                                                                                                c3Eq