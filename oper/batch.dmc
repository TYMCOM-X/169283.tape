OPEN BATCH.

TYPE SYSDATE SYSTIME FMT / "BATCH DETAIL - VERSION 8" 5X D3 2X A5 END.

DEFINE INTEGER MTACHG LSTREQ NUMCHAR LSTJOB ERRFLG JOBNUM.
DEFINE TEXT 2 SNFLG.

        DPL START.

TYPE FMT / "BEGINNING STANDARD BATCH REPORT" / END.

        FIND BILFLAG EQ 1 STDNON EQ S.
        INIT 1 BATRPT.STD.

        LET SNFLG EQ "-S".

        CALL REPORT.

        IF ERRFLG EQ 1 THEN ABORT.

        TYPE TOT TOTCHRG FMT /
"STANDARD BATCH TOTAL: " M-2P$,$$$,$$9.99 //
"STANDARD BATCH REPORT COMPLETED - BATRPT.STD" END.

        TYPE FMT /
"BEGINNING NON-STANDARD BATCH REPORT" / END.

        FIND BILFLAG EQ 1 STDNON EQ N.
        INIT 1 BATRPT.NON.

        LET SNFLG EQ "-N".

        CALL REPORT.

        IF ERRFLG EQ 1 THEN ABORT.

        TYPE TOT TOTCHRG FMT /
"NON-STANDARD BATCH TOTAL: " M-2P$,$$$,$$9.99 //
"NON-STANDARD BATCH REPORT COMPLETED - BATRPT.NON" END.

        TYPE FMT /
"CALCULATING TOTAL BATCH CHARGES" / END.

        FIND BILFLAG EQ 1.

        TYPE TOT TOTCHRG FMT /
M-2P$,$$$,$$9.99 END.

        GOTO END.

ABORT:  TYPE FMT / "RUN ABORTED" / END.
        CLOSE.
        DPL STOP.

REPORT: SORT BY CUSDIS CUSNUM USRNAM SYSTEM MONTH DAY HOUR MINUTE JOBTYPE.

        REPORT START.

SEC I.

PAGE 45.
BODY 45.

        HEADING PRINT USRNAM EOMDATE CUSNUM SNFLG CUSDIS CUSNAM FMT
"BREAKDOWN OF PERIPHERAL PROCESSING FOR USER " A " AS OF " D1 //
"CUSTOMER NUMBER " I A2 " DISTRICT NUMBER " I " - " A //
"REQ.NO. MM/DD HH:MM PROJECT CODE JOB VOLUME COPIES MTA CHRG NO.CHARS "
"DEL.CHRG TOTAL CHRG COM.DATE SYS" / END.

SEC G.

        GETREC TOTAL.
        LET JOBNUM EQ JOBTYPE.
        IF JOBNUM EQ 10 THEN TYP10.
        IF JOBNUM EQ 11 THEN ERR11.
        GOTO HEAD.

TYP10:  LET LSTREQ EQ REQNUM.
        LET NUMCHAR EQ JOBCHRG.

TYP11:  GETREC EOFERR.
        IF JOBTYPE NE 11 THEN ERR10.
        IF REQNUM NE LSTREQ THEN RECERR.
        LET JOBNUM EQ 10.
        GOTO HEAD.

ERR11:  TYPE FMT / "FOUND JOB TYPE 11 WITHOUT FINDING JOB TYPE 10" END.
        TYPE REQNUM FMT / "REQUEST NUMBER " I END.
        LET ERRFLG EQ 1.
        RETURN.

EOFERR: TYPE FMT / "FOUND EOF WHEN EXPECTING JOB TYPE 11" END.
        TYPE REQNUM FMT / "REQUEST NUMBER " I END.
        LET ERRFLG EQ 1.
        RETURN.

ERR10:  TYPE JOBTYPE FMT / "FOUND JOB TYPE " I " WHEN EXPECTING JOB TYPE 11" END.
        TYPE LSTREQ FMT / "JOB TYPE 10 - REQUEST NUMBER " I END.
        TYPE JOBTYPE REQNUM FMT "JOB TYPE " I " - REQUEST NUMBER " I END.
        LET ERRFLG EQ 1.
        RETURN.

RECERR: TYPE FMT / "JOB TYPE 10 AND 11 CONFLICT" END.
        TYPE LSTREQ FMT / "JOB TYPE 10 - REQUEST NUMBER " I END.
        TYPE REQNUM FMT "JOB TYPE 11 - REQUEST NUMBER " I END.
        LET ERRFLG EQ 1.
        RETURN.

HEAD:   SEC H.

        ON CHANGE CUSNUM PRINT FMT C2 $ END.
        ON CHANGE USRNAM PRINT FMT C2 $ END.

SEC P.

        PRINT REQNUM MONTH DAY HOUR MINUTE PROJCODE JOBNUM FMT
1X I5 2X P99 "/" P99 1X P99 ":" P99 1X A12 2X I2 $ END.

        LET MTACHG EQ 0.
        IF JOBNUM EQ 1 THEN JOB1.
        IF JOBNUM EQ 2 THEN JOB236.
        IF JOBNUM EQ 3 THEN JOB236.
        IF JOBNUM EQ 4 THEN JOB457.
        IF JOBNUM EQ 5 THEN JOB457.
        IF JOBNUM EQ 6 THEN JOB236.
        IF JOBNUM EQ 7 THEN JOB457.
        IF JOBNUM EQ 8 THEN JOB8.
        IF JOBNUM EQ 9 THEN JOB9.
        IF JOBNUM EQ 10 THEN JOB10.

TYPE JOBNUM FMT / "FOUND UNKNOWN JOB TYPE - " I END.
        LET ERRFLG EQ 1.
        RETURN.


JOB1:   PRINT VOLUME JOBCHRG DELCHRG TOTCHRG MO DA YR SYSTEM FMT
1X I6 3X I2 21X M-2P$$$$9.99 1X M-2P$$$$$$9.99 1X 2(P99 "/") P99 1X I3 END.
        GOTO TOTAL.

JOB236: PRINT VOLUME DELCHRG TOTCHRG MO DA YR SYSTEM FMT
1X I6 26X M-2P$$$$9.99 1X M-2P$$$$$$9.99 1X 2(P99 "/") P99 1X I3 END.
        GOTO TOTAL.

JOB457: LET MTACHG EQ JOBCHRG.
        PRINT VOLUME MTACHG DELCHRG TOTCHRG MO DA YR SYSTEM FMT
1X I6 8X P$$$$9.99 10X M-2P$$$$9.99 1X M-2P$$$$$$9.99 1X 2(P99 "/") P99 1X I3 END.
        GOTO TOTAL.

JOB8:   PRINT VOLUME DELCHRG TOTCHRG MO DA YR SYSTEM FMT
24X M2I8 1X M-2P$$$$9.99 1X M-2P$$$$$$9.99 1X 2(P99 "/") P99 1X I3 END.
        GOTO TOTAL.

JOB9:   PRINT VOLUME JOBCHRG DELCHRG TOTCHRG MO DA YR SYSTEM FMT
1X I6 17X I8 1X M-2P$$$$9.99 1X M-2P$$$$$$9.99 1X 2(P99 "/") P99 1X I3 END.
        GOTO TOTAL.

JOB10:  PRINT VOLUME JOBCHRG NUMCHAR DELCHRG TOTCHRG MO DA YR SYSTEM FMT
1X I6 3X I2 12X I8 1X M-2P$$$$9.99 1X M-2P$$$$$$9.99 1X 2(P99 "/")
P99 1X I3 END.

TOTAL:  SEC T.

        ON CHANGE SYSTEM PRINT SYSTEM TOT MTACHG TOT DELCHRG TOT TOTCHRG FMT /
"SYSTEM " I2 " TOTALS:" 34X P$$$$9.99 10X M-2P$$$$9.99 1X M-2P$$$$$$9.99 / END.

        ON CHANGE USRNAM PRINT USRNAM TOT MTACHG TOT DELCHRG TOT TOTCHRG FMT
"**TOTALS FOR USER " A12 21X P$$$$9.99 10X M-2P$$$$9.99 1X M-2P$$$$$$9.99 END.

        ON CHANGE CUSNUM PRINT CUSNUM TOT MTACHG TOT DELCHRG TOT TOTCHRG FMT //
"*****TOTALS FOR CUSTOMER " I5 21X P$$$$9.99 10X M-2P$$$$9.99
1X M-2P$$$$$$9.99 END.

FINISH: SEC F.

        REPORT END.

        RETURN.

END:    DPL END.

        @(UAS)BATRPT
