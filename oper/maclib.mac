;	COPYRIGHT C 1972 BY CORE & CODE, INC
	TITLE	CR4ZZZ
;SET UP ACCUMULATOR REFERENCE NAMES
	A=0
	B=1
	C=2
	ENTRY CR4ZZZ
CR4ZZZ:	Z
	MOVN	B,@(16)		;MOVE NEG NO OF ARRAYS TO SET ADDR FOR
	HRLI	B,3(16)		;GET ADDR 1ST DUMMY ARG IN CALL
	MOVSS	B		;SWAP SELF TO SET COUNTER & INDEX
	MOVN	C,@1(16)	;MOVE NEG NO OF ARGS IN CALL THRU
	HRLI	C,CR4L4+1	;GET ADDR 1ST ARG IN CALL THRU
	MOVSS	C		;SWAP SELF TO SET COUNTER & INDEX
CR4L1:	MOVE	A,@(B)		;GET ARRAY ADDRESS
	HLL	A,(B)		;PUT ARG DESCRIPTION INTO LEFT HALF
	MOVEM	A,(C)		;PLACE COMPLETE ARG WORD INTO POSITION
	AOBJP	C,CR4L3		;IF NO MORE ARGS GO TO CR4L3
	AOBJN	B,CR4L1		;IF MORE ARRAYS GO TO CR4L1
CR4L2:	MOVE	A,(B)		;NO MORE ARRAYS BUT MORE ARGS, MOVE TO A
	MOVEM	A,(C)		;MOVE ADDITIONAL ARGS INTO POSITION
	AOS	B		;INCREMENT INDEX FROM CALL
	AOBJN	C,CR4L2		;IF MORE ARGS GO TO CR4L2
CR4L3:	HRR	A,2(16)		;MOVE SUBROUTINE ADDR TO ACA
	HRRM	A,.+1		;MOVE SUBROUTINE ADDR TO CALL
CR4L4:	JSA	16,0		;CALL SUBROUTINE
	REPEAT	^D16,<		;SET ASIDE 16 POS FOR ARGS IN CALL THRU
	JUMP	00,0
>
	JRA	16,^D19(16)	;RETURN
	END
