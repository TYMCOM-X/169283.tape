C                 P R O P R I E T A R Y
C
C
C   THIS PROGRAM IS THE PROPERTY OF GRAPHIC CONTROLS CORP.
C
C   NO COPY OF THE SOURCE IS TO BE GIVEN TO THE CUSTOMER
C
C
C
C
C   WRITTEN BY JOSEPH P. O'CONNELL AND DONALD PIERANTOZZI, GRAPHIC
C   CONTROLS CORP.
C
C   WRITTEN FOR GENERAL FOODS CORP.
C
C   THIS PROGRAM FORECASTS SALES AND REQUIRED INVENTORY LEVELS FOR UP TO
C   40 DIFFERENT PRODUCTS
C
C   THE MAIN PROGRAM ACCEPTS A COMMAND FROM THE USER, INTERPRETS IT
C   AND THEN CALLS THE APPROPRIATE SUBROUTINE 
C
C
C
C
C
        DOUBLE PRECISION MONTH(12)
        DIMENSION SUM(40,4),PREV(40,0/12),QM(4)
        COMMON N,K,II,JJ,SUM,PREV,IQTR,QM,YEAR,QTR,MONTH
        COMMON /WORK/ UNITS(40,13),IF,IYES
        DIMENSION KOMND(7)
1       FORMAT('0COMMAND? '$)
2       FORMAT(A1)
3       FORMAT('0AVAILABLE COMMANDS ARE:  ADD, COMPUTE, DELETE,'/
     1' END, LIST, MODIFY AND STOP'/)
        DATA KOMND /'A','C','D','E','L','M','S'/
        DATA IF/1/,IYES/'Y'/
C   FIRST ASCERTAIN THAT ALL FILES HAVE THE SAME NUMBER OF RECORDS
        CALL CHECK
100     TYPE 1
        READ(5,2) KOM
        DO 110 I=1,7
        IF(KOM.EQ.KOMND(I)) GO TO 120
110     CONTINUE
        WRITE(5,3)
        GO TO 100
120     GO TO (210,220,230,240,250,260,240) I
210     CALL ADD
        GO TO 100
220     CALL COMPUT
        GO TO 100
230     CALL DELETE
        GO TO 100
240     CALL EXIT
250     CALL LIST
        GO TO 100
260     CALL MODIFY
        GO TO 100
        END
C
C
C
C
C
C   THIS ROUTINE ALLOWS THE USER TO ADD INFORMATION FOR A NEW PRODUCT
C   TO THE EXISTING FILES.
C   ALL INFORMATION IN A FILE IS READ INTO AN ARRAY, THE NEW PRODUCT
C   INFO IS ADDED AS THE LAST ROW.  THE ARRAY IS THEN WRITTEN
C   BACK INTO TH FILE
C   NOTE:  THE YEAR MUST BE SHOWN AS A 4 DIGIT NUMBER, IF A 2 DIGIT
C   NUMBER IS INPUT, 1900 IS ADDED TO IT
C
C
C
        SUBROUTINE ADD
        COMMON /WORK/ UNITS(40,13),IF,IYES
        DIMENSION IFIL(2),IFRM(1),KFRM(1)
        DATA IFIL/'HSTY1','HSTY2'/
1       FORMAT(' PRODUCT NAME? '$)
2       FORMAT(6A5)
4       FORMAT(' PRODUCT NUMBER IS',I3)
5       FORMAT('0ANY SALES HISTORY? '$)
6       FORMAT(A1)
8       FORMAT(' ENTER PERCENTAGE FORECASTS FOR 12 MONTHS'/)
9       FORMAT(12F)
10      FORMAT(2I)
11      FORMAT(2I5)
12      FORMAT(12F7.0)
13      FORMAT(' WHAT IS THE MOST RECENT YEAR'/
     1' FOR WHICH YOU HAVE HISTORY? '$)
14      FORMAT(' WHAT IS THE SECOND PREVIOUS YEAR? '$)
201     FORMAT(' ENTER SALES FOR ',I4/)
301     FORMAT(' SALES FORECAST FOR NEXT YEAR?'/)
302     FORMAT(' SALES FORECASTS FOR FOUR QUARTERS?'/)
401     FORMAT(13F)
402     FORMAT('0BEGINNING INVENTORY? '$)
403     FORMAT(' INVENTORY PROJECTIONS FOR TWELVE MONTHS?'/)
404     FORMAT(13F7.0)
3       FORMAT(I)
501     FORMAT('0CONVERSION FACTORS FOR CWT & CASES? '$)
502     FORMAT(2F)
503     FORMAT(2F7.4)
C
C   ADD THE NEW NAME
        CALL IFILE(IF,'NAMES')
        READ(IF,3) NUM
        READ(IF,2) ((UNITS(I,K),K=1,6),I=1,NUM)
        ENDFILE IF
        NUM=NUM+1
        WRITE(5,1)
        READ(5,2) (UNITS(NUM,K),K=1,6)
        WRITE(5,4) NUM
        CALL OFILE(IF,'NAMES')
        WRITE(IF,11) NUM
        WRITE(IF,2) ((UNITS(I,K),K=1,6),I=1,NUM)
        ENDFILE IF
C
C   UPDATE SALES HISTORY
        WRITE(5,5)
        READ(5,6) IANS
        IF(IANS.EQ.IYES) GO TO 200
C   IF THERE IS NO HISTORY, USE THE PERCENTAGE FORECASTS IN THE FILE
        CALL IFILE(IF,IFIL(1))
        READ(IF,10) NUM,IYR
        READ(IF,9) ((UNITS(I,K),K=1,12),I=1,NUM)
        ENDFILE IF
        NUM=NUM+1
        IF(NUM.NE.1) GO TO 100
C   IF THIS IS THE FIRST PRODUCT, FIND OUT THE YEAR
        WRITE(5,13)
        READ(5,10) IYR
        IF (IYR.LT.100) IYR=IYR+1900
C   READ % FORECASTS AND MULTIPLY BY 100
100     WRITE(5,8)
        READ(5,9) (UNITS(NUM,K),K=1,12)
        DO 150 K=1,12
150     UNITS(NUM,K)=UNITS(NUM,K)*100.0
        CALL OFILE(IF,IFIL(1))
        WRITE(IF,11) NUM,IYR
        WRITE(IF,12) ((UNITS(I,K),K=1,12),I=1,NUM)
        ENDFILE IF
C   SECOND YEAR
        CALL IFILE(IF,IFIL(2))
        READ(IF,10) NUM,IYR
        IF(NUM.NE.0) GO TO 185
C   IF FIRST PRODUCT, FIND OUT THE YEAR
        WRITE(5,14)
        READ(5,10) IYR
        IF (IYR.LT.100) IYR=IYR+1900
        GO TO 195
185     READ(IF,9) ((UNITS(I,K),K=1,12),I=1,NUM)
195     ENDFILE IF
        CALL OFILE(IF,IFIL(2))
        NUM=NUM+1
175     WRITE(IF,11) NUM,IYR
        WRITE(IF,12) ((UNITS(I,K),K=1,12),I=1,NUM)
        GO TO 300
C   USE ACTUAL SALES FIGURES
200     DO 210 I=1,2
        CALL IFILE(IF,IFIL(I))
        READ(IF,10) NUM,IYR
        IF(NUM.NE.0) GO TO 250
C  IF FIRST PRODUCT, ASK FOR THE YEAR
        IF(I.EQ.1) WRITE(5,13)
        IF(I.EQ.2) WRITE(5,14)
        READ(5,10) IYR
        IF (IYR.LT.100) IYR=IYR+1900
250     WRITE(5,201) IYR
        READ(IF,9) ((UNITS(J,K),K=1,12),J=1,NUM)
        ENDFILE IF
        CALL OFILE(IF,IFIL(I))
        NUM=NUM+1
        READ(5,9) (UNITS(NUM,K),K=1,12)
        WRITE(IF,11) NUM,IYR
        WRITE(IF,12) ((UNITS(J,K),K=1,12),J=1,NUM)
        ENDFILE IF
210     CONTINUE
C
C   UPDATE SALES FORECAST FILE
300     CALL IFILE(IF,'SALES')
C   KIND=1 FOR ANNUAL FORECASTS
C   KIND=4 FOR QUARTERLY FORECASTS
        READ(IF,10) NUM,KIND
        IF(KIND.EQ.1) GO TO 350
C   IFRM IS INPUT FORMAT ARRAY
C   KFRM IS OUTPUT FORMAT ARRAY
        TYPE 302
        IFRM(1)='4F'
        KFRM(1)='4F7.0'
        GO TO 375
350     TYPE 301
        IFRM(1)='F'
        KFRM(1)='F7.0'
375     READ(IF,IFRM) ((UNITS(I,K),K=1,KIND),I=1,NUM)
        NUM=NUM+1
        ENDFILE IF
        CALL OFILE(IF,'SALES')
        READ(5,IFRM) (UNITS(NUM,K),K=1,KIND)
        WRITE(IF,11) NUM,KIND
        WRITE(IF,KFRM) ((UNITS(I,K),K=1,KIND),I=1,NUM)
        ENDFILE IF
C
C   UPDATE INVENTORY FILE
400     CALL IFILE(IF,'INVTY')
        READ(IF,3) NUM
        READ(IF,401) ((UNITS(I,K),K=1,13),I=1,NUM)
        NUM=NUM+1
        WRITE(5,402)
        READ(5,401) UNITS(NUM,1)
        WRITE(5,403)
        ENDFILE IF
        CALL OFILE(IF,'INVTY')
        WRITE(IF,11) NUM
        IF(NUM.EQ.1) GO TO 450
        WRITE(IF,404) ((UNITS(I,K),K=1,13),I=1,NUM-1)
450     READ(5,401) (UNITS(NUM,K),K=2,13)
        WRITE(IF,404) (UNITS(NUM,K),K=1,13)
        ENDFILE IF
C
C   UPDATE FACTORS FILE
500     CALL IFILE(IF,'FACTR')
        READ(IF,3) NUM
        TYPE 501
        READ(IF,502) ((UNITS(I,K),K=1,2),I=1,NUM)
        ENDFILE IF
        NUM=NUM+1
        READ(5,502) (UNITS(NUM,K),K=1,2)
        CALL OFILE(IF,'FACTR')
        WRITE(IF,11) NUM
        WRITE(IF,503) ((UNITS(I,K),K=1,2), I=1,NUM)
        ENDFILE IF
        RETURN
        END
C
C
C
C
C
C   THIS SUBROUTINE DELETES DATA FOR A SPECIFIED PRODUCT FROM ALL FILES
C   EACH FILE IS PROCESSED INDIVIDUALLY,  ALL DATA IS READ INTO AN ARRAY
C   THE ARRAY, EXCEPT FOR THE ROW BEING DELETED, IS THEN WRITTEN BACK INTO
C   THE FILE.   IF THE LAST PRODUCT IS DELETED, A DUMMY RECORD IS WRITTEN
C
C
        SUBROUTINE DELETE
        DIMENSION IFRM(1),KFRM(1)
        COMMON /WORK/ UNITS(40,13),IF,IYES
        DIMENSION HIST(2)
        DATA HIST/'HSTY1','HSTY2'/
1       FORMAT(2I)
3       FORMAT(6A5)
2       FORMAT('0PRODUCT NUMBER? '$)
4       FORMAT(' IS '6A5/' THE CORRECT PRODUCT? '$)
5       FORMAT(A1)
6       FORMAT(I5)
7       FORMAT(12F)
8       FORMAT(12F7.0)
9       FORMAT(2I5)
10      FORMAT(13F)
11      FORMAT(13F7.0)
12      FORMAT(2F)
13      FORMAT(2F7.4)
20      FORMAT(' HISTORIC SALES DATA DELETED')
21      FORMAT(' SALES FORECASTS DELETED')
22      FORMAT(' INVENTORY FIGURES DELETED')
23      FORMAT(' CONVERSION FACTORS DELETED'///)
24      FORMAT(' THERE IS NO DATA IN THE FILES'/)
C   IF THERE IS NO DATA, RETURN
        CALL IFILE(IF,'NAMES')
        READ(IF,1) NUM
        IF(NUM.GT.0) GO TO 50
        TYPE 24
        ENDFILE IF
        RETURN
C
C   DELETE THE NAME
50      READ(IF,3) ((UNITS(I,K),K=1,6),I=1,NUM)
        ENDFILE IF
100     WRITE(5,2)
        READ(5,1) NO
C   IF THE USER INPUT A ZERO FOR TH PRODUCT #, RETURN
        IF(NO.EQ.0) RETURN
C   GIVE THE USER A VISUAL CHECK FOR THE RIGHT PRODUCT
        WRITE(5,4) (UNITS(NO,K),K=1,6)
        READ(5,5) IANS
        IF(IANS.NE.IYES) GO TO 100
        CALL OFILE(IF,'NAMES')
        NUM=NUM-1
        WRITE(IF,6) NUM
        DO 110 I=1,NUM+1
        IF(I.EQ.NO) GO TO 110
        WRITE(IF,3)(UNITS(I,K),K=1,6)
110     CONTINUE
        IF(NUM.EQ.0) WRITE(IF,3) (UNITS(1,K), K=1,6)
        ENDFILE IF
C
C   DELETE FROM BOTH HISTORY FILES
        DO 500 J=1,2
        CALL IFILE(IF,HIST(J))
        READ(IF,1) NUM,IYR
        READ(IF,7) ((UNITS(I,K),K=1,12),I=1,NUM)
        ENDFILE IF
        CALL OFILE(IF,HIST(J))
        NUM=NUM-1
        WRITE(IF,9) NUM,IYR
        DO 200 I=1,NUM+1
        IF(I.EQ.NO) GO TO 200
        WRITE(IF,8) (UNITS(I,K),K=1,12)
200     CONTINUE
        IF(NUM.EQ.0) WRITE(IF,8) (UNITS(1,K), K=1,12)
        ENDFILE IF
500     CONTINUE
        TYPE 20
C
C   DELETE FROM THE SALES FORECASTS FILE
        CALL IFILE(IF,'SALES')
        READ(IF,1) NUM,KIND
        IF(KIND.NE.1) GO TO 205
        IFRM(1)='F'
        KFRM(1)='F7.0'
        GO TO 210
205     IFRM(1)='4F'
        KFRM(1)='4F7.0'
210     READ(IF,IFRM) ((UNITS(I,K),K=1,KIND),I=1,NUM)
        ENDFILE IF
        NUM=NUM-1
        CALL OFILE (IF,'SALES')
        WRITE(IF,9) NUM,KIND
        DO 220 I=1,NUM+1
        IF(I.EQ.NO) GO TO 220
        WRITE(IF,KFRM) (UNITS(I,K),K=1,KIND)
220     CONTINUE
        IF(NUM.EQ.0) WRITE(IF,KFRM) (UNITS(1,K), K=1,KIND)
        ENDFILE IF
        TYPE 21
C
C   DELETE FROM TH INVENTORY FILE
        CALL IFILE(IF,'INVTY')
        READ(IF,1) NUM
        READ(IF,10) ((UNITS(I,K),K=1,13),I=1,NUM)
        ENDFILE IF
        NUM=NUM-1
        CALL OFILE (IF,'INVTY')
        WRITE(IF,9) NUM
        DO 230 I=1,NUM+1
        IF(I.EQ.NO) GO TO 230
        WRITE(IF,11) (UNITS(I,K),K=1,13)
230     CONTINUE
        IF(NUM.EQ.0) WRITE(IF,11) (UNITS(1,K), K=1,13)
        ENDFILE IF
        TYPE 22
C
C   DELETE CONVERSION FACTORS FROM THE FACTOR FILE
        CALL IFILE(IF,'FACTR')
        READ(IF,1) NUM
        READ(IF,12) ((UNITS(I,K),K=1,2),I=1,NUM)
        ENDFILE IF
        NUM=NUM-1
        CALL OFILE(IF,'FACTR')
        WRITE(IF,9) NUM
        DO 240 I=1,NUM+1
        IF(I.EQ.NO) GO TO 240
        WRITE(IF,13) (UNITS(I,K),K=1,2)
240     CONTINUE
        IF(NUM.EQ.0) WRITE(IF,13) (UNITS(1,K), K=1,2)
        ENDFILE IF
        TYPE 23
        RETURN
        END
C
C
C
C
C
C   THIS SUBROUTINE DETERMINES WHAT IS TO BE MODIFIED, AND THEN CALLS
C   THE APPROPRIATE ROUTINE TO MAKE THE ACTUAL CHANGE
C
C
        SUBROUTINE MODIFY
        DIMENSION NAME(4)
        DATA NAME /'H','F','I','C'/
1       FORMAT('0WHAT DO YOU WISH TO MODIFY? '$)
2       FORMAT(A1)
3       FORMAT('0PLEASE TYPE HISTORY, FORECASTS, INVENTORY,'/
     1' OR CONVERSION FACTOR')
110     TYPE 1
        READ(5,2) IANS
        DO 100 I=1,4
        IF(IANS.NE.NAME(I)) GO TO 100
        GO TO (210,220,230,240) I
100     CONTINUE
        TYPE 3
        GO TO 110
210     CALL HISTY
        RETURN
220     CALL SALES
        RETURN
230     CALL INVTY
        RETURN
240     CALL FACTR
        RETURN
        END
C
C
C
C
C
C   THIS SUBROUTINE MODIFIES EITHER OF THE SALES HISTORY FILES
C
C
        SUBROUTINE HISTY
        COMMON /WORK/ UNITS(40,13),IF,IYES
        DIMENSION IFIL(2)
        DATA IFIL/'HSTY1','HSTY2'/
1       FORMAT(' WHICH YEAR? '$)
2       FORMAT(I)
3       FORMAT(2I)
4       FORMAT(' NO DATA FOR ',I4)
5       FORMAT(12F)
6       FORMAT(' PRODUCT #, MONTH, UNITS? '$)
7       FORMAT(2I,F)
8       FORMAT(' ANY MORE HISTORY CHANGES? '$)
9       FORMAT(A1)
10      FORMAT(2I5)
11      FORMAT(12F7.0)
        TYPE 1
        READ(5,2) IYR
        IF (IYR.LT.100) IYR=IYR+1900
        IDEX=1
110     CALL IFILE(IF,IFIL(IDEX))
        READ(IF,3) NUM,KYR
        IF(IYR.EQ.KYR) GO TO 100
        ENDFILE IF
        IDEX=IDEX+1
        IF(IDEX.LE.2) GO TO 110
        TYPE 4, IYR
        RETURN
C   READ ALL DATA INTO AN ARRAY
100     READ(IF,5) ((UNITS(I,K),K=1,12),I=1,NUM)
        ENDFILE IF
310     TYPE 6
C   READ PRODUCT #, MONTH AND NEW AMOUNT
        READ(5,7) NO,MO,UNIT
        IF(NO.EQ.0) GO TO 200
        IF (NO.GT.NUM) GO TO 310
C   CONVERT THE MONTH INTO THE PROPER INDEX
C   APRIL IS THE FIRST ENTRY IN EACH ROW
        MO=MO-3
        IF(MO.LE.0) MO=MO+12
        UNITS(NO,MO)=UNIT
        TYPE 8
        READ(5,9) IANS
        IF(IANS.EQ.IYES) GO TO 310
C   NO MORE CHANGES SO REWRITE THE FILE
200     CALL OFILE(IF,IFIL(IDEX))
        WRITE(IF,10) NUM,IYR
        WRITE(IF,11) ((UNITS(I,K),K=1,12),I=1,NUM)
        ENDFILE IF
        RETURN
        END
C
C
C
C
C
C   THIS SUBROUTINE ALLOWS THE USER TO INDIVIDUALLY UPDATE ANY
C   FIGURE IN THE SALES FORECASTS FILE OR TO COMPLETELY CHANGE ALL
C   ANNUAL DATA TO QUARTERLY AND VICE VERSA
C
C
        SUBROUTINE SALES
        DIMENSION KFRM(1),IFRM(1)
        COMMON /WORK/ UNITS(40,13),IF,IYES
1       FORMAT(2I)
2       FORMAT(4F)
11      FORMAT(' DO YOU WISH TO CHANGE TO QUARTERLY? '$)
12      FORMAT(A1)
201     FORMAT (' DO YOU WISH TO CHANGE TO ANNUAL? '$)
13      FORMAT(' DATA FOR PRODUCT # ',I2,'? '$)
14      FORMAT(4F)
3       FORMAT(' PRODUCT #, SALES? '$)
4       FORMAT(I,4F)
5       FORMAT(' ANY MORE CHANGES TO FORECASTS? '$)
6       FORMAT(A1)
7       FORMAT(2I5)
8       FORMAT(4F7.0)
C   KFRM IS THE INPUT FORMAT ARRAY
C   IFRM IS THE OUTPUT FORMAT ARRAY
        CALL IFILE(IF,'SALES')
        READ(IF,1) NUM,KIND
        IF(KIND.EQ.1) KFRM(1)='F'
        IF(KIND.NE.1) KFRM(1)='4F'
        READ(IF,KFRM) ((UNITS(I,K),K=1,KIND),I=1,NUM)
        ENDFILE IF
        IF(KIND.EQ.4) GO TO 200
C   CHANGE TO QUARTERLY?
        WRITE(5,11)
        READ(5,12) IANS
        IF(IANS.NE.IYES) GO TO 100
        KIND=4
        KFRM(1)='4F'
        GO TO 210
C   CHANGE TO ANNUAL?
200     WRITE(5,201)
        READ(5,12) IANS
        IF(IANS.NE.IYES) GO TO 100
        KIND=1
        KFRM(1)='F'
C   CHANGING FROM ANNUAL TO QUARTERLY, OR VICE VERSA
210     DO 500 I=1,NUM
        WRITE(5,13) I
500     READ(5,KFRM) (UNITS(I,K),K=1,KIND)
        GO TO 130
C   INPUT INDIVIDUAL CHANGES
100     WRITE(5,3)
        READ(5,4) NO,(UNITS(NO,K),K=1,4)
110     WRITE(5,5)
        READ(5,6) IANS
        IF(IANS.EQ.IYES) GO TO 100
130     CALL OFILE(IF,'SALES')
        WRITE(IF,7) NUM,KIND
        IF(KIND.EQ.1) IFRM(1)='F7.0'
        IF(KIND.NE.1) IFRM(1)='4F7.0'
        WRITE(IF,IFRM) ((UNITS(I,K),K=1,KIND),I=1,NUM)
        ENDFILE IF
        RETURN
        END
C    THIS SUBROUTIN UPDATES INDIVIDUAL ITEMS IN THE INVENTORY FILE.
C   A MONTH OF ZERO INDICATES BEGINNING INVENTORY, WHICH IS THE
C   FIRST ITEM PER RECORD, THE NEXT ONE IS APRIL, ETC.
C
C
        SUBROUTINE INVTY
        COMMON /WORK/ UNITS(40,13),IF,IYES
1       FORMAT(I)
2       FORMAT(13F)
3       FORMAT(' PRODUCT #, MONTH, UNITS? '$)
4       FORMAT(' ANY MORE CHANGES? '$)
5       FORMAT(A1)
6       FORMAT(13F7.0)
7       FORMAT(2I,F)
8       FORMAT(I5)
        CALL IFILE(IF,'INVTY')
        READ(IF,1) NUM
        READ(IF,2) ((UNITS(I,K),K=1,13),I=1,NUM)
        ENDFILE IF
100     WRITE(5,3)
C   READ PRODUCT #, MONTH AND NEW AMOUNT
        READ(5,7) NO,MO,UNIT
        IF(NO.EQ.0) GO TO 200
        IF (NO.GT.NUM) GO TO 100
C   CONVERT THE MONTH TO THE PROPER INDEX, APRIL=2, ETC.
        IF(MO.EQ.0) GO TO 120
        MO=MO-2
        IF(MO.LE.1) MO=MO+12
        GO TO 140
120     MO=1
140     UNITS(NO,MO)=UNIT
        WRITE(5,4)
        READ(5,5) IANS
        IF(IANS.EQ.IYES) GO TO 100
200     CALL OFILE(IF,'INVTY')
        WRITE (IF,8) NUM
        WRITE(IF,6) ((UNITS(I,K),K=1,13),I=1,NUM)
        ENDFILE IF
        RETURN
        END
C
C
C
C
C
C   THIS SUBROUTINE ALLOWS THE USER TO UPDATE FACTORS FOR ANY PRODUCT.
C   BOTH FACTORS MUST BE INPUT EVEN IF ONLY ONE IS TO BE CHANGED.
C
C
        SUBROUTINE FACTR
        COMMON /WORK/ UNITS(40,13),IF,IYES
1       FORMAT(I)
2       FORMAT(2F)
3       FORMAT(' PRODUCT #, CWT, CASE? '$)
4       FORMAT(I,2F)
5       FORMAT(' ANY MORE CHANGES? '$)
6       FORMAT(A1)
7       FORMAT(I5)
8       FORMAT(2F7.4)
        CALL IFILE(IF,'FACTR')
        READ(IF,1) NUM
        READ(IF,2) ((UNITS(I,K),K=1,2),I=1,NUM)
        ENDFILE IF
100     WRITE(5,3)
C   READ PRODUCT #, AND NEW CONVERSION FACTORS
        READ(5,4) NO,(UNITS(NO,K),K=1,2)
        WRITE(5,5)
        READ(5,6) IANS
        IF(IANS.EQ.IYES) GO TO 100
        CALL OFILE(IF,'FACTR')
        WRITE(IF,7) NUM
        WRITE(IF,8) ((UNITS(I,K),K=1,2),I=1,NUM)
        ENDFILE IF
        RETURN
        END

C
C
C
C
C
C   THIS SUBROUTINE LISTS THE CONTENTS OF ANY FILE FOR THE USER
C
C
        SUBROUTINE LIST
        DIMENSION ITEMP(40),KFILES(5)
        COMMON /WORK/ UNITS(40,13),IF,IYES
        DATA KFILES /'N','I','H','C','F'/
1       FORMAT('0WHICH FILE DO YOU WISH TO LIST? '$)
2       FORMAT(A1)
3       FORMAT(' PLEASE TYPE NAME, INVENTORY, HISTORY,'/
     + ' CONVERSION FACTOR, OR FORECASTS'/)
4       FORMAT(2I)
5       FORMAT('1NUMBER       PRODUCT NAME'/1X,25('-')/)
6       FORMAT(6A5)
7       FORMAT(I5,3X,6A5)
8       FORMAT(1H1,29X,'INVENTORY'//'  #    MAR  APR  MAY  JUN  JUL
     +  AUG  SEP  OCT  NOV  DEC  JAN  FEB  MAR'/1X,69('-')/)
9       FORMAT(13F)
10      FORMAT(I3,2X,13(1X,I4))
11      FORMAT(' WHICH YEAR? '$)
12      FORMAT(' NO DATA FOR THAT YEAR')
13      FORMAT(1H1,21X,'SALES HISTORY FOR ',I4,//
     + '  #    APR  MAY  JUN  JUL  AUG  SEP  OCT  NOV
     +  DEC  JAN  FEB  MAR'/1X,64('-')/)
14      FORMAT(1H1,20X,'CONVERSION FACTORS'//18X,'  #   CWT      CASES'
     +  /19X,19('-')/)
15      FORMAT(2F)
16      FORMAT(18X,I3,2X,F6.3,4X,F6.3)
17      FORMAT(1H1,18X,'ANNUAL SALES FORECASTS'//
     +  18X,'  #   SALES'/20X,10('-')/)
18      FORMAT(1H1,15X,'QUARTERLY SALES FORECASTS'//
     +  '  #    1ST  2ND  3RD  4TH'/1X,23('-')/)
19      FORMAT('0THERE IS NO DATAHE FILE')
20      FORMAT(18X,I3,3X,I5)
C   FIND OUT WHICH FILE
90      TYPE 1
        ACCEPT 2, NAMFI
        DO 95 I=1,5
        IF(NAMFI.NE.KFILES(I)) GO TO 95
C   BRANCH TO PROPER PLACE
        GO TO (100,200,300,400,500) I
95      CONTINUE
        TYPE 3
        GO TO 90
C   LIST ALL NAMES
100     CALL IFILE(IF,'NAMES')
        READ(IF,4) NUM
        IF(NUM.EQ.0) GO TO 600
        TYPE 5
        DO 110 I=1,NUM
        READ(IF,6) (ITEMP(K),K=1,6)
110     WRITE(5,7) I,(ITEMP(K),K=1,6)
        GO TO 700
C   LIST THE INVENTORY
200     CALL IFILE(IF,'INVTY')
        READ(IF,4) NUM
        IF(NUM.EQ.0) GO TO 600
        TYPE 8
        NN=13
C
C   THIS ROUTINE IS USED FOR INVENTORY, HISTORY AND QUARTERLY SALES
230     DO 225 I=1,NUM
225     READ(IF,9) (UNITS(I,K),K=1,NN)
        DO 210 I=1,NUM
        DO 220 K=1,NN
C   CHANGE TO INTEGER FOR DISPLAY
220     ITEMP(K)=UNITS(I,K)
210     TYPE 10, I,(ITEMP(K),K=1,NN)
        GO TO 700
C
C   DISPLAY THE HISTORY--SELECT THE YEAR
300     TYPE 11
        ACCEPT 4, IYR
        IF (IYR.LT.100) IYR=IYR+1900
        CALL IFILE(IF,'HSTY1')
        READ(IF,4) NUM,KYR
        IF(IYR.EQ.KYR) GO TO 310
        ENDFILE IF
        CALL IFILE(IF,'HSTY2')
        READ(IF,4) NUM,KYR
        IF(IYR.EQ.KYR) GO TO 310
        TYPE 12
        GO TO 700
310     IF(NUM.EQ.0) GO TO 600
        TYPE 13, IYR
        NN=12
        GO TO 230
C
C   LIST THE CONVERSION FACTORS
400     CALL IFILE(IF,'FACTR')
        READ(IF,4) NUM
        IF(NUM.EQ.0) GO TO 600
        TYPE 14
        DO 410 I=1,NUM
        READ(IF,15) F1,F2
410     TYPE 16, I,F1,F2
        GO TO 700
C
C   LIST SALES FORECASTS
500     CALL IFILE(IF,'SALES')
        READ(IF,4) NUM,NN
        IF(NUM.EQ.0) GO TO 600
C   TYPE THE PROPER HEADING
        IF(NN.EQ.1) TYPE 17
        IF(NN.EQ.4) TYPE 18
        IF(NN.EQ.4) GO TO 230
C   ANNUAL SALES
        DO 510 I=1,NUM
510     READ(IF,9) UNITS(I,1)
        DO 520 I=1,NUM
        ITEMP(1)=UNITS(I,1)
520     TYPE 20, I,ITEMP(1)
        GO TO 700
600     TYPE 19
700     ENDFILE IF
        RETURN
        END
C
C
C
C
        SUBROUTINE COMPUT
        DIMENSION INVUNT(40,0/12),CWTFAC(40),CASFAC(40),PCT1(40,12),
     1  PCT2(40,12),PREV(40,0/12),PCTP(40,12),SF(40,0/4),PSF(40,0/12),
     2  SUM(40,4),INVCWT(40,0/12),INVCAS(40,0/12),SICWT(0/12),
     3 SICAS(0/12),AVICWT(12),AVICAS(12),SFCWT(40,0/12),SFCASE(40,0/12),
     4  SSFCWT(0/12),SSFCAS(0/12),PMCWT(0/12),PMCASE(0/12),HMCWT(12),
     5  HMCASE(12),MONTH(12),QM(4),AQCWT(4),AQCAS(4),TQCWT(4),
     6  TQCAS(4),PMPCWT(40,0/12),PMPCAS(40,0/12),PUNIT(40,0/12),
     7  PADCWT(0/12),PADCAS(0/12)
        REAL INVUNT,INVCWT,INVCAS
        DOUBLE PRECISION MONTH,AVG,TOTAL,INV,PROD,SALES
        COMMON N,K,II,JJ,SUM,PREV,IQTR,QM,YEAR,QTR,MONTH
        DATA MONTH(1),MONTH(2),MONTH(3),MONTH(4),MONTH(5),
     1  MONTH(6),MONTH(7),MONTH(8),MONTH(9),MONTH(10),
     2  MONTH(11),MONTH(12),QM(1),QM(2),QM(3),QM(4),TOTAL,QTR,
     3  YEAR,AVG,CASES,CWT,INV,PROD,SALES/'APRIL     ',
     4  'MAY       ','JUNE      ','JULY      ','AUGUST    ',
     5  'SEPTEMBER ','OCTOBER   ','NOVEMBER  ','DECEMBER  ',
     6  'JANUARY   ','FEBRUARY  ','MARCH     ','JUNE ','SEPT.',
     7  'DEC. ','MARCH','TOTAL  ',' QTR.',' YEAR','AVERAGE',
     8  'CASES','CWT. ',' INVENTORY','PRODUCTION','   SALES  '/
C
        WRITE(5,1901)
1901    FORMAT(//' ENTER WEIGHTING FACTOR FOR HISTORICAL DATA:='$)
        ACCEPT 1902,XWT1
        IF(XWT1.GT.1.0) XWT1=XWT1/100.
1902    FORMAT(F)
        ILIM=0
        CALL RDX(SF,1,ILIM,'SALES')
        II=1
C
C       CHECK FOR QUATERLY OR YEARLY SALES DATA
C
        IF(K .EQ. 1)GO TO 260
C
C       QUATERLY DATA
C
        JJ=3
        GO TO 250
C
C       YEARLY DATA
C
260     JJ=12
250     ILIM=0
C
C       READ INVENTORY UNITS
C
        CALL RDX(INVUNT,ILIM,12,'INVTY')
C
C       READ CWT AND CASE CONVERSION FACTORS
C
        CALL RDXFAC(CWTFAC,CASFAC,'FACTR')
C
C       READ FIRST YEAR HISTORICAL DATA
C
        CALL RDX(PREV,1,12,'HSTY1')
C
C       SUM THE HISTORY DATA
C
        CALL SUMPY
C
C       COMPUTE MONTHLY PERCENTAGE VALUES
C
        CALL PCT(PCT1,XWT1)
C
C       READ SECOND YEAR HISTORICAL DATA
C
        CALL RDX(PREV,1,12,'HSTY2')
C
C       COMPUTE PERCENTAGE WEIGHT SPLIT FOR THE SECOND YEAR
C
        XWT2=1.0-XWT1
C
C       SUM THE SECOND YEAR HISTORY DATA
C
        CALL SUMPY
C
C       COMPUTE MONTHLY PERCENTAGE VALUES
C
        CALL PCT(PCT2,XWT2)
C
C       BUILD FORECAST PERCENTAGES
C
        DO 100 I=1,N
        DO 100 J=1,12
        PCTP(I,J)=PCT1(I,J)+PCT2(I,J)
100     CONTINUE
C
C       COMPUTE MONTHLY SALES FORECAST FOR NEXT FISCAL YEAR
C
        DO 700 I=1,N
        M=II
        MM=JJ
        DO 701 J=1,K
        DO 702 L=M,MM
        PSF(I,L)=PCTP(I,L)*SF(I,J)
702     CONTINUE
        M=M+JJ
        MM=MM+JJ
701     CONTINUE
700     CONTINUE
C
C       SET QUARTER INDICATOR TO 1ST
C
        IQR=1
C
C       CONVERT INVENTORY UNITS TO CWT
C
        CALL XCON(CWTFAC,INVCWT,INVUNT,0)
C
C       CONVERT INVENTORY UNITS TO CASES
C
        CALL XCON(CASFAC,INVCAS,INVUNT,0)
C
C       SUM THE CWT FOR 12 MONTHS
C
        CALL XSUM(SICWT,INVCWT,0)
C
C       SUM THE CASES FOR 12 MONTHS
C
        CALL XSUM(SICAS,INVCAS,0)
C
C       COMPUTE AVERAGE CWT FOR 12 MONTHS
C
        CALL AVGINV(AVICWT,SICWT)
C
C       COMPUTE AVERAGE CASES FOR 12 MONTHS
C
        CALL AVGINV(AVICAS,SICAS)
C
C       COMPUTE QUARTERLY AND YEARLY AVERAGES FOR CWT AND CASES
C
        CALL COMP(AQCAS,AQCWT,AYCWT,AYCAS,3.0,12.0,AVICWT,AVICAS)
        WRITE(5,650)
650     FORMAT(/////)
        WRITE(5,651)
651     FORMAT(33X,'STORAGE'/)
        IQTR=IQR
        DO 1900 I=1,12
        PADCWT(I)=SICWT(I-1)
        PADCAS(I)=SICAS(I-1)
1900    CONTINUE
        WRITE(5,660)
660     FORMAT(' ',10X,'PREVIOUS',10X,'CURRENT')
        WRITE(5,661)CWT
661     FORMAT(' ',12X,'INV.',14X,'INV.',11X,'MONTH',10X,A5/)
        CALL PRINT(PADCWT,SICWT,AQCWT,AVICWT,AVG)
        WRITE(5,652)AVG,INV,YEAR,AYCWT
        WRITE(5,650)
        WRITE(5,651)
        WRITE(5,660)
        WRITE(5,661)CASES
        IQTR=IQR
        CALL PRINT(PADCAS,SICAS,AQCAS,AVICAS,AVG)
        WRITE(5,652)AVG,INV,YEAR,AYCAS
652     FORMAT(//' ',A7,A10,A5,39X,F8.1)
C
C       CONVERT SALES FORECAST TO CWT WEIGHT
C
        CALL XCON(CWTFAC,SFCWT,PSF,1)
C
C       CONVERT SALES FORECAST TO CASES
C
        CALL XCON(CASFAC,SFCASE,PSF,1)
C
C       COMPUTE MONTHLY PRODUCTION FOR CASES
C
        CALL PRODX(SFCASE,INVCAS,PMPCAS)
C
C       COMPUTE MONTHLY PRODUCTION FOR CWT WEIGHT
C
        CALL PRODX(SFCWT,INVCWT,PMPCWT)
C
C       SUM THE SALES FORECAST CWT FOR 12 MONTHS
C
        CALL XSUM(SSFCWT,SFCWT,1)
C
C       SUM THE SALES FORECAST CASES FOR 12 MONTHS
C
        CALL XSUM(SSFCAS,SFCASE,1)
C
C       SUM THE CWT PRODUCTIONS FOR 12 MONTHS
C
        CALL XSUM(PMCWT,PMPCWT,0)
C
C       SUM THE CASES PRODUCTIONS FOR 12 MONTHS
C
        CALL XSUM(PMCASE,PMPCAS,0)
C
C       COMPUTE MONTHLY HANDLING FOR CWT
C
        CALL HANDX(PMCWT,SSFCWT,HMCWT)
C
C       COMPUTE MONTHLY HANDLING FOR CASES
C
        CALL HANDX(PMCASE,SSFCAS,HMCASE)
C
C       COMPUTE QUARTERLY AND YEARLY TOTALS FOR CASES AND CWT
C
        CALL COMP(TQCAS,TQCWT,TYCWT,TYCAS,1.0,1.0,HMCWT,HMCASE)
        IQTR=IQR
        WRITE(5,650)
        WRITE(5,655)
655     FORMAT(33X,'HANDLING'/)
        WRITE(5,662)CWT
662     FORMAT(' ',12X,'SALES',9X,'PRODUCTION',9X,'MONTH',10X,A5/)
        CALL PRINT(SSFCWT,PMCWT,TQCWT,HMCWT,TOTAL)
        WRITE(5,653)TOTAL,YEAR,TYCWT
653     FORMAT(//' ',A7,A5,49X,F8.1/)
        WRITE(5,650)
        WRITE(5,655)
        WRITE(5,662)CASES
        IQTR=IQR
        CALL PRINT(SSFCAS,PMCASE,TQCAS,HMCASE,TOTAL)
        WRITE(5,653)TOTAL,YEAR,TYCAS
C
        WRITE(5,650)
        CALL PRUNIT(PSF,SALES)
        CALL PRUNIT(INVUNT,INV)
        CALL PX(PUNIT,PMPCAS,CASFAC)
        CALL PRUNIT(PUNIT,PROD)
        CALL EXIT
        END
C
C       THIS SUBROUTINE READS CWT AND CASE CONVERSION FACTORS
C       FOR INVENTORY UNITS
C
        SUBROUTINE RDXFAC(XFAC,YFAC,NAME)
        DIMENSION XFAC(40),YFAC(40)
        COMMON N
        CALL IFILE(20,NAME)
        READ(20,3)N
3       FORMAT(I)
        DO 1 I=1,N
        READ(20,2)XFAC(I),YFAC(I)
2       FORMAT(2F)
1       CONTINUE
        END FILE 20
        RETURN
        END
C
C       THE PRIMARY FUNCTION OF THIS SUBROUTINE IS TO READ DATA
C       FOR 2-DIMENSIONAL VECTORS
C
        SUBROUTINE RDX(Y,LIM1,LIM2,NAME)
        DIMENSION Y(40,0/12)
        COMMON N,K
        IX=0
        CALL IFILE(20,NAME)
        READ(20,60)N,IX
60      FORMAT(2I)
        IF(IX .EQ. 0 .OR. IX.GT.100)GO TO 50
        K=IX
        LIM2=IX
50      DO 1 I=1,N
        READ(20,2)(Y(I,J),J=LIM1,LIM2)
2       FORMAT(13F)
1       CONTINUE
        END FILE 20
        RETURN
        END
C
C       THIS SUBROUTINE WILL SUM THE PREVIOUS HISTORICAL DATA
C       ACCORDING TO QUATERLY OR YEARLY FIGURES. THE LOOP IS
C       CONTROLLED BY THE SCALARS II AND JJ WHICH ARE INITIALIZED
C       UPON ENTRANCE INTO THE PHASE, I.E. COMPUTE!
C
        SUBROUTINE SUMPY
        DIMENSION SUM(40,4),PREV(40,0/12)
        COMMON N,K,II,JJ,SUM,PREV
        DO 1 I=1,N
        M=II
        MM=JJ
        DO 2 J=1,K
        SUM(I,J)=0.0
        DO 3 L=M,MM
        SUM(I,J)=SUM(I,J)+PREV(I,L)
3       CONTINUE
        M=M+JJ
        MM=MM+JJ
2       CONTINUE
1       CONTINUE
        RETURN
        END
C
C       THE FUNCTION OF THIS SUBROUTINE IS TO DETERMINE THE
C       VALUES OF THE HISTORICAL MONTHLY PERCENTAGES ACCORDING TO
C       THE WEIGHT BREAKDOWN DEFINED AT ENTRANCE TO THIS PHASE
C
        SUBROUTINE PCT(PCTX,XWTN)
        DIMENSION PCTX(40,12),SUM(40,4),PREV(40,0/12)
        COMMON N,K,II,JJ,SUM,PREV
        DO 1 I=1,N
        M=II
        MM=JJ
        DO 2 J=1,K
        DO 3 L=M,MM
        PCTX(I,L)=PREV(I,L)/SUM(I,J)*XWTN
3       CONTINUE
        M=M+JJ
        MM=MM+JJ
2       CONTINUE
1       CONTINUE
        RETURN
        END
C
C       THE FUNCTION OF THIS SUBROUTINE IS TO COVERT THE
C       INVENTORY UNITS TO THE DESIRED CONVERSION UNITS; EITHER
C       CWT OR CASES
C
        SUBROUTINE XCON(XFAC,YCON,UNIT,LIM)
        DIMENSION YCON(40,0/12),UNIT(40,0/12),XFAC(40)
        COMMON N
        DO 1 I=1,N
        DO 1 J=LIM,12
        YCON(I,J)=UNIT(I,J)*XFAC(I)
1       CONTINUE
        RETURN
        END
C
C       THE FUNCTION OF THIS SUBROUTINE IS TO PRODUCE YEARLY TOTALS
C       FOR THE N PRODUCTS
C
        SUBROUTINE XSUM(Y,X,LIM)
        DIMENSION Y(0/12),X(40,0/12)
        COMMON N
        DO 1 J=LIM,12
        DO 1 I=1,N
        Y(J)=Y(J)+X(I,J)
1       CONTINUE
        RETURN
        END
C
C       THE FUNCTION OF THIS SUBROUTINE IS TO DETERMINE THE
C       AVERAGE INVENTORY UNITS FOR THE PROJECTED YEAR. THE
C       FORMULA IS DEFINED BY GENERAL FOODS. THE MONTHS
C       J-2 AND J-1 ARE THE SAME WHILE J IS REVERSED
C
        SUBROUTINE AVGINV(AIX,SIX)
        DIMENSION AIX(12),SIX(0/12)
        DO 2 J=2,11,3
        DO 1 I=J-1,J
        AIX(I)=(3.0*SIX(I-1)+5.0*SIX(I))*0.11
1       CONTINUE
        AIX(J+1)=(3.0*SIX(J+1)+5.0*SIX(J))*0.11
2       CONTINUE
        RETURN
        END
C
C       THE FUNCTION OF THIS SUBROUTINE IS TO COMPUTE THE
C       MONTHLY PRODUCTION UNITS FOR N PRODUCTS. THE FORMULA
C       IS  DEFINED BY GENERAL FOODS.
C
        SUBROUTINE PRODX(SFX,INVX,PMX)
        DIMENSION SFX(40,0/12),INVX(40,0/12),PMX(40,0/12)
        COMMON N
        REAL INVX
        DO 1 I=1,N
        DO 1 J=1,12
        PMX(I,J)=INVX(I,J)+SFX(I,J)-INVX(I,J-1)
        IF(PMX(I,J) .LT. 0)GO TO 2
        GO TO 1
2       PMX(I,J)=1.0*10.0**(-10)*PMX(I,J)
1       CONTINUE
        RETURN
        END
C
C       THE FUNCTION OF THIS SUBROUTINE IS TO COMPUTE THE
C       HANDLING FOR THE FISCAL YEAR FOR THE N PRODUCTS
C       THE FORMULA IS DEFINED BY GENERAL FOODS
C
        SUBROUTINE HANDX(PMX,SSFX,HMX)
        DIMENSION PMX(0/12),SSFX(0/12),HMX(12)
        DO 1 I=1,12
        HMX(I)=((SSFX(I)+PMX(I))/2.0)*1.1
1       CONTINUE
        RETURN
        END
C
C       THE FUNCTION OF THIS SUBROUTINE IS TO COMPUTE THE
C       QUATERLY AND YEARLY FIGURES FOR BOTH THE CWT AND CASES
C       THE SCALARS DIV1 AND DIV2 ARE PRESET BEFORE ENTRANCE
C       TO THIS ROUTINE TO DETERMINE IF THE FIGURES WILL
C       BE AVERAGE OR SUMMATION
C
        SUBROUTINE COMP(YCAS,YCWT,ZCWT,ZCAS,DIV1,DIV2,XCWT,XCAS)
        DIMENSION YCAS(4),YCWT(4),XCWT(12),XCAS(12)
        II=1
        JJ=3
        DO 1 J=1,4
        DO 2 I=II,JJ
        YCWT(J)=YCWT(J)+XCWT(I)
        YCAS(J)=YCAS(J)+XCAS(I)
        ZCWT=ZCWT+XCWT(I)
        ZCAS=ZCAS+XCAS(I)
2       CONTINUE
        II=JJ+1
        JJ=II+2
        YCWT(J)=YCWT(J)/DIV1
        YCAS(J)=YCAS(J)/DIV1
1       CONTINUE
        ZCWT=ZCWT/DIV2
        ZCAS=ZCAS/DIV2
        RETURN
        END
C
C       THE FUNCTION OF THIS SUBROUTINE IS TO PRINT THE
C       VALUES OF THE STORAGE OR HANDLING FIGURES
C
        SUBROUTINE PRINT(W,X,Y,Z,T1)
        DIMENSION W(0/12),X(0/12),Y(4),Z(12),QM(4),MONTH(12),
     1  PREV(40,0/12),SUM(40,4)
        DOUBLE PRECISION MONTH,T1
        COMMON N,K,II,JJ,SUM,PREV,IQTR,QM,YEAR,QTR,MONTH
        DO 10 I=1,4
        DO 20 J=1,3
        INDEX=J+((IQTR-1)*3)
        WRITE(5,4)W(INDEX),X(INDEX),MONTH(INDEX),Z(INDEX)
4       FORMAT(' ',11X,F7.1,12X,F7.1,8X,A10,7X,F7.1)
20      CONTINUE
        WRITE(5,5)T1,QM(IQTR),QTR,Y(IQTR)
5       FORMAT(/' ',A7,' - ',2A5,41X,F8.1/)
        IF(IQTR .EQ. 4)GO TO 100
        IQTR=IQTR+1
        GO TO 10
100     IQTR=1
10      CONTINUE
        RETURN
        END
C
C       THE FUNCTION OF THIS SUBROUTINE IS TO PRINT EITHER THE
C       PROUDUCTION, SALES OR INVENTORY UNITS BY MONTH/PRODUCT
C       IF THE USER SO REQUESTS
C
        SUBROUTINE PRUNIT(XUNIT,TITLE)
        DIMENSION XUNIT(40,0/12),K(12)
        COMMON N
        DOUBLE PRECISION TITLE
        WRITE(5,1)TITLE
1       FORMAT(//' DO YOU WISH TO PRINT ',A10,
     1  ' BY MONTH BY PRODUCT IN UNITS?:='$)
        ACCEPT 2,IANS
2       FORMAT(A1)
        IF(IANS .NE. 'Y')GO TO 3
        WRITE(5,4)TITLE
4       FORMAT(//' ',31X,A10,/)
        WRITE(5,5)
5       FORMAT(' PROD#',4X,'APR',2X,'MAY',2X,'JUNE',1X,'JULY',
     1  1X,'AUG',2X,'SEPT',1X,'OCT',2X,'NOV',2X,'DEC',2X,'JAN',
     2  2X,'FEB',2X,'MAR')
        DO 6 I=1,N
        DO 8 J=1,12
        K(J)=XUNIT(I,J)
8       CONTINUE
        WRITE(5,7)I,(K(J),J=1,12)
7       FORMAT(' ',I3,6X,12(I4,1X))
6       CONTINUE
3       RETURN
        END
C
C       THE FUNCTION OF THIS SUBROUTINE IS TO CONVERT THE
C       PRODUCTION FIGURES BACK TO CASE UNITS
C       IF THE USER REQUESTS TO SEE THEM
C
        SUBROUTINE PX(PUNIT,PCASE,CASFAC)
        DIMENSION PUNIT(40,0/12),PCASE(40,0/12),CASFAC(40)
        COMMON N
        DO 1 I=1,N
        DO 1 J=1,12
        IF(PCASE(I,J) .GE. 0.0) GO TO 2
        PUNIT(I,J)=PCASE(I,J)*1.0*10**(10)
        GO TO 1
2       PUNIT(I,J)=PCASE(I,J)/CASFAC(I)
1       CONTINUE
        RETURN
        END
C
C
C
C
C
C   THIS SUBROUTINE CHECKS TO SEE IF ALL FILES HAVE THE SAME NUMBER OF
C   RECORDS.  IF THEY DO NOT, THE USER CANNOT PROCEED
C
        SUBROUTINE CHECK
        DIMENSION N(6),FILE(6)
        DATA FILE/'HSTY1','HSTY2','SALES','INVTY','FACTR','NAMES'/
        DO 1 I=1,6
        CALL IFILE(20,FILE(I))
        READ(20,2)N(I)
2       FORMAT(I)
        END FILE 20
1       CONTINUE
        DO 3 I=1,6-1
        IF(N(I) .NE. N(I+1))GO TO 4
3       CONTINUE
        RETURN
4       TYPE 5
        TYPE 6,(FILE(I),I=1,6)
5       FORMAT(//' ERROR-# OF RECORDS FOR EACH DATA FILE DONOT MATCH?'
     1  ,'-VALUES ARE:'/)
6       FORMAT(1X,6(A5,3X)/)
        TYPE 7,(N(I),I=1,6)
7       FORMAT(1X,6(I4,4X)//)
        CALL EXIT
        END
 D*&ô