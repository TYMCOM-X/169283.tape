IDENTIFICATION DIVISION.
PROGRAM-ID.  COBRG      COBOL REPORT GENERATOR VERSION 1A, EDIT 2.
AUTHOR. AL BLACKINGTON/CAM.
INSTALLATION. DIGITAL EQUIPMENT CORP.
DATE-WRITTEN.  FEB - MAR 1968; CONVERTED TO COBOL-68 OCTOBER 1970.
DATE-COMPILED.
SECURITY. COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORP., MAYNARD, MA.

REMARKS.
	IF LINE-PRINTER HAS ONLY 120 COLUMNS, CHANGE VALUE OF 'PRINTER-SIZE' TO 120.

	NO LINE NUMBERS ARE PUNCHED ON THE OUTPUT CARDS.  IF DESIRED,
	SEE 'PUNCH-REC' IN DATA DIVISION, AND 'PUNCH-CARD' & 'PUNCH-TOP-FORM' IN PROCEDURE
	DIVISION.

ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
OBJECT-COMPUTER. DECsystem-10; SEGMENT-LIMIT IS 1.
SPECIAL-NAMES.  CHANNEL (1) IS TOP-OF-FORM.

INPUT-OUTPUT SECTION.
FILE-CONTROL.
	SELECT CARD		ASSIGN TO DSK.
	SELECT GENERATED-PROG	ASSIGN TO DSK.
	SELECT LISTING		ASSIGN TO DSK.
	SELECT TRAN-FILE-IN	ASSIGN TO DSK;
		ACCESS MODE IS RANDOM;
		FILE-LIMIT IS 999;
		ACTUAL KEY IS TRAN-KEY.
	SELECT TRAN-FILE-OUT	ASSIGN TO DSK.
DATA DIVISION.
FILE SECTION.

FD  GENERATED-PROG
	LABEL RECORDS ARE STANDARD
	VALUE OF IDENTIFICATION IS CBL-VAL-ID
	DATA RECORD IS PUNCH-REC.

01  PUNCH-REC DISPLAY-7.
*	IF LINE NUMBERS WANTED, PUT:
*		02 PUNCH-1-6 PIC X(6).
*		02 FILLER    PIC X.

	02 PUNCH-8		PICTURE X.
	02 FILLER		PICTURE XX.
	02 PUNCH-11		PICTURE X.
	02 PUNCH-12		PICTURE X.
	02 PUNCH-13		PICTURE X.
	02 PUNCH-14		PICTURE X.
	02 PUNCH-15		PICTURE X.
	02 PUNCH-16		PICTURE X.
	02 PUNCH-17		PICTURE X.
	02 PUNCH-18		PICTURE X.
	02 PUNCH-19		PICTURE X.
	02 PUNCH-20		PICTURE X.
	02 PUNCH-21		PICTURE X.
	02 PUNCH-22		PICTURE X.
	02 PUNCH-23		PICTURE X.
	02 PUNCH-24		PICTURE X.
	02 PUNCH-25		PICTURE X.
	02 PUNCH-26		PICTURE X.
	02 PUNCH-27		PICTURE X.
	02 PUNCH-28		PICTURE X.
	02 PUNCH-29		PICTURE X.
	02 FILLER		PICTURE XX.
	02 PUNCH-32		PICTURE X.
	02 PUNCH-33		PICTURE X.
	02 FILLER		PICTURE X(3).
	02 PUNCH-37		PICTURE X.
	02 PUNCH-38		PICTURE X.
	02 PUNCH-39		PICTURE X.
	02 PUNCH-40		PICTURE X.
	02 PUNCH-41		PICTURE X.
	02 FILLER		PICTURE XX.
	02 PUNCH-44		PICTURE X.
	02 PUNCH-45		PICTURE X.
	02 PUNCH-46		PICTURE X.
	02 PUNCH-47		PICTURE X.
	02 PUNCH-48		PICTURE X.
	02 PUNCH-49		PICTURE X.
	02 PUNCH-50		PICTURE X.
	02 FILLER		PICTURE XXX.
	02 PUNCH-54		PICTURE X.
	02 PUNCH-55		PICTURE X.
	02 PUNCH-56		PICTURE X.
	02 FILLER		PICTURE XX.
	02 PUNCH-59		PICTURE X.
	02 PUNCH-60		PICTURE X.
	02 PUNCH-61		PICTURE X.
	02 PUNCH-62		PICTURE X.
	02 PUNCH-63		PICTURE X.
	02 PUNCH-64		PICTURE X.
	02 PUNCH-65		PICTURE X.
	02 FILLER		PICTURE XX.
	02 PUNCH-68		PICTURE X.
	02 PUNCH-69		PICTURE X.
	02 FILLER		PICTURE XX.
	02 PUNCH-72		PICTURE X.
	02 FILLER		PICTURE X(15).

	66 PUNCH-8-11  RENAMES PUNCH-8  THRU PUNCH-11.
	66 PUNCH-8-18  RENAMES PUNCH-8  THRU PUNCH-18.
	66 PUNCH-8-23  RENAMES PUNCH-8  THRU PUNCH-23.
	66 PUNCH-8-24  RENAMES PUNCH-8  THRU PUNCH-24.
	66 PUNCH-8-72  RENAMES PUNCH-8  THRU PUNCH-72.
	66 PUNCH-12-16 RENAMES PUNCH-12 THRU PUNCH-16.
	66 PUNCH-12-20 RENAMES PUNCH-12 THRU PUNCH-20.
	66 PUNCH-12-24 RENAMES PUNCH-12 THRU PUNCH-24.
	66 PUNCH-12-28 RENAMES PUNCH-12 THRU PUNCH-28.
	66 PUNCH-12-37 RENAMES PUNCH-12 THRU PUNCH-37.
	66 PUNCH-12-39 RENAMES PUNCH-12 THRU PUNCH-39.
	66 PUNCH-12-44 RENAMES PUNCH-12 THRU PUNCH-44.
	66 PUNCH-12-45 RENAMES PUNCH-12 THRU PUNCH-45.
	66 PUNCH-12-72 RENAMES PUNCH-12 THRU PUNCH-72.
	66 PUNCH-16-72 RENAMES PUNCH-16 THRU PUNCH-72.
	66 PUNCH-17-40 RENAMES PUNCH-17 THRU PUNCH-40.
	66 PUNCH-24-39 RENAMES PUNCH-24 THRU PUNCH-39.
	66 PUNCH-40-47 RENAMES PUNCH-40 THRU PUNCH-47.
	66 PUNCH-40-72 RENAMES PUNCH-40 THRU PUNCH-72.
	66 PUNCH-41-44 RENAMES PUNCH-41 THRU PUNCH-44.
	66 PUNCH-41-72 RENAMES PUNCH-41 THRU PUNCH-72.
	66 PUNCH-45-48 RENAMES PUNCH-45 THRU PUNCH-48.
	66 PUNCH-45-69 RENAMES PUNCH-45 THRU PUNCH-69.
	66 PUNCH-46-55 RENAMES PUNCH-46 THRU PUNCH-55.
	66 PUNCH-48-54 RENAMES PUNCH-48 THRU PUNCH-54.
	66 PUNCH-49-72 RENAMES PUNCH-49 THRU PUNCH-72.
	66 PUNCH-55-68 RENAMES PUNCH-55 THRU PUNCH-68.
	66 PUNCH-56-65 RENAMES PUNCH-56 THRU PUNCH-65.
	66 PUNCH-56-69 RENAMES PUNCH-56 THRU PUNCH-69.
FD  LISTING
	LABEL RECORDS ARE STANDARD
	VALUE OF IDENTIFICATION IS LIST-FILE-EXT.

01  P-L DISPLAY-7.
	02 PL-1-20		PICTURE X(20).
	02 FILLER		PICTURE X(100).
01  PL-N  DISPLAY-7.
	02 PL-CODE		PICTURE X.
	02 FILLER		PICTURE X.
	02 PL-ID		PICTURE X(6).
	02 FILLER		PICTURE X(4).
	02 PL-BLOCKING		PICTURE XXX.
	02 FILLER		PICTURE X(6).
	02 PL-INPUT-DEV		PICTURE X(6).
	02 FILLER		PICTURE XX.
	02 PL-OUTPUT-DEV	PICTURE X(6).
	02 FILLER		PICTURE X(4).
	02 PL-IN-VAL		PICTURE X(9).
	02 FILLER		PICTURE X(4).
	02 PL-OUT-VAL		PICTURE X(9).
	02 FILLER		PICTURE XX.
	02 PL-DATE		PICTURE X(9).
	02 FILLER		PICTURE XX.
	02 PL-AUTHOR 		PICTURE X(20).
01  PL-B  DISPLAY-7.
	02 FILLER		PICTURE X(4).
	02 PL-LEVEL		PICTURE X.
	02 FILLER		PICTURE X(7).
	02 PL-SPB		PICTURE X.
	02 PL-SKB		PICTURE X.
	02 FILLER		PICTURE X(5).
	02 PL-SPA		PICTURE X.
	02 PL-SKA		PICTURE X.
	02 FILLER		PICTURE X(5).
	02 PL-BRK-SIZE		PICTURE XXX.
	02 FILLER		PICTURE X(4).
	02 PL-BRK-NAME		PICTURE X(24).
01  PL-A DISPLAY-7.
	02 FILLER		PICTURE X(3).
	02 PL-AC-NUM 		PICTURE X.
	02 FILLER		PICTURE X(3).
	02 PL-AC-SIZE		PICTURE XX.
	02 FILLER		PICTURE X(4).
	02 PL-AC-POINT		PICTURE XX.
	02 FILLER		PICTURE X(7).
	02 PL-AC-IN-NAME	PICTURE X(24).
	02 FILLER		PICTURE X(4).
	02 PL-AC-OUT-NAME	PICTURE X(24).
01  PL-H  DISPLAY-7.
	02 FILLER		PICTURE X(3).
	02 PL-PAGE-SIZE		PICTURE X.
	02 FILLER		PICTURE XX.
	02 PL-PAGE-LOC		PICTURE XXX.
	02 FILLER		PICTURE X(3).
	02 PL-HDR-SP 		PICTURE X.
	02 PL-HDR-SK 		PICTURE X.
	02 FILLER		PICTURE X(4).
	02 PL-PAGE-RESET	PICTURE X(10).
	02 FILLER		PICTURE X.
	02 PL-HDRS.
	  03 PL-HDR-1		PIC X(30).
	  03 PL-HDR-2		PIC X(30).
	  03 FILLER		PIC X(31).
	02 PL-HDRS-1 REDEFINES PL-HDRS.
	  03 PL-HDR-3		PIC X(33).
	  03 PL-HDR-4		PIC X(33).
	  03 FILLER		PIC X(25).
	02 PL-HDRS-2 REDEFINES PL-HDRS.
	  03 FILLER		PIC X(61).
	  03 PL-ERROR		PIC X(30).
01  PL-T DISPLAY-7.
	02 FILLER		PICTURE X(7).
	02 PL-T-AC-NUM		PICTURE X.
	02 FILLER		PICTURE X(11).
	02 PL-T-LEVELS		PICTURE X(11).
	02 FILLER		PICTURE X(4).
	02 PL-T-OUT-NAME	PICTURE X(24).
01  PL-L DISPLAY-7.
	02 FILLER		PICTURE X(4).
	02 PL-L-WHEN 		PICTURE X.
	02 FILLER		PICTURE X(5).
	02 PL-L-IN-NAME		PICTURE X(24).
	02 FILLER		PICTURE X(8).
	02 PL-L-OUT-NAME	PICTURE X(24).
FD  CARD
	LABEL RECORDS ARE STANDARD
	VALUE OF IDENTIFICATION IS CARD-FILE-EXT.

01	CARD-RECORD; DISPLAY-7.
	02 CARD-CODE		PIC X.
	02 FILLER		PIC X(79).
FD  TRAN-FILE-IN
	BLOCK CONTAINS 1 RECORD
	LABEL RECORDS ARE STANDARD
	VALUE OF IDENTIFICATION IS TRAN-VAL-ID-IN.

01  TRAN-RECORD-IN; DISPLAY-7.
	02 TRAN-EOF-IN		PICTURE X.
	02 TRAN-CHAR PIC X; OCCURS 79 TIMES.

FD	TRAN-FILE-OUT
	BLOCK CONTAINS 1 RECORD
	LABEL RECORDS ARE STANDARD
	VALUE OF IDENTIFICATION IS TRAN-VAL-ID-OUT.

01	TRAN-RECORD-OUT; DISPLAY-7.
	02 TRAN-EOF-OUT		PICTURE X.
	02 FILLER		PICTURE X(79).
WORKING-STORAGE SECTION.


77	JOB-NUMBER	PIC S999; COMP.
77	EOF-FLAG		PICTURE X.
	88 MORE-INPUT VALUE SPACE.
	88 NO-MORE-INPUT VALUE "X".
77  HOLD-X			PICTURE X.
77  HOLD-N REDEFINES HOLD-X; PICTURE 9.
77  LEVEL			PICTURE S99; COMPUTATIONAL.
77  INDX 			PICTURE S99; COMPUTATIONAL.
77  INDX-1			PICTURE S99; COMPUTATIONAL.
77  LINE-NUMBER		PICTURE S9(6); COMP.
77  LINE-COUNT		PICTURE S999; COMPUTATIONAL.
77  PAGE-COUNT		PICTURE S999; COMP.
77  H-CARD-COUNT 		PICTURE S9; COMPUTATIONAL.
77  A-CARD-COUNT 		PICTURE S99; COMPUTATIONAL.
77  LOW-INDX		PICTURE S99; COMPUTATIONAL.
77  PREV-CARD-CODE		PICTURE X.
77  TEST-FOR-DETAIL		PICTURE X.
	88 THERE-IS-NO-DETAIL VALUE "N".
77  HOLD-Y			PICTURE X.
77  HEADER-1		PICTURE X(120).
77  HEADER-2		PICTURE X(120).
77  HOLD-I-KEY		PICTURE S9(7); COMPUTATIONAL.
77  HOLD-P-KEY		PICTURE S9(7); COMPUTATIONAL.
77  TRAN-KEY		PICTURE S9(7); COMPUTATIONAL.
77	TFO-RECORD-NUMBER	PICTURE S9(10); COMPUTATIONAL.
77	HI-TRAN-LIMIT		PICTURE S9(10); COMPUTATIONAL; VALUE 999.
77	HIGHEST-SPACE		PICTURE S9; COMPUTATIONAL; VALUE 9.
77	HIGHEST-CHANNEL		PICTURE S9; COMPUTATIONAL; VALUE 8.
77	PRINTER-SIZE		PICTURE S999; COMP; VALUE 132.

77	I INDEX.
77	J INDEX.
77	CHAR			PICTURE X; DISPLAY-7.
77	IN-DEVICE		PICTURE X(7).
77	OUT-DEVICE		PICTURE X(7).
77	IN-VAL-ID		PICTURE X(9).
77	OUT-VAL-ID		PICTURE X(9).
77	TRAN-VAL-ID-OUT		PICTURE X(9).
01	COMMAND; DISPLAY-7.
	02 COMMAND-CHAR OCCURS 72 TIMES; PICTURE X.

01	FILE-AND-EXT; DISPLAY-7.
	02 FILE-EXT-CHAR OCCURS 9 TIMES; PICTURE X.

01	CARD-FILE-EXT.
	02 CARD-FILE-NAME	PICTURE X(6).
	02 CARD-EXT-NAME	PICTURE XXX.
01	LIST-FILE-EXT.
	02 LIST-FILE-NAME	PICTURE X(6).
	02 LIST-EXT-NAME	PICTURE XXX.

01	TRAN-VAL-ID-IN.
	02 TRAN-JOB-NUM		PICTURE 999.
	02 FILLER		PICTURE X(6); VALUE "CRGTMP".

01	CBL-VAL-ID.
	02 CBL-VAL-ID-FILE	PICTURE X(6).
	02 CBL-VAL-ID-EXT	PICTURE XXX; VALUE "CBL".

01  N-CARD DISPLAY-7.
	02 CARD-ID 		PICTURE X(6).
	02 CARD-BLOCKING	PICTURE 999.
	02 CARD-IN-DEV		PIC X(6).
	02 CARD-OUT-DEV		PIC X(6).
	02 CARD-IN-VAL-ID	PIC X(9).
	02 CARD-OUT-VAL-ID	PIC X(9).
	02 CARD-DATE 		PICTURE X(9).
	02 CARD-AUTHOR		PICTURE X(20).
01  B-CARD REDEFINES N-CARD; DISPLAY-7.
	02 CARD-LEVEL		PICTURE X.
	02 CARD-SPACING.
		03 CARD-SPB		PICTURE 9.
		03 CARD-SKB		PICTURE 9.
		03 CARD-SPA		PICTURE 9.
		03 CARD-SKA		PICTURE 9.
	02 CARD-BRK-SIZE	PICTURE 999.
	02 CARD-BRK-NAME	PICTURE X(24).
01  H-CARD REDEFINES N-CARD; DISPLAY-7.
	02 CARD-PAGE-SIZE	PICTURE 9.
	02 CARD-PAGE-LOC	PICTURE 999.
	02 CARD-HDR-SP		PICTURE 9.
	02 CARD-HDR-SK		PICTURE 9.
	02 CARD-HDRS.
	  03 CARD-HDR-1		PICTURE X(30).
	  03 CARD-HDR-2		PICTURE X(30).
	  03 FILLER		PICTURE X(6).
	02 CARD-PAGE-RESET.
	  03 CPR OCCURS 10 TIMES; PICTURE X.
01  A-CARD REDEFINES N-CARD; DISPLAY-7.
	02 CARD-AC-NUM		PICTURE 9.
	02 CARD-AC-SIZE		PICTURE 99.
	02 CARD-AC-POINT	PICTURE 99.
	02 CARD-AC-IN-NAME	PICTURE X(24).
	02 CARD-AC-OUT-NAME	PICTURE X(24).
01  T-CARD REDEFINES N-CARD; DISPLAY-7.
	02 CARD-T-AC-NUM	PICTURE 9.
	02 CARD-T-OUT-NAME	PICTURE X(24).
	02 CARD-T-LEVELS.
		04 CTL OCCURS 11 TIMES; PICTURE X.
01  L-CARD REDEFINES N-CARD; DISPLAY-7.
	02 CARD-L-WHEN		PICTURE X.
	02 CARD-L-IN-NAME	PICTURE X(24).
	02 CARD-L-OUT-NAME	PICTURE X(24).

01	IO-CARD REDEFINES N-CARD; PICTURE X(80); DISPLAY-7.

01	MISC-CARD-STUFF REDEFINES N-CARD; DISPLAY-7.
*	REDEFINITION OF CARD-LEVEL:
	02 CL-REDEF		PICTURE 9.
01	H2-CARD REDEFINES N-CARD; DISPLAY-7.
	02 CARD-HDR-3		PICTURE X(33).
	02 CARD-HDR-4		PICTURE X(33).

01	MORE-MISC-CARD-STUFF REDEFINES N-CARD; DISPLAY-7.
*	REDEFINITION OF CARD-L-WHEN:
	02 CARD-L-WHEN-N	PICTURE 9.
01	SET-PERIOD.
	02 SPER-1		PICTURE X.
	02 FILLER		PICTURE X(5).
	02 SPER-7		PICTURE X.
	02 FILLER		PICTURE XX.
	02 SPER-10		PICTURE X.
	02 FILLER		PICTURE X(10).
	02 SPER-21		PICTURE X.
	02 FILLER		PICTURE XXX.
	02 SPER-25		PICTURE X.
01	DUMMY-SPER REDEFINES SET-PERIOD.
	02 SPER OCCURS 25 TIMES; PICTURE X.
66	SPER-1-7 RENAMES SPER-1 THRU SPER-7.
66	SPER-1-10 RENAMES SPER-1 THRU SPER-10.
66	SPER-1-21 RENAMES SPER-1 THRU SPER-21.
66	SPER-1-25 RENAMES SPER-1 THRU SPER-25.


01	NOT-AN-N-CARD;	DISPLAY-7.
	02 FILLER	PICTURE X(7);	VALUE "NOT AN ".
	02 FILLER	PICTURE X;	VALUE QUOTE.
	02 FILLER	PICTURE X;	VALUE "N".
	02 FILLER	PICTURE X;	VALUE QUOTE.
	02 FILLER	PICTURE X(5);	VALUE " CARD".
	02 FILLER	PICTURE X(15);	VALUE SPACES.

01	MORE-THAN-1-BATCH;	DISPLAY-7.
	02 FILLER	PICTURE X(21);	VALUE "MORE THAN 1 BATCH OF ".
	02 FILLER	PICTURE X;	VALUE QUOTE.
	02 MT1B-A	PICTURE X.
	02 FILLER	PICTURE X;	VALUE QUOTE.
	02 FILLER	PICTURE X(6);	VALUE " CARDS".
01  BREAK-FIELDS.
	02 BF-ENTRY OCCURS 25 TIMES; INDEXED BY BF-INDX.
	  03 BF-LEVEL		PICTURE X.
	  03 BF-SIZE		PICTURE 999.
	  03 BF-NAME 		PICTURE X(24).

*	ONE ENTRY OF PRINTER CONTROL INFORMATION FOR EACH LEVEL OF BREAK
01  SKIP-TABLE.
	02 ST-ENTRY OCCURS 11 TIMES.
	  03 ST-SKIP-A		PICTURE X.
	  03 ST-SKIP-B		PICTURE X.
	  03 ST-SPACE-A		PICTURE X.
	  03 ST-SPACE-B		PICTURE X.

*	TABLE OF ACCUMULATOR DEFINITIONS

01  ACCUM-TABLE.
	02 AT-ENTRY OCCURS 10 TIMES.
	  03 AC-SIZE 		PICTURE 99.
	  03 AC-DECIMAL		PICTURE 99.
	  03 AC-IN-NAME		PICTURE X(24).
	  03 AC-LIST-NAME	PICTURE X(24).
	  03 AC-PICTURE		PICTURE X(13).
*	THE FOLLOWING ITEM RESETS ACCUM-TABLE
01	AT-RESET.
	02 FILLER		PICTURE 9999; VALUE ZEROES.
	02 FILLER		PICTURE X(61); VALUE SPACES.

*	THIS TABLE IS FOR THE PRINT-OUTPUT FILE.
*	FOR EACH ACCUMULATOR (0-9, TABLE BTT-PRINT), SPECIFY ON WHICH BREAKS
*		TO PRINT TOTALS
01	BREAK-TOTALS-TABLE.
	02 BTT-PRINT OCCURS 10 TIMES.
	  03 BTTP-FLAGS.
		04 BTTPF OCCURS 11 TIMES; PICTURE X.
	  03 BTTP-NAME		PICTURE X(24).

01  HOLD-FLAGS.
	02 H-FLGS OCCURS 11 TIMES; PICTURE X.

*	TABLE OF LIST INFORMATION

01  LIST-TABLE; DISPLAY-7.
	02 LT-ENTRY OCCURS 50 TIMES; INDEXED BY LT-INDX.
	  03 LT-IN-NAME		PICTURE X(24).
	  03 LT-OUT-NAME	PICTURE X(24).
	  03 LT-WHEN 		PICTURE X.
*	TABLE OF EMIT INFORMATION

01	EMIT-TABLE; DISPLAY-7.
	02 EM-ENTRY OCCURS 50 TIMES; INDEXED BY EM-INDX.
	  03 EM-WHEN		PICTURE X.
	  03 EM-IN-NAME		PICTURE X(24).
	  03 EM-OUT-NAME	PICTURE X(24).

*	TABLE OF HEADER INFORMATION.

01  HEADER-INFO.
	02 PAGE-SIZE		PICTURE 9.
	02 PAGE-LOC		PICTURE 999.
	02 HDR-SP		PICTURE 9.
	02 HDR-SK		PICTURE 9.
	02 HEADER-RESET.
	  03 HR-WHEN OCCURS 11 TIMES; PICTURE X.
	02 PAGE-HDR-1		PICTURE X(30).
	02 PAGE-HDR-2		PICTURE X(30).
	02 PAGE-HDR-3		PICTURE X(33).
	02 PAGE-HDR-4		PICTURE X(33).

*	TABLE OF DEFINED BREAKS.

01  BREAK-TABLE.
	02 BT-ENTRY.
	  03 BREAKS OCCURS 11 TIMES; PICTURE X.
	02 NEXT-BREAK OCCURS 11 TIMES; PICTURE X.

01  CHANNEL-TABLE.
	02 CHAN-ENTRY OCCURS   9 TIMES; PICTURE X.
	02 SPACE-ENTRY OCCURS 10 TIMES; PICTURE X.
01  CHANNEL-LINE.
	02 FILLER		PICTURE X(9)  VALUE "CHANNEL (".
	02 CHL-NUMBER-A		PICTURE X.
	02 FILLER		PICTURE X(13) VALUE ") IS CHANNEL-".
	02 CHL-NUMBER-B		PICTURE X.



01  SAVE-FIELD-AC.
	02 FILLER		PICTURE X(9)  VALUE "77  SAVE-".
	02 SFA-NUM		PICTURE 99.
	02 FILLER		PICTURE X(21) VALUE SPACES.
	02 FILLER		PICTURE X(8)  VALUE "PICTURE ".
	02 SFA-SIZE.
	  03 FILLER		PICTURE XX.
	  03 SFAS-A.
		04 SFAS-B.
	 05 SFAS-C		PICTURE X.
	 05 FILLER		PICTURE X.
		04 FILLER		PICTURE X.
	  03 FILLER		PICTURE XX.
	02 FILLER		PICTURE X(17) VALUE SPACES.

01  PROG-ID-LINE.
	02 FILLER		PICTURE X(12)  VALUE "PROGRAM-ID. ".
	02 PID-NAME		PICTURE X(7).
01  DATE-WRITTEN-LINE.
	02 FILLER		PICTURE X(14) VALUE "DATE-WRITTEN. ".
	02 DWL-DATE		PICTURE X(10).

01  AUTHOR-LINE.
	02 FILLER		PICTURE X(8)  VALUE "AUTHOR. ".
	02 AU-NAME		PICTURE X(21).

01  SELECT-CLAUSE.
	02 FILLER		PICTURE X(7)  VALUE "SELECT ".
	02 SEL-NAME		PICTURE X(7).
	02 FILLER		PICTURE X(14) VALUE SPACES.
	02 FILLER		PICTURE X(10) VALUE "ASSIGN TO ".
	02 SEL-DEVICE		PICTURE X(7).

01  BLOCK-CONTAINS-LINE.
	02 FILLER		PICTURE X(15); VALUE "BLOCK CONTAINS ".
	02 BLC-VALUE		PICTURE Z9.
	02 FILLER		PICTURE X(8); VALUE " RECORDS".

01	VALUE-OF-ID-LINE.
	02 FILLER		PICTURE X(27); VALUE "VALUE OF IDENTIFICATION IS ".
	02 FILLER		PICTURE X; VALUE QUOTE.
	02 VIDL-NAME		PICTURE X(9).
	02 FILLER		PICTURE X; VALUE QUOTE.
	02 FILLER		PICTURE X; VALUE ".".

01  FILLER-LINE.
	02 FILLER		PICTURE X(9); VALUE "02 FILLER".
	02 FILLER		PICTURE X(19); VALUE SPACES.
	02 FILLER		PICTURE X(10); VALUE "PICTURE X(".
	02 FILL-LINE-1.
	  03 FL-AMT-1A		PIC X.
	  03 FL-AMT-1B		PIC XXXX.
	02 FILL-LINE-2 REDEFINES FILL-LINE-1.
	  03 FL-AMT-2A		PIC XX.
	  03 FL-AMT-2B		PIC XXX.
	02 FILL-LINE-3 REDEFINES FILL-LINE-1.
	  03 FL-AMT-3A		PIC XXX.
	  03 FL-AMT-3B		PIC XX.
01  PAGE-COUNT-LINE.
	02 FILLER		PICTURE X(14) VALUE "02 HEADER-PAGE".
	02 FILLER		PICTURE X(14) VALUE SPACES.
	02 FILLER		PICTURE X(8)  VALUE "PICTURE ".
	02 PCL-SIZE.
	  03 FILLER		PICTURE XX.
	  03 PCLS-1		PICTURE 9.
	  03 FILLER		PICTURE X(3).
	02 FILLER		PICTURE X(19) VALUE SPACES.

01  HDR-VALUE-LINE.
	02 FILLER		PIC X(15); VALUE "02 FILL PIC X(3".
	02 HDR-Q-SIZE		PIC X.
	02 FILLER		PIC X(9); VALUE "); VALUE ".
	02 HDR-Q-1		PICTURE X.
	02 HDR-VALUE		PICTURE X(30).
	02 HDR-Q-2		PIC X.
	02 HDR-Q-3		PIC X(4).
01	HDR-VALUE-LINE-REDEF REDEFINES HDR-VALUE-LINE.
	02 FILLER		PIC X(25).
	02 HDR-Q-4		PIC X.
	02 HDR-VALUE-1		PIC X(33).
	02 HDR-Q-5		PIC X.
	02 HDR-Q-6		PIC X.

01  HOLD-PC-A.
	02 HPC-K		PICTURE X.
	02 HPC-L		PICTURE X(12).
	01 DUMMY-1 REDEFINES HOLD-PC-A.
	02 HPC-A.
	  03 FILLER		PICTURE X(3).
	  03 HPC-B.
		04 FILLER		PICTURE X.
		04 HPC-C 		PICTURE 9.
	  03 FILLER		PICTURE X.
	02 HPC-D.
	  03 FILLER		PICTURE X(3).
	  03 HPC-E.
		04 HPC-F 		PICTURE 9.
		04 FILLER		PICTURE X.
	  03 FILLER		PICTURE XX.
01  HOLD-PC-B.
	02 HPC-G		PICTURE X     VALUE "S".
	02 HPC-H		PICTURE X(7).
	02 FILLER		PICTURE X(5)  VALUE SPACES.

01  ZZ-TABLE	   PICTURE X(18); VALUE "Z9.   ZZ9.  ZZZ9. ".
	01 DUMMY-2 REDEFINES ZZ-TABLE.
	  02 ZZT-X OCCURS 3 TIMES;	PICTURE X(6).

01  XX-TABLE	   PICTURE X(21); VALUE "X.     XX.    XXX.   ".
	 01 DUMMY-3 REDEFINES XX-TABLE.
	  02 XXT-X OCCURS 3 TIMES;	PICTURE X(7).
01  TEMP-3.
	02 FILLER		PICTURE X.
	02 TEMP-2.
	  03 FILLER		PICTURE X.
	  03 TEMP-1		PICTURE 9.
01	TEMP-3N REDEFINES TEMP-3; PIC S999.


01  ACCUM-02-LINE.
	02 FILLER		PICTURE X(3) VALUE "02 ".
	02 FILLER		PICTURE X(12); VALUE "ACCUMULATOR-".
	02 AL-AC		PICTURE 9.
	02 FILLER		PICTURE X(13) VALUE SPACES.
	02 FILLER		PICTURE X(8)  VALUE "PICTURE ".
	02 AL-PICTURE		PICTURE X(13).


01  ADD-TO-LEVEL-0.
	02 FILLER		PICTURE X(4) VALUE "ADD ".
	02 AL0-NAME		PICTURE X(24).
	02 FILLER		PICTURE X(16); VALUE " TO ACCUMULATOR-".
	02 AL0-AC		PICTURE 9.
	02 FILLER		PICTURE X(10); VALUE " OF LEVEL-".
	02 AL0-LOWEST		PICTURE X.
	02 FILLER		PICTURE X(5); VALUE "-ACS.".


01  ADD-TO-NEXT-LINE.
	02 FILLER		PICTURE X(24); VALUE "ADD CORRESPONDING LEVEL-".
	02 AN-LEVEL-A		PICTURE 9.
	02 FILLER		PICTURE X(14); VALUE "-ACS TO LEVEL-".
	02 AN-LEVEL-B		PICTURE X.
	02 FILLER		PICTURE X(5); VALUE "-ACS.".

01  LIST-AC-LINE.
	02 FILLER		PICTURE X(5)  VALUE "MOVE ".
	02 LAC-IN-NAME		PICTURE X(24).
	02 FILLER		PICTURE X(4)  VALUE " TO ".
	02 LAC-OUT-NAME		PICTURE X(25).
	02 FILLER		PICTURE X     VALUE SPACES.
01  SAVLD-LINE.
	02 FILLER		PICTURE X(5)  VALUE "MOVE ".
	02 SF-NAME		PICTURE X(24).
	02 FILLER		PICTURE X(9)  VALUE " TO SAVE-".
	02 SF-NUMBER 		PICTURE XX.
	02 FILLER		PICTURE X     VALUE ".".
	02 FILLER		PICTURE X(19) VALUE SPACES.


01  GO-TO-LINE.
	02 FILLER		PICTURE X(10) VALUE "    GO TO ".
	02 FILLER		PICTURE X(6)  VALUE "LEVEL-".
	02 LOW-BREAK 		PICTURE X.
	02 FILLER		PICTURE X(7)  VALUE "-BREAK.".


01  GO-TO-HI.
	02 FILLER		PICTURE X(10) VALUE "GO TO RESE".
	02 FILLER		PICTURE X(8)  VALUE "T-LEVEL-".
	02 HIGH-BREAK		PICTURE X.
	02 FILLER		PICTURE X     VALUE ".".


01  IF-LINE.
	02 IFL-1		PICTURE XXX.
	02 FILLER		PICTURE X(5)  VALUE "SAVE-".
	02 IFL-3		PICTURE XX.
	02 FILLER		PICTURE X(17) VALUE " IS NOT EQUAL TO ".
	02 IFL-2		PICTURE X(24).


01  AC-TOTAL-LINE.
	02 FILLER		PICTURE X(17); VALUE "MOVE ACCUMULATOR-".
	02 ACTL-2		PICTURE 9.
	02 FILLER		PICTURE X(10); VALUE " OF LEVEL-".
	02 ACTL-1		PICTURE X.
	02 FILLER		PICTURE X(8); VALUE "-ACS TO ".
	02 ACTL-3		PICTURE X(25).

01	SET-LINE-COUNT.
	02 FILLER		PICTURE X(5) VALUE "MOVE ".
	02 SLC-AMOUNT		PICTURE 99.
	02 FILLER		PICTURE X(15) VALUE " TO LINE-COUNT.".
01  HEADER.
	02 FILLER		PICTURE X(46) VALUE SPACES.
	02 FILLER		PICTURE X(10) VALUE "P R O G R ".
	02 FILLER		PICTURE X(7)  VALUE "A M    ".
	02 HEADER-NAME		PICTURE XBXBXBXBXBX.
	02 FILLER		PICTURE X(37) VALUE SPACES.
	02 FILLER		PICTURE X(5)  VALUE "PAGE ".
	02 HEADER-PAGE		PICTURE ZZ9-.
PROCEDURE DIVISION.

MAIN-BODY SECTION.
*	THIS SECTION CONTAINS THE ENTIRE MAIN-LINE CODING.

START.
	PERFORM INITIALIZE-IO THRU IIO-EXIT.
RESTART.
	PERFORM INITIALIZE-TABLES.
	OPEN OUTPUT TRAN-FILE-OUT; MOVE 1 TO TFO-RECORD-NUMBER.
	PERFORM READ-IN-DATA-CARDS.
	CLOSE TRAN-FILE-OUT.
ERROR-SW. GO TO START-OUTPUT.  NOTE IF ANY ERRORS WERE DETECTED
		IN THE INPUT, THIS WILL GO TO INPUT-ERROR.
INPUT-ERROR. DISPLAY "?ERRORS IN PROGRAM ", PID-NAME.
	GO TO END-TEST.
START-OUTPUT.
	OPEN OUTPUT GENERATED-PROG; MOVE SPACES TO PUNCH-REC.
	DISPLAY "GENERATING ", PID-NAME.
	OPEN INPUT TRAN-FILE-IN.
	PERFORM PUT-OUT-ID-ED THRU POIDED-EXIT.
	PERFORM PUT-OUT-DD THRU PODD-EXIT.
	PERFORM PUT-OUT-READ THRU POR-EXIT.
	IF LOW-BREAK IS EQUAL TO SPACE OR "F"
		MOVE "NOTE NO BREAK COMPARISONS." TO PUNCH-12-72;
		PERFORM PUNCH-CARD;
	ELSE
		PERFORM PUT-OUT-BREAK-TESTS THRU POBT-EXIT VARYING LEVEL
		    FROM 10 BY -1 UNTIL LEVEL IS EQUAL TO ZERO.

	PERFORM PUT-OUT-DETAIL-PRINT.

	IF LOW-BREAK IS NOT EQUAL TO SPACE
		PERFORM PUNCH-TOP-FORM;
		PERFORM PUT-OUT-BREAK-PRINTS THRU POBP-EXIT VARYING LEVEL
		    FROM 1 BY 1 UNTIL LEVEL IS > 11;
		PERFORM PUNCH-TOP-FORM;
		PERFORM PUT-OUT-RESETS THRU PORES-EXIT VARYING LEVEL
		    FROM 11 BY -1 UNTIL LEVEL IS EQUAL TO ZERO.

	PERFORM PUNCH-TOP-FORM.
	PERFORM PUT-OUT-SUBROUTINES.
	PERFORM PUT-OUT-FINISH.

	CLOSE GENERATED-PROG, TRAN-FILE-IN.

END-TEST.
	MOVE SPACES TO P-L; WRITE P-L BEFORE TOP-OF-FORM.
	IF MORE-INPUT GO TO RESTART.
FINISH.
	CLOSE CARD, LISTING.
	STOP RUN.
*	THIS SECTION CONTAINS SUBROUTINES USED BY MORE THAN ONE
*		NON-RESIDENT SECTION.

COMMON-ROUTINES SECTION.

PUNCH-TOP-FORM.
	MOVE SPACES TO PUNCH-REC.
	ADD 100 TO LINE-NUMBER.
*	IF LINE-NUMBERS DESIRED, PUT HERE: 'MOVE LINE-NUMBER TO PUNCH-1-6.'
	WRITE PUNCH-REC BEFORE TOP-OF-FORM.
	MOVE SPACES TO PUNCH-REC.



PUNCH-BLANK-CARD. NOTE THAT SINCE THE PUNCH-CARD ROUTINE ALWAYS
	CLEARS THE RECORD AFTER WRITING, WE WILL WRITE A BLANK
	RECORD (UNLESS SOMEONE PUT SOMETHING IN IT).


PUNCH-CARD.
	ADD 100 TO LINE-NUMBER.
*	IF LINE-NUMBERS WANTED, 'MOVE LINE-NUMBER TO PUNCH-1-6' GOES HERE
	WRITE PUNCH-REC.
	MOVE SPACES TO PUNCH-REC.

PRINT-ONLY.
	WRITE P-L.
	SUBTRACT 1 FROM LINE-COUNT.
	IF LINE-COUNT IS POSITIVE GO TO CLEAR-PRINT.

PRINT-HDR.
	MOVE SPACES TO P-L; WRITE P-L BEFORE TOP-OF-FORM.
PH-A.
	ADD 1 TO PAGE-COUNT; MOVE PAGE-COUNT TO HEADER-PAGE.
	WRITE P-L FROM HEADER BEFORE 2.
	MOVE 56 TO LINE-COUNT.

CLEAR-PRINT.
	MOVE SPACES TO P-L.

PP-EXIT.  EXIT.


PRINT-2.
	WRITE P-L BEFORE 2.
	SUBTRACT 2 FROM LINE-COUNT.
	IF LINE-COUNT IS POSITIVE GO TO CLEAR-PRINT;
		ELSE GO TO PRINT-HDR.
ERROR-OUT.
	ALTER ERROR-SW TO PROCEED TO INPUT-ERROR.
ERROR-OUT-A.
	ALTER PRINT-THE-LINE TO PROCEED TO PP-EXIT.
	PERFORM PRINT-ONLY THRU PP-EXIT.
EO-EXIT.   EXIT.


PRINT-THE-LINE. GO TO PRINT-ONLY.   NOTE IF ANY ERRORS WERE FOUND
		IN THE CARD, THE CARD HAS BEEN PRINTED, AND THIS WILL
		GO TO PP-EXIT.


SUB-HEADING.
	IF LINE-COUNT IS NOT GREATER THAN 5
		WRITE P-L; PERFORM PRINT-HDR THRU PP-EXIT;
	ELSE
		WRITE P-L BEFORE 2.
	MOVE HEADER-1 TO P-L; WRITE P-L.
	MOVE HEADER-2 TO P-L; WRITE P-L BEFORE 2.
	SUBTRACT 5 FROM LINE-COUNT.
	MOVE SPACES TO P-L.
SUB-HEAD-EXIT.	EXIT.
*	THIS SECTION CONTAINS THE INITIALIZATION ROUTINES.

INITIALIZATION SECTION 01.

INITIALIZE-IO.
	ENTER MACRO NUMJOB USING JOB-NUMBER.
	MOVE JOB-NUMBER TO TRAN-JOB-NUM; MOVE TRAN-VAL-ID-IN TO TRAN-VAL-ID-OUT.
	PERFORM GET-COMMAND.
	OPEN INPUT CARD.
	OPEN OUTPUT LISTING.
	ALTER IIO-SW TO PROCEED TO IIO-EXIT.
	GO TO READ-N-CARD.


NO-N-CARD.
	MOVE N-CARD TO P-L; MOVE NOT-AN-N-CARD TO PL-ERROR.
	PERFORM PRINT-ONLY THRU PP-EXIT.
	ALTER IIO-SW TO PROCEED TO SKIP-IIO.


READ-N-CARD.
	READ CARD; AT END GO TO FINISH.
	IF CARD-CODE IS NOT EQUAL TO "N" GO TO NO-N-CARD.


IIO-SW. GO TO. NOTE THIS WILL GO TO SKIP-IIO OR IIO-EXIT.
SKIP-IIO.
	MOVE SPACES TO P-L; WRITE P-L BEFORE TOP-OF-FORM.
IIO-EXIT.   EXIT.
INITIALIZE-TABLES.

	MOVE ZEROES TO LT-INDX, BF-INDX, EM-INDX.
	MOVE ZERO TO A-CARD-COUNT, H-CARD-COUNT.
	MOVE "N" TO TEST-FOR-DETAIL.
	MOVE ZERO TO PAGE-COUNT.
	MOVE SPACES TO BREAK-FIELDS,  BREAK-TOTALS-TABLE.
	PERFORM RESET-ACCUM-TABLE VARYING INDX FROM 1 BY 1
		UNTIL INDX > 10.
	MOVE SPACES TO LIST-TABLE, SKIP-TABLE, EMIT-TABLE.
	MOVE SPACES TO HEADER-INFO.
	MOVE ZEROES TO PAGE-SIZE, PAGE-LOC.
	MOVE SPACES TO BREAK-TABLE.
	MOVE 00     TO BLC-VALUE.
	MOVE SPACES TO CHANNEL-TABLE.
	MOVE SPACE TO LOW-BREAK, HIGH-BREAK.
	MOVE ZEROES TO HOLD-I-KEY, HOLD-P-KEY.
	MOVE 1 TO TRAN-KEY.
	MOVE SPACE TO EOF-FLAG.
	ALTER ERROR-SW TO PROCEED TO START-OUTPUT.

RESET-ACCUM-TABLE.
	MOVE AT-RESET TO AT-ENTRY (INDX).
*	SCAN COMMAND FOR 'FILE.EXT'.

SCAN-FILE-EXT.
	MOVE	SPACES TO FILE-AND-EXT; MOVE 1 TO J.
SFE-1.
	PERFORM SFE-9.
	IF CHAR = " " OR "_" OR "?" GO TO SFE-EXIT.
	IF CHAR = "." MOVE 7 TO J; GO TO SFE-2.
	IF J < 7
		MOVE CHAR TO FILE-EXT-CHAR (J);
		SET J UP BY 1.
	GO TO SFE-1.

SFE-2.
	PERFORM SFE-9.
	IF CHAR = " " OR "_" OR "." OR "?" GO TO SFE-EXIT.
	IF J < 10
		MOVE CHAR TO FILE-EXT-CHAR (J);
		SET J UP BY 1.
	GO TO SFE-2.

SFE-9.
	IF I > 71
		MOVE SPACE TO CHAR;
	ELSE SET I UP BY 1; MOVE COMMAND-CHAR (I) TO CHAR.

	IF CHAR = " " OR "_" OR "." NEXT SENTENCE; ELSE
	IF CHAR NOT > "Z" AND NOT < "A" OR
		CHAR NOT > "9" AND NOT < "0" NEXT SENTENCE;
	ELSE MOVE "?" TO CHAR.

SFE-EXIT. EXIT.

*	SCAN COMMAND FOR 'FILE.EXT_FILE.EXT'

GET-COMMAND.
	DISPLAY "*"; ACCEPT COMMAND.
	MOVE ZERO TO I; PERFORM SCAN-FILE-EXT THRU SFE-EXIT.
	IF CHAR NOT EQUAL TO "_"
		DISPLAY "?BAD COBRG COMMAND";
		GO TO GET-COMMAND.
	MOVE FILE-AND-EXT TO LIST-FILE-EXT.

	PERFORM SCAN-FILE-EXT THRU SFE-EXIT.
	IF CHAR NOT = SPACE
		DISPLAY "?BAD COBRG COMMAND";
		GO TO GET-COMMAND.
	MOVE FILE-AND-EXT TO CARD-FILE-EXT.

	IF CARD-FILE-NAME = SPACES
		DISPLAY "?NO INPUT FILE NAME";
		GO TO GET-COMMAND.

	IF LIST-FILE-NAME = SPACES MOVE CARD-FILE-NAME TO LIST-FILE-NAME.
	IF LIST-EXT-NAME = SPACES MOVE "LST" TO LIST-EXT-NAME.
*	THIS SECTION READS THE PARAMETER FILE; IT IS ENTERED AFTER
*		THE "N" CARD HAS BEEN READ.


READ-IN-DATA-CARDS SECTION.

START.
	ENTER MACRO FORMN USING CARD-RECORD, N-CARD.
	MOVE "          BLOCKING   INPUT   OUTPUT      INPUT        OUTPUT"
				TO HEADER-1.
	MOVE "   NAME    FACTOR    DEVICE  DEVICE   VALUE OF ID  VALUE OF ID
-			"   DATE     AUTHOR" TO HEADER-2.
	MOVE CARD-ID TO HEADER-NAME.
	PERFORM PH-A THRU PP-EXIT.
	IF CARD-IN-DEV = SPACES MOVE "DSK" TO CARD-IN-DEV.
	IF CARD-OUT-DEV = SPACES MOVE "DSK" TO CARD-OUT-DEV.
	PERFORM SUB-HEADING THRU SUB-HEAD-EXIT.

	MOVE CARD-CODE      TO PL-CODE.
	MOVE CARD-ID        TO PL-ID, CBL-VAL-ID-FILE.
	MOVE CARD-BLOCKING  TO PL-BLOCKING, BLC-VALUE.
	MOVE CARD-IN-DEV    TO PL-INPUT-DEV.
	MOVE CARD-OUT-DEV   TO PL-OUTPUT-DEV.
	MOVE CARD-IN-VAL-ID TO PL-IN-VAL, IN-VAL-ID.
	MOVE CARD-OUT-VAL-ID TO PL-OUT-VAL, OUT-VAL-ID.
	MOVE CARD-DATE      TO PL-DATE.
	MOVE CARD-AUTHOR    TO PL-AUTHOR.

	MOVE CARD-ID TO SPER-1-7.
	PERFORM MOVE-PERIOD VARYING INDX FROM 7 BY -1 UNTIL INDX LESS THAN 1.
	MOVE SPER-1-7 TO PID-NAME.

	MOVE CARD-DATE TO SPER-1-10.
	PERFORM MOVE-PERIOD VARYING INDX FROM 10 BY -1 UNTIL INDX < 1.
	MOVE SPER-1-10 TO DWL-DATE.

	MOVE CARD-AUTHOR TO SPER-1-21.
	PERFORM MOVE-PERIOD VARYING INDX FROM 21 BY -1 UNTIL INDX < 1.
	MOVE SPER-1-21 TO AU-NAME.

	MOVE CARD-IN-DEV TO SPER-1-7.
	PERFORM MOVE-PERIOD VARYING INDX FROM 7 BY -1 UNTIL INDX < 1.
	MOVE SPER-1-7 TO IN-DEVICE.
	MOVE CARD-OUT-DEV TO SPER-1-7.
	PERFORM MOVE-PERIOD VARYING INDX FROM 7 BY -1 UNTIL INDX < 1.
	MOVE SPER-1-7 TO OUT-DEVICE.

	IF CARD-ID IS EQUAL TO SPACES
		MOVE "NO PROG-ID" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.


CHECK-BLOCKING.
	IF CARD-BLOCKING IS NOT NUMERIC
		MOVE "IMPROPER BLOCKING FACTOR" TO PL-ERROR;
		MOVE 01 TO BLC-VALUE;
		PERFORM PRINT-ONLY THRU PP-EXIT.

	PERFORM PRINT-2 THRU PP-EXIT.
	MOVE "N" TO PREV-CARD-CODE.
READ-NEXT-CARD.
	ALTER PRINT-THE-LINE TO PROCEED TO PRINT-ONLY.
	READ CARD; AT END
		MOVE "X" TO EOF-FLAG; GO TO FINISH-RIDC.
READ-NC-0.
	IF NO-MORE-INPUT OR CARD-CODE EQUALS "N"
		GO TO FINISH-RIDC.
	IF CARD-CODE EQUALS "1" MOVE "I" TO CARD-CODE; ELSE
	IF CARD-CODE EQUALS "0" MOVE "O" TO CARD-CODE.
	IF CARD-CODE EQUALS "I" OR "O" GO TO DATA-RECORD-CARD.
	IF CARD-CODE IS EQUAL TO PREV-CARD-CODE GO TO READ-NC-B;
		ELSE MOVE CARD-CODE TO PREV-CARD-CODE.
	IF CARD-CODE IS EQUAL TO "B" GO TO BREAK-CARD.
	IF CARD-CODE IS EQUAL TO "H" GO TO HEADER-CARD.
	IF CARD-CODE IS EQUAL TO "A" GO TO ACCUM-CARD.
	IF CARD-CODE IS EQUAL TO "L" GO TO LIST-CARD.
	IF CARD-CODE IS EQUAL TO "T" GO TO TOTAL-CARD.
	IF CARD-CODE IS EQUAL TO "E" GO TO EMIT-CARD;   ELSE
		MOVE SPACES TO HEADER-1, HEADER-2;
		PERFORM PRINT-2 THRU PP-EXIT;
		GO TO READ-NC-C.
READ-NC-B.
	IF CARD-CODE IS EQUAL TO "B" GO TO BREAK-CARD-0.
	IF CARD-CODE IS EQUAL TO "H" GO TO HEADER-CARD-0.
	IF CARD-CODE IS EQUAL TO "A" GO TO ACCUM-CARD-0.
	IF CARD-CODE IS EQUAL TO "L" GO TO LIST-CARD-0.
	IF CARD-CODE IS EQUAL TO "T" GO TO TOTAL-CARD-0.
	IF CARD-CODE IS EQUAL TO "E" GO TO EMIT-CARD-0.

READ-NC-C.
	MOVE N-CARD TO P-L; MOVE "IMPROPER INPUT CARD" TO PL-ERROR.
	PERFORM ERROR-OUT THRU EO-EXIT.
CYCLE-BACK.
	PERFORM PRINT-THE-LINE THRU PP-EXIT.
	GO TO READ-NEXT-CARD.

MOVE-PERIOD.
	IF SET-PERIOD = SPACES MOVE 0 TO INDX;
	ELSE IF SPER (INDX) = SPACE NEXT SENTENCE;
	ELSE IF SPER (INDX) = "." MOVE 0 TO INDX;
	ELSE SET INDX UP BY 1;
		MOVE "." TO SPER (INDX);
		MOVE 0 TO INDX.
SET-UP-PICTURES.
	IF AC-SIZE (INDX) IS EQUAL TO ZERO GO TO SUPIC-EXIT.
	SUBTRACT AC-DECIMAL (INDX) FROM AC-SIZE (INDX)
		GIVING LEVEL.
	IF LEVEL IS EQUAL TO ZERO GO TO SUPIC-B.
	IF LEVEL IS LESS THAN 10
		MOVE " S9( )"  TO HPC-A; MOVE LEVEL TO HPC-C;
	ELSE
		MOVE "S9(  )"  TO HPC-A; MOVE LEVEL TO HPC-B.

	MOVE AC-DECIMAL (INDX) TO LEVEL.
	IF LEVEL IS EQUAL TO ZERO
		MOVE ".     " TO HPC-D; GO TO SUPIC-A.
	IF LEVEL IS LESS THAN 10
		MOVE "V9( ). " TO HPC-D; MOVE LEVEL TO HPC-F;
	ELSE
		MOVE "V9(  )." TO HPC-D; MOVE LEVEL TO HPC-E.
SUPIC-A.
	IF HPC-K IS EQUAL TO SPACE MOVE HPC-L TO HOLD-PC-A.
	MOVE HOLD-PC-A TO AC-PICTURE (INDX).
	GO TO SUPIC-EXIT.

SUPIC-B.
	MOVE AC-DECIMAL (INDX) TO LEVEL.
	IF LEVEL IS LESS THAN 10
		MOVE "V9( ). " TO HPC-D; MOVE LEVEL TO HPC-F;
	ELSE
		MOVE "V9(  )." TO HPC-D; MOVE LEVEL TO HPC-E.
	MOVE HPC-D TO HPC-H.

	MOVE HOLD-PC-B TO AC-PICTURE (INDX).

SUPIC-EXIT.  EXIT.



SETUP-NEXT-BREAK.
	MOVE BREAKS (LEVEL) TO HOLD-X.
	IF HOLD-X IS EQUAL TO SPACE GO TO SNB-EXIT.
	ADD 1, LEVEL GIVING INDX.
SNB-1.
	IF INDX IS EQUAL TO 12 GO TO SNB-EXIT.
	MOVE BREAKS (INDX) TO HOLD-X.
	IF HOLD-X IS EQUAL TO SPACE
		ADD 1 TO INDX; GO TO SNB-1.
	MOVE HOLD-X TO NEXT-BREAK (LEVEL).
SNB-EXIT. EXIT.

GET-LOW-BREAK.
	MOVE BREAKS (INDX) TO LOW-BREAK.
GET-HIGH-BREAK.
	MOVE BREAKS (INDX) TO HIGH-BREAK.
FINISH-RIDC.
	PERFORM SETUP-NEXT-BREAK THRU SNB-EXIT VARYING LEVEL FROM 01
		BY 1 UNTIL LEVEL IS GREATER THAN 10.
	IF BT-ENTRY IS NOT EQUAL TO SPACES
		PERFORM GET-LOW-BREAK VARYING INDX FROM 01 BY 1
		   UNTIL LOW-BREAK IS NOT EQUAL TO SPACE;
		MOVE INDX TO LOW-INDX;
		MOVE LOW-BREAK TO AL0-LOWEST;
		PERFORM GET-HIGH-BREAK VARYING INDX FROM 11 BY -1
		   UNTIL HIGH-BREAK IS NOT EQUAL TO SPACE.

	PERFORM SET-UP-PICTURES THRU SUPIC-EXIT VARYING INDX FROM 01
		BY 1 UNTIL INDX > 10.
*	THIS SECTION PROCESSES A "B" RECORD.

BREAK-CARD SECTION 03.

START.
	MOVE "         PRINTER-CONTROL     BREAK" TO HEADER-1.
	MOVE "  LEVEL   BEFORE  AFTER   SIZE   NAME" TO HEADER-2.
	PERFORM SUB-HEADING THRU SUB-HEAD-EXIT.


BREAK-CARD-0.
	ENTER MACRO FORMB USING CARD-RECORD, B-CARD.
	MOVE "B"	    TO PL-CODE.
	MOVE CARD-LEVEL     TO PL-LEVEL.
	MOVE CARD-SPB       TO PL-SPB.
	MOVE CARD-SKB       TO PL-SKB.
	MOVE CARD-SPA       TO PL-SPA.
	MOVE CARD-SKA       TO PL-SKA.

	IF CARD-LEVEL IS NOT EQUAL TO "F"
		MOVE CARD-BRK-NAME  TO PL-BRK-NAME;
		MOVE CARD-BRK-SIZE  TO PL-BRK-SIZE.

	IF BF-INDX IS EQUAL TO 25
		MOVE "TOO MANY BREAKS" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		GO TO READ-NEXT-CARD.
	ADD 1 TO BF-INDX.

	IF CARD-LEVEL = "F"
		MOVE 11 TO INDX; MOVE "F" TO BREAKS (11);
		GO TO BREAK-CARD-A;
	ELSE
	IF CARD-LEVEL IS LESS THAN 0 OR GREATER THAN 9
		MOVE "IMPROPER LEVEL" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
	ELSE
		ADD CL-REDEF, 01 GIVING INDX;
		MOVE CARD-LEVEL TO BREAKS (INDX).

	IF CARD-BRK-NAME IS EQUAL TO SPACES
		MOVE "NO NAME GIVEN" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.
	IF CARD-BRK-SIZE IS EQUAL TO ZERO
		MOVE "IMPROPER FIELD SIZE" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
	ELSE
	IF CARD-BRK-SIZE IS NUMERIC NEXT SENTENCE;  ELSE
		MOVE "IMPROPER FIELD SIZE" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.
BREAK-CARD-A.
	IF CARD-SPACING IS NOT EQUAL TO SPACES AND
		ST-ENTRY (INDX) IS NOT EQUAL TO SPACES
		MOVE "SPACING TAKEN FROM ABOVE" TO PL-ERROR;
		PERFORM ERROR-OUT-A THRU EO-EXIT;
		GO TO FILL-IN-BF.
	IF CARD-SPACING IS EQUAL TO SPACES AND
		ST-ENTRY (INDX) IS NOT EQUAL TO SPACES GO TO FILL-IN-BF.

	IF CARD-SPB IS NOT NUMERIC OR CARD-SPB IS GREATER THAN HIGHEST-SPACE OR
		CARD-SPA IS NOT NUMERIC OR CARD-SPA IS GREATER THAN HIGHEST-SPACE OR
		CARD-SKA IS NOT NUMERIC OR CARD-SKA IS GREATER THAN HIGHEST-CHANNEL OR
		CARD-SKB IS NOT NUMERIC OR CARD-SKB IS GREATER THAN HIGHEST-CHANNEL
		MOVE "IMPROPER SPACE OR SKIP" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT; GO TO FILL-IN-BF;
	ELSE
		ADD CARD-SKA, 01 GIVING INDX-1;
		MOVE CARD-SKA TO CHAN-ENTRY (INDX-1);
		ADD CARD-SKB, 01 GIVING INDX-1;
		MOVE CARD-SKB TO CHAN-ENTRY (INDX-1).

	IF CARD-SPA IS NOT EQUAL TO 0 AND CARD-SKA IS NOT EQUAL TO 0
	OR CARD-SPB IS NOT EQUAL TO 0 AND CARD-SKB IS NOT EQUAL TO 0
		MOVE "BOTH SPACE AND SKIP" TO PL-ERROR;
		PERFORM ERROR-OUT-A THRU EO-EXIT.

	ADD CARD-SPA, 1 GIVING INDX-1; MOVE CARD-SPA TO SPACE-ENTRY (INDX-1).
	ADD CARD-SPB, 1 GIVING INDX-1; MOVE CARD-SPB TO SPACE-ENTRY (INDX-1).
	MOVE CARD-SPB TO ST-SPACE-B (INDX).
	MOVE CARD-SPA TO ST-SPACE-A (INDX).
	MOVE CARD-SKA TO ST-SKIP-A  (INDX).
	MOVE CARD-SKB TO ST-SKIP-B  (INDX).

FILL-IN-BF.
	IF CARD-LEVEL IS NOT EQUAL TO "F"
		MOVE CARD-LEVEL     TO BF-LEVEL (BF-INDX);
		MOVE CARD-BRK-NAME  TO BF-NAME  (BF-INDX);
		MOVE CARD-BRK-SIZE  TO BF-SIZE  (BF-INDX).

	PERFORM PRINT-THE-LINE THRU PP-EXIT.
	GO TO READ-NEXT-CARD.
*	THIS SECTION PROCESSES AN "H" CARD.

HEADER-CARD SECTION 04.

START.
	MOVE "   PAGE   HEADER" TO HEADER-1.
	MOVE " SIZE LOC CONTROL RESET PAGE   HEADING" TO HEADER-2.
	PERFORM SUB-HEADING THRU SUB-HEAD-EXIT.


HEADER-CARD-0.
	ADD 1 TO H-CARD-COUNT.
	MOVE "H" TO PL-CODE.
	IF H-CARD-COUNT IS GREATER THAN 2
		MOVE 3 TO H-CARD-COUNT;
		MOVE "TOO MANY HEADER CARDS" TO PL-ERROR;
		PERFORM ERROR-OUT-A THRU EO-EXIT;
		GO TO CYCLE-BACK.

	MOVE "H" TO PL-CODE.
	IF H-CARD-COUNT IS EQUAL TO 2
		ENTER MACRO FORMH2 USING CARD-RECORD, H2-CARD;
		MOVE CARD-HDR-3 TO PAGE-HDR-3, PL-HDR-3;
		MOVE CARD-HDR-4 TO PAGE-HDR-4, PL-HDR-4;
		GO TO CYCLE-BACK.

	ENTER MACRO FORMH1 USING CARD-RECORD, H-CARD.
	MOVE CARD-HDR-1 TO PL-HDR-1, PAGE-HDR-1.
	MOVE CARD-HDR-2 TO PL-HDR-2, PAGE-HDR-2.

	MOVE CARD-PAGE-SIZE	TO PL-PAGE-SIZE.
	MOVE CARD-PAGE-LOC	TO PL-PAGE-LOC.
	MOVE CARD-HDR-SP	TO PL-HDR-SP.
	MOVE CARD-HDR-SK	TO PL-HDR-SK.
	MOVE CARD-PAGE-RESET TO PL-PAGE-RESET.

	IF CARD-PAGE-SIZE IS NOT NUMERIC
		MOVE "IMPROPER PAGE SIZE" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.

	MOVE CARD-PAGE-SIZE TO PAGE-SIZE.
	MOVE CARD-PAGE-LOC TO PAGE-LOC.

	IF CARD-PAGE-LOC EQUALS SPACES AND CARD-PAGE-SIZE EQUALS ZERO
		GO TO HEADER-CARD-C.

	IF CARD-PAGE-LOC IS NUMERIC NEXT SENTENCE;  ELSE
		MOVE "IMPROPER PAGE LOC" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		GO TO HEADER-CARD-C.

	IF PAGE-SIZE IS EQUAL TO ZERO AND PAGE-LOC IS NOT EQUAL TO ZERO
		MOVE "NO PAGE SIZE" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		GO TO HEADER-CARD-C.
	IF CARD-PAGE-LOC IS GREATER THAN 133 - PAGE-SIZE
		MOVE "PAGE LOC TOO FAR RIGHT" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.
HEADER-CARD-C.
	IF CARD-HDR-SP IS NOT NUMERIC OR CARD-HDR-SP IS GREATER THAN HIGHEST-SPACE
		MOVE "IMPROPER HEADER SPACING" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
	ELSE
		ADD CARD-HDR-SP, 1 GIVING INDX-1;
		MOVE CARD-HDR-SP TO HDR-SP, SPACE-ENTRY (INDX-1).

	IF CARD-HDR-SK IS NOT NUMERIC OR CARD-HDR-SK IS GREATER THAN HIGHEST-CHANNEL
		MOVE "IMPROPER HEADER SKIPPING" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
	ELSE
		ADD CARD-HDR-SK, 1 GIVING INDX-1;
		MOVE CARD-HDR-SK TO HDR-SK, CHAN-ENTRY (INDX-1).
	IF HDR-SK IS NOT EQUAL TO ZERO AND HDR-SP IS NOT EQUAL TO ZERO
		MOVE "BOTH SPACE AND SKIP" TO PL-ERROR;
		PERFORM ERROR-OUT-A THRU EO-EXIT.

	PERFORM TEST-PAGE-RESET THRU TPR-EXIT VARYING INDX FROM 01
		BY 1 UNTIL INDX IS GREATER THAN 10.

	MOVE CARD-HDR-1 TO PAGE-HDR-1.
	MOVE CARD-HDR-2 TO PAGE-HDR-2.

	GO TO CYCLE-BACK.

TEST-PAGE-RESET.
	MOVE CPR (INDX) TO HOLD-X.
	IF HOLD-X IS EQUAL TO SPACE GO TO TPR-EXIT.

	IF HOLD-X IS LESS THAN ZERO OR GREATER THAN 9
		MOVE "IMPROPER BREAK FOR RESET" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		GO TO TPR-EXIT;
	ELSE
		ADD HOLD-N, 01 GIVING LEVEL.
	IF BREAKS (LEVEL) IS EQUAL TO SPACE
		MOVE "NON-EXISTENT BREAK FOR RESET" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		GO TO TPR-EXIT.

	IF HR-WHEN (LEVEL) IS EQUAL TO HOLD-X
		MOVE "SAME BREAK NOTED TWICE" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
	ELSE
		MOVE HOLD-X TO HR-WHEN (LEVEL).

TPR-EXIT.  EXIT.
*	THIS SECTION PROCESSES AN "A" CARD.

ACCUM-CARD SECTION 05.

START.
	MOVE "    ACCUMULATOR" TO HEADER-1.
	MOVE "   #  SIZE  POINT     INPUT NAME                  LISTING NAME"
		TO HEADER-2.
	PERFORM SUB-HEADING THRU SUB-HEAD-EXIT.


ACCUM-CARD-0.
	ENTER MACRO FORMA USING CARD-RECORD, A-CARD.
	ADD 1 TO A-CARD-COUNT.
	MOVE "A"		 TO PL-CODE.
	MOVE CARD-AC-NUM	 TO PL-AC-NUM.
	MOVE CARD-AC-SIZE	 TO PL-AC-SIZE.
	MOVE CARD-AC-POINT	 TO PL-AC-POINT.
	MOVE CARD-AC-IN-NAME     TO PL-AC-IN-NAME.
	MOVE CARD-AC-OUT-NAME    TO PL-AC-OUT-NAME.
	IF CARD-AC-SIZE IS NOT NUMERIC GO TO AC-CARD-A.

	IF CARD-AC-SIZE IS EQUAL TO ZERO OR GREATER THAN 18
		NEXT SENTENCE ELSE GO TO AC-CARD-B.
AC-CARD-A.
		MOVE "IMPROPER AC SIZE" TO PL-ERROR;
		MOVE ZERO TO CARD-AC-SIZE;
		PERFORM ERROR-OUT THRU EO-EXIT.

AC-CARD-B.
	IF CARD-AC-NUM IS LESS THAN ZERO OR GREATER THAN 9
		MOVE "IMPROPER AC NUMBER" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		MOVE ZERO TO CARD-AC-SIZE.
	ADD CARD-AC-NUM, 01 GIVING INDX.
	IF AC-SIZE (INDX) IS NOT EQUAL TO ZERO
		MOVE "DUPLICATE AC" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		MOVE ZERO TO CARD-AC-SIZE.

	IF CARD-AC-POINT IS NOT NUMERIC OR
		CARD-AC-POINT IS GREATER THAN CARD-AC-SIZE
		MOVE "IMPROPER POINT LOCATION" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		MOVE ZERO TO CARD-AC-SIZE.

	IF CARD-AC-IN-NAME IS EQUAL TO SPACES
		MOVE "NO INPUT NAME" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		MOVE ZERO TO CARD-AC-SIZE.

	IF CARD-AC-SIZE IS EQUAL TO ZERO GO TO CYCLE-BACK.

	MOVE CARD-AC-SIZE TO AC-SIZE (INDX).
	MOVE CARD-AC-POINT TO AC-DECIMAL (INDX).
	MOVE CARD-AC-IN-NAME TO AC-IN-NAME (INDX).
	MOVE CARD-AC-OUT-NAME TO AC-LIST-NAME (INDX).

	GO TO CYCLE-BACK.
*	THIS SECTION PROCESSES A "T" CARD.

TOTAL-CARD SECTION 06.

START.
	MOVE "   AC NUMBER       WHEN           OUTPUT NAME"
		TO HEADER-2; MOVE SPACES TO HEADER-1.
	PERFORM SUB-HEADING THRU SUB-HEAD-EXIT.


TOTAL-CARD-0.
	ENTER MACRO FORMT USING CARD-RECORD, T-CARD.
	MOVE "T"	       TO PL-CODE.
	MOVE CARD-T-AC-NUM     TO PL-T-AC-NUM.
	MOVE CARD-T-LEVELS     TO PL-T-LEVELS.
	MOVE CARD-T-OUT-NAME   TO PL-T-OUT-NAME.

	IF CARD-T-AC-NUM IS LESS THAN ZERO OR GREATER THAN 9
		MOVE "IMPROPER AC NUMBER" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		MOVE ZERO TO INDX;
	ELSE
		ADD CARD-T-AC-NUM, 01 GIVING INDX;
		MOVE CARD-T-OUT-NAME TO BTTP-NAME (INDX);
		IF AC-SIZE (INDX) IS EQUAL TO ZERO
		MOVE "NON-EXISTENT AC" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.

	IF CARD-T-OUT-NAME IS EQUAL TO SPACES
		MOVE "NO OUTPUT NAME" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.

	IF CARD-T-LEVELS IS EQUAL TO SPACES
		MOVE "NO BREAKS SPECIFIED" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
	ELSE
		IF INDX IS NOT EQUAL TO ZERO
		MOVE SPACES TO HOLD-FLAGS;
		PERFORM TEST-T-FLAGS THRU TTF-EXIT VARYING LEVEL FROM 01
			BY 1 UNTIL LEVEL IS GREATER THAN 11.

END-T-CARD.
	PERFORM PRINT-THE-LINE THRU PP-EXIT.

	GO TO READ-NEXT-CARD.
TEST-T-FLAGS.
	MOVE CTL (LEVEL) TO HOLD-X.
	IF HOLD-X IS EQUAL TO SPACE GO TO TTF-EXIT.
	IF HOLD-X IS EQUAL TO "F"
		MOVE 11 TO INDX-1; MOVE "F" TO BREAKS (11);	ELSE
	IF HOLD-X IS LESS THAN 0 OR GREATER THAN 9
		MOVE "IMPROPER BREAK" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		GO TO TTF-EXIT;
	ELSE
		ADD HOLD-N, 01 GIVING INDX-1.

	MOVE HOLD-X TO BTTPF (INDX, INDX-1).

	IF H-FLGS (INDX-1) IS EQUAL TO SPACE
		MOVE HOLD-X TO H-FLGS (INDX-1);
	ELSE
		MOVE "DUPLICATE BREAKS" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.

TTF-EXIT. EXIT.
	NOTE THIS SECTION PROCESSES AN "L" CARD.

LIST-CARD SECTION 07.

START.
	MOVE "   WHEN   INPUT NAME                      OUTPUT NAME"
		TO HEADER-2; MOVE SPACE TO HEADER-1.
	PERFORM SUB-HEADING THRU SUB-HEAD-EXIT.


LIST-CARD-0.
	ENTER MACRO FORML USING CARD-RECORD, L-CARD.
	MOVE "L"		TO PL-CODE.
	MOVE CARD-L-WHEN	TO PL-L-WHEN.
	MOVE CARD-L-IN-NAME	TO PL-L-IN-NAME.
	MOVE CARD-L-OUT-NAME    TO PL-L-OUT-NAME.

   IF LT-INDX IS EQUAL TO 50
		MOVE "TOO MANY LIST CARDS" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		GO TO CYCLE-BACK.

   ADD 1 TO LT-INDX.

	MOVE CARD-L-WHEN TO LT-WHEN (LT-INDX).
	MOVE CARD-L-IN-NAME TO LT-IN-NAME (LT-INDX).
	MOVE CARD-L-OUT-NAME TO LT-OUT-NAME (LT-INDX).

   IF CARD-L-WHEN IS EQUAL TO "A" NEXT SENTENCE; ELSE
	IF CARD-L-WHEN IS LESS THAN 0 OR GREATER THAN 9
		MOVE "IMPROPER BREAK NUMBER" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
	ELSE
		ADD 1, CARD-L-WHEN-N GIVING INDX;
		IF BREAKS (INDX) EQUAL TO SPACE
		MOVE "UNDEFINED BREAK LEVEL" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.

	IF CARD-L-IN-NAME IS EQUAL TO SPACES
		MOVE "NO INPUT NAME" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.
	IF CARD-L-OUT-NAME IS EQUAL TO SPACES
		MOVE "NO OUTPUT NAME" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.

       GO TO CYCLE-BACK.
*	PROCESS 'E' RECORD

EMIT-CARD SECTION 08.

START.
	MOVE "  BREAK   INPUT NAME                      OUTPUT NAME" TO HEADER-2.
	MOVE SPACES TO HEADER-1.
	PERFORM SUB-HEADING THRU SUB-HEAD-EXIT.

EMIT-CARD-0.
	ENTER MACRO FORML USING CARD-RECORD, L-CARD.
	MOVE "E"             TO PL-CODE.
	MOVE CARD-L-WHEN     TO PL-L-WHEN.
	MOVE CARD-L-IN-NAME  TO PL-L-IN-NAME.
	MOVE CARD-L-OUT-NAME TO PL-L-OUT-NAME.

	IF EM-INDX IS EQUAL TO 50
		MOVE "TOO MANY EMIT CARDS" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
		GO TO CYCLE-BACK.

	SET EM-INDX UP BY 1.

	MOVE CARD-L-WHEN TO EM-WHEN (EM-INDX).
	MOVE CARD-L-IN-NAME TO EM-IN-NAME (EM-INDX).
	MOVE CARD-L-OUT-NAME TO EM-OUT-NAME (EM-INDX).

	IF CARD-L-WHEN = "F" MOVE "F" TO BREAKS (11);
	ELSE
	IF CARD-L-WHEN IS LESS THAN "0" OR > "9"
		MOVE "IMPROPER BREAK NUMBER" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT;
	ELSE
	ADD 1, CARD-L-WHEN-N GIVING INDX;
	IF BREAKS (INDX) IS EQUAL TO SPACES
		MOVE "UNDEFINED BREAK NUMBER" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.

	IF CARD-L-IN-NAME = SPACES
		MOVE "NO INPUT NAME" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.
	IF CARD-L-OUT-NAME = SPACES
		MOVE "NO OUTPUT NAME" TO PL-ERROR;
		PERFORM ERROR-OUT THRU EO-EXIT.

	GO TO CYCLE-BACK.
*	THIS SECTION PROCESSES AN "I" OR "O" RECORD.

DATA-RECORD-CARD SECTION 09.

START.
	IF CARD-CODE IS EQUAL TO "O" GO TO DRC-A.

	IF HOLD-I-KEY IS NOT EQUAL TO ZERO
		MOVE "I" TO MT1B-A; MOVE MORE-THAN-1-BATCH TO PL-ERROR;
		GO TO CYCLE-BACK.
	MOVE TFO-RECORD-NUMBER TO HOLD-I-KEY; MOVE "I" TO PREV-CARD-CODE
	GO TO DRC-C.

DRC-A.
	IF HOLD-P-KEY IS NOT EQUAL TO ZERO
		MOVE "O" TO MT1B-A; MOVE MORE-THAN-1-BATCH TO PL-ERROR;
		GO TO CYCLE-BACK.
	MOVE TFO-RECORD-NUMBER TO HOLD-P-KEY; MOVE "O" TO PREV-CARD-CODE.

DRC-C.
	ENTER MACRO FORMI USING CARD-RECORD, IO-CARD.
	MOVE IO-CARD TO TRAN-RECORD-OUT.
	PERFORM WRITE-TRAN THRU WT-EXIT.
	READ CARD; AT END MOVE "X" TO EOF-FLAG, CARD-CODE.
	IF CARD-CODE IS EQUAL TO "1" MOVE "I" TO CARD-CODE; ELSE
	IF CARD-CODE IS EQUAL TO "0" MOVE "O" TO CARD-CODE.
	IF CARD-CODE IS EQUAL TO PREV-CARD-CODE GO TO DRC-C.

	MOVE "\" TO TRAN-EOF-OUT.
	PERFORM WRITE-TRAN THRU WT-EXIT.
	GO TO READ-NC-0.


WRITE-TRAN.
	IF TFO-RECORD-NUMBER = HI-TRAN-LIMIT GO TO WT-EXIT.
	IF TFO-RECORD-NUMBER = HI-TRAN-LIMIT - 1 GO TO PANIC-50.
WRITE-TRAN-1.
	WRITE TRAN-RECORD-OUT.
	ADD 1 TO TFO-RECORD-NUMBER.
WT-EXIT. EXIT.

PANIC-50.
	MOVE SPACES TO P-L; WRITE P-L BEFORE 2.
	MOVE "*** TOO MANY RECORD CARDS ***" TO P-L; WRITE P-L.
P50-LOOP.
	READ CARD; AT END MOVE "X" TO EOF-FLAG.
	IF NO-MORE-INPUT OR CARD-CODE EQUALS "N"
		MOVE "\" TO TRAN-EOF-OUT; GO TO WRITE-TRAN-1;
	ELSE
		GO TO P50-LOOP.
*	THIS SECTION PUTS OUT THE IDENTIFICATION AND ENVIRONMENT DIVISIONS.

PUT-OUT-ID-ED SECTION 10.

START.
	MOVE ZEROES TO LINE-NUMBER.
	MOVE "IDENTIFICATION DIVISION." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	MOVE PROG-ID-LINE TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	IF AU-NAME IS NOT EQUAL TO SPACES
		MOVE AUTHOR-LINE TO PUNCH-8-72;
		PERFORM PUNCH-CARD.
	IF DWL-DATE IS NOT EQUAL TO SPACES
		MOVE DATE-WRITTEN-LINE TO PUNCH-8-72;
		PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.


*	PUT OUT ENVIRONMENT DIVISION.


	MOVE 1000 TO LINE-NUMBER.
	MOVE "ENVIRONMENT DIVISION." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	MOVE "CONFIGURATION SECTION." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.

*	SPECIAL-NAMES PARAGRAPH

	MOVE "SPECIAL-NAMES.  " TO PUNCH-8-23.
	PERFORM PUNCH-CARD.
	PERFORM CHANNEL-OUT VARYING INDX FROM 3 BY 1
		UNTIL INDX IS GREATER THAN HIGHEST-CHANNEL.
	MOVE "CHANNEL (1) IS TOP-OF-FORM." TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
*	GET READY FOR 'SELECT' CLAUSES

	MOVE "INPUT-OUTPUT SECTION." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	MOVE "FILE-CONTROL.   " TO PUNCH-8-23.
	PERFORM PUNCH-CARD.
*	PUNCH OUT "SELECT" CLAUSES.

	MOVE "IN-FILE" TO SEL-NAME.
	MOVE IN-DEVICE TO SEL-DEVICE.
	MOVE SELECT-CLAUSE TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	MOVE "L-PRINT" TO SEL-NAME.
	MOVE OUT-DEVICE TO SEL-DEVICE.
	MOVE SELECT-CLAUSE TO PUNCH-12-72.
	PERFORM PUNCH-CARD.

	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

POIDED-EXIT. EXIT.


*	SUBROUTINE USED TO PUNCH OUT 'CHANNEL (N) IS NAME'

CHANNEL-OUT.
	MOVE CHAN-ENTRY (INDX) TO HOLD-X.
	IF HOLD-X IS EQUAL TO SPACE
		ADD HDR-SK, 01 GIVING INDX-1;
		IF INDX-1 IS EQUAL TO INDX MOVE HDR-SK TO HOLD-X.
	IF HOLD-X IS NOT EQUAL TO SPACE
		MOVE HOLD-X  TO CHL-NUMBER-A, CHL-NUMBER-B;
		MOVE CHANNEL-LINE TO PUNCH-12-72;
		PERFORM PUNCH-CARD.
*	THIS SECTION PUTS OUT THE DATA DIVISION.

PUT-OUT-DD SECTION 12.

START.
	MOVE 010000 TO LINE-NUMBER.
	MOVE "DATA DIVISION.  " TO PUNCH-8-23.
	PERFORM PUNCH-CARD.
	MOVE "FILE SECTION.   " TO PUNCH-8-23.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	MOVE "FD  IN-FILE" TO PUNCH-8-18.
	PERFORM PUNCH-CARD.

	IF BLC-VALUE IS NOT EQUAL " 0 "
		MOVE BLOCK-CONTAINS-LINE TO PUNCH-12-72;
		PERFORM PUNCH-CARD.

	MOVE IN-VAL-ID TO VIDL-NAME.
	PERFORM PUT-OUT-LR-DR.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	IF HOLD-I-KEY IS NOT EQUAL TO ZERO
		MOVE HOLD-I-KEY TO TRAN-KEY;
		PERFORM PUNCH-RECORD-CARDS THRU PRC-EXIT.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	MOVE "FD  L-PRINT" TO PUNCH-8-18.
	PERFORM PUNCH-CARD.
	MOVE OUT-VAL-ID TO VIDL-NAME; PERFORM PUT-OUT-LR-DR.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
	MOVE "01  PRINT-LINE; DISPLAY-7." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	IF PAGE-SIZE IS EQUAL TO ZERO MOVE PRINTER-SIZE TO TEMP-3N;
		GO TO PUT-OUT-DD-B.

	SUBTRACT 1 FROM PAGE-LOC GIVING TEMP-3N.
	PERFORM PUT-OUT-FILLER THRU POF-EXIT.

	IF PAGE-SIZE IS EQUAL TO 1
		MOVE "9." TO PCL-SIZE;  ELSE
	IF PAGE-SIZE IS GREATER THAN 4
		MOVE "Z( )9." TO PCL-SIZE;
		SUBTRACT 1 FROM PAGE-SIZE GIVING PCLS-1; ELSE
	SUBTRACT 01 FROM PAGE-SIZE GIVING INDX;
		MOVE ZZT-X (INDX) TO PCL-SIZE.

	MOVE PAGE-COUNT-LINE TO PUNCH-12-72.
	PERFORM PUNCH-CARD.

	COMPUTE TEMP-3N = PRINTER-SIZE - (PAGE-LOC - 1 + PAGE-SIZE).

PUT-OUT-DD-B.
	PERFORM PUT-OUT-FILLER THRU POF-EXIT.
*	NOW WE PUNCH OUT THE WORKING-STORAGE SECTION.

PUT-OUT-DD-C.
	PERFORM PUNCH-TOP-FORM.
	MOVE 100000 TO LINE-NUMBER.

	MOVE "WORKING-STORAGE SECTION." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	IF PAGE-SIZE IS NOT EQUAL TO ZERO
		MOVE "77  PAGE-COUNT  " TO PUNCH-8-23;
		MOVE "S9( ); " TO HPC-D; MOVE PAGE-SIZE TO HPC-F;
		MOVE "PICTURE " TO PUNCH-40-47; MOVE HPC-D TO PUNCH-48-54
		MOVE "COMPUTATIONAL." TO PUNCH-56-69;
		PERFORM PUNCH-CARD.
	MOVE "77  LINE-COUNT  " TO PUNCH-8-23.
	MOVE "PICTURE S99;    COMPUTATIONAL." TO PUNCH-40-72.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	PERFORM PUT-OUT-SAVES THRU POS-EXIT VARYING INDX FROM 01 BY 1
		UNTIL INDX IS GREATER THAN BF-INDX.

PUT-OUT-DD-D.
	MOVE 105000 TO LINE-NUMBER.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	MOVE "01  OUTPUT-LINE; DISPLAY-7." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	IF HOLD-P-KEY IS NOT EQUAL TO ZERO
		MOVE HOLD-P-KEY TO TRAN-KEY;
		PERFORM PUNCH-RECORD-CARDS THRU PRC-EXIT.
	PERFORM PUNCH-CARD.

	IF A-CARD-COUNT IS NOT EQUAL TO ZERO
		PERFORM PUT-OUT-ACS VARYING LEVEL FROM 01 BY 1
		UNTIL LEVEL IS GREATER THAN 11.

	MOVE 150000 TO LINE-NUMBER.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
	IF H-CARD-COUNT IS EQUAL TO ZERO GO TO PUT-OUT-DD-Z.
	MOVE "01  HEADER." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	MOVE QUOTE TO HDR-Q-1, HDR-Q-2.
	MOVE ".   " TO HDR-Q-3.
	MOVE ZERO TO HDR-Q-SIZE.
	MOVE PAGE-HDR-1 TO HDR-VALUE; PERFORM PUT-OUT-HEADER.
	MOVE PAGE-HDR-2 TO HDR-VALUE; PERFORM PUT-OUT-HEADER.
	MOVE QUOTE TO HDR-Q-4, HDR-Q-5.
	MOVE "." TO HDR-Q-6.
	MOVE "3" TO HDR-Q-SIZE.
	MOVE PAGE-HDR-3 TO HDR-VALUE-1; PERFORM PUT-OUT-HEADER.
	MOVE PAGE-HDR-4 TO HDR-VALUE-1; PERFORM PUT-OUT-HEADER.
PUT-OUT-DD-Z.
	PERFORM PUNCH-TOP-FORM.
	GO TO PODD-EXIT.

PUT-OUT-LR-DR.
	IF VIDL-NAME = SPACES
		MOVE "LABEL RECORDS ARE OMITTED." TO PUNCH-12-37;
	ELSE
		MOVE "LABEL RECORDS ARE STANDARD" TO PUNCH-12-37;
		PERFORM PUNCH-CARD;
		MOVE VALUE-OF-ID-LINE TO PUNCH-12-72.
	PERFORM PUNCH-CARD.


PUT-OUT-HEADER.
	MOVE HDR-VALUE-LINE TO PUNCH-12-72.
	PERFORM PUNCH-CARD.



*	PUT OUT '77 SAVE-NN  PICTURE X(N).'

PUT-OUT-SAVES.
	MOVE BF-SIZE (INDX) TO TEMP-3.
	IF TEMP-3 IS EQUAL TO SPACES GO TO POS-EXIT.
	IF TEMP-3 IS LESS THAN "004"
		MOVE TEMP-2 TO LEVEL; MOVE XXT-X (LEVEL) TO SFA-SIZE;
	ELSE
		IF TEMP-3 IS LESS THAN "010"
		MOVE "X( ).  " TO SFA-SIZE; MOVE TEMP-1 TO SFAS-C;
	ELSE
		IF TEMP-3 IS LESS THAN "100"
		MOVE "X(  ). " TO SFA-SIZE; MOVE TEMP-2 TO SFAS-B;
	ELSE
		MOVE "X(   )." TO SFA-SIZE; MOVE TEMP-3 TO SFAS-A.

	MOVE INDX TO SFA-NUM.

	MOVE SAVE-FIELD-AC TO PUNCH-8-72.
	PERFORM PUNCH-CARD.

POS-EXIT. EXIT.
*	PUT OUT '02 FILLER   PICTURE X(N).'

PUT-OUT-FILLER.
	IF TEMP-3N = ZERO GO TO POF-EXIT.

	IF TEMP-3N < 10
		MOVE TEMP-1 TO FL-AMT-1A;
		MOVE ")." TO FL-AMT-1B;
	ELSE IF TEMP-3N < 100
		MOVE TEMP-2 TO FL-AMT-2A;
		MOVE ")." TO FL-AMT-2B;
	ELSE
		MOVE TEMP-3 TO FL-AMT-3A;
		MOVE ")." TO FL-AMT-3B.

	MOVE FILLER-LINE TO PUNCH-12-72.
	PERFORM PUNCH-CARD.

POF-EXIT.  EXIT.

PUT-OUT-ACS.
	MOVE BREAKS (LEVEL) TO HOLD-X.
	IF HOLD-X IS NOT EQUAL TO SPACE
		PERFORM PUNCH-CARD;
		MOVE "01  LEVEL- -ACS; COMPUTATIONAL." TO PUNCH-8-72;
		MOVE HOLD-X TO PUNCH-18;
		PERFORM PUNCH-CARD;
		PERFORM POA-1 THRU POA-EXIT VARYING INDX FROM 01 BY 1
		UNTIL INDX IS GREATER THAN 10.

POA-1.
	IF AC-SIZE (INDX) IS EQUAL TO ZERO GO TO POA-EXIT.

	MOVE AC-PICTURE (INDX) TO AL-PICTURE.
	SUBTRACT 1 FROM INDX GIVING AL-AC.
	MOVE ACCUM-02-LINE TO PUNCH-12-72.
	PERFORM PUNCH-CARD.

POA-EXIT. EXIT.


PODD-EXIT. EXIT.
PUNCH-RECORD-CARDS.
	READ TRAN-FILE-IN; INVALID KEY
		DISPLAY "?BAD TRAN KEY"; STOP RUN.
	ADD 1 TO TRAN-KEY.
	IF TRAN-EOF-IN EQUALS "\" GO TO PRC-EXIT.
	PERFORM JAM-NULLS VARYING INDX FROM 79 BY -1 UNTIL INDX < 1.
	MOVE TRAN-RECORD-IN TO PUNCH-REC.
	PERFORM PUNCH-CARD.
	GO TO PUNCH-RECORD-CARDS.

PRC-EXIT. EXIT.


JAM-NULLS.
	IF TRAN-CHAR (INDX) = SPACE MOVE LOW-VALUE TO TRAN-CHAR (INDX);
		ELSE MOVE ZERO TO INDX.
*	THIS SECTION PUTS OUT THE READ STATEMENT.

PUT-OUT-READ SECTION 14.

START.
	MOVE 300000 TO LINE-NUMBER.
	MOVE "PROCEDURE DIVISION." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
	MOVE "ONLY SECTION." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	MOVE "START." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	MOVE "OPEN INPUT IN-FILE." TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	MOVE "OPEN OUTPUT L-PRINT." TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	MOVE "MOVE SPACES TO OUTPUT-LINE." TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.


	IF PAGE-SIZE IS NOT EQUAL TO ZERO
		MOVE "MOVE ZERO TO PAGE-COUNT." TO PUNCH-12-72;
		PERFORM PUNCH-CARD.
	MOVE "PERFORM PL-HDR THRU PL-EXIT." TO PUNCH-12-39.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	MOVE "READ-IN." TO PUNCH-8-23.
	PERFORM PUNCH-CARD.

	IF LOW-BREAK IS EQUAL TO SPACE
		MOVE "READ IN-FILE; AT END GO TO FINISH." TO PUNCH-12-72;
	ELSE IF HIGH-BREAK IS EQUAL TO "F"
		MOVE "READ IN-FILE; AT END GO TO LEVEL- -BREAK." TO PUNCH-12-72;
		MOVE LOW-BREAK TO PUNCH-45;
	ELSE
		MOVE "READ IN-FILE; AT END" TO PUNCH-12-72;
		PERFORM PUNCH-CARD;
		MOVE "ALTER LEVEL- -SW TO PROCEED TO FINISH;" TO PUNCH-16-72;
		MOVE HIGH-BREAK TO PUNCH-28;
		PERFORM PUNCH-CARD;
		MOVE "GO TO LEVEL- -BREAK." TO PUNCH-16-72;
		MOVE LOW-BREAK TO PUNCH-28.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
	IF LOW-BREAK IS EQUAL TO SPACE GO TO POR-EXIT.
	MOVE "INIT-SW. GO TO RESET-INIT." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	MOVE "    NOTE AFTER THE FIRST READ, INIT-SW WILL GO TO COMPARE."
	  TO PUNCH-12-72; PERFORM PUNCH-CARD.
	MOVE "RESET-INIT. ALTER INIT-SW TO PROCEED TO COMPARE."
		TO PUNCH-8-72; PERFORM PUNCH-CARD.
	MOVE GO-TO-HI TO PUNCH-12-39.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
	MOVE "COMPARE." TO PUNCH-8-23.
	PERFORM PUNCH-CARD.

POR-EXIT. EXIT.
	NOTE THIS SECTION PUTS OUT THE BREAK TESTING STATEMENTS.

PUT-OUT-BREAK-TESTS SECTION 16.

START.
	MOVE BREAKS (LEVEL) TO HOLD-X.
	IF HOLD-X IS EQUAL TO SPACE GO TO POBT-EXIT.

	MOVE "IF " TO IFL-1.
	PERFORM FIND-ALL-FIELDS VARYING INDX FROM 01 BY 1 UNTIL
		INDX IS GREATER THAN BF-INDX.

	MOVE NEXT-BREAK (LEVEL) TO HOLD-Y.
	MOVE "    ALTER LEVEL- -SW TO PROCEED TO RESET-LEVEL- ;"
		TO PUNCH-12-72.
	MOVE HOLD-X TO PUNCH-28, PUNCH-59.
	PERFORM PUNCH-CARD.

	MOVE GO-TO-LINE TO PUNCH-12-39.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

POBT-EXIT. EXIT.



FIND-ALL-FIELDS.
	IF BF-LEVEL (INDX) IS EQUAL TO HOLD-X
		MOVE BF-NAME (INDX) TO IFL-2;
		MOVE INDX TO IFL-3;
		MOVE IF-LINE TO PUNCH-12-72;
		PERFORM PUNCH-CARD;
		MOVE "OR " TO IFL-1.
*	THIS SECTION PUTS OUT THE DETAIL PRINTING STATEMENTS.

PUT-OUT-DETAIL-PRINT SECTION 18.

START.
	MOVE "PRINT-DETAIL.   " TO PUNCH-8-23.
	PERFORM PUNCH-CARD.

	PERFORM PUT-OUT-ALL-LISTS VARYING INDX-1 FROM 01 BY 1
		UNTIL INDX-1 IS GREATER THAN LT-INDX.
	PERFORM LIST-ALL-ACS VARYING INDX-1 FROM 01 BY 1
		UNTIL INDX-1 IS GREATER THAN 10.

	IF THERE-IS-NO-DETAIL GO TO PODP-Z.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
	MOVE "MOVE OUTPUT-LINE TO PRINT-LINE." TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	MOVE "PERFORM PRINT-1 THRU PL-EXIT." TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	MOVE "MOVE SPACES TO OUTPUT-LINE." TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
	GO TO PODP-Z.



PUT-OUT-ALL-LISTS.
	IF LT-WHEN (INDX-1) IS EQUAL TO "A"
		MOVE LT-IN-NAME (INDX-1) TO LAC-IN-NAME;
		MOVE LT-OUT-NAME (INDX-1) TO SPER-1-25;
		PERFORM MOVE-PERIOD VARYING INDX FROM 24 BY -1 UNTIL INDX < 1;
		MOVE SPER-1-25 TO LAC-OUT-NAME;
		MOVE LIST-AC-LINE TO PUNCH-12-72;
		PERFORM PUNCH-CARD;
		MOVE "Y" TO TEST-FOR-DETAIL.


LIST-ALL-ACS.
	MOVE AC-LIST-NAME (INDX-1) TO SPER-1-25.
	IF SPER-1-25 IS NOT EQUAL TO SPACES
		PERFORM MOVE-PERIOD VARYING INDX FROM 24 BY -1 UNTIL INDX < 1;
		MOVE SPER-1-25 TO LAC-OUT-NAME;
		MOVE AC-IN-NAME (INDX-1) TO LAC-IN-NAME;
		MOVE LIST-AC-LINE TO PUNCH-12-72;
		PERFORM PUNCH-CARD;
		MOVE "Y" TO TEST-FOR-DETAIL.

DETAIL-ADDS.
	IF AC-SIZE (INDX) IS NOT EQUAL TO ZERO
		MOVE AC-IN-NAME (INDX) TO AL0-NAME;
		SUBTRACT 1 FROM INDX GIVING AL0-AC;
		MOVE ADD-TO-LEVEL-0 TO PUNCH-12-72;
		PERFORM PUNCH-CARD.
PODP-Z.
	IF LOW-BREAK IS EQUAL TO SPACE
		MOVE BREAKS (11) TO AL0-LOWEST;
	ELSE
		MOVE LOW-BREAK TO AL0-LOWEST.
	IF AL0-LOWEST IS NOT EQUAL TO SPACE
		PERFORM DETAIL-ADDS VARYING INDX FROM 01 BY 1
		UNTIL INDX IS GREATER THAN 10.

	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	MOVE "GO TO READ-IN." TO PUNCH-12-39.
	PERFORM PUNCH-CARD.
*	THIS SECTION PUTS OUT THE STATEMENTS WHICH PRINT TOTALS
*		ON THE BREAK.

PUT-OUT-BREAK-PRINTS SECTION 20.

 START.
	MOVE BREAKS (LEVEL) TO HOLD-X.
	IF HOLD-X IS EQUAL TO SPACE GO TO POBP-EXIT.

	MOVE "LEVEL- -BREAK.  " TO PUNCH-8-23.
	MOVE HOLD-X TO PUNCH-14.
	PERFORM PUNCH-CARD.

	IF LEVEL EQUALS 11 AND ST-ENTRY (11) EQUALS SPACES
		MOVE "1001" TO ST-ENTRY (11).

	IF ST-SKIP-B (LEVEL) IS EQUAL TO ZERO GO TO POBP-A.
	MOVE "MOVE SPACES TO PRINT-LINE." TO PUNCH-12-39.
	PERFORM PUNCH-CARD.
	MOVE "PERFORM PRINT-CH-  THRU PL-EXIT." TO PUNCH-12-72.
	MOVE ST-SKIP-B (LEVEL) TO PUNCH-29.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
	GO TO POBP-B.

POBP-A.
	IF ST-SPACE-B (LEVEL) IS EQUAL TO ZERO GO TO POBP-B.
	MOVE "MOVE SPACES TO PRINT-LINE." TO PUNCH-12-39.
	PERFORM PUNCH-CARD.

	MOVE "PERFORM PRINT-  THRU PL-EXIT." TO PUNCH-12-72.
	MOVE ST-SPACE-B (LEVEL) TO PUNCH-26.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

POBP-B.
	ALTER POBP-C TO PROCEED TO POBP-H.
	IF A-CARD-COUNT IS NOT EQUAL TO ZERO
		PERFORM POBP-MOVE VARYING INDX-1 FROM 01 BY 1
		UNTIL INDX-1 IS GREATER THAN 10.

	PERFORM POBP-EMIT VARYING INDX-1 FROM 1 BY 1
		UNTIL INDX-1 IS GREATER THAN EM-INDX.

	IF HR-WHEN (LEVEL) IS NOT EQUAL TO SPACE
		AND PAGE-SIZE IS NOT EQUAL TO ZERO
		MOVE "MOVE ZERO TO PAGE-COUNT." TO PUNCH-12-39;
		PERFORM PUNCH-CARD.
POBP-C. GO TO POBP-D. NOTE IF NO TOTALS ARE PRINTED AT THIS
		BREAK, THIS WILL GO TO POBP-H.

POBP-D.
	MOVE "MOVE OUTPUT-LINE TO PRINT-LINE." TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	IF ST-SKIP-A (LEVEL) IS NOT EQUAL TO ZERO
		MOVE "PERFORM PRINT-CH-  THRU PL-EXIT." TO PUNCH-12-72;
		MOVE ST-SKIP-A (LEVEL) TO PUNCH-29;
		GO TO POBP-E.

	MOVE "PERFORM PRINT-  THRU PL-EXIT." TO PUNCH-12-72.
	MOVE ST-SPACE-A (LEVEL) TO TEMP-1; MOVE TEMP-1 TO PUNCH-26.

POBP-E.
	PERFORM PUNCH-CARD.
	MOVE "MOVE SPACES TO OUTPUT-LINE." TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
POBP-H.
	MOVE BREAKS (LEVEL) TO HOLD-X.

	IF LEVEL IS EQUAL TO 11 NEXT SENTENCE; ELSE

	MOVE NEXT-BREAK (LEVEL) TO AN-LEVEL-B;
	IF AN-LEVEL-B IS NOT EQUAL TO SPACE AND
		A-CARD-COUNT IS NOT EQUAL TO ZERO
		MOVE HOLD-X TO AN-LEVEL-A;
		MOVE ADD-TO-NEXT-LINE TO PUNCH-12-72;
		PERFORM PUNCH-CARD;
		PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	IF HOLD-X = "F"
		MOVE "GO TO FINISH." TO PUNCH-12-24;
	ELSE
	IF NEXT-BREAK (LEVEL) = SPACE
		MOVE "LEVEL- -SW. GO TO. NOTE GO TO RESET-LEVEL-  OR FINISH."
			TO PUNCH-8-72;
		MOVE HOLD-X TO PUNCH-14, PUNCH-50;
	ELSE
		MOVE "LEVEL- -SW. GO TO. NOTE GO TO RESET-LEVEL-  OR
-			" LEVEL- -BREAK." TO PUNCH-8-72;
		MOVE HOLD-X TO PUNCH-14, PUNCH-50;
		MOVE NEXT-BREAK (LEVEL) TO PUNCH-61.

POBP-I.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.


POBP-EXIT. EXIT.
POBP-MOVE.
	IF BTTPF (INDX-1, LEVEL) IS NOT EQUAL TO SPACE
		ALTER POBP-C TO PROCEED TO POBP-D;
		MOVE HOLD-X TO ACTL-1;
		MOVE BTTP-NAME (INDX-1) TO SPER-1-25;
		PERFORM MOVE-PERIOD VARYING INDX FROM 24 BY -1 UNTIL INDX < 1;
		MOVE SPER-1-25 TO ACTL-3;
		SUBTRACT 1 FROM INDX-1 GIVING ACTL-2;
		MOVE AC-TOTAL-LINE TO PUNCH-12-72;
		PERFORM PUNCH-CARD.


POBP-ADD.
	IF AC-SIZE (INDX) IS NOT EQUAL TO ZERO
		SUBTRACT 1 FROM INDX GIVING AN-LEVEL-A;
		MOVE INDX TO AN-LEVEL-B;
		MOVE ADD-TO-NEXT-LINE TO PUNCH-12-72;
		PERFORM PUNCH-CARD.

POBP-EMIT.
	MOVE EM-WHEN (INDX-1) TO HOLD-X.
	IF LEVEL = 11 AND HOLD-X = "F" OR HOLD-N = LEVEL - 1
		ALTER POBP-C TO PROCEED TO POBP-D;
		MOVE "MOVE " TO PUNCH-12-16;
		MOVE EM-IN-NAME (INDX-1) TO PUNCH-17-40;
		MOVE " TO " TO PUNCH-41-44;
		MOVE EM-OUT-NAME (INDX-1) TO SPER-1-25;
		PERFORM MOVE-PERIOD VARYING INDX FROM 24 BY -1 UNTIL INDX < 1;
		MOVE SPER-1-25 TO PUNCH-45-69;
		PERFORM PUNCH-CARD.
*	THIS SECTION PUTS OUT THE STATEMENTS WHICH RESET THE BREAK.

PUT-OUT-RESETS SECTION 22.

START.
	MOVE BREAKS (LEVEL) TO HOLD-X.
	IF HOLD-X IS EQUAL TO SPACE GO TO PORES-EXIT.

	MOVE "RESET-LEVEL- .  " TO PUNCH-8-23.
	MOVE HOLD-X TO PUNCH-20.
	PERFORM PUNCH-CARD.

	IF A-CARD-COUNT IS NOT EQUAL TO ZERO
		MOVE "MOVE LOW-VALUES TO LEVEL- -ACS." TO PUNCH-12-72;
		MOVE HOLD-X TO PUNCH-37;
		PERFORM PUNCH-CARD.

	PERFORM PORES-SAVE VARYING INDX FROM 01 BY 1
		UNTIL INDX IS GREATER THAN BF-INDX.

	PERFORM PORES-SETUP VARYING INDX-1 FROM 01 BY 1
		UNTIL INDX-1 IS GREATER THAN LT-INDX.

	IF LEVEL = 11 GO TO PORES-1.

	MOVE NEXT-BREAK (LEVEL) TO HOLD-Y.
	IF HOLD-Y IS EQUAL TO SPACE  NEXT SENTENCE; ELSE
		MOVE "ALTER LEVEL- -SW TO PROCEED TO LEVEL- -BREAK."
		TO PUNCH-12-72;
		MOVE HOLD-X TO PUNCH-24; MOVE HOLD-Y TO PUNCH-49;
		PERFORM PUNCH-CARD.

PORES-1.
	IF HOLD-X IS EQUAL TO LOW-BREAK
		MOVE "GO TO PRINT-DETAIL." TO PUNCH-12-72;
		PERFORM PUNCH-CARD.

	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

PORES-EXIT. EXIT.


PORES-SAVE.
	IF BF-LEVEL (INDX) IS EQUAL TO HOLD-X
		MOVE BF-NAME (INDX) TO SF-NAME;
		MOVE INDX TO SF-NUMBER;
		MOVE SAVE-FIELD-LINE TO PUNCH-12-72;
		PERFORM PUNCH-CARD.


PORES-SETUP.
	IF LT-WHEN (INDX-1) IS EQUAL TO HOLD-X
		MOVE LT-IN-NAME (INDX-1) TO LAC-IN-NAME;
		MOVE LT-OUT-NAME (INDX-1) TO SPER-1-25;
		PERFORM MOVE-PERIOD VARYING INDX FROM 25 BY -1 UNTIL INDX < 1;
		MOVE SPER-1-25 TO LAC-OUT-NAME;
		MOVE LIST-AC-LINE TO PUNCH-12-72;
		PERFORM PUNCH-CARD.
*	THIS SECTION PUTS OUT THE FINALIZATION STATEMENTS.

PUT-OUT-FINISH SECTION 24.

START.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD 2 TIMES.
	MOVE "FINISH." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	MOVE "CLOSE IN-FILE." TO PUNCH-12-39.
	PERFORM PUNCH-CARD.
	MOVE "CLOSE L-PRINT." TO PUNCH-12-39.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	MOVE "STOP RUN." TO PUNCH-12-20.
	PERFORM PUNCH-CARD.
*	THIS SECTION PUTS OUT THE PRINTING SUBROUTINES.

PUT-OUT-SUBROUTINES SECTION 26.

START.
	PERFORM PUT-OUT-CHANS VARYING INDX FROM 3 BY 1 UNTIL
		INDX IS GREATER THAN HIGHEST-CHANNEL + 1.

	MOVE 1 TO SPACE-ENTRY (2).
	PERFORM PUT-OUT-SPACES THRU POSPACE-EXIT VARYING INDX FROM 2 BY 1 UNTIL
		INDX IS GREATER THAN HIGHEST-SPACE + 1.

	MOVE "PL-HDR. MOVE SPACES TO PRINT-LINE." TO PUNCH-8-72.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

	MOVE "PRINT-CH-1. WRITE PRINT-LINE BEFORE TOP-OF-FORM."
		TO PUNCH-8-72; PERFORM PUNCH-CARD.

	IF H-CARD-COUNT IS EQUAL TO ZERO GO TO POSUB-Z.
	MOVE "MOVE HEADER TO PRINT-LINE." TO PUNCH-12-39.
	PERFORM PUNCH-CARD.
	IF PAGE-SIZE IS NOT EQUAL TO ZERO
		MOVE "ADD 1 TO PAGE-COUNT; MOVE PAGE-COUNT TO HEADER-PAGE."
	  TO PUNCH-12-72; PERFORM PUNCH-CARD.

	IF HDR-SP = 1 MOVE "WRITE PRINT-LINE." TO PUNCH-12-28;
	ELSE
		MOVE "WRITE PRINT-LINE BEFORE ADVANCING" TO PUNCH-12-44;
		IF HDR-SK IS NOT EQUAL TO ZERO
		MOVE "CHANNEL- ." TO PUNCH-46-55;
		MOVE HDR-SK TO PUNCH-54;
	ELSE
		MOVE "  LINES.  "  TO PUNCH-46-55;
		MOVE HDR-SP TO PUNCH-46.

	PERFORM PUNCH-CARD.
	SUBTRACT HDR-SP FROM 56 GIVING SLC-AMOUNT.
	MOVE SET-LINE-COUNT TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.
	GO TO POSUB-Z.
PUT-OUT-CHANS.
	MOVE CHAN-ENTRY (INDX) TO HOLD-X.
	IF HOLD-X IS NOT EQUAL TO SPACE
		MOVE "PRINT-CH- . WRITE PRINT-LINE BEFORE ADVANCING CHANNEL- ."
		TO PUNCH-8-72;
		MOVE HOLD-X TO PUNCH-17, PUNCH-62;
		PERFORM PUNCH-CARD;
		MOVE "IF LINE-COUNT IS POSITIVE GO TO PL-EXIT; ELSE GO TO PL-HDR."
		TO PUNCH-12-72;
		PERFORM PUNCH-CARD;
		PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.


PUT-OUT-SPACES.
	MOVE SPACE-ENTRY (INDX) TO HOLD-X.
	IF HOLD-X IS EQUAL TO SPACE GO TO POSPACE-EXIT.

	IF HOLD-X IS EQUAL TO 1
		MOVE "PRINT-1. WRITE PRINT-LINE." TO PUNCH-8-72;
	ELSE
		MOVE "PRINT- . WRITE PRINT-LINE BEFORE   LINES;" TO
		PUNCH-8-72;
		MOVE HOLD-X TO PUNCH-14, PUNCH-41.
	PERFORM PUNCH-CARD.

	MOVE "SUBTRACT   FROM LINE-COUNT." TO PUNCH-12-72.
	MOVE HOLD-X TO PUNCH-21.
	PERFORM PUNCH-CARD.

	MOVE "IF LINE-COUNT IS POSITIVE GO TO PL-EXIT; ELSE GO TO PL-HDR."
		TO PUNCH-12-72.
	PERFORM PUNCH-CARD.
	PERFORM PUNCH-BLANK-CARD THRU PUNCH-CARD.

POSPACE-EXIT.  EXIT.


POSUB-Z.
	MOVE "PL-EXIT.  EXIT. " TO PUNCH-8-23.
	PERFORM PUNCH-CARD.
@c