 D*OPTNS YSCMP
IDENTIFICATION DIVISION.
PROGRAM-ID. SALPGT.
AUTHOR. JOHN DOE.
INSTALLATION. ABC COMPANY, WIDGET DIVISION.
DATE-WRITTEN. 03/01/73.
DATE-COMPILED. 03/01/73.
SECURITY. NONE.
REMARKS. THIS PROGRAM WILL PRODUCE A SIMPLE REPORT OF SALARY
        AND WAGE INFORMATION.
ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
SOURCE-COMPUTER. PDP-10.
OBJECT-COMPUTER. PDP-10.
SPECIAL-NAMES.
        CHANNEL (1) IS TOP-OF-PAGE.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
        SELECT EMPLOYEE-FILE ASSIGN TO DSK
        RECORDING MODE IS ASCII.
        SELECT RATE-FILE ASSIGN TO DSK
        RECORDING MODE IS ASCII.
        SELECT SALARY-REPORT ASSIGN TO DSK
        RECORDING MODE IS ASCII.
DATA DIVISION.
FILE SECTION.
FD  EMPLOYEE-FILE
        LABEL RECORDS ARE STANDARD
        VALUE OF IDENTIFICATION IS "SALPG1EMP".
01      EMPREC.
        05 NAME   PIC X(15).
        05 EMPNUM PIC X(5).
        05 HOURS  PIC 99V9.
        05 S-CODE.
           10 S-CD-1 PIC X.
           10 S-CD-2 PIC X.
FD RATE-FILE
        LABEL RECORDS ARE STANDARD
        VALUE OF IDENTIFICATION IS "SALPG1RAT".
01      RATE-REC PIC X(7).
FD SALARY-REPORT
        LABEL RECORDS ARE STANDARD
        VALUE OF IDENTIFICATION IS "SALPGTRPT".
01      SALARY-REC PIC X(46).
WORKING-STORAGE SECTION.
77  COMPUTED-AMT PIC S9(5)V99 COMP, VALUE ZERO.
77  LINE-COUNT PIC S9(3) COMP, VALUE ZERO.
01  DATER.
        05 YR PIC XX.
        05 MO PIC XX.
        05 DA PIC XX.
        05 FILLER PIC X(6).
01  TOTAL.
        05 HOURS  PIC S9(4)V9 COMP, VALUE ZERO.
        05 AMOUNT PIC S9(6)V99 COMP, VALUE ZERO.
01  RATE-TABLE.
        05 RT-TBL OCCURS 18 TIMES INDEXED BY IDX.
           10 S-CODE.
              15 S-CD-1 PIC X.
              15 S-CD-2 PIC X.
           10 RATE  PIC S9(3)V99.
01  SALREC.
        05 NAME   PIC X(15).
        05 FILLER PIC X(3) VALUE SPACE.
        05 EMPNUM PIC X(5).
        05 FILLER PIC X(4) VALUE SPACE.
        05 HOURS  PIC ZZ.Z.
        05 FILLER PIC X(4) VALUE SPACE.
        05 S-CODE PIC XX.
        05 AMOUNT PIC $$$$$$.99.
01  HDG-1.
        05 MO     PIC XX.
        05 FILLER PIC X VALUE '/'.
        05 DA     PIC XX.
        05 FILLER PIC X VALUE '/'.
        05 YR     PIC XX.
        05 FILLER PIC X(30) VALUE
           '    WEEKLY SALARY ANALYSIS    '.
        05 FILLER PIC X(5) VALUE 'PAGE '.
        05 PG-NUM PIC 999.
01  HDG-2  PIC X(46) VALUE
        ' EMPLOYEE NAME      EMP.   HRS.   SAL.   GROSS'.
01  HDG-3 PIC X(46) VALUE
        '                    NO.           CODE  AMOUNT'.
01  TRLR-1 PIC X(46) VALUE
        '                           ----       --------'.
01  TRLR-2.
        05 FILLER PIC X(7) VALUE '  TOTAL'.
        05 FILLER PIC X(18) VALUE SPACE.
        05 HOURS  PIC Z(4).Z.
        05 FILLER PIC X(5) VALUE SPACE.
        05 AMOUNT PIC $(7).99.
01  TRLR-3 PIC X(46) VALUE
        '                END OF REPORT                 '.
PROCEDURE DIVISION.
HOUSE-KEEP.
        OPEN INPUT EMPLOYEE-FILE.
        OPEN INPUT RATE-FILE.
        OPEN OUTPUT SALARY-REPORT.
        SET IDX, PG-NUM TO ZERO.
        MOVE TODAY  TO DATER.
        MOVE CORRESPONDING DATER TO HDG-1.
        PERFORM BUILD-RATE-TABLE THRU BUILD-EXIT.
PAGE-HDG.
        MOVE ZERO TO LINE-COUNT.
        ADD 1 TO PG-NUM.
        WRITE SALARY-REC FROM HDG-1 AFTER ADVANCING TOP-OF-PAGE.
        WRITE SALARY-REC FROM HDG-2 AFTER ADVANCING 2 LINES.
        MOVE ALL SPACE TO SALARY-REC, WRITE SALARY-REC.
        WRITE SALARY-REC FROM HDG-3 BEFORE ADVANCING 2 LINES.
MAIN-PGM.
        READ EMPLOYEE-FILE AT END CLOSE EMPLOYEE-FILE,
               GO TO WRITE-TRLRS.
        MOVE CORRESPONDING EMPREC TO SALREC.
        ADD HOURS IN EMPREC TO HOURS IN TOTAL.
        MOVE ZERO TO IDX.
        PERFORM SEARCH-RATES.
        ADD COMPUTED-AMT TO AMOUNT IN TOTAL.
        MOVE COMPUTED-AMT TO AMOUNT IN SALREC.
        WRITE SALARY-REC FROM SALREC.
        ADD 1 TO LINE-COUNT.
        IF LINE-COUNT GREATER 19, PERFORM PAGE-HDG.
        GO TO MAIN-PGM.
 DETAP     SEARCH-RATES                      00001 03 06 04
 RL1                                         0
 RL2                                        X1              X
 RL1 0            0           0           E
 RL2 2            3           4           L  $
        ADD 1 TO IDX.
 C IDX > 18                                  Y
 R   N            :           :           -
 C S-CODE IN EMPREC = S-CODE IN RT-TBL (IDX) *
 R   N            Y           Y           -
 C S-CD-1 IN RT-TBL (IDX) =                  -
 R   -            'H'         NOT         -
 A MOVE ZERO TO COMPUTED-AMT                 01
 A COMPUTE COMPUTED-AMT ROUNDED =
    HOURS IN EMPREC * RATE (IDX)
 R                X
 A MOVE RATE (IDX) TO COMPUTED-AMT
 R                            X
 A GO TO                                     02)SEARCH-EXIT
 R   SEARCH-RATES             SEARCH-EXIT
 A STOP RUN.
 R                                        15
 A DISPLAY 'ERROR IN SEARCH-RATES-TABLE'.
 R                                        10
 TEND
SEARCH-EXIT.
        EXIT.
WRITE-TRLRS SECTION.
        WRITE SALARY-REC FROM TRLR-1.
        MOVE CORRESPONDING TOTAL TO TRLR-2.
        WRITE SALARY-REC FROM TRLR-2.
        WRITE SALARY-REC FROM TRLR-3 AFTER ADVANCING 2 LINES.
        CLOSE SALARY-REPORT.
        DISPLAY
        '*** END OF JOB, TYPE SALPGT.RPT FOR SALARY REPORT.'.
        STOP RUN.
BUILD-RATE-TABLE.
        SET IDX UP BY 1.
        IF IDX GREATER 18, GO TO BUILD-EXIT.
        READ RATE-FILE INTO RT-TBL (IDX),
           AT END CLOSE RATE-FILE, GO TO BUILD-EXIT.
        GO TO BUILD-RATE-TABLE.
BUILD-EXIT.
        EXIT.
