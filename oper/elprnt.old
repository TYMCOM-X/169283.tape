      SUBROUTINE ELPRNT
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(16),IDI,IDO
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/TRAN/JTRFLG,TSTEP,TSTOP,TSTART,NOTINT,STEPS(5),ENDPTS(5),
     1   KFROUT,FORFRE,KFPTS
C
C
      DIMENSION ITABLE(1),NNODS(10)
      EQUIVALENCE (ITABLE(1),YNL(1))
      DATA NNODS /2,2,2,2,4,2,3,2,3,4/
      DATA IPULSE,ISINE,IEXP,IBLNK/'PULSE','SINE','EXP',' '/
C
C  PRINT LISTING OF ELEMENTS
C
      IF (NOPRNT.GT.0) GO TO 250
      WRITE (IDO,11)
11      FORMAT(/1X,'C I R C U I T   S U M M A R Y'/)
C
C  PRINT EXTERNAL MODELS
C
      ISTART=LOCATE(20)
      ISTOP=LOCATE(21)-1
      IF (ISTART.GT.ISTOP) GO TO 50
      WRITE (IDO,21)
21      FORMAT(/1X,'*****EXTERNAL MODEL ELEMENTS'/)
        WRITE (IDO,22)
22      FORMAT(1X,'NAME',4X,'MODEL',4X,'NODES')
      DO 40 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)-1
      JSTOP=VALUE(I)
      DO 30 J=1,JSTOP
      NODE1=NODPLC(LOC+J)
   30 ITABLE(J)=JUNODE(NODE1)
      WRITE (IDO,31) NAME(I),MODNAM(MNAM),(ITABLE(J),J=1,JSTOP)
   31 FORMAT (1X,A5,3X,A5,3X,10(I3,2X)/17X,10(I3,2X))
   40 CONTINUE
C
C  PRINT RESISTORS, CAPACITORS, AND INDUCTORS
C
   50 ISTOP=LOCATE(4)-1
      IF (ISTOP.LT.1) GO TO 80
      WRITE (IDO,51)
51     FORMAT (/1X,'*****RESISTORS, CAPACITORS, AND INDUCTORS'/)
        WRITE (IDO,52)
52      FORMAT(1X,'NAME     NODES    VALUE')
        DO 70 I=1,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      WRITE (IDO,61) NAME(I),JUNODE(NODE1),JUNODE(NODE2),VALUE(I)
61      FORMAT(1X,A5,2X,I3,1X,I3,2X,1PE9.2)
   70 CONTINUE
C
C  PRINT VOLTAGE CONTROLLED CURRENT SOURCES
C
   80 ISTART=LOCATE(5)
      ISTOP=LOCATE(6)-1
      IF (ISTART.GT.ISTOP) GO TO 110
      WRITE (IDO,81)
81    FORMAT (/,1X,'*****VOLTAGE CONTROLLED CURRENT SOURCES'//6X,
     1   'NAME',9X,1H+,4X,1H-,3X,2H+C,3X,2H-C,4X,'VALUE',6X,'DELAY')
      DO 100 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE4=NODPLC(LOC+3)
      MNAM=MNAME(I)
      WRITE (IDO,91) NAME(I),JUNODE(NODE1),JUNODE(NODE2),JUNODE(NODE3),
     1   JUNODE(NODE4),VALUE(I),SOURCE(MNAM)
91    FORMAT (6X,A5,6X,4(I3,2X),1PE9.2,2X,E9.2)
  100 CONTINUE
C
C  PRINT TRANSISTORS
C
  110 ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) GO TO 140
      WRITE (IDO,111)
111     FORMAT(/1X,'*****BIPOLAR JUNCTION TRANSISTORS'/)
        WRITE (IDO,112)
112     FORMAT(1X,'NAME     C   B   E  MODEL     AREA')
      DO 130 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      WRITE (IDO,121) NAME(I),JUNODE(NODE1),JUNODE(NODE2),JUNODE(NODE3),
     1   MNAME(I),VALUE(I)
121     FORMAT(1X,A5,2X,3(I3,1X),1X,A5,F9.3)
  130 CONTINUE
C
C  PRINT DIODES
C
  140 ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) GO TO 170
      WRITE (IDO,141)
141     FORMAT(/1X,'*****DIODES'/)
        WRITE (IDO,142)
142     FORMAT(1X,'NAME     +   -  MODEL     AREA')
      DO 160 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      WRITE (IDO,151) NAME(I),JUNODE(NODE1),JUNODE(NODE2),MNAME(I),
     1   VALUE(I)
151     FORMAT(1X,A5,2X,2(I3,1X),1X,A5,F9.3)
  160 CONTINUE
C
C  PRINT JFETS
C
  170 ISTART=LOCATE(9)
      ISTOP=LOCATE(10)-1
      IF (ISTART.GT.ISTOP) GO TO 200
      WRITE (IDO,171)
171     FORMAT(/1X,'*****JFETS'/)
        WRITE (IDO,172)
172     FORMAT(1X,'NAME     D   G   S  MODEL     AREA')
      DO 190 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      WRITE (IDO,121) NAME(I),JUNODE(NODE1),JUNODE(NODE2),JUNODE(NODE3),
     1   MNAME(I),VALUE(I)
  190 CONTINUE
C
C  PRINT MOSFETS
C
  200 ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) GO TO 230
      WRITE (IDO,201)
201     FORMAT(/1X,'*****MOSFETS'/)
        WRITE (IDO,202)
202     FORMAT(1X,'NAME     D   G   S   B  MODEL     AREA')
      DO 220 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE4=NODPLC(LOC+3)
      WRITE (IDO,211) NAME(I),JUNODE(NODE1),JUNODE(NODE2),JUNODE(NODE3),
     1   JUNODE(NODE4),MNAME(I),VALUE(I)
211     FORMAT(1X,A5,2X,4(I3,1X),1X,A5,F9.3)
  220 CONTINUE
C
C  PROCESS INDEPENDENT SOURCES
C
  230 WRITE (IDO,241)
241     FORMAT(/1X,'*****INDEPENDENT SOURCES'/)
        WRITE (IDO,242)
242     FORMAT(1X,'NAME     +   -  DC VALUE   AC VALUE   AC PHASE')
  250 DO 370 ID=4,6,2
      ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 370
      DO 360 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      MNAM=MNAME(I)
      ITYPE=SOURCE(MNAM+3)
      IF (NOPRNT.GT.0) GO TO 270
      NAM=IBLNK
      IF (ITYPE.EQ.1) NAM=IPULSE
      IF (ITYPE.EQ.2) NAM=IEXP
      IF (ITYPE.EQ.3) NAM=ISINE
      WRITE (IDO,261) NAME(I),JUNODE(NODE1),JUNODE(NODE2),SOURCE(MNAM),
     1   SOURCE(MNAM+1),SOURCE(MNAM+4)
261     FORMAT(1X,A5,2X,2(I3,1X),1X,3(G9.3,2X))
  270 IF (JTRFLG.EQ.0) GO TO 350
      IF (ITYPE.EQ.0) GO TO 350
        IF(NOPRNT.GT.0)GO TO 272
        WRITE (IDO,271)NAM
271     FORMAT(6X,'TRANSIENT'/6X,A5)
272   GO TO (280,300,320),ITYPE
C
C  PULSE SOURCE
C
  280 IF (SOURCE(MNAM+2).EQ.1.0) GO TO 290
      IF (SOURCE(MNAM+7).LT.TSTEP) SOURCE(MNAM+7)=TSTEP
      IF (SOURCE(MNAM+8).LT.TSTEP) SOURCE(MNAM+8)=TSTEP
      IF (SOURCE(MNAM+9).LT.TSTEP) SOURCE(MNAM+9)=TSTEP
      IF (SOURCE(MNAM+10).EQ.0.0) SOURCE(MNAM+10)=TSTOP
      IF (SOURCE(MNAM+10).LT.TSTEP) SOURCE(MNAM+10)=TSTEP
      TEMP=TSTEP+SOURCE(MNAM+8)+SOURCE(MNAM+9)+SOURCE(MNAM+10)
      IF (SOURCE(MNAM+11).EQ.0.0) SOURCE(MNAM+11)=TSTOP
      IF (SOURCE(MNAM+11).LT.TEMP) SOURCE(MNAM+11)=TEMP
  290 IF (NOPRNT.GT.0) GO TO 350
      JSTART=MNAM+5
      JSTOP=MNAM+11
      WRITE (IDO,291) (SOURCE(J),J=JSTART,JSTOP)
291     FORMAT(6X,'VALUES',3X,1PE9.2,2X,1PE9.2/6X,'DELAY',4X,1PE9.2
     1   /6X,'RISETIME',1X,1PE9.2/6X,'FALLTIME',1X,1PE9.2
     2  /6X,'WIDTH',4X,1PE9.2/6X,'PERIOD',3X,1PE9.2/)
      GO TO 350
C
C  EXPONENTIAL SOURCE
C
  300 IF (SOURCE(MNAM+2).EQ.1.0) GO TO 310
      IF (SOURCE(MNAM+7).LT.TSTEP) SOURCE(MNAM+7)=TSTEP
      IF (SOURCE(MNAM+8).LT.TSTEP) SOURCE(MNAM+8)=TSTEP
      TEMP=SOURCE(MNAM+7)+TSTEP
      IF (SOURCE(MNAM+9).LT.TEMP) SOURCE(MNAM+9)=TEMP
      IF (SOURCE(MNAM+10).LT.TSTEP) SOURCE(MNAM+10)=TSTEP
  310 IF (NOPRNT.GT.0) GO TO 350
      JSTART=MNAM+5
      JSTOP=MNAM+10
      WRITE (IDO,311) (SOURCE(J),J=JSTART,JSTOP)
311     FORMAT(6X,'VALUES',3X,1PE9.2,2X,1PE9.2/6X,'DELAY1',3X,1PE9.2
     1  /6X,'TAU1',5X,1PE9.2/6X,'DELAY2',3X,1PE9.2/6X,'TAU2',5X,1PE9.2/)
      GO TO 350
C
C  SINUSOIDAL SOURCE
C
  320 IF (SOURCE(MNAM+2).EQ.1.0) GO TO 330
      IF (SOURCE(MNAM+7).EQ.0.0) SOURCE(MNAM+7)=1.0/TSTOP
      IF (SOURCE(MNAM+8).LT.TSTEP) SOURCE(MNAM+8)=TSTEP
  330 IF (NOPRNT.GT.0) GO TO 350
      JSTART=MNAM+5
      JSTOP=MNAM+9
      WRITE (IDO,331) (SOURCE(J),J=JSTART,JSTOP)
331     FORMAT(6X,'OFFSET',3X,1PE9.2/6X,'AMP',6X,1PE9.2
     1   /6X,'FREQ',5X,1PE9.2/6X,'DELAY',4X,1PE9.2/6X'THETA',4X,1PE9.2/)
  350 SOURCE(MNAM+2)=1.0
  360 CONTINUE
  370 CONTINUE
      DO 420 ID=4,6,2
      ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 420
      DO 410 I=ISTART,ISTOP
      MNAM=MNAME(I)
      ITYPE=SOURCE(MNAM+3)
      IF (SOURCE(MNAM+2).EQ.0.0) GO TO 410
      TEMP=SOURCE(MNAM+4)/RAD
      SOURCE(MNAM+4)=SOURCE(MNAM+1)*SIN(TEMP)
      SOURCE(MNAM+1)=SOURCE(MNAM+1)*COS(TEMP)
      IF (JTRFLG.EQ.0) GO TO 400
      IF (ITYPE.EQ.0) GO TO 400
      GO TO (380,400,390),ITYPE
  380 SOURCE(MNAM+8)=SOURCE(MNAM+7)+SOURCE(MNAM+8)
      TEMP=SOURCE(MNAM+9)
      SOURCE(MNAM+9)=SOURCE(MNAM+8)+SOURCE(MNAM+10)
      SOURCE(MNAM+10)=SOURCE(MNAM+9)+TEMP
      GO TO 400
  390 TEMP=SOURCE(MNAM+7)*TWOPI
      SOURCE(MNAM+7)=SOURCE(MNAM+8)
      SOURCE(MNAM+8)=SOURCE(MNAM+9)
      SOURCE(MNAM+9)=TEMP
  400 SOURCE(MNAM+2)=0.0
  410 CONTINUE
  420 CONTINUE
C
C  INVERT RESISTANCE AND INDUCTANCE VALUES
C
      ISTOP=LOCATE(2)-1
      IF (ISTOP.LT.1) GO TO 480
      DO 470 I=1,ISTOP
  470 VALUE(I)=1.0/VALUE(I)
  480 ISTART=LOCATE(3)
      ISTOP=LOCATE(4)-1
      IF (ISTART.GT.ISTOP) GO TO 500
      DO 490 I=ISTART,ISTOP
      VALUE(I)=1.0/VALUE(I)
  490 CONTINUE
C
C  CONSTRUCT NODE TABLE
C
  500 ISTOP=NUNODS+1
      DO 510 I=1,ISTOP
  510 IUR(I)=1
      DO 570 ID=1,10
      ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 570
      DO 560 I=ISTART,ISTOP
      JSTART=LOCAL(I)
      JSTOP=JSTART+NNODS(ID)-1
      DO 550 J=JSTART,JSTOP
      NODE=NODPLC(J)
      ISPOT=IUR(NODE+1)
      K=IUR(NUNODS+1)
  520 K=K-1
      IF (K.LT.ISPOT) GO TO 530
      ITABLE(K+1)=ITABLE(K)
      GO TO 520
  530 ITABLE(ISPOT)=I
      K=NODE
      KSTOP=NUNODS+1
  540 K=K+1
      IF (K.GT.KSTOP) GO TO 550
      IUR(K)=IUR(K)+1
      GO TO 540
  550 CONTINUE
  560 CONTINUE
  570 CONTINUE
C
C  CHECK FOR SINGULAR NODES AND CUTSETS OF CAPACITORS AND/OR
C  CURRENT SOURCES
C
      DO 630 I=2,NUNODS
      JSTART=IUR(I)
      JSTOP=IUR(I+1)-1
      IF (JSTART.GE.JSTOP) GO TO 620
      DO 610 J=JSTART,JSTOP
      IELNUM=ITABLE(J)
      IF (IELNUM.LT.LOCATE(2)) GO TO 630
      IF (IELNUM.LT.LOCATE(3)) GO TO 610
      IF (IELNUM.LT.LOCATE(4)) GO TO 630
      IF (IELNUM.LT.LOCATE(6)) GO TO 610
      GO TO 630
  610 CONTINUE
      NOGO=1
      WRITE (5,611) JUNODE(I)
  611 FORMAT (/1X,'  INPUT DATA CONTAINS CUTSET OF '
     1   'CAPACITORS AND/OR CURRENT SOURCES AT NODE ',I5/)
      GO TO 630
  620 NOGO=1
      WRITE (5,621) JUNODE(I)
  621 FORMAT (/1X,'  LESS THAN TWO ELEMENTS ARE CONNECTED '
     1   'TO NODE ',I5/)
  630 CONTINUE
C
C  PRINT NODE TABLE
C
      IF (NOPRNT.GT.0) RETURN
      WRITE (IDO,801)
801     FORMAT(/1X,'*****NODE TABLE'//1X,'NODE     ELEMENTS CONNECTED'/)
      ISTOP=IUR(NUNODS+1)-1
      DO 810 I=1,ISTOP
      IELNUM=ITABLE(I)
      ITABLE(I)=NAME(IELNUM)
  810 CONTINUE
      DO 830 I=1,NUNODS
      JSTART=IUR(I)
      JSTOP=IUR(I+1)-1
      IF (JSTART.GT.JSTOP) GO TO 830
      WRITE (IDO,821) JUNODE(I),(ITABLE(J),J=JSTART,JSTOP)
821     FORMAT(1X,I3,2X,9(A5,2X),20(/6X,9(A5,2X)))
  830 CONTINUE
      RETURN
      END
