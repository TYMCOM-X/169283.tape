      SUBROUTINE BJT
         DOUBLE PRECISION YNL,TSTORE,TRACUR,VN,VNIM1
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
        DOUBLE PRECISION GMIN,PERTOL,VNTOL
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      DIMENSION VBEO(1),VBCO(1),CJBE(1),CJBC(1),VCCS(1),CCCS(1)
      EQUIVALENCE (VBEO(1),TRACUR(1)),(VBCO(1),TRACUR(2)),
     1   (CJBE(1),TRACUR(3)),(CJBC(1),TRACUR(4)),(VCCS(1),TRACUR(5)),
     2   (CCCS(1),TRACUR(6))
C
        DOUBLE PRECISION ARGBC,ARGBE,BF,BR,CB,CBC,CBE,CC,CCCS,CCS,
     +  CEQBC,CEQBE,CEQCS,CJBC,CJBE,CS,CSAT,CTBC,CTBE,CZBC,CZBE,
     +  C1,C2,C3,C4,DQBDVC,DQBDVE,EVBC,EVBE,EVC,EVCN,EVE,EVEN,FVA,
     +  GBPR,GCCS,GCPR,GEPR,GM,GMPGO,GMU,GO,GPI,GSAT,GTEMP,OIK,OIKR,
     +  OVA,OVB,PC,PE,QB,Q1,Q2,SARGBC,SARGBE,SQARG,TF,TR,TYPE,
     +  VBC,VBCO,VBE,VBEO,VCCC,VTC,XMC,XME
C
      ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) RETURN
      ISPOT=ICURNT(7)
      DO 1000 I=ISTART,ISTOP
C
C  LOOKUP MATRIX POINTERS
C
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      LOC4=NODPLC(LOC+3)
      LOC5=NODPLC(LOC+4)
      LOC6=NODPLC(LOC+5)
      IF (MODE.EQ.2) GO TO 50
      LOC7=MATLOC(IFIND+1)
      LOC8=MATLOC(IFIND+2)
      LOC9=MATLOC(IFIND+3)
      LOC10=MATLOC(IFIND+4)
      LOC11=MATLOC(IFIND+5)
      LOC12=MATLOC(IFIND+6)
      LOC13=MATLOC(IFIND+7)
      LOC14=MATLOC(IFIND+8)
      LOC15=MATLOC(IFIND+9)
      LOC16=MATLOC(IFIND+10)
      LOC17=MATLOC(IFIND+11)
      LOC18=MATLOC(IFIND+12)
      IFIND=IFIND+12
C
C  DC ANALYSIS
C
   50 TYPE=SYMVAL(MNAM,1)
      GBPR=SYMVAL(MNAM,4)
      GCPR=SYMVAL(MNAM,5)
      GEPR=SYMVAL(MNAM,6)
      CSAT=SYMVAL(MNAM,12)*VALUE(I)
C
C  INITIALIZE JUNCTIONS ON FIRST DC PASS
C
      IF (IPASS1.EQ.0) GO TO 60
      IF (MODE.GT.1) GO TO 55
      VBE=VT*DLOG(VT/(1.414*CSAT))
      VBC=-1.0D0
      GO TO 70
   55 VBE=VBEO(ISPOT)
      VBC=VBCO(ISPOT)
      GO TO 80
   60 VBE=TYPE*(VNIM1(LOC5)-VNIM1(LOC6))
      VBC=TYPE*(VNIM1(LOC5)-VNIM1(LOC4))
      CALL JUNCT(VBE,VBEO(ISPOT),CSAT,VT)
      CALL JUNCT(VBC,VBCO(ISPOT),CSAT,VT)
   70 VBEO(ISPOT)=VBE
      VBCO(ISPOT)=VBC
C
C  EBERS-MOLL TRANSISTOR MODEL
C
   80 IF (KIND(MNAM).EQ.2) GO TO 200
      BF=SYMVAL(MNAM,2)
      BR=SYMVAL(MNAM,3)
      OVA=SYMVAL(MNAM,15)
C
C  COMPUTE EQUIVALENT DC CONDUCTANCES
C
      EVBE=DEXP(VBE/VT)
      EVBC=DEXP(VBC/VT)
      FVA=1.0D0-OVA*VBC
      CBE=CSAT*(EVBE-1.0D0)
      CBC=CSAT*(EVBC-1.0D0)
      GSAT=CSAT/VT
      GPI=(GSAT/BF)*EVBE
      IF (GPI.LT.GMIN) GPI=GMIN
      GMU=(GSAT/BR)*EVBC
      IF (GMU.LT.GMIN) GMU=GMIN
      GO=(CBE-CBC)*OVA+GSAT*EVBC*FVA
      GM=GSAT*EVBE*FVA-GO
      GMPGO=GM+GO
      CC=(CBE-CBC)*FVA-CBC/BR
      CB=CBE/BF+CBC/BR
      CEQBC=TYPE*(CC-VBE*(GMPGO)+VBC*(GMU+GO))
      CEQBE=TYPE*(-CC-CB+VBE*GMPGO+VBE*GPI-VBC*GO)
C
C  ZERO TRANSIENT CONDUCTANCES IN DC ANALYSIS
C
      IF (MODE.GT.1) GO TO 110
      TRACUR(ISPOT+2)=CC
      TRACUR(ISPOT+3)=CB
      GCCS=0.0
      CEQCS=0.0
      GO TO 500
C
C  CHARGE STORAGE ELEMENTS
C
  110 CCS=SYMVAL(MNAM,7)
      TF=SYMVAL(MNAM,8)
      TR=SYMVAL(MNAM,9)
      CZBE=SYMVAL(MNAM,10)*VALUE(I)
      CZBC=SYMVAL(MNAM,11)*VALUE(I)
      PE=SYMVAL(MNAM,13)
      PC=SYMVAL(MNAM,14)
C
C  COMPUTE DEPLETION LAYER VOLTAGES
C
      ARGBE=PE-VBE
      IF (ARGBE.LT.1.D-1) ARGBE=1.D-1
      SARGBE=DSQRT(ARGBE)
      ARGBC=PC-VBC
      IF (ARGBC.LT.1.D-1) ARGBC=1.D-1
      SARGBC=DSQRT(ARGBC)
C
C  SMALL SIGNAL PARAMETERS
C
      IF (MODE.GT.2) GO TO 150
      TRACUR(ISPOT+4)=GPI
      TRACUR(ISPOT+5)=TF*(CBE+CSAT)/VT+CZBE/SARGBE
      TRACUR(ISPOT+6)=GMU
      TRACUR(ISPOT+7)=TR*(CBC+CSAT)/VT+CZBC/SARGBC
      TRACUR(ISPOT+8)=GM
      TRACUR(ISPOT+9)=GO
      GO TO 900
C
C  TRANSIENT ANALYSIS
C
  150 CTBE=(2.0D0*TF*CBE-4.0D0*CZBE*SARGBE)/DELTA
      CTBC=(2.0D0*TR*CBC-4.0D0*CZBC*SARGBC)/DELTA
C
C  INITIALIZE ON FIRST TRANSIENT PASS
C
      IF (IPASS1.EQ.0) GO TO 160
      CJBE(ISPOT)=CTBE
      CJBC(ISPOT)=CTBC
      VCCS(ISPOT)=VNIM1(LOC4)
      CCCS(ISPOT)=0.0
C
C  UPDATE AT FIRST ITERATION OF NEW TIMEPOINT
C
  160 IF (MODE.EQ.3) GO TO 170
      CJBE(ISPOT)=CTBE*(1.0D0+DELTA/DELOLD)-CJBE(ISPOT)
      CJBC(ISPOT)=CTBC*(1.0D0+DELTA/DELOLD)-CJBC(ISPOT)
      CCCS(ISPOT)=(2.0D0*CCS/DELOLD)*(VNIM1(LOC4)-VCCS(ISPOT))-
     1  CCCS(ISPOT)
      VCCS(ISPOT)=VNIM1(LOC4)
C
C  COMPUTE CAPACITOR EQUIVALENT CONDUCTANCES AND CURRENT SOURCES
C
  170 GTEMP=(2.0D0*TF*(CSAT+CBE)/VT+2.0D0*CZBE/SARGBE)/DELTA
      GPI=GPI+GTEMP
      CEQBE=CEQBE+TYPE*(GTEMP*VBE-CTBE+CJBE(ISPOT))
      GTEMP=(2.0D0*TR*(CSAT+CBC)/VT+2.0D0*CZBC/SARGBC)/DELTA
      GMU=GMU+GTEMP
      CEQBC=CEQBC+TYPE*(GTEMP*VBC-CTBC+CJBC(ISPOT))
      GCCS=2.0D0*CCS/DELTA
      CEQCS=VCCS(ISPOT)*GCCS+CCCS(ISPOT)
      GO TO 500
C
C  GUMMEL-POON TRANSISTOR MODEL
C
  200 C1=SYMVAL(MNAM,2)*VALUE(I)
      C3=SYMVAL(MNAM,3)*VALUE(I)
      OVA=SYMVAL(MNAM,13)
      OVB=SYMVAL(MNAM,14)
      C2=SYMVAL(MNAM,15)*VALUE(I)
      OIK=SYMVAL(MNAM,16)/VALUE(I)
      VTE=SYMVAL(MNAM,17)
      C4=SYMVAL(MNAM,18)*VALUE(I)
      OIKR=SYMVAL(MNAM,19)/VALUE(I)
      VTC=SYMVAL(MNAM,20)
      EVE=DEXP(VBE/VT)
      EVEN=DEXP(VBE/VTE)
      EVC=DEXP(VBC/VT)
      EVCN=DEXP(VBC/VTC)
      CBE=CSAT*(EVE-1.0D0)
      CBC=CSAT*(EVC-1.0D0)
C
C  DETERMINE BASE CHARGE TERMS
C
      Q1=1.0D0+OVA*VBC+OVB*VBE
      Q2=OIK*CBE+OIKR*CBC
      SQARG=DSQRT(Q1*Q1+4.*Q2)
      QB=(Q1+SQARG)/2.
      DQBDVE=(OVB+(1.0D0/SQARG)*(Q1*OVB+2.0D0*OIK*(CSAT+CBE)/VT))/2.0D0
      DQBDVC=(OVA+(1.0D0/SQARG)*(Q1*OVA+2.0D0*OIKR*(CSAT+CBC)/VT))/2.0D0
      IF (QB.GT.1.0D-5) GO TO 210
      QB=1.D-5
      DQBDVE=0.
      DQBDVC=0.
C
C  DETERMINE DC INCREMENTAL CONDUCTANCES
C
  210 CS=CSAT/QB
      CC=CS*(EVE-EVC)-C3*(EVC-1.0D0)-C4*(EVCN-1.0D0)
      CB=C1*(EVE-1.0D0)+C2*(EVEN-1.0D0)+C3*(EVC-1.0D0)+C4*(EVCN-1.0D0)
      GPI=(C1/VT)*EVE+(C2/VTE)*EVEN
      IF (GPI.LT.GMIN) GPI=GMIN
      GMU=(C3/VT)*EVC+(C4/VTC)*EVCN
      IF (GMU.LT.GMIN) GMU=GMIN
      GO=CS*(EVC/VT+(EVE-EVC)*DQBDVC/QB)
      GM=CS*(EVE/VT-(EVE-EVC)*DQBDVE/QB)-GO
      GMPGO=GM+GO
      CEQBC=TYPE*(CC-VBE*(GMPGO)+VBC*(GMU+GO))
      CEQBE=TYPE*(-CC-CB+VBE*GMPGO+VBE*GPI-VBC*GO)
C
C  ZERO TRANSIENT CONDUCTANCES IN DC ANALYSIS
C
      IF (MODE.GT.1) GO TO 310
      TRACUR(ISPOT+2)=CC
      TRACUR(ISPOT+3)=CB
      GCCS=0.0
      CEQCS=0.0
      GO TO 500
C
C  CHARGE STORAGE ELEMENTS
C
  310 CCS=SYMVAL(MNAM,7)
      TF=SYMVAL(MNAM,8)
      TR=SYMVAL(MNAM,9)
      CZBE=SYMVAL(MNAM,10)*VALUE(I)
      CZBC=SYMVAL(MNAM,11)*VALUE(I)
      PE=SYMVAL(MNAM,21)
      XME=SYMVAL(MNAM,22)
      PC=SYMVAL(MNAM,23)
      XMC=SYMVAL(MNAM,24)
C
C  COMPUTE DEPLETION LAYER VOLTAGES
C
      ARGBE=PE-VBE
      IF (ARGBE.LT.1.D-1) ARGBE=1.D-1
      SARGBE=DEXP(XME*DLOG(ARGBE))
      ARGBC=PC-VBC
      IF (ARGBC.LT.1.D-1) ARGBC=1.D-1
      SARGBC=DEXP(XMC*DLOG(ARGBC))
C
C  SMALL SIGNAL PARAMETERS
C
      IF (MODE.GT.2) GO TO 350
      TRACUR(ISPOT+4)=GPI
      TRACUR(ISPOT+5)=TF*(CBE+CSAT)/VT+CZBE/SARGBE
      TRACUR(ISPOT+6)=GMU
      TRACUR(ISPOT+7)=TR*(CBC+CSAT)/VT+CZBC/SARGBC
      TRACUR(ISPOT+8)=GM
      TRACUR(ISPOT+9)=GO
      GO TO 900
C
C  TRANSIENT ANALYSIS
C
  350 CTBE=(2.0D0*TF*CBE-2.0D0*CZBE*ARGBE/(SARGBE*(1.0D0-XME)))/DELTA
      CTBC=(2.0D0*TR*CBC-2.0D0*CZBC*ARGBC/(SARGBC*(1.0D0-XMC)))/DELTA
C
C  INITIALIZE ON FIRST TRANSIENT PASS
C
      IF (IPASS1.EQ.0) GO TO 360
      CJBE(ISPOT)=CTBE
      CJBC(ISPOT)=CTBC
      VCCS(ISPOT)=VNIM1(LOC4)
      CCCS(ISPOT)=0.0
C
C  UPDATE ON FIRST ITERATION OF EACH TIMEPOINT
C
  360 IF (MODE.EQ.3) GO TO 370
      CJBE(ISPOT)=CTBE*(1.0D0+DELTA/DELOLD)-CJBE(ISPOT)
      CJBC(ISPOT)=CTBC*(1.0D0+DELTA/DELOLD)-CJBC(ISPOT)
      CCCS(ISPOT)=(2.0D0*CCS/DELOLD)*(VNIM1(LOC4)-VCCS(ISPOT))-
     1  CCCS(ISPOT)
      VCCS(ISPOT)=VNIM1(LOC4)
C
C  COMPUTE CAPACITOR EQUIVALENT CONDUCTANCES AND CURRENT SOURCES
C
  370 GTEMP=(2.0D0*TF*(CSAT+CBE)/VT+2.0D0*CZBE/SARGBE)/DELTA
      GPI=GPI+GTEMP
      CEQBE=CEQBE+TYPE*(GTEMP*VBE-CTBE+CJBE(ISPOT))
      GTEMP=(2.0D0*TR*(CSAT+CBC)/VT+2.0D0*CZBC/SARGBC)/DELTA
      GMU=GMU+GTEMP
      CEQBC=CEQBC+TYPE*(GTEMP*VBC-CTBC+CJBC(ISPOT))
      GCCS=2.0D0*CCS/DELTA
      CEQCS=GCCS*VCCS(ISPOT)+CCCS(ISPOT)
C
C  LOAD CURRENT EXCITATION VECTOR
C
  500 VN(LOC4)=VN(LOC4)+CEQCS-CEQBC
      VN(LOC5)=VN(LOC5)+CEQBC+CEQBE
      VN(LOC6)=VN(LOC6)-CEQBE
C
C  LOAD Y MATRIX, REAL TERMS
C
      YNL(LOC1)=YNL(LOC1)+GCPR
      YNL(LOC2)=YNL(LOC2)+GBPR
      YNL(LOC3)=YNL(LOC3)+GEPR
      YNL(LOC4)=YNL(LOC4)+GMU+GO+GCPR+GCCS
      YNL(LOC5)=YNL(LOC5)+GBPR+GPI+GMU
      YNL(LOC6)=YNL(LOC6)+GPI+GEPR+GMPGO
      YNL(LOC7)=YNL(LOC7)-GCPR
      YNL(LOC8)=YNL(LOC8)-GBPR
      YNL(LOC9)=YNL(LOC9)-GEPR
      YNL(LOC10)=YNL(LOC10)-GCPR
      YNL(LOC11)=YNL(LOC11)-GMU+GM
      YNL(LOC12)=YNL(LOC12)-GMPGO
      YNL(LOC13)=YNL(LOC13)-GBPR
      YNL(LOC14)=YNL(LOC14)-GMU
      YNL(LOC15)=YNL(LOC15)-GPI
      YNL(LOC16)=YNL(LOC16)-GEPR
      YNL(LOC17)=YNL(LOC17)-GO
      YNL(LOC18)=YNL(LOC18)-GPI-GM
  900 ISPOT=ISPOT+15
 1000 CONTINUE
      RETURN
      END
  