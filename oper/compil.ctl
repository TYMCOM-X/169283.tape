; Job %1(1) to make COMPIL.SHR from COMPIL.MAC
; Submit with command .SUBMIT COMPIL/RESTART:1/TIME:10:0/UNIQ:0

; Required files: [10,7]  (Latest released versions)
;	MACRO.SHR
;	LOADER.SHR
;	COMPIL.SHR
;	JOBDAT.REL
;[SELF] COMPIL.MAC

; Output files
;	COMPIL.SHR
;	COMPIL.LST
;	COMPIL.MAP
;	COMPIL.LOG

.PLEASE COMPIL SUBMISSION CONTROL FILE INITIALIZING

; Copy files from [10,7] and use private "SYS:"
.RUN DSK:PIP[10,7]
*DSK:(XB)_[10,7]MACRO.SHR,LOADER.SHR,COMPIL.SHR,JOBDAT.REL

; Make a record of what is being used
.SET WATCH VERSION
.IF (NOERROR) .GOTO A
.RUN DSK:DIRECT[10,7]
*TTY:/CHECKSUM=*.SHR
A::.ASSIGN DSK: SYS:

; Compile, Load, Save, and Testing Section
.LOAD /MAP:COMPIL /CREF /COMPILE COMPIL.MAC %A
.SSAV DSK:COMPIL

; Try it just to make sure it works
.LOAD COMPIL.MAC
.DEASSIGN


; Produce source listing
.RUN CREF[10,7]
*DSK:COMPIL.LST_COMPIL.CRF
;
; Now get checksummed directories of all files
.RU DIRECT[10,7]
*TTY:=/CHECKSUM COMPIL.MAC,COMPIL.SHR,COMPIL.CTL,CML???.RN?,COMPIL.HLP

; Submission tape creation routine
.OPERATOR %
.RUN TECO[10,7]
*!AGAIN!
*% PLEASE COMPIL.CTL - MAKE A SUBMISSION DECTAPE<YES/NO>?
*HK0UM
*!LOOP!UMQM-13"EOLOOP'QM-27"E10UM'QM-10"NQMIOLOOP'
*BJH-3"E:SYES"SOYES''
*H-2"E:SYE"SOYES':SNO"SONO''
*H-1"E:SY"SOYES':SN"SONO''OAGAIN
*!YES!
*YES, MAKE THE TAPE
*OEND!NO!
*? NO, FORGET MAKING THE TAPE
=!END!
.NOOPERATOR
.IF (ERROR) .GOTO OVER

MAKE::.MOUNT DTA:DEC/VID:'COMPIL SUBMISSION TAPE'/WE
.RU PIP[10,7]
*DEC:^^COMPIL^^_/Z
.IF (ERROR)	; OK, we can do without the ID
*DEC:(XB)_DSK:COMPIL.MAC,COMPIL.SHR,COMPIL.CTL,CML???.RN?
.IF (NOERROR) .GOTO CONT
.PLEASE COMPIL - TROUBLE MAKING SUBMISSION DTA<TRY AGAIN?>
.DEASSIGN
.BACKTO MAKE

CONT::.DIR DEC:
.PLEASE COMPIL - OPERATOR GET A DIRECTORY OF COMPIL SUBMISSION TAPE & DISMOUNT
.REWIND DEC:
.IF (ERROR)
.DEASSIGN

OVER::.RUN PIP[10,7]
*COMPIL.RUN_CML???.RND
.RUN RUNOFF[10,7]
*COMPIL.RUN
*COMPIL.DOC
.DELETE COMPIL.RUN
.IF (ERROR)
.PLEASE COMPIL SUCCESSFUL

; Remove all temporary files

%ERR::
%CERR::
%FIN::.DEASSIGN
.DELETE MACRO.SHR,LOADER.SHR,COMPIL.SHR,JOBDAT.REL
.IF (ERROR) 	; Don't care if failed
.PLEASE COMPIL DONE
.PRINT /AFTER:+3 COMPIL.LST,COMPIL.HLP/COP:3,COMPIL.DOC/COP:3,COMPIL.LOG
.IF (ERROR) .GOTO END
.K/F
END:
