140 DIM N$(6),I(20),C(50,16),V(16),W(50,4),L(15),Y(9)
160 PRINT "DO YOU WANT RATE OF RETURN ONLY,TYPE Y OR N..."
165 MARGIN 132
170 INPUT N$(6)
180 FOR I=1 TO 4
190 READ N$(I)
192 NEXT I
195 READ V(0),V(16)
198 FOR I = 1 TO 15
200 READ V(I)
205 NEXT I
210 IF V(1)<0 THEN 220
215 IF V(1)<51 THEN 240
220 PRINT V(1);"IS NUM OF YEARS AND IS WRONG"
230 STOP
240  I(1)=V(2)*V(3)*43560
250  I(3)=1-(V(11)*.01)
260 IF V(0)=0 THEN 270
262 V(15)=I(1)
270  I(2)=((I(1)-V(15))*I(3))+V(15)
280 FOR I=1 TO 8
285 PRINT
288 NEXT I
290 PRINT TAB(14);"FINANCIAL ANALYSES PROGRAM"
292 PRINT
294 PRINT
300 PRINT "PROJECT: ";N$(1);TAB(40);" RUN: ";N$(2)
310 PRINT "DATA BY: ";N$(4);TAB(40);"DATE: ";N$(3)
320 PRINT
330 IF N$(6)="Y" THEN 430
340 PRINT "PROJECT LIFE: ";V(1);" YEARS"
350 PRINT "INCOME TAX: ";V(11);" PCT - TAX ON RESIDUAL: ";      V(12);" PCT"
360 PRINT
370 PRINT "PROJECT HAS ";V(2);" ACRES VALUED AT $";V(3);" PER SQ FT"
372 PRINT USING " LAND IS WORTH $ ###,###,### BEFORE TAXES ",I(1)
374 PRINT
380 PRINT USING  "GIVEN A BASE OF $###,###,### THE LAND INVESTMENT IS $###,###,###",V(15),I(2)
390 IF V(0)=1 THEN 420
400 PRINT "  THE LAND INVESTMENT DOES NOT REQUIRE CASH"
410 GO TO 430
420 PRINT "  THE LAND INVESTMENT REQUIRES CASH"
430 FOR N=1 TO V(1)
440  W(N,1)=V(5)
450 IF V(4)<>1 THEN 480
460 READ X
470  W(N,1)=W(N,1)+X
480 NEXT N
490 FOR N=1 TO V(1)
500  W(N,2)=V(7)*.01
510 IF V(6)<>1 THEN 540
520 READ X
530  W(N,2)=W(N,2)+(X*.01)
540 NEXT N
550 FOR N=1 TO V(1)
560 W(N,3)=V(9)*.01
565 W(N,4)=V(10)
570 IF V(8)=1 THEN 620
580 IF V(8)<>2 THEN 640
590 READ X
600  W(N,4)=W(N,4)+X
610 GO TO 640
620 READ X
630  W(N,3)=W(N,3)+(X*.01)
640  X=1-(W(N,2)+W(N,3))
650  C(N,1)=(W(N,1)*X)-W(N,4)
660 C(N,14)=W(N,1)
670 C(N,15)=C(N,14)-C(N,1)
680 NEXT N
690 IF N$(6)="Y" THEN 740
700 PRINT
702 PRINT "               OPERATING INCOME AND EXPENSES"
704 PRINT " YEAR    GROSS      VAC PCT      OP PCT      OP ACT"
710 FOR I=1 TO V(1)
711 W(I,2)=W(I,2)*100 
722 W(I,3)=W(I,3)*100 
725 NEXT I
730 GO SUB 3250
740 READ N$(5)
745 Y(8)=0
750 IF N$(5)="LAST" THEN 1830
760 GO SUB 1220
768 FOR I =1 TO 14
770 READ L(I)
775 NEXT I
780 IF L(2)=0 THEN 1270
782 IF L(1)<0 THEN 1240
784 IF L(1)>V(1) THEN 1240
790  X=L(3)*.01*L(2)
800  Y(1)=L(4)*.01*L(2)
810  Z1=100-(L(3)+L(4))
820  Z=Z1*.01*L(2)
830 IF Y(1)<=0 THEN 1050
832 IF L(5)>1 THEN 850
834 PRINT "DEPRECIATION LIFE CANNOT BE ";L(5);"IN ";N$(5)
840 GO TO 1250
850 IF L(6)<>1 THEN 860
851 FOR N=1 TO L(5)
852 Y(8)=Y(8)+N
853 NEXT N
854 GO TO 870
860  Y(2)=((100+L(6))*.01)*(1/L(5))
870  Y(3)=0
875 Y(4)=0
880  Y(9)=L(14)
890  Y(7)=Y(1)
900  Y(9)=Y(9)+1
910  Y(3)=Y(3)+1
915 IF L(6)<>1 THEN 920
917 Y(2)=(L(5)-(Y(3)-1))/Y(8)
920 IF Y(9)>V(1) THEN 1050
922 IF Y(3)=L(5) THEN 1030
924 IF Y(4)=1 THEN 1010
930 IF L(6)<>1 THEN 936
932 Y(5)=Y(1)*Y(2)
934 GO TO 940
936 Y(5)=Y(7)*Y(2)
940 IF L(6)<>1 THEN 946
942 Y(6)=Y(1)/(L(5)-(Y(3)-1))
944 GO TO 950
946 Y(6)=Y(7)/(L(5)-(Y(3)-1))
950 IF Y(5)>Y(6) THEN 960
952 IF L(6)<>1 THEN 1000
960  W(Y(9),2)=Y(5)
970  W(Y(9),1)=Y(7)-W(Y(9),2)
980  Y(7)=Y(7)-W(Y(9),2)
990 GO TO 900
1000  Y(4)=1
1010  W(Y(9),2)=Y(6)
1020 GO TO 970
1030  W(Y(9),2)=Y(7)
1040  W(Y(9),1)=0
1050 PRINT
1060 IF N$(6)="Y" THEN 1160
1070 PRINT USING " YEAR ### SPEND $###,###,### FOR 'LLLLLLLLLL",L(1),L(2),N$(5)
1080 IF L(3)<=0 THEN 1100
1090 PRINT USING "     ####.##  PCT OR $###,###,###  IS EXPENSED ",L(3),X
1095 PRINT
1100 IF Z1<=0 THEN 1120
1110 PRINT USING "    #,###.##  PCT OR $ ###,###,###  IS CAPITALIZED ",Z1,Z
1115 PRINT
1120 IF L(4)<=0 THEN 1160
1130 PRINT USING "####.##  PCT OR $###,###,### IS DEPRECIATED OVER ### YEARS ",L(4),Y(1),L(5)
1132 IF L(6)<>1 THEN 1140
1133 PRINT USING "       USING SUM-OF-YEARS-DIGITS, STARTING IN YEAR ###",L(14) 
1136 GO TO 1160
1140 PRINT USING"        AT A DEP RATE OF  #,###.## PCT, STARTING IN YEAR ###",L(6)+100,L(14)
1142 PRINT
1144 PRINT
1160 FOR I=0 TO V(1)
1165 C(I,6)=C(I,6)+W(I,2) 
1168 C(I,5)=C(I,5)+W(I,1) 
1169 NEXT I
1170 IF Y(1)<=0 THEN 1185
1171 IF V(16)=0 THEN 1175
1172 IF L(10)=0 THEN 1175
1173 GO SUB 3450
1174 GO TO 1185
1175 IF L(5)+L(14)>=V(1) THEN 1178
1176 FOR I=L(14)+1 TO L(5)+L(14)
1177 C(I,16)=C(I,16)+Y(1)/L(5) 
1178 NEXT I
1179 GO TO 1185
1180 FOR I=L(14)+1 TO V(1) 
1182 C(I,16)=C(I,16)+Y(1)/L(5)
1183 NEXT I
1185  C(L(1),2)=C(L(1),2)+Z
1190  C(L(1),3)=C(L(1),3)+X
1200  C(L(1),4)=C(L(1),4)+Y(1)
1210 GO TO 1270
1220 FOR I=1 TO 4
1222 FOR N=0 TO 50
1224 W(N,I)=0
1226 NEXT N
1228 NEXT I
1230 RETURN
1240 PRINT "THE";N$(5);"CAN NOT START IN YEAR";L(1)
1250 PRINT "THE ITEM IS IGNORED"
1260 GO TO 740
1270  X=L(8)+(L(7)*L(2)*.01)
1280 IF X<=0 THEN 740
1282 IF L(13)>=0 THEN 1310
1290 PRINT "DATA IN";N$(5);"IS WRONG"
1300 PRINT "AMOUNT-X";X;"LIFE-L9";L(9);"YEAR L-13";L(13)
1310  R=(L(10)*.01)/12
1320 IF R>0 THEN 1350
1330  P=X/L(9)
1340 GO TO 1370
1350  N=L(9)*12
1360  P=X*(R/(1-(1/(1+R)^N)))
1370 GO SUB 1220
1380  Y(2)=L(11)
1390  Y(4)=L(13)
1400  Y(1)=X
1410  Y(3)=0
1420  Y(4)=Y(4)+1
1430 IF Y(4)>V(1) THEN 1640 
1435 IF Y(2)<=0 THEN 1480
1440  W(Y(4),2)=12*Y(1)*R
1450  W(Y(4),3)=Y(1)
1460  Y(2)=Y(2)-1
1470 IF Y(4)=L(12) THEN 1620 
1475 GO TO 1420
1480 FOR J=1 TO 12
1490  Q=R*Y(1)
1500  W(Y(4),2)=W(Y(4),2)+Q
1510  Q1=P-Q
1520  W(Y(4),1)=W(Y(4),1)+Q1
1530  Y(1)=Y(1)-Q1
1540 NEXT J
1550  W(Y(4),3)=Y(1)
1560 IF Y(4)=L(12) THEN 1620
1570  Y(3)=Y(3)+1
1580 IF Y(3)<L(9) THEN 1420
1590  W(Y(4),1)=W(Y(4),1)+W(Y(4),3)
1600  W(Y(4),3)=0
1610 GO TO 1640
1620  W(Y(4),1)=W(Y(4),1)+Y(1)
1630  W(Y(4),3)=0
1640 FOR I=0 TO V(1)
1650  C(I,9)=C(I,9)+W(I,3)
1660  C(I,10)=C(I,10)+W(I,2)
1670  C(I,11)=C(I,11)+W(I,1)
1680 NEXT I
1690  C(L(13),8)=C(L(13),8)+X
1700 PRINT
1710 IF N$(6)="Y" THEN 740
1720 PRINT USING " YEAR ##  BORROW $###,###,### AT ##.## PCT FOR  'LLLLLLLLLL",L(13),X,L(10),N$(5)
1730 IF L(11)<=0 THEN 1750
1740 PRINT "   THERE ARE ";L(11);" INTEREST ONLY PAYMENTS"
1750 PRINT "   THERE ARE ";L(9);" PRINCIPLE PAYMENTS WHICH"
1760 PRINT USING "        GENERATES A CONSTANT ANNUAL PAYMENT OF $###,###,### ",P*12
1762 PRINT
1770 IF L(12)<=0 THEN 1790
1780 PRINT "   THERE IS A BALOON PAYMENT IN YEAR ";L(12)
1790 IF L(7)<=0 THEN 740
1800 PRINT "   LOAN IS ";L(7);" PCT OF ABOVE IMPROVEMENT PLUS $";       L(8)
1810 GO TO 740
1812 REM ********** NEXT SECTION IS FINEOPLINK PROGRAM ***********
1814 REM *********************************************************
1816 REM *********************************************************
1820 REM THIS IS START OF CASH FLOWS
1830 REM ADD IN LAND VALUE I(2) TO CASH FLOW TABLE
1840 C(0,7)=-I(2)
1845 C(0,12)=-I(2)
1850 FOR N=0 TO V(1)
1860  X=C(N,1)-C(N,3)-C(N,6)
1870  X=X*I(3)
1880  C(N,7)=X+C(N,6)-C(N,2)-C(N,4)+C(N,7)
1890  X=C(N,1)-C(N,3)-C(N,6)-C(N,10)
1900  X=X*I(3)
1910  C(N,12)=X-C(N,2)-C(N,4)+C(N,6)+C(N,8)-C(N,11)+C(N,12)
1920 IF N>0 THEN 1950
1930  C(N,13)=C(N,12)
1940 GO TO 1960
1950  C(N,13)=C((N-1),13)+C(N,12)
1960 NEXT N
1970 IF V(13)>0 THEN 1990
1980 PRINT USING "****V13-DISCOUNT RATE IS WRONG",V(13)
1990  X=1/((1+(V(13)*.01))^V(1))
2000  Y=C(V(1),5)
2010 FOR N=0 TO V(1)
2012 Y=Y+C(N,2)
2014 NEXT N
2020  Z=1-(V(12)*.01)
2030  I(6)=Y
2040  I(15)=((I(1)-V(15)+V(14)-I(6))*Z)+I(6)+V(15)
2050 PRINT
2060 IF N$(6)="Y" THEN 2210
2070 PRINT ">>>>>AT END OF YEAR ";V(1);" THERE ARE THE FOLLOWING RESIDUALS:"
2080 :'LLLLLLLLLLLLL ###,###,###    ###,###,###
2090 PRINT "              UNDISCOUNTED     DISCOUNTED"
2100 R$="LAND        "
2110 PRINT USING 2080,R$,I(1),I(1)*X
2120 R$="BOOK VALUE  "
2130 PRINT USING 2080,R$,I(6),I(6)*X
2140 R$="SPECIAL     "
2150 PRINT USING 2080,R$,V(14),V(14)*X
2160 R$="LOAN        "
2170 PRINT USING 2080,R$,-C(V(1),9),X*(-C(V(1),9))
2180 R$="TOTAL       "
2190 PRINT USING 2080,R$,I(15)-C(V(1),9),X*(I(15)-C(V(1),9))
2200 PRINT "(NOTE: TOTAL IS ONLY FIGURE ABOVE THAT IS AFTER TAX)"
2210  Z=(1+(V(13)*.01))
2220  I(4)=C(0,7)
2230  I(5)=C(0,12)
2235 FOR N=1 TO V(1)
2240 I(4)=I(4)+(C(N,7)/Z^N)
2250 I(5)=I(5)+(C(N,12)/Z^N)
2255 NEXT N
2260  I(9)=I(4)+(I(15)*X)
2270  I(10)=I(5)+((I(15)-C(V(1),9))*X)
2280 PRINT
2290 IF N$(6)="Y" THEN 2390
2300 PRINT "           NET PRESENT WORTH OF INVESTMENT"
2310 PRINT "            DISCOUNTED AT";V(13);"PCT"
2320 PRINT
2330 PRINT "              NO RESIDUALS   WITH RESIDUALS"
2340 PRINT
2350 R$="NO FINANCING "
2360 PRINT USING 2080,R$, I(4),I(9)
2370 R$="FINANCING    "
2380 PRINT USING 2080,R$,I(5),I(10)
2390  Y=I(15)
2400 FOR N=0 TO V(1)
2402 Y=Y+C(N,7)
2404 NEXT N
2410 IF Y>0 THEN 2440
2420  I(13)=1
2430 GO TO 2490
2440 GO SUB 1220
2450 FOR N=0 TO V(1)
2452 W(N,1)=C(N,7)
2454 NEXT N
2460  W(1,2)=I(15)
2470 GO SUB 2800
2480  I(11)=B*100
2490 IF C(0,12)<0 THEN 2520
2500  I(14)=2
2510 GO TO 2600
2520 IF (C(V(1),13)+I(15)-C(V(1),9))>0 THEN 2550
2530  I(14)=1
2540 GO TO 2600
2550 GO SUB 1220
2560 FOR N=0 TO V(1)
2562 W(N,1)=C(N,12)
2564 NEXT N
2570  W(1,2)=I(15)-C(V(1),9)
2580 GO SUB 2800
2590  I(12)=B*100
2600 PRINT
2610 PRINT "  :  :  :  :  :  :  :  :  :  :  :  :  :  :  :  :  :  :  :"
2620 PRINT "  :  :"
2630 IF I(13)=0 THEN 2670
2640 PRINT "BEFORE FINANCING THE PROJECT LOSES MONEY AND HENCE"
2650 PRINT "THERE IS NO RATE OF RETURN"
2660 GO TO 2690
2670 PRINT "  :  :       BEFORE FINANCING THE PROJECT HAS A"
2680 PRINT "  :  :           ";I(11);"RATE OF RETURN"
2690 PRINT "  :  :"
2700 IF I(14)=2 THEN 2710 
2702 IF I(14)=1 THEN 2740
2704 GO TO 2770
2710 PRINT "SINCE THERE IS NO INITIAL CASH INVESTMENT AFTER FINANCING,"
2720 PRINT "THERE IS NO RATE OF RETURN AFTER FINANCING"
2730 GO TO 2960
2740 PRINT "SINCE THE PROJECT DOES NOT HAVE A POSITIVE CASH FLOW"
2750 PRINT "AFTER FINANCING, THERE IS NO RATE OF RETURN AFTER FINANCING"
2760 GO TO 2960
2770 PRINT "  :  :       AFTER FINANCING THE PROJECT HAS A"
2780 PRINT "  :  :           ";I(12);"RATE OF RETURN"
2790 GO TO 2960
2800 B=0
2810  A=.01
2820  C=W(0,1)
2830 FOR N=1 TO V(1)
2840  C=C+(W(N,1)/((1+B)^N))
2850 NEXT N
2860  C=C+(W(1,2)/((1+B)^V(1)))
2870 IF C<=0 THEN 2910
2880  B=B+A
2890 IF B>=1.0 THEN 2950
2900 GO TO 2820
2910 IF A<=.0001 THEN 2950
2920  B=B-A
2930  A=A*.1
2940 GO TO 2820
2950 RETURN
2960 PRINT "  :  :"
2970 PRINT "  :  :  :  :  :  :  :  :  :  :  :  :  :  :  :  :  :  :  :"
2980 PRINT
2990 PRINT
3000 PRINT
3010 PRINT"DO YOU WANT TO CONTINUE WITH CASH FLOWS? TYPE 0 FOR STOP, OR 1 FOR CONTINUE."
3012 INPUT S1
3014 IF S1=0 THEN 90610
3015 GO SUB 1220
3030 IF V(0)=1 THEN 3080
3040  C(0,7)=C(0,7)+I(2)
3050  C(0,12)=C(0,12)+I(2)
3070 FOR N=0 TO V(1)
3072 C(N,13)=C(N,13)+I(2) 
3074 NEXT N
3080  W(0,1)=C(0,7)
3090 FOR N=1 TO V(1)
3092 W(N,1)=W(N-1,1)+C(N,7) 
3094 NEXT N
3100 PRINT
3110 PRINT "                        ANNUAL CASH FLOW"
3120 PRINT
3130 PRINT USING "START WITH AN AFTER FINANCING CASH FLOW OF $###,###,###",C(0,13)
3132 PRINT USING "   AND END WITH CUM CASH FLOW OF $###,###,###",C(V(1),13)
3140 PRINT USING "START WITH A BEFORE FINANCING CASH FLOW OF $###,###,###",W(0,1)
3142 PRINT USING "   AND END WITH CUM CASH FLOW OF $###,###,###",W(V(1),1)
3150 PRINT
3160 PRINT "   YEAR             WITH FINANCING               ";
3170 PRINT "      NO FINANCING"
3180 PRINT "                  ANNUAL    CUMMULATIVE          ";
3190 PRINT "   ANNUAL    CUMMULATIVE"
3200 PRINT
3205 FOR N=1 TO V(1)
3210 PRINT USING "  ###        ###,###,###    ###,###,###        ###,###,###    ###,###,###",N,C(N,12),C(N,13),C(N,7),W(N,1)
3220 NEXT N
3222 PRINT
3225 PRINT USING "TOTAL WITH RESIDUALS        ###,###,###                       ###,###,###",C(V(1),13)+I(15)-C(V(1),9),W(V(1),1)+I(15)
3230 PRINT TAB(10);"-------------------------------------------------"
3235 GO TO 5000
3242 REM ***************THIS IS THE END OF FINEOPLINK PROGRAM
3244 REM ****************************************************
3246 REM ******************************************************
3250 N=1
3260 :### ##,###,###   ###,###.##  ###,###.## ###,###,###
3270 GO SUB 3420
3280 FOR N=2 TO V(1)-1
3290 FOR J=1 TO 4
3300 IF W(N-1,J)<>W(N,J) THEN 3370
3310 IF W(N,J)<>W(N+1,J) THEN 3370
3320 NEXT J
3330 IF S1=1 THEN 3380
3340 PRINT "....."
3350  S1=1
3360 GO TO 3380
3370 GO SUB 3420
3380 NEXT N
3390 N=V(1)
3400 GO SUB 3420
3410 RETURN
3420 PRINT USING 3260,N,W(N,1),W(N,2),W(N,3),W(N,4)
3430  S1=0
3440 RETURN
3450 P=Y(1)*(L(10)*.01/(1-(1/(1+(L(10)*.01))^L(5))))
3455 Y(2)=Y(1)
3460 IF L(14)+L(5)<V(1) THEN 3466
3462 FOR N=L(14)+1 TO V(1)
3464 GO TO 3470
3465 NEXT N
3466 FOR N=L(14)+1 TO L(14)+L(5)
3470 Q=L(10)*.01*Y(2)
3480 Q1=P-Q
3490 Y(2)=Y(2)-Q1
3500 C(N,16)=C(N,16)+Q1
3510 NEXT N
3520 RETURN
3530 REM *******************************************
3540 REM *********THIS IS F1CASH PROGRAM********
3550 REM ******************************************
5000 FOR I=0 TO 9
5010 T(I)=0
5020 NEXT I
5030 I=0
5040 :###   ###,###,### ###,###,###,  ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,###
5050 :      ###,###,### ###,###,###,  ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,###
5060 GO SUB 6220
5070 PRINT TAB(48);"STATEMENT OF PROJECTED NET INCOME"
5080 PRINT TAB(48);"---------------------------------"
5090 FOR N=0 TO V(1)
5100 FOR J=0 TO 16
5110 C(N,J)=INT(C(N,J)+.5)
5120 NEXT J
5130 NEXT N
5140 IF V(16)=0 THEN 5180
5150 PRINT TAB(46);"SINKING";SPACE$(33);"INCOME     FEDERAL AND"
5160 PRINT TAB(9);"GROSS      OPERATING    OPERATING     FUND      CONTRIBUTION   INTEREST      BEFORE     STATE TAXES      NET"
5170 GO TO 5200
5180 PRINT TAB(45);"STRAIGHT-";SPACE$(32);"INCOME     FEDERAL AND"
5190 PRINT TAB(9);"GROSS      OPERATING    OPERATING     LINE      CONTRIBUTION   INTEREST      BEFORE     STATE TAXES      NET"
5200 PRINT "YEAR    REVENUE     EXPENSES      INCOME    DEPRECIATION  TO EARNINGS   EXPENSE        TAXES      ON INCOME     INCOME"
5210 PRINT "---- ------------ ------------ ------------ ------------ ------------ ------------ ------------ ------------ ------------"
5220 FOR N=1 TO V(1)
5230 T(1)=T(1)+C(N,14)
5240 T(2)=T(2)+C(N,15)
5250 W(N,1)=C(N,14)-C(N,15)
5260 T(3)=T(3)+W(N,1)
5270 T(4)=T(4)+C(N,16)
5280 W(N,2)=W(N,1)-C(N,16)
5290 T(5)=T(5)+W(N,2)
5300 T(6)=T(6)+C(N,10)
5310 W(N,3)=W(N,2)-C(N,10)
5320 T(7)=T(7)+W(N,3)
5330 W(N,4)=INT(W(N,3)*V(11)*.01+.5)
5340 T(8)=T(8)+W(N,4)
5350 T(9)=T(9)+W(N,3)-W(N,4)
5360 PRINT USING 5040,N,C(N,14),C(N,15),W(N,1),C(N,16),W(N,2),C(N,10),W(N,3),W(N,4),W(N,3)-W(N,4)
5370 NEXT N
5375 PRINT "---- ------------ ------------ ------------ ------------ ------------ ------------ ------------ ------------ ------------"
5380 PRINT USING 5050,T(1),T(2),T(3),T(4),T(5),T(6),T(7),T(8),T(9)
5385 PRINT "         "
5390 GO SUB 6220
5400 PRINT TAB(48);"STATEMENT OF PROJECTED CASH FLOWS"
5410 PRINT TAB(48);"---------------------------------"
5420 PRINT
5430 PRINT
5440 PRINT TAB(43);"FEDERAL AND   CASH FROM  ------------ DEBT SERVICE -----------      NET     DISCOUNTED"
5450 PRINT "     OPERATING   ACCELERATED    TAXABLE    STATE TAXES  OPERATIONS";SPACE$(32);"TAX         CASH      CASH AT"
5460 PRINT USING " YR    INCOME    DEPRECIATION    INCOME      ON INCOME  (COL 1-COL 4)  INTEREST     PRINCIPAL    CREDIT        FLOW     ###.##%",I(12)
5470 PRINT " -- ----------- - ----------- ------------ ------------ ------------ ------------ ------------ ----------- ------------ -----------"
5480 :### ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,### ###,###,###  ###,###,### ###,###,###
5485 :    ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,###  ###,###,### ###,###,###  ###,###,### ###,###,###
5490 T(1)=0
5500 T(2)=0
5510 T(4)=0
5520 T(5)=0
5530 T(7)=0
5540 T(8)=0
5550 T(9)=0
5560 FOR N=1 TO V(1)
5570 T(1)=T(1)+C(N,6)
5580 W(N,2)=C(N,14)-C(N,15)-C(N,6)
5590 T(2)=T(2)+W(N,2)
5600 W(N,3)=INT(W(N,2)*V(11)*.01+.5)
5610 T(4)=T(4)+W(N,3)
5620 T(5)=T(5)+C(N,14)-C(N,15)-W(N,3)
5630 T(7)=T(7)+C(N,11)
5640 W(N,1)=INT(C(N,10)*V(11)*.01+.5)
5650 T(8)=T(8)+W(N,1)
5660 W(N,4)=C(N,14)-C(N,15)-W(N,3)-C(N,10)-C(N,11)+W(N,1)
5670 T(9)=T(9)+W(N,4)
5680 X=(1+(I(12)*.01))^N
5690 Y=INT(W(N,4)/X+.5)
5700 T(0)=T(0)+Y
5710 PRINT USING 5480,N,C(N,14)-C(N,15),C(N,6),W(N,2),W(N,3),C(N,14)-C(N,15)-W(N,3),C(N,10),C(N,11),W(N,1),W(N,4),Y
5720 IF C(N,2)+C(N,3)+C(N,4)>1 THEN 5750
5730 IF C(N,8)<>0 THEN 5750
5740 GO TO 5800
5750 Y=-C(N,2)-C(N,4)+C(N,8)-(C(N,3)*I(3))
5760 Z=INT(Y/X+.5)
5770 PRINT USING " (CAPITAL ITEM)                                                                                            ###,###,### ###,###,###",Y,Z
5780 T(9)=T(9)+Y
5790 T(0)=T(0)+Z
5800 NEXT N
5810 PRINT "   ------------ ------------ ------------ ------------ ------------ ------------ ------------ ----------- ------------ ------------"
5820 PRINT USING 5485,T(3),T(1),T(2),T(4),T(5),T(6),T(7),T(8),T(9),T(0)
5830 PRINT "   ============ ============ ============ ============ ============ ============ ============= ==========="
5840 PRINT
5850 PRINT USING "PROJECT RESIDUAL                                                                                           ###,###,### ###,###,###",I(15)-C(V(1),9),(I(15)-C(V(1),9))/X
5860 PRINT "PROJECT EQUITY - BASED ON THE DISCOUNTED CASH FLOW METHOD OF ANALYSIS,"
5870 PRINT USING "                 THE RATE OF RETURN ON EQUITY IS ###.##%.                                                  ###,###,### ###,###,###",I(12),T(9)+I(15)-C(V(1),9),T(0)+(I(15)-C(V(1),9))/X
5880 PRINT TAB(106);"============ ==========="
5890 PAGE 1
5895 NOPAGE
5898 PRINT CHR$(0)
5900 PRINT"IF YOU WANT TO CHANGE THE DISCOUNT RATE FOR A NEW DISCOUNTED"
5910 PRINT"CASH FLOW, PLEASE TYPE THE NEW DISCOUNT RATE OR A ZERO FOR NO."
5920 INPUT I(12)
5930 IF I(12)=0 THEN 90610
5940 PRINT
5950 PRINT " DISCOUNTED"
5960 PRINT "  CASH AT"
5970 PRINT USING "   ##.##%",I(12)
5980 PRINT "------------"
5990 T(0)=0
6000 FOR N=1 TO V(1)
6010 X=(1+(I(12)*.01))^N
6020 Y=INT(W(N,4)/X+.5)
6030 PRINT USING "###,###,###",Y
6040 T(0)=T(0)+Y
6050 IF C(N,2)+C(N,3)+C(N,4)>1 THEN 6080
6060 IF C(N,8)<>0 THEN 6080
6070 GO TO 6120
6080 Y=-C(N,2)-C(N,4)+C(N,8)-(C(N,3)*I(3))
6090 Y=IX+.5)
6100 T(0)=T(0)+Y
6110 PRINT USING "###,###,###",Y
6120 NEXT N
6130 PRINT "-----------"
6140 PRINT USING "###,###,###",T(0)
6150 PRINT
6160 PRINT
6170 PRINT USING "###,###,###",(I(15)-C(V(1),9))/X
6180 PRINT "-----------"
6190 PRINT USING "###,###,###",T(0)+(I(15)-C(V(1),9))/X
6200 PRINT "============"
6210 GO TO 5900
6220 PAGE 1
6225 NOPAGE
6228 PRINT CHR$(0)
6230 PRINT TAB(55);"THE IRVINE COMPANY"
6240 PRINT TAB(55);"FINANCIAL ANALYSIS"
6250 PRINT TAB(64);"OF"
6260 PRINT TAB(48);N$(1)
6270 RETURN
90000 DATA HARBOR VIEW II,I,MARCH 26,HOFFMAN,0,0
90010 DATA 50,20.667,1.303,1,1458900,0,0,2,0,492500,50,31,8,0,440000
90020 DATA -471300,-357200,-357200,-357200,-357200,-249300,-249300,-249300,-249300,-249300,-130600,-130600,-130600
90021 DATA -130600,-130600,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
90040 DATA -125100,-122500,-122500,-122500,-122500,-85500,-85500,-85500,-85500,-85500,-44800,-44800,-44800,-44800,-44800
90041 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
90100 DATA "32",0,1695400,0,100,40,1,0,5800000,30,8.0,0,0,0,0
90200 DATA "50",0,2649000,0,100,25,1,0,0,0,0,0,0,0,0
90300 DATA "18",0,953600,0,100,15,1,0,0,0,0,0,0,0,0
90400 DATA CHATTELS,0,319000,0,100,8,1,0,0,0,0,0,0,0,0
90410 DATA CHATTELS,8,319000,0,100,8,1,0,0,0,0,0,0,0,8
90420 DATA CHATTELS,16,319000,0,100,8,1,0,0,0,0,0,0,0,16
90430 DATA CHATTELS,24,319000,0,100,8,1,0,0,0,0,0,0,0,24
90440 DATA CHATTELS,32,319000,0,100,8,1,0,0,0,0,0,0,0,32
90450 DATA CHATTELS,40,319000,0,100,8,1,0,0,0,0,0,0,0,40
90500 DATA INT FIN,0,214000,100,0,0,0,0,0,0,0,0,0,0,0
90600 DATA LAST
90610 END
L  