TITLE	OBATCH V26
; COPYRIGHT 1969,1970,1971, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.
SUBTTL	5-OCT-71

VERNUM=26
SUBVER=0
EDITOR=0
VPATCH=0
VLEFT=SUBVER+VERNUM*100+EDITOR*100000

LOC 137
VLEFT,,VPATCH
RELOC



;ACCUMULATORS
F=0
F2=1
TACM=2
TAC=TACM+1
TAC1=TAC+1
TAC2=TAC1+1
T=6
A=7
B=A+1
C=B+1
D=C+1
SU=D+1
LD=SU+1
E=16
P=17


;CHANNEL ASSIGNMENTS
TMP=1
OUD=14
PTY=16
TTY=17
;FLAGS (LH OF F)
COB=1			;$CBL CARD
ERR=2			;ERROR ENCOUNTERED
FOR=4			;$F4 CARD
BINUN=10		;TAPE NAME IS THE BINARY UNIT
RUNCOM=20		;RUN COMMAND HAS BEEN ISSUED OVER PTY
XCUTE=40		;JOB IN EXECUTION
JBCRDI=100		;JOB CARD READ
LDRIN=200		;LOADER IS IN CONTROL
POSLDR=400		;POSITION TAPE FOR LOADER
ENDJOB=1000		;EOJ CARD READ
NOMAP=2000		;NOMAP ON JOB CARD
NEEDLD=4000		;NEED LOADER ON $EOF,$EOJ CARD
XRUN=10000		;DOING A RUN FOR $RUN CARD
ENDUMP=20000		;POST MORTEM DUMP ON UNUSUAL END
TMPBIT=40000		;TEMPORARY BIT
NXTCDI=100000		;NEXT INPUT CARD ALREADY IN
PTYDAT=200000		;WANT JOB NO. FROM PTYIN
GOGOGO=400000		;JOB HAS BEEN SAVED, CAN BE STARTED
;RH OF F
OUDIR=4
INITOU=20000


;LH OF F2
NOPRNT=1
NOTMON=2
QSTOK=4		;OUTPUT QUESTION MARK OK
CRF=10			;RUN CREF AFTER MACRO
CMPLST=20		;COMP OR ASS'Y LISTING
TIMGIV=40
BINUSE=100
TIMKIL=200
JOB2=400
NOGO2=1000
KJOB=2000
OFF=4000
DSKCPY=10000
NOLSTD=20000
SCRASK=40000
OBJOUT=400000


;RH OF F2
SWPBIT=200000		;MONITOR IS A SWAPPING ONE
LOGBIT=100000		;LOGIN IS REQUIRED
NODRVR=2000		;NO VERSION OF DRIVER ON SYSTEM DEVICE
NOTTY=4000		;TTY WAS RELEASED

;CONDITIONAL ASSEMBLY PARAMETERS

IFNDEF	NTS,<NTS=0>	;DEFAULT CONDITION--TIMESHARING SYSTEM
			;     SET TO NON 0 FOR NON-TIMESHARE
IFNDEF	SER4,<SER4=0>	;DEFAULT CONDITION--SERIES 4 MONITOR
			;     -1 FOR NON SERIES 4 MONITOR
IFNDEF HDR,<HDR=0>	;NO  HEADER OR TRAILERS
	IFN	HDR,<
	IFNDEF	CONAME,<
;DEFAULT COMPANY NAME
	DEFINE CONAME,<
	SIXBIT	/DIGITAL EQUIPMENT CORP./
	>
>>
IFNDEF	DISK,<DISK=0>	;DEFAULT CONDITION - NO DISK
			;IF THERE IS A DISK, ASSEMBLE WITH DISK=-1
	IFNDEF	LOGSWT,<LOGSWT=0>  ;DEFALT CONDITION - MESSAGE OF
				;THE DAY NOT OUTPUT TO JOB
	IFNDEF	PROT,<PROT=0>	;DEFAULT CONDITION - IJOBS WILL
				;BE DELETED UPON END OF JOB
	IFNDEF	SITSW,<SITSW=0>  ;DEFAULT CONDITION - DO NOT ASSEMBLE
				;1. DO NOT ALLOW USER TO PRINT ON LPT
				;IF USER OJOBS GO TO DISK OR TAPE
				;2. DO NOT ASSIGN LPT
	IFNDEF	DEBUG,<DEBUG=0>	;DEFAULT -OJOB OUTPUT TO
				;SELECTED OUTPUT DEVICE.
				;IF SET -1,OJOB TEXT OUTPUT
				;TO OPERATOR TTY.
	IFNDEF	CORMAX,<CORMAX=^D64>  ;STR 10-3740
				;THIS CONDITIONAL PROVIDES A
				;MEANS FOR THE 10-40 INSTALLATION
				;TO USE AN UPPER CORE LIMIT
				;AND TO ABORT JOBS WHICH REQUEST
				;MORE THAN THE INSTALLATION
				;SET MAXIMUM

INTERN DCNT
;CALLI ARGUMENTS
DEVCHR=4
EXIT=12
UTPCLR=13
LOGIN=15
REASIN=21
MSTIME=23
RUNTM=27
PJOB=30
SLEEP=31

;MTAPE ARGUMENTS
REW=1
RUN=11
BSR=7
BSF=17
WEF=3
SKP=16
SKPR=6
ENTP=10

DVDIR=4
DVMTA=20
DVDSK=200000
ALTMOD=175		;ASCII ALTMODE

TAPBIT=1000		;DEVICE IS A MAG TAPE
BELBIT=2000		;DEVICE BELONGS TO BP



MONMOD=1000		;PTY IN MONITOR MODE
IOPTRE=2000		;A BIT FOR PTY IO RESPONSE WAITING
IOPTW=4000		;PTY OUTPUT REQUEST

;PTY STATUS BITS FOR SERIES 5 SCANNER SERVICE

IOPTW5=20000		;PTY WANTS OUTPUT
IOPTR5=40000		;PTY WANTS INPUT
MONMO5=100000		;PTY IN MONITOR MODE
;THESE ROUTINES ARE IN THE CONTROL SECTION
EXTERN	NEWCRD,COMWD,COMCHR,ANSWER,CONCH,TELOP7
EXTERN	OPRSLP,PTY6,PTY7,TELOPR,TELUSR,TYPCRD
EXTERN	TELUS7,INDH,PTIH,PTIH1,PTIH2,TTI,OUTINI
EXTERN	NXTJOB,INDH1,LINH1,LINH2,.JBFF,OUNAM,INNAM
EXTERN	PTO1,PTO2,TTI1,HLFFUL


	IFE	PROT,<EXTERNAL KILIJ>
;STUFF THE CONTROL ROUTINES NEED
INTERN FSTCOM,USRDON,USRDMP,COUNT,KILL,FORNME,YARGH
IFE NTS,<INTERN BPJOB>
INTERN MACNME,BPTCHR,CNT,DCNT
IFN	DISK,<INTERN PRJCT,PRGRMR>


;INITIAL ENTRY FOR JOB
FSTCOM:	MOVEM P,PDLPT
	MOVEI	TAC,10		;RESET COUNT FOR TIME LOOP
	MOVEM	TAC,COUNT
	SETZM	DCNT
	HRLI	F,0
	TLZE	F2,JOB2		;JOB CARD READ, NOT PROCESSED?
	PUSHJ	P,JOB		;YES,GO DO IT NOW
	HRLI	F2,0		;INITIALIZE F2
	SETZM	CPYSCR
	SETZM	CRFSCR
	PUSHJ	P,FORCEC	;MAKE SURE PTY IS LISTENING
	IFE	HDR,<		;WE DONT WANT FF IF WE HAVE A HEADER
	PUSHJ	P,FORM1		;START LISTING WITH A FORM-FEED
>
	JRST	COMAND
	SUBTTL	CONTROL CARD INTERPRETER
COMDSP:	TRNE	T,-1		;IGNORE ALL CARDS UNTIL
	TLNE	F,JBCRDI	;JOB CARD IS READ
	IFE	HDR,<
	PUSHJ	P,(TAC1)	;DISPATCH TO CARD HANDLER
>
	IFN HDR,<
	JRST	CHKPRT
>
COMAND:	TLZE	F,ENDJOB
	JRST	USRDON		;EOJ CARD READ - THROUGH
	TLZE	F,NXTCDI	;NEXT CARD IN CORE?
	JRST	COMND2		;YES. DONT GO TO NXTCRD
	PUSHJ	P,NEWCRD	;READ THE NEXT CARD
	CAIE	TAC,(SIXBIT .  $.)
	JRST	.-2		;LOOP TILL CONTROL CARD
COMND2:	IFE	HDR,<
	PUSHJ	P,TYPCRD>
	TLZ	F,TMPBIT+FOR+BINUN+POSLDR+XCUTE+COB
	TLZ	F2,NOPRNT+QSTOK+CRF+CMPLST+BINUSE
	PUSHJ	P,COMWD		;GET FUNCTION FROM CARD
	MOVSI	T,-NOCRDS	;TRY TO MATCH WITH TABLE
CMATCH:	HLLZ	TAC1,TAC	;1ST 3 CHARS OF FUNCTION
	XOR	TAC1,CRD(T)
	TLNN	TAC1,777777	;MATCH?
	JRST	COMDSP		;YES. DISPATCH
	AOBJN	T,CMATCH	;NO. TRY AGAIN
	JSP	A,TELUSR	;CANT FIND. IGNORE CARD
	SIXBIT	/UNRECOGNIZED CARD. IGNORED#/
	JRST	PDINI


;CONTROL CARD LIST
CRD:	<SIXBIT .JOB.>+JOB
	<SIXBIT .ASS.>+ASSIGN
	<SIXBIT .TAP.>+TAPE
	<SIXBIT .F4 .>+F4
	<SIXBIT .MAC.>+MAC
	<SIXBIT .CBL.>+COBL
	<SIXBIT .CRE.>+CREF
	<SIXBIT .LDR.>+LDR
	<SIXBIT .EOF.>+EOF
	<SIXBIT .BIN.>+BINCRD
	<SIXBIT .EXL.>+XLDR
	<SIXBIT .SSA.>+SSAVE
	<SIXBIT .SAV.>+SAVE
	<SIXBIT .RUN.>+SVRUN
	<SIXBIT .GET.>+GET
	<SIXBIT .EOJ.>+EOJ
	<SIXBIT .*  .>+COMCRD
	<SIXBIT .PAU.>+PAUSE
	<SIXBIT .** .>+COMENT
	<SIXBIT .STA.>+STRT
	<SIXBIT .DUM.>+DUMP
NOCRDS=.-CRD
	IFN	HDR,<
CHKPRT:	PUSH	P,TAC1
	TRNE	T,-1
	PUSHJ	P,TYPCRD
	POP	P,TAC1
	PUSHJ	P,(TAC1)
	JRST	COMAND
>
	SUBTTL	$JOB CARD
;$JOB CARD
JOB:	TLNE	F,JBCRDI	;SECOND $JOB CARD?
	JRST	JOBTWO		;YES. $EOJ CARD MISSING
	HRRZ	A,LINH1	;LOCATION OF BUFFER
	MOVEI	TACM,12
	PUSHJ	P,TELOP7	;TYPE JOB CARD FOR OPR
	HRLI	F,JBCRDI+NOMAP	;JOB CARD READ ONLY USER FLAG
	SETZB	LD,SU
	SETZM	PRGCNT		;PROGRAM COUNT
	SETZM	PRCNME
	SETZM	FUDGER
IFN	SER4,<
	SETOM	LOGWD	;SET LOGWD=NON 0 (ASSUME DISK SYSTEM)
>
JOBINI:	MOVE	E,10
	MOVEM	E,CNT
	IFN 	HDR,<
	PUSHJ	P,GETDAT	;USE DAYTIME COMMAND TO SET UP HEADER
>
LOGIN1:	JSP	A,PTY6
	SIXBIT	/LOGIN#/
	TLO	F,PTYDAT
	PUSHJ	P,PTYIN		;GET RESPONSE
	JRST	HOPE		;TRY AGAIN
	JRST	JOBLOG		;JOB INITIALIZED
CANTIN:	JSP	A,TELOPR	;INFORM OPR CANT INITIALIZE
	SIXBIT	/CANT INITIALIZE JOB.#/
	JRST	OPW		;GO BACK TO COMIN

HOPE:	PUSHJ	P,FORCEC
	SOSG	CNT
	JRST	CANTIN
	JRST	LOGIN1

JOBTWO:	HRLI	F2,JOB2		;INDICATE $JOB CARD READ ALREADY
	JRST	USRDON		;AND KILL THIS GUY


;COMMENT CARD
COMCRD:	HRRZ	A,LINH1		;SET TO GIVE CARD TO OPER
	MOVEI	TACM,12		;LINE FEED TERMINATES
	PUSHJ	P,TELOP7	;TYPE TO OPER
	JRST	COMIN		;AND WAIT FOR "CONT"


PAUSE:	JSP	A,TELOPR
	SIXBIT	/PAUSE#/
	JRST	COMIN		;WAIT FOR "CONTINUE"

COMENT:	HRRZ	A,LINH1		;SET TO GIVE CARD TO OPER
	MOVEI	TACM,12		;LINE FEED TERMINATES
	PUSHJ	P,TELOP7
	JRST	CONT

EXTERN CONT

JOBLOG:
JOBIN:	HRLM	TAC,JOBNO
	JSP	A,TELOPR
	SIXBIT	/RUNNING JOB/
JOBNO:	SIXBIT	/   #/
	HLRZ	TAC,JOBNO
	PUSHJ	P,TOBIN
	MOVEM	TAC,UJOB	;USER JOB NUMBER
	IFN	HDR,<
	MOVEI	B,0		;CLEAR THE NAME FIELD
	MOVEM	B,NAME		;IN CASE WE HAD A
	MOVE	B,[XWD NAME,NAME+1]
	BLT	B,NAME+4	;LONGER NAME LAST TIME
>
	MOVE	B,[POINT 6,NAME,]
	EXTERN	GETCHA
	MOVEI	A,^D30
NMLOOK:	MOVEI	TAC,LINH1-1
	PUSHJ	P,GETCHA
	JRST	BDJBCD
	CAIN	TACM,","
	JRST	NMFND
	SUBI	TACM,40
	IDPB	TACM,B
	SOJG	A,NMLOOK
	JRST	BDJBCD
NMFND:	IFN	HDR,<
	PUSHJ	P,HEAD
>

;GET CORE REQUIREMENT
	PUSHJ	P,COMWD		;CORE
	LSH	TAC,-6
IFE	SER4,<
	TRNE	F2,SWPBIT	;IS IT A SWAPPING MONITOR?
	MOVEI	TAC,0		;YES, JOBS WILL EXPAND AS REQUIRED
>
	HLLM	TAC,PRCOR	;STORE IN COMMANDS WHICH REQUIRE 
IFN	SER4,<			;STR 10-3740
	HLRZ	TAC,TAC
	MOVE	D,TAC		;COPY CORE REQUEST INTO D
	SETZ	TAC,		;INITIALIZE WORK REGISTER
CORSHF:	MOVE	TACM,D
	ANDI	TACM,77		;GET LEAST SIGNIFICANT DIGIT
	CAIN	TACM,0
	JRST	CORCHK
	SUBI	TACM,20		;MAKE A DIGIT FROM SIXBIT
	LSH	TAC,3		;PREPARE TO DEPOSIT DIGIT
	ADD	TAC,TACM	; DEPOSIT DIGIT INTO TAC
	LSH	D,-6		;PEEL ONE DIGIT OFF REQUEST
	JRST	CORSHF		;GO BACK FOR MORE
CORCHK:	CAILE	TAC,CORMAX	;OVER INSTALLATION MAXIMUM?
	JRST	BDJCOR		;YES, BOMB THIS GUY
>
;GET EXECUTION TIME
	PUSHJ	P,COMWD		;GET TIME
	PUSHJ	P,TOBIN		;TO BINARY
	IMULI	TAC,^D1000  ;TIME TO MSECS
	MOVEM	TAC,TIME


	MOVE	B,[POINT 6,PSTRNG,]  ;YES. READ PROJ-PROG
	PUSHJ	P,COMWD		;PROJECT NUMBER
	CAIE	TACM,(SIXBIT .  /.)
	CAIN	TACM,(SIXBIT .  ,.)	;","
	SKIPA	TAC1,TACM		;YES, SAVE DELIMITER
	JRST	CANTLG		;NO. CANT LOG IN
	PUSHJ	P,STRDAT	;STORE PROJECT IN STRING
	IFN	LOGSWT,<
	ROTC	TAC,-6
	PUSHJ	P,STRDAT
	JRST	.+2>
	PUSHJ	P,SETSLS	;MAKE A /
	PUSHJ	P,COMSTR	;READ AND STORE PROGRAMMER NUMBER
	PUSHJ	P,FINSTR	;SHOVE STRING OVER TO MONITOR
	TLO	F2,QSTOK	;"?" AS 1ST CHAR OK
	PUSHJ	P,PTYWT		;WAIT FOR LOGIN TO TYPE OUT
	TLZ	F2,QSTOK
JOBOPT:	PUSHJ	P,COMWD		;GET NEXT FIELD
	JUMPE	TAC,NOOPT	;END OF CARD
	CAMN	TAC,[SIXBIT .DUMP.]
	TLO	F,ENDUMP	;DUMP
	CAMN	TAC,[SIXBIT .NOGO.]
	TLO	F2,NOGO2	;NOGO
	JRST	JOBOPT		;LOOK FOR ANOTHER

BDJCOR:	JSP	A,TELUSR	;STR 10-3740
	SIXBIT	/EXCESSIVE CORE REQUEST#/

BDJBCD:	JSP	A,TELUSR
	SIXBIT	/JOB CARD BAD - JOB IGNORED#/
	JRST	USRDON
INTERNAL SCRDEV,NOSCR1

;CARD IS FINISHED. INITIALIZE FOR JOB
NOOPT:
IFE	SER4,<
	TRNN	F2,LOGBIT	;LOGIN NEEDED?
	JRST	INITMP		;NO
>
	PUSHJ	P,NEWCRD	;GET CARD WITHPASSWORD
	MOVSI	TAC,70000	;RESET BYTE POINTER TO START OF CARD
	ADDM	TAC,LINH1
	PUSHJ	P,COMWD		;GET KEY
	CAIE	TACM,(SIXBIT .  $.)
	PUSHJ	P,TACPTY	;TYPE KEY
	JRST	CANTLG		;BAD KEY, KILL HIM
IFN	SER4,<
	SOSG	PTIH2
	JRST	INITMP
	ILDB	TAC,PTIH1	;LOOK AT RESPONSE- IF "?" IS
	CAIN	TAC,"?"		;INPUT FROM THE MONITOR
	SKIPA			;THERE IS NO LOGIN  - HENCE
	JRST	.-5
	SETZM	LOGWD		;NO LOGOUT
>



;JOB IS INITIALIZED. LOOK FOR OPTIONS ON CARD
INITMP:	INIT	TMP,0		;INITIALIZE SCRATCH DEVICE
SCRDEV:	SIXBIT	/BPTEMP/
	0
	JRST	NOSCRT		;NEED THE TAPE
	CALLI	TMP,UTPCLR	;CLEAR DIRECTORY IF DECTAPE
	MTAPE	TMP,REW		;REWIND IF MAG TAPE
	MOVE	TAC,SCRDEV	;PUT SCRATCH TAPE IN SU LIST
	MOVEM	TAC,SULST
	MOVEM	TAC,BINARY	;ORIGINALLY PUT BINARIES ON SCRATCH
	CALLI	TAC,DEVCHR	;WHAT IS BPTEMP
	TLNN	TAC,DVDIR
	SKIPA	TAC,LOC3	;A MAG TAPE
	MOVEI	TAC,BELBIT	;A DIRECTORY DEVICE
	HRRZM	TAC,SUDEV
	IFE	NTS,<
	MOVE	TAC,SCRDEV	;REASSIGN TO USER JOB
	PUSHJ	P,FROMBP
	JRST	YARGH
>
	IFN	NTS,<
	RELEASE	17,	;RELEASE TTY SO USER CAN GET IT EASILY
	TRO	F2,NOTTY
>
	POPJ	P,
;USER PROJ-PROG AND KEY ARE INCORRECT
CANTLG:	JSP	A,TELUSR
	SIXBIT	/CANT LOG JOB IN.#/
	MOVEI	TACM,3		;TYPE A ^C, WHICH WILL NOT
	JSP	A,PTY7		;BE ECHOED BY THE MONITOR
	BYTE	(7)3		;SINCE IT IS IN LOGIN
	JRST	USRDN1		;GO GET NEXT JOB
	JRST	USRDN1		;GO GET NEXT JOB CARD


NOSCRT:	JSP	A,TELOPR
NOSCR1:	SIXBIT	/PLEASE ASSIGN DEVICE BPTEMP#/
	PUSHJ	P,COMIN		;WAIT FOR OPR TO TYPE "CONT"
	JRST	INITMP		;TRY TO GET BPTEMP AGAIN
	SUBTTL	$ASSIGN CARD
;HANDLE $ASSIGN CARD
ASSIGN:	MOVEI	TAC1,PTY	;RETRIEVE PTY CHANNEL
	CALLI	TAC1,61		;GET PTY STATUS( 5-SERIES )
	JRST	ASSGN1		; NON 5-SERIES SCANNER
	TLNN	TAC1,MONMO5	;CHECK MONITOR MODE BIT
	JRST	ASSGN1+1
	JRST	ASSGN1+2
ASSGN1:	STATO	PTY,MONMOD	;IS PTY IN MONITOR MODE?
	TLO	F2,NOTMON	;NO. REMEMBER SO CAN "CONT" LATER
	PUSHJ	P,CONC		;PUT IN MONITOR MODE
	PUSHJ	P,COMWD		;GET LABEL
	PUSH	P,TAC		;SAVE IT
	PUSHJ	P,COMWD		;GET SYMBOLIC UNIT
	MOVEM	TAC,ASINSU	;INTO PTY COMMAND
	MOVEI	TAC1,(SU)	;SEARCH REST OF SULIST
	CAMN	TAC,SULST(TAC1)	;FOR SAME NAME
	JRST	ASN2		;FOUND - JUST RE ASSIGN
	SOJGE	TAC1,.-2
	MOVEM	TAC,SULST+1(SU)	;SU NAME LIST
	POP	P,TAC		;RESTORE LABEL
	IFN	SITSW,<
	HLRZ	TAC1,TAC
	CAIN	TAC1,(SIXBIT .LPT.)
	PUSHJ	P,ASLPT
>
	LDB	TAC1,PBYT1	;1ST CHARACTER
	CAIE	TAC1,(SIXBIT .  D.)
	CAIN	TAC1,(SIXBIT .  M.)
	JRST	ASKMNT		;TAPE LABEL. GET REAL UNIT FROM OPR
	CAME	TAC,OUNAM
	CAMN	TAC,INNAM
	JRST	NOASN3		;PHYSICAL DEVICE = IN OR OUT IS ILLEGAL


PHYSDV:	AOS	SU		;COUNT UP NUMBER OF SU'S
	MOVEM	TAC,SUDEV(SU)	;PHYSICAL DEVICE NAME ON LIST
	CAIN	TAC1,(SIXBIT . T.)
	TRZ	TAC,7777
	TRZ	TAC,77
	LDB	TAC1,PBYT1
	CAIN	TAC1,(SIXBIT .  M.)
	TRZ	TAC,TAPBIT	;STR 10-3156
	MOVEM	TAC,ASINPD	;INTO PTY MESSAGE
PHYDV1:
	IFE	NTS,<
	MOVE	TAC,ASINPD
	PUSHJ	P,FROMBP	;IF DEVICE BELONGS TO BP REASSIGN IT
	JRST	.+3		;DIDN'T BELONG
	MOVEI	TAC,BELBIT	;BELONGED - REMEMBER SO THAT
	ORM	TAC,SUDEV(SU)	; CAN GET IT BACK LATER
>
	JSP	A,PTY6		;OUTPUT MESSAGE OVER PTY
	SIXBIT	/ASSI/
ASINPD:	0
ASINSU:	0			;LOGICAL NAME
	SIXBIT	/#/		;END OF MESSAGE
	PUSHJ	P,PTYIN		;GET MONITOR RESPONSE
	JRST	NOASIN		;QUESTION MARK - BAD NEWS
	JRST	TAPRL2		;OK. TYPE "CONT" IF NECESSARY
	IFN	SITSW,<
ASLPT:	JSP	A,TELUSR
	SIXBIT /PLEASE USE TTY NOT LPT#/
	MOVSI	TAC,(SIXBIT .TTY.)
	POPJ	P,
>
;HERE WHEN REASSIGNING A TAPE
ASN2:	MOVE	TAC,SUDEV(TAC1)	;PHYSICAL NAME OF DEVICE
	TRZ	TAC,7777
	TRO	TAC,3		;SET IT FOR OPER TYPE OUT
	MOVEM	TAC,NEWUNI
	PUSH	P,TAC1
	PUSHJ	P,COMWD		;GET NEW REEL-NUMBER
	MOVEM	TAC,NEWNME
	JSP	A,TELOPR	;ASK TO MOUNT NEW TAPE
	SIXBIT	/PLEASE MOUNT TAPE/
NEWNME:	0
	SIXBIT	/ AS/
NEWUNI:	0
	PUSHJ	P,COMIN		;WAIT FOR CONTINUE
	POP	P,TAC1
	MOVE	TAC,SUDEV(TAC1)
	SOJA	SU,PHYSDV+1	;NOW TELL MONITOR
;ASK OPR TO MOUNT A TAPE
ASKMNT:	CAMN	TAC,[SIXBIT .DSK   .]
	JRST	PHYSDV		;SORRY ABOUT THAT
	TLO	F,TMPBIT	;A TAPE. REMEMBER THAT FACT
	MOVEM	TAC,LABEL
	MOVEM	TAC,NLABEL	;TAPE LABEL
	PUSHJ	P,COMWD		;PROTECT?
	MOVEM	TAC,B
	CAMN	TAC,[SIXBIT .NOREW.]
	JRST	NOPROT
PROTST:	JUMPE	TAC,NOPROT	;NO
	JSP	A,TELOPR	;YES
	SIXBIT	/MOUNT TAPE /
LABEL:	0
	SIXBIT	/ WRITE PROTECTED#/
	PUSHJ	P,COMWD
	JRST	GETPHY
NOPROT:	JSP	A,TELOPR
	SIXBIT	/MOUNT TAPE/
NLABEL:	0
	SIXBIT	/ WRITE ENABLED#/
GETPHY:	PUSHJ	P,ANSWER	;GET PHYSICAL UNIT FROM OPR
	CAME	TAC,[SIXBIT .END.]	;WANT TO END?
	JRST	GETPH7
	JSP	A,TELUSR
	SIXBIT	/****** JOB ABORTED#/
	JRST	USRDON
GETPH7:	CAME	TAC,[SIXBIT .DSK.]
	JRST	GETPHZ
	TLNE	F2,SCRASK	;CANT ASSIGN DISK IF BATCH WANTS A SCRATCH TAPE
	JRST	NOASN2		;SINCE BATCH CANT WRITE IN USER'S UFD
	JRST	GETPH3		;USER ASKING FOR TAPE, ITS OK
GETPHZ:	CAME	TAC,OUNAM	;CANNOT ASSIGN BATCH INPUT OR OUTPUT DEVICE
	CAMN	TAC,INNAM	;IF EITHER IS TAPE
	JRST	NOASN2
GETPH3:	MOVE	C,TAC
	CALLI	C,DEVCHR	;DEVICE CHARACTERISTICS
	MOVEM	TAC,.+2		;IF IT IS A MAG TAPE
	INIT	TMP,0		;REWIND IT SO POSITION COUNT
OPRNAM:	0			;MAKES SENSE
	0
	JRST	NOASN2		;CANT GET IT
	TLNE	F2,BINUSE	;STR 10-2042
	JRST	TELLS1
	MOVEM	TAC,TELLUS
	JSP	A,TELUSR
TELLUS:	0
	SIXBIT	/#/
TELLS1:	JSP	A,TELOPR	;TELL OPR HE CAN MOUNT IT
	SIXBIT	/OK#/
	PUSHJ	P,COMIN		;WAIT TILL HE TYPES "CONTIN"
	CAMN	B,[SIXBIT .NOREW.]	;NOREWIND?
	JRST	.+3
	TLNE	C,DVMTA
	MTAPE	TMP,REW
	TLNE	F2,BINUSE	;COMMING FROM GSCRAT?
	CALLI	TMP,UTPCLR	;YES. ZERO DIRECTORY
	MOVE TAC,OPRNAM
	RELEASE	TMP,
	TLNN	C,DVDIR
	TRO	TAC,TAPBIT	;SUDEV TAPBIT MEANS POSITION DEPENDENT
	JRST	PHYSDV


;GOT "?" FROM MONITOR ON ASSIGN
NOASIN:	MOVE	TAC,SUDEV(SU)	;PHYSICAL NAME OF DEVICE
	TLNN	F,TMPBIT	;DID OPR ASSIGN IT?
	JRST	NOTAVL		;NO. USER WANTS SPECIFIC DEVICE
NOASN2:	MOVEM	TAC,.+2		;YES. HE GOOFED
	JSP	A,TELOPR	;QUESTION HIM ABOUT IT
	SIXBIT	/****** NOT AVAILABLE#/
	JRST	GETPHY		;TRY TRY AGAIN DEAR OPERATOR

NOASN3:	MOVEM	TAC,.+2
	JSP	A,TELUSR
	SIXBIT	/****** NOT AVAILABLE#/
	JRST	USRDON

;HE WANTS SPECIFIC UNAVAILABLE DEVICE
NOTAVL:	MOVEM	TAC,NEEDEV
	JSP	A,TELOPR	;COMPLAIN TO OPR
	SIXBIT	/USER NEEDS DEVICE/
NEEDEV:	0
	SIXBIT	/.#/
	PUSHJ	P,COMIN		;WHAT DO YOU THINK?
	JRST	PHYDV1		;HE DIDN'T KILL JOB, SO DEVICE
				;IS PROBABLY NOW AVAILABLE
;OPERATOR TRIED TO GIVE A DISK FOR SCRATCH TAPE
;IT HAS TO BE A REAL TAPE, BECAUSE CAN'T ENTER SOMEONE ELSE'S UFD
GETPH2:	JSP	A,TELOPR
	SIXBIT	/SORRY, BUT IT HAS TO BE A TAPE#/
	JRST	GETPHY


;$BIN CARD
BINCRD:	PUSHJ	P,FINDUN	;GET SU FROM CARD
	MOVEM	TAC,BINARY	;THIS TAPE WILL RECIEVE BINARIES
	POPJ	P,
	SUBTTL	$TAPE CARD
;$TAPE CARD
TAPE:	PUSHJ	P,FINDUN	;GET INDEX OF UNIT ON CARD
TAPSET:	MOVE	D,A
	MOVEM	TAC,TAPEIN	;SU ON CARD
	MOVEI	TAC1,PTY	;RETRIEVE CHANNEL NUMBER
	CALLI	TAC1,61		;GET PTY STATUS BITS
	JRST	TAPST1		; NOT 5-SERIES SCANNER
	TLNN	TAC1,MONMO5	;MONITOR MODE?
	JRST	TAPST1+1
	JRST	TAPST1+2
TAPST1:	STATO	PTY,MONMOD	;IN USER MODE?
	TLO	F2,NOTMON	;NO
	PUSHJ	P,CONC		;^C
	MOVE	TAC,TAPEIN
	PUSHJ	P,TOBP		;ASSIGN IT TO BP
	INIT	TMP,0		;INITIALIZE
TAPEIN:	0
	0
	JRST	YARGH		;BIG TROUBLE IF CANT ASSIGN IT
	TLZE	F,POSLDR	;CALLED FROM $LDR CODE?
	POPJ	P,		;YES. RETURN


NXTOP:	PUSHJ	P,COMWD		;GET NEXT OPERATION
	JUMPE	TAC,TAPRL1	;THROUGH
	MOVSI	TAC1,-TAPOPS	;SEARCH LIST
	CAME	TAC,TAPEOP(TAC1)
	AOBJN	TAC1,.-1
	XCT	TAPEDO(TAC1)	;DO ACTUAL POSITIONING
	TRNE	D,TAPBIT	;MAG TAPE?
	XCT	TAPOS(TAC1)	;YES. ADJUST POSITION COUNT
	JRST	NXTOP

;THROUGH WITH TAPE. GIVE IT BACK TO USER
TAPRL1:	MOVE	A,D
TAPERL:	MOVEM	A,SUDEV(T)	;UPDATED FILE COUNT
	RELEASE	TMP,
	IFE	NTS,<
	MOVE	TAC,TAPEIN
	PUSHJ	P,FROMBP	;REASSIGN IT
	JSP	T,YARGH
>
TAPRL2:	TLZN	F2,NOTMON	;BACK TO USER MODE?
	POPJ	P,		;NO. RETURN
TAPRL3:	JSP	A,PTY6		;YES. CONTINUE
	SIXBIT	/CON#/
	PUSHJ	P,PTYWT		;WAIT FOR MONITOR RESPONSE (CR-LF)
	TLZ	F,RUNCOM	;SO F4 CARD WONT GO TO LOOP
	JRST	NOMON		;TURN OFF MONMOD AND RETURN


PTY6WT:	PUSHJ	P,PTY6		;TYPE "CONT"
	JRST	PTYWT		;WAIT FOR RESPONSE AND RETURN
TAPEOP:	SIXBIT	.DZ.
	SIXBIT	.MA.
	SIXBIT	.MW.
	SIXBIT	.MB.
	SIXBIT	.MT.
TAPOPS=.-TAPEOP

TAPEDO:	CALLI	TMP,UTPCLR
	MTAPE	TMP,SKP
	MTAPE	TMP,REW
	MTAPE	TMP,BSF
	JFCL	0		;ONLY GO THROUGH THIS IF REALLY MAG TAPE
	JFCL	0		;NO-OP IF NOT A MATCH
TAPOS:	JFCL	0		;NO-OP IF DECTAPE OPERATION
	AOS	D		;BUMP FILE COUNT BY 1
	TRZ	D,177		;ZERO COUNT
	PUSHJ	P,CNTBAK	;BUMP DOWN IF NOT AT LOAD POINT
	PUSHJ	P,LOGEND	;COUNT FILES TO END OF TAPE
	JFCL	0		;IF NO MATCH

;DECREASE COUNT IF NOT REWOUND
CNTBAK:	TRNE	D,177		;REWOUND?
	SOS	D		;NO
	POPJ	P,

;SKIP TO LOGICAL END OF TAPE, COUNT NUMBER OF FILES
LOGEND:	MTAPE	TMP,BSR		;
SKPTMP:	MTAPE	TMP,SKP		;SKIP 1 FILE
	MTAPE	TMP,0		;WAIT FOR TAPE TO GET THERE
	CLOSE	TMP,1		;TURN OFF EOF BIT
	MTAPE	TMP,SKPR	;READ A RECORD
	MTAPE	TMP,0		;WAIT TILL DONE
	STATO	TMP,20000	;EOF?
	AOJA	D,SKPTMP	;NO. SKIP TO NEXT EOF
	ADDI	D,2		;YES. COUNT LAST FILE
	P,
	SUBTTL	$F4, $CBL, $MAC, $CREF CARDS
;$CBL CARD
COBL:	TLO	F,COB
	MOVE	TAC,[SIXBIT .COBOL.]
	JRST	MAC+1

;$F4 CONTROL CARD
F4:	SKIPA	TAC,FORNME	;NAME IS "F4"

CREF:	TLOA	F2,CRF+NOPRNT	;MACRO PLUS CREF
	TLOA	F,FOR		;LIGHT FORTRAN BIT

;MACRO CONTROL CARD
MAC:	MOVE	TAC,MACNME	;NAME IS "MACROX"
	TLNE	F,LDRIN		;LOADER IN CONTROL?
	JRST	NOCOMP		;YES. COMPILATION ILLEGAL
	TLZ	F,XCUTE
	CAMN	TAC,PRCNME	;IS THIS PROGRAM IN CORE?
	JRST	CRDINI		;YES. FORGET R COMMAND
RUNPRG:	MOVEM	TAC,PRCNME	;NO. ISSUE R COMMAND 
	TLO	F,RUNCOM
	PUSHJ	P,CONC		;MAKE SURE TALKING TO MONITOR
	IFE	NTS,<
	MOVEI	TAC1,PTY
	CALLI	TAC1,61
	  JRST	RUNPG1
RUNPG1:	SETSTS	PTY,0	;CLEAR MONMOD BEFORE CALLING LOOP
>
	JSP	A,PTY6		;ISSUE COMMAND OVER PTY
	SIXBIT	/R/
PRCNME:	0
PRCOR:	SIXBIT	/*** #/		;USER'S CORE INSERTED HERE
	TLZ	F,GOGOGO
	TLZE	F,XRUN		;FROM $RUN CARD?
	POPJ	P,		;YES. RETURN
CRDINI:	PUSHJ	P,COMWD		;GET PROGRAM NAME
	JUMPN	TAC,NAMED
	AOS	FUDGER
	MOVE	TAC,NAMLOC
	ADD	TAC,FUDGER
NAMED:	MOVEM	TAC,PRGNAM	;AND SAVE
	MOVE	B,[POINT 6,PSTRNG,] ;POINTER TO CONSTRUCTED STRING
	CAIE	TACM,(SIXBIT .  :.)  ;STR 10-2177
	JRST	XCRDI		;NO--NAME IS O.K.
	AOS	FUDGER		;YES-RESET NAME AND SAVE SPEC.
	MOVE	TACM,NAMLOC	;UPDATE NAME
	ADD	TACM,FUDGER
	MOVEM	TACM,PRGNAM	;AND STORE IT
	SKIPA			;ALREADY HAVE SPECIFICATION
XCRDI:	PUSHJ	P,COMWD		;GET DEVICE SPECIFICATION
	ROT	TAC,6
	CAIE	TAC,(SIXBIT .  B.)
	PUSHJ	P,NOBIN		;NO BINARY SPECIFIED
	PUSHJ	P,SETBIN	;INSERT BINARY IN STRING
	PUSHJ	P,SETCOM	;INSERT COMMA
	PUSHJ	P,COMWD		;NEXT SPECIFICATION
	ROT	TAC,6
	TLNE	F2,CRF		;CREF?
	JRST	CHKCRF		;YES. LISTING ONLY ON BPTEMP
TSTLST:	CAIE	TAC,(SIXBIT .  L.)
	PUSHJ	P,NOLST		;NO LIST DEVICE SPECIFIED
	PUSHJ	P,SETLST	;INSERT LIST DEV IN STRING
	PUSHJ	P,SETARO	;INSERT "_"
	PUSHJ	P,COMWD		;GET SOURCE SPEC
	ROT	TAC,6
TSTSRC:	JUMPE	TAC,TTYSRC	;INPUT FROM IN DEV
	CAIE	TAC,(SIXBIT .  S.)
	JRST	BDCRD		;IMPROPER SPECIFICATION
	TLO	F,TMPBIT	;1ST TIME THROUGH
	PUSHJ	P,COMWD		;GET 1ST PART OF SOURCE SPEC.
	MOVE	C,TACM		;SAVE DELIMITER
CRD1:	CAMN	TAC,[SIXBIT .TTY.]  ;IS DEVICE TTY?
	JRST	CHKTTY		;YES. MAY HAVE TO COPY
	PUSHJ	P,SETDEV	;NO. INSERT DEVICE IN STRING
	TRNE	A,TAPBIT	;MAG TAPE?
	AOS	SUDEV(T)	;YES. BUMP POSITION COUNT
CRD3:	TLZE	F,TMPBIT	;1ST TIME CHECK FOR 
	JUMPE	C,SRCNAM	;BARE DEVICE, ADD NAME

CRD2:	PUSHJ	P,COMWD		;GET NEXT PART OF DESCRIPTION
	SKIPN	TAC
	JUMPE	TACM,FSTRNG	;FINISH UP IF END OF CARD
	MOVE	C,TACM		;SAVE DELIMITER
	CAIN	TACM,(SIXBIT .  :.)  ;DEVICE NAME?
	JRST	CRD1		;YES
	PUSHJ	P,STRDAT	;NO. PUT NAME ON LIST
	SKIPE	C
	IDPB	C,B		;INSERT DELIMITER
	JRST	CRD2		;AND TRY AGAIN


;INSERT "NAME.SRC" IN LIST
SRCNAM:	TLZE	F2,DSKCPY	;IF COPIED A FILE ONTO THE DISK
	JRST	FSTRNG		;THE STRING IS ALREADY SET UP
	MOVE	TAC,PRGNAM	;PROGRAM NAME
	PUSHJ	P,STRDAT
OUDIR=4
OUMAG=20
;FINISH STRING AND GO INTO LOOP
FSTRNG:	PUSH	P,B
	TLNE	F,NXTCDI	;COPY SOURCE FOR MACRO?
	PUSHJ	P,COPY		;YES. GO COPY
	TLZE	F,RUNCOM	;NEED RESPONSE FROM PTY?
	PUSHJ	P,LOOPS		;YES. WAIT FOR IT
	POP	P,B
FSTRGA:	PUSHJ	P,FINSTR	;FINISH PSTRNG AND OUTPUT OVER PTY
	TLO	F,NEEDLD	;LOADER WILL BE NEEDED
	PUSHJ	P,LOOPS		;GO SERVICE IN AND OUTPUT REQUESTS
	TLZE	F2,CRF		;RUN CREF?
	JRST	RUNCRF		;YES
FSTRGB:
	IFE	NTS,<
	PUSHJ	P,OUTST		;GET OUTPUT DEV BACK IF GIVEN
	JRST	FRMTST			;AWAY TO USER
	TLZ	F2,CMPLST	;DONT NEED A FORM FEED - F40 PUT ONE IN
	JRST	TAPRL3		;GIVE "CONT" OVER PTY, WAIT FOR RESPONSE
>

FRMTST:	TLZE	F2,CMPLST	;LISTING FILE JUST PUT ON OUT DEV?
	PUSHJ	P,FORM		;YES. ADD FORM FEED
	POPJ	P,
;FINISH STRING AND OUTPUT
FINSTR:	MOVEI	TAC,3		;TERMINATING CHAR
	IDPB	TAC,B	
	MOVEI	A,PSTRNG	;LOC OF STRING
	JRST	PTY6		;OUTPUT


;OUTPUT A FORMFEED ON LISTING DEVICE
FORM:	TRNE	F,OUMAG+OUDIR	;TAPE OR DISC?
	JRST	FORM1		;YES, OUTPUT A FORM-FEED CHARACTER
	CLOSE	OUD,		;NO, CLOSE IT INSTEAD
	POPJ	P,		;AND NO ASCII FF'S
FORM1:	MOVEI	TACM,14		;SINGLE CHARACTER
	MOVEI	A,.+2
	JRST	TELUS7		;OUTPUT A FF
	BYTE	(7)40,14


	IFE	NTS,<
;TEST TO SE IF USER HAS OUTPUT DEVICE. IF SO, GET IT BACK
OUTST:	TLZN	F2,OBJOUT	;USER HAVE OUTPUT DEVICE?
	POPJ	P,
	PUSHJ	P,CONC
	MOVE	TAC,OUNAM	;YES. GET IT BACK FROM USER
	PUSHJ	P,TOBP
	TLO	F,RUNCOM
	TRO	F,INITOU	;TELL BPCS JUST TO INIT 
	PUSHJ	P,OUTINI
	JRST	CPOPJ1
>
;SET LISTING DEVICE = BPTEMP FOR CREF
CHKCRF:	PUSH	P,TAC		;SAVE TAC
	MOVE	TAC,SCRDEV
	TLOE	F2,BINUSE	;CAN CREF USE BPTEMP?
	JRST	NOCREF		;NO. ASK OPR FOR SCRATCH TAPE
CRFNM:	MOVEM	TAC,CRFNAM	;NAME OF SCRATCH DEVICE
	PUSHJ	P,STRDAT	;"BPTEMP"
	MOVE	TAC,[SIXBIT .:/C_.]
	PUSHJ	P,STRDAT
	POP	P,TAC
	CAIE	TAC,(SIXBIT .  L.)  ;LISTING SPECIFICATION?
	JRST	TSTSRC		;NO
	PUSHJ	P,COMWD		;YES. FORGET IT - LISTING ONLY
	JRST	TSTSRC-2	;ON REGULAR OUTPUT DEVICE


;HAVE A LISTING ON BPTEMP, GET A CREF FROM IT
RUNCRF:	TLZ	F2,NOPRNT
	MOVE	TAC,CRFNAM
	PUSHJ	P,FNDUN1	;GET CHARACTERISTICS OF CREF SCRATCH
	TRNE	A,TAPBIT	;MAG TAPE?
	PUSHJ	P,MTBSF1	;YES. BACKSPACE A FILE
;TAPE IS POSITIONED. RUN CREF
RCREF:	MOVE	B,[POINT 6,PSTRNG,]
	MOVSI	TAC,(SIXBIT .TTY.)
	SETZM	PRGNAM		;SET LISTING DEVICE IN STRING
	PUSHJ	P,REGOUT
	PUSHJ	P,SETARO
	MOVE	TAC,CRFNAM	;SOURCE FILE
	PUSHJ	P,SETNM1	;INSERT "BPTEMP:NAME"
	MOVE	TAC,CRFNME	;RUN "CREF"
	TLO	F,XRUN
	PUSHJ	P,RUNPRG	;R CREF
	PUSHJ	P,LOOP		;WAIT FOR IT TO APPEAR
	TLO	F2,CMPLST	;FORM FEED AFTER LISTING
	JRST	FSTRGA		;GIVE STRING TO CREF


;BACKSPACE TAPE IN TAC 1 FILE
MTBSF:	PUSHJ	P,FNDUN1	;SET PARAMETERS FOR TAPSET
MTBSF1:	TLOA	F,POSLDR
MTBSF3:	TLOA	F,POSLDR
	PUSHJ	P,TAPSET
	MTAPE	TMP,BSR
	MTAPE	TMP,BSF		;BACKSPACE 1 FILE
	MTAPE	TMP,0
	STATO	TMP,4000	;BOT?
	MTAPE	TMP,SKPR	;NO. OVER LAST EOF
MTBSF2:	TLZ	F2,NOTMON	;DONT CONTINUE
	AOS	A,D
	JRST	TAPERL		;GIVE BACK TO USER
IFE	DISK,<
;NO "S:SU" SPECIFICATION - INPUT =TTY
TTYSRC:	MOVSI	TAC,(SIXBIT .TTY.)

;SOURCE SPECIFICATION IS "TTY" - CHECK FOR POSSIBLE COPY
CHKTTY:	TLNN	F,FOR		;F4?
	JRST	.+3		;NO. HAVE TO COPY IT
	PUSHJ	P,SETDV2	;YES. INSERT "TTY" ON STRING
	JRST	CRD3		;BACK FOR NEXT PART OF SPEC.

	MOVE	TAC,BPTCHR	;IS BPTEMP A DISK?
	TLNE	TAC,DVDSK	;(BUT ASSEMBLED FOR NO DISK)
	TLO	F,NXTCDI	;YES. FOR A NEW SCRATCH DEVICE
	MOVE	TAC,SCRDEV	;WILL COPY CARDS ONTO BPTEMP
	TLON	F,NXTCDI	;IF ONLY 1 FILE PER ASSEMBLY
	TLOE	F2,BINUSE	;AND BINARY OUTPUT NOT ON BPTEMP
	JRST	NOCOPY		;TOO BAD CHARLIE
CPYNM:	MOVEM	TAC,CPYNAM
	PUSHJ	P,SETNM1	;PUT "BPTEMP:NAME" ON PSTRNG
	MOVE	TAC,[SIXBIT /.BPT/]  ;ADD EXTENSION ".BPT" TO PSTRNG
	PUSHJ	P,STRDAT
	JRST	CRD3		;AND LOOK FOR MORE INPUT


COPY:	TLOE	F,RUNCOM	;ASSIGN BPTEMP TO BP
	PUSHJ	P,LOOPS
	PUSHJ	P,CONC		;SHOVE OVER "^C"
	MOVE	TAC,CPYNAM
	MOVEM	TAC,TAPEIN
	PUSHJ	P,TOBP		;GET TAPE
	MOVEI	TAC,CPYBUF
	MOVEM	TAC,.JBFF	;USE IT FOR COPY
	INIT	TMP,0		;SET UP TO COPY
CPYNAM:	0
	XWD	OUTBF,0
	JRST	YARGH
	OUTBUF	TMP,1
	ENTER	TMP,PRGNAM	;ENTER "NAME.BPT"
	JRST	NOENTR		;TOUGH LUCK
>
IFN	DISK,<
TTYSRC:
CHKTTY:				;COPY CARDS ONTO DISK, TELL CUSP WHERE
	TLOE	F,NXTCDI	;ALREADY COPIED ONTO DISK?
	JRST	NOCOPY		;YES. FORGET IT
	TLO	F2,DSKCPY	;NO. INDICATE COPYING TO DISK
	MOVSI	TAC,(SIXBIT /DSK/)
	PUSHJ	P,STRDAT	;BUILD STRING - "DSK"
	MOVE	TAC,[SIXBIT /:QQBAT/] ; ":QQBAT"
	PUSHJ	P,STRDAT
	MOVE	TAC,[SIXBIT /.BPT[/] ;".BPT["
	PUSHJ	P,STRDAT
	MOVE	TAC,PRJCT	;"PRJNO,"
	PUSHJ	P,STRDAT
	MOVSI	TAC,(SIXBIT /,/)
	PUSHJ	P,STRDAT	;STR 10-2262 INSERT , AND ] IN STRING
	MOVE	TAC,PRGRMR	;"PRGNO]"
	PUSHJ	P,STRDAT
	MOVSI	TAC,(SIXBIT /]/)
	PUSHJ	P,STRDAT
	JRST	CRD3		;LOOK FOR MORE INPUT


;HERE ACTUALLY TO COPY ONTO THE DISK
COPY:	MOVEI	TAC,CPYBUF
	MOVEM	TAC,JOBFF
	MOVEI	E,(SIXBIT .  4.)
	INIT	TMP,0		;SET TO COPY
	SIXBIT	/DSK/
	XWD	OUTBF,0
	JRST	YARGH		;CANT INIT
	OUTBUF	TMP,1
	ENTER	TMP,QQNAME	;ENTER "QQBAT.BPT"
	JRST	NOENTR
>
COPYA:	PUSHJ	P,NEWCRD	;GET NEXT CARD
	ADDI	TAC,40		;TO ASCII
	CAIE	TAC,"$"		;CONTROL CARD?
	JRST	COPYC		;NO. COPY IT
	CLOSE	TMP,		;TERMINAL EOF
IFE	DISK,<
	MOVE	TAC,CPYNAM
	PUSHJ	P,FNDUN1
	MOVEI	TAC1,MTBSF3
	TRNN	A,TAPBIT	;MAG TAPE - BACK 1 FILE
	MOVEI	TAC1,MTBSF2	;NOT MAG - JUST GIVE BACK
	SOS	D,A		;MTBSF WILL AOS A,D
	PUSHJ	P,(TAC1)
	IFE	NTS,<
	MOVEI	TAC1,PTY
	CALLI	TAC1,61
	  JRST	COPYA1
COPYA1:	SETSTS	PTY,0		;ZAP MONMOD SO EVENT. RTN FROM LOOP IS OK.
>
	JSP	A,PTY6		;GET BACK TO CUSP
	SIXBIT	/STA#/
	POPJ	P,		;AND START IT UP
>
IFN	DISK,<
	RELEASE	TMP,		;SET DISK UP RIGHT
	POPJ	P,		;AND GO AWAT

QQNAME:	SIXBIT	/QQBAT/
	SIXBIT	/BPT/
	0
	0
PRJCT:	0
PRGRMR:	0
>
COPYB:	CAIN	TAC,12		;MORE DATA ON CARD?
	JRST	COPYA		;NO. GET NEXT
	ILDB	TAC,LINH1	;GET NEXT CHAR
	JUMPE	TAC,COPYA	;END OF CARD
COPYC:	SOSG	OUTBF+2
	OUTPUT	TMP,
	IDPB	TAC,OUTBF+1	;INSERT CHARACTER
	JRST	COPYB
;COULDN'T ENTER THE FILE NAME
NOENTR:	RELEASE TMP,		;GIVE UP TAPE
	IFE	NTS,<
	MOVE	TAC,SCRDEV
	PUSHJ	P,FROMBP
	JRST	YARGH		;GASP
>
	JSP	A,TELUSR
	SIXBIT	/SCRATCH DEVICE DIRECTORY FULL#/
	JRST	BDCRD1

;BAD CARD
BDCRD:	PUSHJ	P,DONTST	;GET BACK OUTPUT DEVICE
	JSP	A,TELUSR
SIXBIT	/UNRECOGNIZED FIELD ON CARD. CARD IGNORED#/
BDCRD1:	TLO	F,ERR		;EXECUTION NOT ALLOWED
	JRST	PDINI		;RESTORE P AND GET NEXT CARD

;INSERT VARIOUS CHARACTERS INTO STRING
SETSLS:	MOVEI	TAC,(SIXBIT .  /.)
	JRST	SETCHR
SETARO:	MOVEI	TAC,(SIXBIT .  _.)
	JRST	SETCHR
SETCOM:	SKIPA	TAC,COMLOC
SETCOL:	MOVEI	TAC,(SIXBIT .  :.)
SETCHR:	IDPB	TAC,B
COMLOC:	POPJ	P,(SIXBIT .  ,.)
;CANT CREF ONTO BPTEMP - GET SCRATCH
NOCREF:	MOVE	D,[SIXBIT .BPTEM2.]
	PUSHJ	P,GSCRAT	;GET A SCRATCH TAPE
	CRFSCR
	JRST	CRFNM		;GO USE IT

IFE	DISK,<
;CANT COPY ONTO BPTEMP FOR MACRO ASS'Y
NOCOPY:	MOVE	D,[SIXBIT .BPTEM1.]
	PUSHJ	P,GSCRAT	;GET A SCRATCH
	CPYSCR
	JRST	CPYNM		;GO USE IT
>
IFN	DISK,<
NOCOPY:	JSP	A,TELUSR
	SIXBIT	/CAN ONLY COPY 1 FILE ONTO THE DISK#/
	JRST	USRDON
>
;GET A SCRATCH DEVICE
GSCRAT:	MOVE	T,@(P)
	SKIPE	TAC,(T)		;TAPE ALREADY GOTTEN?
	JRST	CPOPJ1		;YES. GO AWAY
	PUSH	P,T
	TLOE	F,RUNCOM	;WILL HAVE TO WAIT FOR RESP.
	PUSHJ	P,LOOPS		;WAIT TILL CUSP TYPES "*"
	POP	P,T
	PUSHJ	P,CONC
	JSP	A,TELOPR	;NO. ASK FOR ONE
	SIXBIT	/PLEASE MOUNT A SCRATCH TAPE#/
	MOVEM	D,ASINSU	;MAKE BELIEVE IT WAS A $ASSIGN 
	MOVEM	D,(T)		;JUST ONCE PER JOB
	AOS	SU
	MOVEM	D,SULST(SU)	;SYMBOLIC UNIT
	TLO	F2,SCRASK
	PUSHJ	P,GETPHY	;GET PHYSICAL DEV, ASSIGN TO USER
	TLZ	F2,SCRASK
	JSP	A,PTY6
	SIXBIT	/STA#/		;RESTART CUSP
	MOVE	TAC,D		;RETURN WITH SU IN TAC
	JRST	CPOPJ1



COMSTR:	PUSHJ	P,COMWD

;INSERT C(TAC) INTO STRING
	LSHC	TACM,6
	IDPB	TACM,B
STRDAT:	JUMPN	TAC,.-2
	POPJ	P,


;PUT DEVICE NAME INTO PSTRNG
SETBIN:	TLO	F,BINUN
SETLST:	PUSHJ	P,COMWD		;GET DEVICE SPECIFICATION
SETSTR:	CAMN	TAC,[SIXBIT .TTY.]
	JRST	REGOUT		;TTY IS LEGAL
	CAMN	TAC,[SIXBIT .*.]
	POPJ	P,		;NO OUTPUT WANTED
	PUSHJ	P,FNDUN1	;CHECK IF LEGAL DEVICE
	TLZE	F,BINUN		;PUT NAME ON LDR LIST?
	TLNE	F,ERR
	JRST	SETNM3		;NO
	MOVE	TAC1,PRGNAM	;YES. NAME OR POSITION?
	TRNE	A,TAPBIT
	MOVE	TAC1,A		;POSITION
LSTLOC:	MOVEM	TAC1,LDRLST(LD)	;ONTO LIST
	HRRM	T,LDRDEV(LD)	;DEVICE INDEX ONTO LIST
	AOS	LD,PRGCNT
SETNM3:	TRNE	A,TAPBIT	;MAG TAPE?
	AOS	SUDEV(T)	;YES. BUMP CURRENT POSITION
SETNM1:	PUSHJ	P,SETDV2	;DEVICE NAME ONTO LIST
	MOVE	TAC,PRGNAM	;NAME ONTO STRING
	JRST	STRDAT

SETDEV:	PUSHJ	P,FNDUN1	;CHECK DEVICE LEGALITY
SETDV2:	PUSHJ	P,STRDAT	;INSERT DEVICE ONTO STRING
	JRST	SETCOL		;AND A COLON

;BINARY SPECIFICATION OMITTED
NOBIN:	TLOA	F,BINUN
NOLST:	TLOA	F2,CMPLST+NOLSTD	;FORM FEED AFTER COMP/ASS'Y
	SKIPA	C,BINARY	;USE BINARY DEVICE
	MOVSI	C,(SIXBIT .TTY.) ;USE PTY FOR LISTING
	CAMN	C,SCRDEV
	TLO	F2,BINUSE	;BPTEMP IN USE
	EXCH	C,TAC		;SAVE TAC
	PUSHJ	P,SETSTR	;PUT DEVICE ON LIST
	TLZE	F2,NOLSTD	;/M IN F40 COMP IF NO LST DEV SPECIFIED
	PUSHJ	P,STRDAT
	POP	P,A		;WHERE YOU CAME FROM
	XCT	1(A)		;INSERT COMMA OR COLON
	MOVE	TAC,C		;RESTORE TAC
	JRST	4(A)		;AND CONTINUE


;STANDARD LISTING DEVICE.  GIVE DEVICE TO USER IF IT
;HAS NO DIRECTORY
REGOUT:	PUSHJ	P,SETOUT	;GET OUTPUT DEVICE
	JRST	SETNM1		;GO AWAY HAPPY
;YOUR CARDS ARE MESSED UP CLOWN
NOCOMP:	JSP	A,TELUSR
SIXBIT	/CANT COMPILE OR ASSEMBLE AFTER A $EXLDR CARD#/
	JRST	BDCRD1




SETOUT:	MOVE	TAC1,OUNAM	;IF OUTPUT DEVICE IS A TTY
	CALLI	TAC1,DEVCHR	;DO NOT REASSIGN
	TLNN	TAC1,10		;DVTTY=10 IS TELETYPE OR
	TRNE	F,OUDIR		;DIRECTORY?
	POPJ	P,		;YES. FORGET IT
REGOU1:	MOVE	TAC,OUNAM	;NO, GIVE IT AWAY
	MOVE	TAC1,SU		;IF A LOGICAL ASSIGN ON THIS
	CAMN	TAC,SULST(TAC1)	;UNIT HAS BEEN PERFORMED THEN
	JRST	SETOU2		;THE PHYSICAL DEVICE WITH THE SAME
	SOJG	TAC1,.-2	;NAME CANT BE ASSIGNED
	RELEASE	OUD,		;OUTPUT PARTIAL BUFFER
	TLO	F2,OBJOUT
	PUSHJ	P,FROMBP
	JRST	OUTASN		;CANT REASSIGN WHAT ISN'T ASSIGNED
	POPJ	P,		;GO AWAY HAPPY


OUTASN:	MOVE	TAC,OUNAM
	IFN	SITSW,<
	HLRZ	A,TAC
	CAIN	A,(SIXBIT .LPT.)
	POPJ	P,
>
	MOVEM	TAC,OUTDEV
	JSP	A,TELOPR
	SIXBIT	/PLEASE ASSIGN/
OUTDEV:	0
	SIXBIT	/TO BATCH PROCESSOR#/
	PUSHJ	P,COMIN		;WAIT FOR "CONTINUE"
	JRST	REGOU1

SETOU2:	MOVSI	TAC,(SIXBIT .TTY.)
	POPJ	P,
	SUBTTL	$LDR CARD
;$LDR CARD
LDR:	PUSHJ	P,FINDUN	;GET SU FROM CARD
	TLNE	F,LDRIN		;LOADER IN CONTROL?
	JRST	GIVLDR		;YES. GIVE CARD TO IT
	TLO	F,POSLDR	;JUST INIT TAPE FOR BP
	MOVE	D,A		;SAVE CHARACTERISTICS WORD
	TRNE	A,TAPBIT	;IF A MAG TAPE
	PUSHJ	P,TAPSET
LDRNAM:	PUSHJ	P,COMWD		;GET NAME OF PROGRAM
	TRNN	D,TAPBIT	;MAG TAPE?
	JRST	LDRNM		;NO. SAVE NAME IN LIST
	HRRZM	D,LDRLST(LD)	;YES. SAVE TAPE POSITION IN LIST
	MTAPE	TMP,SKP		;AND SPACE OVER BINARY FILE
	AOSA	D
LDRNM:	MOVEM	TAC,LDRLST(LD)	;SAVE NAME IN LIST
	HRRZM	T,LDRDEV(LD)	;SAVE INDEX OF SU
LDRNM1:	JUMPE	TACM,CHKLIB	;CHECK LIB SPECIFICATION IF NO COMMA
	CAIN	TACM,(SIXBIT /  ./)
	JRST	SKPEXT		;IGNORE SPECIFICATION OF AN EXTENSION
LDRBMP:	AOBJP	LD,LDRNAM	;YES
CHKLIB:	PUSHJ	P,COMWD		;NEXT FIELD ON CARD
	CAMN	TAC,[SIXBIT .LIB.]  ;LIBE?
	JRST	LDRLIB		;YES. SET BIT(S) IN LDRDEV
	JUMPN	TAC,LDRBMP	;NO. ANYTHING ELSE ON CARD IS A PROGRAM
LDREL:	TRNE	D,TAPBIT	;MAG TAPE?
	PUSHJ	P,TAPRL1	;YES. GIVE IT BACK TO USER
	MOVEI	LD,1(LD)	;RESET LD
	MOVEM	LD,PRGCNT	;AND COUNT PROGRAM
	TLO	F,NEEDLD	;WILL NEED LOADER
	POPJ	P,		;AND RETURN

;SET LIBE BIT ON EVERY PTOGRAM ON CARD
LDRLIB:	HLRZ	TAC,LD		;COUNT OF PROGRAMS ON CARD
	SUB	LD,TAC
	MOVSI	TAC1,400000	;LIGHT SIGN BIT FOR LIBE 
	ORM	TAC1,LDRDEV(LD)
	SOSL	TAC
	AOJA	LD,.-2		;ALL PROGRAMS ON CARD
	JRST	LDREL

SKPEXT:	PUSHJ	P,COMWD		;SKIP EXTENSION ON CARD
	JRST	LDRNM1		;CHECK IF ANY MORE PROGRAMS ON CARD
;GIVE COMMAND STRING TO LOADER,WHICH IS CURRENT USER JOB
GIVLDR:	MOVE	B,[POINT 6,PSTRNG,]
	PUSHJ	P,STRDAT	;PUT IN STRING
	PUSHJ	P,SETCOL	;INSERT COLON
	PUSHJ	P,COMCHR	;NEXT CHARACTER FROM CARD
	CAIN	TACM,(SIXBIT .  ,.)  ;ANOTHER PROGRAM FOLLOWING?
	AOS	PRGCNT		;YES. COUNT IT
	IDPB	TAC,B		;IN STRING
	JUMPN	TAC,.-4		;BLANK TERMINATES
	PUSHJ	P,COMWD		;NEXT FIELD
	CAME	TAC,[SIXBIT .LIB.]
	JRST	SHVSTR	
	TLO	F,TMPBIT	;"LIB" SPECIFIED
	JSP	A,PTY6		;TELL LOADER
	SIXBIT	./L#.
	PUSHJ	P,PTYWT		;WAIT FOR RESPONSE
SHVSTR:	PUSHJ	P,FINSTR	;GIVE STRING TO LOADER
	AOS	PRGCNT		;COUNT PROGRAM
	PUSHJ	P,LOOPS		;WAIT TILL LOADER DOES IT
	TLZN	F,TMPBIT	;LIBE MODE?
	POPJ	P,		;NO. THROUGH WITH CARD
NOLBMD:	JSP	A,PTY6		;YES. TURN IT OFF
	SIXBIT	./N#.
	JRST	PTYWT		;WAIT FOR RESPONSE AND RETURN

	SUBTTL	$SAVE, $GET, $RUN CARDS


;$GET CARD
GET:	SETZM	PRCNME
	SETZM	LD
	MOVSI	TAC,(SIXBIT .GET.)
	JRST	SAVCOM		;DO A MONITOR GET


;$RUN CARD
SVRUN:	TLZ	F,ERR+GOGOGO+LDRIN+NEEDLD
	TLO	F2,NOPRNT	;DONT PRINT "JOB SETUP"
	PUSHJ	P,GET		;GET PROGRAM
	TLZ	F2,NOPRNT
	JRST	GOGO		;START JOB RUNNING

;$START CARD
STRT:	TLZ	F,LDRIN+NEEDLD+ERR+GOGOGO
	JRST	GOGO

;$SAVE AND $SSAVE CARDS
SSAVE:	SETOM	SSVE
SAVE:	SKIPN	PRGCNT
	JRST	SVNSTR		;ALLOW SAVE, BUT NOT START
	PUSHJ	P,LDRCHK	;FINISH LOAD IF NEEDED
	JRST	NOSAV
	TLNN	F2,NOGO2	;UNLESS NOGO WAS ON JOB CARD
	TLO	F,GOGOGO	;EXECUTION PERMITTED
SVNSTR:	SKIPE	SSVE
	JRST	SSVER
	MOVSI	TAC,(SIXBIT .SAV.)
	JRST	SAVCOM
SSVER:	SETZM	SSVE
	MOVSI	TAC,(SIXBIT .SSA.)

SAVCOM:	MOVEM	TAC,SVWRD	;GET OR SAVE
	PUSHJ	P,CONC
	PUSHJ	P,FINDUN	;GET SU
SAVDEV:	MOVE	TACM,TAC	;STR 10-3814
	ANDI	TACM,77
	CAIE	TACM,0
	CAIN	TACM,40
	JRST	.+2
	JRST	.+3
	LSH	TAC,-6
	JRST	SAVDEV
	MOVEM	TAC,SVWRD1	;IN COMMAND
	PUSHJ	P,COMWD		;GET NAME
	MOVEM	TAC,SVWRD2	;IN COMMAND
	PUSHJ	P,COMWD		;SEE IF CORE ARGUMENT GIVEN
	LSH	TAC,-6
	SKIPN	TAC
	HLL	TAC,PRCOR	;NO, USE CORE FROM JOB CARD
IFN	SER4,<
	HLLM	TAC,SVCOR	;STORE IN SAVE COMMAND
>
	JSP	A,PTY6		;TALK TO MONITOR
SVWRD:	0
SVWRD1:	0
	SIXBIT	/:/
SVWRD2:	0
IFN	SER4,<
SVCOR:	SIXBIT	/*** #/>
IFE	SER4,<
SVCOR:	SIXBIT	/#/>
	PUSH	P,LP1LOC	;RETURN TO LOOP AFTER
	JRST	WTRESP		;GETTING A MONITOR RESPONSE


NOSAV:	JSP	A,TELUSR
	SIXBIT	/NO LOAD - PROGRAM NOT SAVED#/
LP1LOC:	POPJ	P,LOOPL

;WAIT FOR A RESPONSE OVER PTY, THEN GO TO PTYIN
WTSLEP:	PUSHJ	P,OPRSLP	;WAIT FOR RESPONSE
WTRESP:	MOVEI	TAC1,PTY	;GET PTY CHANNEL
	CALLI	TAC1,61
	JRST	WTRES1		;FOR OLD SCANNER SERVICE
	TLNN	TAC1,IOPTR5	;INPUT WAITING?
	JRST	WTSLEP		;NO, GO WAIT FOR IT
	JRST	WTRES2		;YES, GO PROCESS
WTRES1:	STATO	PTY,IOPTRE	;RESPONSE?
	JRST	WTSLEP		;NO, SLEEP AND TRY AGAIN
WTRES2:	INPUT	PTY,		;YES, READ PTY BUFFER
	SKIPN	PTIH2		;IF THERE IS A REAL RESPONSE
	JRST	WTRESP
	JRST	PTYINA		;GO TO PTYIN AFTER ITS INPUT
	SUBTTL	$EOF, $EOJ CARDS
;$EOJ CARD
EOJ:	TLO	F,ENDJOB	;LIGHT BIT

;$EOF CARD
EOF:	TLZE	F,GOGOGO	;JOB SAVED (HENCE LOADED)?
	JRST	GOGO		;YES. START IT
	TLNN	F,LDRIN+NEEDLD	;LOADER OPERATIONS NEEDED?
	POPJ	P,		;NO. GOODBYE
	TLNE	F2,NOGO2
	TLNN	F,NOMAP		;NO EXECUTIN AND NO MAP - DONT LOAD
	PUSHJ	P,LDRCHK	;YES. TALK TO LOADER
	JRST	NOXCUT
	TLNN	F2,NOGO2
GOGO:	TLNE	F,ERR	;GO INTO EXECUTION?
	JRST	NOXCUT		;NO. TOO BAD CHARLIE
	IFE	NTS,<
	SKIPN	HLFFUL		;IN FULL DUPLEX MONITOR
	JRST	GOGO1+1
	MOVEI	TAC1,PTY
	CALLI	TAC1,61
	  JRST	GOGO1
GOGO1:	SETSTS	PTY,0		;RESET MONMOD
>
	JSP	A,PTY6		;START HIM UP
	SIXBIT	/STA #/
	MOVEI	TAC,12		;STR 10-2085
	MOVEM	TAC,CHARSV
	JSP	A,TELUSR
	SIXBIT	/BEGIN EXECUTION:#/
	TLO	F,XCUTE
DLYLUP:	SKIPE	HLFFUL
	JRST	LOOPS	;WAIT FOR SERVICE REQUESTS
DLYLP2:	PUSHJ	P,OPRSLP
	MOVEI	TAC1,PTY	;PTY CHANNEL NUMBER
	CALLI	TAC1,61		;NEW SCANNER SERVICE??
	JRST	DLYLP3		;NO--GO OLD ROUTE
	TLNN	TAC1,IOPTR5+MONMO5
	JRST	DLYLP2
	JRST	LOOPS
DLYLP3:	STATO	PTY,IOPTRE	;WAIT FOR MONITOR SUPPLIED
	JRST	DLYLP2		;CAR.RET--LINE FEED
DLYLP4:	PUSHJ	P,NOMON		;TURN OFF MONMOD
	JRST	LOOP		;AND GO TO LOOP
NOXCUT:	JSP	A,TELUSR
	SIXBIT	/NO EXECUTION#/
	POPJ	P,

NOMON:	MOVEI	TAC1,PTY	;GET PTY CHANNEL
	CALLI	TAC1,61		; GET PTY CHARACTERISTICS
	JRST	NOMON1		; NOT 5-SERIES SCANNER
	HRRI	TAC1,1
	TLZ	TAC1,MONMO5
	SETSTS	PTY,(TAC1)	;SET PTY STATUS MINUS MONMO5
	POPJ	P,		;RETURN
NOMON1:	GETSTS	PTY,A
	TRZ	A,MONMOD	;SET STATUS OF PTY TO ORIGINAL STATUS
	SETSTS	PTY,(A)		;MINUS MONMOD
	POPJ	P,
;CHECK ON LOADER - START AND/OR FINISH IT
LDRCHK:	TLNN	F,ERR
	SKIPN	PRGCNT		;FORGET IT IF NO PROGRAMS
	POPJ	P,		;OR ERROR
	PUSH	P,TAC		;SAVE TAC
	TLZE	F,NEEDLD	;BRING IN LOADER?
	PUSHJ	P,GETLD		;YES. LOAD EVERYTHING SO FAR
	TLZE	F,LDRIN		;LOADER RUNNING?
	PUSHJ	P,FINLD		;YES. FINISH UP
TPOPJ1:	AOSA	-1(P)
CPOPJ1:	AOSA	(P)
TPOPJ:	POP	P,TAC
CPOPJ:	POPJ	P,
	SUBTTL	$EXLDR CARD
;$EXLDR CARD
XLDR:	TLZ	F,NEEDLD	;HAVE LOADER NOW
	TLNE	F,ERR		;ERROR IN COMPILATION/ASSEMBLY?
	JRST	NOXCUT		;YES. STOP RIGHT HERE
	PUSHJ	P,COMWD
	CAME	TAC,[SIXBIT .MAP.]
	TLOA	F,NOMAP		;STORAGE MAP WANTED
	TLZ	F,NOMAP

;BRING LOADER INTO CORE, GIVE IT CONTENTS OF LDRLST
GETLD:	TLO	F,LDRIN+XRUN	;LOADER WILL BE IN CORE
	MOVE	TAC,[SIXBIT .LOADER.]
	PUSHJ	P,RUNPRG	;GO GET LOADER
	PUSHJ	P,LOOP		;WAIT FOR IT
	TRNE	F2,NODRVR	;IS DRIVER ON SYS?
	JRST	GETLD1		;NO, DO NOT TRY TO LOAD IT
	JSP	A,PTY6		;TRY TO LOAD DRIVER - FOR DUMPS
	SIXBIT	/SYS:DRIVER#/
	TLO	F2,QSTOK	;DONT PANIC IF NOT THERE
	PUSHJ	P,LOOP
	TLZ	F2,QSTOK
GETLD1:	JUMPE	LD,CPOPJ	;LD=0 MEANS NO STUFF TO  BRING IN
	MOVNS	LD
	HRLZS	LD		;LD = XWD -N,0
SETPNT:	MOVE	B,[POINT 6,PSTRNG,]
	HRRE	T,LDRDEV(LD)	;DEVICE PROGRAM IS ON
	JUMPL	T,SYSPRG
	MOVE	TAC,SULST(T)	;NAME OF TAPE
	PUSHJ	P,STRDAT	;PUT IT IN STRING
	PUSHJ	P,SETCOL	;INSERT COLON
	HRRZ	C,SUDEV(T)	;PHYSICAL DEVICE
	MOVE	TAC,LDRLST(LD)	;PROGRAM NAME OR POSITION
	TRNE	C,TAPBIT	;DIRECTORY DEVICE?
	JRST	GETPOS		;NO. USE POSITION
	PUSHJ	P,STRDAT	;YES. PUT NAME IN STRING
GETLIB:	MOVSI	TAC,(SIXBIT ./N.)
	SKIPGE	LDRDEV(LD)	;LIBE SEARCH?
	MOVSI	TAC,(SIXBIT ./L.)  ;YES. TELL LOADER
	PUSHJ	P,STRDAT
	PUSHJ	P,FINSTR	;FINISH STRING AND GIVE TO LOADER
	PUSHJ	P,LOOPS
	AOBJN	LD,SETPNT
	SKIPGE	LDRDEV-1(LD)	;IS LOADER IN LIBE MODE?
	PUSHJ	P,NOLBMD	;YES. RESTORE IT TO /N MODE
	POPJ	P,

;GET PROGRAM BY POSITION OF DEVICE
GETPOS:	SUBI	C,(TAC)		;CURRENT POSITION - DESIRED POS.
	AOS	SUDEV(T)	;BUMP CURRENT POSITION
	JUMPE	C,GETLIB	;POSITIONED RIGHT
	MOVE	TAC,SULST(T)	;GET NAME (SU)
	TLO	F,POSLDR	;HAVE TO REPOSITION IT
	PUSHJ	P,TAPSET	;ASSIGN TAPE TO BP
	JUMPG	C,GETBAK	;BACKSPACE IT
	AOS	C
	MTAPE	TMP,SKP		;GO FORWARD
	AOJL	C,.-1		;N FILES
GETREL:	MOVE	A,LDRLST(LD)	;SET CURRENT POSITION AND GIVE UP TAPE
	AOS	A		;HAVE MOVED IT 1 FILE 
	PUSHJ	P,TAPERL	;GIVE TAPE BACK TO USER
	JRST	GETLIB		;AND TELL LOADER

;BACKUP TAPE
GETBAK:	MTAPE	TMP,BSR		;GET OVER INITIAL EOF
	MTAPE	TMP,BSF		;BACKSPACE 1 FILE
	SOJG	C,.-1		;N TIMES
	MTAPE	TMP,0
	STATO	TMP,4000	;DONT BSR IF AT LOAD POINT
	MTAPE	TMP,SKPR	;OVER LAST EOF
	JRST	GETREL		;GIVE BACK

SYSPRG:	MOVE	TAC,[SIXBIT .SYS:.]
	PUSHJ	P,STRDAT
	MOVE	TAC,LDRLST(LD)
	JRST	GETLIB-1
;EVERYTHING HAS BEEN LOADED. FINISH LOADER UP
FINLD:	TLNE	F,ERR		;ERROR?
	JRST	CONC		;YES. DONT LOAD ANYTHING
	JSP	A,PTY6
	SIXBIT	./F#.		;START LIBE SEARCH
	PUSHJ	P,LOOPS		;WAIT FOR IT
	TLNE	F,ERR		;ERROR?
	JRST	CONC		;YES. DONT TRY TO GET MAP
	TLNE	F,NOMAP
	JRST	FINOMP		;NO MAP
	JSP	A,PTY6		;ASK FOR MAP
	SIXBIT	./M#.
	PUSHJ	P,LOOP		;WAIT FOR MAP, OR UNDEFINEDS
FINOMP:	MOVEI	TAC,"["		;<CONTROL> [ = ALTMODE
	PUSHJ	P,CONCH
	TLO	F2,NOPRNT
	PUSHJ	P,LOOP		;WAIT FOR LOADER TO EXIT
	TLZ	F2,NOPRNT
	POPJ	P,
	SUBTTL	EXECUTION LOOP
	EXTERN	COMIN
LOOPS:	PUSHJ	P,OPRSLP
LOOP:	MOVEI	TAC1,PTY	;PTY CHANNEL
	CALLI	TAC1,61		;WHICH SCANNER
	JRST	LOOPL1		;OLD
	TLNN	TAC1,IOPTR5
	JRST	CHKUIN
	JRST	LOOPS2
LOOPL1:	STATO	PTY,IOPTRE	;USER TRYING TO OUTPUT?
	JRST	CHKUIN		;NO. SEE IF HE WANTS INPUT
LOOPS2:	PUSHJ	P,PTYIN		;YES, GET HIS RESPONSE
LOOPL:	TLOA	F,TMPBIT	;QUESTION MARK - LIGHT BIT
	TLZ	F,TMPBIT
	MOVEI	TAC1,PTY	;PTY CHANNEL NUMBER
	CALLI	TAC1,61		;GET PTY STATUS BITS
	JRST	LOOPX
	TLNN	TAC1,400000
	JRST	IMPROP
	JRST	LOOPL2
LOOPX:	STATZ	PTY,400000	;IO-IMPROPER ON PTY?
	JRST	IMPROP		;YES. IT HAD BETTER BE IN KJOB
LOOPL2:	SKIPN	PTIH2		;NO
	JRST	LOOP		;IGNORE AN EMPTY BUFFER
	MOVEI	T,-52(TAC)	;SAVE FIRST CHARACTER
	TLNN	F,XCUTE		;IF GOT "*" FROM F4 OR MACRO
	JUMPE	T,CHKTHR	;TEST IF END
LOOPB:	MOVEI	TACM,0		;SET TERMINATING CHAR
	IDPB	TACM,A		;AS A NULL
	HRRZ	A,SAVPIT#
	AOS	A
	TLNN	F,TMPBIT	;IGNORE NOPRNT IF ERROR
	TLNN	F2,NOPRNT	;DONT TYPE IF SWITCH SET
	IFE	NTS,<
	TLNE	F2,OBJOUT	;OR USER HAS OUTPUT TAPE
	SKIPA
>
	IFE	DEBUG,<
	PUSHJ	P,TELUS7>
	IFN	DEBUG,<
	PUSHJ	P,TELOP7>
	TLZE	F,TMPBIT	;QUESTION MARK?
	JRST	XQUEST		;YES. TOO BAD
LOOPC:	HRRZ	TAC,@TTI	;CHECK IF OPR TYPED ON TTY
	SKIPG	(TAC)
	PUSHJ	P,OPRSLP	;YES. SEE WHAT HE WANTS
	SKIPE	DCNT		;DUMPING
	JRST	.+3		;YES,FORGET TIME
	SOSG	COUNT		;NO. TIME TO CHECK MAX TIME?
	PUSHJ	P,TIMER		;YES. CHECK RUN TIME
	MOVEI	TAC1,PTY	;GET PTY CHANNEL #
	CALLI	TAC1,61		;WHICH SCANNER
	JRST	LOOPC1		;OLD
	TLNE	TAC1,IOPTR5
	JRST	LOOP+2
	TLNE	TAC1,IOPTW5+MONMO5
	JRST	LOOPS
	JRST	LOOPA
LOOPC1:	STATZ	PTY,IOPTRE
	JRST	LOOP+2		;MORE INPUT
LOOPC2:	STATZ	PTY,IOPTW
	JRST	LOOPS		;GIVE PROGRM TIME TO LIGHT IOPTRE 
				;AGAIN IF IT IS GOING TO
LOOPA:	SKIPE	DCNT
	JRST	.+3
	SOSG	COUNT		;TIME TO CHECK TIME ?
	PUSHJ	P,TIMER		;YES
	MOVEI	TAC1,PTY	;RETRIEVE CHANNEL NUMBER
	CALLI	TAC1,61		;GET STATUS (5-SERIES)
	JRST	LOOPA1		;NOT 5-SERIES
	TLNN	TAC1,MONMO5	;CHECK MODE
	JRST	LOOPS
	POPJ	P,
LOOPA1:	STATO	PTY,MONMOD
	JRST	LOOPS		;NO. STAY IN LOOP
	POPJ	P,		;YES. RETURN

IMPROP:	TLNN	F2,KJOB		;IN KJOB?
	JRST	IMPERR		;NO. SYSTEM ERROR
	TLOE	F2,OFF		;YES. 1ST TIME READING THIS BUFFER?
	POPJ	P,		;NO. JOB IS REALLY KILLED
	JRST	LOOPL2		;YES. PUT LAST BUFFER ON OUTPUT DEVICE

IMPERR:	MOVEI	E,(SIXBIT .  5.)
	JRST	YARGH
;CHECK TO SEE IF USER WANTS INPUT
CHKUIN:	MOVEI	TAC1,PTY	;GET CHANNEL NUMBER
	CALLI	TAC1,61		; GET PTY STATUS (5-SERIES)
	JRST	CHKUN1		; NOT 5-SERIES SCANNER
	TLNN	TAC1,IOPTW5
	JRST	LOOPA
	JRST	CHKUN2
CHKUN1:	STATO	PTY,IOPTW	;WAITING FOR INPUT?
	JRST	LOOPA		;NO. CHECK OPR AND SLEEP
CHKUN2:	TLNE	F2,KJOB		;LOGOUT ASKING FOR CONFIRMATION?
	JRST	CONFRM		;YES. TYPE "K" TO REALLY LOG OUT
	TLNN	F,XCUTE		;NO. IN EXECUTION?
	JRST	CHKF4		;NO. ONLY F4 CAN REQUEST INPUT
NXTCD:	TLZE	F,NXTCDI	;YES. 2ND TTY EOF?
	JRST	TAKOUT		;YES. TAKE JOB OUT AND GET NEXT CARD
	TLNE	F,ENDJOB	;LAST CARD ALREADY READ?
	JRST	EOFXMT		;YES. GIVE EOF
	PUSHJ	P,NEWCRD	;NO. GET NEXT INPUT CARD
	CAIN	TAC,(SIXBIT .  $.)  ;CONTROL CARD?
	JRST	EOFXMT		;YES. SEND EOF
	HRRZ	A,LINH1		;NO. SET A FOR OUTPUT
	MOVEI	TACM,12		;TERMINATING CHAR = LINE FEED
	PUSHJ	P,PTY7		;GIVE CARD TO USER
	JRST	LOOPS


CHKF4:	TLNN	F,FOR+COB		;IS F4 REQUESTING INPUT?
	POPJ	P,		;NO. MUST HAVE MISSED LAST "*"
	JRST	NXTCD		;YES. GO GET IT

CHKTHR:	ILDB	TAC,PTIH1	;IS NEXT CHARACTER NULL?
	JUMPN	TAC,LOOPB	;IF NOT REALLY END, LOOP SOME MORE
CHKTH1:	MOVEI	TAC1,PTY	;GET CHANNEL NUMBER
	CALLI	TAC1,61		; GET PTY CHARACTERISTIC BITS
	JRST	CHKTH4		; NON 5-SERIES RETURN
	TLNE	TAC1,IOPTW5
	POPJ	P,
	JRST	CHKTH3
CHKTH4:	STATZ	PTY,IOPTW	;REALLY END, OBJ. JOB WAITING FOR COMMAND?
	POPJ	P,		;YES, READY
CHKTH3:	PUSHJ	P,OPRSLP	;NO, GIVE IT CHANCE TO DO INPUT
	MOVEI	TAC1,PTY	;FOR PTY CHANNEL NUMBER
	CALLI	TAC1,61		;WHICH SCANNER?
	JRST	CHKTH2		;OLD
	TLNE	TAC1,IOPTR5
	JRST	LOOPB
	JRST	CHKTH1
CHKTH2:	STATZ	PTY,IOPTRE
	JRST	LOOPB
	JRST	CHKTH1




TAKOUT:	POP	P,TAC
	PUSHJ	P,CONC
	JRST	COMND2

CONFRM:	JSP	A,PTY6		;TYPE K FOR LOGOUT
	SIXBIT	/F#/		;"F" FOR NEW LOGOUT
	JRST	LOOPS		;WAIT FOR FINAL MESSAGE TO APPEAR
;CHECK TIME EVERY 5, OR SO, SECONDS
TIMER:	MOVE	TAC,UJOB
	CALLI	TAC,RUNTM	;GET USERS RUN TIME
	CAML	TAC,TIME	;OK?
	JRST	OVRTIM		;OVERTIME
TIMEA:	MOVEI	TAC,5	;OK
	TLNN	F2,TIMGIV	;CHECK EVERY CYCLE IF ALREADY OVERTIME
	MOVEM	TAC,COUNT	;CHECK AGAIN IN 10 CYCLES
	POPJ	P,

OVRTIM:	TLOE	F2,TIMGIV	;HAS HE HAD EXTRA TIME?
	JRST	TABORT		;YES. KILL HIM
	TLNE	F,XCUTE		;STR 10-3743
				;IN EXECUTION?
	JRST	OVRTM1		;YES, DON'T TRY TO TELL F4, ETC
	JSP	A,TELUSR	;TELL OJOB ABOUT TIME PROBLEM
				;STR 10-2894
	SIXBIT	/MAX. TIME EXCEEDED#/
OVRTM1:	JSP	A,TELOPR	;NO, INFORM OPR
	SIXBIT	/MAX. TIME EXCEEDED#/
	MOVEI	TAC,^D10000	;GIVE HIM A GIFT OF ABOUT 8 SECS
	ADDM	TAC,TIME	;AND THEN KILL HIM
	JRST	TIMEA		;UNLESS OPR SAYS TO KEEP GOING
;HEARD QUESTION MARK FROM PTY
XQUEST:	TLZ	F2,CMPLST+CRF
	ILDB	TAC,B
	CAIN	TAC,"?"		;TWO QUESTION MARKS
	JRST	DBLQST		;YES. TELL OPERATOR
XQST2:	TLNE	F,XCUTE		;CUSP?
	JRST	ABORT		;NO. KILL USER JOB
	MOVEI	TAC1,PTY	;PTY CHANNEL NUMBER
	CALLI	TAC1,61		;GET STATUS BITS
	JRST	XQST2A		; NOT 5-SERIES SCANNER
	TLNN	TAC1,MONMO5
	JRST	XQST3
	JRST	XQST2B
XQST2A:	STATO	PTY,MONMOD	;ERROR TAKE JOB TO MONITOR?
	JRST	XQST3		;NO. STILL IN CUSP
XQST2B:	MOVE	TAC,PRCNME	;NAME OF PROGRAM WITH ERROR
	MOVEM	TAC,ERPRNM
	JSP	A,TELOPR	;TELL OPR ABOUT ERROR
	SIXBIT	/ ERROR WHILE RUNNING/
ERPRNM:	0
	SIXBIT	/ -#/
	HRRZ	A,PTIH1		;REPEAT ERROR MESSAGE
	MOVEI	TACM,0		;RESET TERMINATING CHARACTER
	PUSHJ	P,TELOP7
	PUSHJ	P,COMIN		;MAKE OPR TAKE ACTION
	PUSHJ	P,TAPRL3	;HE TYPED "CONT"
	JRST	LOOP

XQST3:	TLO	F,ERR	;STILL IN CUSP - MAKE SURE MESSAGE IS PRINTED
	TLNN	F2,OBJOUT
	TLZE	F2,NOPRNT	;WAS MESSAGE PRINTED TO USER?
	SKIPA	TAC,LSTLOC	;NO. PRINT IT
	JRST	LOOPS		;YES. CONTINUE
	HRL	TAC,A		;SAVE CONTENTS OF MESSAGE BUFFER
	BLT	TAC,LDRLST+26
XQST3C:	PUSHJ	P,OPRSLP
	MOVEI	TAC1,PTY	;PTY CHANNEL #
	CALLI	TAC1,61
	JRST	XQST3A
	TLNN	TAC1,IOPTR5
	JRST	XQST3B
	INPUT	PTY,
	JRST	XQST3C
XQST3A:	STATO	PTY,IOPTRE
	JRST	XQST3B
	INPUT	PTY,		;EMPTY OUT PTY RESPONSES WAITING
	JRST	XQST3C
XQST3B:	PUSHJ	P,FSTRGB	;GET OUT DEV BACK FROM USER PROG.
	MOVEI	A,LDRLST	;TYPE ERROR MESSAGE 
	MOVEI	TACM,177	;TERMINATING CHARACTER
	PUSHJ	P,TELUS7
	JRST	LOOPS		;CONTINUE
DBLQST:	PUSHJ	P,TELOP7	;"??" - GIVE COMMENT TO OPERATOR
	MOVEI	TAC,1
	CALLI	TAC,SLEEP	;RESCHEDULE TO OBJECT JOB
	MOVEI	TAC1,PTY	;GET PTY CHANNEL
	CALLI	TAC1,61		; GET STATUS BITS
	JRST	DBLQS1		; NOT 5-SERIES SCANNER
	TLNN	TAC1,IOPTW5
	JRST	LOOP
	JRST	DBLQS2
DBLQS1:	STATO	PTY,IOPTW	;DOES OBJ JOB WANT AN ANSWER FROM OPERATOR?
	JRST	LOOP		;NO, GO ON AS IF NOTHING HAPPENED
DBLQS2:	INPUT	TTY,		;YES, GET ANSWER FROM OPERATOR
	HRRZ	A,TTI1
	AOS	A
	MOVEI	TACM,12
	PUSHJ	P,PTY7		;AND PASS IT ON TO OBJECT JOB
	JRST	LOOPS		;THEN GO ON



;SHOVE OVER EOF TO USER
EOFXMT:	TLO	F,NXTCDI	;NEXT EOF WILL EXIT JOB
	MOVEI	TAC,"Z"		;GIVE ^Z
	PUSHJ	P,CONCH
	JRST	LOOPA		;WAIT FOR PROGRAM TO DO SOMETHING

	SUBTTL	PTY INPUT
;PTY INPUT ROUTINE
PTYIN:	MOVEI	TAC1,PTY	;PATCH FOR BATCH TO OPERATE
	CALLI	TAC1,61		;PROPERLY WITH LEVEL D SCANSER
	JRST	PTYINZ		;NOT NEW SCANSER, USE OLD WAY
	TLNE	TAC1,IOPTR5	;OUTPUT WAITING?
	JRST	PTYINX		;YES, GET IT
	JRST	PTYINY		;NO, GO TO SLEEP
PTYINZ:	STATZ	PTY,IOPTRE
	JRST	PTYINX		;YES
PTYINY:	PUSHJ	P,OPRSLP	;NO-SLEEP FOR SECOND, TRY AGAIN
	JRST	PTYIN
PTYINX:	INPUT	PTY,		;YES-READ A BUFFER.
PTYINA:	MOVE	TAC1,PTIH2	;PICK UP COUNT
	MOVE	A,PTIH1		;GET BYTE POINTER
	MOVEM	A,SAVPIT
	AOJA	TAC1,PTYIN3	;CHECK FOR INITIAL "?"
	ILDB	TAC,A		;LOOK AT CHARACTER
	MOVEM	TAC,CHARSV
	CAIN	TAC,12		;LINE FEED?
	JRST	PTYIN3		;YES. CHECK NEXT CHAR FOR "?"
PTYIN1:	SOJG	TAC1,.-4
	ILDB	TAC,PTIH1	;RETURN WITH 1ST CHAR IN TAC

	IFN	NTS,<
	JRST	CPOPJ1
>
	IFE	NTS,<
	TLZN	F,PTYDAT	;NO. WANT JOB NUMBER BACK?
	JRST	CPOPJ1		;NO
PTYIN2:	ILDB	TAC,PTIH1	;LOOK FOR SPACE
	SUBI	TAC,40
	JUMPG	TAC,PTYIN2	;LOOP IF NON-SPACE CHARACTER
	JUMPL	TAC,CPOPJ	;ERR RTN IF NO SPACE BEFORE <CR>
PTJBNO:	ILDB	TAC1,PTIH1	;NUMBER = PART OF JOB NUMBER
	SUBI	TAC1,40		;TO SIXBIT
	CAIL	TAC1,20		;NUMBER?
	CAILE	TAC1,32
	JRST	PTJBN1		;NO RETURN
	ROT	TAC1,-6
	LSHC	TAC,6		;YES. ACCUMULATE JOB NUMBER
	JRST	PTJBNO

PTJBN1:	JUMPN	TAC,CPOPJ1	;SOME NUMBER WAS PICKED UP
	POPJ	P,		;NO NUMBER--ERROR RETURN
>

PTYIN3:	ILDB	TAC,A		;GET NEXT CHARACTER
	TLNN	F2,QSTOK	;IF "?" AREN'T LEGAL
	CAIE	TAC,"?"		;CHECK IF THIS IS ONE
	SOJA	TAC1,PTYIN1	;NO
	MOVE	B,CHARSV
	CAIN	B,"."
	JRST	.+3		;STR 10-2601
	CAIE	B,12
	SOJA	TAC1,PTYIN1
	MOVE	B,A		;POINTER TO "?" INTO B
	SUBI	TAC1,2		;FOUND ONE
	IBP	A		;POINT A TO END OF BUFFER
	SOJG	TAC1,.-1
	IBP	PTIH1		;BUMP POINTER TO ADDRESS OF MESSAGE
	POPJ	P,		;AND TAKE NON-SKIP RETURN
	SUBTTL	USEFUL ROUTINES
;SHOVE A ^C OVER PTY
	IFE	NTS,<
CONCIN:	INPUT	PTY,
	JRST	OPRSLP
>
CONC:
	IFE	NTS,<
	PUSH	P,TAC1		;SAVE TAC1
	MOVEI	TAC1,1000	;SET UP FOR A LONG TESTING LOOP
CONCA:	MOVEI	TAC2,PTY	;PTY CHANNEL #
	CALLI	TAC2,61
	JRST	CONC1		;OLD SCANNER
	TLNE	TAC2,IOPTR5	;INPUT RESPONSE WAITING?
	JRST	CONC2
	JRST	CONC3
CONC1:	STATZ	PTY,IOPTRE	;RESPONSE WAITING?
CONC2:	PUSHJ	P,CONCIN	;YES, READ AND IGNORE IT
CONC3:	SOJG	TAC1,CONCA	;KEEP TRYING FOR A WHILE
	MOVEI	TAC1,PTY	;GET PTY CHANNEL NUMBER
	CALLI	TAC1,61		;GET PTY STATUS BITS
	JRST	CONC3A		;NOT 5-SERIES SCANNER
CONC3A:	POP	P,TAC1
	SETSTS	PTY,MONMOD
CONC3B:	PUSHJ	P,.+1
>
	MOVEI	TAC,"C"
	PUSHJ	P,CONCH
	JRST	PTYWT		;WAIT FOR RESPONSE AND RETURN

;GET TAPE FROM USER
TOBP:
	IFN	NTS,<
	POPJ	P,
>
	IFE	NTS,<
	MOVEI	E,(SIXBIT .  3.) ;FOR POSSIBLE ERROR PRINT
	IFN	SITSW,<
	HLRZ	A,TAC
	CAIN	A,(SIXBIT .LPT.)
	POPJ	P,
>
	PUSH	P,TAC		;SAVE TAC
	MOVEM	TAC,.+3
	JSP	A,PTY6		;REASSIGN COMMAND OVER PTY
	SIXBIT	/REAS/
	0			;NAME
BPJOB:	SIXBIT	/*** #/		;BP JOBNO GOES HERE
	PUSHJ	P,PTYIN		;WAIT FOR RESPONSE
	JRST	.+1
	JRST	TPOPJ		;OK. RESTORE TAC AND RETURN
>

;GIVE TAPE TO USER. TAPE NAME IN TAC ON ENTRY
FROMBP:	PUSH	P,TAC
	RELEASE	TMP,
	MOVEI	E,(SIXBIT .  2.) ;FOR POSSIBLE ERROR PRINT
	IFE	NTS,<
	MOVE	TACM,UJOB	;USER JOB NUMBER
	CALLI	TACM,REASIN	;REASSIGN
	JUMPE	TACM,FRMERR	;IF JOB NUMBER NOT ASSIGNED
	JUMPE	TAC,TPOPJ	;IF DEVICE NOT ASSIGNED
>
	JRST	TPOPJ1		;IF REALLY REASSIGNED

FRMERR:	MOVEI	E,(SIXBIT .  6.)
	JRST	YARGH
;WAIT FOR PTY RESPONSE. DIE IF "?"
PTYWT:	MOVEI	E,(SIXBIT .  7.)
	PUSHJ	P,PTYIN		;GET RESPONSE
	JRST	YARGH		;QUESTION MARK
	POPJ	P,		;OK

;CONVERT A NUMBER TO BINARY
TOBIN:	MOVEI	TAC2,0
	LSHC	TACM,6
	ANDI	TACM,17
	IMULI	TAC2,12
	ADD	TAC2,TACM
	JUMPN	TAC,TOBIN+1
	MOVE	TAC,TAC2	;ANSWER IN TAC
	POPJ	P,

;TYPE C(TAC) ON PTY
TACPTY:	MOVEM	TAC,.+4
	MOVEI	TACM,30000
	HRLZM	TACM,.+3	;TERMINATOR CHAR
	JSP	A,PTY6
	0
	0
TACPYA:	MOVEI	TAC1,PTY	;STR 10-3817
	CALLI	TAC1,61
	JRST	TACPYB
	TLNE	TAC1,IOPTR5
	JRST	TACPYX
	JRST	TACPYC
TACPYB:	STATZ	PTY,IOPTRE
	JRST	TACPYX
TACPYC:	PUSHJ	P,OPRSLP
	JRST	TACPYA
TACPYX:	INPUT	PTY,
	MOVE	TAC1,PTIH2
	MOVE	A,PTIH1
	MOVEM	A,SAVPIT
TACPYD:	ILDB	TAC,A
	CAIE	TAC,"#"
	JRST	TACPYE
	SOJ	TAC1,
	JRST	TACPYD
TACPYE:	CAIE	TAC,"?"
	JRST	TACPYY
	ILDB	TAC,A
	SOJG	TAC1,.-1
	JRST	LOOPS
TACPYY:	PUSHJ	P,PTYIN1		;WAIT FOR RESPONSE
	SKIPA			;ERROR RETURN
	AOS	(P)		;GOOD - SKIP- RETURN
	JRST	LOOPS		;WAIT FOR REST OF VERBIAGE TO PASS
;READ TAPE NAME FROM CARD, FIND MATCH IN SULST
FINDUN:	PUSHJ	P,COMWD		;SU NAME
FNDUN1:	CAMN	TAC,[SIXBIT .SYS.]
	JRST	SYSUNI		;UNIT IS "SYS"
	MOVE	TAC1,SU
	CAMN	TAC,SULST(TAC1)	;LOOK FOR MATCH
	JRST	FOUNDU		;MATCH
	SOJGE	TAC1,.-2
	MOVEM	TAC,NOUNIT
	IFE	NTS,<
	PUSHJ	P,OUTST	;MAKE SURE WE HAVE DEVICE
>
LOC3:	JFCL	TAPBIT+BELBIT
	JSP	A,TELUSR
	SIXBIT	/UNIT/
NOUNIT:	0
	SIXBIT	/ NOT ASSIGNED. CARD IGNORED#/
	TLO	F,ERR		;EXECUTION WILL NOT BE PERMITTED
	JRST	PDINI		;RETURN TO COMAND
FOUNDU:	HRRZ	T,TAC1		;MATCH. T=INDEX
	MOVE	A,SUDEV(T)	;A=SUDEV
	POPJ	P,

SYSUNI:	SETZB	A,T
	SOJA	T,CPOPJ
;HERE IF "IMPOSSIBLE" ERROR CONDITION EXISTS
YARGH:	HRLM	E,ERNUM
	JSP	A,TELOPR
	SIXBIT	/ERROR NUMBER/
ERNUM:	SIXBIT	/    - SEE MANUAL#/
	SKIPE	PDLPT
OPW:	MOVE	P,PDLPT
	JRST	COMIN		;MAKE OPERATOR TAKE ACTION


;PUT JOB INTO MONITOR MODE AND GET OUTPUT DEVICE BACK
DONTST:
	IFE	NTS,<
	PUSHJ	P,FORCEC	;ABSOLUTELY ^C
	PUSHJ	P,OUTST		;GET OUTPUT DEVICE
	JFCL
	POPJ	P,
>


;SHOVE OVER A ^C NOW - NO MATTER WHAT JOB IS DOING
FORCEC:	MOVEI	TAC,3
	SKIPG	PTO2
	OUTPUT	PTY,
	IDPB	TAC,PTO1
FORCE1:	SETSTS	PTY,MONMOD	;TURN OFF IOPTRE TO ALLOW ECHO-CHECK
	OUTPUT	PTY,		;FIRST ^C
	IDPB	TAC,PTO1	;SECOND ^C 
	CLOSE	PTY,		;THERE IT GOES
	PUSHJ	P,PTYIN		;MUST GET RESPONSE
	JFCL			;EVEN "?" WILL PUT IT IN MON. MODE
	POPJ	P,
;ABORT FOR OVERTIME - GIVE DUMP IF REQUESTED
TABORT:	PUSHJ	P,DONTST
	HLRZM	P,COUNT		;SO WE WONT TIME OUT AGAIN
	JSP	A,TELUSR
	SIXBIT	/MAX TIME EXCEEDED - JOB KILLED#/

;FINISH JOB IMMEDIATELY
INTERN	LISTEN
ABORT:	TLZE	F,ENDUMP	;DUMP REQUESTED?
USRDMP:	PUSHJ	P,DUMP		;YES. GIVE IT
USRDON:	SETOM	LISTEN
	PUSHJ	P,DONTST
	IFE	NTS,<
	MOVE	TAC,UJOB	;GET USERS RUN TIME
	CALLI	TAC,RUNTM
	IDIVI	TAC,^D1000	;TO SECONDS
	PUSHJ	P,TOSIX		;TO SIXBIT
	MOVEM	TAC2,UTIME
	JSP	A,TELOPR
TIMESS:	SIXBIT	/RUN TIME - /
UTIME:	0
	SIXBIT	/SECS.#/
>
	IFE	HDR,<
	PUSHJ	P,FORM>
	IFE	PROT,<
	PUSHJ	P,KILIJ>
	PUSHJ	P,KILL		;DO A KJOB
	IFN	HDR,<
	PUSHJ	P,TRAIL
>
USRDN1:	SETZM	LISTEN
	MOVE	P,PDLPT
	JRST	NXTJOB		;COME BACK WITH NEXT JOB READY TO GO
PDINI:	MOVE	P,PDLPT		;RESET P
	JRST	COMAND		;GO READ CONTROL CARDS


TOSIX:	MOVEI	TAC2,0
	IDIVI	TAC,12
	ADDI	TAC1,(SIXBIT .  0.)
	LSHC	TAC1,-6
	JUMPN	TAC,TOSIX+1
	POPJ	P,		;RETURN SIXBIT IN TAC2
	EXTERN	WRDIN,ITEM
;KILL JOB
KILL:	PUSHJ	P,FORCEC	;^C
	IFE	NTS,<
KDEV1:	MOVE	TAC,SULST(SU)	;SYMBOLIC NAME
	MOVE	TAC1,SUDEV(SU)	;NAME, BITS
	TRNE	TAC1,BELBIT	;BELONG TO BP?
KDEV2:	PUSHJ	P,TOBP		;YES. GET IT
	SOJGE	SU,KDEV1
>
THRUDV:	MOVEI	TAC1,PTY
	CALLI	TAC1,61
	  JRST	THRDV1
THRDV1:	SETSTS	PTY,0		;TURN OFF MONMOD
	IFN	HDR,<
	PUSHJ	P,GETDAT	;FIND TIME OF DEATH
	>
	JSP	A,PTY6		;DO KJOB
	SIXBIT	/KJOB #/
	TLO	F2,KJOB		;INDICATE IN LOGOUT
	TLZ	F,FOR+COB	;SO LOOP WONT GIVE NEXT CARD OVER PTY
IFN	SER4,<
	SKIPE	LOGWD		;IF THERE IS A LOGOUT CUSP
>
IFE	SER4,<
	TRNE	F2,LOGBIT
>
	JRST	DLYLUP		;WAIT TILL IOPTRE COMES ON, 
	JRST	LOOPS		;OTHERWISE, JUST GET TIME AND END
				;TURN OFF MONMOD AND GO TO LOOP

;GIVE A DUMP
DUMP:	PUSHJ	P,DONTST	;^C
	SETOM	DCNT
	IFE	NTS,<
	MOVEI	A,EXWORD	;EXAMINE LOC. 140
	PUSHJ	P,PTY6WT
	ILDB	TAC,PTIH1	;SEARCH FOR "/"
	CAIE	TAC,"/"
	JRST	.-2
	PUSHJ	P,GET6		;GET LH OF WORD
	CAME	TAC,[SIXBIT .446251.]
	JRST	NODUMP		;LH .NE. "DRI"
	PUSHJ	P,GET6		;GET RH
	CAME	TAC,[SIXBIT .664562.]
>
	IFN	NTS,<
	MOVE	TAC,140
	CAME	TAC,[SIXBIT .DRIVER.]
>
	JRST	NODUMP		;RH .NE. "VER"
	JSP	A,TELOPR	;LET OPERATOR KNOW DUMP IS 
	SIXBIT	/DUMPING#/	;IN PROGRESS
	TLO	F2,QSTOK	;PREVENT ERROR WHILE DUMPING
				;FROM CAUSING A RECURSIVE
				;DUMP
	IFE	NTS,<
	JSP	A,PTY6		;DRIVER IN 140 - START AT 141
	SIXBIT	/ST 141#/	;FOR DUMP W/ MNEMONICS
	PUSHJ	P,OPRSLP	;LET DUMP START
	MOVEI	TAC1,PTY
	CALLI	TAC1,61
	  JRST	DUMP1
DUMP1:	SETSTS	PTY,IOPTRE	;TURN OFF MONMOD
>
	IFN	NTS,<
	EXTERN	JOBSA
	MOVEI	TAC,141
	HRRM	TAC,JOBSA	;SET START TO 141
	JSP	A,PTY6
	SIXBIT	/START#/
>
	JRST	LOOP		;AND HANDLE DUMP
	IFE	NTS,<
EXWORD:	SIXBIT	/E 140#/

GET6:	MOVEI	TAC,PTIH
	PUSHJ	P,WRDIN		;GET A WORD FROM PTY INPUT
	MOVE	TAC,ITEM	;PICH UP WORD
	POPJ	P,		;AND RETURN
>


;CAN'T DUMP CORE - TELL USER WHY NOT
NODUMP:	MOVEI	A,.+3
	PUSHJ	P,TELOPR	;LET OPERATOR KNOW ALSO
	JRST	TELUSR
	SIXBIT	/DRIVER NOT LOADED - CANT DUMP#/
	IFN	HDR,<
GETDAT:	JSP	A,PTY6
	SIXBIT  /DAYTI#/	;ASK FOR TIME OF DAY
	PUSHJ	P,PTYIN		;GET RESPONSE
	HALT	COMIN		;CAN'T HAPPEN ??
	CAIGE	TAC,"0"		;THIS BYPASSES A BUG SOMPLACE
	JRST	GETDAT		;IN BATCH OR PTYSRF
	MOVE	TAC1,[POINT 6,HTIME]
	MOVEI	TAC2,^D18
	SKIPA
HLOOP:	ILDB	TAC,PTIH1
	CAIGE	TAC,40
	MOVEI	TAC,40
	SUBI	TAC,40
	IDPB	TAC,TAC1
	SOJN	TAC2,HLOOP
	POPJ	P,
HEAD:	MOVE	E,[SIXBIT /START /]
	JRST	TAIL
TRAIL:	MOVE	E,[SIXBIT /*END* /]
TAIL:	PUSHJ	P,FORM1		;SKIP TO THE TOP OF A PAGE
	MOVEM	E,HMSG
	MOVEM	E,HLAST
	MOVEI	E,5		;FIVE LINES OF HEADER
HDLOOP:	MOVEI	 A,HMSG
	PUSHJ	 P,TELUSR
	JSP	A,TELUSR
	SIXBIT /#/ 		;DOUBLE SPACE LISTING
	SOJG	E,HDLOOP
	POPJ	P,
>

	SUBTTL	CONSTANTS AND STORAGE

CHARSV:	0
LISTEN:	0
FORNME:	SIXBIT	/F4/
MACNME:	SIXBIT	/MACRO/
CRFNME:	SIXBIT	/CREF/
PBYT1:	POINT	6,TAC,5
PDLPT:	0
SSVE:	0
BINARY:	0		;TAPE FOR GENERATED BINARIES
PRGNAM:
PRGNME:	0		;NAME OF PROGRAM BEING ASSEMBLED
	SIXBIT	/BPT/
	BLOCK	2
UJOB:	0		;JOB NUMBER OF USER JOB
TIME:	0		;MAX TIME
COUNT:	0
CNT:	0		;INITIALIZATION COUNTER
DCNT:	0
PRGCNT:	0
CRFNAM:	0
CRFSCR:	0
CPYSCR:	0
BPTCHR:	0
	IFN	SER4,<
LOGWD:	0
>
	IFN	HDR,<
HMSG:	0
	SIXBIT /PDP-10 BATCH/
	0
HTIME:	BLOCK 	3
	SIXBIT /   ** /
>
NAME:	BLOCK	5	;NAME FROM JOB CARD
	IFN	HDR,<
	SIXBIT  / **   /
	CONAME
HLAST:	0
	SIXBIT /#/
>
OUTBF:	BLOCK	3
SUDEV:	BLOCK	20	;PHYSICAL DEV NAMES
SULST:	BLOCK	20	;CORRESPONDING SYMBOLIC UNITS
PSTRNG:	BLOCK	12		;BUILD COMAND STRING HERE
LDRLST:	BLOCK	40	;LIST OF PROGRAMS TO LOAD AFTER LDR STARTS
LDRDEV:	BLOCK	40		;DEVICES CORRESPONDING TO LST
CPYBUF:	BLOCK	203	;BUFFER FOR COPYING MACRO CARDS
NAMLOC:	SIXBIT	/TEMP00/
FUDGER:	Z


BPEND:	END
   $@