TITLE	ZEROES FOR LIBOL 5(223)		
SUBTTL	CHECK FOR STRING OF ALL ZEROES		P. WILSON/CAM

;COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORP, MAYNARD, MA.

	HISEG

;CALLED WITH A SINGLE PARAMETER --
;	BITS 0-5	BYTE POINTER RESIDUE FOR INPUT FIELD.
;	BIT 6		1 IF AN OPERATIONAL SIGN IS PRESENT.
;	BITS 7-17	SIZE OF THE INPUT FIELD.
;	BITS 18-35	ADDRESS OF FIRST CHARACTER OF INPUT FIELD.
;CALL TO SET.1 PUTS PARAMETER IN AC 13.
;RETURNS:
;	RET.1	IF STRING LENGTH IS ZERO.
;OTHERWISE:
;	RET.1 IF FIELD CONTAINS SOME NON-ZERO CHARACTER.
;	RET.2 IF FIELD CONTAINS ALL ZEROES.
FLDSIZ=0			;TOTAL BYTES IN STRING.
CHAR=1				;CHARACTER CURRENTLY BEING CONSIDERED.
ZONE=2				;HIGH-ORDER BITS (40 OR 00) FOR ASCII/SIXBIT ZEROES.
SIGN=3				;1 IF FIELD IS SIGNED, ZERO OTHERWISE.
POINTR=13			;BYTE POINTER FOR INPUT STRING.
	EXTERNAL	SET.1,RET.1,RET.2
	ENTRY	ZERO.6
	ENTRY	ZERO.7
ZERO.6:	TDZA	ZONE,ZONE	;SIXBIT INPUT, SET ZONE=ZERO.
ZERO.7:	MOVEI	ZONE,40		;ASCII STRING, ZONE=60-20.
	PUSHJ	17,SET.1	;GET PARAMETER.
	LDB	SIGN,SGNPTR	;IS INPUT FIELD SIGNED?
	LDB	FLDSIZ,NBYTES	;FIND TOTAL CHARACTERS IN STRING.
	JUMPE	FLDSIZ,RET.1	;DON'T WORK ON ZERO FIELD SIZE.
	TLZ	POINTR,007777	;CLEAN UP INPUT BYTE POINTER.
	TLO	POINTR,000640(ZONE)  ;PUT BYTE SIZE IN POINTER.
NXTNUM:	JUMPE	FLDSIZ,RET.2	;ALL DONE, EXIT TO CALL+2.
	ILDB	CHAR,POINTR	;MORE TO DO, GET NEXT CHARACTER.
	CAIN	CHAR,20(ZONE)	;SKIP IF CHARACTER .NE. ZERO.
	SOJA	FLDSIZ,NXTNUM	;THIS CHARACTER IS ZERO, TRY NEXT ONE.
;THE FOLLOWING INSTRUCTION WILL SKIP IF THE CURRENT NON-ZERO CHARACTER
;  IS THE FINAL CHARACTER OF THE INPUT STRING (FLDSIZ=1) AND IF THE
;  FIELD IS EXPLICITY SIGNED (SIGN=1).
	CAME	FLDSIZ,SIGN
	JRST	RET.1		;OTHERWISE RETURN.
	CAIE	CHAR,32(ZONE)	;SIGNED NEGATIVE OR
	CAIN	CHAR,37(ZONE)	;  POSITIVE ZERO,
	JRST	RET.2		;  TAKE SUCCESS RETURN.
	JRST	RET.1		;FAILURE OTHERWISE.
SGNPTR:	POINT	1,POINTR,6	;BIT 6 ON MEANS FIELD COULD BE SIGNED.
NBYTES:	POINT	11,POINTR,17	;POINTER TO SIZE FIELD IN PARAMETER.
	END
