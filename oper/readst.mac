TITLE READST - PROGRAM TO READ STATISTICS FILE GENERATED BY GETSTT

P=17
PTR=16
C=15

T1=1
T2=2
T3=3
T4=4
T5=5
T6=6
T7=7

FIL==1
LPT==2

;RECORD DEFINITIONS IN BUFFER

TIMER==0
INCTIM==1
NULLR==2
LOSTR==3
JOBR==4
SIZER==5
ASIZER==6
MSIZER==7
SIZE1R==10
SIZE2R==11
SIZE3R==12
SIZE4R==13
SIZE5R==14
SIZE6R==15
SIZE7R==16
SIZE8R==17
ENTSIZ==20	;NUMBER OF WORDS PER ENTRY

ARRAY PDL[40],BUF[200]
ARRAY BASE,NULL,LOST,JOB,SIZE,ASIZE,MSIZE,SIZE1,SIZE2[^D288]
ARRAY SIZE3,SIZE4,SIZE5,SIZE6,SIZE7,SIZE8[^D288]

MAXHIT==^D46	;HIGHT FOR DISPLAY
LPTWID==^D114
TTYWID==^D66

OPDEF OSTR [2B8]
OPDEF OCHR [1B8]
STPT:	RESET
	INIT FIL,16
	SIXBIT /DSK/
	0
	EXIT
	LOOKUP FIL,NAME
	 JRST NOFIL
	MOVSI T1,-NUMEL
ZERLP:	HRRZ T2,ARRLOC(T1)
	SETZM (T2)
	HRLZ T3,T2
	HRRI T3,1(T2)
	BLT T3,^D287(T2)
	AOBJN T1,ZERLP
	SETZM BASE
	MOVE T1,[XWD BASE,BASE+1]
	BLT T1,BASE+^D287
	SETZM SNREC#	;NO RECORDS SEEN YET
	SETZB C,PTR
	MOVE P,[IOWD 40,PDL]
SETINT:	OUTSTR [ASCIZ /
INTERVAL SIZE (IN MULTIPLES OF 5 MIN) /]
	PUSHJ P,RDNUM
	PUSHJ P,WTLF
	CAIG T2,^D24
	CAIG T2,0
	JRST SETINT
	IMULI T2,5
	MOVEM T2,MINSIZ#
	IMULI T2,^D<60*60>
	MOVEM T2,SIZINT#
	MOVE T1,[^D<24*60*60*60>]
	IDIV T1,T2
	SKIPE T2
	ADDI T1,1
	MOVEM T1,NUMINT#
	MOVNM T1,MNUMNT#
	OUTSTR [ASCIZ /
TYPE DATE AS MM-DD-YY (RETURN FOR TODAY) /]
	PUSHJ P,RDNUM
	JUMPE T2,TODAY
	PUSH P,T2
	PUSHJ P,RDNUM
	EXCH T2,(P)
	PUSH P,T2
	PUSHJ P,RDNUM
	SUBI T2,^D64
	IMULI T2,^D12
	POP P,T1
	ADDI T2,-1(T1)
	IMULI T2,^D31
	POP P,T1
	ADDI T2,-1(T1)
	MOVEM T2,DESDAT#	;THIS IS THE DATE WE WANT
	PUSHJ P,WTLF
	JRST LOOP

TODAY:	DATE T2,
	MOVEM T2,DESDAT
	PUSHJ P,WTLF
	JRST LOOP

WTLF:	CAIN C,12
	POPJ P,
	INCHWL C
	JRST WTLF
RDNUM:	MOVEI T2,0
RDNM1:	CAIN C,15
	POPJ P,
	INCHWL C
	CAIG C,"9"
	CAIGE C,"0"
	JRST RDNM1
RDNM2:	IMULI T2,^D10
	ADDI T2,-"0"(C)
	INCHWL C
	CAIG C,"9"
	CAIGE C,"0"
	POPJ P,
	JRST RDNM2

NAME:	3
	USER
	SIXBIT /USE/
	SIXBIT /DAT/

USER:	SIXBIT /WFW/
	0

NOFIL:	OUTSTR [ASCIZ /CAN NOT ACCESS FILE/]
	EXIT

ARRLOC:	NULL(T1)
	LOST (T1)
	JOB(T1)
	SIZE(T1)
	ASIZE(T1)
	MSIZE(T1)
	SIZE1(T1)
	SIZE2(T1)
	SIZE3(T1)
	SIZE4(T1)
	SIZE5(T1)
	SIZE6(T1)
	SIZE7(T1)
	SIZE8(T1)

NUMEL==.-ARRLOC
DATLOC:	NULLR(PTR)
	LOSTR(PTR)
	JOBR(PTR)
	SIZER(PTR)
	ASIZER(PTR)
	MSIZER(PTR)
	SIZE1R(PTR)
	SIZE2R(PTR)
	SIZE3R(PTR)
	SIZE4R(PTR)
	SIZE5R(PTR)
	SIZE6R(PTR)
	SIZE7R(PTR)
	SIZE8R(PTR)

IFN <.-DATLOC-NUMEL>,<PRINTX NUMBER OF ENTRIES ERROR - DATLOC>

GETREC:	AOBJN PTR,CONBUF
	INPUT FIL,COM
	STATZ FIL,20000
	POPJ P,
	MOVSI PTR,-<200/ENTSIZ>
	HRRI PTR,BUF
	JRST BUFRET

CONBUF:	ADDI PTR,ENTSIZ-1
BUFRET:	SKIPN (PTR)
	JRST GETREC
	AOS (P)
	POPJ P,

COM:	IOWD 200,BUF
	0

MULFCT:	PUSHJ P,TIMSCL
	PUSHJ P,TIMSCL
	IMUL T2,T3
	IMUL T2,T3
	IMUL T2,T3
	IMUL T2,T3
	IMUL T2,T3
	IMUL T2,T3
	IMUL T2,T3
	IMUL T2,T3
	IMUL T2,T3
	IMUL T2,T3
	IMUL T2,T3
	IMUL T2,T3

IFN <.-MULFCT-NUMEL>,<PRINTX NUMBER OF ENTRIES ERROR - MULFCT>

TIMSCL:	IMUL T2,T3
	PUSH P,T3
	IDIV T2,INCTIM(PTR)
	POP P,T3
	POPJ P,
LOOP:	PUSHJ P,GETREC
	 JRST SCALE
	HLRZ T1,TIMER(PTR)
	CAME T1,DESDAT
	JRST LOOP
	SETOM SNREC
	MOVEI T6,0	;TOTAL OF INC TIME USED UP
	HRRZ T1,TIMER(PTR)
	IMULI T1,^D60
	IDIV T1,SIZINT
	MOVE T3,SIZINT
	SUB T3,T2
LOOPRC:	CAMLE T3,INCTIM(PTR)
	MOVE T3,INCTIM(PTR)
	ADDM T3,BASE(T1)
	MOVSI T4,-NUMEL
LOOP2:	MOVE T2,@DATLOC(T4)
	XCT MULFCT(T4)
	ADDM T2,@ARRLOC(T4)
	AOBJN T4,LOOP2
	ADD T6,T3
	ADDI T1,1
	CAML T1,NUMINT
	JRST LOOP
	MOVE T3,INCTIM(PTR)
	SUB T3,T6
	JUMPLE T3,LOOP
	CAMLE T3,SIZINT
	MOVE T3,SIZINT
	JRST LOOPRC


ARRAY MAXARY,ARYSCL[NUMEL],OBUF[3]

SCALE:	SKIPN SNREC
	JRST NOREC
	MOVE T1,[JSR UUO0]
	MOVEM T1,41
	HRLZ T1,MNUMNT
SCALP:	SKIPN BASE(T1)
	JRST NOBASE	;NO ENTRIES THIS PERIOD
	MOVSI T5,-NUMEL
SCALP2:	MOVE T2,@ARRLOC(T5)
	IMUL T2,SCALF(T5)
	IDIV T2,BASE(T1)
	LSH T3,1
	CAML T3,BASE(T1)
	ADDI T2,1		;ROUND
	MOVEM T2,@ARRLOC(T5)
	AOBJN T5,SCALP2
NOBASE:	AOBJN T1,SCALP
	JRST OUTASK
SCLSET:	SETZM MAXARY
	MOVE T1,[XWD MAXARY,MAXARY+1]
	BLT T1,MAXARY+NUMEL-1
	MOVSI T5,-NUMEL
SCLST2:	PUSHJ P,SETSIZ
SCLST1:	MOVE T2,@ARRLOC(T5)
	CAMLE T2,MAXARY(T5)
	MOVEM T2,MAXARY(T5)
	AOBJN T1,SCLST1
	AOBJN T5,SCLST2
	MOVSI T5,-NUMEL
ADJLP:	MOVSI T6,-SCALSZ
ADJLP1:	MOVE T1,SCALA(T6)
	IMUL T1,HITMAX
	CAML T1,MAXARY(T5)
	JRST ADJFND
	AOBJN T6,ADJLP1
	OUTSTR [ASCIZ /SCALE ERROR/]
	EXIT

ADJFND:	MOVEM T6,ARYSCL(T5)
	AOBJN T5,ADJLP
	POPJ P,

SCALF:	REPEAT 2,<^D1000>
	REPEAT 14,<^D10>

IFN <.-SCALF-NUMEL>,<PRINTX NUMBER OF ENTRIES ERROR - SCALF>

NOREC:	OUTSTR [ASCIZ /NO RECORDS FOR DATE/]
	EXIT

SCALA:	1
	5
	^D10
	^D20
	^D50
	^D100
	^D200
	^D500

SCALSZ==.-SCALA

TITL:	[ASCIZ /NULL TIME/]
	[ASCIZ /LOST TIME/]
	[ASCIZ /NUMBER OF JOBS/]
	[ASCIZ /TOTAL SIZE/]
	[ASCIZ /AVERAGE SIZE/]
	[ASCIZ /MEDIAN SIZE/]
	[ASCIZ /JOBS < 8K/]
	[ASCIZ /JOBS < 16K AND >= 8K/]
	[ASCIZ /JOBS < 24K AND >= 16K/]
	[ASCIZ /JOBS < 32K AND >= 24K/]
	[ASCIZ /JOBS < 40K AND >= 32K/]
	[ASCIZ /JOBS < 48K AND >= 40K/]
	[ASCIZ /JOBS < 56K AND >= 48K/]
	[ASCIZ /JOBS >= 56K/]

IFN <.-TITL-NUMEL>,<PRINTX NUMBER OF ENTRIES ERROR - TITL>

	[ASCIZ /ALL TO LPT/]
SETHIT:	MOVEI T2,LPTWID
	SKIPN LPTSW
	MOVEI T2,TTYWID
	SKIPN FORMAT
	MOVEI T2,MAXHIT
	MOVEM T2,HITUSE#
	OUTSTR [ASCIZ /
HEIGHT /]
	PUSHJ P,RDNUM
	PUSHJ P,WTLF
	SKIPN T2
	MOVE T2,HITUSE
	SETZM HITBAS#
	MOVEM T2,HITMAX#
	CAMG T2,HITUSE
	POPJ P,
	OUTSTR [ASCIZ /
BASE /]
	PUSHJ P,RDNUM
	PUSHJ P,WTLF
	MOVEM T2,HITBAS
	ADDM T2,HITUSE
	POPJ P,
UNITT:	[ASCIZ /%/]
	[ASCIZ /%/]
	[ASCIZ / JOB/]
	[ASCIZ / K/]
	[ASCIZ / K/]
	[ASCIZ / K/]
	REPEAT 8,<[ASCIZ / JOB/]>

IFN <.-UNITT-NUMEL>,<PRINTX NUMBER OF ENTRIES ERROR - UNITT>

SCALT:	[ASCIZ /.1/]
	[ASCIZ /.5/]
	[ASCIZ /1/]
	[ASCIZ /2/]
	[ASCIZ /5/]
	[ASCIZ /10/]
	[ASCIZ /20/]
	[ASCIZ /50/]

IFN <.-SCALT-SCALSZ>,<PRINTX NUMBER OF ENTRIES ERROR - SCALT>

OUTASK:	OUTSTR [ASCIZ /
GRAPH NUMBER, RETURN GIVES LIST /]
	SETZM LPTSW#
	PUSHJ P,RDNUM
	PUSHJ P,WTLF
	JUMPG T2,SELGRF
	MOVSI T1,-NUMEL-1
LSTLP:	MOVEI T2,1(T1)
	PUSHJ P,OUTDEC
	OUTSTR [ASCIZ /: /]
	OUTSTR @TITL(T1)
	OUTSTR [ASCIZ /
/]
	AOBJN T1,LSTLP
	JRST OUTASK

OUTDEC:	IDIVI T2,^D10
	HRLM T3,(P)
	SKIPE T2
	PUSHJ P,OUTDEC
	HLRZ T2,(P)
	ADDI T2,"0"
	OCHR T2
	POPJ P,

MONTAB:	[ASCIZ /-JAN-/]
	[ASCIZ /-FEB-/]
	[ASCIZ /-MAR-/]
	[ASCIZ /-APR-/]
	[ASCIZ /-MAY-/]
	[ASCIZ /-JUNE-/]
	[ASCIZ /-JULY-/]
	[ASCIZ /-AUG-/]
	[ASCIZ /-SEPT-/]
	[ASCIZ /-OCT-/]
	[ASCIZ /-NOV-/]
	[ASCIZ /-DEC-/]
SELGRF:	CAILE T2,NUMEL+1
	EXIT
	PUSH P,T2
FRMASK:	OUTSTR [ASCIZ /
FORMAT TYPE /]
	PUSHJ P,RDNUM
	PUSHJ P,WTLF
	CAILE T2,1
	JRST FRMASK
	MOVEM T2,FORMAT#
	JUMPN T2,FRMDON
	SETZM INTBAS#
	MOVE T1,NUMINT
	CAIG T1,^D72
	JRST FRMDON
RDBAS:	OUTSTR [ASCIZ /
BASE TIME /]
	PUSHJ P,RDNUM
	PUSHJ P,WTLF
	CAILE T2,^D2400
	JRST RDBAS
	IDIVI T2,^D100
	IMULI T2,^D60
	ADD T2,T3
	IDIV T2,MINSIZ
	MOVEM T2,INTBAS
FRMDON:	POP P,T2
	CAILE T2,NUMEL
	JRST LPTIT
	MOVEI T6,-1(T2)
	PUSHJ P,SETHIT
	PUSHJ P,GRPPR
	JRST OUTASK

GRPPR:	PUSH P,T6
	PUSHJ P,SCLSET
	POP P,T6
	OCHR [14]
	MOVE T1,DESDAT
	IDIVI T1,^D31
	ADDI T2,1
	PUSHJ P,OUTDEC
	IDIVI T1,^D12
	OSTR @MONTAB(T2)
	MOVEI T2,^D64(T1)
	PUSHJ P,OUTDEC
	OSTR [ASCIZ /
/]
	OSTR @TITL(T6)
	OSTR [ASCIZ /
/]
	OSTR [ASCIZ /1 UNIT = /]
	MOVE T5,ARYSCL(T6)
	OSTR @SCALT(T5)
	OSTR @UNITT(T6)
	OSTR [ASCIZ /

/]
	SKIPE FORMAT
	JRST VFORM
	MOVE T7,ARYSCL(T6)
	MOVE T7,SCALA(T7)
	MOVE T5,HITUSE
	MOVEI C,"X"
OUTLP1:	PUSHJ P,SETSIZ
	MOVEI T4,0
OUTLP2:	MOVE T2,@ARRLOC(T6)
	LSH T2,1
	IDIV T2,T7
	ADDI T2,1
	LSH T2,-1
	CAMG T5,T2
	PUSHJ P,OUTDTA
	ADDI T4,1
	AOBJN T1,OUTLP2
	HRRZ T1,T5
	IDIVI T1,^D10
	JUMPN T2,NODOT
	OCHR [15]
	MOVEI T1,^D72
	OCHR ["."]
	SOJG T1,.-1
NODOT:	OSTR [ASCIZ /
/]
	SUBI T5,1
	CAMLE T5,HITBAS
	JRST OUTLP1
	PUSHJ P,SETSIZ
	MOVEI T4,0
	MOVEI C,"-"
DSHLP:	SKIPN BASE(T1)
	PUSHJ P,OUTDTA
	ADDI T4,1
	AOBJN T1,DSHLP
	OSTR [ASCIZ /
/]
	PUSH P,T6
	MOVEI T6,3	;WHICH DIGIT WE WANT TO PRINT
HORTIM:	PUSHJ P,SETSIZ
	HRRZ T4,T1
	IMUL T4,MINSIZ	;THE TIME SLOT WE START IN
	PUSH P,T4
	ADDI T4,^D59
	IDIVI T4,^D60
	IMULI T4,^D60
	MOVEM T4,TIMNXT#	;THE NEXT TIME SLOT WE WANT TO PRINT
	POP P,T5
HORTM1:	MOVE T2,TIMNXT
	SUB T2,T5	;TIME TO GO
	MOVE T3,T2
	SUB T3,MINSIZ	;TIME TO GO NEXT TIME
	JUMPGE T3,NOHTM	;NOT TIME YET, NEXT TIME IS STILL POS
	JUMPLE T2,HORTM2	;DEFINATELY TIME IF ALREDY OVER
	MOVMS T3
	CAMLE T2,T3	;SEE WHICH IS GREATER INTERVAL
	JRST NOHTM	;THIS TIME IS GREATER
HORTM2:	MOVE T2,T5
	IDIV T2,TIMSC3(T6)
	IDIV T2,TIMSC2(T6)	;GET DIGIT
	ADDI T3,"0"
	OCHR T3
	MOVEI T2,^D60
	ADDM T2,TIMNXT
	SKIPA
NOHTM:	OCHR [" "]
	ADD T5,MINSIZ
	AOBJN T1,HORTM1
	OSTR [ASCIZ /
/]
	SOJGE T6,HORTIM
	POP P,T6
COMPRE:	OSTR [ASCIZ /
/]
	POPJ P,

OUTDTA:	JUMPE T4,NOSPC
	OCHR [" "]
	SOJG T4,.-1
NOSPC:	OCHR C
	AOS (P)
	POPJ P,

TIMSC3:	EXP ^D1,^D10,^D60,^D600

TIMSC2:	EXP ^D10,^D6,^D10,^D10
SETSIZ:	MOVE T1,NUMINT
	SUB T1,INTBAS
	CAILE T1,^D72
	MOVEI T1,^D72
	MOVNS T1
	HRLZS T1
	HRR T1,INTBAS
	POPJ P,

VFORM:	HRLZ T1,MNUMNT
	SETZB T2,TIMNXT
VFRM2:	MOVE T3,TIMNXT
	SUB T3,T2
	MOVE T4,T3
	SUB T4,MINSIZ
	JUMPGE T4,NOVTIM
	JUMPLE T3,VTIM1
	MOVMS T4
	CAMLE T3,T4
	JRST NOVTIM
VTIM1:	PUSH P,T2
	PUSHJ P,OUTDC4
	MOVEI T2,^D60
	ADDM T2,TIMNXT
	POP P,T2
	SKIPA
NOVTIM:	OSTR [ASCIZ /    /]
	ADD T2,MINSIZ
	OCHR [" "]
	SKIPN BASE(T1)
	JRST	[OCHR ["-"]
		JRST VCRLF]
	OCHR [" "]
	MOVE T7,ARYSCL(T6)
	MOVE T7,SCALA(T7)
	MOVE T5,HITBAS
VFRM1:	MOVE T3,@ARRLOC(T6)
	LSH T3,1
	IDIV T3,T7
	ADDI T3,1
	LSH T3,-1
	CAML T5,T3
	JRST VCRLF
	OCHR ["X"]
	ADDI T5,1
	CAMGE T5,HITUSE
	JRST VFRM1
VCRLF:	OCHR [15]
	MOVEI C,12
	SKIPE LPTSW
	MOVEI C,23
	OCHR C
	AOBJN T1,VFRM2
	JRST COMPRE

OUTDC4:	IDIVI T2,^D60
	PUSH P,T3
	PUSHJ P,OUTDC2
	POP P,T2
OUTDC2:	CAIG T2,^D9
	OCHR ["0"]
	JRST OUTDEC
LPTIT:	INIT LPT,0
	SIXBIT /LPT/
	XWD OBUF,0
	JRST NOLPT
        ENTER LPT,LPTTAB
        JRST NOLPT
	SETOM LPTSW
	PUSHJ P,SETHIT
	MOVSI T6,-NUMEL
	PUSHJ P,GRPPR
	AOBJN T6,.-1
	RELEASE LPT,0
	SETZM LPTSW
	JRST OUTASK

NOLPT:	OUTSTR [ASCIZ /
NO LPT/]
	JRST OUTASK

UUO0:	0
	MOVEM P,UUOP#
	LDB P,[POINT 9,40,8]
	CAILE P,2
	JRST UUOERR
	XCT UUOTAB(P)

UUOERR:	OUTSTR [ASCIZ /UUO ERROR/]
	EXIT

UUOTAB:	JRST UUOERR
	JRST UUOCHR
	JRST UUOSTR

UUOCHR:	SKIPN LPTSW
	JRST TTYCHR
	MOVE P,@40
	JSR LPTCHR
UUOXIT:	MOVE P,UUOP
	JRST @UUO0

TTYCHR:	OUTCHR @40
	JRST UUOXIT

UUOSTR:	SKIPN LPTSW
	JRST TTYSTR
	MOVEM T1,UUOT1#
	HRRZ T1,40
	HRLI T1,(POINT 7,0)
UUSTLP:	ILDB P,T1
	JUMPE P,UUSTXT
	JSR LPTCHR
	JRST UUSTLP

UUSTXT:	MOVE T1,UUOT1
	JRST UUOXIT

TTYSTR:	OUTSTR @40
	JRST UUOXIT

LPTCHR:	0
	SOSG OBUF+2
	OUTPUT LPT,0
	IDPB P,OBUF+1
	JRST @LPTCHR


LPTTAB: SIXBIT /READST/
        SIXBIT /DAT/
        0
        0

	END STPT
  