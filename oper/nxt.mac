        TITLE NXTKEY
        ENTRY NXTKEY

        EXTERN %LCHR,%BPTR,%KPTR,%KLIM,%IERR,%HPOS,%HWRD,%DPTR
        EXTERN %HLEN



;  ROUTINE TO ACCESS NEXT KEY IN KEY ARRAY.
;    CALL NXTKEY(ARY,KARY,LEN,HPOINT,IERR)
;  WHERE ARY IS STRING ARRAY THAT HAS KEYS.
; HPOINT- IF HPOINT IS ZERO, HASH CODE OF 1ST KEY WILL BE CALCULATED
;
;  KARY= WILL BE AN ARRAY THAT WILL CONTAIN THE NEXT KEY.
;        KEY WILL BE STORED IN THIS ARRAY IN A5 FORMAT.
; LEN=  THIS WILL BE THE LENGTH, IN CHARACTERS, OF THE KEY.
;       IT WILL = 0  UPON READING THE . (DOT) THAT TERMINATES
;       THE SEQUENCE OF KEYS.  THUS, EACH CALL WILL PRODUCE
;     THE NEXT KEY UNTIL  LEN = 0.

;   IERR=0   MEANS NO PROBLEM
;   IERR=-1  MEANS SCANNING EXCEEDS ARRAY LENGTH ALLOWED FOR KEY


;****REGISTERS

$DPTR=1
$KLEN=2
$BPTR=3
$CHAR=4
$CTR=5
$TOT=6
$TOT2=7   ;THESE MUST BE SEQUENTIAL
$HSW=10


;*****CONSTANTS
DOT=56
SPACE=40
COMA=54


NXTKEY:  0
        SETZB $KLEN,@%IERR               ;HOUSEKEEP
        SETZB $HSW,$TOT         ;INIT HASH SWITCH, TOTAL
        SKIPN @%HPOS            ;DO WE NEED TO CALC HASH?
        JRST GOHASH             ;YES

        MOVE %LCHR              ;NO,SO OTHER KEYS TO PROCESS
        CAIN DOT                ;GET LAST CHAR. IS IT DOT?
        JRST OUT2               ;YES, EARLY OUT

        MOVE $BPTR,%BPTR                ;NOT END OF STRING,GET
        JRST LUPST              ;CURRENT POINTER, START THE SHOW

GOHASH: SETO $HSW               ;HASH SWITCH SET
        MOVE $BPTR,@(16)        ;GET STRT OF STRING
        TLZ $BPTR,77774B32      ;REMOVE ARG
        OR $BPTR,%KPTR          ;MAKE BYTE PTR

LUPST:  MOVEI $CTR,%KLIM        ;MAX # CHAR IN KEY ALLOWED
        MOVE $DPTR,%DPTR               ;GET KEAY ARRAY ADDRESS

NXTCHR: ILDB $CHAR,$BPTR        ;NOW SCAN ALONG

        CAIE $CHAR,DOT          ;CK FOR TERMAINAL CHAR
        CAIN $CHAR,COMA ;
        JRST OUT1

        CAIN $CHAR,SPACE        ;IGNORE SPACES
        JRST COUNT

        ADDI $KLEN,1            ;BUMP LEN BY 1
        IDPB $CHAR,$DPTR        ;DEPOSIT CHAR
        SKIPE $HSW              ;ARE WE HASHING?
        ADD $TOT,$CHAR          ;YES
COUNT:  SOJG $CTR,NXTCHR

        SETOM @%IERR            ;DROP THROUGH IS ERROR
        JRA 16,1(16)            ;REPORT,RETURN

OUT1:   JUMPE $HSW,OUT11        ;HASH?
        IDIV $TOT,%HLEN
        ADDI $TOT2,1            ;YES,DIV BY LENGTH+1
        MOVE $TOT,$TOT2
        LSHC $TOT,-5       ;MODULO 16
        ADDI $TOT2,1
        MOVEM $TOT,@%HPOS
        MOVEM $TOT2,@%HWRD       ;RETURN RESULT
OUT11:  MOVEM $BPTR,%BPTR       ;KEEP BPTR FOR NXT TIME AROUND
        MOVEM $CHAR,%LCHR       ;AND LAST CHAR
OUT2:   MOVEM $KLEN,@%KLEN      ;KEY LENGTH
        JRA 16,1(16)

        END
