C
C     ..................................................................
C
C        SUBROUTINE SICI
C
C        PURPOSE
C           COMPUTES THE SINE AND COSINE INTEGRAL
C
C        USAGE
C           CALL SICI(SI,CI,X)
C
C        DESCRIPTION OF PARAMETERS
C           SI    - THE RESULTANT VALUE SI(X)
C           CI    - THE RESULTANT VALUE CI(X)
C           X     - THE ARGUMENT OF SI(X) AND CI(X)
C
C        REMARKS
C           THE ARGUMENT VALUE REMAINS UNCHANGED
C
C        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
C           NONE
C
C        METHOD
C           DEFINITION
C           SI(X)=INTEGRAL(SIN(T)/T, SUMMED OVER T FROM INFINITY TO X)
C           CI(X)=INTEGRAL(COS(T)/T, SUMMED OVER T FROM INFINITY TO X)
C           EVALUATION
C           REDUCTION OF RANGE USING SYMMETRY.
C           DIFFERENT APPROXIMATIONS ARE USED FOR ABS(X) GREATER
C           THAN 4 AND FOR ABS(X) LESS THAN 4.
C           REFERENCE
C           LUKE AND WIMP, 'POLYNOMIAL APPROXIMATIONS TO INTEGRAL
C           TRANSFORMS',  MATHEMATICAL TABLES AND OTHER AIDS TO
C           COMPUTATION, VOL. 15, 1961, ISSUE 74, PP. 174-178.
C
C     ..................................................................
C
      SUBROUTINE SICI(SI,CI,X)
C
C     TEST ARGUMENT RANGE
C
      Z=ABS(X)
      IF(Z-4.) 10,10,50
C
C     Z IS NOT GREATER THAN 4
C
   10 Y=Z*Z
     0SI=-1.5707963+X*((((((.97942154E-11*Y-.22232633E-8)*Y+.30561233E-6
     1)*Y-.28341460E-4)*Y+.16666582E-2)*Y-.55555547E-1)*Y+1.)
C
C     TEST FOR LOGARITHMIC SINGULARITY
C
      IF(Z) 30,20,30
   20 CI=-1.E38
      RETURN
   300CI=0.57721566+ALOG(Z)-Y*(((((-.13869851E-9*Y+.26945842E-7)*Y-
     1.30952207E-5)*Y+.23146303E-3)*Y-.10416642E-1)*Y+.24999999)
   40 RETURN
C
C     Z IS GREATER THAN 4.
C
   50 SI=SIN(Z)
      Y=COS(Z)
      Z=4./Z
     0U=((((((((.40480690E-2*Z-.022791426)*Z+.055150700)*Z-.072616418)*Z
     1+.049877159)*Z-.33325186E-2)*Z-.023146168)*Z-.11349579E-4)*Z
     2+.062500111)*Z+.25839886E-9
     0V=(((((((((-.0051086993*Z+.028191786)*Z-.065372834)*Z+.079020335)*
     1Z-.044004155)*Z-.0079455563)*Z+.026012930)*Z-.37640003E-3)*Z
     2-.031224178)*Z-.66464406E-6)*Z+.25000000
      CI=Z*(SI*V-Y*U)
      SI=-Z*(SI*U+Y*V)
C
C     TEST FOR NEGATIVE ARGUMENT
C
      IF(X) 60,40,40
C
C     X IS LESS THAN -4.
C
   60 SI=-3.1415927-SI
      RETURN
      END
