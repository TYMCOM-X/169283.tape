C	*****PDP10 BILLING - GBIL2*****
C
C	WRITTEN BY LARRY OLEJNICZAK - MAY 1969
C	UPDATED AND REVISED BY JILL MARCIN FEBRUARY - 1970
C	UPDATED AND REVISED FOR MULTIPLE SITES BY N. LATHAM,9/70
C	UPDATED AND REVISED FOR ADDITIONAL OFF RECORD DATA, N.L.,6/71
C	UPDATED FOR CUPERTINO N.L. 1/72

C	**************************************************
C
C	GBIL2 READS THE FILE 'DAILY.DAT' FROM DSK.
C	IT THEN MATCHES ON'S,OFF'S AND CHK'S SORTS GOOD
C	RECORDS, THEN WRITES THEM OUT TO THE DISK TO
C	'GOOD.DAT' TO BE USED FOR INPUT INTO GBIL3.
C	ERRORS ARE WRITTEN ON DSK,'ERROR.DAT.'

C	**************************************************
C
	SUBROUTINE GBIL2
	EXTERNAL EXIT
	INTEGER QLENGT
	COMMON ITAB(600,26),TAB3(600,7),ISITE,IDATE(6),IACCT(10),
     1 IN(26),RIN(5),ISAVE(26),RSAVE(5),QLENGT,IOFFRC(21)
	DIMENSION ICNTOT(31),TRUTOT(31)

	DIMENSION IZEROS(21)
C
C	*****SET UP ALPHA CONSTANTS
C

C**THIS PARAMETER MUST BE CHANGED IF FIELDS ADDED TO OFF RECORD**
	QLENGT=14+1

	IMAX=600
	IDAT=2HDA
	ION=2HON
	IOFF=2HOF
	ICK=2HHK
	DAILY=5HDAILY
	ERROR=5HERROR
	IEOT=3HZZZ
	IEOT1=2HXX
	IZERO='000'
C
C	*****SET UPT NUMERIC CONSTANTS
C
	IFILL=0
	FILL=0.
	ICOUNT=1
C
C	*****ASSIGN FILES TO CHANNELS
C
	CALL ERRSET(0)
	CALL IFILE(21,DAILY)
	CALL OFILE(22,ERROR)
	CALL OFILE(20,'SPECL')
C
C	*****READ FIRST RECORD FROM INPUT TAPE.
C
	READ(21,9)IDATE(1),ISITE,(IDATE(J),J=2,4)
	IF(ISITE.EQ.'4') GOOD='GOOD4'
	IF(ISITE.EQ.'5') GOOD='GOOD5'
	IF(ISITE.EQ.'3') GOOD='GOOD3'
	IF(ISITE.EQ.'4'.OR.ISITE.EQ.'5' .OR.
     + ISITE.EQ.'3') GO TO 7
	TYPE 20
20	FORMAT(' SITE IN DAILY.DAT IS ILLEGAL. NOTIFY S.Q.A.
     +  RUN ABORTED.')
	STOP

7	CALL OFILE(1,GOOD)
9	FORMAT(A2,A1,I2,1XI2,4XI2)
C
C	*****CHECK TO MAKE SURE THE FIRST WORD IS 'DA'
C	*****IF NOT,STOP.
C
	IF(IDATE(1).EQ.IDAT)GO TO 30
8	TYPE 11
11	FORMAT(1H1,' DATE MISSING ON FILE - START OVER.')
	STOP
6	FORMAT(8A5)

C	*****WRITE DATE RECORDS ON OUTPUT FILE,GOOD.DAT AND SPECL.DAT
C
30	WRITE(1,5),IDATE(1),IDATE(1),IDATE(2),IDATE(1),IDATE(3),IDATE(4),
     1 IDATE(1),IFILL,IFILL,FILL,FILL,IFILL,IDATE(3),IDAT,IDAT,
     2 IDAT,IDAT,IDAT,IDAT,IDAT,IDAT,ISITE
5	FORMAT(1X,2A3,I3,A2,2I2,A2,I6,I3,F11.2,F11.0,I6,I2,8A5,A1)

C**HEADER/DATE RECORD FOR SPECL.DAT
	WRITE(20,500) IZERO,IDAT,ISITE,(IDATE(J),J=2,4),
     1 (IZEROS(J),J=1,QLENGT-1)
500	FORMAT(1XA3,A2,A1,9I12,/,10I12,/,10I12)
C
C	*****READ NEXT REACORD FROM DAILY.DAT
C
40	READ(21,10,ERR=888),IS,JOB,IC,IU,IU1,IT,IT1,
     1 RUNN,RKCT,ISTOR,IDAY
	ITRUEX=RUNN
	IKCTEX=RKCT
10	FORMAT(A2,2I3,2A3,2I2,F11.2,F11.0,I6,I2,8A5)
	GO TO 1113
41	IF(IS.EQ.ICK) GO TO 40
	IF(IS.NE.ION) GO TO 42
	READ(21,6) IACCT
	GO TO 40
42	IF(IS.NE.IOFF) GO TO 40
	READ(21,502) (IOFFRC(K),K=1,QLENGT)
	GO TO 40
888	TYPE 10,IS,JOB,IC,IU,IU1,IT,IT1,
     1 RUNN,RKCT,ISTOR,IDAY
	TYPE 889
889	FORMAT(' ERROR IN ABOVE RECORD. RUN ABORTED.')
	STOP
C
C	*****IF DATE RECORD,BRANCH TO WRITE REMAINING ERRORS
C
1113	IF(ICOUNT.GE.99)GO TO 11111
	IF(IS.EQ.IDAT)GO TO 40
	IF(IS.EQ.IEOT1)GO TO 1000
C
C	*****IF ON,READ NEXT 8 WORDS,IF NOT,FILL WITH BLANKS
C
	IF(IS.NE.ION)GO TO 47
	READ(21,6),(IACCT(J),J=1,8)
	GO TO 46
11111	TYPE 11112,ICOUNT
11112	FORMAT(' THERE ARE ',I6,' RECORDS. CONTACT PROGRAMMER.'//)
	STOP
C
C	*****OFF RECORD
C
47	IF(IS.NE.IOFF) GO TO 501

C***NOW READ SPECIAL OFF FIELDS AND WRITE IN SPECL.DAT FILE
	READ(21,502)(IOFFRC(K),K=1,QLENGT)
502	FORMAT(7I12,/,7I12,/,7I12)

	WRITE(20,503) IU,IU1,ITRUEX,IKCTEX,(IOFFRC(K),K=1,QLENGT)
503	FORMAT(1X2A3,9I12,/,10I12,/,10I12)

C**CONTINUE WITH STANDARD PROCESSING
501	DO 48 J=1,8
48	IACCT(J)=IBLNK
46	IF(ICOUNT-1)49,49,80
C
C	*****IF ONLY 1 RECORD,IF OFF-ERROR,OTHERWISE PUT IN TABLE
C
49	IF(IS.NE.IOFF)GO TO 120
C
C	*****WRITE ERROR RECORD IN ERROR.DAT
C
50	WRITE(22,51)IS,JOB,IC,IU,IU1,IT,IT1,
     1 RUNN,RKCT,ISTOR,IDAY, (IACCT(J),J=1,8),ISITE
51	FORMAT(A2,2I3,2A3,2I2,F11.0,F11.0,I6,I2,8A5,A1)
C
C	*****GO TO READ NEXT RECORD
C
	GO TO 40
C
C	*****SEARCH FOR MATCHING JOB NUMBERS
C
80	INDEX=ICOUNT-1
	DO 110 I=1,INDEX
	IF(ITAB(I,7).EQ.JOB)GO TO 150
110	CONTINUE
C
C	*****NO MATCH!!
C
C	*****IF OFF,ERROR,OTHERWISE,SAVE IN TABLE
C
	IF(IS.EQ.IOFF)GO TO 50
	GO TO 120
C
C	*****IF MATCHING JOB NUMBERS,CHECK TO SEE IF USER NUMBERS MATCH
C
150	IF(ITAB(I,1).EQ.IU.AND.ITAB(I,2).EQ.IU1)GO TO 151
	GO TO 152
151	IF(IDAY.GE.ITAB(I,11))GO TO 180

C
C	******JOB NUMBERS =,USER NUMBERS NOT=.(OR DATES CONFLICT)
C	******GO TO SUBROUTINE TO COMPUT TIMES OR WITE ERROR
C
152	CALL COMP1(I,ICOUNT)
C
C	******IF LAST RECORD READ WAS OFF,ERROR,IF NOT,INSERT IN
C	******TABLE..
C
	IF(IS.EQ.IOFF)GO TO 50
	GO TO 120
C
C	******JOB NUMBERS =, USER NUMBERS =...4 POSSIBILITES.
C	1)  ON-ON COMBINATION OR HK-ON COMBINATION...
C
180	IF(ITAB(I,6).EQ.ION.AND.IS.EQ.ION.OR.
     1    ITAB(I,6).EQ.ICK.AND.IS.EQ.ION)GO TO 201
C
C	2)  ON-HK COMBINATION OR HK-HK COMBINATION...
C
	IF(ITAB(I,6).EQ.ION.AND.IS.EQ.ICK.OR.
     1    ITAB(I,6).EQ.ICK.AND.IS.EQ.ICK)GO TO 205
C
C	3)OF-ON COMBINATION OR OF-HK COMBINATION
C	OR OF-OF COMBINATION.
C
	IF(ITAB(I,6).EQ.IOFF.AND.IS.EQ.ION.OR.
     1    ITAB(I,6).EQ.IOFF.AND.IS.EQ.ICK.OR.
     3 ITAB(I,6).EQ.IOFF.AND.IS.EQ.IOFF)GO TO 210
C
C	4)  ON-OF COMBINATION OR HK-OF COMBINATION
C
	IF(ITAB(I,6).EQ.ION.AND.IS.EQ.IOFF.OR.
     1 ITAB(I,6).EQ.ICK.AND.IS.EQ.IOFF) GO TO 185
	GO TO 186
C	GOOD RECORD..WRITE THEM OUT NOW.....
C	FIRST COMPUTE CONNECT TIME(CHECK FOR GOING PAST MIDNIGHT)
C
185	ITIMB=(ITAB(I,4)*60+ITAB(I,5))+(1440*(ITAB(I,11)-1))
	ITIM=(IT*60+IT1)+(1440*(IDAY-1))
	IF(ITIM.LT.ITIMB)ITIM=ITIM+1440
189	ICTIME=ITIM-ITIMB
	RUNN=RUNN-TAB3(ICOUNT,1)
	IF(RUNN.LT.0.) RUNN=0.
	WRITE(1,5)(ITAB(I,JJJ),JJJ=1,3),ITAB(I,6),
     2IT,IT1,IS,ICTIME,JOB,RUNN,
     1  RKCT,ISTOR,IDAY,(ITAB(I,LLL),LLL=12,19),ISITE
	CALL DELETE(I,ICOUNT)
	GO TO 40
186	TYPE 187
187	FORMAT(1H1,' IMPOSSIBLE COMBINATION.CONTACT PROGRAMMER.'///)
	STOP
C
C	******INSERT NEW RECORD INTO TABLE
C
120	ITAB(ICOUNT,1)=IU
	ITAB(ICOUNT,2)=IU1
	ITAB(ICOUNT,3)=IC
	ITAB(ICOUNT,4)=IT
	ITAB(ICOUNT,5)=IT1
	ITAB(ICOUNT,6)=IS
	ITAB(ICOUNT,7)=JOB
	ITAB(ICOUNT,10)=ISTOR
	ITAB(ICOUNT,11)=IDAY
	DO 121 J=1,8
121	ITAB(ICOUNT,J+11)=IACCT(J)
	TAB3(ICOUNT,1)=RUNN
	TAB3(ICOUNT,2)=RKCT
	ICOUNT=ICOUNT+1
	GO TO 40
C	******IF RECORD IN TABLE IS COMPLETE,WRITE IT OUT,
C	******IF NOT,WRITE AN ERROR AND INSERT NEW RECORD IN TABLE
C
201	CALL COMP1(I,ICOUNT)
	GO TO 120
C
C	******NEW RECORDIS CHKPNT..SAVE WITH OLD
C
205	ITAB(I,20)=IS
	ITAB(I,21)=IT
	ITAB(I,22)=IT1
	ITAB(I,25)=ISTOR
	ITAB(I,26)=IDAY
	TAB3(I,4)=RUNN
	TAB3(I,5)=RKCT
	GO TO 40
C
C	BOTH OLD AND NEW RECORDS ARE ERRORS!!!
C
210	TYPE 211
211	FORMAT(1H1,' THIS COMBINATION OF ON-OFF SHOULD NEVER HAPPEN.'/
     1 ' CONTACT PROGRAMMER.'////)
	STOP
1000	ICOUNT=ICOUNT-1
1002	IF(ICOUNT)1010,1010,1001
1001	CALL COMP1(1,ICOUNT)
	GO TO 1002
1010	WRITE(1,5)IDATE(1),IDATE(1),IDATE(2),IDATE(1),IDATE(3),IDATE(4),
     6 IDATE(1),IFILL,IFILL,FILL,FILL,IFILL,IFILL,
     1 IDAT,IDAT,IDAT,IDAT,IDAT,IDAT,IDAT,IDAT,ISITE
	WRITE(22,51)IDATE(1),(IDATE(J),J=2,3),IDATE(1),IDATE(1),IDATE(4)
     1 ,IFILL,IFILL,IFILL,FILL,FILL,IFILL,IFILL,IFILL,IFILL,IFILL,IFILL,
     2 IFILL,IFILL,IFILL,IFILL,ISITE
11123	END FILE 22
	REWIND 22
C
C	******SET COUNTER =0
C
	END FILE 1
	REWIND 1
	CALL IFILE(1,GOOD)
	K=0
C
C	******SET UP VARIABLES FOR MERGE TAPES
C
	IX=23
	IY=24
C
C	******WRITE EOF ON FIRST MERGE TAPE
C
	WRITE(24,5),IEOT,IEOT,IFILL,IEOT,IFILL,IFILL,IEOT
     1,IFILL,IFILL,FILL,FILL,IFILL,IFILL,IEOT,IEOT,IEOT,IEOT,
     2 IEOT,IEOT,IEOT,IEOT,ISITE
C
C	******REWIND FIRST MERGE TAPE
C
	REWIND 24
C
C	******READ FIRST RECORD AND CHECK TO SEE
C	******IF FIRST WORD IS 'DA'
C	******IF NOT, GO PRINT ERROR MESSAGE AND STOP
C
	READ(1,5)ICHECK
	IF(ICHECK.NE.IDAT)GO TO 8
C
C	******INCREMENT COUNTER AND READ NEXT RECORD
C
1300	K=K+1
	READ(1,5)(ITAB(K,J),J=1,9),TAB3(K,1),TAB3(K,2),
     1 (ITAB(K,JJ),JJ=10,20)
C
C	******IS RECORD A DATE? IF SO, GO TO END ROUTINE,IF NOT CONTINUE
C
	IF(ITAB(K,1).EQ.IDAT)GO TO 1330
C
C	******READ 600 RECORDS OR LESS.
C	******HAVE WE READ 600 RECORDS? IF NOT,KEEP GOING,IF YES,
C	******GO TO SORT AND MERGE RECORDS READ.
C
1320	IF(K.NE.600)GO TO 1300
C
C	******600 RECORDS IN CORE,SO SORT THEM
	CALL ISORT(K)
C
C	******SWITCH MERGE TAPES
C
	IXX=IX
	IX=IY
	IY=IXX
C
C	******NOW MERGE NEW RECORDS WITH OLD ONES.
C
	CALL MERGE(K,IX,IY,IEOT)
C
C	******ZERO OUT COUNTER AND GO READ 600 MORE RECORDS
C
	K=0
	GO TO 1300
C
C	******HAVE READ LAST GROUP OF RECORD.ARE THERE >0 RECORDS?
C	******IF NO,END,IF YES, SORT AND MERGE THEM.
C
1330	K=K-1
	IF(K)1999,1999,1310
1310	CALL ISORT(K)
	IXX=IX
	IX=IY
	IY=IXX
	CALL MERGE(K,IX,IY,IEOT)
1999	IXX=IX
	IX=IY
	IY=IXX
2000	REWIND 1
	CALL OFILE(1,GOOD)
	WRITE(1,5),IDATE(1),IDATE(1),IDATE(2),IDATE(1),IDATE(3),IDATE(4),
     1 IDATE(1),IFILL,IFILL,FILL,FILL,IFILL,IDATE(3),
     3 IDAT,IDAT,IDAT,IDAT, IDAT,IDAT,IDAT,IDAT,ISITE
2001	READ(IX,5)(IN(J),J=1,9),RIN(1),RIN(2),(IN(JJ),JJ=10,20)
	WRITE(1,5)(IN(J),J=1,9),RIN(1),RIN(2),(IN(JJ),JJ=10,20)
	IF(IN(1).NE.IEOT)GO TO 2001
	END FILE 1
	REWIND 1
	DO 5000 J=1,31
	TRUTOT(J)=0.
	ICNTOT(J)=0
5000	CONTINUE
	CALL IFILE(1,GOOD)
	ILCNT=0
	CALL IHEAD(IDATE)
	READ(1,5),IDUM
	IF(IDUM.NE.IDAT)GO TO 8
C
C	******READ DSK FILE AND CHECK FOR END OF FILE
C
3005	READ(1,5)(IN(J),J=1,9),RIN(1),RIN(2),(IN(J),J=10,20)
	IF(IN(1).EQ.IEOT)GO TO 3100
C	:::::START OF LOOP FOR ONE USER NAME********
C
C	******SAVE NEW USER NO. AND ADD AMOUNTS TO TOTALS
3010	IUSTOR=0
	IUS1=IN(1)
	IUS2=IN(2)
	IUS3=IN(20)
	ICN=IN(8)
	TRU=RIN(1)
	IJ=IN(11)
	ICNTOT(IJ)=ICNTOT(IJ)+IN(8)
	TRUTOT(IJ)=TRUTOT(IJ)+RIN(1)
	ICNSTR=0
	IF(IN(10).LT.0.OR.IN(7).EQ.ICK)GO TO 3015
	IUSTOR=IN(10)
	ICNSTR=ICNSTR+1
C
C	******READ NEXT RECORD
C
3015	READ(1,5)(IN(J),J=1,9),RIN(1),RIN(2),(IN(J),J=10,20)
	IJ=IN(11)
C
C	******IS THIS THE SAME USER NO. AS BEFORE?IF SO,
C	******ADD AMOUNTS TO TOTALS...IF NOT,PRINT LINE,
	IF(IN(1).NE.IUS1.OR.IN(2).NE.IUS2.OR.
     1 IN(20) .NE. IUS3)GO TO 3050
C
C	******USER NOS. MATCH!
C
	ICN=ICN+IN(8)
	TRU=TRU+RIN(1)
	ICNTOT(IJ)=ICNTOT(IJ)+IN(8)
	TRUTOT(IJ)=TRUTOT(IJ)+RIN(1)
	IF(IN(10).LT.0.OR.IN(7).EQ.ICK)GO TO 3015
3016	IUSTOR=IUSTOR+IN(10)
	ICNSTR=ICNSTR+1
	GO TO 3015
3049	STOR=0
	GO TO 3053
C
C	******USER NOS. DO NOT MATCH!PRINT LINE...
C
3050	IF(ICNSTR.EQ.0)GO TO 3049
	STOR=IUSTOR/ICNSTR
3053	PRINT 3051,IUS1,IUS2,ICN,TRU,STOR
3051	FORMAT(9X,2A3,6XI6,2XF11.0,2XF10.0)
C
	ICNSTR=0
C
C	******INCREMENT NUMBER OF LINED PRINTED. IF>51,SLEW PAGE 
C	******AND PRINT HEADING
C
	ILCNT=ILCNT+1
	IF(IN(1).EQ.IEOT) GO TO 3100
	IF(ILCNT.LT.49)GO TO 3010
	CALL IHEAD(IDATE)
	ILCNT=1
C
3100	END FILE 24
3101	PRINT 3102
3102	FORMAT(1H1,T25,'****TOTALS****',//,' DATE',3X,'CONNECT TIME(HRS)',
     + 3X,'TRU',/)
	DO 5005 IJ=1,31
	IF(ICNTOT(IJ).EQ.0.AND.TRUTOT(IJ).EQ.0.) GO TO 5005
	ICNTOT(IJ)=ICNTOT(IJ)/60
5002	PRINT 5006,IJ,ICNTOT(IJ),TRUTOT(IJ)
5006	FORMAT(3X,I2,T12,I9,3XF6.0)
5005	CONTINUE
	PRINT 5010
5010	FORMAT(///)
	IEND1=99999
	IEND2=0
	END1=0
	CALL IFILE(22,ERROR)
	READ(22,51)(IN(J),J=1,7),RIN(1),RIN(2),(IN(JJ),JJ=10,20)
3199	IF(IN(1).EQ.IDAT)GO TO 9999
	PRINT 3200,(IDATE(J),J=2,4),ISITE
3200	FORMAT(1H1,9X,'ERROR RECORDS   ',1XI2,1H/,I2,1H/,I2,
     1 ' ON SITE ',A1//,T24,'TIME',9X'TRU',/)
3201	PRINT 3202,(IN(J),J=1,7),RIN(1),RIN(2),(IN(JJ),JJ=10,20)
3202	FORMAT(1XA2,2(2XI3),2X,2A3,2X,2I2,2F11.0,I6,3XI2,1X,8A5,A1)
	READ(22,51)(IN(J),J=1,7),RIN(1),RIN(2),(IN(JJ),JJ=10,20)
	IF(IN(1).NE.IDAT)GO TO 3201


9999	END FILE 3
	CALL RENAME('FOR23','DAT',0,0,0,IERR)
	CALL RENAME('FOR24','DAT',0,0,0,IERR)
	END FILE 20
	RETURN
	END



	SUBROUTINE COMP1(I,ICOUNT)
C***WRITE ERROR RECORDS IN ERROR.DAT
	COMMON ITAB(600,26),TAB3(600,7),ISITE
	IZERO=0
	IF(ITAB(I,20).NE.IZERO)GO TO 10
5	WRITE(22,12)ITAB(I,6),ITAB(I,7),ITAB(I,3),ITAB(I,1),ITAB(I,2),
     1 ITAB(I,4),ITAB(I,5),(TAB3(I,JKL),JKL=1,2),
     2(ITAB(I,KK),KK=10,19),ISITE
12	FORMAT(A2,2I3,2A3,2I2,F11.0,F11.0,I6,I2,8A5,A1)
	CALL DELETE(I,ICOUNT)
	RETURN
10	CALL COMP2(I,ICOUNT)
	RETURN
	STOP
	END



	SUBROUTINE COMP2(I,ICOUNT)
C**WRITE GOOD RECORDS IN GOOD.DAT
	COMMON ITAB(600,26),TAB3(600,7),ISITE
	IZERO=0
	ITIMB=(ITAB(I,4)*60.+ITAB(I,5))+(1440*(ITAB(I,11)-1))
	ITIM=ITAB(I,21)*60.+ITAB(I,22)+(1440*(ITAB(I,26)-1))
	IF(ITIM.LT.ITIMB)ITIM=ITIM+1440
	ICTIME=ITIM-ITIMB
	RUNN=TAB3(I,4)-TAB3(I,1)
	IF(RUNN.LT.0.) RUNN=0.
	WRITE(1,3)(ITAB(I,J),J=1,3),ITAB(I,6),
     2 ITAB(I,21),ITAB(I,22),ITAB(I,20),
     1 ICTIME,ITAB(I,7),RUNN,IZERO,ITAB(I,25),ITAB(I,26),
     3 (ITAB(I,LL),LL=12,19),ISITE
3	FORMAT(1X,2A3,I3,A2,2I2,A2,I6,I3,F11.2,F11.0,I6,I2,8A5,A1)
	CALL DELETE(I,ICOUNT)
	RETURN
	STOP
	END



	SUBROUTINE DELETE(N,ICOUNT)
	COMMON ITAB(600,26),TAB3(600,7),ISITE
	I1=ICOUNT-1
	DO 10J=N,I1
	DO 5 I=1,26
5	ITAB(J,I)=ITAB(J+1,I)
	DO 15 JJ=1,7
15	TAB3(J,JJ)=TAB3(J+1,JJ)
10	CONTINUE
	DO 20 J=1,26
20	ITAB(ICOUNT,J)=0
	DO 25 JJ=1,7
25	TAB3(ICOUNT,JJ)=0.
	ICOUNT=ICOUNT-1
	RETURN
	STOP
	END



	SUBROUTINE ISORT(K)
	COMMON IT(600,26),RIT(600,7),ISITE,IDATE(6),IACCT(10),
     1 IN(26),RIN(5),ISAVE(26),RSAVE(5)
C
C	******SORT RECORDS IN USER NO.SEQUENCE.
C	******FIND LOWEST USER NO.
C
	DO 100 M=1,K
	ID=IT(M,1)
	ID1=IT(M,2)
	IPOINT=M
	DO 90 J=M,K
	IF(ID-IT(J,1))90,80,70
80	IF(ID1-IT(J,2))90,75,70
75	IF(IT(IPOINT,11)-IT(J,11))90,76,70
76	IF(IT(IPOINT,5)-IT(J,5))90,77,70
77	IF(IT(IPOINT,6)-IT(J,6))90,70,70
70	ID=IT(J,1)
	ID1=IT(J,2)
	IPOINT=J
90	CONTINUE
C
C	******SAVE LOWEST USER NUMBER
C
	DO 95 L=1,26
	ISAVE(L)=IT(IPOINT,L)
95	CONTINUE
	RSAVE(1)=RIT(IPOINT,1)
	RSAVE(2)=RIT(IPOINT,2)
C
C	******SQUEEZE LOWEST RECORD OUT OF TABLE AND MOVE TO TOP.
C	******IF ALLREADY AT TOP,NO NEED TO SQUEEZE
C
94	IF(IPOINT.EQ.M)GO TO 98
96	DO 97 L=1,26
97	IT(IPOINT,L)=IT(IPOINT-1,L)
	RIT(IPOINT,1)=RIT(IPOINT-1,1)
	RIT(IPOINT,2)=RIT(IPOINT-1,2)
	IPOINT=IPOINT-1
	GO TO 94
C
C	******MOVE LOWEST RECORD TO TOP OF ARRAY
C
98	DO 99	L=1,20
99	IT(M,L)=ISAVE(L)
	RIT(M,1)=RSAVE(1)
	RIT(M,2)=RSAVE(2)
100	CONTINUE
	RETURN
	STOP
	END



	SUBROUTINE MERGE(K,IX,IY,IEOT)
	COMMON IT(600,26),RIT(600,7),ISITE,IDATE(6),IACCT(10),
     1 IN(26),RIN(5),ISAVE(26),RSAVE(5)
	FILL=0.
	IFILL=0
	K1=1
9	READ(IX,5)(IN(J),J=1,9),RIN(1),RIN(2),(IN(JJ),JJ=10,20)
5	FORMAT(1X,2A3,I3,A2,2I2,A2,I6,I3,F11.2,F11.0,I6,I2,8A5,A1)
C
C	******TEST FOR LAST RECORD
C
	IF(IN(1).EQ.IEOT)GO TO 15
C
C	******NOT LAST RECORD,SO CHECK CORE FOR EOT
C
10	IF(K-K1)11,12,12
C
C	******NO MORE IN CORE,WRITE RECORDS FROM TAPE
C
11	WRITE(IY,5)(IN(J),J=1,9),RIN(1),RIN(2),(IN(JJ),JJ=10,20)
	GO TO 9
C
C	******MORE IN CORE,PROCESS THESE,OLDEST FIRST
C
12	IF(IN(1)-IT(K1,1))140,110,130
C	**TAPE****CORE***LESS*=*GREATER
110	IF(IN(2)-IT(K1,2))140,145,130
145	IF(IN(20)-IT(K1,20)) 140,150,130
150	IF(IN(11)-IT(K1,11)) 140,160,130
160	IF(IN(5)-IT(K1,5)) 140,170,130
170	IF(IN(6).LT.IT(K1,6)) GO TO 140
C
130	WRITE(IY,5)(IT(K1,J),J=1,9),RIT(K1,1),RIT(K1,2),
     1 (IT(K1,JJ),JJ=10,20)
C
C	******MOVE POINTER TO NEXT CORE RECORD AND RETURN FOR NEXT
C
	K1=K1+1
	GO TO 10
C
C
140	WRITE(IY,5)(IN(J),J=1,9),RIN(1),RIN(2),(IN(JJ),JJ=10,20)
	GO TO 9
C
C	******KEEP COUNT OF RECORDS IN CORE
C	******IF LAST RECORD ON TAPE IX,
C	******THEN SEE IF ANY MORE IN CORE
C
15	IF(K-K1)35,20,20
C
C	******YES,WRITE THEM OUT
C
20	DO 30 I=K1,K
30	WRITE(IY,5)(IT(I,J),J=1,9),RIT(I,1),RIT(I,2),
     1 (IT(I,JJ),JJ=10,20)
C
C	******WRITE EOF ON TYPE IY
C
35	WRITE(IY,5)IEOT,IEOT,IFILL,IEOT,IFILL,IFILL,IEOT,IFILL,IFILL,
     1FILL,FILL,IFILL,IFILL,IEOT,IEOT,IEOT,IEOT,IEOT,IEOT,IEOT,
     1 IEOT,ISITE
	END FILE IY
	REWIND IY
	REWIND IX
	RETURN
	END



	SUBROUTINE IHEAD(ISUB)
	COMMON ITAB(600,26),TAB3(600,7),ISITE
	DIMENSION ISUB(6)
	PRINT 1,ISUB(2),ISUB(3),ISUB(4),ISITE
1	FORMAT(1H1,9X,'PDP-10 USAGE ',I2,1H/,I2,1H/,I2,' ON SITE ',A2//
     1 7X'ID NUMBER',5X,'CONNECT',6X,'TRU',6X,'AVERAGE',/
     2 T23,'TIME',T44,'STORAGE',/
     3 T23,'(MIN)',//)
	RETURN
	END



   