*OPS2          ON-LINE PERIPHERAL SERVICE
*              MAY 8, 1973      V.E. VAN VLEAR

*      IF 1 ASSEMBLES FOR DDT SYM

N620   E 0     NO. OF 620
EIR    E 100177   ENABLE INTERRUPTS
DIR    E 100077 DISABLE INTERRUPTS
SZMBF  E 30    SIZE OF MSG BUFFERS
KYWA   E 164   KEY WORD 10 ADR.
AMBFP  E 165   ADR. OF MSG. BUFFER PTR.
HICNTV E 60000  HIGH IDLE COUNT VECTOR (SEE HICNT)

*SPECIAL CHAR. SYMBOLS
SOHC   E 200   START OF HEADING
STXC   E 100   START OF TEXT
ETXC   E 300   END OF TEXT
DLEC   E 10    DATA-LINK ESCAPE
ITBC   E 370   ENT INTERMEDIATE XMSN BLK
ETBC   E 144   END XMSN BLK
ENQC   E 264   ENQUIRY
SYNC   E 114
EOTC   E 354   END OF XMSN
NAKC   E 274   NEG. ACKNOWLEDGMENT
ACK1   E 206   ACK 1
CMAC   E 326   COMMA FOR WACK
ACK0   E 16    ACK 0
EMC    E 230   END OF MEDIA (SHORT RECORD)


BUF0   E 11000
BUF1   E BUF0+454
BUF2   E BUF1+454
BUF3   E BUF2+454
BUF4   E BUF3+454
BUF5   E BUF4+454
BUF6   E BUF5+454
BUF7   E BUF6+454
EBUF   E BUF7+454

       R 0
       +DIR
       LDA K17
IL     OAR 77; DAR; JAP IL
       JMP IZ     INITIALIZE DIM

       R 10
       JMPM POFF    POWER GOING OFF
       JMPM PON
       R 14
       JMPM DUMMY
       JMPM DUMMY
       R 20
       JMPM I330
       JMPM DUMMY; JMPM DUMMY; JMPM DUMMY; JMPM DUMMY; JMPM DUMMY
       JMPM DUMMY
       R 40
       JMPM IS0; JMPM IR0
       JMPM IS1; JMPM IR1
       JMPM IS2; JMPM IR2
       JMPM IS3; JMPM IR3
       JMPM IS4; JMPM IR4
       JMPM IS5; JMPM IR5
       JMPM IS6; JMPM IR6
       JMPM IS7; JMPM IR7

       R 100   CELLS THAT SOMETIMES CONTAIN INDIRECT ADDRESSES FOLLOW
BP     E *; R *10
IP     0
JT     0

JPTR   +PTR1

TP0    +TB     INPUT INTERRUPT BFR POINTER
TP1    +TB     CHASES TP0. FEEDS CHARACTERS TO PROGRAMS.
TP2    +TB     PUT OUTPUT CHRS INTO BFR.
TP3    +TB     INTERRUPT POINTER. CHASES TP2.
ISWTCH +TI1
OSWTCH +TO1
PLV    0
IMWA   0
IMWB   0
OMWA   0
OMWB   0
ISAVP   +BUF4
OSAVP   +BUF7


*      END OF INDIRECTS
JOBL   E *; R *10
LJOBL  E *; R *10
ACKC   E *; R *10
ACKC1  E *; R *10
PWD    E *; R *10
WD     E *; R *10
CH     E *; R *10
CCH    E *; R *10
CRC    E *; R *10
SWSR   E *; R *10
SWCH   E *; R *10
SWWD   E *; R *10
SVEX   E *; R *10
SVEX1  E *; R *10
SVEN   E *; R *10
INS1   E *; R *10
INS2   E *; R *10
SCHB   E *; R *10
STATE  E *; R *10
BFADA  E *; R *10
BFADB  E *; R *10
ERCNT  E *; R *10
SWBF   E *; R *10
SWIGCS E *; R *10
CSERCT E *; R *10
ERRCM  E *; R *10
BUFK   +BUF0; +BUF1; +BUF2; +BUF3; +BUF4; +BUF5; +BUF6; +BUF7; +EBUF
RSBIT  +1; +2; +4; +10; +20; +40; +100; +200
CSBIT  E RSBIT
COBIT  +400; +1000; +2000; +4000; +10000; +20000; +40000; +100000
ECBIT  E RSBIT
ENBIT  E COBIT
CPBIT  E RSBIT


TABLE  E *
0;100005;100017;12;100033;36;24;100021;100063;66;74;100071;50;100055;100047;42
100143;146;154;100151;170;100175;100167;162;120;100125;100137;132;100113;116;104;100101
100303;306;314;100311;330;100335;100327;322;360;100365;100377;372;100353;356;344;100341
240;100245;100257;252;100273;276;264;100261;100223;226;234;100231;210;100215;100207;202
100603;606;614;100611;630;100635;100627;622;660;100665;100677;672;100653;656;644;100641
740;100745;100757;752;100773;776;764;100761;100723;726;734;100731;710;100715;100707;702
500;100505;100517;512;100533;536;524;100521;100563;566;574;100571;550;100555;100547;542
100443;446;454;100451;470;100475;100467;462;420;100425;100437;432;100413;416;404;100401
101403;1406;1414;101411;1430;101435;101427;1422;1460;101465;101477;1472;101453;1456;1444;101441
1540;101545;101557;1552;101573;1576;1564;101561;101523;1526;1534;101531;1510;101515;101507;1502
1700;101705;101717;1712;101733;1736;1724;101721;101763;1766;1774;101771;1750;101755;101747;1742
101643;1646;1654;101651;1670;101675;101667;1662;1620;101625;101637;1632;101613;1616;1604;101601
1200;101205;101217;1212;101233;1236;1224;101221;101263;1266;1274;101271;1250;101255;101247;1242
101343;1346;1354;101351;1370;101375;101367;1362;1320;101325;101337;1332;101313;1316;1304;101301
101103;1106;1114;101111;1130;101135;101127;1122;1160;101165;101177;1172;101153;1156;1144;101141
1040;101045;101057;1052;101073;1076;1064;101061;101023;1026;1034;101031;1010;101015;101007;1002

RSMSK  0
ECMSK  0
SWNJB  0
SWNRN  0
SWLJB  0
SWIDLE 0
HICNT  +HICNTV
IMWP   0
IMBFA  0
IMWAH  0
OMWP   0
OMBFA  0
EOMBFA 0
OMWAH  0
JOB    0
IJOB   0
JB     0
SVA    0
SVB    0
SVX    0
LSVA   0
LSVB   0
LSVP   0
SYS    0
CRA    0
CRB    0
CRX    0
CRM    0
TIC    0
TOC    0
IA     0
IB     0
IX     0
PLWC   0
TI9    0
TO9    0
CI9    0
CO9    0
ECHO   1
TASK   0; 0; 0; 0; -1
OPEN   0
VALC   0
ADR    0
NUM    0
TB     E *     TTY BFRS. INPUT ON LEFT, OUTPUT ON RIGHT.
       0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0
       0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0
       0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0
       0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0; 0 (TB+77)
ETB    E *


        L *+165

I330   0; JMP I330A
POFF   0; 0   POWER GOING OFF JUST HALT
PON    0; JMP IZ    POWER ON GO INITIALIZE
H1     +DIR; STA HA
       LDA H; IAR; INR JPTR; STA JPTR,7
       ADD Y77777; STA H
       LDA JPTR; SUB YEPTR1; JAPM CRASH
       LDA HA
HEXC   +EIR
HRET   1000 (JMP)
H      0; JMP H1
HA     0
K17    17
Y77777 77777
YEPTR1 +EPTR1

L1     +DIR; INR JPTR,7
L      E *
       +DIR; STB LB; LDB JPTR,7
L10    STB H; LDB JPTR; DBR; STB JPTR; LDB LB
       XEC HEXC   USUALLY ENABLES INTERRUPTS
       JMP HRET

LI     E *
       +DIR; STB LB; LDB JPTR,7; LDB 0,6; JMP L10
LB     0

XDIR   0; JMPM XDUM; +DIR
XEIR   0; JMPM XDUM
VHEXC  +EIR
XDUM   0; +DIR; STA XE1
       LDA XDUM; ADD Y77775; STA XMARK
XAB    LLRL 20; LDB 3,6; STB HEXC; TAB
       LDA XE1; XEC .XDUM
       1000 JMP
XMARK  +.XMARK
Y77775 100000-3
XE1    0
PTR1   100000+PTR1; R *20
EPTR1  E *



PURE   E *      BEGINNING OF PURE (READ-ONLY) MEMORY
CRASH  0; JMP 0

DUMMY  0;0

STBTK1 STB TASK+1
I330A  E *     CALLED 330 TIMES PER SECOND
       JMPM XDIR
       STA IA; STB IB; STX IX
       JMP .ISWTCH
I330B  E *
       LDA IA; LDB IB; LDX IX
       JMPM XEIR; JMP .I330

TI1    CIA 60; LRLA 17; JAN .OSWTCH (NOT A START BAUD);
       LDA YITAB; STA ISWTCH; LDA =1000; STA TI9; JMP .OSWTCH
TI2    INR ISWTCH; JMP .OSWTCH
TI3    LDB TI9; CIA 60; LLSR 1; TBA; LRLA 17; JAN TI4 (NO MORE INFO BITS);
       STB TI9; LDA YITAB1; STA ISWTCH; JMP .OSWTCH
TI4    LDA TIC; SUB =ETB-TB; JAP TI5 (BFR FULL); INR TIC
       LLRL 21; ANA =-400; ORA =.   CHR WITH PARITY BIT ON IN LEFT OF A
       ERA TP0,7; ANA =-400; ERA TP0,7; STA TP0,7  STORE CHR IN BFR
       LSRA 10; ERA =223   (CONTROL S); JAZ IZ2 
       INR TP0; LDA TP0; SUB =ETB; JAN TI5; LDA =TB; STA TP0
TI5    LDA YTI1; STA ISWTCH; JMP .OSWTCH

TO4    LDA YTO1; STA OSWTCH
       LDA TOC; DAR; STA TOC
       INR TP3; LDA TP3; SUB =ETB; JAN TO1; LDA =TB; STA TP3
TO1    LDA TOC; JAZ I330B (BFR EMPTY)
       LDA TP3,7; ANA =377; ORA =1400; STA TO9
       OME 60,*; LDA YOTAB; STA OSWTCH; JMP I330B
TO2    INR OSWTCH; JMP I330B
TO3    LDA TO9; JAZ TO4
       OAR 60; LSRA 1; STA TO9
       LDA YOTAB; STA OSWTCH; JMP I330B

CI     E *     GET AN INPUT CHR OUT OF TB
       LDA TIC; JAZ LI (BFR EMPTY);
       LDA TP1,7; LSRA 10; STA CI9
       LDB ECHO; JBZ CI1; JMPM H; +CO; +LI
       LDA CI9; ERA =215 (CAR.RET.); JAZ CI2
       ERA =7; JAZ CI3 (LINE FEED);
CI1    INC ,B; JMPM H; +TRING
       LDA CI9; LDB TIC; DBR; STB TIC; JMP L1
CI2    LDA =212; JMPM H; +CO; +LI; JMP CI1
CI3    LDA =215; JMP CI2+1

CO     E *     PUT A CHR INTO OUTPUT BFR
       ORA =200 (PARITY); TAB
       LDA TOC; SUB =ETB-TB; JAP *-2 (WAIT FOR ROOM IN THE BUFFER)
       LDA TP2,7; ANA =-400; COP AB,B; STB TP2,7
       LDB =2; JMPM H; +TRING
       INR TOC; JMP L1

TRING  E *     INCR. TTY BFR POINTER
       INR TP0,6; LDA TP0,6; SUB =ETB; JAN L; LDA =TB; STA TP0,6; JMP  L

ITAB   +TI2; +TI2; +TI2; +TI3
YTI1   +TI1
YITAB  +ITAB.
YITAB1 1+ITAB.
OTAB   +TO2; +TO2; +TO3
YTO1   +TO1
YOTAB  +OTAB.
YOTAB1 1+OTAB.


CRTYP  LDA =215 (CAR.RET.); JMPM H; +CO; 1+*
       LDA =212 (LIN.FEED); JMPM H; +CO; 1+*; JMP L



IZ     E *     INITIALIZE SYSTEM
       JMPM XDIR

IZ2    TZA; STA TIC; STA TOC
       LDA =TI1; STA ISWTCH; LDA =TO1; STA OSWTCH
       LDA =TB; STA TP0; STA TP1; STA TP2; STA TP3
       COM ,A; STA ECHO
       LDA =EIR; STA HEXC; STA VHEXC
       JMPM XEIR
       JMPM H; +CRTYP; JMPM H; +CRTYP

        LDB CRASH; JBZ RST; JMPM TON
        CLB; STB CRASH
RST    JMPM H; +CRTYP
RST0   TZX; STX OPEN
RST1   TZX; STX VALC; STX NUM
CR     E *     COMMAND RECOGNITION
       LDX =TASK-1
       IXR; LDA 0,5; JAZ *-2   LOOK FOR TASKS TO DO
       STA JT; TZB; STB 0,5   REMOVE VECTOR FOR TASK
       +EIR
       JAP .JT; STA 0,5   (END OF LIST CONTAINS A -1)

CR4    JMPM H; +CI; +CR
       SUB =260 (ZERO); JAP CRPNM (POSSIBLE NO.)
CR6    LDA CI9 (SAVED CHAR.)
       TAB; ERA =257 (SLASH); JAZ CRSL
       TBA; ERA =207 (BELL); JAZ START
       TBA; ERA =212 (LN.FD); JAZ CRLF
       TBA; ERA =215 (C.R. ); JAZ CRCR
       TBA; ERA =336 (UP AR); JAZ CRUA
        TBA; ERA =27B (EQU); JAZ CREQ
        TBA; ERA =256 (DOT); JAZ CRDOT
        TBA; ERA =303 (C); JAZ CRCCH
        TBA; ERA =311 (I); JAZ CRI
        TBA; ERA =312 (J); JAZ CRJ
        TBA; ERA =313 (K); JAZ CRK
        TBA; ERA =314 (L); JAZ CRL
        TBA; ERA =317 (O); JAZ CRO
        TBA; ERA =320 (P); JAZ CRP
        TBA; ERA =322 (R); JAZ RESTRT
        TBA; ERA =323 (S); JAZ CRS
        TBA; ERA =327 (W); JAZ CRW
       JMP RST (IGNORE AND RESTORE ON OTHERS)

CRPNM  TAB; SUB =10; JAP CR6 (NO NO.)
       LRLB 15; LDA NUM; LLRL 3
CRNM    STA NUM; INR VALC (GOT SOME NO.)
       JMP CR

CRSL   INR OPEN; LDA VALC; JAZ CRSL1 (NO PRECEDING NO.)
       LDA NUM
CRSL2  STA ADR
CRSL2A LDA =240 (SPACE); JMPM H; +CO; 1+*
       LDX ADR; LDB 0,5; JMPM TON (TYPE OCTAL)
       LDA =240; JMPM H; +CO; 1+*; JMP RST1
CRSL1  LDB ADR; LDA 0,6; JMP CRSL2

CRCR   JMPM CRCHD; JMP RST0

CRLF   LDA =377; JMPM H; +CO; 1+*; JMPM CRCHD; IBR; JMP CRTA
CRUA   JMPM H; +CRTYP; JMPM CRCHD; DBR
CRTA   STB ADR; JMPM TON
       LDA =257; JMPM H; +CO; 1+*; JMP CRSL2A
CRCHD  0; LDB ADR; LDA VALC; JAZ .CRCHD (NO NO.)
       LDA NUM; STA 0,6; JMP .CRCHD
CREQ    LDB ADR; LDA VALC; JAZ CREQ1
        LDB NUM
CREQ    JMPM TON; JMP CR
CRDOT   LDA ADR
TP      ADD NUM; JMP CRNM
CRJ     LDA =JOB; JMP TP
CRK     LDA =BUFK; JMP TP
CRL     LDA =JOBL; JMP TP
CRS     LDA =STATE; JMP TP
CRCCH   LDA =CH; JMP TP
CRW     LDA =WD; JMP TP
CRP     LDA =PWD; JMP TP
CRI     LDA =ISAVP; JMP TP
CRO     LDA =OSAVP; JMP TP

*      TYPE OCTAL NO.
*      REQUIRES NO. IN B
TON    0; LDX =6; STX TONCT
       TZA; LLRL 1; JAZ TON1
TON2   STB TONSV; ADD =260; JMPM H; +CO; 1+*
       LDA TONCT; DAR; JAZ .TON
       STA TONCT; LDB TONSV; TZA; LLRL 3; JMP TON2
TON1   LDA TONCT; DAR; JAZ TON2
       STA TONCT; TZA; LLRL 3; JAZ TON1; JMP TON2
TONCT  0
TONSV  0

IR0    0; STX SVX; STA SVA; LDA IR0; TZX; JMP IRC
IR1    0; STX SVX; STA SVA; LDA IR1; INC ,X; JMP IRC
IR2    0; STX SVX; STA SVA; LDA IR2; LDX =2; JMP IRC
IR3    0; STX SVX; STA SVA; LDA IR3; LDX =3; JMP IRC
IR4    0; STX SVX; STA SVA; LDA IR4; LDX =4; JMP IRC
IR5    0; STX SVX; STA SVA; LDA IR5; LDX =5; JMP IRC
IR6    0; STX SVX; STA SVA; LDA IR6; LDX =6; JMP IRC
IR7    0; STX SVX; STA SVA; LDA IR7; LDX =7

****   RECEIVE WORD FROM REMOTE INTERRUPT

IRC    STB SVB; STA IP; CIB 76; LDA SWSR,5; JAN IRC1  NOW SENDING
       LDA SWWD,5; JAN IRCE
       STB WD,5; DEC ,A; STA SWWD,5; LDA SWNJB; JAP IRC1 (A RUNNING JOB)
ICM    LDA SWLJB; JAZ ILJB
ICM1   EXC 277; JMP LP1A
ILJB   STX IJOB; LDX JOB; LDA SVA; STA LSVA
       LDA SVB; STA LSVB; LDA IP; STA LSVP
       INR SWLJB; LDX IJOB; JMP ICM1
IRC1   LDA SVA; LDB SVB; LDX SVX; EXC 277; JMP .IP
IRCE   0

IS0    0; STX SVX; STA SVA; LDA IS0; TZX; JMP ISN
IS1    0; STX SVX; STA SVA; LDA IS1; INC ,X; JMP ISN
IS2    0; STX SVX; STA SVA; LDA IS2; LDX =2; JMP ISN
IS3    0; STX SVX; STA SVA; LDA IS3; LDX =3; JMP ISN
IS4    0; STX SVX; STA SVA; LDA IS4; LDX =4; JMP ISN
IS5    0; STX SVX; STA SVA; LDA IS5; LDX =5; JMP ISN
IS6    0; STX SVX; STA SVA; LDA IS6; LDX =6; JMP ISN
IS7    0; STX SVX; STA SVA; LDA IS7; LDX =7

*      SEND WORD TO REMOTE INTERRUPT

ISN    STB SVB; STA IP; LDA SWSR,5; JAZ ISN1
       LDA SWWD,5; JAN ISN2
ISNE   0
ISN1   DEC ,A; OAR 76; JMP IRC1
ISN2   TZA; STA SWWD,5; LDA WD,5; OAR 76; JMP ICM

PARERX LDX =17
PARER  LDA =1; STA ERRCM,5
SM12   TZA; STA SWLJB
       TXA; LRLA 10; ORA =120000; ORA ERRCM,5; JMP MSG7A


*      INITIATE RECEIVE FROM REMOTE MODE

S201RC 0; LDA S201RC; STA SVEX1,5
       TZA; STA SWSR,5; STA SWCH,5; STA SWWD,5 SET SWITCHES TO RECEIVE
       LDA RSMSK; ORA RSBIT,5; STA RSMSK; OAR 64   SET RS TO RECEIVE
       LDA =SRC1; JMP RCOM
SRC1   CIA 64; ANA CSBIT,5; JAZ LP2
       INR SWNRN; INR SWNJB
       LDA =SRC2; JMP RCOM
SRC2   CIA 64; ANA COBIT,5; JAZ SRC2A; JMP LP2
SRC2A  INR SWNRN; INR SWNJB
       TXA; TZB; LLRL 1; ORA =21; OAR 77    SET WAIT FOR SPACE MODE
       DAR; OAR 77             ENABLE CHANNELS
*      PHASE IN ON BIT STREAM ON A SYNC CHAR.
PHASE  LDA =PH; JMP RCOM
PH     LDA SWWD,5; JAP LP2
       INR SWNRN; INR SWNJB
PH1    DEC ,A; STA SWCH,5; LDA WD,5; STA PWD,5; INR SWWD,5; JAZ PHASE
       JMPM WDPH; TZA; STA SWCH,5     SET SW. FOR 2ND CHAR POS.
       LDA =PH2; JMP RCOM
PH2    LDA SWWD,5; JAP LP2
       INR SWNRN; INR SWNJB
       LDA PWD,5; LDB WD,5; LLRL 10
       JMPM WDPH; JMP PH1

WDPH   0
       TAB; ANA =177400; ERA =46000; JAZ WDPH0
       TBA; ANA =77600; ERA =23000; JAZ WDPH1
       TBA; ANA =37700; ERA =11400; JAZ WDPH2
       TBA; ANA =17740; ERA =4600; JAZ WDPH3
       TBA; ANA =7760; ERA =2300; JAZ WDPH4
       TBA; ANA =3770; ERA =1140; JAZ WDPH5
       TBA; ANA =1774; ERA =460; JAZ WDPH6
       TBA; ANA =776; ERA =230; JAZ WDPH7
       JMP .WDPH
WDPH1  LDA TINS1+1; LDB TINS2+1; JMP WDP
WDPH2  LDA TINS1+2; LDB TINS2+2; JMP WDP
WDPH3  LDA TINS1+3; LDB TINS2+3; JMP WDP
WDPH4  LDA TINS1+4; LDB TINS2+4; JMP WDP
WDPH5  LDA TINS1+5; LDB TINS2+5; JMP WDP
WDPH6  LDA TINS1+6; LDB TINS2+6; JMP WDP
WDPH7  LDA TINS1+7; LDB TINS2+7; JMP WDP

TINS1  LSRA 10; LSRA 7; LSRA 6; LSRA 5
       LSRA 4; LSRA 3; LSRA 2; LSRA 1
TINS2  NOP; LLRL 1; LLRL 2; LLRL 3
       LLRL 4; LLRL 5; LLRL 6; LLRL 7

WDPH0  LDA TINS1; LDB TINS2
WDP    STA INS1,5; STB INS2,5
       JMPM GCH; ERA =SYNC; JAZ SRC4; 0
SRC4   JMPM GCH; ERA =SYNC; JAZ SRC5; JMP S201RC WAIT FOR 2 SYNCS
SRC5   JMPM GCH; ERA =SYNC; JAZ SRC5   IGNORE MORE THAN TWO SYNCS
       LDA SVEX1,5; STA *3
       TBA; JMP .JT

*      INITIATE SEND TO REMOTE MODE

S201SN 0; LDA S201SN; STA SVEX1,5; TXA; TZB; LLRL 1
       OAR 77; LDA =-1; STA SWSR,5   SET SEND MODE
       TZA; STA SWCH,5; STA SWWD,5   NO CHAR AND NO WORD
       LDA =SSN0; JMP RCOM
SSN0   CIA 64; ANA COBIT,5; JAZ LP2
       INR SWNRN; INR SWNJB
SSN1   LDA RSMSK; ORA RSBIT,5; ERA RSBIT,5; STA RSMSK; OAR 64   MAKE REQ. TO SEND
       LDA =SSN2; JMP RCOM
SSN2   CIA 64; ANA CSBIT,5; JAZ SSN2A; JMP LP2
SSN2A  INR SWNRN; INR SWNJB
SSN3   LDA =SYNC; JMPM SCH
       LDA =SYNC; JMPM SCH
       TXA; TZB; LLRL 1; ORA =20; OAR 77
       LDA =SYNC; JMPM SSIX    SEND 6 MORE SYNCS
       LDA SVEX1,5; STA *2; JMP .JT

*      END THE SEND MODE
E201SN 0; LDA E201SN; STA SVEX1,5; LDA =377; JMPM SSIX
       LDA =ESN1; JMP RCOM
ESN1   LDA SWWD,5; JAN LP2
       INR SWNRN; INR SWNJB; LDA SVEX1,5; STA *2; JMP .JT


*      SEND SIX CHARS. IN A
SSIX   0; LDB SSIX; STB SVEX,5
       STA CCH,5; JMPM SCH; LDA CCH,5; JMPM SCH; LDA CCH,5; JMPM SCH
       LDA CCH,5; JMPM SCH; LDA CCH,5; JMPM SCH; LDA CCH,5; JMPM SCH
       LDA SVEX,5; STA *2; JMP .JT

*      CHECK THE CK-SUM

CKCKS  0; LDA CKCKS; STA SVEX,5
       TZA; STA CCH,5; JMPM CKSM; JMPM CKSM
       JMPM GCH; LRLA 10; STA JT; JMPM GCH; ORA JT
       ERA CRC,5; JAZ CKCKS1
*      BAD CKSUM
       INR ERCNT,5; INR CSERCT,5; LDA SWIGCS,5; JAZ CKCKS1 (IGN. BAD CS)
       LDA CSERCT,5; JAP CSERR
CKCKS1 LDB SVEX,5; STB *2; JMP .JT

*      SEND ACK

*      REVERSE ACKS
RVACK  0; LDA ACKC,5; LDB ACKC1,5; STB ACKC,5; STA ACKC1,5; JMP .RVACK

*      GET CHARACTER FROM REMOTE

GCH    0
       LDA SWCH,5; JAP GCH1   NO CHAR. WAITING
       INR SWCH,5; LDA PWD,5; LDB INS1,5; STB XINS1
XINS1  NOP
       ANA =377; STA CCH,5
       TAB; JMP .GCH
GCH1   LDA SWWD,5; JAP GCH2   WAIT FOR WORD
GCH4   DEC ,A; STA SWCH,5
       LDA INS2,5; STA XINS2
       LDA PWD,5; LDB WD,5; INR SWWD,5; STB PWD,5
XINS2  NOP
       ANA =377; STA CCH,5
       TAB; JMP .GCH
GCH2   LDA GCH; STA SVEN,5; LDA =GCH3; JMP RCOM
GCH3   LDA SWWD,5; JAP LP2
       LDA SVEN,5; STA GCH; INR SWNRN; INR SWNJB; JMP GCH4

*      SEND CHARACTER TO REMOTE

SCH    0; TAB; LDA SWSR,5; JAN SCH1
SCHE   0
SCH1   LDA SWWD,5; JAN SCH3   WORD READY IF NEG.
       LDA SWCH,5; JAN SCH2   HAVE 1 CH. ALREADY
SCH1A  STB CH,5; DEC ,A; STA SWCH,5; JMP .SCH
*      MAKE A WORD
SCH2   LRLB 10; LDA CH,5; LLRL 10; STA WD,5
       DEC ,A; STA SWWD,5  SET WORD READY SW. FOR INTERRUPT
       TZA; STA SWCH,5   NO CHAR. STORED
       JMP .SCH
SCH3   LDA SWCH,5; JAP SCH1A   GO STORE CHAR.
       LDA SCH; STA SVEN,5; STB SCHB,5; LDA =SCH4; JMP RCOM
SCH4   LDA SWWD,5; JAN LP2
       INR SWNRN; INR SWNJB; LDA SVEN,5; STA SCH
       LDB SCHB,5; JMP SCH2

*       READ KEY WORD
RDKEY   0
        NOP; SEN 134,*-1; OME 37,IMWAH
        LDA =KYWA; OAR 34
        NOP; SEN 134,*-1; CIA 35; JMP .RDKEY


*      START SCHEDULER
START   TZA; LDX =7; STX JOB
S0      STA JOBL,5; STA LJOBL,5; STA STATE,5
        JXZ RESTRT
        DXR; JMP S0
RESTRT  EXC 77
S1      JMPM RDKEY; ERA =100; JAZ S2; JMP S1
S2      LDA =177577; OAR 35; LDA =KYWA+100000; OAR 34
        TZA; STA JOB
S3      JMPM RDKEY; ERA =100; JAZ S4
        INR JOB; LDA JOB; ERA =20000; JAZ S1
        JMP S3
S4     NOP; SEN 134,*-1
       OME 37,IMWAH; LDA =AMBFP; OAR 34
       NOP; SEN 134,*-1
       CIA 36; JAZM CRASH
       STA IMBFA; STA IMWP; ADD =SZMBF; STA OMWP; STA OMBFA
  ADD =SZMBF; STA EOMBFA; COM ,A; STA SWLJB
       EXC 277
START1 COM ,A; STA SWNRN (SET NO JOBS RNNING SW.)
       STA SWNJB (NO JOBS FOUND IN SCAN); STA SWIDLE (FOR IDLE LOOP CNT.)
LP     LDX =7
LP1    STX JOB
LP1A   LDA JOBL,5; JAZ LP2
       STA *2; JMP *

EJB    STA STATE,5; TZA
RCOM   STA JOBL,5; COM ,A; STA SWNRN
LP2    JXZ LP3
       DXR; JMP LP1
LP3    LDA SWNJB; JAP START1
        JSS3 0          STOP AND GO TO DDT
        JSS1 LP31       BYPASS CK OF KEY WORD
       INR SWIDLE (MAKE COUNT OF IDLE LOOPS)
*      CK KEY WORD IN 10 FOR ACTIVITY
       NOP; SEN 134,*-1; OME 37,IMWAH; LDA =KYWA; OAR 34
       NOP; SEN 134,*-1; SEN 234,PARERX
       CIA 35; ERA =200; JAZ LP31 (NO CHANGE)
       ERA =300; JAZ *+4; JMPM CRASH
       LDA =200; CPA; OAR 35; LDA =KYWA+100000; OAR 34
       COM ,A; STA SWIDLE (RESET IDLE COUNT)
*      CHECK FOR UNEXPECTED PHONE DISCONNECTS
LP31   LDX =7
LPT    LDA ECMSK; ANA ECBIT,5; JAZ LPT1
*IF ESTAB.COMM. BIT ON, SHOULD HAVE CARRIER
       CIA 65; ANA CPBIT,5; JAZ LPT2 (NO CARRIER)
LPT1   JXZ LPT3; DXR; JMP LPT
*      KILL JOB
LPT2   LDA =GONE; STA LJOBL,5; LDA ECMSK; ORA ECBIT,5; ERA ECBIT,5
       STA ECMSK; TZA; STA JOBL,5; STA STATE,5; JMP LPT1
LPT3   LDA SWLJB; JAN LP3A (NO LOW SCHED. JOB)
*      RESTART LOW SCHEDULER JOB
       TZA; STA SWLJB; LDX IJOB; LDA LSVA; LDB LSVB; JMP .LSVP
LP3A   NOP; SEN 134,*-1
       OME 37,IMWAH; OME 34,IMWP       OUTPUT ADR. FOR MSG. WORD
       NOP; SEN 134,*-1
       SEN 234,PARER
       CIA 35; JAZ LP50 (NO MSG)
*      PROCESS MESSAGE
       STA IMWA; IME 36,IMWB; INR SWLJB; COM ,A; OAR 35; OAR 36
       LDA IMWP; ORA COBIT+7; OAR 34
       INR IMWP; LDA IMWP; ERA OMBFA; JAZ LP3C (WRAP-AROUND)
LP3B   LDA IMWA; LSRA 10; ANA =17; STA JOB; TAX
       LDA IMWA; LSRA 14; ADD =MSGT-1+.; STA *2; JMP *
LP3C   LDA IMBFA; STA IMWP; JMP LP3B

MSGT   +MSG1; +MSG2; +MSG3; +MSG4; +MSG5; +MSG6; +MSG7; +MSG8
*      FIND NEW JOB SLOT
MSG1   TZX; STX JOB; LDA STATE,5; JAZ MSG1B (FOUND ONE)
       TXA; ERA =7; JAZ MSG1C
       IXR; JMP MSG1+1
*      NO JOBS NOW
MSG1C  LDA =17400 (MSG 1, PORT 17); JMP MSG7A
MSG1B  LDA IMWA; ANA =100; STA STATE,5 (S/R FLAG)
       TXA; LRLA 10; ORA =10000; ORA STATE,5; ORA =N620
       STA OMWA; LDA STATE,5
       LDB =ID; JAZ *+3
       LDB =OD; INR STATE,5; STB JOBL,5; JMP MSG7B

MSG2   LDA BFADA,5; JAZ *+3 (MUST BE ZERO)
       0
        LDA IMWA; ANA =200; ERA =200; STA SWIGCS,5
       LDA IMWA; LDB IMWB; LLRL 1; STA BFADA,5
       LSRB 1; STB BFADB,5; JMP MSGC

MSG3   E MSG2

MSG4   TXA; TZB; STB JOBL,5
       LLRL 1; OAR 77; LDA STATE,5; ANA =100; ORA =40
       STA STATE,5; JMP MSGC

MSG5   LDA STATE,5; ANA =40; JMPM CRASH (MUST BE IN IDLE)
       LDA IMWA; ANA =100; STA STATE,5
       LDB =ID; JAZ *+3
       LDB =OD; INR STATE,5; STB JOBL,5; JMP MSGC
MSG6   JMPM CRASH

*      REQUEST FOR STATE
MSG7   LDA BP,5; SUB BUFK,5; LLSR 11; LDA ERCNT,5; LLSR 7
       TXA; LRLA 10; ORA =70000; ORA STATE,5
MSG7A  STB OMWB; STA OMWA; NOP
MSG7B  SEN 134,*-1
       OME 37,OMWAH; OME 34,OMWP; NOP; SEN 134,*-1
       CIA 35; JAZ MSG7C; JMPM CRASH
MSG7C  LDA OMWA; CPA; OAR 35; LDA OMWB; CPA; OAR 36; LDA OMWP; ORA COBIT+7; OAR 34
       INR OMWP; LDA OMWP; ERA EOMBFA; JAZ MSG7D
MSGC   COM ,A; STA SWLJB; JMP START1
MSG7D  LDA OMBFA; STA OMWP; JMP MSGC

MSG8   LDA ECMSK; ORA ECBIT,5; ERA ECBIT,5; STA ECMSK
       OAR 64; TZA; STA JOBL,5; STA LJOBL,5; STA STATE,5; JMP MSGC
LP50   LDX =7
LP5    STX JOB; LDA LJOBL,5; JAZ LP6
       INR SWLJB; STA *2; JMP *
LP6    JXZ LP7
       DXR; JMP LP5
LP7    LDA SWIDLE; SUB HICNT; JAPM CRASH; JMP LP

*      LOW SCHED. RUNS

GONE   TZA; STA LJOBL,5; LDA RSMSK; ORA ENBIT,5
        STA RSMSK; OAR 64
       TZA; STA JOBL,5; STA LJOBL,5; STA STATE,5
       TXA; LRLA 10; ORA =100000; JMP MSG7A+1

*      RELEASE BUFFERS
IDBFR  LDA =40000; JMP BFR
ODBFR  LDA =20000
BFR    STA OMWA; TXA; LRLA 10; ORA OMWA
       TZB; STB LJOBL,5; JMP MSG7A

*      SEND FULL BUFFER TO 10
SBF    TZA; STA LJOBL,5
       LDA BUFK,5; STA OMWA
       LDA BFADB,5; ORA =100000; STA OMWB
       LDA BFADA,5; NOP
SBF0   SEN 134,*-1; OAR 37; NOP
SBF1   SEN 134,*-1
       LDA OMWA,7; CPA; OAR 35
       INR OMWA; LDA OMWA,7; CPA; OAR 36
       INR OMWA; OME 34,OMWB
       INR OMWB; LDA OMWA; SUB BP,5; JAN SBF1A
       LDA =ID3; STA JOBL,5; LDA STATE,5; ANA =1
       LRLA 6; STA OMWA; TXA; LRLA 10; ORA =50000; ORA OMWA
SBF2   ORA BFADA,5; LDB BFADB,5; JMP MSG7A
SBF1A  LDA OMWB; JAN SBF1
       LDA =100000; STA OMWB; LDA BFADA,5; ADD =1; JMP SBF0

*      GET FULL BUFFER FROM 10
RBF    TZA; STA LJOBL,5
       LDA BUFK,5; STA IMWA; LDA BFADB,5; STA IMWB
       LDA BFADA,5; NOP
RBF0   SEN 134,*-1; OAR 37
RBF1   OME 34,IMWB; NOP; SEN 134,*-1; SEN 234,PARER
       CIA 35; STA IMWA,7; INR IMWA
       LDA IMWA; ERA BUFK+1,5; JAZ RBF2
       CIA 36; STA IMWA,7; INR IMWA; INR IMWB
       LDA IMWB; JAP RBF1
       TZA; STA IMWB; LDA BFADA,5; ADD =1; JMP RBF0
RBF2   INR STATE,5 (TO STATE 104)
       LDA =OD2A; STA JOBL,5; TXA; LRLA 10; ORA =30000; JMP SBF2

EODMSG TXA; LRLA 10
       ORA =110000; TZB; JMP MSG7A


CSERR  LDB =40; LDA =2
ERRC   STA ERRCM,5; LDA SM12; STB STATE,5; JMP IB6B

*      INPUT DATA FROM REMOTE

ID     LDA =2; STA STATE,5; INR SWNJB; INR SWNRN
       LDA RSMSK; ORA ENBIT,5; ERA ENBIT,5; STA RSMSK
       JMPM S201RC   INITIATE RECEIVE MODE
       ERA =ENQC; JAZ ID1; JMP ID   WAIT FOR ENQ
ID1    LDA ECMSK; ORA ECBIT,5; STA ECMSK; LDA =ACK0; LDB =ACK1
       INR STATE,5             TO STATE 3
ID2    STA ACKC,5; STB ACKC1,5   SET ACK CHAR
ID3    JMPM S201SN   INITIATE SEND MODE TO SEND ACK
       LDA =4; STA STATE,5
       LDA =DLEC; JMPM SCH; LDA ACKC,5; JMPM SCH
       JMPM RVACK   REVERSE ACKS
       LDA =-3; STA CSERCT,5
ID3A   JMPM E201SN   TERMINATE SEND
*      WAIT FOR STX
ID4    LDA =5; STA STATE,5; JMPM S201RC; ERA =STXC; JAZ IBLK
       TBA; ERA =ENQC; JAZ ID5
       TBA; ERA =EOTC; JAZ EOTEJ
ID5    INR ERCNT,5; LDA =6; STA STATE,5
       LDA ACKC1,5; LDB ACKC,5; JMP ID2   PUT ACK BACK TO PREVIOUS

*      INPUT BLK

IBLK   LDA =10; STA STATE,5; LDB BUFK,5; STB BP,5   BUFFER PTR.
       TZA; STA SWBF,5 (FOR NO CHAR. WAITING)
IBLK1  TZA; STA CRC,5   CLEAR CK SUM
IB1    JMPM GCH; ERA =SYNC; JAZ IB1     IGNORE SYNC IN TEXT
       JMPM CKSM; JMPM STCH   GET CHAR, CK-SUM, AND STORE IN BUFF
       LDB CCH,5
       TBA; ERA =ETBC; JAZ IB3
       TBA; ERA =ETXC; JAZ IB2
       TBA; ERA =ITBC; JAZ IB4
       TBA; ERA =ENQC; JAZ SNAQ
       TBA; ERA =EOTC; JAZ EOTEJ; JMP IB1

*      END OF BLOCK
IB2    INR STATE,5
IB3    INR STATE,5
       JMPM CKCKS; JAZ IB6   GOOD CKSUM
*      BAD BLOCK, SEND NAQ FOR RETRY
SNAQ   JMPM S201SN
       LDA =NAKC; JMPM SCH
       JMP ID3A

*INTERMEDIATE BLOCK
IB4    JMPM CKCKS; JAZ IBLK1; JMP SNAQ

*SEND INTERMEDIATE BLOCK TO COMPUTER
IB6    LDA =IDBFR; STA LJOBL,5; TZA; STA BFADA,5; LDA =IB6A; JMP RCOM

IB6A   LDA BFADA,5; JAZ LP2 (WAIT FOR BUFFER ADR.)
       LDA =SBF
IB6B   STA LJOBL,5; TZA; JMP RCOM

CKSM   0; TZB; LDA CRC,5; LLRL 10; ORA CCH,5
       ERA TABLE,6; STA CRC,5; JMP .CKSM

EOTEJ  LDB =40; LDA =3; JMP ERRC

*      STORE CHARACTER IN BUFFER
STCH   0; LDA BP,5; ERA BUFK+1,5; JAZ SNAQ (OVERFLOW)
       LDA CCH,5; ADD =ITB; TAB; LDA 0,6
       LDB SWBF,5; JBZ STCH1
       LDB BP,5; ORA 0,6; STA 0,6; INR BP,5
       TZA; STA SWCH,5; JMP .STCH
STCH1  LRLA 10; LDB BP,5; STA 0,6; INR SWBF,5; JMP .STCH

EJ     LDA =40; JMP EJB

*      OUTPUT DATA TO REMOTE

OD     LDA =102; STA STATE,5; LDA RSMSK; ORA ENBIT,5; ERA ENBIT,5; STA RSMSK
       INR SWNRN; INR SWNJB; LDA =ACK0; LDB =ACK1
       STA ACKC,5; STB ACKC1,5
       JMPM S201SN; LDA =ENQC; JMPM SCH; JMPM E201SN
*      WAIT FOR ACK
       JMPM S201RC; ERA =DLEC; JAZ OD1; JMP OD
OD1    JMPM GCH; ERA ACKC,5; JAZ OD1A; JMP OD
OD1A   LDA ECMSK; ORA ECBIT,5; STA ECMSK (ESTABLISHED COMM.)
OD2    JMPM RVACK; LDA =103
OD20   STA STATE,5; TZA; STA BFADA,5 (SET NO BUFF. ADR.)
       LDA =ODBFR; STA LJOBL,5; LDA =OD21; JMP RCOM
OD21   LDA BFADA,5; JAZ LP2
       LDA =-3; STA CSERCT,5
       LDA =RBF; STA LJOBL,5; TZA; JMP RCOM
OD2A   JMPM S201SN; INR STATE,5; LDA =STXC; JMPM SCH
       LDA BUFK,5; STA BP,5; TZA; STA CRC,5; STA SWBF,5
OD3    LDA BP,5; TAB; ERA BUFK+1,5; JAZ INER
       LDA SWBF,5; JAZ OD3A
       TZA; STA SWCH,5; LDA 0,6; INR BP,5
OD3B   ANA =177; ADD =OTB
       TAB; LDA 0,6; STA CCH,5
       JMPM SCH
       JMPM CKSM
       LDA CCH,5; TAB; ERA =ETXC; JAZ OD4
       TBA; ERA =ITBC; JAZ OD7
       TBA; ERA =ETBC; JAZ OD8; JMP OD3
OD3A   INR SWCH,5; LDA 0,6; LSRA 10; JMP OD3B
OD4    INR STATE,5 (FOR ETX)
OD8    INR STATE,5 (FOR ETB)
*      END OF DATA, SEND CRC,5 CHARS.
       JMPM SCKS   SEND CK SUM CHARS.
       JMPM E201SN; JMPM S201RC; ERA =DLEC; JAZ OD5; JMP ODE1
OD5    JMPM GCH; ERA ACKC,5; JAZ OD6
ODE1   INR ERCNT,5; INR CSERCT,5; LDA =124; STA STATE,5
       LDA CSERCT,5; JAP CSERR; JMP OD2A
OD6    LDA STATE,5; ANA =1; JAZ OD2 (ETX NOT SENT)
*      SEND EOT
       LDA =115; STA STATE,5; JMPM S201SN
       LDA =EOTC; JMPM SCH; LDA =EODMSG; STA LJOBL,5
       JMPM E201SN; LDA =140; JMP EJB

OD7    JMPM SCKS; JMP OD3

INER   LDB =140; LDA =4; JMP ERRC

*      SEND CHECK SUM CHARACTERS
SCKS   0; TZA; STA CCH,5; JMPM CKSM; JMPM CKSM
       LDA CRC,5; LSRA 10; JMPM SCH
       LDA CRC,5; ANA =377; JMPM SCH
       TZA; STA CRC,5; JMP .SCKS


ITB    E *
0;0;40;173;0;0;55;134;20;0;46;175;0;0;0;60
0;150;0;110;0;171;0;131;30;161;0;121;0;0;0;70
0;144;0;104;0;165;0;125;0;155;0;115;0;0;0;64
14;0;74;0;0;0;45;0;34;0;52;0;24;0;100;0
2;142;0;102;0;163;0;123;22;153;0;113;26;0;0;62
0;0;133;0;0;0;174;0;0;0;41;0;0;0;72;0
0;146;0;106;27;167;0;127;10;157;0;117;0;0;0;66
16;0;53;0;6;0;76;0;36;0;73;0;0;0;75;0
1;141;0;101;0;176;57;0;21;152;0;112;0;0;0;61
0;151;0;111;0;172;0;132;31;162;0;122;0;0;140;71
11;145;0;105;12;166;0;126;0;156;0;116;0;0;0;65
15;0;50;0;5;0;137;0;35;0;51;0;25;0;47;0
3;143;0;103;0;164;0;124;23;154;0;114;0;0;0;63
13;0;56;0;0;0;54;0;0;0;44;0;0;0;43;0
177;147;0;107;33;170;0;130;0;160;0;120;4;0;0;67
17;0;135;0;7;0;77;0;37;0;136;0;32;0;42;0


OTB    E *
0;200;100;300;354;264;164;364;150;240;244;320;60;260;160;360
10;210;110;310;74;274;114;144;30;230;374;344;70;270;170;370
2;132;376;336;332;66;12;276;262;272;72;162;326;6;322;206
17;217;117;317;57;257;157;357;37;237;136;172;62;176;166;366
76;203;103;303;43;243;143;343;23;223;213;113;313;53;253;153
353;33;233;107;307;47;247;147;347;27;227;122;7;362;372;266
236;201;101;301;41;241;141;341;21;221;211;111;311;51;251;151
351;31;231;105;305;45;245;145;345;25;225;3;126;13;205;340



END    E *
  
    4 R”