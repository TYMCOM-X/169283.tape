100 DIM K$(200)
110 S1$="    SELECT 'CCCCCCCCC ASSIGN TO DSK DSK DSK."
120 S$="    SELECT 'CCCCCCCCC ASSIGN TO DSK."
125 Q$=CHR$(34)
140 PRINT "ENTER AUTHOR";
150 INPUT A$
160 PRINT "ENTER PROGRAM NAME (MAXIMUM OF 6 CHARACTERS)";
170 INPUT P$
172 IF LEN(P$)>6 GO TO 160
175 P$=P$+".CBL"
180 FILE #2:P$
190 SCRATCH #2
200 REM PAR START
210 READ L$
220 IF L$<>"PROGRAM-ID." GO TO 240
230 L$=L$+" "+P$+"."
240 IF L$="DATE-WRITTEN." GO TO 320
250 IF L$<>"AUTHOR." GO TO 270
260 L$=L$+" "+A$+"."
270 IF L$="FILE-CONTROL." GO TO 380
280 IF L$="DATA DIVISION." GO TO 520
290 IF L$="PROCEDURE DIVISION." GO TO 840
300 PRINT #2,L$
310 GO TO 200
320 REM PAR DATE
330 PRINT "ENTER 6 DIGIT DATE DDMMYY";
340 INPUT D$
350 L$=L$+" "+D$+"."
360 PRINT #2,L$
370 GO TO 200
380 REM PAR FILES
390 PRINT #2,L$
400 PRINT "ENTER INPUT FILE NAME(S) SEPARATED BY COMMA'S"
410 PRINT "FORMAT IS NINE DIGITS (SPACE OK) ENCLOSED IN QUOTES"
420 MAT INPUT F1$
430 I1=NUM
440 FOR I=1 TO I1
450 PRINT #2 USING S$,F1$(I)
460 NEXT I
470 PRINT #2 USING S1$,"SORTFIL"
480 PRINT "ENTER OUTPUT FILE NAME"
490 INPUT F1$
500 PRINT #2 USING S$,F1$
510 GO TO 200
520 REM PAR DD
530 PRINT #2, L$
540 PRINT #2 "FILE SECTION."
550 PRINT "ENTER RECORD SIZE(#CHARACTERS)";
560 INPUT R9
570 R9$=STR$(R9)
580 FOR I=1 TO I1
590 PRINT #2 "FD "+F1$(I)
600 PRINT #2 "    RECORD CONTAINS "+R9$+" CHARACTERS"
610 PRINT #2 "    VALUE OF IDENTIFICATION IS "+Q$+F1$(I)+Q$
620 F2$ = MID$(F1$(I),LEN(F1$(I)))
630 IF F2$<> " " GO TO 660
640 F1$(I) = LEFT$(F1$(I),LEN(F1$(I))-1)
650 GO TO 620
660 PRINT #2 "    DATA RECORD IS "+F1$(I)+"-REC."
670 PRINT #2 "01 "+F1$(I)+"-REC    PICTURE X("+R9$+")."
680 NEXT I
690 PRINT #2 "SD SORTFIL"
700 PRINT #2 "    RECORD CONTAINS "+R9$+" CHARACTERS"
710 PRINT #2 "    DATA RECORD IS SORTREC."
720 PRINT #2 "01  SORTREC."
730 GO TO 1410
740 PRINT #2 "FD  "+F1$
750 PRINT #2 "    RECORD CONTAINS "+R9$+" CHARACTERS"
760 PRINT #2 "    VALUE OF IDENTIFICATION IS "+Q$+F1$+Q$
770 F2$=MID$(F1$,LEN(F1$))
780 IF F2$<> " " GO TO 810
790 F1$=LEFT$(F1$,LEN(F1$)-1)
800 GO TO 770
810 PRINT #2 "    DATA RECORD IS "+F1$+"-REC."
820 PRINT #2 "01  "+F1$+"-REC    PICTURE IS X("+R9$+")."
830 GO TO 200
840 REM PAR PD
850 PRINT #2 "PROCEDURE DIVISION."
860 PRINT #2 "STARTER SECTION."
870 PRINT #2 "STRT."
880 IF K$(1) = "A" GO TO 920
890 K1$ = " ON DESCENDING KEY "
900 K2$ = "D"
910 GO TO 940
920 K1$ = " ON ASCENDING KEY "
930 K2$ = "A"
940 PRINT #2 "    SORT SORTFIL"+K1$
950 FOR I = 1 TO NUM/3
960 IF K$(I) = K2$ GO TO 1020
970 K2$ = K$(I)
980 IF K2$ = "A" GO TO 1010
990 PRINT #2 "    ON DESCENDING KEY"
1000 GO TO 1020
1010 PRINT #2 "    ON ASCENDING KEY"
1020 PRINT #2 "    KEY-"+STR$(I)
1030 NEXT I
1040 PRINT #2 "    INPUT PROCEDURE IS SORTIN"
1050 PRINT #2 "    OUTPUT PROCEDURE IS SORTOUT."
1060 PRINT #2 "    STOP RUN."
1070 PRINT #2 "SORTIN SECTION."
1080 FOR I=1 TO I1
1090 M$ = STR$(I)
1100 PRINT #2 "SRTIN-"+M$+"."
1110 PRINT #2 "    OPEN INPUT "+F1$(I)+"."
1120 PRINT #2 "SRTRD-"+M$+"."
1130 PRINT #2 "    READ "+F1$(I)+" INTO SORTREC AT END GO TO ENDSRT-"+M$+"."
1140 PRINT #2 "    RELEASE SORTREC."
1150 PRINT #2 "    ADD 1 TO INCOUNT."
1160 PRINT #2 "    GO TO SRTRD-"+M$+"."
1170 PRINT #2 "ENDSRT-"+M$+"."
1180 PRINT #2 "    CLOSE "+F1$(I)+"."
1190 NEXT I
1200 PRINT #2 "SORTOUT SECTION."
1210 PRINT #2 "SRTOT."
1220 PRINT #2 "    OPEN OUTPUT "F1$+"."
1230 PRINT #2 "SRTWT."
1240 PRINT #2 "    RETURN SORTFIL AT END GO TO OUTSRT."
1250 PRINT #2 "    WRITE "+F1$+"-REC FROM SORTREC."
1260 PRINT #2 "    ADD 1 TO OTCOUNT."
1270 PRINT #2 "    GO TO SRTWT."
1280 PRINT #2 "OUTSRT."
1290 PRINT #2 "    CLOSE "+F1$+"."
1300 READ L$
1310 IF L$<> "    DISPLAY " GO TO 1360
1320 PRINT #2 L$+Q$+"***SORT OUT OF BALANCE FOR "+P$+Q$+"."
1330 PRINT #2 L$+"INCOUNT "+Q$+"RECORDS INPUT"+Q$+"."
1340 PRINT #2 L$+"OTCOUNT "+Q$+"RECORDS SORTED"+Q$+"."
1350 GO TO 1300
1360 PRINT #2 L$
1370 IF L$="    EXIT." GO TO 1390
1380 GO TO 1300
1390 STOP
1400 REM
1410 REM PAR BUILDREC
1411 X1$="X."
1412 X2$="XX."
1413 X3$="XXX."
1420 DIM F$(200),R$(1000)
1430 PRINT "INPUT PARAMETERS"
1440 PRINT "POS. OF KEY, SIZE OF KEY, A OR D"
1450 PRINT "(ASCENDING OR DESCENDING)"
1460 MAT INPUT F$
1470 FOR I=1 TO NUM/3
1480 K$(I)=F$(I*3)
1490 R$(VAL(F$(I*3-2)))=":"+F$(I*3-1)+"KEY-"+STR$(I)
1500 IF I>9 GO TO 1520
1510 R$(VAL(F$(I*3-2)))=R$(VAL(F$(I*3-2)))+" "
1520 NEXT I
1530 FOR I=1 TO R9
1540 I9=LEN(R$(I))
1550 IF I9>0 GO TO 1580
1560 I2=I2+1
1570 GO TO 1690
1580 REM PAR KEY
1590 IF I2=0 GO TO 1620
1591 IF I2>3 GO TO 1600
1592 GOSUB 2040
1593 GO TO 1610
1600 PRINT #2 "    02 FILLER    PICTURE X("+STR$(I2)+")."
1610 I2=0
1620 REM PAR NO FILLER
1630 J=INSTR(R$(I),":")
1640 J1=INSTR(R$(I),"KEY")
1650 J2$=MID$(R$(I),J+1,J1-(J+1))
1651 J2=VAL(J2$)
1652 IF J2>3 GO TO 1660
1653 GOSUB 3040
1654 GO TO 1670
1660 PRINT #2 "    02 "+MID$(R$(I),J1)+"    PICTURE X("+J2$+")."
1670 I=I+VAL(J2$)-1
1680 R$(I)=""
1690 REM PAR NEXT I
1700 NEXT I
1710 IF I2 = 0 GO TO 1740
1711 IF I2 > 3 GO TO 1720
1712 GOSUB 2040
1713 GO TO 1730
1720 PRINT #2 "    02 FILLER   PICTURE X("+STR$(I2)+")."
1730 I2 = 0
1740 REM PAR END
1750 FOR I = 1 TO 1000
1760 R$(I) = ""
1770 NEXT I
1780 GO TO 740
1790 DATA "IDENTIFICATION DIVISION."
1800 DATA "PROGRAM-ID."
1810 DATA "AUTHOR."
1820 DATA "INSTALLATION. MSDO NAS NORTH ISLAND."
1830 DATA "DATE-WRITTEN."
1840 DATA "DATE-COMPILED. TODAY."
1850 DATA "SECURITY. NONE."
1860 DATA "REMARKS. STANDARD ANSI SORT PROGRAM."
1870 DATA "ENVIRONMENT DIVISION."
1880 DATA "CONFIGURATION SECTION."
1890 DATA "SOURCE-COMPUTER. PDP-10."
1900 DATA "OBJECT-COMPUTER. PDP-10."
1910 DATA "INPUT-OUTPUT SECTION."
1920 DATA "FILE-CONTROL."
1930 DATA "DATA DIVISION."
1940 DATA "WORKING-STORAGE SECTION."
1950 DATA "77 INCOUNT    PICTURE 9(10) VALUE 0."
1960 DATA "77 OTCOUNT    PICTURE 9(10) VALUE 0."
1970 DATA "77 CHECK      PICTURE 9(10) VALUE 0."
1980 DATA "PROCEDURE DIVISION."
1990 DATA "    SUBTRACT INCOUNT FROM OTCOUNT GIVING CHECK."
2000 DATA "    IF CHECK EQUAL TO 0 GO TO GOOD-END."
2010 DATA "    DISPLAY "
2020 DATA "GOOD-END."
2030 DATA "    EXIT."
2040 IF I2<> 1 GO TO 2050
2045 PRINT #2 "    02 FILLER    PICTURE "+X1$
2050 IF I2 <> 2 GO TO 2062
2055 PRINT #2 "    02 FILLER    PICTURE "+X2$
2060 GO TO 2070
2062 IF I2 <> 3 GO TO 2070
2065 PRINT #2 "    02 FILLER    PICTURE "+X3$
2070 RETURN
3040 IF J2 <> 1 GO TO 3050
3045 PRINT #2 "    02 "+MID$(R$(I),J1)+"    PICTURE "+X1$
3050 IF J2 <> 2 GO TO 3062
3055 PRINT #2 "    02 "+MID$(R$(I),J1)+"    PICTURE "+X2$
3060 GO TO 3070
3062 IF J2 <> 3 GO TO 3070
3065 PRINT #2 "    02 "+MID$(R$(I),J1)+"    PICTURE "+X3$
3070 RETURN
99999 END
