!       PRCOPY.SIM - VERSION 1 - CHRIS NEUSTRUP - MARCH 29, 1973
!
!               COPIES ROYALTY BILLING FILES INTO (BILLING10)
!               ON EACH PDP10

        GLOBAL %COPPER, RALPH:, OPENAM(10)


RALPH:  IOCS(1) ; COPPER ; EXIT
\\->RALPH

        LOCAL NUMP, NAME1, NAME2, NOROY:, DONE:, OPFLG
        LOCAL NEWNAM(10), OLDNAM(10), RDLOOP:, OPEMSG, EOFMSG
        LOCAL BAKNAM(10), NAME3, NOBACK:, NCT:, NUMC, COUNT(10)
        LOCAL %GET.FILE.NAME, P, TRANSI(10), K, FILNAM(10), NEXT:, NUM
        LOCAL NUMX, %DOUT, JJ, LUDNAM(10), PL, DULNAM(10), SPRNAM(10)
        LOCAL BARF:, EXI:, ELOOP:, SAVEIT(10), FILCNT, D, SITENO(10)

%COPPER
        MSG ("$PRCOPY  VERSION 1.1$$")
        OPEMSG _ '$CANNOT OPEN ROYALTY CONTROL FILE$'
        EOFMSG _ '$EOF ON ROYALTY CONTROL FILE$'
        OPFLG _ 0
        FILCNT _ 3

!               OPEN CONTROL FILE
        NUMP _ OPEN('(CUD10)ROYAL.DAT',SEQUEN+INPUT+CHARACTER,NOROY,1)
        OPFLG _ 1

!               GET TRANSFER FILE NAME AND OPEN IT
        GET.FILE.NAME(NUMP) ; MOVE 10 FROM FREE TO TRANSI
                        MOVE 10 FROM FREE TO SAVEIT
                        MOVE 10 FROM FREE TO SITENO
                        D _ CH(SITENO,15)
        NUMC _ OPEN(TRANSI,SEQUEN+OUTPUT+CHARACTER,NCT,1)
        P _ CHPT(OPENAM,-1)
        LOOP WHILE W(NUMC,NCHV P) # EOLIT
        REPEAT 2 DO
                GET.FILE.NAME(NUMP) ; P _ CHPT(OPENAM,-1)
                LOOP WHILE W(NUMC,NCHV P) # EOLIT
                END

RDLOOP: GET.FILE.NAME(NUMP) ; MOVE 10 FROM FREE TO OLDNAM
!               IS THIS THE EOF?
        IF CH(OLDNAM)=$Z THEN GO DONE
!               GET TWO OTHER FILE NAMES
        GET.FILE.NAME(NUMP) ; MOVE 10 FROM FREE TO NEWNAM
                                MOVE 10 FROM OPENAM TO FILNAM
        GET.FILE.NAME(NUMP) ; MOVE 10 FROM FREE TO BAKNAM

!               DOES THE BACK-UP FILE EXIST?
        NUMX _ OPEN(BAKNAM,SEQUEN+INPUT+WORD,NOBACK,1); CLOSE (NUMX)
                GO RDLOOP

!               NO BACK-UP, SO DO THE RENAME
NOBACK: RENAME (NEWNAM,BAKNAM,NEXT)
NEXT:   RENAME (OLDNAM,NEWNAM,RDLOOP)

!               GOT THE BILLING FILE, ADD IT TO TRANSFER FILE
        P _ CHPT(FILNAM,-1) ; INC FILCNT
        LOOP WHILE W(NUMC,NCHV P) # EOLIT
        GO RDLOOP

NOROY:  IF OPFLG=0 THEN MSG(OPEMSG) ELSE MSG(EOFMSG) ; EXIT

NCT:    MSG('$NO TRANSFER FILE FOR ROYALTY. NOTIFY SQA. RUN ABORTED.$')
        EXIT


!               WRITE SOFTWARE EOF
DONE:   REPEAT 5 W(NUMC,$Z)
        W(NUMC,CARRET) ; W(NUMC,LNFEED) ; W(NUMC,EOLIT)
        CLOSE (NUMP) ; CLOSE (NUMC)
        IF D=$1 THEN DO
                        MSG ('THE FOLLOWING') ; DOUT(FILCNT)
                MSG (' FILES HAVE BEEN GATHERED IN THE REALM OF C31.$$')
                     END ELSE DO
                        MSG ('TRANSFER THE FOLLOWING ') ; DOUT(FILCNT)
                        MSG (' FILES TO C31.$$')
                     END
        NUMC _ OPEN(SAVEIT,SEQUEN+INPUT+CHARACTER,EXI,1)
ELOOP:  P _ GETLINE(NUMC) ; IF CH(FREE) = $Z THEN GO EXI
        PRINT (FREE)
        GO ELOOP

EXI:    CLOSE (NUMC) ; EXIT

END COPPER


%GET.FILE.NAME (NUM)
        LOCAL PP
        PP _ GETLINE(NUM) ; MOVE 10 FROM FREE TO OPENAM
        LOOP WHILE NCHV PP # CARRET ; CHV PP _ EOLIT ; RETURN
END GET.FILE.NAME


%DOUT(N)
  LOCAL H, L, S, D, C
C _  H _ L _ 0;  S _ [N<0];  N_ ABS N
  WHILE N>0 THEN DO
    [ N; D] _ N DIVMOD 10; INC C
    [ H; L] _ [ H; L] DSHL 4;  L BOR _ D
  END
  PUT($ );  IF S#0 THEN PUT($-)
  WHILE DEC C >= 0 THEN DO
    PUT( L BAND HEX F + $0);  [ H; L] _ [ H; L] DSHR 4
  END
  RETURN
END DOUT
