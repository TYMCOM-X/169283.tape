OPEN RESRPT.

        TYPE FMT /
"BEGINNING RESOURCE REPORT.....VERSION 6" / END.

        FIND ALL.
        SORT BY SYSNUM TRU15 DOWN.
        INIT 1 RES370.RPT.

        DEFINE INTEGER TYM10 TYM11 TYM15 TYM16.
        DEFINE INTEGER REV10 REV11 REV15 REV16.

        REPORT START.

SEC I.

        PAGE ON 1 60.
        BODY ON 1 60.

        HEADING PRINT ON 1 SYSNUM GENDATE FMT 26X
"R E S O U R C E  R E P O R T" //
"PRIME/NON-PRIME USAGE FOR SYSTEM NUMBER " I " AS OF " D1 //
" CID  CUSTOMER NAME" 18X "DIS" 5X "P R O D U C T   C O D E S" /
" NO." 33X "NO." 5X "50" 5X "51" 6X "55" 6X "56" / END.

SEC G.
SEC H.

        ON CHANGE SYSNUM PRINT ON 1 FMT C2 $ END.

SEC P.

        LET TYM10 EQ 0 TYM11 EQ 0 TYM15 EQ 0 TYM16 EQ 0.
        LET REV10 EQ 0 REV11 EQ 0 REV15 EQ 0 REV16 EQ 0.

        PRINT ON 1 CUSNUM CUSNAM DISNUM CNC10/60 CNC11/60 TRU15 TRU16
FMT I5 1X A30 1X I3 2(1X I6) 2(1X M-2I7) END.

        IF CUSNUM EQ 1 THEN INTERN.

        LET REV10 EQ CNC10.
        LET REV11 EQ CNC11.
        LET REV15 EQ TRU15.
        LET REV16 EQ TRU16.

        GOTO TOTAL.

INTERN: LET TYM10 EQ CNC10.
        LET TYM11 EQ CNC11.
        LET TYM15 EQ TRU15.
        LET TYM16 EQ TRU16.

TOTAL:  SEC T.

        ON CHANGE SYSNUM PRINT ON 1 SYSNUM (TOT TYM10)/60 (TOT TYM11)/60
TOT TYM15 TOT TYM16 FMT / "** SYSTEM " I3
" NON-BILLABLE TOTALS:" 6X 2(1X I6) 2(1X M-2I7) END.

        ON CHANGE SYSNUM PRINT ON 1 SYSNUM (TOT REV10)/60 (TOT REV11)/60
TOT REV15 TOT REV16 FMT / "** SYSTEM " I3
" BILLABLE TOTALS:" 10X 2(1X I6) 2(1X M-2I7) END.

        ON CHANGE SYSNUM PRINT ON 1 SYSNUM (TOT CNC10)/60 (TOT CNC11)/60
TOT TRU15 TOT TRU16 FMT // "***** SYSTEM " I3
" GRAND TOTALS:" 10X 2(1X I6) 2(1X M-2I7) END.

        ON CHANGE SYSNUM PRINT ON 1 ((TOT CNC10)+(TOT CNC11))/60
((TOT TRU15)+(TOT TRU16)) FMT 41X "-------------" 1X "---------------"
/ 45X I5 8X M-2I8 END.

SEC F.

        REPORT END.

        TYPE FMT /
"END RESOURCE REPORT - RES370.RPT" END.

        @(UAS)TYM370
