MODULE GUTIL(DOLLAR,ENTRIES=($GIREC,$GERR,$GTOKN,$GEOPD,$GLIT,$GOPND,$GNITM,$GNFI,$GICHK,$GNSI,$GNRPI,$GNRI,$GALIGN),
                                FSAVE,TIMING,TIMER=EXTERNAL(SIX12))=
  BEGIN
  REQUIRE ADEFS.BLI[7,107355];
  REQUIRE FDEFS.BLI[7,107355];
  REQUIRE FPDEFS.BLI[7,107355];
  REQUIRE GDEFS.BLI[7,107355];
  REQUIRE UDEFS.BLI[7,107355];
  REQUIRE SDEFS.BLI[7,107355];
  REQUIRE TDEFS.BLI[7,107355];
  REQUIRE ZDEFS.BLI[7,107355];
  UNDECLARE $GIREC, $GERR, $GTOKN, $GALIGN;
  UNDECLARE $GLIT,$GOPND, $GNITM, $GNFI, $GEOPD, $GICHK, $GNSI, $GNRPI, $GNRI;

    MAP G$ GGBFP$;              % GLOBAL POINTER TO GAMMA REC.       %
    MAP GDBG$ GDBGF$;
    MAP Z$ ZBSTK$;


%***
  INITIALIZE A GAMMA FILE RECORD
***%
  GLOBAL ROUTINE $GIREC(TYPE,SIZE)=
    BEGIN
    $UZERO(GV$MAXGREC, .GGBFP$);
    GGBFP$[G$LENGTH] := .SIZE;
    GGBFP$[G$ID]     := .GGFID$;
    GGBFP$[G$TYPE]   := .TYPE;
    GGFID$ := .GGFID$ + 1;
    END;

%****
   USER ERROR MESSAGE ROUTINE
****%
  GLOBAL ROUTINE $GERR(ERRNO,STKP)=
    BEGIN
    LOCAL EMESGP;
    BIND EFACTOR = 500;
    BIND EMESG = PLIT(PLIT ASCIZ'500 : ILLEGAL LABEL AS OBJECT OF FINISH ',
                      PLIT ASCIZ'501 : ILLEGAL LABEL AS OBJECT OF REPEAT ',
                      PLIT ASCIZ'502 : ILLEGAL DATA TYPE FOR THIS CONTEXT',
                      PLIT ASCIZ'503 : DATA TO BE STORED DOES NOT AGREE WITH THE RECEIVING FIELD DATA TYPE ',
                      PLIT ASCIZ'504 : THE OBJECT OF A FINISH AR REPEAT IS NOT A LABEL',
                      PLIT ASCIZ'505 : INVALID DATA TYPE FOR THIS OPERAND',
                      PLIT ASCIZ'506 : OPERANDS NOT OF SAME DATA TYPE',
                      PLIT ASCIZ'507 : THE NO. OF INTEGER PLACES IN THE RECEIVING FIELD IS LESS THAN IN THE VALUE TO BE STORED',
                      PLIT ASCIZ'508 : RECIEVING OPERAND IS NOT A FIELD OR REPORT ITEM',
                      PLIT ASCIZ'509 : INVALID NAME USAGE; NEED A REPORT NAME',
                      PLIT ASCIZ'510 : INVALID NAME USAGE; NEED A FIELD NAME',
                      PLIT ASCIZ'511 : STRING REPEAT FACTOR MUST BE AN INTEGER',
                      PLIT ASCIZ'512 : INVALID OPERAND FOR AN OUTPUT LIST',
                      PLIT ASCIZ'513 : ALLIGMENT ATTEMPT EXCEEDS THREE WORDS',
                      PLIT ASCIZ'514 : FORMS CONTROL REQUIRES INTEGER OPERAND',
                      PLIT ASCIZ'515 : INVALID REPORT NAME ASSOCIATED WITH REPORT ITEM ',
                      PLIT ASCIZ'516 : INPUT ITEM MUST BE A FIELD',
                      PLIT ASCIZ'517 : INTERMEDIATE RESULT IS LONGER THAN THREE WORDS',
                      PLIT ASCIZ'518 : INVALID CONDITIONAL EXP. IN WHILE-DO OR DO-WHILE STMT',
                      PLIT ASCIZ'519 : LINE LIMIT REQUIRES AN INTEGER',
                      PLIT ASCIZ'520 : BOTH LEFT AND RIGHT OPERANDS ARE @NULL',
                      PLIT ASCIZ'521 : RELATIONAL OPERATION NOT VALID WITH @NULL AS AN OPERAND',
                      PLIT ASCIZ'522 : IDENTIFIER IS NOT A RELATION NAME',
                      PLIT ASCIZ'523 : IMPRPER ACCESS OF A RELATION',
                      PLIT ASCIZ'524 : KEYS IMPROPERLY SPECIFIED FOR THIS RELATION',
                      PLIT ASCIZ'525 : SELECT SHOULD HAVE BEEN USED INSTEAD OF FULL KEYED FOR EACH',
                      PLIT ASCIZ'526 : FIELD VALUE NOT AVAILABLE FOR USE AT THIS TIME',
                      PLIT ASCIZ'527 : THIS FIELD IS NOT IN THE SPECIFIED RELATION',
                      PLIT ASCIZ'528 : WHERE NEEDS A CONDITIONAL EXP',
                      PLIT ASCIZ'529 : THIS IDENTIFIER IS NOT A FIELD',
                      PLIT ASCIZ'530 : ALTER CAN ONLY APPEAR WITHIN THE SCOPE OF A FOR EACH OR SELECT',
                      PLIT ASCIZ'531 : CONVERTION WILL LOSE SIGNIFICANT DATA',
                      PLIT ASCIZ'532 : KEY FIELD DATA MISSING ON INSERT',
                      PLIT ASCIZ'533 : KEY FIELD VALUE HAS CHANGED SINCE BEING READ',
                      PLIT ASCIZ'534 : IMPROPER QUALIFICATION OF A KEY FIELD IN THE USING LIST',
                      PLIT ASCIZ'535 : FIELD IN USING LIST IS NOT COMPATIBLE WITH THE CORRESPONDING FIELD IN THE RELATION',
                      PLIT ASCIZ'536 : CANNOT SPECIFY A KEY FIELD IN THIS FORM OF THE USING LIST',
                      PLIT ASCIZ'537 : INVALID NAME IN USER VERIFY CODE',
                      PLIT ASCIZ'538 : STRING NEEDS USER VALUE CONSTRAINTS',
                      PLIT ASCIZ'539 : THE OBJECT OF A DELETE IS NOT QUALIFIED PROPERLY',
                      PLIT ASCIZ'599 : LAST MESG');

    IF .GDBGF$[GDB$GERR] EQL 1
      THEN
        BEGIN
        EMESGP := (.ERRNO - EFACTOR);
        $UAPSZ((.EMESG[.EMESGP])<36,7>);
        $UOUTL(0);
        $UAPSZ($USPTR('LINE: '));
        $UAPDC(.ZBSTK$[Z$LN,.STKP]);
        $UAPSZ($USPTR(' CHAR POS: '));
        $UAPDC(.ZBSTK$[Z$CP,.STKP]);
        $UOUTL(0);
        END;
    IF .ZBSTK$[Z$ERFL,.STKP] EQL AV$FALSE
      THEN
        $FERROR(ZBSTK$[Z$ID,.STKP]<0,36>,.ERRNO,0);
    END;

%***
  SUBSTITUTE A STANDARD FIELD OR LITERAL FOR THE ONE DETERMINED TO BE IN ERROR
***%
  GLOBAL ROUTINE $GEOPD(STKP,TYPE) =
    BEGIN

    MAP Z$ ZBSTK$;

    IF .TYPE EQL ZV$NUMERIC
      THEN
        BEGIN
        IF .ZBSTK$[Z$ID,.STKP] EQL TV$NAME
          THEN
            BEGIN
            ZBSTK$[Z$STP,.STKP]  := .GNFNP$;     % NUMERIC FIELD %
            ZBSTK$[Z$DEFSTP,.STKP]:= .GNFDP$;
            ZBSTK$[Z$INCSTP,.STKP]:= .GNFIP$;
            END
          ELSE
            BEGIN
            ZBSTK$[Z$STP,.STKP]    := .GNLNP$;   % NUMERIC LITERAL %
            ZBSTK$[Z$ID,.STKP] := TV$INTLIT;
            ZBSTK$[Z$INCSTP,.STKP] := .GNLIP$;
            END;
        ZBSTK$[Z$ICNT,.STKP] := 1;
        ZBSTK$[Z$DCNT,.STKP] := 0;
        END
      ELSE
        IF .ZBSTK$[Z$ID,.STKP] EQL TV$NAME
          THEN
            BEGIN
            ZBSTK$[Z$STP,.STKP]    := .GCFNP$;   %STRING FIELD %
            ZBSTK$[Z$DEFSTP,.STKP] := .GCFDP$;
            ZBSTK$[Z$INCSTP,.STKP] := .GCFIP$;
            END
          ELSE
            BEGIN
            ZBSTK$[Z$STP,.STKP]    := .GCLNP$;   % STRING LITERAL %
            ZBSTK$[Z$ID,.STKP] := TV$STRING;
            ZBSTK$[Z$INCSTP,.STKP] := .GCLIP$;
            END;
    ZBSTK$[Z$DATATYP,.STKP] := .TYPE;
    ZBSTK$[Z$ERFL,.STKP]    := AV$TRUE;
    END;



%****
   PUSH THE CURRENT TOKEN ON TO THE STACK
****%
  GLOBAL ROUTINE $GTOKN=
    BEGIN

    $GPUSH(.GBTKN$,3);

    END;

%***
  STACK A LITERAL
***%
  GLOBAL ROUTINE $GLIT =
    BEGIN

    LOCAL FP$I PICTURE;
    LOCAL S$ NAMESTE:INCSTE;
    MAP Z$ ZBSTK$;
    MAP A$ GBTKN$;

    IF .GBTKN$[A$SBID] EQL TV$9CR               % IF THE TOKEN IS FOR @CR THEN STACK IT AND GET OUT. %
      THEN
        BEGIN
        $GPUSH(.GBTKN$,3);
        RETURN 0;
        END;

    %**
      GET THE LITERAL INCARNATION ENTRY VIA THE NAME ENTRY THEN STACK THE LITERAL 
    **%

    NAMESTE := $SGSTE(.GBTKN$[A$STP]);
    INCSTE  := $SGSTE(.NAMESTE[S$LITINCP]);
    $SFRNCH(.NAMESTE);
    $GPUSH(.GBTKN$,7);
    ZBSTK$[Z$SBID,.ZSTKP$] := ZV$LIT;
    ZBSTK$[Z$ICNT,.ZSTKP$] := .INCSTE[S$NID];
    ZBSTK$[Z$DCNT,.ZSTKP$] := .INCSTE[S$NDP];
    ZBSTK$[Z$DEFSTP,.ZSTKP$] := .INCSTE[S$STPX];
    ZBSTK$[Z$INCSTP,.ZSTKP$] := .INCSTE[S$STPX];

    PICTURE := 0;
    IF .GBTKN$[A$ID] EQL TV$STRING    OR
       .GBTKN$[A$ID] EQL TV$CCLIT
      THEN
        BEGIN
        PICTURE[FP$TYPE]   := FPV$STR;
        PICTURE[FP$C]      := .INCSTE[S$NCHAR];
        ZBSTK$[Z$DATATYP,.ZSTKP$] := ZV$CHAR;
        END
      ELSE
        BEGIN
        PICTURE[FP$SIGN]   := FPV$NSC;
        PICTURE[FP$TYPE]   := FPV$NUM;
        PICTURE[FP$SP2]    := FPV$TRUE;
        PICTURE[FP$XI]     := .INCSTE[S$NID];
        PICTURE[FP$DI]     := .INCSTE[S$NDP];
        ZBSTK$[Z$DATATYP,.ZSTKP$] := ZV$NUMERIC;
        END;
    ZBSTK$[Z$FP,.ZSTKP$] := .PICTURE;
    $SFRNCH(.INCSTE);
    IF .GICNF$ EQL ZV$SET                       % IF THE CONDITIONAL INPUT LIST FLAG IS SET THEN THIS
                                                  IS THE CONDITIONAL LITERAL, SO SET THE INPUT CASE
                                                  LIST FLAG.
                                                %
      THEN
        GICLF$ := ZV$SET;
    END;

%***
  SET UP A GAMMA N-TUPLE OPERAND USING A STACK ENTRY. G$OPTYP IS EQUAL TO GV$STP
  SINCE IT IS INIT. TO ZERO AND GV$STP IS BOUND TO ZERO.
***%
  GLOBAL ROUTINE $GOPND(OPND) =
    BEGIN
    LOCAL GOPND;
    LOCAL G$OPND GOPNDP;
    MAP   Z$     ZBSTK$;

    GOPND := 0;
    GOPNDP := GOPND;
    IF .ZBSTK$[Z$SBID,.OPND] EQL ZV$RPITEM
      THEN
        BEGIN
        GOPNDP[0,G$OPTYP] := GV$RIP;
        GOPNDP[0,G$RITYP] := .ZBSTK$[Z$RITYP,.OPND];        % THIS VALUE WAS DETERMINED, WHEN THE REPORT ITEM
                                                                  WAS STACKED, ACCORDING TO THE CRITERIA STATED IN
                                                                  THE GAMMA FILE DOCUMENTATION.
                                                                    (IE GV4PAGENO,GV$PAGESIZ,ETC.)
                                                                %
        GOPNDP[0,G$OPVAL] := .ZBSTK$[Z$RPISTP,.OPND];       % THIS VALUE IS THE INCARNATION STP FOR THE
                                                                  ASSOCIATED REPORT AND WEA ALSO ESTABLOSHED
                                                                  WHEN THE REPORT ITEM WAS STACKED.
                                                                %
        END
      ELSE
        BEGIN
        GOPNDP[0,G$OPTYP] :=
           (SELECT .ZBSTK$[Z$SBID,.OPND] OF
                  NSET
              ZV$REL   : GV$STP;
              ZV$LRSLT :
                        BEGIN
                        GOPNDP[0,G$NPTYPE] := .ZBSTK$[Z$RSLTYP,.OPND];
                        GV$NP
                        END;
              ZV$FLD   :
                        BEGIN
                        IF .GMODE$ EQL GV$VRF
                          THEN
                            BEGIN
                            ZBSTK$[Z$INCSTP,.OPND] := 0;
                            IF .ZBSTK$[Z$DATATYP,.OPND] EQL ZV$NUMERIC
                              THEN
                                BEGIN
                                GOPNDP[0,G$OPDTYP] := GV$NUMERIC;
                                GOPNDP[0,G$OPICNT] := .ZBSTK$[Z$ICNT,.OPND];
                                GOPNDP[0,G$OPDCNT] := .ZBSTK$[Z$DCNT,.OPND];
                                END
                              ELSE
                                BEGIN
                                GOPNDP[0,G$OPDTYP] := GV$STRING;
                                GOPNDP[0,G$OPNCHAR]:= .ZBSTK$[Z$LCHAR,.OPND];
                                END;
                            GV$PARAM
                            END
                          ELSE
                            GV$STP
                        END;
              ZV$NRSLT :
                        BEGIN
                        GOPNDP[0,G$NPTYPE] := .ZBSTK$[Z$RSLTYP,.OPND];
                        GV$NP
                        END;
              ZV$LIT   : GV$LIT;
              ZV$SRSLT :
                        BEGIN
                        GOPNDP[0,G$NPTYPE] := .ZBSTK$[Z$RSLTYP,.OPND];
                        GV$NP
                        END;
                  TESN      );
        GOPNDP[0,G$OPVAL] := .ZBSTK$[Z$INCSTP,.OPND];
        END;

    .GOPND
    END;

%***
  CREATE AN ITEM ENTRY
***%
  GLOBAL ROUTINE $GNITM(DEFP) =
    BEGIN

    LOCAL S$ ITEMP;
    MAP   S$ DEFP;

    ITEMP := $SGSTSP(SV$ITEM);
    ITEMP[S$STP1] := .DEFP[S$STP1];
    ITEMP[S$ISTP] := SV$NULL;
    ITEMP[S$FDSTP]:= .DEFP[S$STPX];
    ITEMP[S$DDBT] := SV$NONE;
    ITEMP[S$TPKI] := SV$NULL;
    ITEMP[S$PKI]  := 0;
    ITEMP[S$KDFLG]:= SV$DATAF;
    ITEMP[S$RSFSN]:= .DEFP[S$RSFSN];
    ITEMP[S$RSDSTP] := SV$NULL;
    ITEMP[S$NOCF]   := SV$FALSE;
    ITEMP[S$RSBO]   := 0;
    DEFP[S$RSFSN] := .ITEMP[S$STPX];
    $SFGCH(.ITEMP);
    $SFGCH(.DEFP);


    .ITEMP
    END;

%***
  CREATE A NEW FIELD INCARNATION ENTRY
***%
  GLOBAL ROUTINE $GNFI(NAMEP,DEFP) =
    BEGIN

    LOCAL S$ INCARP;
    MAP   S$ NAMEP:DEFP;

    INCARP := $SGSTSP(SV$FLDINC);
    INCARP[S$STP1]   := .DEFP[S$STP1];
    INCARP[S$IDSTP]  := .NAMEP[S$IDSTP];
    INCARP[S$AFLG]   := SV$TRUE;
    INCARP[S$DTYP]   := .DEFP[S$DTYP];
    INCARP[S$FP]     := .DEFP[S$FP];
    INCARP[S$RNSTP]  := SV$NULL;
    INCARP[S$DDBT]   := SV$NONE;
    INCARP[S$DBT]    := SV$NONE;
    INCARP[S$FISTP]  := SV$NULL;
    INCARP[S$KDFLG]  := SV$DATAF;                % THIS ASSIGNMENT IS MADE ON THE ASSUMPTION THAT THE INCARNATION
                                                  IS BEING ALLOCATED FOR A SCALAR FIELD. IF IT RELATES TO A RELATION
                                                  ITS VALUE IS OVERRRIDEN IN THE ROUTINE $GASCOPE TO REFLECT ITS
                                                  USAGE IN THAT RELATION.
                                                %
    INCARP[S$IESTP] := .DEFP[S$RSFSN];           % THIS ASSIGNMENT IS MADE UNDER THE SAME ASSUMPTION AS FOR S$KDFLG %
    INCARP[S$VALCHGF] := SV$FALSE;
    INCARP[S$SORTORD] := SV$ASCEND;
    IF .DEFP[S$DTYP] EQL SV$SLIT
      THEN
        INCARP[S$LCHAR]  := .DEFP[S$LCHAR]
      ELSE
        BEGIN
        INCARP[S$INTD]   := .DEFP[S$INTD];
        INCARP[S$DECD]   := .DEFP[S$DECD];
        END;
    INCARP[S$NULL]   := .DEFP[S$NULL];
    NAMEP[S$IDSTP]   := NAMEP[S$CISTP] := .INCARP[S$STPX];
    $SFGCH(.INCARP);
    $SFGCH(.NAMEP);


    .INCARP
    END;

%*** 
  CHECK A SEMANTIC STACK ENTRY AND INSURE THAT IT CONTAINS AN ACTIVE INCARNATION STP.
  THIS ROUTINE RETURNS  ZV$FALSE IF AN ACTIVE INCARNATION DID NOT EXIST, OTHERWISE IT
  RETURNS THE VALUE  ZV$TRUE.
***%
  GLOBAL ROUTINE $GICHK(OPND)=
    BEGIN

    MAP Z$ ZBSTK$;
    LOCAL S$ NAMESTE:DEFSTE;
    LOCAL STATUS;

    IF .ZBSTK$[Z$ID,.OPND] EQL TV$NAME
      THEN
        IF .ZBSTK$[Z$SBID,.OPND] NEQ ZV$NRSLT AND
           .ZBSTK$[Z$INCSTP,.OPND] EQL ZV$NULL
          THEN
            IF .GMODE$ EQL GV$VRF
              THEN
                STATUS := ZV$TRUE
              ELSE
                BEGIN
                NAMESTE := $SGSTE(.ZBSTK$[Z$STP,.OPND]);
                IF .NAMESTE[S$CISTP] EQL SV$NULL
                  THEN
                    BEGIN
                    DEFSTE := $SGSTE(.NAMESTE[S$DSTP]);
                    $SFRNCH($GNITM(.DEFSTE));
                    $SFRNCH($GNFI(.NAMESTE,.DEFSTE));
                    $SFRNCH(.DEFSTE);
                    STATUS := ZV$FALSE;
                    END
                  ELSE
                    STATUS := ZV$TRUE;
                ZBSTK$[Z$INCSTP,.OPND] := .NAMESTE[S$CISTP];
                $SFRNCH(.NAMESTE);
                END
          ELSE
            STATUS := ZV$TRUE
      ELSE
        STATUS := ZV$TRUE;

    .STATUS

    END;


%****
  CREATE A SOURCE INCARNATION ENTRY.
****%
  GLOBAL ROUTINE $GNSI(NAMESTE)=
    BEGIN

    MAP S$ NAMESTE;
    LOCAL S$ INCSTE;

    INCSTE := $SGSTSP(SV$SRCINC);
    INCSTE[S$STP1]   := .NAMESTE[S$STPX];
    INCSTE[S$IDSTP]  := .NAMESTE[S$IDSTP];
    INCSTE[S$DBT]    := SV$NONE;
    INCSTE[S$SDSTP]  := .NAMESTE[S$DSTP];
    INCSTE[S$FISTP]  := SV$NULL;
    INCSTE[S$RICSTP] := SV$NULL;
    INCSTE[S$RNUM]   := 0;
    INCSTE[S$SDFSTP] := SV$NULL;
    INCSTE[S$INFSTP] := SV$NULL;

    NAMESTE[S$IDSTP] := NAMESTE[S$CISTP] := .INCSTE[S$STPX];

    $SFGCH(.NAMESTE);
    $SFGCH(.INCSTE);

    .INCSTE
    END;


%*****
  RETURNS RELATION INCARNATION IN A SYMBOL TABLE BUFFER.
*****%
  GLOBAL ROUTINE $GNRI(NAMESTE)=
    BEGIN

    MAP S$ NAMESTE;
    LOCAL S$ INCSTE;

    INCSTE := $SGSTSP(SV$RELINC);
    INCSTE[S$STP1]     := .NAMESTE[S$STPX];
    INCSTE[S$IDSTP]    := .NAMESTE[S$IDSTP];
    INCSTE[S$AFLG]     := SV$TRUE;
    INCSTE[S$DBT]      := SV$NULL;
    INCSTE[S$RESTP]    := .NAMESTE[S$DSTP];
    INCSTE[S$FISTP]    := SV$NULL;
    INCSTE[S$REALIZED] := SV$FALSE;
    INCSTE[S$RCBTY]    := SV$NULL;
    INCSTE[S$RICSTP]   := SV$NULL;
    INCSTE[S$SORTC]    := SV$NULL;
    INCSTE[S$RNUM]     := 0;
    INCSTE[S$SORTN]    := 0;
    INCSTE[S$CHGREF]   := SV$FALSE;
    INCSTE[S$IOMODE]   := SV$NULL;

    NAMESTE[S$CISTP] := NAMESTE[S$IDSTP] := .INCSTE[S$STPX];

    $SFGCH(.NAMESTE);
    $SFGCH(.INCSTE);

    .INCSTE

    END;


%****
  CREATE A REPORT INCARNATION ENTRY.
****%
  GLOBAL ROUTINE $GNRPI(NAMESTE)=
    BEGIN

    MAP S$ NAMESTE;
    LOCAL S$ INCSTE;

    INCSTE := $SGSTSP(SV$REPTINC);
    INCSTE[S$STP1]    := .NAMESTE[S$STPX];
    INCSTE[S$IDSTP]   := .NAMESTE[S$IDSTP];
    INCSTE[S$APND]    := SV$FALSE;
    INCSTE[S$RHL]     := SV$NULL;
    INCSTE[S$RFINIL]  := SV$NULL;
    INCSTE[S$RFDTLL]  := SV$NULL;
    INCSTE[S$RFINCL]  := SV$NULL;
    INCSTE[S$RCGPI]   := SV$NULL;
    INCSTE[S$PHL]     := SV$NULL;
    INCSTE[S$PFL]     := SV$NULL;
    INCSTE[S$RPDSTP]  := .NAMESTE[S$DSTP];

    NAMESTE[S$IDSTP] := NAMESTE[S$CISTP] := .INCSTE[S$STPX];

    $SFGCH(.INCSTE);
    $SFGCH(.NAMESTE);

    .INCSTE
    END;


%*****
  THIS ROUTINE ALIGNS NUMERIC ITEMS, WRITES THE APPROPRIATE GAMMA RECORD, AND RETURNS THE N-TUPLE OPERAND TO THE USER.
*****%
  GLOBAL ROUTINE $GALIGN(STKPTR,DCNT)=
    BEGIN

    LOCAL OPND, ICNT;
    LOCAL G$OPND OPNDP;

    OPNDP := OPND;
    ICNT := .ZBSTK$[Z$ICNT,.STKPTR];
    IF .ICNT + .DCNT GTR 30
      THEN
        BEGIN
        $GERR(513,.STKPTR);
        .ICNT := .ICNT - (30 - (.ICNT + .DCNT));
        END;

    $GIREC(GV$ALIGN,4);
    GGBFP$[G$ICNT]  := .ICNT;
    GGBFP$[G$DCNT]  := .DCNT;
    GGBFP$[G$UCNT]  := 1;
    GGBFP$[G$OPND1] := $GOPND(.STKPTR);
    GGBFP$[G$OPND2] := .DCNT - .ZBSTK$[Z$DCNT,.STKPTR];
    $GWBUF(.GGBFP$,.GOB1I$);

    OPNDP[0,G$OPTYP] := GV$NP;
    OPNDP[0,G$NPTYPE] := GV$ARITH;
    OPNDP[0,G$OPVAL] := .GGBFP$[G$ID];

    .OPND

    END;
  END
ELUDOM
   