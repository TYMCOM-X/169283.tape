TITLE	IOGEN FOR COBOL 5(107)		
SUBTTL	I/O GENERATORS		AL BLACKINGTON/SIP/CAM

;COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORP, MAYNARD, MA.

TWOSEG
RELOC	400000

;MACRO DEFINITIONS:

;SET CURRENT AC'S TO THE VALUE X
	DEFINE SETAC (X), <
	IFE X,<SETZM EAC>
	IFN X,<
	MOVEI TE,X
	MOVEM TE,EAC>
	>

;EXECUTE Z IF A MODE IS EQUAL TO Y
	DEFINE MODTRU (W,X,Y,Z),<
	HRRZ	T'W,EMODE'X
	CAIN	T'W,Y'MODE
	IFG Z-777777,<Z>
	IFLE Z-777777,<
	JRST Z>
	>

;EXECUTE Z IF A MODE IS NOT EQUAL TO Y
	DEFINE MODFLS (W,X,Y,Z),<
	HRRZ T'W,EMODE'X
	CAIE T'W,Y'MODE
	IFG Z-7777777,<X>
	IFLE Z-777777,<
	JRST Z>
	>
;SPECIAL CASES OF MODTRU & MODFLS
	DEFINE TMODEA (Y,Z),<
	MODTRU E,A,Y,Z
	>
	DEFINE FMODEA (Y,Z),<
	MODFLS E,A,Y,Z
	>
	DEFINE TMODEB (Y,Z),<
	MODTRU E,B,Y,Z
	>
	DEFINE FMODEB (Y,Z),<
	MODFLS E,B,Y,Z
	>
;WRITE OUT A UUO CALL ONTO GENFIL
	DEFINE RYTUUO,<
	PUSHJ	PP,SETUUO
	PUSHJ	PP,PUTASY
	>
;EXECUTE (OR GO TO) Z IF INTEGRAL PLACES OF A > B
	DEFINE INTAGB (Z),<
	MOVE TE,ESIZEA
	SUB TE,EDPLA
	SUB TE,ESIZEB
	ADD TE,EDPLB
	IFLE Z-777777,<JUMPG TE,Z>
	IFG Z-777777,<SKIPLE TE
	Z
	>
	>

;EXECUTE (OR GO TO) Z IF DECIMAL PLACES OF A > B.
	DEFINE DPAGB (Z),<
	MOVE	TE,EDPLA
	CAMLE	TE,EDPLB
	IFG Z-777777,<Z>
	IFLE Z-777777,<JRST Z>
	>

;EXECUTE (OR GO TO) Z IF DECIMAL PLACES OF B > A.
	DEFINE DPBGA (Z),<
	MOVE	TE,EDPLB
	CAMLE	TE,EDPLA
	IFG Z-777777,<Z>
	IFLE Z-777777,<JRST Z>
	>

;EXECUTE (OR GO TO) Z IF DECIMAL PLACES OF A = B.
	DEFINE DPAEB (Z),<
	MOVE TE,EDPLA
	CAMN TE,EDPLB
	IFG Z-777777,<Z>
	IFLE Z-777777,<JRST Z>
	>
;EXIT IF THE ERROR FLAG IS ON
	DEFINE EQUIT,<
	TSWF FERROR
	POPJ PP,
	>
IOGEN:	INTERNAL IOGEN

EXTERNAL CMNGEN,MOVGEN
EXTERNAL PUTASY, PUTASN
EXTERNAL MOVGN., MXX., MXTMP., MACX., MXAC.
EXTERNAL SETOPN, GETEMP, GETTAG,SUBSCR,SETUUO,STASHL,PUT.LD,LITD.
EXTERNAL FATAL,  OPFAT, OPNFAT, BADEOP, LNKSET,WARN
EXTERNAL KILL, BMPEOP, EWARN
EXTERNAL ASRJ.,AQRJ.,AZRJ.,SPIFGN,READEM

ENTRY READGN	;"READ" GENERATOR
ENTRY RITEGN	;"WRITE" GENERATOR
ENTRY OPENGN	;"OPEN" GENERATOR
ENTRY CLOSGN	;"CLOSE" GENERATOR
ENTRY SEEKGN	;"SEEK" GENERATOR
ENTRY DISPGN	;"DISPLAY" GENERATOR
ENTRY ACCGEN	;"ACCEPT" GENERATOR
ENTRY REWGEN	;"REWRITE" GENERATOR
ENTRY DELGEN	;"DELETE" GENERATOR

INTERNAL LARGE	;FIND LARGEST RECORD FOR A FILE


;GENERATE AN "OPEN"

OPENGN:	PUSHJ	PP,SETOP	;SET UP EOPTAB
	EQUIT;
	MOVSI	CH,OPN##

OPNGN1:	LDB	TE,[POINT 3,W1,11]
	DPB	TE,[POINT 3,CH,11]
	JRST	PUTOP


;GENERATE A "CLOSE"

CLOSGN:	PUSHJ	PP,SETOP
	EQUIT;
	MOVSI	CH,CLOS##
	TLNN	W1,DELETF	;IF 'DELETE' FLAG NOT UP,
	JRST	OPNGN1		;  THIS IS A STANDARD CLOSE


	MOVSI	CH,PURGE.	;THIS IS A 'CLOSE WITH DELETE'
	JRST	PUTOP
;GENERATE CODING FOR "READ"

READGN:	PUSHJ	PP,SETOP	;SET UP OPERAND
	EQUIT;

	SETZM	EINTO		;CLEAR "INTO" INDICATION
	TLNN	W1,INTO		;"INTO" OPTION FOR THIS READ?
	JRST	RDGN1		;NO

	PUSHJ	PP,LARGE	;YES--FIND LARGEST DATA RECORD FOR THIS FILE
	MOVE	TA,OPERND
	HRRZ	TE,EOPNXT
	SUBI	TE,2(TA)
	JUMPL	TE,RDGN9
	HRRZI	TC,EINTO+2
	HRLI	TC,2(TA)
	BLT	TC,EINTO+2(TE)

RDGN1:	MOVSI	CH,READ##
	PUSHJ	PP,PUTOP	;SET UP  AND WRITE OPERATOR
;"READ" (CONT'D)

;CHECK TO SEE THAT THE NEXT OPERATOR IS "SPIF"

	PUSHJ	PP,RDGN10	;READ UP THRU NEXT OPERATOR
	CAIN	TE,SPIF.
	TLNN	W1,ATINVK
	JRST	RDGN6

	LDB	TE,FI.ACC	;IS FILE
	JUMPN	TE,RDGN3	;  SEQUENTIAL?
	TLNE	W1,ATEND	;YES--IS SPIF "AT END"?
	JRST	SPIFGN		;YES--DO IT

	MOVEI	DW,^D208	;NO--TROUBLE
	JRST	RDGN4

RDGN3:	TLNE	W1,INVKEY	;IT'S RANDOM FILE--IS SPIF "INVALID KEY"?
	JRST	SPIFGN		;YES--DO IT

	MOVEI	DW,^D209	;NO--TROUBLE
RDGN4:	LDB	CP,W1CP
	LDB	LN,W1LN
	PUSHJ	PP,WARN
	JRST	SPIFGN

;READ WAS NOT FOLLOWED BY A "SPIF" OF CORRECT TYPE

RDGN6:	MOVEI	DW,^D318	;ASSUME FILE IS SEQUENTIAL
	LDB	TE,FI.ACC	;IF FILE IS NOT
	SKIPE	TE		;  SEQUENTIAL
	MOVEI	DW,^D319	;  USE 'INVALID KEY REQUIRED'

RDGN7:	MOVE	TC,OPLINE
	LDB	CP,TCCP
	LDB	LN,TCLN
	PUSHJ	PP,FATAL
	JRST	@EOPCOD(W2)

;NOT ENOUGH OPERANDS FOR "READ INTO"
RDGN9:	SETZM	EINTO
	JRST	BADEOP


;READ UP THRU NEXT OPERATOR

RDGN10:	MOVE	EACA,EOPLOC	;RESET
	MOVEM	EACA,EOPNXT	;  EOPTAB
	SETZB	EACC,ETEMPC	;MORE RESETS
	PUSHJ	PP,READEM	;DO THE READ
	HRRZ	TE,W2		;PICK UP OPERATOR CODE
	MOVE	TA,CURFIL	;SET 'TA' TO CURRENT FILE
	POPJ	PP,
;GENERATE CODING FOR "WRITE"

RITEGN:	MOVEI	CH,WRITE##	;SET UP 'WRITE' UUO
RITGN0:	MOVEM	CH,EIOOP
	PUSHJ	PP,SETOP	;SET UP OPERAND
	EQUIT;

	MOVE	TE,CURFIL	;OPERAND IS ACTUALLY
	MOVEM	TE,CURDAT	;  A RECORD-NAME
	PUSHJ	PP,GTFATH	;SET UP "FT"
	EQUIT

	MOVE	TA,CURDAT
	LDB	TE,DA.EXS	;GET RECORD SIZE
	MOVEM	TE,ERECSZ	;SAVE IT

	TLNN	W1,FROM
	JRST	RITGN1

	MOVE	TC,OPERND	;GET RECORD TABLE-LINK
	MOVEI	TA,2(TC)	;GET "FROM" DATA-NAME
	MOVEM	TA,CUREOP
	PUSHJ	PP,MOVGN.	;GENERATE MOVE

RITGN1:	MOVE	TA,CURFIL
	LDB	TD,FI.ACC
	MOVE	TE,EIOOP
	CAIE	TD,2		;IF FILE IS INDEXED
	CAIN	TE,WRITE	;  OR THIS IS A 'WRITE'
	JRST	RITG1A		;  ALL IS WELL

	MOVEI	DW,^D371	;'NOT LEGAL UNLESS ISAM'
	PUSHJ	PP,OPFAT

RITG1A:	JUMPE	TD,RITG1E	;IF SEQUENTIAL, WE CAN HAVE ADVANCING
	TLNN	W1,ADVANC	;IS THERE AN ADVANCING CLAUSE?
	JRST	RITGN2		;NO ADVANCING

	MOVEI	DW,^D372	;'ADVANCING ILLEGAL'
	PUSHJ	PP,OPFAT
	JRST	RITGN3

RITG1E:	TLNE	W1,ADVANC	;"ADVANCING" OPTION?
	JRST	WADVGN		;YES

	LDB	TB,FI.ERM	;GET EXTERNAL RECORDING MODE
	CAIN	TB,ASCMOD	;IF FILE IS NOT ASCII
	CAIN	TE,2		;  OR ACCESS MODE IS INDEXED,
	JRST	RITGN2		;  USE NORMAL WRITE

	MOVE	TC,[EXP 10B17+1];USE "ADVANCING"
	JRST	WADVG5
;"WRITE" GENERATOR  (CONT'D).

; NO ADVANCING

RITGN2:	MOVE	TE,EIOOP	;IF THIS IS
	CAIN	TE,DELETE##	;  A 'DELETE',
	JRST	RITG2B		;  SKIP SOME CODE

	LDB	TE,FI.ACC	;IS IT
	CAIE	TE,2		;  INDEXED FILE?
	JRST	RITG2B		;NO

;GENERATE MOVE OF SYMBOLIC KEY TO RECORD KEY

	MOVE	EACA,EOPLOC
	MOVEM	W1,1(EACA)
	MOVEM	W1,3(EACA)
	LDB	TE,FI.SKY
	JUMPE	TE,RITG2B
	MOVEM	TE,2(EACA)
	LDB	TE,FI.RKY
	JUMPE	TE,RITG2B
	MOVEM	TE,4(EACA)
	ADD	EACA,[XWD 4,4]
	MOVEM	EACA,EOPNXT
	PUSHJ	PP,MOVGEN

;PUT OUT 'WRITE', 'REWRITE', OR 'DELETE'

RITG2B:	HRLZ	CH,EIOOP	;GET OP-CODE
	PUSHJ	PP,PUTOP	;SET UP AND WRITE OPERATOR

	MOVE	CH,[XWD AS.XWD,1]	;PUT OUT XWD
	PUSHJ	PP,PUTASY
	MOVE	CH,ERECSZ	;PUT RECORD SIZE IN
	ROT	CH,-14		;  BITS 0-11
	HRRI	CH,AS.CNB
	PUSHJ	PP,PUTASN
	HRRZI	CH,0		;ZERO FOR RIGHT HALF
	PUSHJ	PP,PUTASN
;IF FILE IS RANDOM OR ISAM--"INVALID KEY" REQUIRED

RITGN3:	PUSHJ	PP,RDGN10	;READ UP THRU NEXT OPERATOR
	CAIE	TE,SPIF.
	JRST	RITGN9

	SETZM	EINTO		;CLEAR READ INTO FLAG FOR ENDIFGEN
	LDB	TE,FI.ACC
	TLNN	W1,INVKEY
	JRST	RITGN7

;"INVALID KEY" FOUND

	JUMPN	TE,SPIFGN	;IF NOT SEQ, ALL OK

RITGN6:	MOVEI	DW,^D320	;"INV KEY NOT ALLOWED"
	JRST	RDGN7

;"AT END" FOUND

RITGN7:	SKIPN	TE		;IF FILE IS SEQ,
	JRST	RITGN6		;  ALL IS WELL

RITGN8:	MOVEI	DW,^D319	;"INV KEY REQUIRED"
	JRST	RDGN7

;NO "SPIF" OF ANY KIND FOUND

RITGN9:	LDB	TE,FI.ACC	;IF FILE IS NOT SEQ,
	JUMPN	TE,RITGN8	;  TROUBLE
	JRST	@EOPCOD(W2)
;GENERATE CODE FOR "WRITE"  (WITH ADVANCING)

WADVGN:	MOVE	EACC,EOPNXT
	MOVE	TA,0(EACC)	;GET TABLE-LINK FOR "ADVANCING" OPERAND
	PUSHJ	PP,LNKSET

	MOVE	TC,-1(EACC)
	TLNN	TC,GNLIT	;IS IT A LITERAL?
	JRST	WADVG4		;NO

	TLNN	TC,GNNUM	;YES--IS IT NUMERIC?
	JRST	BADLIN		;NO--ERROR

	HRLI	TA,350700	;YES--CREATE A BYTE POINTER TO LITERAL IN VALTAB
	LDB	TD,TA		;GET SIZE
	JUMPE	TD,BADLIN	;IF ZERO--ERROR

	MOVEI	TC,0		;SET RESULT TO ZERO

WADVG2:	ILDB	TE,TA		;GET A DIGIT
	CAIG	TE,"9"		;IS IT REALLY A DIGIT?
	CAIGE	TE,"0"
	JRST	BADLIN		;NO--ERROR
	ADDI	TC,-"0"(TE)	;YES--ADD INTO RESULT

	CAILE	TC,^D66		;TOO BIG?
	JRST	BADLIN		;YES--ERROR

	SOJLE	TD,WADVG3	;NO--ANY MORE DIGITS?

	IMULI	TC,^D10		;YES
	JRST	WADVG2

WADVG3:	HRLI	TC,10		;SET CHANNEL TO 8
	JRST	WADVG5

WADVG4:	LDB	TE,[POINT 3,0(EACC),20]	;GET TYPE OF OPERAND
	CAIE	TE,TB.MNE
	JRST	WADVG6

	MOVE	TC,1(TA)
	TLNN	TC,MTCHAN
	JRST	BADMNE

	LDB	TC,CHANUM
	MOVSS	TC
	HRRI	TC,1
;GENERATE CODE FOR "WRITE ADVANCING" (CONT'D)

WADVG5:	MOVSI	CH,WADV.	;SET UP OP-CODE
	PUSHJ	PP,PUTOP	;WRITE OUT OPERATOR

	MOVE	TE,ERECSZ	;GET SIZE OF OUTPUT RECORD
	DPB	TE,[POINT 12,TC,11]

	TLNN	W1,AFTER	;"AFTER ADVANCING"?
	TLO	TC,1B31		;NO--SET "BEFORE"
	MOVE	CH,[XWD AS.XWD,1];CREATE THE XWD
	PUSHJ	PP,PUTASY
	MOVE	CH,TC
	HRRI	CH,AS.CNB
	PUSHJ	PP,PUTASN
	HRRZ	CH,TC
	JRST	WADVG9

;ADVANCING <DATA-NAME> LINES

WADVG6:	CAIE	TE,TB.DAT
	JRST	BADMNE

	LDB	TE,DA.DEF	;IF ITEM IS
	JUMPE	TE,UNDEFD	;  UNDEFINED, TROUBLE

	LDB	TE,DA.CLA	;IS THIS NUMERIC?
	CAIE	TE,2
	JRST	NOTINT

	LDB	TE,DA.NDP
	JUMPN	TE,NOTINT
	LDB	TE,DA.USG
	CAIN	TE,D1MODE+1
	JRST	WADVG7
;GENERATE CODE FOR "WRITE ADVANCING"  (CONT'D)
;ADVANCING <DATA-NAME> LINES (CONT'D)

;<DATA-NAME> IS NOT A 1-WORD COMP--CONVERT AND STASH IN TEMP
	MOVEI	TE,1		;GET A SINGLE TEMP WORD
	PUSHJ	PP,GETEMP
	MOVEM	EACC,EINCRB
	MOVSM	EACC,ESAVAC

	SETZM	EDPLB
	MOVEI	TE,^D10
	MOVEM	TE,ESIZEB
	MOVE	TE,[XWD ^D36,AS.MSC]
	MOVEM	TE,EBASEB
	MOVEI	TE,D1MODE
	MOVEM	TE,EMODEB

	MOVEI	LN,EBASEA
	HRRZ	TC,EOPNXT
	SUBI	TC,1
	PUSHJ	PP,SETOPN

	SWOFF	FASIGN;		;SET "A" IS UNSIGNED
	SWON	FBSIGN		;SET "B" IS SIGNED
	PUSHJ	PP,MXX.		;GENERATE A MOVE TO TEMPORARY

	MOVE	EACC,ESAVAC
	HRRI	EACC,AS.MSC
	JRST	WADVG8

;<DATA-NAME> IS A 1-WORD COMP
WADVG7:	HRRZ	TE,EOPNXT
	HRRZ	EACC,0(TE)

WADVG8:	MOVSI	CH,WADV.
	PUSHJ	PP,PUTOP

	MOVE	CH,[XWD AS.XWD,1]
	PUSHJ	PP,PUTASY
	MOVE	CH,[EXP 1B12+10B17+AS.CNB]
	TLNN	W1,AFTER
	TLO	CH,1B31
	MOVE	TE,ERECSZ	;PUT IN RECORD SIZE
	DPB	TE,[POINT 12,CH,11]
	PUSHJ	PP,PUTASN
	MOVE	CH,EACC
WADVG9:	PUSHJ	PP,PUTASN
	JRST	RITGN3
;GENERATE CODING FOR "SEEK"

SEEKGN:	PUSHJ	PP,SETOP
	EQUIT;

	MOVE	TA,CURFIL	;IF FILE IS
	LDB	TE,FI.ACC	;  NOT
	CAIE	TE,1		;  RANDOM,
	JRST	NOTRAN		;  ERROR

	MOVSI	CH,SEEK##
	JRST	PUTOP


;GENERATE CODE FOR 'REWRITE'

REWGEN:	MOVEI	CH,RERIT.
	JRST	RITGN0


;GENERATE CODE FOR 'DELETE'

DELGEN:	MOVEI	CH,DELETE
	JRST	RITGN0
;THE "DISPLAY" GENERATOR

DISPGN:	MOVEM	W1,OPLINE	;SAVE LN&CP OF OPERATOR
	CAMN	EACA,EOPLOC	;ANY OPERANDS?
	JRST	BADEOP		;NO--TROUBLE

	TLNE	W1,CONSOL	;"UPON" OPTION USED ?
	PUSHJ	PP,EDUPON	;YES--CHECK IT OUT

FSTRY:	MOVEM	EACA,EOPNXT	;POSITION OF LAST OPERAND SEEN INTO EOPNXT

	MOVE	EACA,EOPLOC	;GET POINTER TO BEGINNING OF TABLE
				;NOT TO 1ST USED SLOT
	HRRZM	EACA,CUREOP	;CURRENT ENTRY BEING USED IN EOPTAB
				;IS ONE HELD IN CUREOP.

	AOSA	EACA,CUREOP	;NOW WE POINT TO 1ST USED ENTRY, 1ST WORD...

GOTMOR:	HRRZ	EACA,CUREOP	;GET NEXT DEEPEST ENTRY
	MOVSM	EACA,OPERND	;  IN OPERAND TABLE
	MOVE	EACB,(EACA)	;GET 1ST WORD OF NEXT OPERAND
	MOVEI	EACA,1(EACA)	;BUMP EACA TO POINT TO SECOND WORD
	TLNE	EACB,GNLIT	;IS IT A LITERAL ?
	JRST	DISLIT		;YEP !


;OK, IT'S NOT A LITERAL:
;EITHER IT REQUIRES CONVERSION <& MXTMP. WILL WORRY ABOUT SUBSRCIPTING, ETC,>
;OR IT'S DISPLAY-7, IN WHICH CASE YOU WORRY ABOUT SUBSCRIPTING.


	MOVE	TA,(EACA)	;GET OPERAND TABLE-LINK
	MOVSM	TA,CURDAT	;  AND SAVE IT
	PUSHJ	PP,LNKSET	;CONVERT TO ADDRESS
	HRRM	TA,CURDAT	;  AND SAVE THAT

	LDB	TC,DA.USG	;GET USAGE OF OPERAND
	JRST	@DISPDO(TC)		;DO WHAT TABLE SENDS YOU TO DO




DISPDO:	EXP	ENDTST			; _ 0	TYPE NO YET ASSIGNED
	EXP	STNDRD			; _ 1	DISPLAY-6
	EXP	DISPD7			; _ 2	DISPLAY-7
	EXP	ENDTST			; _ 3	BAD DISPLAY USEAGE CODE
	EXP	STNDRD			; _ 4	1 WORD COMP
	EXP	STNDRD			; _ 5	2 WORD COMP
	EXP	DISPFP			; _ 6	COMP-1
	EXP	STNDRD			; _ 7	INDEX
;"DISPLAY" GENERATOR (CONT'D).

;NOW CALL ON THE MOVE GENERATOR FOR A LITTLE HELP


STNDRD:	HRRZ	TC,CUREOP
	PUSHJ	PP,MXTMP.	;MOVE X TO A TEMP., GENERATING CONVERSION
	TSWF	FERROR		;ANY TROUBLE?
	JRST	ENDTST		;YES--IGNORE THIS OPERAND

	MOVE	EACD,TA		;SAVE CALL PARAMETERS
	MOVE	EACC,TB

STND1:	TLNE	W1,NOADV	;IS IT 'WITH NO ADVANCING'?
	JRST	STND2		;YES--DON'T WORRY ABOUT 'END-OF-LINE' FLAG

	MOVE	TC,CUREOP	;SAVE ADDRESS OF THIS OPERAND
	PUSHJ	PP,BMPEOP	;ANY MORE OPERANDS?
	TLO	EACC,1B<^D18+7>	;NO--SET "END-OF-LINE" FLAG
	MOVEM	TC,CUREOP	;RESET ADDRESS OF CURRENT OPERAND

STND2:	MOVE	CH,[XWD DSPLY.+ASINC,AS.MSC]
	RYTUUO;

	HRRZ	CH,ELITPC
	IORI	CH,AS.LIT
	PUSHJ	PP,PUTASN

	MOVE	TA,[XWD XWDLIT,2]
	PUSHJ	PP,STASHL
	MOVE	TA,EACC
	MOVE	TE,ESIZEA	;GET SIZE OF OPERAND
	CAIG	TE,1777		;WILL IT FIT IN 10 BITS?
	JRST	STND3		;YES
	TLZ	TA,1B<^D18+7>	;NO--TURN OF 'END-OF-LINE'
	MOVEI	TE,^D1000	;CHANGE SIZE TO 1000
STND3:	TLZ	TA,1777		;USE SIZE IN 'TE'
	TLO	TA,(TE)
	MOVNS	TE
	ADDM	TE,ESIZEA

	PUSHJ	PP,STASHL
	MOVE	TA,EACD
	PUSHJ	PP,STASHL
	AOS	ELITPC

	SKIPN	ESIZEA		;IS OPERAND COMPLETELY OUT?
	JRST	ENDTST		;YES--LOOK FOR NEXT ONE

	ADD	EACD,[XWD ^D200,0]	;NO--BUMP ADDRESS
	JRST	STND2
;ITEM TO BE DISPLAYED IS ASCII

DISPD7:	MOVE	TC,CUREOP	;SET UP PARAMETERS IN "A"
	MOVEI	LN,EBASEA
	PUSHJ	PP,SETOPN
	TSWF	FERROR		;ANY TROUBLE?
	JRST	ENDTST		;YES--FORGET THIS OPERAND

	TSWT	FANUM		;NUMERIC?
	TSWT	FASUB		;NO--SUBSCRIPTED?
	JRST	STNDRD		;EITHER NUMERIC OR NOT SUBSCRIPTED

;NON-NUMERIC AND SUBSCRIPTED -- USE "SUBSC." UUO

	MOVE	TA,CURDAT
	HRRZ	TB,ESIZEA	;USE INTERNAL SIZE UNLESS
	LDB	TE,DA.EDT	;  ITEM IS
	SKIPE	TE		;  EDITED,
	LDB	TB,DA.EXS	;  IN WHICH CASE USE EXTERNAL SIZE

	MOVEI	DT,ESAVES
	PUSHJ	PP,BMPEOP
	IORI	TB,1B<^D18+7>
	MOVEM	TB,SUBCON
	MOVS	TC,OPERND
	MOVEM	TC,CUREOP
	PUSHJ	PP,SUBSCR
	JRST	DISP7B		;ALL SUBSCRIPTS WERE NUMERIC LITERALS

	MOVE	CH,[XWD DSPLY.,SXR]
	PUSHJ	PP,PUTASY
	JRST	ENDTST

DISP7B:	MOVE	EACC,TE
	HRRI	EACC,AS.CNB
	MOVS	EACD,TE
	HRR	EACD,EBASEA
	JRST	STND2
;DISPLAY A COMP-1 FIELD

DISPFP:	MOVE	TC,CUREOP
	MOVEI	LN,EBASEA
	PUSHJ	PP,SETOPN
	TSWF	FERROR;
	JRST	ENDTST

	MOVEI	TE,5
	MOVEM	TE,EAC
	PUSHJ	PP,MXAC.

	MOVE	CH,[XWD EPJPP,DSP.FP]
	PUSHJ	PP,PUT.EX##

	MOVE	TC,CUREOP
	PUSHJ	PP,BMPEOP
	JRST	DISFP1

	SETZM	ETEMPC
	JRST	GOTMOR

DISFP1:	MOVEM	TC,CUREOP
	PUSHJ	PP,ASRJ.
	MOVSI	EACC,446001
	HRRI	EACC,AS.CNB
	MOVS	EACD,EASRJ
	HRRI	EACD,AS.MSC
	MOVEI	TE,1
	MOVEM	TE,ESIZEA
	JRST	STND2
;"DISPLAY" GENERATOR (CONT'D)

;DISPLAY A LITERAL

DISLIT:	TLNE	EACB,GNFIGC	;IS IT A FIG. CONST.?
	JRST	DISFC		;YES

	MOVEI	LN,EBASEA	;NO--SET UP PARAMETERS
	HRRZ	TC,CUREOP
	PUSHJ	PP,SETOPN
	TSWF	FERROR		;ANY TROUBLE?
	JRST	ENDTST		;YES--FORGET THIS ONE

	MOVE	TE,[XWD EBASEA,EBASEB]	;MAKE "B" LOOK LIKE "A"
	BLT	TE,EBASBX

	PUSHJ	PP,LITD.
	MOVS	EACD,EINCRA
	HRRI	EACD,AS.MSC
	MOVE	EACC,[EXP ^D36B5+AS.CNB]
	MOVE	TE,ESIZEA
	DPB	TE,[POINT 7,EACC,17]

	JRST	STND1
;"DISPLAY" GENERATOR (CONT'D)

;DISPLAY A FIGURATIVE CONSTANT

DISFC:	TLNE	EACB,GNTALY!GNTODY	;"TALLY" OR "TODAY"?
	JRST	STNDRD		;YES--USE STANDARD ROUTINE

	TLNE	EACB,GNFCS	;SPACE?
	JRST	FIGC1
	TLNE	EACB,GNFCZ
	JRST	FIGC2
	TLNE	EACB,GNFCQ	;QUOTE?
	JRST	FIGC3

	MOVEI	DW,^D184	;NONE OF THE ABOVE
	PUSHJ	PP,OPNFAT

FIGC1:	PUSHJ	PP,ASRJ.	;CREATE LITERAL FOR SPACE
	HRLZ	EACD,EASRJ
	JRST	FIGC4

FIGC2:	PUSHJ	PP,AZRJ.	;CREATE LITERAL FOR ZERO
	HRLZ	EACD,EAZRJ
	JRST	FIGC4

FIGC3:	PUSHJ	PP,AQRJ.	;CREATE LITERAL FOR QUOTE
	HRLZ	EACD,EAQRJ

FIGC4:	HRRI	EACD,AS.MSC	;FINISH RIGHT HALF OF XWD
	MOVE	EACC,[EXP ^D36B5+1B17+AS.CNB]
	MOVEI	TE,1		;SIZE OF OPERAND IS 1
	MOVEM	TE,ESIZEA
	JRST	STND1
;"DISPLAY" GENERATOR  (CONT'D).

ENDTST:	PUSHJ	PP,BMPEOP	;ANY MORE OPERANDS?
	POPJ	PP,		;NO--QUIT
	SETZM	ETEMPC		;YES--RESET %TEMP BASE
	JRST	GOTMOR		;CONTINUE PROCESSING


EDUPON:	HRRZ	TA,(EACA)	;GET TABLE ENTRY FOR "UPON" OPERAND
	CAIL	TA,700001
	CAILE	TA,777777	;BETWEEN COARSE LIMITS OF MNEMONIC TABLE?
	JRST	BADNEW		;BAD NEWS, NOT A MNEM TABLE LINK


	PUSHJ	PP,LNKSET	;CONVERT TO REAL ADDRESS
	MOVE	EACB,1(TA)	;GET MNEMONIC TABLE ENTRY
	TLNE	EACB,1B21	;CONSOLE FLAG UP ?
	JRST	REPOS		;YES   HE'S AOK
				;REPOSITION POINTER TO LOOK AT LAST
				;"WRIT-ABLE" ITEM.


BADNEW:	MOVEI	DW,^D102
	PUSHJ	PP,EWARN

REPOS:	SUB	EACA,[XWD 2,2]	;BACK OFF EACA
	CAMN	EACA,EOPLOC	;WAS THAT THE ONLY OPERAND?
	JRST	BADEOP		;YES--TROUBLE
	POPJ	PP,		;NO--RETURN
;"ACCEPT" GENERATOR

ACCGEN:	MOVEM	W1,OPLINE	;SAVE LN&CP OF OPERATOR
	CAMN	EACA,EOPLOC	;ANY OPERANDS?
	JRST	BADEOP		;NO--TROUBLE

	TLNE	W1,CONSOL
	PUSHJ	PP,EDUPON

	MOVEM	EACA,EOPNXT
	HRRZ	TC,EOPLOC
	ADDI	TC,1
	MOVEM	TC,CUREOP

	SWOFF	FASUB!FALWY0	;AC'S NOT SUBSCRIPTED AND NOT ZERO

ACEPT1:	MOVEM	TC,OPERND

	MOVEI	LN,EBASEB
	PUSHJ	PP,SETOPN

	MOVE	TE,[XWD EBASEB,EBASEA]	;SET "A" EQUAL TO "B"
	BLT	TE,EBASAX

	MOVE	TA,CUREOP	;IS "B" EDITED?
	MOVE	TA,1(TA)
	CAIN	TA,TALLY.##
	JRST	ACEPT2
	PUSHJ	PP,LNKSET
	LDB	TE,DA.EDT
	JUMPE	TE,ACEPT2
	MOVEI	TD,EDMODE	;YES--RESET MODE TO
	HRRM	TD,EMODEB	;  'EDITED'

ACEPT2:	HRLZ	TC,ESIZEB
	PUSHJ	PP,BMPEOP
	TLO	TC,1B<^D18+7>
	MOVSM	TC,SUBCON

	MOVE	TC,OPERND
	MOVEM	TC,CUREOP

	MOVE	TE,0(TC)
	TLNN	TE,GNTALY	;TALLY IS NUMERIC
	TLNE	TE,GNOPNM
	JRST	ACEP15
;"ACCEPT" GENERATOR  (CONT'D).

;FIELD IS ALPHANUMERIC

	HRRZ	TE,EMODEB
	CAIE	TE,D7MODE
	JRST	ACEP10

	HRRZ	TE,EMODEB
	CAIN	TE,EDMODE
	JRST	ACEP10

	TSWT	FBSUB;
	JRST	ACEPT5

	MOVEI	DT,ESAVSB
	PUSHJ	PP,SUBSCR
	JRST	ACEPT4

	MOVE	CH,[XWD ACEPT.,SXR]
	PUSHJ	PP,SETUUO
	PUSHJ	PP,PUTASY
	JRST	ACEPT6

ACEPT4:	HRRZM	TE,EINCRA
	LSH	TE,-14
	HLLM	TE,ERESA

ACEPT5:	PUSHJ	PP,ACEP20

ACEPT6:	PUSHJ	PP,BMPEOP	;ANY MORE OPERANDS?
	POPJ	PP,		;NO--QUIT

	MOVE	TC,CUREOP	;YES--LOOP BACK FOR MORE
	JRST	ACEPT1
;"ACCEPT" GENERATOR (CONT'D).

;FIELD IS EITHER ALPHA-EDITED, OR NON-ASCII ALPHANUMERIC

ACEP10:	MOVE	TE,[XWD ^D36,AS.MSC]
	MOVEM	TE,EBASEA

	MOVE	TE,ESIZEA
	IDIVI	TE,5
	SKIPE	TD
	ADDI	TE,1
	PUSHJ	PP,GETEMP
	HRRZM	EACC,EINCRA
	MOVEI	TE,D7MODE
	MOVEM	TE,EMODEA

	PUSHJ	PP,ACEP20

	SWOFF	FASIGN!FANUM;
	PUSHJ	PP,MXX.
	JRST	ACEPT6


;FIELD IS NUMERIC OR NUMERIC EDITED

ACEP15:	PUSHJ	PP,ACEP25

	SETZM	EAC
	SWON	FASIGN!FANUM
	MOVEI	TE,D2MODE
	MOVEM	TE,EMODEA

	PUSHJ	PP,MACX.
	JRST	ACEPT6
;"ACCEPT" GENERATOR  (CONT'D).

;CREATE LITERAL AND CALL FOR ALPHANUMERIC

ACEP20:	MOVE	TA,[XWD XWDLIT,2]
	PUSHJ	PP,STASHL
	HRLZ	TA,SUBCON
	LSH	TA,6
	HLR	TA,ERESA
	ROT	TA,-6
	HRRI	TA,AS.CNB
	PUSHJ	PP,STASHL
	MOVE	TA,EBASEA
	HRL	TA,EINCRA
ACEP21:	PUSHJ	PP,STASHL

	MOVSI	CH,ACEPT.
	PUSHJ	PP,SETUUO
	PUSHJ	PP,PUT.LD
	AOS	ELITPC
	POPJ	PP,


;CREATE LITERAL AND CALL FOR NUMERIC

ACEP25:	MOVE	TA,[XWD XWDLIT,2]
		PP,STASHL
	MOVS	TA,SUBCON
	TLO	TA,1B<^D18+6>
	HRRI	TA,AS.CNB
	PUSHJ	PP,STASHL
	HRLZ	TA,EDPLA
	JUMPGE	TA,ACEP26
	MOVMS	TA
	TLO	TA,40
ACEP26:	HRRI	TA,AS.CNB
	JRST	ACEP21
;SET UP POINTERS TO OPERANDS

SETOP:	MOVEM	W1,OPLINE	;SAVE OPERATOR'S LN&CP
	SWOFF	FEOFF1		;CLEAR MOST FLAGS
	CAME	EACA,EOPLOC	;ANY OPERANDS?
	JRST	SETOP1		;YES

	SWON	FERROR		;NO--SET FLAG SO NO CODE GENERATED
	JRST	BADEOP

SETOP1:	HRRZ	TA,EOPLOC	;SET TA TO FIRST ONE
	ADDI	TA,1
	MOVEM	TA,OPERND	;SAVE

	MOVE	TA,1(TA)	;RESOLVE INTO ACTUAL ADDRESS
	MOVSM	TA,CURFIL
	PUSHJ	PP,LNKSET
	HRRM	TA,CURFIL
	POPJ	PP,


;SET UP AND WRITE OPERATOR

PUTOP:	HLR	CH,CURFIL
	AND	CH,[XWD -1,LMASKB]
	IORI	CH,AS.FIL

	PUSHJ	PP,SETUUO
	JRST	PUTASY
;GET FILTAB ENTRY CORRESPONDING TO THE SPECIFIED OUTPUT RECORD

GTFATH:	LDB	CH,[POINT 3,(TA),2]	;IS THIS A DATA-NAME?
	CAIE	CH,TB.DAT
	JRST	NOTREC		;NO--ERROR
	LDB	TE,DA.DEF	;IS IT DEFINED?
	JUMPE	TE,NOTREC	;IF NOT, ERROR

GTFAT1:	MOVE	CH,TA
	LDB	TA,DA.BRO
	JUMPE	TA,NOTREC

	LDB	TE,LNKCOD	;IS FATHER/BROTHER LINK TO DATAB?
	CAIE	TE,TB.DAT
	JRST	GTFAT2		;NO

	PUSHJ	PP,LNKSET	;YES--CONVERT TO ADDRESS
	JRST	GTFAT1		;LOOP TO NEXT

GTFAT2:	CAIE	TE,TB.FIL	;IS FATHER/BROTHER A FILE?
	JRST	NOTREC		;NO--ERROR

	MOVSM	TA,CURFIL	;YES--SAVE LINK
	PUSHJ	PP,LNKSET	;CONVERT TO ADDRESS
	HRRM	TA,CURFIL	;  AND SAVE THAT

	POPJ	PP,
;FIND LARGEST DATAB RECORD FOR THIS FILTAB--PUT OPERAND FOR IT INTO "EINTO"

LARGE:	MOVE	TA,CURFIL	;GET LINK TO
	LDB	TA,FI.DRL	;  FIRST DATA RECORD

	HRRZI	TD,0		;CLEAR SIZE OF LARGEST

LARGE1:	MOVE	CH,TA		;SAVE DATAB LINK
	JUMPE	TA,LARGE4	;MUST BE AN ERROR CASE, NONE THERE
	PUSHJ	PP,LNKSET

	LDB	TC,DA.EXS	;GET SIZE OF THAT RECORD
	CAIG	TC,(TD)		;IS THIS LARGEST SO FAR?
	JRST	LARGE2		;NO

	MOVE	TD,TC		;YES
	HRRZM	CH,EINTO+1
	MOVEM	TA,EINTO

LARGE2:	LDB	TC,DA.FAL	;IF THERE IS NO
	JUMPN	TC,LARGE3	;  BROTHER, WE ARE DONE


	LDB	TA,DA.BRO	;GET BROTHER LINK
	JRST	LARGE1		;LOOP

LARGE4:	MOVEI	TA,<CD.DAT>B20+1	;AIM AT DUMMY
	PUSHJ	PP,LNKSET	;  & GO ON (ERROR MSG FROM ELSEWHERE)
LARGE3:	MOVE	TA,EINTO
	LDB	CH,DA.LNC	;GET LN&CP OF LARGEST RECORD
	MOVEM	CH,EINTO

	POPJ	PP,
;DIAGNOSTIC ROUTINES

;FILE IS NOT RANDOM

NOTRAN:	MOVEI	DW,^D205
	JRST	OPFAT


;IMPROPER "ADVANCING N LINES"

BADLIN:	MOVEI	DW,^D98
	JRST	ADVERA


;ADVANCING <DATA-NAME> HAD DECIMAL PLACES

NOTINT:	MOVEI	DW,^D207
	JRST	ADVERA

;MNEMONIC-NAME WASN'T A CHANNEL

BADMNE:	MOVEI	DW,^D98

ADVERA:	HRRZ	TE,EOPNXT
	MOVEI	TE,-1(TE)
	MOVEM	TE,CUREOP
	PUSHJ	PP,OPNFAT
	JRST	RITGN3

;NOT WRITING A RECORD

NOTREC:	MOVEI	DW,^D206
	MOVE	TE,OPERND
	HRRZM	TE,CUREOP
	JRST	OPNFAT

;UNDEFINED DATA-NAME IN "ADVANCING"

UNDEFD:	MOVEI	DW,^D104
	JRST	ADVERA
;MISCELLANEOUS CONSTANTS

	ADVANC==1B27	;"ADVANCING" IN GENFIL OPERATOR
	AFTER==1B28	;"AFTER ADVANCING" IN GENFIL OPERATOR
	FROM==1B29	;"WRITE FROM" IN GENFIL OPERATOR
	INTO==1B27	;"READ INTO" IN GENFIL OPERATOR
	CONSOL==1B27	;"UPON" FOR DISPLAY, "FROM" FOR ACCEPT
	DELETF==1B30	;"WITH DELETE" IN GENFIL OPERATOR
	NOADV==1B28	;"WITH NO ADVANCING" IN 'DISPLAY' OPERATOR





CHANUM:	POINT 6,1(TA),35	;CHANNEL NUMBER IN MNETAB
ASCMOD==2


EXTERNAL CURDAT,EIOOP,LMASKB
EXTERNAL EINTO,ENDIFT,OPERND,ESAVAC,EAC,W1LN,W1CP,EPJPP
EXTERNAL EASRJ,EAZRJ,EAQRJ,ERECSZ
EXTERNAL EOPLOC,EOPNXT,TAGLOC,TAGCNT,UUOBIT,CURFIL,CUREOP,OPLINE
EXTERNAL ETEMPC,ELITPC,ESAVAC
EXTERNAL LITLOC,BYTE.W
EXTERNAL SUBCON, SXR, DSP.FP

EXTERNAL EBASEB,EMODEB,EDPLB,EINCRB,ESIZEB,ERESB
EXTERNAL EBASEA,EMODEA,EDPLA,EINCRA,ESIZEA,ERESA,EBASAX,EBASBX
EXTERNAL ESAVES, ESAVSB
EXTERNAL AS.FIL
EXTERNAL JRST.,ACEPT.,DSPLY.,RERIT.,WADV.
EXTERNAL PURGE.
EXTERNAL AS.TAG,AS.CNB,AS.MSC,AS.LIT,XWDLIT
EXTERNAL AS.XWD,D1MODE,D2MODE,D7MODE,EDMODE
EXTERNAL ATINVK,ATEND,INVKEY,SPIF.,TCCP,TCLN,EOPCOD

EXTERNAL LNKCOD,TB.DAT,TB.FIL,TB.MNE
EXTERNAL DA.LNC,DA.DEF,DA.USG,DA.NDP,DA.EXS,DA.BRO,DA.CLA,DA.EDT,DA.FAL
EXTERNAL FI.ACC,FI.ERM,FI.DRL,FI.RKY,FI.SKY

	END
    b@?§