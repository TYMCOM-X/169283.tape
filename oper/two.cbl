IDENTIFICATION DIVISION.
PROGRAM-ID. "PAYROL".
AUTHOR. BOB CLEMENTS.
INSTALLATION. SHELL DISTRICT.
REMARKS. ASSIGNMENT NUMBER 2.
ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
SOURCE-COMPUTER. PDP-10.
OBJECT-COMPUTER. PDP-10.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT YTDMAS ASSIGN TO DSK
    RECORDING MODE IS ASCII.
    SELECT PAYTRN ASSIGN TO DSK
    RECORDING MODE IS ASCII.
    SELECT NEWMAS ASSIGN TO DSK
    RECORDING MODE IS ASCII.
    SELECT PAYCHK ASSIGN TO DSK
    RECORDING MODE IS ASCII.
    SELECT PAYREP ASSIGN TO DSK
    RECORDING MODE IS ASCII.
I-O-CONTROL.
SAME RECORD AREA FOR NEWMAS, YTDMAS.
DATA DIVISION.
FILE SECTION.
FD PAYTRN
    VALUE OF ID IS "PAYTRNDAT"
    LABEL RECORDS ARE STANDARD.
01 EMPLOYEE-REC.
    03 SSN  PIC  9(9).
    03 LASTNAME PIC X(15).
    03 FIRSTNAME    PIC X(10).
    03 HOURS   PIC 99V9.
    03 RATE PIC 99V999.
FD YTDMAS
    VALUE OF ID IS "YTDMASHIS"
    LABEL RECORDS ARE STANDARD.
01 MASTER-IN.
    02 SSN PIC 9(9).
    02 EMPLOYEE-NO PIC 9(6).
    02 DEPT-NO PIC 9999.
    02 EXEMPTIONS PIC 99.
    02 YTD-GROSS PIC 9(5)V99.
    02 YTD-FIT PIC 9999V99.
    02 YTD-FICA PIC 999V99.
    02 YTD-INS PIC 999V99.
FD NEWMAS
    VALUE OF ID IS "NEWMASHIS"
    LABEL RECORDS ARE STANDARD.
01  NEWMAS-OUT PIC X(44).
FD PAYREP
    VALUE OF ID IS "PAYREPOUT"
    LABEL RECORDS ARE STANDARD.
01 PAYREP-REC PIC X(70).
FD PAYCHK
    VALUE OF ID IS "PAYCHKOUT"
    LABEL RECORDS ARE STANDARD.
01 PAYCHK-REC PIC X(70).
WORKING-STORAGE SECTION.
01 HEAD1.
  03 FILLER   PIC X(30)    VALUE SPACES.
  03 FILLER   PIC X(40)   VALUE "PAYROLL REPORT".
01 HEAD2.
  03 FILLER   PIC XX VALUE SPACES.
  03 FILLER   PIC X(7)  VALUE "SOC-SEC".
  03 FILLER   PIC X(10)  VALUE SPACES.
  03 FILLER   PIC X(5)  VALUE "FIRST".
  03 FILLER   PIC X(10)  VALUE SPACES.
  03 FILLER   PIC X(4)  VALUE "LAST".
  03 FILLER   PIC X(7)  VALUE SPACES.
  03 FILLER   PIC X(5)  VALUE "HOURS".
  03 FILLER   PIC XXX   VALUE SPACES.
  03 FILLER   PIC X(6)  VALUE "HOURLY".
  03 FILLER   PIC XXX  VALUE SPACES.
  03 FILLER   PIC X(5)  VALUE "GROSS".
01 HEAD3.
  03 FILLER   PIC X(2)   VALUE SPACES.
  03 FILLER   PIC X(6)   VALUE "NUMBER".
  03 FILLER   PIC X(11)   VALUE SPACES.
  03 FILLER   PIC X(4)   VALUE "NAME".
  03 FILLER PIC X(11)     VALUE SPACES.
  03 FILLER   PIC X(4)   VALUE "NAME".
  03 FILLER   PIC X(8)    VALUE SPACES.
  03 FILLER   PIC X(3)   VALUE "WKD".
  03 FILLER   PIC X(5)   VALUE SPACES.
  03 FILLER   PIC X(4)   VALUE "RATE".
  03 FILLER   PIC X(8)   VALUE "     PAY".
01 HEAD4.
  03 FILLER PIC X(30) VALUE "           FIT-WH        FICA-".
  03 FILLER PIC X(30) VALUE "WH       INS-WH     NET".
01 PAY-REC1.
  03 FILLER PIC X VALUE SPACE.
  03 SSN PIC 999B99B9999.
  03 FILLER PIC XXX VALUE SPACES.
  03 FIRSTNAME PIC X(10) VALUE SPACES.
  03 FILLER PIC X(5) VALUE SPACES.
  03 LASTNAME PIC X(15) VALUE SPACES.
  03 HOURS PIC ZZ.99.
  03 RATE PIC ZZZ9.999.
  03 GROSSPAY PIC ZZ,ZZZ.99.
01 PAY-REC2.
  03 FILLER PIC X(8) VALUE SPACES.
  03 FIT PIC ZZ,ZZZ.99.
  03 FILLER PIC X(6) VALUE SPACES.
  03 FICA PIC Z,ZZZ.99.
  03 INS PIC ZZ,ZZZ,ZZZ.99.
  03 NETPAY PIC Z,ZZZ,ZZZ.99.
01 CHECK-REC1.
  03 FILLER PIC X VALUE SPACE.
  03 CHK-NO-MSG PIC X(10) VALUE SPACES.
  03 CHK-NO  PIC Z(5)9 VALUE ZEROES.
  03 FILLER PIC X(18) VALUE SPACES.
  03 DATES-MSG PIC X(15) VALUE SPACES.
  03 CK-DATE PIC X(20) VALUE SPACES.
01 CHECK-REC2.
  03 FILLER PIC X(11) VALUE " PAY TO:   ".
  03 FIRSTNAME PIC X(15) VALUE SPACES.
  03 LASTNAME PIC X(31) VALUE SPACES.
  03 NETPAY PIC $*,***,***.99.
77 BEG-CHK-NO PIC 9(6) VALUE ZERO.
77 CURRENT-DATE PIC X(20) VALUE SPACES.
77 PAY-PERIOD PIC X(20) VALUE SPACES.
77 GROSS-W PIC S9999V99 VALUE ZEROES COMP.
77 FICA-W PIC S999V99 VALUE ZEROES COMP.
77 FIT-W PIC S9999V99 VALUE ZEROES COMP.
77 INS-W PIC S999V99 VALUE ZEROES COMP.
77 NET-W PIC S9999V99 VALUE ZEROES COMP.
PROCEDURE DIVISION.
BEGIN.
    DISPLAY "ENTER BEGINNING CHECK NUMBER: " WITH NO ADVANCING.
    ACCEPT BEG-CHK-NO.
    DISPLAY "ENTER CURRENT DATE: " WITH NO ADVANCING.
    ACCEPT CURRENT-DATE
    DISPLAY "ENTER ENDING PAY PERIOD DATE: " WITH NO ADVANCING.
    ACCEPT PAY-PERIOD.
    OPEN INPUT PAYTRN, YTDMAS
        OUTPUT NEWMAS, PAYREP, PAYCHK.
HEADING.
    WRITE PAYREP-REC FROM HEAD1 AFTER ADVANCING 4 LINES.
    WRITE PAYREP-REC FROM HEAD2 AFTER ADVANCING 2 LINES.
    WRITE PAYREP-REC FROM HEAD3 AFTER ADVANCING 1 LINES.
    WRITE PAYREP-REC FROM HEAD4 AFTER ADVANCING 2 LINES.
READ-PROCESS.
    READ YTDMAS AT END GO TO FINISHUP.
    READ PAYTRN AT END GO TO FINISHUP.
FIGURE-PAY.
    COMPUTE GROSS-W ROUNDED = HOURS IN EMPLOYEE-REC * RATE IN
        EMPLOYEE-REC.
    COMPUTE FICA-W ROUNDED = GROSS-W * .04.
    COMPUTE FIT-W ROUNDED = (GROSS-W - (EXEMPTIONS * 13)) * .15.
    COMPUTE INS-W = 5.4 * EXEMPTIONS.
        IF INS-W > 14.00, MOVE 14.00 TO INS-W.
    COMPUTE NET-W = GROSS-W - FICA-W - INS-W - FIT-W.
BUILDPAY.
    MOVE CORRESPONDING EMPLOYEE-REC TO PAY-REC1.
    MOVE CORRESPONDING EMPLOYEE-REC TO CHECK-REC2.
    MOVE GROSS-W TO GROSSPAY.
    MOVE NET-W TO NETPAY IN PAY-REC2, NETPAY IN CHECK-REC2.
    EXAMINE SSN IN PAY-REC1 REPLACING ALL " " BY "-".
    WRITE PAYREP-REC FROM PAY-REC1 AFTER ADVANCING 2 LINES.
    MOVE FIT-W TO FIT.
    MOVE FICA-W TO FICA.
    MOVE INS-W TO INS.
    WRITE PAYREP-REC FROM PAY-REC2 AFTER ADVANCING 1 LINE.
CHECK-WRITER.
    MOVE "CHECK NO. " TO CHK-NO-MSG.
    MOVE BEG-CHK-NO TO CHK-NO.
    MOVE "CURRENT DATE: " TO DATES-MSG.
    MOVE CURRENT-DATE TO CK-DATE.
    WRITE PAYCHK-REC FROM CHECK-REC1 AFTER ADVANCING 5 LINES.
    MOVE "EMP NO.   " TO CHK-NO-MSG.
    MOVE EMPLOYEE-NO TO CHK-NO.
    MOVE "PAY PERIOD:   " TO DATES-MSG.
    MOVE PAY-PERIOD TO CK-DATE.
    WRITE PAYCHK-REC FROM CHECK-REC1 AFTER ADVANCING 1 LINE.
    WRITE PAYCHK-REC FROM CHECK-REC2 AFTER ADVANCING 3 LINES.
    ADD 1 TO BEG-CHK-NO.
BUILD-MASTER.
    ADD GROSS-W TO YTD-GROSS.
    ADD FIT-W TO YTD-FIT.
    ADD FICA-W TO YTD-FICA.
    ADD INS-W TO YTD-INS.
    WRITE NEWMAS-OUT FROM MASTER-IN.
    GO TO READ-PROCESS.
FINISHUP.
    CLOSE PAYTRN, YTDMAS, NEWMAS, PAYREP, PAYCHK.
    STOP RUN.
