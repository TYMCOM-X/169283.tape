10 PRINT "FOR HEADING TYPE 0";
20 INPUT Q
30 LET N5=8
35 LET N4=8
40 RANDOMIZE
50 IF Q<>0 THEN 400
60 PRINT "CONGRATULATIONS! YOU'VE BEEN MADE PREMIER OF SETATS DETINU,"
70 PRINT "A SMALL COMMUNIST ISLAND 30 BY 70 MILES LONG. YOUR JOB IS TO"
80 PRINT "DECIDE THE COUNTRY'S BUDGET AND DISTRIBUTE MONEY TO YOUR"
90 PRINT "COUNTRYMEN FROM THE COMMUNAL TREASURY.";
100 PRINT "THE MONEY SYSTEM IS RALLODS, AND EACH PERSON NEEDS 100"
110 PRINT "RALLODS PER YEAR TO SURVIVE. YOUR COUNTRY'S INCOME COMES"
120 PRINT "FROM FARM PRODUCE AND TOURISTS VISITING YOUR MAGNIFICIENT"
130 PRINT "FORESTS, HUNTING, FISHING, ETC. HALF YOUR LAND IS FARM LAND"
140 PRINT "WHICH MAY ALSO BE SOLD TO FOREIGN INDUSTRY WHO IMPORT AND"
150 PRINT "SUPPORT THEIR OWN WORKERS. "
160 PRINT "CROPS COST BETWEEN 10 AND 15 RALLODS PER SQ. MILE TO PLANT."
170 PRINT "CROPS BRING BETWEEN 45 AND 65 RALLODS PER SQ. MILE."
180 PRINT "INDUSTRY WILL BUY LAND FOR 95 TO 105 RALLODS PER SQ. MILE."
190 PRINT "INDUSTRY MUST OWN 2 SQ. MILES OF LAND BEFORE"
200 PRINT "YOU GET ANY TOURIST TRADE. INCOME FROM TOURIST TRADE WILL"
210 PRINT "INCREASE UNTIL 26 SQ. MILES BELONG TO INDUSTRY."
220 PRINT "INDUSTRY PAYS TAXES OF 44 RALLODS PER SQ. MILE OF LAND."
230 PRINT "INDUSTY ALSO BRINGS POLLUTION. THE NUMBER OF UNITS OF"
240 PRINT "POLLUTION PRODUCED IS EQUAL TO THE SQUARE OF THE NUMBER OF"
250 PRINT "SQ. MILES OF LAND INDUSTRY OWNS. POLLUTION MAY BE COMBATTED"
260 PRINT "BY SPENDING .44 RALLODS PER UNIT OF POLLUTION. POLLUTION"
270 PRINT "DECREASES THE TOURIST TRADE AND KILLS YOUR COUNTRYMEN"
280 PRINT "YOU CAN INCREASE THE CROP YIELD THROUGH EDUCATION. THE"
290 PRINT "VALUE OF CROPS IS DEPENDENT ON THE AMOUNT OF MONEY USED FOR"
300 PRINT "EDUCATION IN THE LAST 3 YEARS. 10 RALLODS PER PERSON IS THE"
310 PRINT "MAXIMUM USEFUL AMOUNT TO SPEND ON EDUCATION."
320 PRINT "YOUR GOAL IS TO COMPLETE YOUR";N4;"YEAR TERM OF OFFICE."
400 PRINT "GOOD LUCK!"
410 LET M=INT(60000+(RND(1)*1000)-(RND(1)*1000))
420 LET P=INT(500+(RND(1)*10)-(RND(1)*10))
430 LET L=2000
440 LET W=0
445 LET X2=0
450 LET E0=0
455 LET X1=0
460 LET E1=0
465 LET S9=0
470 LET S0=0
475 LET M5=0
480 LET S1=0
490 D1=INT(P/3)
500 LET L1=INT((RND(1)*5)+10)
510 LET L0=INT(RND(1)*10+95)
520 GOSUB 6000
530 PRINT "YOU HAVE";M;"RALLODS IN THE TREASURY."
540 PRINT P;"COUNTRYMEN,";
550 IF W=0 THEN 570
560 PRINT W;"FOREIGN WORKERS,";
570 PRINT " AND";L;"SQ. MILES OF LAND."
580 PRINT "THIS YEAR INDUSTRY WILL BUY LAND FOR";L0;"RALLODS/SQ. MILE"
590 PRINT "LAND CURRENTLY COSTS";L1;"RALLODS/SQ. MILE TO PLANT"
600 IF S1=0 THEN 620
610 PRINT "POLLUTION LEVEL IS";S1;"UNITS"
620 GOSUB 6000
630 PRINT "HOW MANY SQ. MILES DO YOU WISH TO SELL TO INDUSTRY";
640 INPUT L2
650 IF L2<0 THEN 630
660 IF L2<L-1000 THEN 730
670 PRINT "  THINK AGAIN, YOU'VE ONLY";L-1000;"SQ. MILES OF FARM LAND"
680 IF X1<>0 THEN 630
690 PRINT "(FOREIGN INDUSTRY IS NOT INTERESTED IN FOREST LAND BECAUSE"
700 PRINT "OF THE GREATER DEVELOPMENT COST)"
710 LET X1=1
720 GO TO 630
730 LET M=INT(M+L2*L0)
740 LET L=INT(L-L2)
745 LET X9=INT (44*(2000-L))
750 LET M2=0
760 LET M3=0
770 LET M4=0
780 PRINT "HOW MANY RALLODS DO YOU WISH TO DISTRIBUTE TO";
785 PRINT " YOUR COUNTRYMEN";
790 INPUT M1
800 IF M1<0 THEN 780
810 IF M1<=M THEN 840
820 GOSUB 6030
830 GO TO 780
840 LET M=INT(M-M1)
850 IF M=0 THEN 3000
860 PRINT "HOW MANY SQ. MILES DO YOU WISH TO PLANT";
870 INPUT L3
880 IF L3<0 THEN 860
890 IF L3<=P*2 THEN 920
900 PRINT "  SORRY, BUT EACH COUNTRYMAN CAN ONLY PLANT 2 SQ. MILES"
910 GO TO 860
920 IF L3<=L-1000 THEN 950
930 PRINT "  SORRY, BUT YOU ONLY HAVE";L-1000;"SQ. MILES OF FARM LAND."
940 GO TO 860
950 LET M4=INT(L3*L1)
960 IF M4<=M THEN 990
970 GOSUB 6030
980 GO TO 860
990 LET M=INT (M-M4)
1000 IF M=0 THEN 3000
1010 PRINT "HOW MUCH DO YOU WISH TO SPEND FOR EDUCATION";
1020 INPUT M2
1030 IF M2<0 THEN 1010
1040 IF M2<=M THEN 1070
1050 GOSUB 6030
1060 GO TO 1010
1070 LET M=INT (M-M2)
1080 IF M=0 THEN 3000
1090 IF L=2000 THEN 3000
1100 PRINT "HOW MUCH DO YOU WISH TO SPEND TO COMBAT POLLUTION";
1110 INPUT M3
1120 IF M3<0 THEN 1100
1130 IF M3<=M THEN 1160
1140 GOSUB 6030
1150 GO TO 1100
1160 LET M=INT (M-M3)
3000 GOSUB 6000
3010 LET D0=0
3015 LET E2=M2/P
3020 LET D2=INT(P-M1/100)
3030 IF D2<=0 THEN 3060
3040 LET D0=D2
3050 PRINT D2;"COUNTRYMEN DIED OF STARVATION"
3060 LET D3=INT((S1/100000)*(.1+.4*RND(1))*P)
3070 IF D3<=0 THEN 3100
3080 PRINT D3;"COUNTRYMEN DIED FROM EXCESS POLLUTION."
3090 LET D0=D0+D3
3100 IF D0<=0 THEN 3220
3110 F=D0*9
3120 PRINT "YOU WERE FORCED TO SPEND";F;"RALLODS FOR FUNERAL EXPENSES"
3130 LET M=INT(M-F)
3140 IF M>=0 THEN 3220
3150 PRINT "INSUFFICIENT RESOURCES FORCED SALE OF LAND"
3160 LET T0=-INT((M-L0+1)/L0)
3170 LET L=L-T0
3180 IF L>=1000 THEN 3210
3190 PRINT "NOT ENOUGH LAND TO SELL"
3200 GO TO 7000
3210 LET M=M+T0*L0
3220 IF D0>200 THEN 7100
3230 LET P=P-D0
3240 LET D1=D1-D0
3250 IF D1<0 THEN 7300
3260 IF D2<=2 THEN 3280
3270 IF M>500 THEN 7800
3280 IF L2=0 THEN 3340
3290 LET T0=INT (L2+L2*RND(2)*2)
3300 IF W=0 THEN 3320
3310 LET T0=INT(T0+.1*W)
3320 PRINT T0;"WORKERS CAME TO THE ISLAND AND"
3330 LET W=W+T0
3340 LET T0=INT(((500-P)/10-D3/3-D2/5)*.75*(1+RND(1)))
3350 PRINT ABS(T0);"COUNTRYMEN ";
3360 IF T0<0 THEN 3390
3370 PRINT "CAME TO";
3380 GO TO 3400
3390 PRINT "LEFT";
3400 PRINT " THE ISLAND."
3410 LET P=P+T0
3420 IF P<W THEN 7700
3430 LET T0=0
3440 IF L=2000 THEN 3500
3450 LET T0=INT((S1/100000)*L3)
3460 IF T0<=L3 THEN 3480
3470 LET T0=L3
3480 IF T0=0 THEN 3500
3490 PRINT "OF";L3;"SQ. MILES PLANTED, ";
3500 PRINT "YOU HARVESTED";L3-T0;"SQ. MILES OF CROPS."
3510 IF T0=0 THEN 3580
3520 IF X2>=2 THEN 3580
3530 PRINT "   (DUE TO ";
3540 IF X2=0 THEN 3560
3550 PRINT "INCREASED ";
3560 PRINT "POLLUTION LEVEL)"
3570 LET X2=X2+1
3580 LET T1=INT((39+RND(1)*20)*(1+.25*(E0+E1)/20))
3590 PRINT "  MAKING";INT(T1*(L3-T0));"RALLODS."
3600 LET M=M+INT(T1*(L3-T0))
3610 LET E0=E1
3620 IF E2<=10 THEN 3640
3630 LET E2=10
3640 LET E1=E2
3650 LET T0=2000-L
3660 IF T0<2 THEN 4090
3670 IF T0<=26 THEN 3700
3690 LET T0=26
3700 LET T0=INT(T0*500*(.52+.5*RND(1)))
3800 LET T1=S1/100000
3900 IF T1<=1 THEN 3920
3910 LET T1=1
3920 LET T1=T1*T0
3930 PRINT "YOU MADE";INT(T0-T1);"RALLODS FROM TOURIST TRADE"
3940 LET M=M+INT(T0-T1)
3950 IF INT(T0-T1)>=M5 THEN 4080
3955 IF S1<=S9 THEN 4080
3960 PRINT "  DECREASE BECAUSE ";
3970 LET T2=RND(1)*10
3980 ON (T2/2)+1 GO TO 3990,4010,4030,4050,4070,4070
3990 PRINT "FISH POPULATION HAS DWINDLED DUE TO WATER POLLUTION"
4000 GO TO 4080
4010 PRINT "AIR POLLUTION IS KILLING GAME BIRD POPULATION"
4020 GO TO 4080
4030 PRINT "MINERAL BATHS ARE BEING RUINED BY WATER POLLUTION"
4040 GO TO 4080
4050 PRINT "UNPLEASANT SMOG IS DISCOURIGING SUN BATHERS"
4060 GO TO 4080
4070 PRINT "HOTELS ARE LOOKING SHABBY DUE TO SMOG GRIT"
4080 LET M5=INT(T0-T1)
4085 LET S9=S1
4090 LET S2=(2000-L)^2-M3/.44
4095 PRINT "YOU RECIEVED";X9;"RALLODS FROM TAXES ON INDUSTRY"
4100 IF S2>=0 THEN 4150
4110 LET S1=INT(S1+S2/2)
4120 IF S1>=S0 THEN 4140
4130 LET S1=S0
4140 GO TO 4170
4150 LET S1=INT (S1+S2)
4160 LET S0=INT (S0+S2/10)
4170 LET N5=N5-1
4180 IF N5>0 THEN 500
4190 GO TO 8000
6000 PRINT
6010 PRINT
6020 RETURN
6030 PRINT "  SORRY, YOU ONLY HAVE";M;"RALLODS IN THE TREASURY"
6040 RETURN
7000 PRINT "YOU HAVE BEEN THROWN OUT OF OFFICE AND YOU ARE NOW"
7010 PRINT "RESIDING IN PRISON."
7020 STOP
7100 PRINT D0;"COUNTRYMEN DIED IN ONE YEAR!!!!!"
7110 PRINT "DUE TO THIS EXTREME MISMANAGEMENT YOU HAVE NOT ONLY"
7120 PRINT "BEEN IMPEACHED AND THROWN OUT OF OFFICE BUT YOU"
7130 LET T0=INT(RND(1)*2.01)+1
7140 ON T0 GO TO 7150,7170,7190
7150 PRINT "ALSO HAD YOUR LEFT EYE GOUGED OUT."
7160 STOP
7170 PRINT "HAVE ALSO GAINED A VERY BAD REPUTATION."
7180 STOP
7190 PRINT "HAVE ALSO BEEN DECLARED NATIONAL FINK."
7200 STOP
7300 PRINT "OVER ONE THIRD OF THE POPULATION HAS DIED SINCE YOU"
7310 PRINT "WERE ELECTED TO OFFICE. THE PEOPLE (REMAINING)"
7320 PRINT "HATE YOUR GUTS."
7330 GO TO 7500
7500 IF RND(1)<=.5 THEN 7000
7510 PRINT "YOU HAVE BEEN ASSASINATED."
7520 STOP
7700 PRINT "THE NUMBER OF FOREIGN WORKERS HAS EXCEEDED THE NUMBER"
7710 PRINT "OF COUNTRYMEN. AS A MAJORITY THEY HAVE REVOLTED AND"
7720 PRINT "TAKEN OVER THE COUNTRY."
7730 GO TO 7500
7800 PRINT "MONEY WAS LEFT OVER IN THE TREASURY WHICH YOU DID NOT"
7810 PRINT "SPEND. AS A RESULT SOME OF YOUR COUNTRYMEN DIED OF"
7820 PRINT "STARVATION. THE PUBLIC IS ENRAGED AND YOU HAVE BEEN"
7830 PRINT "FORCED TO EITHER RESIGN OR COMMIT SUICIDE."
7840 STOP
8000 PRINT "CONGRATUALTIONS!"
8010 PRINT "YOU HAVE SUCCESSFULLY COMPLETED YOUR";N4;"YEAR TERM"
8020 PRINT "OF OFFICE. YOU HAVE DONE SUCH A GOOD JOB THAT YOU MIGHT"
8030 PRINT "WANT TO RUN FOR ELECTION AGAIN THIS TIME."
8040 PRINT "TYPE 0 IF YOU DO"
8050 INPUT Q
8055 LET N5=N4
8060 IF Q=0 THEN 490
8070 STOP
9000 END
 