;ROUTINE NAME:		SUDLIN.X22	(SUDS.SHR)
;VERSION:		003/000
;AUTHOR:		DENNIS R. PRIVITERA
;LOCATION:		TAPE 372
;STATUS:		ACTIVE
;COMMENTS:
;
IFDEF REENT,<
TITLE	SIMPLE USER DIALOG SYSTEM (SUDS)
>
IFNDEF REENT,<
TITLE	SIMPLE USER DIALOG SYSTEM (SUDS)......SHARABLE / VERSION
>
SUBTTL	D.R. PRIVITERA/	10/8/69	V.003
;
;
	PAGE
	IFNDEF REENT,<REENT=1>
	IFNDEF TEST,<TEST=0>
	IFNDEF TELCOM,<TELCOM=0>
	IFNDEF COBOLL,<COBOLL=0>
;
	T1=1		;TEMPORARY ACCUMULATORS
	T2=2
	T3=3
	T4=4
	FLAG=5		;FLAG ACCUMULATOR
	FL=FLAG		;ANOTHER NAME FOR FLAG
	CH=6
	IOPNT=7			;IO POINTER USED BY CHAR SUBROUTINE
	CP=10			;COMMAND POINTER
	P=11			;PUSH DOWN POINTER
	OFLAG=12		;USED IN NEW OND OLDCOD ROUTINE
	MSGPNT=13		;MESSAGE POINTER
	SIXTXT=14
	ORDPNT=15		;POINTER TO ORDERS FOR LINED
;
;
;	FLAG DEFINITIONS
;	'FLAG'(LH)
;
	PTPIN=400000		;=1 WHEN IN PAPER TAPE INPUT MODE.
	STACKM=200000		;=1 WHEN IN LINE STACK PROCESSING MODE
	COMPFL=100000		;=1 IN COMPILE MODE	/ =0 IN RUN MODE
	COMTST=10000		;SET WHILE IN AN INPUT RECHKING MODE FOR UNIQUE INPUTS
	LINCK=40000		;=1 WHEN CHECKING A 'FIND' LINE #
	EDITFL=20000		;=1 WHEN TRANSLATING A 'FIND' COMMAND
	MORE=10000		;FLAG FOR MORE COMMANDS TO PROCESS
	DELET=4000		;DELETE FLAG
	EXTEN=2000		;EXTENSION FLAG
	DEVICE=1000		;DEVICE FLAG
	ALTDET=400		;ALTMODE DETECTED FLAG
	CCLIN=200		;ENTRY FROM CCL
	LOGIN=100		;ENTRY FROM LOGIN
	TTYIN=40		;TTY ENTRY
	NEWT=20			;NEW COMMAND TYPED
	OLDT=10			;OLD COMMAND TYPED
	MRGMOD=2	;SET TO 1 WHEN IN FILE MERGE MODE
	DASH=1		;DASH FLAG FOR LISTER
;
;
;	FLAG DEFINITIONS
;	'FLAG' (RH)
;
	GPFLAG=400000		; GENERAL PURPOSE
	LINMOD=200000		;=1 WHEN IN LINED COMMAND MODE
	UNSAVF=100000		;=1 WHEN IN UNSAVE MODE
	SCRTCH=40000		;=1 WHEN IN SCRATCH MODE
	REPROC=20000		;=1 WHEN IN RENAME MODE
	REPLAC=10000		;=1 WHEN IN REPLACE MODE
	NOREL=4000		;=1 IF NO PERMANENT REL FILE EXISTS FOR C(FILE)
	SEQMOD=2000		;=1 WHEN IN SEQUENCE MODE
	RNP=1000		;=1	WHEN A RUN COMMAND HAS NO PARAMETERS
	DLTALT=400		;USED	WHEN $ TO ALTMODE CONVERSION WANTED
	RF=200			;=1 WHEN WHEN IN REPITTION CALCULATION
	WAITFL=100		;=1 WHEN 'FIND'/WAIT MODE REQ'
	LISFLG=40		;=1	"	" /LIST	"	"
	REPT=20			;=1	"	" /REPITITION IS SEEN
	SECDRP=10		;=1 'FIND'/REPLACE MODE
	SECINS=4		;=1	"	/INSERT	"
	SECDEL=2		;=1	"	/DELETE	"
	LINE=1			;=1	LINE # IN FIND COMMAND 
;
;
;
;
	EXTERNAL	JOBSA,JOBREN
;
;	DATA CHANNELS
;
	DSKCC=1		;DISK DATA COMMAND CHAN
	TTYCC=2		;TTY DATA COMMAND CHAN
	DSKOCH=3		;CCL OUTPUT CHANNAL
	TSTCHN=7	;TESTER CHANNAL
	SYSCHN=6		;TMP LOOKUP CHANNAL
	SRCCHN=15		; ###SRC.TMP CHAN FOR LINE MODE PROC.
	MRGCHN=16		; MERGE FILE CHANNAL FOR LINE MERGING
	SROCHN=17		; MERGE FILE OUTPUT CHANNAL
;
;
	ALTMOD=33	;ASCII ALTMODE CODE
	RESET=0
	DEVCHR=4
;
;
;
	DEFINE FLUSH (LOC,SIZE)
	<SETZM	LOC
	 MOVE	T1,[XWD LOC,LOC+1]
	 BLT	T1,LOC+^D<SIZE>-1
	>
	DEFINE MOVBLK (LOC,LOC1,SIZE)<
		MOVE T1,[XWD LOC,LOC1]
		BLT T1,LOC1+<SIZE>-1
		>
	PAGE
	VSUDS=006
;
	INTERNAL	VSUDS
	LOC	137
	EXP VSUDS
	RELOC	0
	HISEG
;
;
;********************************************************************
;
;
; ENTRY BEGINS .....GOOD SAILING
;
EXTERN EDIBF1,EDOBF2
EXTERN INILEN,DATLEN
EXTERN TYIBF1,TYIBF2,EDOBF1,EDIBF2,CCLBF1,CCLBF2,TYOBF1,TYOBF2
EXTERN ORDERS,CUSP,SYSNM.,FILNM.,DEVNM.,SYSTMP,BBBTMP,TMPBLK,BASLST
EXTERN ENDX,COMBUF,IDSK,ITTY,PDX,COMWRD,DEVC,IOLST,OUTBLK,CCLDCH
EXTERN OTBUF,TEMPN,JOBN,IMEDRN,RECOVR,SSTRNG,ISTRNG,BEGLIN,REPTCT
EXTERN DELCNT,POINTR,FILEN,EXTN,RCSYS,INPFIL,SYSCOD,FRSTSEQ
EXTERN STSEQ,ENDSEQ,SEQINC,NEWSEQ,STRSAV,NXTLOC,LNCCNT,ISRCB
EXTERN OSRCB,IMRGB,OMRGB,SMLLIN,SVJOBF,LSIZE,NXTSN,NXTSN1,XILB
EXTERN XILB1,TABLE
EXTERN TYIBF,EDOBF,EDIBF,CCLBF,TYOBF,MOV1X,MOV2X,M1X,M2X,DCOM2X
EXTERN PDCOMX,FILL1X,FILL2X,BLTX,LNCNTY,CCLHRX,SBERR,SBERR1,LERRM
EXTERN LERRN,ERROR,INWRIT,PSAVE,FREWDS,PNTR,WRTLST,STPSIZ,PDLPNT
EXTERN SVJBFF,OUTWRD,SEQLST,BKNAM,IFILNM,IDIRC,IFILXT,OFILNM
EXTERN ODIRC,ZDIRC,ZILCH,CCLDIR,PUT.X,GETPOX,LINES,PDL
START:	SETO T4,	;COMPIL ENTRY
	JRST STARTC
STARTA: TDZA T4,T4	;SUDS ALREADY IN PROGRESS
STARTB: MOVEI T4,1	;HELLO ENTRY
STARTC:	HRROI T1,6
	CALLI	T1,41		;MAKE SURE HE IS IN SUDS MODE..
	JFCL
	TRNN	T1,600		;IS SUDS MODE BIT SET?
	JRST	[TTCALL 3,[ASCIZ /YOU ARE NOT IN SUDS MODE..
PLEASE TYPE	"HELLO"

/]
	CALLI 1,12	]
		;NO..
	MOVE	P,PDP		;INIT PUSH DOWN POINTER
	EXTERN INILOW,LOWTOP
;
	MOVE	T1,[XWD INIDAT,INILOW]	;LOAD CONSTANTS IN LOW SEG
	BLT	T1,INILOW+LOWBLK
;	MOVEI	T1,LOWTOP+1
;	HRLM	T1,JOBSA		;DEFINE TOP OF LOW SEGMENT IN JOB DATA AREA
;
	CALLI	RESET		;A HEALTHY PRACTICE
	SETZM	FL		; CLEAR FLAG
	HRRZI	T1,PHAS7	;SET REENTER ADDRESS
	MOVEM	T1,JOBREN
	CAIN	T4,0		;SET FLAGS
	TLO	FL,TTYIN
	CAIN	T4,-1
	TLO	FL,CCLIN
	CAIN	T4,1
	TLO	FL,LOGIN
START1:	CALL	,[SIXBIT /PJOB/]	;SET UP ###SYS.TMP FILE HEADER
	MOVEI	T3,3
	IDIVI	,^D10
	ADDI	T1,"0"-40
	LSHC	T1,-6
	SOJG	T3,.-3
	HLLM	T2,TMPBLK
	HLLM	T2,SYSTMP
	HLLM	T2,TEMPN
	HLLZM	T2,JOBN
	MOVE	T1,[XWD 1,CORLST]	;TRY TO READ INCORE SYSTMP
	CALLI	T1,44
	JRST	.+2		;INCORE SYSTMP DOES NOT EXIST TRY DISK FILE
	JRST	SYSIN		;ALL SET ..PROCEDE
SYSFIL:	INIT	SYSCHN,17	;INIT ###SYS.TMP FILE
	SIXBIT 	/DSK/
	0
	HALT
	LOOKUP	SYSCHN,SYSTMP
	JRST	MAKTMP
	INPUT	SYSCHN,IOLST
	JRST	SYSIN
;
;
;
CORLST:	SIXBIT /SYS/
	IOWD	^D11,CUSP
;
MAKTMP:	MOVE	T1,[XWD 3,CORLST]	;TRY TO WRITE INCORE SYSTMP
	CALLI	T1,44
	JRST	.+2		;TRY TO WRITE IT ON DISK INSTEAD.
	JRST	MAKTX		;	ALL SET .. PROCEED
	ENTER	SYSCHN,SYSTMP
	HALT
MAKTX:	MOVEI	T1,-1		;SET FIRST ENTRY FLAG
	HRLZM	T1,SYSNM.	;SAV IT

;
SYSIN:	SETZM	FILNM.+3	;ZERO PROJ/PROG NUMBER 
	HLLZS	FILNM.+1	;ZERO GARBAGE IN RIGHT HALF OF EXT.
	MOVE	0,CUSP
	TRNE	0,1		;PTPIN MODE?
	TLO	FL,PTPIN	;YES
	JUMPGE	T4,INITTY		; TTY ENTRY
	TTCALL 10,0	;RESCAN
	TLZ FL,CCLIN	;TURN OFF
INITTY:
	INIT TTYCC,1
	SIXBIT	/TTY/
	XWD	,ITTY
	JRST	.-3
	INBUF	TTYCC,1
	TLNE	FL,LOGIN	;SPECIAL LOGIN ENTRY
	JRST	LOGE		;LOGIN ENTRY
	;
INIT.1:	MOVE	CP,[POINT 7,COMBUF]
	SETZM COMBUF
	MOVE T1,[XWD COMBUF,COMBUF+1]
	BLT T1,COMBUF+17
	MOVEI	IOPNT,TTYCC
	MLON
SECB:	PUSHJ	P,SELECT	;GET A COMMAND CHARACTER (REGULAR MODE)

	CAIN	CH,"_"		;IS IT A RUBOUT
	JRST	[CAMN CP,[POINT 7,COMBUF]
		 JRST RUBLIN+1
		 CAMN CP,[POINT 7,COMBUF-1,34]
		 JRST RUBLIN+1
		 MOVE T1,CP
		 PUSHJ	P,RUBOUT
		 MOVEM T1,CP
		 JRST	SECB]	
	MLOFF
	CAIN	CH,175		;ALTMOD?
	JRST	RUBLIN		; YES THEN DELETE LINE
	CAIN	CH,15
	JRST	SECB
	CAIN	CH,12
	MOVEI	CH,33
	MOVE	T1,CH
	CAILE	T1,140		;MAKE LOWER CASE UPPER WHEN SEEN
	CAILE	T1,172
	SKIPA
	SUBI	T1,40
	IDPB	T1,CP		;SAVE THE CHAR
	CAIE	T1,33		;DID WE FINISH
	JRST	SECB		;NO NOT YET ..CONTINUE
	MOVEI	T1,177		;YES .LOAD EOF MARKER
	IDPB	T1,CP
	IDPB	T1,CP		;JUST TO MAKE SURE
	CLOSE TTYCC,0
	MOVE	T1,[XWD 700,0]
	MOVEM	T1,ITTY+1
	JRST	DECODR		;REAL ACTION STARTS
;
;
CCLENT:	MOVE	CP,[POINT 7,COMBUF]
	MOVEI	IOPNT,DSKCC
	PUSHJ	P,DSKET
	JRST	DECODR		;START UP COMMAND DECODE PROCESS
;
LOGE:	MOVEI	IOPNT,TTYCC
	JRST	LOGENT
;
;
;
; CODE TO BACKSPACE LXFR POINTER BY (1 CHARACTER)
; AND SET DELETED CHAR POSITION TO ZERO UNLESS ALREADY AT BEGINNING OF LINE
; . MUST BE A [POINT 7,N,-] FORM POINTER
; . USES AND DESTROYS REGISTERS T1,T2,T3,T4
; . CALL	PUSHJ P,RUBOUT	WITH POINTER TO BE BACKSPACED IN T1
; . IF AT BEGINNING OF LINE NO BAKSPACING OCCURS
;
; . NEW POINTER RETURNED IN T1
;
RUBOUT:
BKSPCE:	SETZM	CH
	DPB	CH,T1		;CLEAR CHAR POSITION DELETED
	LDB	T2,P.P		;GET POSITION POINTER FROM BYTE POINTER
	ADDI	T2,7		;MOVE IT BACKWORD
	CAIL	T2,^D36		;TIME TO MOVE BACK A WORD?
	JRST	BKWRD		;YES
BRT:	DPB	T2,P.P	;RESET POINTER
	POPJ	P,		;RETURN
	;
BKWRD:	MOVEI	T2,1		;SET 'P' TO 1
	SUBI	T1,1		;BACKUP ONE WORD
	JRST	BRT
;
P.P:	POINT 7,T1,5		;BYTE POSITION POINTER
;
;
RUBLIN:	TTCALL	3,[ASCIZ . DELETED
.]
	MOVE	CP,[POINT 7,COMBUF]
	JRST	SECB
;
XOFF:	MOVEI	0,23
	JRST .+2
XON:	MOVEI	0,21
	TTCALL	1,0
	POPJ	P,
;
PTPINM:	TTCALL	14,0	;START READER IF NO LINE WAITING
	PUSHJ	P,XON
	TTCALL	4,CH		;GET A CHAR FROM INPUT BUFFER
	AOS	(P)		;RETURN TO CALL +2
	POPJ	P,
	PAGE
;	SUBROUTINE TO LOAD COMMAND STRING FROM ANY CHANNAL INPUT
;	INTO A N INTERNAL BUFFER AREA SPECIFIED	BY THE POINTER
;	IN 'CP'
;	'IOPNT' CONTAINS THE CHANNAL TO BE INPUT FROM
;
DSKET:	TLZ	FL,ALTDET		;CLEAR ALTMODE DETECTION FLAG
	PUSHJ	P,SELECT	;GET A COMMAND CHAR
	CAILE CH,140	;TEST FOR LOWER CASE
	CAILE CH,172
	SKIPA	;NO
	SUBI CH,40	;CONVERT TO UPPER
	CAIE	CH,ALTMOD
	CAIN	CH,15		; CARRIAGE RETURN?
	MOVEI	CH,33		;LOAD EOF MARKER
	IDPB	CH,CP
	CAIE	CH,33		;FINISH YET
	JRST	DSKET		;NOT YET
	PUSHJ	P,SELECT	;SKIP OVER LF
	MOVEI	CH,177		;LOAD EOF MARKER
	IDPB	CH,CP
	IDPB	CH,CP		;JUST TO MAKE SURE
	POPJ	P,		;RETURN WITH STRING IN SPECIFIED AREA
;
;	COMMAND HANDLER SUBROUTINE
;	RETURN A COMMAND WORD IN 'COMWRD' IN SIXBIT CODE.
;	LEADING BLANKS ARE IGNORED.
;	WORD DELIMITERS ARE 'BLANK', ':', OR '.' AND COMMA
;	CALL	MOVE CP,[7,@STRING ADDRESS@]
;		PUSHJ	P,DECOD
;
DECOD:	MOVE	T2,[POINT 6,COMWRD]	;OUTPUT POINTER
	SETZM	COMWRD			;CLEAR ACCUMULATOR WORD
	SETZM	T3			;CLEAR TRAILING BLANKS FLAG
;
GETCOM:	TLNE	FL,ALTDET		;WAS ALTMODE LAST CHAR DETECTED
	POPJ	P,		;LOOKS THAT WAY. JUST RETURN
	ILDB	CH,CP		;GET A COMMAND CHARACTER
	CAIN	CH," "		;BLANK?
	JRST	BLANK		;FIND OUT IF ITS LEADING OR TRAILING
	CAIN	CH,":"
	POPJ	P,
	CAIN	CH,";"
	POPJ	P,
	CAIN	CH,"?"
	POPJ	P,
	CAIN	CH,"/"
	POPJ	P,
	CAIN	CH,"."
	POPJ	P,
	CAIN	CH,"+"
	POPJ	P,
	CAIN	CH,"-"		;DASH SEPARATER FOUND IN LISTER
	POPJ	P,
	CAIN	CH,","
	POPJ	P,
	CAIN	CH,"["		;IS IT [, PRECEDING PROG/PROGRAM "?
	POPJ	P,
	CAIN	CH,"]"		;IS IT ],TERMINATING PPN AND COMMAND?
	JRST ALTCHK		;YES. SET TERMINATION FLAG
	CAIN	CH,ALTMOD	;IS CHAR AN ALTMODE
	JRST	ALTCHK
	TLNN	T2,770000	;SIX STORED YET?
	JRST	ENDIT
	SUBI	CH,40		;MAKE CHARACTER SIXBIT
	IDPB	CH,T2
	SETOM	T3
	JRST	GETCOM		;GET ANOTHER
BLANK:	SKIPL	T3		;TRAILING BLANK?
	JRST	GETCOM		;NO
	POPJ	P,
;
ENDIT:	ILDB	CH,CP
	CAIN	CH," "		;IS CHAR A BLANK
	POPJ	P,		;THEN GO AWAY
	CAIN	CH,"]"		;] TERMINATOR?
	JRST 	ALTCHK		;YES. TREAT AS TERMINATOR
	TLNN	FL,EDITFL	;IN EDIT MODE
	JRST	.+3
	CAIN	CH,"?"		;SPECIAL REQUIRMENT IN EDIT MODE
	POPJ	P,
	CAIN	CH,ALTMOD	;IS CHAR AN ALTMOD
	JRST	ALTCHK		;YES
	JRST	ENDIT
;
ALTCHK:	TLO	FL,ALTDET		;SET ALTMODE DETECTED FLAG ON
	POPJ	P,		;THEN RETURN
	PAGE
;	COMMAND DISPATCH ROUTINE ..
;
;	ENTER HERE WITH COMMAND IN 'COMBUF'
;
DECODR:	MOVE	CP,[POINT 7,COMBUF]	;SET COMMAND POINTER
	ILDB	CH,CP
	CAIN	CH,40
	JRST	.-2		;SKIP BLANKS
	CAIG	CH,"9"				;CHARACTER A DIGIT?
	CAIGE	CH,"0"			;
	JRST	.+2			;NO
	JRST	LIG.1			;YES .. DO LINE INPUT PROCESSING
	TLNN	FL,STACKM	;IN STACK MODE?
	JRST	DCODE.1		;NO
	MOVEM	FL,STRSAV+FL	;THIS IS A CLUDGE WILL FIX IT LATER..
	MOVBLK	(COMBUF,XILB1,20)
	PUSHJ	P,MERGER	;MERGE EVERYTHING FIRST
	MOVBLK	(XILB1,COMBUF,20)
DCODE.1:	MOVE	CP,[POINT 7,COMBUF]	;SET COMMAND POINTER
	PUSHJ	P,DECOD			;PUT COMMAND IN COMWRD
	MOVE	T1,COMWRD
	SETOM	T2
	LSH	T1,6		;MAKE A MASK FOR CHECKING COMMAND TABLE
	LSH	T2,-6
	JUMPN	T1,.-2
	MOVSI	T1,-COMTLG	;SET SIZE OF COMMAND TABLE
DECDR1:	MOVE	T3,COMTAB(T1)	;GET A COMMAND
	ANDCM	T3,T2		;MASK IT
	CAMN	T3,COMWRD	;HAVE WE FOUND IT?
	JRST	ACTION	;YES 
RECKC:	AOBJN	T1,DECDR1	;TRY NEXT ONE IN TABLE
	TLZE	FL,COMTST	;IN RECHECK MODE
	POPJ	P,
DX1:    MOVEI IOPNT,DSKOCH
        MOVEI T1,(SIXBIT/SVC/)
        HRRM T1,TMPBLK
        HRLZM T1,BLOCA

EXTERNAL BLOCA,BUFFA

DX5:    MOVE CP,[POINT 7,COMBUF]
        PUSHJ P,CLEARA
        MOVE T1,[POINT 7,BUFFA]
        PUSHJ P,DX3
        JRST DX2
DX3:    ILDB CH,CP
        JUMPE CH,.+2
        CAIN CH,33
        POPJ P,
        IDPB CH,T1
        JRST DX3
DX2:    MOVE CP,[POINT 7,SCRLF]
        PUSHJ P,DX3
        MOVE CP,[POINT 7,BUFFA]
DX7:        MOVE T1,[3,,BLOCA]
        TMPCOR T1,
        SKIPA
        JRST DX6
DX4:    MOVEI IOPNT,DSKOCH
        PUSHJ P,CHNINT
        PUSHJ P,CHROUT
        CLOSE DSKOCH,
DX6:        MOVE T1,[SIXBIT/RPG/]
        JRST STR
ACTION:	TLZE	FL,COMTST	;IN RECHECK MODE
	JRST	DX1		;IF COMMAND ISNT UNIQUE YOU LOSE
	TLO	FL,COMTST	;NOT RECHECKING YET SO LETS DO IT
RUNTST:	MOVE	T3,COMWRD	;PERMIT RUN COMMAND WITHOUT RECHECKING
	CAMN	T3,[SIXBIT/RUN/]
	JRST 	ACT1	;IT IS RUN, SO DONT RECHECK
	CAMN	T3,[SIXBIT/RU/]	;NOT RUN, BUT IS RU.	ALLOW THIS TOO
	JRST	ACT1
        CAME T3,[SIXBIT/R/]
        JRST .+3
 MOVEI T1,5
                JRST ACT1      ;RUNSYS COMMAND
	PUSH	P,T1		;NOT RUN, SO MUST RECHK. SAVE T1 VALUE FIRST
	PUSHJ	P,RECKC
	POP	P,T1
ACT1:	HRLI	T1,0		;CLEAR (LH) OF DISPATCH WORD
	SETOM	T2		;GETLIN CHARACTERISTICS
	TTCALL	6,T2
	TLNE	T2,2		;IS USER IN TAPE MODE?
	CAIN T1,7
	XCT	COMTB1(T1)	;EITHER HE WASNT IN TAPE MODE
				; HE WAS AND THIS IS A KEY COMMAND.
				; AT ANY RATE DISPATCH HIM...
	SKIPE COMWRD
	TTCALL	3,[ASCIZ /RETURN TO KEY MODE FIRST..

/]
	JRST STARTA
;
;
DEFINE TABLE<
COMAND OLD,<JRST OLDCOD>
COMAND NEW,<JRST NEWCOD>
COMAND RUN,<JRST RUNCOM>
COMAND RUNSYS,<JRST RNSCOD>
COMAND RUNBIN,<JRST RNBCOD>
COMAND KEY,<JRST KEYCOM>
COMAND SYSTEM,<JRST SYSCOM>
COMAND STATUS,<JRST STATCM>
COMAND CATALOG,<JRST CATCOM>
COMAND LENGTH,<JRST LNGCOM>
COMAND RESEQUENCE,<JRST RSEQCM>
COMAND LIST,<JRST LINCOM>
COMAND DELETE,<JRST DELCOM>
COMAND INSERT,<JRST INSCOM>
COMAND BYE,<JRST SOLNG>
COMAND GOODBYE,<JRST SOLNG>
COMAND LOCATE,<JRST LOCAT>
COMAND FIND,<JRST EDIT>
COMAND COMPILE,<JRST CMPCOM>
COMAND SAVE,<JRST SAVCOM>
COMAND RECOVER,<JRST RECCOM>
COMAND REPLACE,<JRST REPCOM>
COMAND RENAME,<JRST RENCOM>
COMAND HELP,<JRST HLPCOM>
COMAND SCRATCH,<JRST SCRCOM>
COMAND UNSAVE,<JRST UNSAVC>
COMAND MACRO,<JRST MACSYS>
COMAND MONITOR,<JRST MONSYS>
IFNDEF TELCOM,<
COMAND TELCOMP,<JRST TELSYS>
>
COMAND COBOL,<JRST COBLNG>
COMAND FORTRAN,<JRST FRSYS>
COMAND BASIC,<JRST BASYS>
COMAND REQUEST,<JRST DEVREQ>
COMAND PAGE,<JRST PAGER>
COMAND TAPE,<JRST TAPCOM>
COMAND WEAVE,<JRST MRGCOM>
COMAND *EDIT,<JRST EDM.5>
COMAND EDIT,<JRST EDM.6>
COMAND JOBS,<JRST JOBCOM>
COMAND USERS,<JRST USERCM>
>
DEFINE COMAND(A,B)
<<SIXBIT /A/>>
;
COMTAB:	BYTE(7)12,0,0,0,0
	0		;FOR A CHARRIAGE RETURN LF CHECK
	TABLE
COMTLG=.-COMTAB
;
DEFINE COMAND (A,B)
<B>
;
COMTB1:	JRST	PHAS7
	JRST	PHAS7
	TABLE
;
	PAGE
SYSCOM:	PUSHJ	P,DECOD		;GET THE SPECIFIED SYSTEM (IF ANY)
	SKIPN	COMWRD		;WAS THERE ONE THERE?
	JRST	SYSMSG		;NO..SO ASK FOR ONE
DESER:	MOVE	T1,COMWRD	;MASK SETUP
	SETOM	T2
	LSH	T1,6
	LSH	T2,-6
	JUMPN	T1,.-2
FNDSYS:	MOVSI	T1,-AVSLNG	;START TO SCAN TABLE
	MOVE	T3,AVSYS(T1)	;LOOK FOR A LEGAL NAME
	ANDCM	T3,T2
	CAMN	T3,COMWRD	;IS THIS ONE
	JRST	SYSOK		;HIT PAYDIRT MOVE ON
RECHK:	AOBJN	T1,FNDSYS+1	;CHECK NEXT ONE
	TLZE	FL,COMTST	;ARE WE IN RECHK MODE
	POPJ	P,		;YES
WSYS:	PUSHJ	P,WHAT
	JRST	SYSMSG		;TABLE EXAUSTED TRY AGAIN
;
SYSOK:	TLZE	FL,COMTST	;DID WE CHK FOR UNIQUENESS YET
	JRST	WSYS		;YES AND THERES ANOTHER POSSIBLE SYS NAME
	TLO	FL,COMTST	;NO ..SO LETS SEE IF HIS SYS NAME IS OK
	PUSH	P,T1
	PUSHJ	P,RECHK
	POP	P,T1		;SYS NAME IS LEGAL
	HRRZM	T1,SYSNM.	;SAVE SYSTEM NUMBER
	PUSHJ	P,WRTSYS	;WRITE OUT THE SYS FILE SO AS TO
				;MAKE SURE SYSTEM NAME GETS RECORDED.
	HRRZ	T1,SYSNM.		;IS SYSTEM MONITOR
	CAIN	T1,1	
	JRST	SOLNG		;YES
NOCHK:	SKIPE	FILNM.		;HAS A FILE NAME BEEN SPECIFIED?
	JRST	PHAS7		;FILE ALREADY SPECIFIED,SO SAY READY
	MOVEI	MSGPNT,1	;NO SO GO ASK OLD NEW STUFF
NOCHK2:	PUSHJ	P,DECOD		;SEE IF SOMETHING THERE
	SKIPE	COMWRD		;NOTHING FOUND SO REQUEST DATA
	JRST	NOCHK1		;PROCESS WHATEVER IS THERE
	XCT	MESAGE(MSGPNT)	;TYPE NEW OR OLD
	MOVE	CP,[POINT 7,COMBUF]
	PUSHJ	P,DSKET		;TTY INPUT
	MOVE	CP,[POINT 7,COMBUF]
	PUSHJ	P,DECOD		;GET THE ANSWER
NOCHK1:	MOVE	T1,COMWRD	;CHECK FOR NEW OR OLD
	SETOM	T2
	LSH	T1,6
	LSH	T2,-6
	JUMPN	T1,.-2
	MOVSI	T1,-NEWOLD	;ONLY PERMIT FIVE POSSIBILITIES IN ANSWER TO NEW/OLD QUERY
NEXT:	MOVE	T3,COMTAB(T1)
	ANDCM	T3,T2
	CAMN	T3,COMWRD
	XCT	COMTB1(T1)		;DISPATCH AS APPROPRIATE
	AOBJN	T1,NEXT
	PUSHJ	P,WHAT
	JRST	NOCHK2		;NEED TO ASK AGAIN
LOGENT:
;
SYSMSG:	MOVE	CP,[POINT 7,COMBUF]
	TTCALL	3,[ASCIZ /SYSTEM NAME--/]
	PUSHJ	P,DSKET		;INPUT ANSWER TO QUESTION
	MOVE	CP,[POINT 7,COMBUF]
	PUSHJ	P,DECOD		;LOAD SYSTEM NAME IN COMWRD
	JRST	SYSCOM+1		;FIND OUT IF ITS LEGAL
;
WHAT:	TTCALL	3,[ASCIZ /WHAT?/]
	TTCALL	3,CRLF
	POPJ	P,
CRLF:	BYTE(7) 15,12,12,0,0
SCRLF:	BYTE(7)	15,12,0,0,0

;	TABLE USED TO DEFINE WHICH PROCESSORS ARE AVAILABLE ON
;	THIS SYSTEM
;
DEFINE TABLE<
SYSTAG FORTRAN,<F40>
SYSTAG MONITOR,<EXEC>
SYSTAG BASIC,<BASIC>
SYSTAG MACRO,<MACRO>
SYSTAG COBOL,<COBOL>
SYSTAG *EDIT,<*EDIT>
SYSTAG EDIT,<EDIT>
>
;
DEFINE SYSTAG (A,B)
<<SIXBIT /A/>>
;
AVSYS:	TABLE
AVSLNG=.-AVSYS
;
DEFINE SYSTAG (A,B)<
SIXBIT /B/>
;
SYSCNM:	TABLE
;
;	ROUTINE TO HANDLE NEW/OLD/PROGRAM & DEVIVE SPECIFICATIONS
;
DEFINE TYPE (A)
<TTCALL 3,[ASCIZ /A/]>
;
MESAGE:	TYPE <SYSTEM NAME-->
	TYPE <NEW OR OLD-->
	TYPE <NEW PROGRAM NAME-->
	TYPE <OLD PROGRAM NAME-->
	TYPE <READY>
	TYPE <SYSTEM:	>
	TYPE <PROGRAM:	>
	TYPE <DEVICE:	>
	TYPE <JOB:	>
	TYPE <TERMINAL: >
	TYPE <INCOMPLETE COMMAND -- RETYPE IT>
	TYPE <FIND-MODE INVALID -- RETYPE COMMAND>
	TYPE <STRING DELIMETER NOT FOUND>
	TYPE <ASSUMING LIST MODE>
	TYPE <DELETE-MODE COMMAND ERROR>
	TYPE <CHANGE NAME TO-->
	TYPE <CATALOG OPTIONS: SIZES,PROTECTIONS,DATES OR TIMES
>
	TYPE <SYSTEM PROGRAM NAME:	>
	PAGE
;
;
; TABLES FOR NEW OLD PROCESSING
;
NEWOLD=4	;# COMMANDS PLUS 2 PERMITTED IN ANSWER TO NEW/OLD QUER
;
FLACT:	TLO	FL,NEWT
	TLO	FL,OLDT
	TDO	FL,[XWD OLDT,GPFLAG]
	TDO	FL,[XWD OLDT,GPFLAG]
	TDO	FL,[XWD OLDT,GPFLAG]
;
SPMSG:	MOVEI	MSGPNT,2
	MOVEI	MSGPNT,3
	MOVEI	MSGPNT,3
	MOVEI	MSGPNT,^D17
	MOVEI	MSGPNT,3
;
RNSCOD:	MOVEI	T1,3	;RUNSYS COMMAND ENTRY
	MOVE	T2,[SIXBIT/SYS/]	;DEVICE IS SYS
	JRST	OUT1			;SO SKIP DEFAULT DSK CODING

RNBCOD:	MOVEI	T1,4	;RUNBIN COMMAND ENTRY
	PUSHJ	P,DECOD		;SEE IF FILE NAME BEING SPECIFIED
	SKIPE	COMWRD		;	FOR THIS RUN ONLY
	JRST	NEWFIL		;YES
	SKIPN	FILNM.		;FILE NAME SPECIFIED YET?
	JRST OUT		;NO, SO GO GET ONE
	XCT	FLACT(T1)	;USE CURRENT FILE, SET FLAGS
	JRST	QUIKRN		;GO RUN IT

NEWFIL:	MOVE	T2,[SIXBIT/DSK/]	;NEW FILE SPECIFIED FOR RUNBIN
	MOVEM	T2,DEVNM.		;SAVE DEFAULT DEVICE
	XCT	FLACT(T1)		;SET FLAG
	JRST	PHAS2			;GO COLLECT REST FOR FILE DATA

NEWCOVEI	T1,0		;NEW COMMAND ENTRY
	SKIPA
OLDCOD:	MOVEI	T1,1		;OLD COMMAND ENTRY
OUT:	MOVE	T2,[SIXBIT/DSK/]	;DSK IS DEFAULT DEVICE
OUT1:	MOVEM	T2,DEVNM.		;SAVE DEVICE NAME
	SETZM	FILNM.		;CLEAR OUT FILNAME AND EXT
	SETZM	FILNM.+1
	SETZM	FILNM.+3	;PROJECT PROGRAMMER NO.
	XCT	FLACT(T1)	;SET APPROPRIATE FLAG
	XCT	SPMSG(T1)	;SET APPROPRIATE MESSAGE POINTER
;SCAN REST OF COMMAND FOR FILENAME,EXTENSION,DEVICE,PP NUM
SCANL:	PUSHJ	P,DECOD		;SCAN THE INPUT
	SKIPE	COMWRD		;FIND ANYTHING?
	JRST	PHAS2		;
SCANL1:	TLNE	FL,EXTEN	;ARE WE HERE CAUSE WE EXPECTED AN EXTESION AND DIDNT GET ONE
	PUSHJ	P,CLEARF
	XCT	MESAGE(MSGPNT)	;NO .. INQUIRE FURTHER
	MOVE	CP,[POINT 7,COMBUF]
	PUSHJ	P,DSKET		;INPUT MORE INFORMATION
	MOVE	CP,[POINT 7,COMBUF]
	PUSHJ	P,DECOD		;SCAN INPUT
	SKIPN	COMWRD		;FIND ANYTHING?
	JRST	SCANL1		;NO ..ASK AGAIN
PHAS2:	MOVE	T4,COMWRD	;FIND OUT WHAT IT IS
	CAIN	CH,":"		;IS INPUT A DEVICE NAME
	JRST	COLON		;YES 
	CAIN	CH,"."		;IS THERE AN EXTENSION
	JRST	DOT		;YES
	TLNE	FL,EXTEN
	JRST	SVEXT
	MOVEM	T4,FILNM.	;SAVE FILE NAME
	JRST 	PHAS2X		;CHECK FOR PPN OR END OF COMMAND
;
COLON:	CALLI	T4,DEVCHR	;GET DEVICE CHARACTERISTICS
	JUMPE	T4,DEVERR		;=0 MEANS NO SUCH DEVICE
	TLC	T4,42
	TLNN	T4,42		;IS IT A LEGAL DEVICE
	JRST	.+2
	JRST	DEVERR		;NO REALLY . LOSE SMALL
	MOVE	T4,COMWRD	;IF OK SAVE IT
	MOVEM	T4,DEVNM.
	JRST	SCANL		;SCAN AGAIN
DEVERR:	TTCALL	3,[ASCIZ /ILLEGAL DEVICE NAME!!/]
	TTCALL	3,CRLF
	JRST	SCANL1		;TRY AGAIN
CLEARF:	TLZ	FL,EXTEN
	SETZM	FILNM.
	POPJ	P,
	
DOT:	TLNN	FL,EXTEN	;DOT ALREADY SEEN?
	TLOA	FL,EXTEN	;SET EXTESION SEEN FLAG
	JRST	XERR		;PROBABLY TO MANY DOTS
	MOVEM	T4,FILNM.		;SAVE FILE NAME
	JRST	SCANL	;GO LOOK FOR USER NO.

XERR:	PUSHJ	P,WHAT
	TLZ	FL,EXTEN	;CLEAR EXTENSION FLAG
	JRST	SCANL
SVEXT:	HLLZM	T4,FILNM.+1	;SAVE EXTENSION
PHAS2X: SETZM SAVPPN
        SETZM SAVPPN+1
	CAIE	CH,"["	;EXPECT A PPN?
	JRST	PHAS3		;NO. COMMAND DONE
PPNUM:	PUSHJ	P,DECOD
	SKIPN	COMWRD		;GET INPUT
	JRST	XERR		;COMMAND ERROR. START AGAIN
	MOVE	T4,COMWRD	;FOUND IT
EXTERNAL SAVPPN
        MOVEM T4,SAVPPN
        MOVEI T4,SAVPPN
	MOVEM	T4,FILNM.+3	;SAVE IT
PHAS3:	TRNE	FL,REPROC	;IN RENAME MODE?
	JRST	REN1		;YES
	TLNN	FL,OLDT		;IS THIS A SAVED FILE?
	JRST	PHAS4		;NO
	JRST	PHAS3X


	PAGE
;
;	SUBROUTINE TO INITIALIZE	'TSTCHN' CHANNAL
;
CHNOPN:	SETZM	DEVC		;MUST PROVE IT EXISTS
	EXTERNAL TOBF,TIBF
	MOVE T1,[XWD TOBF,TIBF]
	MOVEM T1,DEVC+2
	MOVE	T1,DEVNM.
	MOVEM	T1,DEVC+1		;OPEN A CHANNAL TO TEST WITH
	OPEN	TSTCHN,DEVC
	JRST	ERRORX		;SOMETHING WENT WRONG
	POPJ	P,		;RETURN
;
PHAS3X:	TRNE	FL,GPFLAG	;WAS AN IMMEDIATE 'RUN' ISSUED?
	JRST	QUIKRN		;YES
	PUSHJ	P,CHNOPN	;OPEN THE TEST CHANNAL
	HRRZ T3,SYSNM.
	CAIN T3,2	;BASIC?
	PUSHJ P,BASFIL	;YES
	LOOKUP	TSTCHN,FILNM.	;SEE IF IT EXISTS
	JRST	NEXF		;CANT FIND IT LET USER KNOW!
	MOVE	T4,FILNM.+3	;RESTORE USER NO.
	PUSH	P,FILNM.+1	;SAV C(EXT)
	HRLI T1,(SIXBIT/REL/)
	PUSHJ P,LOKFOR
	PUSHJ P,RNFIL
	HRLI T1,(SIXBIT/RUN/)
	PUSHJ P,LOKFOR
	PUSHJ P,RNFIL
	MOVSI	T1,(SIXBIT /REL/)	;PLUG IN REL EXTENTION
	CAIN T3,2	;BASIC?
	MOVSI T1,(SIXBIT/RUN/)	;YES
	HLLZM	T1,FILNM.+1
	LOOKUP	TSTCHN,FILNM.	;SEE IF A REL FILE EXISTS
	TRO	FL,NOREL	;SET	NO REL EXISTS FLAG
	MOVEM	T4,FILNM.+3	;RESTORE USER NO.
	POP	P,FILNM.+1	;RESTORE ORIGINAL EXTENSION
	TRNE	FL,UNSAVF	;IN UNSAVE MODE?
	JRST	US2		;YES
	MOVE	T1,FILNM.+1	;THIS IS NECESSARY BECAUSE LOOKUP RETURNS DATE
	TRZ	T1,777777	;WHEN SUCCESSFUL
	MOVEM	T1,FILNM.+1
	RELEASE	TSTCHN,
	JRST	PHAS4

BASFIL:	HLRZ T2,FILNM.+1
	JUMPE T2,.+2
	POPJ P,	;AN EXTENSION WAS SPECIFIED
	MOVSI T2,(SIXBIT/BAS/)
	HLLZM T2,FILNM.+1
	POPJ P,

NEXF:	TTCALL	3,[ASCIZ /FILE NOT FOUND--/]
	MOVE	T4,FILNM.	;TELL HIM THE NAME
	PUSHJ	P,SIXOUT
	SKIPN	T4,FILNM.+1	;IS THER AN EXTENSION
	JRST	NEXF1		;NO
	MOVEI	T3,16		;PUT A SEPERATER '.'
	ROTC	T3,-6
	PUSHJ	P,SIXOUT
NEXF1:	TTCALL	3,CRLF
	SETZM	FILNM.	;CLEAR OUT CAUSE IT WASNT FOUND
	SETZM	FILNM.+1
	RELEASE	TSTCHN,
	TTCALL	3,SCRLF
	JRST	PHAS7
;
	PAGE
;
;ROUTINE TO EXECUTE AN IMMEDIATE RUN COMMAND DEDECTED IN NEW/OLD
; SEQUENCE-- NOW TMP FILES ARE SET UP HERE.

;SET UP 6-WORD ARGUMENT FOR RUN UUO
QUIKRN:	FLUSH	(ORDERS,6)
	MOVE	T2,DEVNM.	;GET SPECIFIED DEVICE (DEFAULT IS DSK)
	MOVEM	T2,ORDERS
	MOVE	T1,FILNM.+3	;GET PROJ/PROG NUMBER
	MOVEM	T1,ORDERS+4	;PUT IN RUN UUO ARGUMENT BLK
	MOVE	T1,FILNM.	;GET FILE NAME ONLY (PROG NEEDS SAV OR SHR OR HGH-EXT.)
	CAMN	T2,[SIXBIT/SYS/]	;IF SYS DON'T CARE IF BASIC
	JRST RN2
	HRRZ T3,SYSNM.	;SEEIF BASIC
	CAIN T3,2
	JRST BASR1	;YES BASIC
RN2:	MOVEM	T1,ORDERS+1
	HRRZI	T1,ORDERS
	JRST	IMEDRN		;TRY TO RUN PROG.	DO IT FROM LOW SEG
				;TO ATTEMPT ERROR RECOVERY.!



BASR1:	MOVEM T1,TMPBLK	;PUT THE FILE NAME DOWN
	MOVE T1,FILNM.+3
	MOVEM T1,TMPBLK +3
	MOVE T1,JOBN
	HLLM T1,BBBTMP
	INIT DSKOCH ,16
	SIXBIT/DSK/
	0
	JFCL
	JRST RUNTIM
	PAGE
;
PHAS4:	TLNN	FL,NEWT		;THIS A NEW FILE
	JRST	PHAS5		;NO
	PUSHJ	P,CHNOPN	;TEST TO SEE IF NEW FILE NAME 
	HRRZ T1,SYSNM.
	CAIN T1,2
	PUSHJ P,BASFIL
	LOOKUP	TSTCHN,FILNM.	;ALREADY IS IN USERS DIRECTORY
	JRST	SCRDIS
	JRST	FLOREX		;NAME FOUND DONT ALLOW HIM TO USE SAME NAME
SCRDIS:	PUSHJ	P,CHNOPN	;YES IT NEEDS CREATING
	MOVE	T1,JOBN		;GET JOB #
	HLLM	T1,TMPBLK
	MOVEI	T1,(SIXBIT /SRC/)
	HRRM	T1,TMPBLK
	ENTER	TSTCHN,TMPBLK
	JRST	ERRORX		;SOMETHING HAPPENED
	RELEASE	TSTCHN,		;CLOSE OUT 
	PUSHJ	P,CHNOPN	;OPEN FOR NEXT LOOKUP
	PUSH	P,TMPBLK
	PUSH	P,TMPBLK+1
	HRRZ T3,SYSNM.
	MOVEI	T1,(SIXBIT /REL/)
	CAIN T3,2	;BASIC?
	MOVEI T1,(SIXBIT/RUN/)	;YES
	HRLM	T1,TMPBLK+1	;IF .REL XISTS GET RID OF IT.
	LOOKUP	TSTCHN,TMPBLK
	JRST	PH4X		;PROBABLY DOESNT EXIST!
	SETZM	TMPBLK
	RENAME	TSTCHN,TMPBLK	;GOODBYE ###SRC.REL
	JRST	ERRORX
PH4X:	POP	P,TMPBLK+1
	POP	P,TMPBLK
	TRNE	FL,SCRTCH		;IN SCRATCH MODE?
	JRST	PHAS7		;YES
	RELEASE	TSTCHN,
PHAS5:	PUSHJ	P,WRTSYS	;WRITE OUT THE SYS.TMP FILE
PHAS6:	TTCALL	3,SCRLF
	TLNE	FL,NEWT		;THIS A NEW FILE?
	JRST	PHAS7
	SKIPL	SYSNM.		;MAKE SURE A SYSTEM IS SPECIFIED.
	JRST	PHAS6X
	TTCALL	3,[ASCIZ /PLEASE SPECIFY SYSTEM FIRST..

/]
	JRST	PHAS7
PHAS6X:	PUSHJ	P,SEQINI
	HRLZ	T1,SYSNM.
	MOVEM T1,SYSCOD
	MOVE	T1,[XWD FILNM.,INPFIL]
	BLT	T1,INPFIL+3
        MOVE T1,[SAVPPN,,INPFIL+3]
        BLT T1,INPFIL+4
	MOVE T1,DEVNM.
	MOVEM T1,INPFIL-1
	JRST	RESSTR


;
;
;	CODE TO OUTPUT THE SUDS SYSTEM INFO FILE EITHER 'INCORE'
;	OR TO THE ###SYS.TMP FILE IF NO ROOM INCORE.
;
WRTSYS:	MOVE	T1,[XWD 3,CORLST]	;ATTEMPT TO WRITE INCORE SYSTMP
	CALLI	T1,44
	JRST	.+2	;CANT SO WRITE IT ON DISK INSTEAD.
	POPJ	P,		;RETURN
	ENTER	SYSCHN,SYSTMP
	JRST	ERRORX
	OUTPUT	SYSCHN,IOLST	;EMPTY INTO ###SYS.TMP FILE
	CLOSE	SYSCHN,
	POPJ	P,		;RETURN
;
;
;
OLD1:	ASCIZ	/DSK:/
OLD2:	ASCIZ	'SRC.REL/B'
OLD3:	ASCIZ	/.REL/
OLD4:	ASCIZ/.RUN/
OLD5:	ASCIZ 'SRC.RUN/B'
;
;
	PAGE
STATCM:	MOVEI	MSGPNT,^D8	;OUTPUT JOB 3
	XCT	MESAGE(MSGPNT)
	SETZM	T3
	MOVE	T4,JOBN
	ROTC	T3,6		;GET RID OF LEADING ZEROS
	CAIN	T3,20
	JRST	.-2
	ROTC	T3,-6		;RETURN LAST CHAR TO T4
	PUSHJ	P,SIXOUT
	TTCALL	3,SCRLF
	AOS	MSGPNT
	XCT	MESAGE(MSGPNT)
	CALLI	T4,34		;OUTOUT TERMINAL NAME
	PUSHJ	P,SIXOUT
	TTCALL	3,SCRLF
	MOVEI	MSGPNT,4
	SETZM	T4
	MOVE	T1,SYSNM.		;OUTPUT SYSTEM NAME
	SKIPGE	T1		;FIRST ENTRY SET.
	HRRI	T1,NILCH		;YES
	SETOM	T2		;TEMP FLAG FOR SYS NAME OUTPUT ONLY
	PUSHJ	P,STOUT
	TTCALL	3,SCRLF
	SETZM	T2		;CLEAR FLAG
	MOVE	T4,FILNM.		;OUTPUT FILENAME
	PUSHJ	P,STOUT
SKIPN	T4,FILNM.+1		;IS THER AN EXTENSION
	JRST	DOUT			;NO
	MOVEI	T3,16			;MAKE A '.'
	ROTC	T3,-6
	PUSHJ	P,STOUTX
DOUT:	TTCALL	3,SCRLF
	TTCALL	3,SCRLF
	JRST	PHAS7
;
STOUT:	AOS	MSGPNT			;INCREMENT MESSAGE POINTER
	XCT	MESAGE(MSGPNT)
STOUTX:	SKIPL	T2	;SYSTEM NAME	OUTPUTTING
	JRST	.+3		;NO
	XCT	NAMX(T1)
	POPJ	P,
	PUSHJ	P,SIXOUT
	POPJ	P,
;
PHAS7:RECCOM:	MOVEI	MSGPNT,4
	TTCALL 3,CRLF
	XCT	MESAGE(MSGPNT)
	TTCALL	3,CRLF
	JRST	STARTA		;START AGAIN
;
	0
NAMX:	TYPE <FORTRAN>
	TYPE <MONITOR>
	TYPE <BASIC>
	TYPE <MACRO>
	TYPE <COBOL>
	TYPE <*EDIT (DATA FILE)>
	TYPE <EDIT>
	0
NILCH=.-NAMX
;
	PAGE
CATCOM:
CATCM1:	SETZM	T4		;CLEAR A TMP FLAG WORD
CATCM2:	PUSHJ	P,DECOD		;SCAN FOR OPTIONS
	SKIPN	T1,COMWRD	;FIND AN OPTION?
	JRST	COMOUT		;NO
;
	SETOM	T2		;FIND OUT WHAT OPTION IT IS
	LSH	T1,6
	LSH	T2,-6
	JUMPN	T1,.-2
	MOVSI	T1,-OPTABE	;SET TABLE LENGTH
;
OPCHK:	MOVE	T3,OPTAB(T1)	;GET AN OPTION
	ANDCM	T3,T2
	CAMN	T3,COMWRD	;GOT A MATCH?
	JRST	OPSET		;YES
	AOBJN	T1,OPCHK	;TRY AGAIN
	MOVEI	MSGPNT,^D16	;TYPE ERROR MESSAGE
	XCT	MESAGE(MSGPNT)
	TTCALL	3,CRLF
	JRST	STARTA
OPSET:	IOR	T4,SWTAB(T1)	;GET THE SWITH BIT
	JRST	CATCM2		;SET IT AND SEE IF MORE OPTION ARE SPECIFIED
COMOUT:	MOVE	CP,[POINT 7,CAT]
	MOVEI	IOPNT,DSKOCH		;MAKE PIP OUTPUT CHANNAL
	MOVEI	T1,(SIXBIT /SVC/)
	HRRM	T1,TMPBLK
        HRLZM T1,BLOCA
        PUSHJ P,CLEARA
        MOVE T1,[POINT 7,BUFFA]
        PUSHJ P,DX3
	JUMPN	T4,CATCM4	; IS IT /F MODE?
;
        JRST CATCM5
CATCM4:	MOVE CP,[POINT 7,DATE]
	TRNE	T4,400000
	PUSHJ	P,DX3		;YES
	MOVE CP,[POINT 7,TIME]
	TRNE	T4,200000
	PUSHJ	P,DX3		;YES
	MOVE CP,[POINT 7,SIZE]
	TRNE	T4,100000
	PUSHJ	P,DX3		;YES
	MOVE CP,[POINT 7,PROTE]
	TRNE	T4,40000
	PUSHJ	P,DX3
CATCM5: MOVE CP,[POINT 7,SCRLF]
        PUSHJ P,DX3
        MOVE CP,[POINT 7,BUFFA]
        JRST DX7
CLEARA: SETZM BUFFA
        MOVE T1,[BUFFA,,BUFFA+1]
        BLT T1,BUFFA+17
        POPJ P,
PIPSTR:	MOVE	CP,[POINT 7,SCRLF]
	PUSHJ	P,CHROUT
	CLOSE	DSKOCH,		;WRITE END OF FILE
;
	MOVE	T1,[SIXBIT /SPIP/]	;START COMPIL CUSP PROCESSING
STR:	MOVEM	T1,CCLDCH+1
	MOVSI	T1,1
	HRRI	T1,CCLDCH
	CALLI	T1,35		;RUN UUO
STRA1:	TTCALL	3,[ASCIZ /PROGRAM NOT FOUND--/]
        JRST NEXF+1
SWOUT:	PUSH	P,T4	;SAVE TMP FLAG
	PUSHJ	P,SIXTOT
	POP	P,T4
	POPJ	P,
;
;

DATE:   ASCIZ'/C'
TIME:   ASCIZ'/TI'
SIZE:   ASCIZ'/SI'
PROTE:   ASCIZ'/PR'
CAT:	ASCIZ	'FIL'

DEFINE CTAB<
OPTION DATES,<OCT 400000>
OPTION LENGTH,<OCT 100000>
OPTION PROTEC,<OCT 40000>
OPTION SIZES,<OCT 100000>
OPTION TIMES,<OCT 200000>
>

DEFINE OPTION (A,B)
<<SIXBIT /A/>>

OPTAB:	CTAB
OPTABE=.-OPTAB

DEFINE OPTION (A,B)
<B>
SWTAB:	CTAB
;


	PAGE
LNGCOM:	TTCALL	3,SCRLF
 INIT	TSTCHN,0	;OPEN THE LOOKUP CHANNAL
	SIXBIT	/DSK/
	0
	HALT
LNGCM1: PUSHJ	P,GTNAME		;ANY FILES SPECIFIED
	SKIPN	FILEN
	JRST	CFIL			;NO
	TRO	FL,GPFLAG		;NAMES FLAG ON SET TO 1
LNGCM2: MOVE	T1,FILEN		;LOOKUP THE FILE
	MOVE	T2,EXTN
	
LNGCM3: PUSHJ	P,LOOK		;GET BLOCK SIZ INTO T1
	TRNN	FL,GPFLAG	;NEED TO OUTPUT NAME
	JRST	.+3		;NO
	PUSHJ	P,NMOUT		;PUSH OUT FILENAME
	PUSHJ	P,TAB
	PUSHJ	P,DECVRT	;OUTPUT CHAR COUNT TO NEAREST BLOCK
	TTCALL	3,[ASCIZ .	.]
	TTCALL	3,[ASCIZ /CHARACTERS
/]
	JRST	XLNG
;
LNGCM8:	TTCALL	3,CRLF
	CLOSE	TSTCHN,
	JRST	PHAS7
;
CFIL:	MOVE	T1,JOBN		;SIZE OF ###SRC.TMP
	HRRI	T1,(SIXBIT /SRC/)
	MOVSI	T2,(SIXBIT /TMP/)
	JRST	LNGCM3

;
;LOOKUP THE FILE WHOSE NAME IS IN T1,EXT IN T2
;RETURN BLOCK COUNT IN T1	SET T2 = 0
;
LOOK:	MOVEM	T1,TMPBLK
	MOVEM	T2,TMPBLK+1
	SETZM	TMPBLK+2
	SETZM	TMPBLK+3
	LOOKUP	TSTCHN,TMPBLK
	JRST	FNF		;DIDNT FIND FILE
	HLRE	T1,TMPBLK+3	;PUT BLOCK COUNT IN T1
	JUMPN	T1,LOOKX		;IF NEGATIVE ITS A WORD COUNT
	IMULI	T1,200		;IF POS ITS A BLOCK COUNT MAKE IT WORDS
LOOKX:	MOVMS	T1,T1		;MAKE SURE THE NUMBER IS POSITIVE WORD COUNT
	SETZM	T2
	POPJ	P,
;
FNF:	TTCALL	3,[ASCIZ .FILE NOT FOUND--.]
	PUSHJ	P,NMOUT
	TTCALL	3,SCRLF
XLNG:	TLNN	FL,MORE	;ANY MORE PAREM'S IN COMMAND LIST?
	JRST	LNGCM8		;NO. FINISH UP
	PUSHJ	P,GTNAME	;GET NEXT NAME IN COMMAND
	JRST	LNGCM2		; REPEAT
;
;
; ROUTINE TO OUTPUT FILENAME TO TTY
;NAME IN 'FILEN' , EXT IN 'EXTN'
;
NMOUT:	MOVE	T4,FILEN
	PUSHJ	P,SIXOUT
	SKIPN	T4,EXTN
	JRST	FNF1
	MOVEI	T3,16
	ROTC	T3,-6
	PUSHJ	P,SIXOUT
FNF1:	POPJ	P,
;
;
;CONVERT THE VALUE IN T1 TO ASCII DECIMAL ANT PRINT IT
;
DECVRT:	JUMPLE	T1,DVT1		;IF <=0 ITS A WORD COUNT
	TRO	FL,1		;SET TMP LEADING ZEROS FLAG
				;USED TO SUPRESS LEADIND ZEROS
	MOVEI	T4,^D8		;TO PRINT 8 DIGITS
	IMULI	T1,5		;CHAR COUNT = WORD COUNT * 5
DVT2:	IDIVI	T1,^D10
	HRLM	T2,(P)
	SOSE	T4		;FINISHED?
	PUSHJ	P,DVT2
	HLRZ	T1,(P)
	SKIPN	T1
	JRST	DVT3		;LOOKING TO SUPPRESS LEADING ZEROS
DVT2A:	ADDI	T1,60
	TTCALL	1,T1
	TRZ	FL,1		;DONT SUPPRESS ZEROS AGAIN
	POPJ	P,		;RETURN SOMEWHERE
DVT3:	TRNE	FL,1
	POPJ	P,
	JRST	DVT2A

;
TAB:	PUSH	P,T1
	MOVEI	T1,11
	TTCALL	1,T1
	POP	P,T1
	POPJ	P,
;
DVT1:	TTCALL	3,[ASCIZ .0 CHARACTERS
.]
	JRST	XLNG

	PAGE
;
;	DSK OUTPUT CHANNAL INITIALIZER
;
CHNINT:	INIT	DSKOCH,1
	SIXBIT	/DSK/
	XWD	OTBUF,
	TTCALL	3,[ASCIZ /E1/]
	OUTBUF	DSKOCH,1
	ENTER	DSKOCH,TMPBLK
	TTCALL	3,[ASCIZ /E2/]
	POPJ	P,
;
;	SUBROUTINE TO OUTPUT AN ASCII COMMAND FOR CCL TMP FILES
;	---	ZERO TERMINATES AN OUTPUT	----
CHROTA:	TRO	FL,DLTALT	;	$ TO ALTMODE
CHROUT:	ILDB	CH,CP
	JUMPE	CH,FIN
        CAIN CH,33
        POPJ P,
	TRNN	FL,DLTALT
	JRST	.+3
	CAIN	CH,"$"
	MOVEI	CH,175
	PUSHJ	P,PUTCHR
	JRST	CHROUT
FIN:	TRZ	FL,DLTALT
	POPJ	P,
;
;
;	SUBROUTINE TO OUT PUT ONE CHARACTER IN 'CH' TO THE
;	CHANNAL SPECIFIED IN IOPNT
;
PUTCHR:	TRNE	FL,LINMOD	;IN LINED COMMAND OUTPUT MODE?
	JRST	ORDOUT		;YES	PUT COMMANDS IN 'ORDERS'
	SOSG	@IOTAB2(IOPNT)
	JRST	DMPOUT
PUTNXT:	XCT	IOTAB5(IOPNT)
	POPJ	P,
DMPOUT:	XCT	IOTAB4(IOPNT)
	JRST	PUTNXT
STATO:	XCT	IOTAB3(IOPNT)
	TTCALL	3,[ASCIZ /E3/]
	POPJ	P,
;
;
;	ROUTINE TO OUTPUT TO 'ORDERS' FOR LINED 
;
ORDOUT:	IDPB	CH,ORDPNT
	POPJ	P,
;
;
;	ROUTINE TO INIT SPECIAL LINED OUTPUT HANDLING
;
ORDMOD:	MOVE	ORDPNT,[POINT 7,ORDERS]
	TRO	FL,LINMOD	;SET MODE FLAG
	POPJ	P,
;
;
	PAGE
;
;	ROUTINE TO RESEQUENCE AN ASCII FILE
;
PIPOPN:	MOVEI	IOPNT,DSKOCH		;SPECIFY OUTPUT CHANNAL
	MOVEI	T1,(SIXBIT /PIP/)		;SET UP ###PIP.TMP
	HRRM	T1,TMPBLK
	PUSHJ	P,CHNINT
	POPJ	P,		;RETURN
;
;
;CODE TO RESEQUENCE	THE CURRENT FILE
;
RSEQCM:	SKIPN	FILNM.		;MUST BE A FILE AVAILABLE
	JRST	NFOX			;THERE ISNT..
	PUSHJ	P,SEQINI	;STANDARDIZE DATA AREA
	MOVEI	ORDPNT,NEWSEQ		;POINT TO NEW STARTING SEQ #
	PUSHJ	P,SEARCH		;SEARCH FOR NEWSEQ PARAM
	JSP	T2,CMACHK		;DIDNT FIND ONE..IS THERE ANY MORE?
	MOVEI	ORDPNT,STSEQ		;SET UP RANGE DATA POINTER
	PUSHJ	P,SEARCH		;LOOK FOR STARTING LINE # OF RANGE
	JSP	T2,CMACHK		;END OF COMMAND YET?
	CAIE	CH,"-"			;RANGE TERMINATOR FOUND
	JRST	INCCHK		;NO
	MOVEI	ORDPNT,ENDSEQ		;SET	UP RANGE DATA POINTER
	PUSHJ	P,SEARCH		;LOOK FOR END OF RANGE NUMBER
	JSP	T2,CMACHK		;ANY MORE DATA?
INCCHK:	MOVEI	ORDPNT,SEQINC	; LOOKS THAT WAY SO SEARCH FOR INC
	PUSHJ	P,SEARCH
	JRST CMACHK+2	;NONE FOUND USE STANDARD INCREMANET
	JSP	T2,CMACHK		;EXPECT TO SEE TERMINATOR
	JRST	ILLPAR			; WE WONT ALLOW GARBAGE..
;
CMACHK:	TLNN	FLAG,ALTDET	;ALTMODE DETECTED?
	JRST	(T2)		;NO
	HRRZ T3,SYSNM.
	CAIN T3,2
	JRST BASRES
	JRST	RESSTR		;YES	END UP COMMAND DECODING
;
;
; CODE TO CONVERT A SIXBIT NUMBER IN T1(LEFT JUSTIFIED) TO BINARY
; VALUE
;
DECCON:	MOVEM	T1,T2
	SETZM	T3
DRAD:	SETZM	T1
	LSHC	T1,6
	SUBI	T1,20
	IMULI	T3,^D10
	ADDM	T1,T3
	JUMPN	T2,DRAD
	MOVEM	T3,T1
	POPJ	P,		;RETURN
;
;
; CODE TO SEARCH THE COMMAND STRING FOR RESEQ PARAMETERS
; AND SAVE THE BINARY VALUE IN THE APPROPRIATE DATA AREA AS 
; POINTED TO BY 'ORDPNT'
;
SEARCH:	PUSHJ	P,DECOD		;GET A PARAM
	SKIPN	T1,COMWRD		;FIND ANYTHING?
	POPJ	P,			;NO
	PUSHJ	P,LEGAL			;IS THE PARAM A LEGAL NUMBER
	PUSHJ	P,DECCON		;YES CONVERT IT TO BINARY
	MOVEM	T1,@ORDPNT		;SAVE DATA
	AOS	(P)
	POPJ	P,		;RETURN
;
SEQINI:	MOVE	T1,[XWD SEQDAT,INPFIL]
	BLT	T1,NEWSEQ
	HLLZ	T1,JOBN
	HLLM	T1,INPFIL
        MOVE T1,[SAVPPN,,INPFIL+3]
        BLT T1,INPFIL+4
	POPJ	P,
SEQDAT:	SIXBIT .###SRC.
	SIXBIT .TMP.
        0
	0
	0
	XWD 0,2
	XWD 0,0
	XWD 0,^D1
	XWD 0,^D99999
	XWD 0,^D10
	XWD 0,^D100
RESSTR:	MOVE	T1,[XWD 3,RESWRD]
	CALL	1,[SIXBIT .TMPCOR.]
	HALT
	MOVEI	1,RESTR
	CALLI	1,35
	HALT
RESWRD:	SIXBIT .RES.
	IOWD ^D12,INPFIL-1
        0
RESTR:	SIXBIT .SYS.
	SIXBIT .NEWRES.
0
0
0
0
;
;

BASRES:	MOVE T1,[XWD 3,RESWRD]
	CALL 1,[SIXBIT/TMPCOR/]
	HALT
	MOVEI 1,BASSTR
	CALLI 1,35
	HALT
BASSTR:	SIXBIT/SYS/
	SIXBIT/BASRES/
	0
	0
	0
	0

SEQCL:	ASCIZ	'DSK:/X/S_DSK:'
;
;
;	SUBROUTINE TO OUTPUT THE ###SRC.TMP FILENAME TO ANY OUTPUT
;	FILE	SPECIFIED BY	'IOPNT'
;	OUTPUT FORMAT:	###SRC.TMP	WHERE ### IS THE JOB NUMBER
;	CALL		PUSHJ	P,OTFX
;
OTFX:	PUSH	P,CP
	MOVE	SIXTXT,JOBN		;GET JOB #
	PUSHJ	P,SIXTOT
	MOVE	CP,[POINT 7,CCLC2]
	PUSHJ	P,CHROUT
	POP	P,CP		;RESTORE CP
	POPJ	P,
;
;
	PAGE
;	ROUTINE TO LIST ALL OR PART OF A FILE
;	COMMAND FORMAT:	LIST	(TO LIST ALL)
;			LIST S1,S2,....SN
;	WHERE S IS A LINE # OR GROUP OF LINES AS:	LINE1-LINE2
;
	PAGE
DELCOM:	TLO	FL,DELET	;WE ARE DELETEING LINES
LINCOM:	MOVEI	IOPNT,DSKOCH
	PUSHJ	P,DECOD		;ANY PARAMETERS
	PUSHJ	P,ORDMOD	;SETUP FOR SPECIAL OUTPUT MODE
	PUSH	P,CH
	PUSHJ	P,LECOPN	;OPEN THE CHANNAL
	POP	P,CH
	SKIPN	COMWRD		; SIMPLE LISTING SPECIFIED?
	JRST	SIMTYP		; YES..
SPLINE:	PUSHJ	P,LEGAL		;IS PARAM LEGAL #
;
	TLNE	FL,DASH		;DASH PROCESSING IN ACTION
	JRST	DSCON
	CAIN	CH,","		;FIND OUT WHAT THE TERMINATOR IS
	JRST	LNCHK1
	CAIN	CH," "		;TREAT BLANK LIKE	A COMMA
	JRST	LNCHK1
	CAIN	CH,"-"
	JRST	LNCHK2
	CAIN	CH,33		;END OF COMMAND
	JRST	LNCHK1		;FINISH UP
ILLPAR:	TLNE	FL,EDITFL
	JRST	FINERR		;EDIT MODE ERROR IS DIFFERENT!
	PUSHJ	P,WHAT		;YOU LOSE
	JRST	PHAS7
LNCHK1:	PUSHJ	P,PORD
	PUSHJ	P,PUTCHR
	MOVE	SIXTXT,COMWRD	;LINE #
	PUSHJ	P,SIXTOT
	PUSHJ	P,LINTRM	;CRLF
LNCHK3:	PUSHJ	P,DECOD
	SKIPN	COMWRD		;ANY MORE
	JRST	LEEND		;NO
	JRST	SPLINE	;FIND OUT WHAT IT IS
;
LNCHK2:	TLNE	FL,DASH		;IF DASH FLAG IS ALREADY SET YOUR IN TROUBLE
	JRST	ILLPAR
	TLO	FL,DASH		;SET DASH FLAG
	PUSHJ	P,PORD		;P OR D COMMAND
	PUSHJ	P,PUTCHR
	MOVE	SIXTXT,COMWRD
	PUSHJ	P,SIXTOT
	MOVEI	CH,","
	PUSHJ	P,PUTCHR
	PUSHJ	P,DECOD		;LOOK FOR EXPECTED SECOND PARAM
	SKIPN	COMWRD		;FIND WHAT YOU WANTED?
	JRST	ILLPAR		;NO YOU LOSE
	JRST	SPLINE		;IS IT LEGAL
DSCON:	MOVE	SIXTXT,COMWRD
	PUSHJ	P,SIXTOT
	PUSHJ	P,LINTRM
	TLZ	FL,DASH
	TLNE	FL,ALTDET	;ALTMODE DETECTED YET?
	JRST	LEEND
	JRST	LNCHK3		;LOOK FOR MORE
;
;
PORD:	TLNE	FL,DELET		;ARE WE DELETING
	JRST	.+3			;YES
	MOVEI	CH,"P"
	POPJ	P,
	MOVEI	CH,"D"
	POPJ	P,
;
;
	PAGE
;	SUBROUTINE TO OPEN 'LINED' CHANNAL AND ISSUE AN 'S'
;	COMMAND TO OPEN THE CURRENT FILE FOR LINED PROCESSING
;
LECOPN:	SKIPN	FILNM.		;ANY FILE AVAIL?
	JRST	NFOX		;NO
;	MOVEI	T1,(SIXBIT /EDT/)
;	HRRM	T1,TMPBLK
;	PUSHJ	P,CHNINT
	TRNN	FL,GPFLAG	;SET INSERT FLAG?
	JRST	LF1		;NO
	MOVEI	CH,"T"
	PUSHJ	P,PUTCHR
	PUSHJ	P,LINTRM
LF1:	MOVEI	CH,"S"
	PUSHJ	P,PUTCHR
LINFE:	PUSHJ	P,OTFX		;FILE.EXT
LINTRM:	PUSH	P,CP
	MOVE	CP,[POINT 7,SCRLF]
	PUSHJ	P,CHROUT
	POP	P,CP
	POPJ	P,
;
;
LEEND:	TLNN	FL,DELET
	JRST	LEEND1
	MOVEI	CH,"E"
	PUSHJ	P,PUTCHR
	PUSHJ	P,LINTRM
LEEND1:	TLZ	FL,DELET	;TURN OFF DELETE FLAG
	MOVEI	CH,"X"
	PUSHJ	P,PUTCHR
	PUSHJ	P,LINTRM
;	CLOSE	DSKOCH,
;	MOVE	T1,[SIXBIT /SLINED/]
	JRST	STARTX+1
;
SIMTYP:	TLNE	FL,DELET		;IN DELETE MODE 
	JRST	ILLPAR		;YOU SHOULD NEVER BE HERE
	PUSHJ	P,PORD		;	SETUP P COMMAND
	PUSHJ	P,PUTCHR
	MOVSI	SIXTXT,(SIXBIT /.,/)	;SETUP REST OF COMMAND
	PUSHJ	P,SIXTOT
	MOVE	SIXTXT,[SIXBIT /99999/]
	PUSHJ	P,SIXTOT
	PUSHJ	P,LINTRM	; LINE TERMINATOR
	JRST	LEEND1		; LET IT DO ITS THING.
;
;
	PAGE
;
INSCOM:	MOVEI	IOPNT,DSKOCH
	PUSHJ	P,ORDMOD		;SETUP FOR SPECIAL OUTPUT MODE
	PUSH	P,CH
	TRO	FL,GPFLAG	;SET THIS SO INSERT MODE GETS SET
	PUSHJ	P,LECOPN
	TRZ	FL,GPFLAG	;CLEAR FLAG
	POP	P,CH
INRPT:	PUSHJ	P,DECOD		;ANY PARAMS
	SETZM	T2		;INIT T2
	SKIPN	T1,COMWRD	;ANY THING THERE
	JRST	INCM3		;NO
	PUSHJ	P,LEGAL		;CHECK FOR LEGALITY
	MOVE	T1,COMWRD
	CAIN	CH,":"
	JRST	INCM1		;EXPECTING ANOTHER PARAM
	CAIN	CH,","		;MORE INSERTS EXPECTED
	JRST	NSTEP	;
	CAIE	CH,ALTMOD
	JRST	ILLPAR
INCM1:	PUSHJ	P,DECOD		;SEE WHAT IT IS
INCM2:	SKIPN	T2,COMWRD
	JRST	NSTEP		;NULL STEP SIZE FOUND
	PUSHJ	P,LEGAL		;OTHERWISE SEE IF ITS LEGAL
NSTEP:	CAIN	CH,","	;MORE COMMANDS
	TLO	FL,MORE		;YES
	MOVEI	CH,"I"		; I COMMAND
	PUSHJ	P,PUTCHR
	MOVE	SIXTXT,T1	;OUTPUT PARAM 1
	PUSHJ	P,SIXTOT
	MOVEI	CH,","		;SEPARATER
	PUSHJ	P,PUTCHR
	MOVE	SIXTXT,T2
	PUSHJ	P,SIXTOT	;SECOND PARAMETER
	PUSHJ	P,LINTRM	;FINISH UP
	TLZE	FL,MORE		;MORE INSERTION EXPECTED
	JRST	INRPT		;YES
	MOVEI	CH,"E"
	PUSHJ	P,PUTCHR
	PUSHJ	P,LINTRM
	JRST	LEEND
;
INCM3:	MOVSI	T2,(SIXBIT /10/)	;STANDARD INCREMENT

	JRST	NSTEP		;CONTINUE
;
;
;
NUMBER:	TLO	FL,LINCK		;CKING FOR A NUMBER
LEGAL:	MOVE	T4,COMWRD
	TRNE	T4,77		;IS # 5 DIGITS 
	JRST	LG3		;NO
LG1:	JUMPE	T4,LG2		;FINISH CHECKING IT
	SETZM	T3
	LSHC	T3,6
	CAIL	T3,20
	CAILE	T3,31
	JRST	LG3
	JRST	LG1
LG2:	TLZN	FL,LINCK
	POPJ	P,
	MOVE	T4,COMWRD
	TRZE	FL,RF
	JRST	LG5
	MOVEM	T4,BEGLIN	;SAVE IT
	TRO	FL,LINE
	POPJ	P,
LG3:	TLZN	FL,LINCK
	JRST	ILLPAR
	MOVE	CP,POINTR	;RESTORE POINTER
	TRZE	FL,RF
	JRST	LG4
	SETZM	BEGLIN
	POPJ	P,
LG4:	SETZM	REPTCT
	POPJ	P,
LG5:	MOVEM	T4,REPTCT	;SAVE REPEAT COUNT
	TRO	FL,REPT
	POPJ	P,
;
;
	PAGE
;
;	ROUTINE TO LOCATE ASCII STRINGS AND TYPE THE LINES OR LINES
;	IN WHICH THEY OCCUR
;		MAXIMUM SEARCH ALLOWABLE :	100 CHARACTERS PER LOCATE COMMAND
;		
;	THE FOLLOWING TECO SEQUENCE OF COMMANDS IS GENERATED:
;
;	ER<FILE.EXT>$<:_@ASCII STRING@$;0L!T!(.A-48)"ECOT$'TL>^G(BELL)$$
;
EDIT:	TLO	FL,EDITFL		;SET EDIT MODE
LOCAT:	SKIPN	FILNM.		;ANY FILE AVAILABLE YET?
	JRST	NFOX		;NOPE...TELL HIM SO!
	MOVEI	IOPNT,DSKOCH		;INIT CHANNAL POINTER
	MOVEI	T1,(SIXBIT /EDT/)	;TMP FILE NAME
	HRRM	T1,TMPBLK
	PUSHJ	P,CHNINT		;INIT OUTPUT CHANNAL
	TLNE	FL,EDITFL
	JRST	EDITOR
	FLUSH	SSTRNG,10
	TLNN	FL,SEQMOD		;IN SEQUENCE MODE?
	PUSHJ	P,GETSTR	;NO...
	TLNN	FL,ALTDET	;MUST FIND AN ALTMODE..
	JRST	LOCER		;OTHERWISE SOMETHING IS WRONG.
	JRST	LOC2
;
;
;
;	SUBROUTINE TO EXTRACT STRINGS SET OFF BY MATCHING DELIMETERS
;
GETSST:	MOVE	T1,[POINT 7,ISTRNG]
	SKIPA
GETSTR:	MOVE	T1,[POINT 7,SSTRNG]	;SET UP TO DEFINE SEARCH STRING
	TLNE	FL,ALTDET		;MUST NOT HAVE REACHED AN ALTMODE
	JRST	LOCER
	HRLZI	T2,^D-50		;MAX STRING LENGTH
	ILDB	CH,CP			;FIND STRING BRACKET CHARACTER
	CAIN	CH," "			;SKIP BLANKS
	JRST	.-2
	HRRZ	T3,CH			;SAVE WHATEVER WAS FOUND
LOC1:	ILDB	CH,CP			;INPUT THE STRING
	CAIN	CH,ALTMOD		;FIND LINE TERMINATOR BEFORE STRING DELIMETER
	JRST	LOCER			;FAULTY COMMAND..SORRY YOU LOSE
	CAIN	CH,@T3			;THIS THE DELIMETER?
	JRST	LOC1X
	IDPB	CH,T1			;SAVE A STRING CHARACTER
	AOBJN	T2,LOC1			;GET ANOTHER
	JRST	LOCER			;YOU DID A N0-NO ..TO MANY CHARS IN SEARCH STRING
;
LOC1X:	TRNN	T2,777777		;IF T2 =0 NO CHARACTERS IN STRING
	JRST	LOCER
	ILDB	CH,CP		;SCAN FOR A BLANK
	CAIN	CH," "
	JRST	.+4
	CAIE	CH,ALTMOD		;OR ALTMODE
	JRST	.-4
	TLO	FL,ALTDET		;IF ALT DETECTED SET FLAG
	POPJ	P,
	POPJ	P,
;
;
;
LOC2:	MOVSI	SIXTXT,(SIXBIT /ER/)		;START TO OUTPUT COMMANDS
	PUSHJ	P,SIXTOT
	PUSHJ	P,OTFX			;OUTPUT FILE.EXT
	PUSHJ	P,ALTOUT		;AND AN ALTMODE
	MOVSI	SIXTXT,(SIXBIT /<:_/)	;MORE OF COMMAND
	PUSHJ	P,SIXTOT
	MOVE	CP,[POINT 7,SSTRNG]
	PUSHJ	P,CHROUT		;OUTPUT STRING
	PUSHJ	P,ALTOUT		;AND AN ALTMODE
	MOVE	CP,[POINT 7,SCT]	;LAST PART OF COMMAND
	PUSHJ	P,CHROTA
;
	MOVEI	CH,7			;NEED A BELL TO EXIT TO SUDS
	PUSHJ	P,PUTCHR
	PUSHJ	P,ALTOUT		; 2 ALTMODES
	PUSHJ	P,ALTOUT
	CLOSE	DSKOCH,
	MOVE	T1,[SIXBIT /STECO/]
	JRST	STR			;START TECO
;
;

SCT:	ASCII /;0L!T!(.A-48)"E/
	ASCII	/COT$'/
	ASCIZ /TL>$^G/


;
LOCER:	TLNE	FL,EDITFL
	JRST	FINERR
	PUSHJ	P,WHAT
	JRST	PHAS7
;
ALTOUT:	MOVEI	CH,175
	PUSHJ	P,PUTCHR
	POPJ	P,
;
	PAGE
FINDCOM:
EDITOR:	MOVEI	MSGPNT,^D10		;SET APPROPRIATE MSG POINTER
	MOVEM	CP,POINTR		;MAY NEED THIS POINTER LATER
	PUSHJ	P,DECOD		;LOOK FOR A LINE #
	SKIPN	T1,COMWRD	;BETER BE SOMETHING THERE
	SETOM	COMWRD		;RETURN HERE CAUSED BY EITHER
				;NO LINE # OR A DECOD PROCESSING DELIMETER
				;WAS DETECTED BEFORE ANY THING ELSE--
				; SET T1 TO -1 AS A DUMMY ARG FOR NUMBER CK
	PUSHJ	P,NUMBER	;IS WHAT WE FOUND A NUMBER?	IF IT IS
				; 'LINE' IN FL GETS SET TO 1 AND 'BEGLIN'
				; GETS THE NUMBER OTHERWISE 'CP' IS 
				; IS RESTORE TO BE ABLE TO RESCAN ARGUMENT
	MOVEI	MSGPNT,^D12	; 
	FLUSH	SSTRNG,10
	PUSHJ	P,GETSTR		;EXPECTING TO SEE A @STRING@
	HRRZM	T2,DELCNT		;SAVE DELETE COUNT IN CASE LATER NEED ARISES.
	PUSHJ	P,DECOD		;GET THE MODE PART OF ARGUMENTS
	SKIPN	T1,COMWRD	;MUST BE SOMETHING THERE
	JRST	FINERR		;IF NOT BOMB HIM AN ATTEMPT TO EXPLAIN
	MOVEI	MSGPNT,^D11
	MOVEM	CH,T3		;SAVE C(CHARACTER) TEMPORARLY
	PUSHJ	P,PEEK		;PEEK AT NEXT NON-BLANK CHARACTER
	CAIN	CH,"?"		;DOES IT APPEAR TO BE WAIT MODE REQUEST?
	PUSHJ	P,SWAIT		;YES...SETUP WAIT MODE FLAG
	SETOM	T2		;FIND OUT IF MODE PARAM IS LEGAL
	LSH	T1,6
	LSH	T2,-6
	JUMPN	T1,.-2
	MOVSI	T1,-FSTABL
SECCK:	MOVE	T3,FSTAB(T1)
	ANDCM	T3,T2
	CAMN	T3,COMWRD
	JRST	EDIT1		;FOUND	ONE
	AOBJN	T1,SECCK
	JRST	FINERR
EDIT1:	XCT	FSDIS(T1)	;SET APPROPRIATE MODE FLAG
	TRNN	FL,WAITFL
	JRST	EDIT2		;NO BLANK TERMINATOR SEARCH WHEN NOT IN WAIT MODE
REMBLK:	ILDB	CH,CP		;SCAN	FOR A BLANK. WAIT ARG TERMINATOR
	CAIN	CH," "		;BLANK?
	JRST	EDIT2		;YES CONTINUE 
	CAIN	CH,33		;ALTMOD?
	JRST	EDIT1A		;THIS IS ALLOWABLE IN DELETE MODE ONLY
	JRST	REMBLK
;
;
SWAIT:	TRO	FL,WAITFL	;SET WAIT FLAG
	CAIN	T3,"?"		;WAS ORIG CHAR A '/'
	POPJ	P,		;YES
	ILDB	CH,CP		;MOVE POINTER UNTIL '?' IS REACHED
	CAIE	CH,"?"
	JRST	.-2
	POPJ	P,
;
;
;
EDIT1A:	TLO	FL,ALTDET	;SET ALTMODE DETECTED FLAG
	TRNN	FL,SECDEL	;DELETE MODE?
	JRST	FINERR		;NO .	WONT ALLOW SUCH NONSENSE!
EDIT2:	MOVEI	MSGPNT,^D12
	TRNE	FL,SECDEL	;NO STING SEARCH IN DELETE MODE
	JRST	EDIT2C
	FLUSH	ISTRNG,10
	PUSHJ	P,GETSST	;GET INSERT STRING
EDIT2C:	SETZM	REPTCT		;****SOMEHOW JUNK GOT INTO THIS LOC..THIS IS A QUICK FIX
				;WHICH SHOULD BE MODIFIED 
	PUSHJ	P,DECOD		;LOOK FOR EITHER REPEAT COUNT OR LIST MODE
	SKIPN	T1,COMWRD	;SOMETHING FOUND?
	JRST	EDIT3		;PROCEED TO OUTPUT COMMAND STRING
	TRO	FL,RF		;SET LOOKING FOR REPETITION FLAG
	PUSHJ	P,NUMBER	;START BY LOOKING FOR A REPEAT COUNT
				;IF ONE IS FOUND REPT FLAG IS SET
				;AND VALUE IS STORED IN 'REPTCT'
	MOVEI	MSGPNT,^D13
	TRNE	FL,REPT		;IF A COUNT WAS FOUND ANOTHER SCAN IS NEEDED
	PUSHJ	P,DECOD
	SKIPN	T1,COMWRD	;POSSIBLE LIST ARG?
	JRST	EDIT3		;NEGATIVE
	SETOM	T2		;OTHERWISE SCRUTINIZE
	LSH	T1,6
	LSH	T2,-6
	JUMPN	T1,.-2
	MOVE	T3,[SIXBIT /LIST/]
	ANDCM	T3,T2
	CAME	T3,COMWRD		;LOOK OK?
	JRST	EDIT2S
	JRST	EDIT2X
;
;
EDIT2S:	TRNN	FL,SECDEL	;WHEN IN DELETE-MODE ASSUME
	JRST	EDIT2L		;GARBAGE WAS TYPED
	TRNE	FL,REPT		;SKIP NEXT MESS IF A LEGAL REPT COUNT WAS FOUND
	JRST	EDIT2L
	MOVEI	MSGPNT,^D14	;OTHERWISE TAKE A LOOK AT 'LIST ' ARG / IS IT OK?

	JRST	FINERR
EDIT2L:	XCT	MESAGE(MSGPNT)	;NOT REALLY . BUT ILL WORK ON THE 
				;ASSUMPTION HE TRIED TO TYPE LIST
				;AND TELL HIM THATS WHAT HES GETTING
EDIT2X:	TRO	FL,LISFLG	;SET THE LIST REQUESTED FLAG
;
;
EDIT3:	MOVSI	SIXTXT,(SIXBIT /EB/)	;MAKE THE COMMAND FILE
	PUSHJ	P,SIXTOT
	PUSHJ	P,OTFX		;FILE NAME
	PUSHJ	P,ALTOUT
	MOVE	CP,[POINT 7,FR0]
	PUSHJ	P,CHROTA
	SKIPN	BEGLIN			;ANY PARTICULAR STARTING LINE
	JRST	EDIT4			;NO
	MOVSI	SIXTXT,(SIXBIT /:N/)	;DO THE LINE SEARCH FIRST
	PUSHJ	P,SIXTOT
	MOVE	T1,[SIXBIT /000000/]	;MAKE BEGLIN 5 CHARACTERS
	MOVE	T2,BEGLIN
EDIT3B:	TRNE	T2,007700
	JRST	EDIT3X
	LSHC	T1,-6
	JRST	EDIT3B
EDIT3X:	MOVE	SIXTXT,T2		;OUTPUT LINE #
	PUSHJ	P,SIXTOT
	PUSHJ	P,ALTOUT
	MOVE	CP,[POINT 7,FR1]
	PUSHJ	P,CHROTA
EDIT4:	MOVE	CP,[POINT 7,FR2]
	PUSHJ	P,CHROTA
	SKIPN	SIXTXT,REPTCT		;IS THERE A REPEAT ?
	MOVSI	SIXTXT,(SIXBIT /1/)	;DEFAULT IS 1 SEARCH
	PUSHJ	P,SIXTOT
	MOVE	CP,[POINT 7,FR3]
	PUSHJ	P,CHROTA
	MOVE	CP,[POINT 7,SSTRNG]	;OUTPUT SEARCH STRING
	PUSHJ	P,CHROUT
	PUSHJ	P,ALTOUT
	FLUSH	SSTRNG,10		;WIPE IT CLEAN
	MOVE	CP,[POINT 7,FR4]
	PUSHJ	P,CHROTA
	TRZE	FL,WAITFL		;DOES HE WANT WAIT MODE
	JRST	EDIT4B			;YES
	MOVE	CP,[POINT 7,FR4X]	;NEED THIS TECO COMMAND
	PUSHJ	P,CHROTA
	JRST	EDIT5
EDIT4B:	MOVE	CP,[POINT 7,FR5]	;GIVE HIM WAIT MODE
	PUSHJ	P,CHROTA
EDIT5:	MOVE	CP,[POINT 7,FR6]
	PUSHJ	P,CHROTA
	TRNN	FL,SECDRP+SECDEL		;REPLACE OR DELETE MODE?
	JRST	EDIT5B		;NO
	HRREI	T4,-6		;YES CONVERT DELETE COUNT TO SIXBIT
	SETZM	T2
	SETZM	T3
	MOVE	T1,DELCNT
EDIT5A:	IDIVI	T1,^D10
	ADDI	T2,20		;MAKE SIXBIT NUMBER
	ROTC	T2,-6
	AOJGE	T4,EDIT6
	JRST	EDIT5A
EDIT5B:	MOVSI	T3,(SIXBIT /0/)	;DEFAULT IS ZERO
EDIT6:	MOVE	SIXTXT,T3
	PUSHJ	P,SIXTOT
	MOVE	CP,[POINT 7,FR7]
	PUSHJ	P,CHROTA
	TRNE	FL,SECDEL
	JRST	EDIT6X
	MOVE	CP,[POINT 7,ISTRNG]
	PUSHJ	P,CHROUT
EDIT6X:	PUSHJ	P,ALTOUT
	FLUSH	ISTRNG,10		;WIPE IT CLEAN
	TRZN	FL,LISFLG		;LIST MODE REQUESTED?
	JRST	EDIT7		;NO
	MOVE	CP,[POINT 7,FR10]
	PUSHJ	P,CHROTA
EDIT7:	MOVE	CP,[POINT 7,FR8]
	PUSHJ	P,CHROTA
EDIT7P:	MOVEI	CH,7		;LAST THING OUT IS A (BELL) CHAR
	PUSHJ	P,PUTCHR
	PUSHJ	P,ALTOUT
	PUSHJ	P,ALTOUT
	CLOSE	DSKOCH,
	MOVE	T1,[SIXBIT /STECO/]
	JRST	STR		;START TECO
;
;
;
FINERR:	TTCALL	3,[BYTE(7)15,12,0,0,0]
	XCT	MESAGE(MSGPNT)
	TTCALL	3,[BYTE(7)15,12,12,0,0]
	JRST	PHAS7
;
;
DEFINE TABLE<
FCOM INSERT,<TRO FL,SECINS>
FCOM REPLACE,<TRO FL,SECDRP>
FCOM DELETE,<TRO FL,SECDEL>
>
;
DEFINE FCOM (A,B)
<<SIXBIT /A/>>
;
FSTAB:	TABLE
FSTABL=.-FSTAB
;
DEFINE FCOM (A,B)
<B>
;
FSDIS:	TABLE
;
;
;	TECO COMMAND STRING TO PERFORM FIND /REPLACE,INSERT,DELETE FUNCTION
;
FR0:	ASCIZ /IEND OF FILE
$0JHXW$HK$ILINE NOT FOUND
$HXY$HK$IWHAT?
$0J1XZHK$!T!/
FR1:	ASCIZ	/"E0L$GY$-L$T$K$OT5$'.U10L$(Q1-5-.)"NQ1JOT$'0UA/
FR2:	ASCIZ /!T1!(/		;REPEAT COUNT
FR3:	ASCIZ /-QA)"EOT5$':N/	;SEARCH STRING
FR4:	ASCIZ /"E0L$GW$-1L$1T$1K$OT5$'!T6!.U2$0L$(Q2-6-.)"LQ2J}OT1$'/	;WAIT MODE
FR4X:	ASCIZ	/Q2J$/
FR5:	ASCIZ /!T7!$(.A-48)"E$C$OT7$'T$Q2J$!T2!U3$(Q3-83)"E$OT5$'(Q3-89)"EOT3$'(Q3-78)"NGZ7RTKOT2$'OT4$/
FR6:	ASCIZ /!T3!-/		;DELETE COUNT
FR7:	ASCIZ /D$I/		;INSERT STRING
FR8:	ASCIZ /!T4!%A$OT1$!T5!<:N%&@?!@&$;>$HPEF$^G/
;
FR10:	ASCIZ	/.U5$0L$!T8!(.A-48)"EC$OT8$'T$Q5J$/
	PAGE
;
;
;
;	ROUTINE TO COMPILE THE CURRENT FILE
;	CALLS CUSP COMPIL....ISSUES COMPILE /COMPILE ###SRC.TMP
;	VIA	###EDS.TMP COMMAND FILE.
;	
;
CMPCOM:	TLO	FL,COMPFL		;SET COMPILE MODE
RUNCOM:	MOVEI	IOPNT,DSKOCH		;INIT CHANNAL POINTER
	HRRZ	T1,SYSNM.		;GET SYSTEM #/ IS IT BASIC?
	CAIN	T1,2
	JRST	PREBAS			;YES.
	CAILE	T1,4		;IN "EDIT" MODE SYSTEM
	JRST	EMOD		;YES..DONT ALLOW RUN OR COMPILE COMMAND
;
	MOVEI	T1,(SIXBIT /SVC/)	;INIT COMMAND FILE HEADER
	HRRM	T1,TMPBLK
	PUSHJ	P,CHNINT
	MOVE	T2,SYSNM.		;SET APPROPRIATE SYSTEM NAME
	MOVE	T1,AVSYS(T2)		;	FOR COMMAND FILE OUTPUT
	MOVEM	T1,RCSYS		;	SAVE IT
	TLNN	FL,COMPFL		;COMPILE MODE?
	JRST	R0			;NO
	TLNE	FL,ALTDET		;WAS AN ALTMODE DETECTED YET?
	JRST	C1		;YES..YOUR IN GOOD SHAPE 
	PUSHJ	P,DECOD		;IF I DONT FIND ONE BEFORE SOME CHARACTERS
	TLNN	FL,ALTDET	;	HE IS	CANCELLED!...
	JRST	ILLPAR
	SKIPN	COMWRD		;THIS MUST BE EMTPY TOO!
	JRST	C1		;LOOKS LIKE HE KNOWS WHAT HES DOING..(I HOPE)
	JRST	ILLPAR		; SORRY BOUT THAT...
C1:	MOVE	CP,[POINT 7,CCLC1]	;PUSH OUT COMMAND
	PUSHJ	P,CHROUT
	PUSHJ	P,FORSYS		;FOR COMPILE WITH APPRP. SYS
	MOVE	SIXTXT,JOBN
	PUSHJ	P,SIXTOT
	MOVE	CP,[POINT 7,CCLC2]
	PUSHJ	P,CHROUT
	JRST	R3			;FINISH UP..
;
;
R0:	PUSH	P,CP		;SAV COMMAND POINTER
	TRO	FL,RNP			;SET NO PARAMS FLAG	ON
R1AA:	MOVE	CP,[POINT 7,CCLC3]
	PUSHJ	P,CHROUT		;PUSH OUT COMMAND
	POP	P,CP		;RESTORE POINTER
	PUSHJ	P,FORSYS		;IF A COMPILE IS GENERATED FOR THIS SYSTEM
R1:	PUSHJ	P,GTNAME		;GO LOOK FOR FILNAME.EXT
	SKIPN	T1,FILEN		;FIND ONE?
	JRST	R2			;NO
	TRZ	FL,RNP			;RESET FLAG
	CAME	T1,FILNM.		;CHECK TO SEE IF FILENAME.EXT
	JRST	R1A			;	MATCHES CURRENT FILE NAME
	SKIPN	T1,EXTN			;	IF IT DOES CHANGE TO ###SRC.TMP
	JRST	R1A
	CAME	T1,FILNM.+1		; EXT'S =	?
	JRST	R1A
	MOVE	T1,[XWD TEMPN,FILEN]	;CHANGE TO C(TMP) FILE NAME
	BLT	T1,FILEN
	SETZM	EXTN
R1A:	MOVE	SIXTXT,FILEN		;PUSH OUT A FILE.EXT
	PUSHJ	P,SIXTOT
	SKIPN	SIXTXT,EXTN		;IS THERE AN EXT?
	JRST	R1D			;NO
	MOVEI	CH,"."
	PUSHJ	P,PUTCHR
	PUSHJ	P,SIXTOT		;PUSH OUT EXTENSION
R1D:	TLNN	FL,MORE			;MORE FILE NAMES EXPECTED?
	JRST	R2			;NO
	MOVEI	CH,","			;PUSH OUT COMMA
	PUSHJ	P,PUTCHR
	JRST	R1			;GET NEXT NAME
R2:	TRNN	FL,RNP			;THIS A SIMPLE RUN COMMAND?
	JRST	R3			;NO
	MOVE	SIXTXT,JOBN		;UNIQUE JOB#
	PUSHJ	P,SIXTOT
	MOVE	CP,[POINT 7,CCLC2]
	PUSHJ	P,CHROUT
R3:	MOVE	CP,[POINT 7,SCRLF]	;LINE TERMINATOR
	PUSHJ	P,CHROUT
	CLOSE	DSKOCH,
	MOVE	T1,[SIXBIT /SOMPIL/]
	JRST	STR			;START COMPIL CUSP..
;
FORSYS:	MOVE	SIXTXT,RCSYS	;GET SYSTEM HE IS UNDER
	PUSHJ	P,SIXTOT
	MOVEI	CH," "
	PUSHJ	P,PUTCHR
	POPJ	P,
;
;
;
;	ROUTINE TO RUN BASIC PROGRAMS
;
;
PREBAS:	TLNN FL,ALTDET
	JRST ILLPAR
BASRUN:	MOVEI T1,(SIXBIT/SRC/)
	HRRM T1,TMPBLK
	MOVE SIXTXT,JOBN
	HLLM SIXTXT,TMPBLK
	HLLM SIXTXT,BBBTMP
	MOVEI T1,(SIXBIT/TMP/)
	HRLM T1,TMPBLK+1
	INIT DSKOCH ,16
	SIXBIT/DSK/
	0
	JFCL
	TLNN FL,COMPFL	;COMPILE ONLY?
	SKIPA 	;NO
	JRST SBAS1	;YES
	LOOKUP DSKOCH ,TMPBLK
	JFCL
	LDB T2,[POINT 11,TMPBLK+2,23]
	MOVEI T1,(SIXBIT/RUN/)
	HRLM T1,TMPBLK+1
	LOOKUP DSKOCH ,TMPBLK
	JRST SBAS
	LDB T3,[POINT 11,TMPBLK+2,23]
	CAIGE T3,(T2)
	JRST SBAS2
RUNTIM:	MOVE T1,[SIXBIT/RUNTIM/]
	JRST STR1
SBAS2:	SETZM TMPBLK+4
	SKIPA
SBAS1:	SETOM TMPBLK+4	;SET FLAG FOR SBAS SO WILL ONLY COMPIL
SBAS:	MOVSI T1,(SIXBIT/TMP/)
	MOVEM T1,TMPBLK+1
	MOVE T1,[SIXBIT/SBAS/]
STR1:	SETZM TMPBLK+2
	MOVEI T2,(SIXBIT/BAS/)
	HRRM T2,BBBTMP
	MOVSI T2,(SIXBIT/TMP/)
	HLLM T2,BBBTMP+1
	SETZM BBBTMP+2
	SETZM BBBTMP+3
	INIT DSKOCH,16
	SIXBIT/DSK/
	0
	JFCL
	ENTER DSKOCH,BBBTMP
	JFCL
	OUTPUT DSKOCH,BASLST
	CLOSE DSKOCH,
	MOVEM T1,CCLDCH+1
	HRRZI T1,CCLDCH
	CALLI T1,35
        JRST STRA1
	JRST STARTA

;
;
BC1:	ASCIZ	/OLD
/
BC2:	ASCIZ	/RUNNH
EXIT
/
;
;
;
EMOD:	TTCALL	3,[ASCIZ /ILLEGAL COMMAND WHEN IN "EDIT" SYSTEM.

/]
	JRST	PHAS7
	PAGE
;
;
;	ROUTINE TO SAVE ###SRC.TMP AND ###SRC.REL(IF IT EXISTS)
;	IN PERMANENT STORAGE UNDER C(FILENAME.EXT) AND C(FILNAME.REL)
;	RESPECTIVELY.
;
REPCOM:	TRO	FL,REPLAC	;SET REPLACE MODE
SAVCOM:	SKIPN	FILNM.		;IS THERE A FILENAME TO USE?
	JRST	NFOX		;NO....DUMMY YOU	CANT SAVE WITHOUT A NAME
	PUSHJ	P,GTNAME	;LOOK FOR A FILENAME
	SKIPN	FILEN		;FIND ONE?
	JRST	SVC1		;NO
	TRO	FL,GPFLAG	;YES : SET FLAG	FOR FILENAME RESTORE
	PUSH	P,FILNM.
	PUSH	P,FILNM.+1	;PUT NAME AND EXT ON PDL
	MOVE	T1,FILEN	;GET NAME TO SAVE WITH
	MOVEM	T1,FILNM.
	MOVE	T1,EXTN		;AND EXT.
	MOVEM	T1,FILNM.+1
SVC1:
	TRNE	FL,REPLAC	;IN REPLACE MODE?
	JRST	REPSAV		;YES....AUTOMATICALLY WIPE OUT AN EXISTING FILE
	PUSHJ	P,CHNOPN	;TEST FOR EXISTING FILE
	LOOKUP	TSTCHN,FILNM.
	JRST	REPSAV		;NONE EXISTS
	JRST	FLOREX	;FOUND IT AND WONT LET YOU INADVERTANTLY DESTROY
REPSAV:	PUSHJ	P,PIPOPN		;OPEN PIP COMMAND CHANNAL
	PUSHJ	P,PRMTMP		;SET UP PIP COMMANDS
	PUSHJ	P,CHNOPN		;OPEN A TEST CHANNAL TO SEE IF 
	MOVE	T1,JOBN			;###SRC.REL EXISTS
	HLLM	T1,TEMPN
	MOVSI	T1,(SIXBIT /REL/)
	HRRZ T3,SYSNM.
	CAIN T3,2
	MOVSI T1,(SIXBIT/RUN/)
	HLLM	T1,TEMPN+1
	LOOKUP	TSTCHN,TEMPN		;DOES ###SRC.REL EXIST?
	JRST	STSAVE		;NO
	PUSHJ	P,LINTRM
	PUSHJ	P,PRMREL		;.REL TRANSFER
STSAVE:	TRZN	FL,GPFLAG	;RESTORE FILENAME IF NECESSARY
	JRST	.+3		;NOT NECESSARY.
	POP	P,FILNM.
	POP	P,FILNM.+1
	JRST	PIPSTR		;START UP
;
FLOREX:	TTCALL	3,[ASCIZ /FILE NAME /]
	MOVE	T1,FILNM.+1		;CLEAR (RH) DUE TO LOOKUP
	TRZ	T1,777777
	MOVEM	T1,FILNM.+1
	MOVE	T4,FILNM.	;OUTPUT ERROR MSG
	PUSHJ	P,SIXOUT
	SKIPN	T4,FILNM.+1
	JRST	FLOR1
	MOVEI	T3,(SIXBIT /  ./)
	ROTC	T3,-6
	PUSHJ	P,SIXOUT
FLOR1:	TTCALL	3,[ASCIZ / IN USE!!

/]
	JRST	PHAS7		;START OVER AGAIN
;
;
PRMTMP:	MOVE	SIXTXT,DEVNM.		;OUTPUT DEV NAME
	PUSHJ	P,SIXTOT
	MOVEI	CH,":"
	PUSHJ	P,PUTCHR
	PUSHJ	P,OFE		;PERMANENT FILENAME.EXT
	MOVEI	CH,"_"
	PUSHJ	P,PUTCHR
	MOVE	CP,[POINT 7,OLD1]
	PUSHJ	P,CHROUT
	PUSHJ	P,OTFX		;###SRC.TMP
	HRRZ	T1,SYSNM.	;GET SYSTEM NUMBER
	CAIE	T1,5		;IS IT EDIT (DATA MODE)
	JRST	PRMEX		;NO
	MOVSI	SIXTXT,(SIXBIT ./N.)	;YES ..SAVE WITHOUT SEQ NUMBERS
	PUSHJ	P,SIXTOT
	MOVSI	SIXTXT,(SIXBIT ./A.)
	PUSHJ	P,SIXTOT
PRMEX:	POPJ	P,		;RETURN
;
;
PRMREL:	HRRZ 16,SYSNM.
	MOVE	SIXTXT,DEVNM.	;OUTPUT DEV NAME
	PUSHJ	P,SIXTOT
	MOVEI	CH,":"
	PUSHJ	P,PUTCHR
	MOVE	SIXTXT,FILNM.	;FILENAME
	PUSHJ	P,SIXTOT
	MOVE	CP,[POINT 7,OLD3]	;.REL EXTENSION
	CAIN 16,2
	MOVE CP,[POINT 7,OLD4]
	PUSHJ	P,CHROUT
	MOVEI	CH,"_"
	PUSHJ	P,PUTCHR
	MOVE	CP,[POINT 7,OLD1]
	PUSHJ	P,CHROUT
	MOVE	SIXTXT,JOBN		;JOB #
	PUSHJ	P,SIXTOT
	MOVE	CP,[POINT 7,OLD2]
	CAIN 16,2
	MOVE CP,[POINT 7,OLD5]
	PUSHJ	P,CHROUT
	POPJ	P,		;RETURN
	PAGE
;	ROUTINE TO CHANGE C(FILENAME)	RENAME COMMAND 
;	FORMAT:	RENAME FILENAME
;	WITH NO NAME SPECIFIED	YOU ARE ASKED FOR ONE..
;
RENCOM:	TRO	FL,REPROC	;SET RENAME PROCESSING FLAG
	MOVEI	MSGPNT,^D15	;SET APPROPRIATE MESG POINTER
	SETZM	FILNM.		;CLEAR C(FILNAME)
	SETZM	FILNM.+1
	MOVEI	IOPNT,TTYCC	;TTY COMMAND CHANNAL
	TLZ	FL,OLDT+NEWT+EXTEN	;CLEAR JUST IN CASE
	JRST	SCANL		;DO IT....
;
REN1:	HRRZ T3,SYSNM.
	CAIN T3,2
	PUSHJ P,BASFIL
	PUSHJ	P,CHNOPN	;OPEN AN OUTPUT CHANNAL FOR SYS.TMP
	TLO	FL,NEWT		;FAKE FLAG TO GET TO READY STATE FROM 
	JRST	PHAS5		; PHAS5 PROCESSING..
;
;
;
;
	PAGE
;
;
;	COMMANDTO HELP USER.....LIST CURRENTLY AVAILABLE COMMANDS
;
;
HLPCOM:	PUSHJ	P,PIPOPN	;OPEN PIP OUTPUT CHAN
;	MOVE T1,[XWD 3,11]
;	CALL T1,[SIXBIT/GETTAB/]
;	JFCL
;	LDB T2,[POINT 7,T1,20]
	MOVE	CP,[POINT 7,HELPER]
;	CAIN T2,"5"
;	MOVE CP,[POINT 7,DHELP]
	PUSHJ	P,CHROUT
	JRST	PIPSTR
;
HELPER:ASCIZ /TTY:_DSK:HELP.TXT[1,4]/

DHELP: ASCIZ /TTY:_DSK:HELP.TXT[1,4]/
;
	PAGE
;
;ROUTINE TO OUTPUT THE CURRENT FILE IN PAGED FORMAT:
;
;
PAGER:	MOVEI	IOPNT,DSKOCH	;OPEN STECO OUTPUT COMMAND FILE
	MOVEI	T1,(SIXBIT /EDT/)
	HRRM	T1,TMPBLK
	PUSHJ	P,CHNINT
	PUSHJ	P,DECOD		;LOOK FOR A STARTING PAGE #
	SKIPN	SIXTXT,COMWRD	;FIND ONE
	MOVSI	SIXTXT,(SIXBIT .1.)	;ASSUME 1
	PUSHJ	P,LEGAL		;IS NUMBER LEGAL
	PUSHJ	P,SIXTOT
	MOVSI	SIXTXT,(SIXBIT .UP.)
	PUSHJ	P,SIXTOT
	MOVE	CP,[POINT 7,PGC]
	PUSHJ	P,CHROTA	;START TO OUTPUT PAGING COMMANDS
	PUSHJ	P,OTFX		;TMP FILE NAME
	PUSHJ	P,ALTOUT
	MOVEI	CH,"I"
	PUSHJ	P,PUTCHR
	PUSHJ	P,OFE		;C(FILE NAME)
	MOVE	CP,[POINT 7,PGC2]
	PUSHJ	P,CHROTA
	JRST	EDIT7P
;
;
PGC:	ASCIZ	/$ERSYS:PAGER.TXT$Y$HXA$HK$ERDSK:/
PGC2:	ASCIZ	/$MA$/
;
	PAGE	
;
;	ROUTINE TO WIPE THE C(FILE.EXT) CLEAN
;	WIPES ###SRC.TMP OF ANY DATA AND DELETES FILE ###SRC.REL
;	IF IT EXISTS.
;
SCRCOM:	SKIPN	FILNM.		;IS THERE A FILE OPENED.?
	JRST	NFOX			;NO.. SO TELL HIM SO.
	TRO	FL,SCRTCH		;SET SCRATCH MODE FLAG ON
	JRST	SCRDIS		;DO YOUR THING...
;
;
	PAGE
;
;
;	ROUTINE TO CALL DEVICE REQUEST INITIATOR TO
;	NOTIFY OPERATOR OF A USER DEVICE REQUEST
;
;
DEVREQ:	MOVSI	T1,1
	HRRI	T1,REQBLK
	CALLI	T1,35		;CALL DEV REQ INIT.
	CALL	1,12
;
REQBLK:	SIXBIT	/SYS/
	SIXBIT	/GETDEV/
	0
	0
	0
	0
;
;
	PAGE
;
;
;	ROUTINE TO SET SYSTEM NAME
;
;
FRSYS:	MOVEI	T1,0		;0=FORTRAN
	JRST	SYSSP
BASYS:	MOVEI	T1,2		;2=BASIC
	JRST SYSSP
MACSYS:	MOVEI	T1,3		;3=MACRO
	JRST	SYSSP

IFE COBOLL,<
COBLNG:	MOVEI	T1,4		;4=COBOL
	JRST SYSSP
>
EDM.5:	MOVEI	T1,5		;EDIT DATA MODE=5
	JRST	SYSSP
EDM.6:	MOVEI	T1,6		;EDIT STANDARD FILE MODE
	JRST	SYSSP
IFN TELCOM,<
TELSYS:	JRST	TELSTR		;EXIT TO TELCOMP..
>
SYSSP:	HRRZM	T1,SYSNM.
	TTCALL	3,SCRLF
	TLO	FL,NEWT		;SET FAKE FLAG
	JRST	PHAS5		;THATS IT....
;
;
IFN TELCOM,<
TELSTR:	HRRZI	T1,TBLK		;START UP TELCOMP..
	CALLI	T1,35
	HALT
>
;
BASCAL:	HRRZI	T1,BASICS
	CALLI	T1,35
	HALT
;
BASICS:	SIXBIT /SYS/
	SIXBIT /BASIC/
	0
	0
	0
	0
TBLK:	SIXBIT	/SYS/
	SIXBIT	/TELCOM/
	0
	0
	0
	0
;
;
	PAGE
;
;
;	ROUTINE TO UNSAVE PROGRAMS OR FILES FROM PERMANENT STORAGE
;	FORMAT:	UNSAVE	(WITH NO PARAMETERS)	WILL UNSAVE
;	THE VERSION OF THE CURRENT VERSION FROM PERM STORAGE IF IT EXISTS.
;			UNSAVE	FILE1,FILE2,FILE3.......	WILL UNSAVE
;	THE NAMED FILES FROM PERMANENT STORAGE 
;	NOTE:	DEVICE NAMES MAY BE SPECIFIED IN THE SECOND FORMAT.
;	I.E.	UNSAVE	DTA3:FILE.EXT,DTA6:FILE2......
;
UNSAVC:	TRO	FL,UNSAVF		;SET UNSAVE MODE FLAG
	PUSH	P,CP		;SAVE CURRENT COMMAND POINTER POSITION.
	PUSHJ	P,DECOD		;ANY PARAMETERS?
	SKIPN	COMWRD		;
	JRST	US1		;NO
;
;
UNSPAR:	MOVEI	IOPNT,DSKOCH		;OPEN APPROP. OUTPUT CHAN
	MOVEI	T1,(SIXBIT /SVC/)
	HRRM	T1,TMPBLK
	PUSHJ	P,CHNINT
	MOVE	CP,[POINT 7,DELW]	;OUTPUT DELETE COMMAND
	PUSHJ	P,CHROUT
	POP	P,CP			;RESTORE COMMAND POINTER
USP1:	ILDB	CH,CP
	PUSHJ	P,PUTCHR
	CAIE	CH,ALTMOD	;END OF COMMAND YET?
	JRST	USP1		;NO
	PUSHJ	P,LINTRM
	CLOSE	DSKOCH,		;WRITE OUT COMMAND FILE
	TTCALL	3,[ASCIZ /
PROGRAMS UNSAVED
/]
	MOVE	T1,[SIXBIT /SOMPIL/]	;CALL COMPIL CUSP
	JRST	STR
;
DELW:	ASCIZ	/DELETE /
;
;
;
US1:	SKIPN	FILNM.		;ANY FILE OPEN?
	JRST	NFOX		;NO..
	JRST	PHAS3X	;GO FIND FILENAME AND .REL OF THAT FILE
US2:	PUSH	P,FILNM.	;IF ONE DOESNT EXIST YOU WONT REACH THIS POINT
	LOOKUP	TSTCHN,FILNM.	;LOOKUP AND DELETE FILE
	HALT
	SETZM	FILNM.
	RENAME	TSTCHN,FILNM.	;GOODBYE FILE..
	HALT
	POP	P,FILNM.	;RESTORE NAME
	TRNE	FL,NOREL	;DOES A.REL EXIST BY THAT NAME
	JRST	US3		;NO
;
	PUSH	P,FILNM.+1	;SAVE ORG EXT.
	MOVSI	T1,(SIXBIT /REL/)
	HRRZ T2,SYSNM.
	CAIN T2,2	;BASIC
	MOVSI T1,(SIXBIT/RUN/)	;YES
	HLLZM	T1,FILNM.+1
	LOOKUP	TSTCHN,FILNM.	;DELETE THE EXISTING REL FILE
	JRST US3-1	;IT FAILED
	SETZM	FILNM.
	RENAME	TSTCHN,
	HALT
	POP	P,FILNM.+1	;RESTORE PRG EXT.
US3:	MOVE	T1,FILNM.+1	;THE LOOKUP REQUIRES WE CLEAN R(HALF) OF FILNM.+1
	TRZ	T1,777777
	MOVEM	T1,FILNM.+1
	RELEASE	TSTCHN,
	JRST	PHAS7
;
	PAGE

;
; ROUTINE TO PLACE LINES TYPED-IN AT COMMAND LEVEL INTO A STACK
; ON ENTRY:
;	LINE TO BE STACKED IS IN	'XILB'
; ON EXIT:
;	THE LINE IS STORED IN 'TABLE' 
;...THE LINES IN TABLE ARE STORED AS DOUBLY-LINKED BLOCKS
;	22 WORDS IN LENGTH
;	WITH A MAXIMUM STORAGE CAPACITY OF 40 WORDS
;	LINES ARE STORED KEYED ON SEQ. NUMBER		.
;				
;	TABLE FORMAT:		WORD 0:(LH)	LEFT LINK
;				WORD 0: (RH)	RIGHT LINK
;				WORD 1: (LH)	NEGATIVE-AREA IN USE
;						POSITIVE/OR ZERO-AREA DUMPED
;				WORD1:(RH)	LENGTH OF LINE
;				WORD 2: (RH)	SEQUENCE NUMBER
;				WORDS 3 THRU 21	LINE DATA
;
	MLON
LIG.1:	SKIPN	FILNM.
	JRST	NFOX
	SETZM XILB
	MOVE T1,[XWD XILB,XILB+1]
	BLT T1,XILB+23
	TLON	FL,STACKM		;SET STACK MODE FLAG=1
	PUSHJ	P,OPNSRC	;OPEN SRCCHN IF NOT ALREADY OPENED.
	PUSHJ	P,LIGINI	;INITIALIZE
LIG.1A:	ILDB	CH,CP		;GET A CHARACTER
	TRNE	FL,GPFLAG		;DATA SCAN ON?
	JRST	LIG.2			;YES
	SKIPN	T2		;LEADING SPACE CHECK
	JRST	[CAIN	CH,40
		 JRST	LIG.1A
		 JRST	LIG.1B]
LIG.1B:	CAIG	CH,"9"			;CHAR A DIGIT?
	CAIGE	CH,"0"			;
	JRST	LIG.3			;NO
	AOJG	T3,LIG.4		;> THAN 5 DIGITS?
	IDPB	CH,T1			; NO	MAKE SEQ NUMBER..
	JRST	LIG.1A			; LOOK AT NEXT CHARACTER
;
LIG.3:	MOVE	T1,[OCT 140603014060]	;FORMAT SEQ NUMBER
	LSHC	T1,7
	JUMPN	T2,.-1
	LSH	T1,1			;CLEAR UP SEQ BIT 35
	TRO	T1,1			;SET SEQ INDICATOR
	MOVEM	T1,XILB
	MOVE	T1,[POINT 7,XILB+1]	;RESET POINTER
	HRREI	T3,^D-80
	TRO	FL,GPFLAG		;SET DATA SCAN ON
;
LIG.2:	CAIN	CH,33		;<CR> NOTE:	33 IS STUFFED 
				; HERE AS <CR> BY INPUT CODE. SEE SECB:
	JRST	LIG.5			;YES
	CAIN	CH,12			;<LF>
	JRST	LIG.5			;YES
	AOJG	T3,LTL.1		;>THAN 80 CHAR'S IF SO LINE IS TO LONG
	PUSHJ P,BLAN	;YES
	IDPB	CH,T1			;SAVE CHARACTER
	JRST	LIG.1A			;LOOK FOR NEXT CHARACTER.
;

BLAN:	ADDI T3,^D79
	JUMPN T3,BLAN1
	SUBI T3,^D79
	CAIE CH,"	"	;IS IT A TAB
	CAIN CH," "	;OR A BLANK
	POPJ P,	;YES OK
	HRRZ 17,SYSNM.
	CAIN 17,2
	JRST DCHAR	;BASIC GO SEE IF FIRST CHAR A D
BLAN2:	PUSH P,CH	;SAVE CHARACTER
	MOVEI CH," "	;GET A BLANK
	IDPB CH,T1	;PUT IT OUT
	AOJ T3,
	POP P,CH	;GET CHAR BACK
	POPJ P,	;RETURN

BLAN1:	SUBI T3,^D79
	POPJ P,

DCHAR:	CAIE CH,"D"	;D?
	JRST BLAN2	;NO 
	MOVE 17,CP
	ILDB 16,17	;GET NEXT CHAR
	CAIE 16,"I"	;COULD BE DIM STATEMENT
	CAIN 16,"A"	;=A?
	JRST BLAN2	;YES SO COULD BE DATA STATEMENT
	MOVEI CH,"	"
	POPJ P,	;DON'T RESTORE THE"D"

LIG.4:	TTCALL	3,[ASCIZ .LINE NUMBER TOO BIG


.]
	SETZM	T4
	JRST	INITTY
LTL.1:	SETOM	T1		;IN PAPER TAPE MODE
	TTCALL	6,T1
	TLNE	T1,2
	JRST	LTL.2			;YES
	TTCALL	3,[ ASCIZ .LINE TOO LONG


.]
	SETZM	T4
	JRST	INITTY
LTL.2:	SETZM	XILB+1
	TTCALL	3,[ASCIZ .LINE .]
	TTCALL	3,XILB
	TTCALL	3,[ASCIZ . TOO LONG


.]
	SETZM	T4
	JRST	INITTY
LIG.5:	MOVEI	CH,15		;FORSE <CRLF>
	IDPB	CH,T1		;SAVE IT
	MOVEI	CH,12
	IDPB	CH,T1
	ADDI	T3,^D82			;CALCULATE CHARACTER COUNT
	CAIN	T3,2		;IF CHAR COUNT = 2 SET LINE LENGTH=1
	JRST	[SETZM T3
		 JRST LIG.5A]	; SO LINE IS DELETED LATER
	IDIVI	T3,5			; WRD COUNT DIVIDED BY 5
	SKIPE	T4
	ADDI	T3,1			;ADD 1 IN CASE OF REMAINDER
LIG.5A:	ADDI	T3,1			;ADD 1 FOR SEQ NUMBER
	PUSHJ	P,STACKER		;STACK THIS LINE
	SETZM	T4
	JRST	INITTY
;
;
LIGINI:	MOVE	CP,[POINT 7,COMBUF]	;RESET POINTER
	HRREI	T3,-5
	TRZ	FL,GPFLAG
	MOVE	T1,[POINT 7,T2]
	SETZM	T2
	POPJ	P,
;
	PAGE
;
	PAGE
;
;	SUBROUTINE TO PICK OFF FILENAME AND EXTENSION	FROM COMMAND
;	STRINGS
;	FILENAME RETURNED IN 'FILEN'
;	EXTENSION RETURNED IN 'EXTN'
;	CP MUST CONTAIN POINTER TO C(COMMAND CHARACTER)
;	CALL	PUSHJ	P,GTNAME
;
;
GTNAME:	TLZ	FL,MORE+EXTEN		;CLEAR FLAGS
	SETZM	FILEN
	SETZM	EXTN
G1:	PUSHJ	P,DECOD		;GET SOMETHING (HOPEFULLY A FILENAME)
	SKIPN	T1,COMWRD	;FIND ONE?
	JRST	G4			;NOPE..
	TLNN	FL,EXTEN	;THIS SUPPOSED TO BE EXTENSION
	JRST	G1X		;NOPE..
	HLLZM	T1,EXTN		;SAVE EXTENSION
	JRST	G1Y
G1X:	MOVEM	T1,FILEN	;SAVE FILENAME
	CAIN	CH,"."		;EXTENSION EXPECTED?
	JRST	G2		;LOOKS THAT WAY
G1Y:	CAIN	CH,","		;MORE PARAMETERS IN THIS COMMAND STRING?
	JRST	G3		;YES
	CAIN	CH,ALTMOD
	JRST	G4
	JRST	ILLPAR		;JUST DONT LIKE WHAT YOU TYPED...TOO BAD
G2:	TLO	FL,EXTEN	;SET LOOKING FOR EXTENSION FLAG
	JRST	G1
G3:	TLO	FL,MORE		;SET MORE TO COME FLAG
G4:	POPJ	P,		;GO AWAY
;
;
;
CCLC1:	ASCIZ	'COMPILE /COMPILE /'
CCLC2:	ASCIZ	/SRC.TMP/
CCLC3:	ASCIZ 	'EXEC /'
	PAGE
;	SUBROUTINE TO OUTPUT TO A SPECIFIED CHANNAL-SIXBIT CHARACTERS
;	CONTAINED IN SIXTXT LEFT JUSTIFIED.
;	CHARACTERS ARE CONVERTED TO ASCII BEFORE TRANSMISSION
;
SIXTOT:	MOVE	T4,SIXTXT
	JUMPE	T4,SXTOT1	;ANYTHING THERE
	SETZM	T3
	ROTC	T3,6
	ADDI	T3,40
	MOVE	CH,T3
	PUSHJ	P,PUTCHR
	JUMPN	T4,SIXTOT+2	;SEE IF THERE IS MORE
SXTOT1:	POPJ	P,
;
;
;	SUBROUTINE TO OUTPUT FILNM. AND FILNM.+1 TO THE CURRENTLY
;	SPECIFIED OUTPUT CHANNAL
;	OUTPUT LOOKS LIKE	FILENAME.EXT
;	CALL	PUSHJ	P,OFE
;
OFE:	MOVE	SIXTXT,FILNM.		;IS THERE A FILE 
	JUMPE	SIXTXT,NFOX		;NO TYPE ERROR
	PUSHJ	P,SIXTOT
	MOVE	SIXTXT,FILNM.+1		;GET EXTENSION
	JUMPE	SIXTXT,OFE1		;IS THERE ONE TO OUTPUT
	MOVEI	CH,"."
	PUSHJ	P,PUTCHR
	PUSHJ	P,SIXTOT
OFE1:	POPJ	P,
;
NFOX:	TTCALL	3,[ASCIZ /NO FILE AVAILABLE!!/]
	TTCALL	3,CRLF
	JRST	PHAS7
;
	PAGE
;
;	SUBROUTINE TO PEEK AT THE FIRST NON-BLANK CHARACTER
;	IN A COMMAND INPUT STRING.	PROCEEDING FROM THE CURRENT POINTER
;	POSITION
;	CP CONTAINS C(POINTER)
;	CHARACTER IS RETURNED IN 'CH'
;	POINTER IS NOT MOVED
;	CALL	PUSHJ	P,PEEK
;
PEEK:	CAIE	CH," "		;IS C(CHAR) A BLANK?
	POPJ	P,		;NO..SOMETHING ALREADY IN 'CH'
	PUSH	P,CP		;SAVE POINTER
	ILDB	CH,CP		;GET A CHARACTER
	CAIN	CH," "		;IS IT A BLANK..?
	JRST	.-2		;YUP.
	POP	P,CP		;RESTORE POINTER
	POPJ	P,		;RETURN
;
;
	PAGE
;
;
; CODE TO TURN ^Q (PAPER TAPE MODE) ON OR OFF.
;
;
	MLON
KEYCOM:	SETOM	T2		;INDICATE KEY COMMAND
	SKIPA
TAPCOM:	SETZM	T2
	HRREI	T1,-1		;SET TO GET CHARACTERISTICS FOR LINE
	TTCALL	6,T1
	SKIPE	T2
	TLZA	T1,2		;CLEAR PTP INPUT MODE
	TLO	T1,2		;SET PTP ^Q MODE
	TTCALL	7,T1		;PUT IT IN TTYTEL
	SETZM	0
	SKIPN	T2
	TRO	0,1		;SET TAPE MODE..
	MOVEM	0,CUSP
	TLO	FL,NEWT
	SKIPE	T2		;TAPE MODE?
	JRST	PHAS5		;NO..
	MOVEI	T1,21		;TRY TO START PAPER TAPE READER..
	TTCALL	1,T1
	JRST	PHAS5
;
;
;
	PAGE
JOBCOM:	SETZM	10	; INDICATE JOB COMMAND
	HRREI	6,-1		;FULL PRJPRG CHK
	PUSHJ	P,JCNT			;COUNT = JOBS
	TTCALL	3,[ASCIZ .] JOB(S) ON THE SYSTEM

.]
	JRST	PHAS7
;
USERCM:	SETOM	10	; INDICATE USER COMMAND
	MOVSI	6,-1		;ACCOUNT CHK ONLY
	PUSHJ	P,JCNT
	TTCALL	3,[ASCIZ .***]	ACCOUNT(S) ON THE SYSTEM

.]
	JRST	PHAS7
;
;
JCNT:	CALLI	4,24		;GET PPN
	AND	4,6			;MASK IT
	MOVEI	2,0			;INITIALIZE COUNTER
	MOVEI	5,2		;FIND OUT HOW MANY JOB #'S TO SEQARCH
	MOVE	7,[XWD 20,12]
	CALLI	7,41		;GET C(HIGHEST JOB RUNNING)
	JFCL
	HRL	5,7
JOBS.1:	MOVE	3,5	;SCAN PRJPRG TABLE
	CALLI	3,41
	JFCL
	AND 	3,6		;MASK IT
	CAMN	3,4		;FIND AN EQUAL
	ADDI	2,1		;YES..COUNT IT
	SUB	5,[XWD 1,0]	;LOOK AGAIN
	TLNE	5,777777	;FINISHED?
	JRST	JOBS.1
	MOVSI	1,-2			;OUTPUT ONLY TW0 DIGITS
JOBS.2:	IDIVI	2,^D10
	ADDI	3,60
	PUSH	P,3
	AOBJN	1,JOBS.2
	POP	P,3
	TTCALL	1,3
	POP	P,3
	TTCALL	1,3
	CALL	4,[SIXBIT .UNSQZE.]
	SKIPE	10		; JOB COMMAND?
	TRZ	4,777777
	TTCALL	3,[ASCIZ . [.]
	PUSHJ	P,SIXOUT
	POPJ	P,
	MLOFF
	MLON
	PAGE
; CODE TO MERGE THE SPECIFIED FILE WITH ###SRC.TMP
;
;
MRGCOM:	SKIPN	FILNM.		;ANY FILE AVAILABLE?
	JRST	NFOX		;NO
	PUSHJ	P,GTNAME	;GET FILE NAME IN COMMAND
	PUSHJ	P,OPNSRC	; OPEN ###SRC.TMP I/O
	TLNE	FL,MORE		;MORE ARGUMENTS?
	JRST	ILLPAR		; YES..THATS BAD..IN FACT NOT ALLOWED.
	MOVE	T1,FILEN	; SETUP FILE NAME EXT FOR LOOKUP..
	MOVEM	T1,TMPBLK
	MOVE	T1,EXTN
	MOVEM	T1,TMPBLK+1
	SETZM	TMPBLK+2
	SETZM	TMPBLK+3
	TLO	FL,MRGMOD	;SET =1 TO INDICATE MERGE MODE
	TLZ	FL,STACKM	;JUST IN CASE..CLEAR IT
	PUSHJ	P,MERGER
	JRST	PHAS7		;RETURN TO COMMAND LEVEL
	PAGE
MONSYS: SETZ 16,
        JRST SOLNG+1
SOLNG:  SETO 16,
MOVE T1,[XWD 2,CORLST]	;DELETE INCORE SYS FILE
 	CALLI	T1,44
 	JRST	.+1
	MOVSI	T1,(SIXBIT .DSK.)	;THIS IS JUST A PRECAUTIONARY
	MOVEM	T1,DEVNM.		; MEASURE IN CASE SYS TMP AREA
					; IS ALREADY WIPED OUT ..
 	PUSHJ	P,CHNOPN		;DELETE SUDS CREATED FILES
	HRLI	T1,(SIXBIT .TMP.)	;
 	PUSHJ	P,LOKFOR		;	KILL SRC.TMP
	PUSHJ	P,RNFIL
	HRLI	T1,(SIXBIT .REL.)	;	KILL SRC.REL
	PUSHJ	P,LOKFOR
	PUSHJ	P,RNFIL
	HRLI	T1,(SIXBIT .BAK.)	;	KILL SRC.BAK
	PUSHJ	P,LOKFOR
	PUSHJ	P,RNFIL
	HRLI T1,(SIXBIT/RUN/)
	PUSHJ P,LOKFOR
	PUSHJ P,RNFIL
	HRROI T1,6
	CALLI T1,41
	JFCL
	TRZ T1,600	;OFF WITH THE HEAD
	CALLI T1,-11
        SKIPE 16
        JRST BYEBYE
SOL1:	TTCALL	3,[ASCIZ /ENTERING MONITOR

/]
	CALLI	1,12		;EXIT SUDS FOR GOOD
BYEBYE: HRRZI T1, LOGOUT
        CALLI T1,35
        JRST SOL1
LOGOUT: SIXBIT/SYS/
        SIXBIT/LOGOUT/
        0
        0
        0
        0
;
;
LOKFOR:	MOVEM	T1,TEMPN+1
	LOOKUP	TSTCHN,TEMPN	;LOOKUP SPECIFIED FILE
	AOS	(P)		;FORGET DELETED IF ITS NOT FOUND
	POPJ	P,		;RETURN
;
RNFIL:	FLUSH	(TEMPN+1,3)
	FLUSH	(0,4)
	RENAME	TSTCHN,0	;DELETE SPECIFIED FILE
	POPJ	P,		;RETURN NO MATTER WHAT
	POPJ	P,

;



;
;
	PAGE
;	CHARACTER HANDLING SUBROUTINES FOR ANY CHANNEL
;	RETURNS A CHARACTER IN 'CH'
;
;
SELECT:	SOSG	@IOTAB2(IOPNT)		;GET ALL CHARS YET
	JRST	LODBUF			;EITHER BUFFER EMTY OR FIRST CALL
GETCH:	XCT	IOTAB1(IOPNT)		;ILDB INSTR. FOR THAT CHANN
	JUMPE	CH,SELECT		;IGNORE NULLS
	POPJ	P,		;CHAR RETURNED IN CH
LODBUF:	XCT	IOTAB0(IOPNT)		;IN UUO
	JRST	GETCH
STAT:	XCT	IOTAB3(IOPNT)		;CHECK ERROR STATUS
	JRST	ERRORX		;ERROR CONDITION DETECTED
	TLNN	FL,STACKM+MRGMOD	;IN STACK LINE MODE? OR MERGE MODE
	POPJ	P,		;NO
	TRO	FL,EOFMRK	;YES INDICATE EOF MARK
	POPJ	P,		;THEN RETURN
;
;
ERRORX:	HALT			;INSERT ERROR ROUNTINES HERE
;
;	SPECIAL	CHAR SUBROUTINE TABLES
;
	DEFINE TAB0(A)
	<IRP A
	<IN	A,>>
;
IOTAB0:	TAB0 <0,DSKCC,TTYCC,0,SRCCHN,MRGCHN>
;
	DEFINE TAB1 (A)
	<IRP A
	<ILDB CH,A+1>>
	DEFINE TAB4(A)
	<IRP A
	<OUT	A,>>
;
IOTAB4:	TAB4 <0,DSKCC,TTYCC,DSKOCH>
;
	DEFINE TAB5(A)
	<IRP A
	<IDPB CH,A+1>>
;
IOTAB5:	TAB5 <0,0,0,OTBUF>
;
;
IOTAB1:	TAB1 <0,IDSK,ITTY,0,ISRCB,IMRGB>
;
	DEFINE TAB2 (A)
	<IRP A
	<XWD 0,A+2>>
;
IOTAB2:	TAB2 <0,IDSK,ITTY,OTBUF,ISRCB,IMRGB>
;
	DEFINE TAB3 (A)
	<IRP A
	<STATZ A,740000>>
;
IOTAB3:	TAB3 <0,DSKCC,TTYCC,DSKOCH,SRCCHN,MRGCHN>
;	SUBROUTINE TO OUTPUT A WORD WITH SIXBIT CHARACTERS
;	ON ENTRY T4 CONTAINS THE CHARACTERS TO BE OUTPUT
;	CALL	MOVE T4,WORD TO OUTPUT
;		PUSHJ P,SIXOUT
;
SIXOUT:	JUMPE	T4,NOCHR	;ANYTHING THERE
	SETZM	T3
	ROTC	T3,6
	ADDI	T3,40		;MAKE IT ASCII
	TTCALL	1,T3
	JUMPN	T4,SIXOUT+1	;FINISH YET. IF NOT OUTPUT ANOTHER
NOCHR:	POPJ	P,		;OTHERWISE RETURN
;
;
	PAGE
	PDP:	IOWD	17,PDX+1		;PUSH DOWN POINTER WORD
0
;
;	DATA AREA TO BE LOADED INTO LOWSEG (PURE)
;	HERE IS THE DATA WHICH MUST MATCH THE STORAGE ASSIGNMENT ABOVE
;
INIDAT:	
;	ORDERS:
	BLOCK 	40
;	CUSP:
	0
;	SYSNM.:
	BLOCK	1
	SIXBIT	/TMP/
	0
	0
	0
;	FILNM.:
	BLOCK	4
;	DEVNM.:
	BLOCK	1
;	BBBTMP:
	SIXBIT/###BAS/
	SIXBIT/TMP/
	0
	0
;	SYSTMP:
	SIXBIT	/###SYS/
	SIXBIT	/TMP/
	0
	0
;	TMPBLK:
	SIXBIT	/###SUD/
	SIXBIT	/TMP/
	0
	0
	0
	;BASLST
	IOWD 5,TMPBLK
	0
;BLOCA
SIXBIT/SVC/
IOWD ^D15,BUFFA
0
;BUFFA
BLOCK ^D15
;	THE REST OF	INITIAL DATA
	OCT	777777
	BLOCK	46
;	DEVC:
	0
	SIXBIT	/DSK/
	0
	IOWD	^D26,SYSNM.
	0
	;OUTBLK:
	BLOCK	4
;	CCLDCH:
	SIXBIT /SYS/
	0
	0
	0
	0
	0
;	OUTBUF:
	BLOCK	3
;	TEMPN:
	SIXBIT /###SRC/
	SIXBIT /TMP/
	0
	0
;	JOBN:
	0
	0
;	IMEDRN:
	CALLI	T1,35
	HRRZI	T1,RECOVR
	CALLI	T1,40
	HALT
	JRST	NEXF
;	RECOVR:
	SIXBIT	/SYS/
	SIXBIT	/SUDS/
	0
	0
	0
	0
	LOWBLK=.-INIDAT
;
; THATS ALL OF IT............................

	Z
	Z		;LOAD IT LATER*******

	PAGE
; LINE STACKER SUBROUTINE
; ON ENTRY:	'XILB' CONTAINS THE LINE TO BE SAVED
;		'T3' CONTAINS THE LENGTH INCLUDING SEQ NUMBER
; CALL		PUSHJ	P,STACKER
; STACKER REGISTER ASSIGNMENTS
;
	APRIME=T4	;SEQ # AT C(POSITION)
	FLAGX=FLAG
	GP=CH		;GEN PURPOSE
	POSIT=IOPNT	;RELATIVE TABLE POSITION POINTER
	AS=CP		;SEQ NUMBER OF NEW LINE BEING STACKED
	LL=15		;CONTAINS LEFT LINK DURING SCAN
	RL=OFLAG		;	"	RIGHT "	"	"
	NXTPOS=MSGPNT	;RELATIVE	C(NEW AREA PONTER)
	PRVPOS=SIXTXT	;	C(SCAN POSITION)
;
; FLAG BITS
;
	LFTCHK==100
	RHTCHK==200
;
SAVACS:	MOVEM	0,STRSAV	;SAVE ALL REGISTERS
	MOVE	0,[XWD 1,STRSAV+1]
	BLT	0,STRSAV+17
	MOVE	0,[XWD 1,2]
	SETZM	1
	BLT	0,17			;AND CLEAR THEM
	MOVE	P,STRSAV+P	;RESTORE P REGISTER
	POPJ	P,		;RETURN
;
STACKER:	PUSHJ	P,SAVACS	;SAVE AC'S AND CLEAR
	MOVE	T1,LNCCNT		;GET LINES IN BUFFER COUNT
	CAIL	T1,^D40			;40 LINES INTHERE YET?
	JRST	[ MOVBLK (XILB,XILB1,24)
		MOVE	FL,STRSAV+FL		;GET FLAG
		PUSHJ	P,MERGER		;YES MERGE WITH SRC.TMP
		SETZM	FL			;CLEAR FLAG
		CALL	SRCCHN,[SIXBIT /WAIT/]
		PUSHJ	P,OPNSRC		;REOPEN SRCCHN FOR OUTPUT
		MOVBLK (XILB1,XILB,24)
		JRST STACK.1]
;
					;STACK LINE..
STACK.1:	MOVE	T3,STRSAV+3		;RESTORE T3
	MOVE	AS,XILB			;GET SEQ NUMBER
	SKIPN	NXTPOS,NXTLOC
	MOVEI	NXTPOS,1
	MOVEI	POSIT,1
TABSCA:	MOVE	APRIME,TABLE+2(POSIT)	;GET C(TABL ENTRY SEQ #)
	CAMN	AS,APRIME		;SEQ#'S EQUAL ?
	JRST	EQUAL			;YES
	CAMG	AS,APRIME		;NEW SEQ# GREATER?
	JRST	LESS			;NO
GREATE:	TROA	FLAGX,LFTCHK	;SET LEFT LINK SCAN FLG ON
LESS:	TRO	FLAGX,RHTCHK		; "	RIGHT "	"	"	"
	MOVE	PRVPOS,POSIT	;SAVE C(POSITION) IN
				; PRVPOS IN CASE A NEW ENTRY IS 
					; TO BE MADE
	HRRZ	RL,TABLE(POSIT)	;C(RIGHT LINK) TO RL
	HLRZ	LL,TABLE(POSIT)	;C(LL) TO LL
	HRRZ	GP,FLAGX		;FLAFX RH BITS TO GP
	CAIN	GP,LFTCHK+RHTCHK	;WHEN BOTH	FLAGS GO ON A NEW
					; LINE IS TO BE ENTERED
	JRST	INSERT			; XFER TO INSERT PROCESSING
	TLO	FLAGX,@FLAGX		;LEFT HALF=RIGHT HALF
					; SO IT CAN BE CHECKED LATER 
	TRNE	FLAGX,LFTCHK		;LEFT SCAN?
	SKIPA	POSIT,LL		; YES	LL TO POSITION OTHERWISE
	MOVE	POSIT,RL		;	RL TO POSITION
	CAMN	POSIT,PRVPOS	;IF LINK POINT TO SAME AREA MAKE A NEW
	JRST	NEWENT		;ENTRY.
	JUMPE	POSIT,NEWENT	;IF LINK=0 NEW ENTRY MUST BE MADE
	JRST	TABSCA			;OTHERWISE CHECK NEXT ENTRY
; CODE TO INSERT A LINE IN THE STACKER--BETWEEN EXISTING ENTRIES.
;
INSERT:	TLNE	FLAGX,RHTCHK	;RIGHT SCAN?
	JRST	INRIGHT			; YES..
	HLLZ	GP,TABLE(RL)		;GET PREVIOUS LL MAKE IT
	HLLM	GP,TABLE(NXTPOS)	;NEW LL OF THIS ENTRY.
	HRLM	NXTPOS,TABLE(RL)	;RESET PREVIOUS LL POINTER
	HRRM	RL,TABLE(NXTPOS)	;MAKE CURRENT RL THE NEW RL
	HRRM	NXTPOS,TABLE(POSIT)	;REPLACE RL OF C(POSITION)
	JRST	NX		;STACKE THE LINE
INRIGH:	HRRZ	GP,TABLE(LL)	;GET PREVIOUS RL MAKE IT RL 
					; OF NEW LINE
	HRRM	GP,TABLE(NXTPOS)	; SAVE IT
	HRRM	NXTPOS,TABLE(LL)	;RESET PREVIOUS RL POINTER
	HRLM	LL,TABLE(NXTPOS)	;MAKE C(LL) NEW LINE'S LL
	HRLM	NXTPOS,TABLE(POSIT)	;REPLACE LL OF C(POSITION)
	JRST	NX		;STACKE THE LINE
;
PAKLIN:	MOVE	T1,NXTPOS	;
	ADDI	T1,TABLE+2		; STACK THE LINE..
	MOVSI	T2,XILB
	HRR	T2,T1
	IOR	T1,[JFCL 0,(T3)]
	BLT	T2,@T1
	HRROM	T3,TABLE+1(NXTPOS)	;SAVE LINE LENGTH
STKEND:	MOVE	0,[XWD STRSAV+1,1]	;RESTORE REGISTERS
	BLT	0,17
	MOVE	0,STRSAV
	AOS	LNCCNT			;INC LINE IN BUFFER COUNT.
	FLUSH	(XILB,20)
	POP	P,		;GET PHONY RETURN ADDRESS OFF P
				; THIS WAS NEEDED TO POSITION 'P'
	POPJ	P,			;RETURN TO STACKER CALLER
	; CODE TO MAKE AN ENTIRLY NEW LINE ENTRY IN THE STACKER
;
NEWENT:	TRNE	FLAGX,RHTCHK	;IN PROCESS OF RIGHT SCAN
	JRST	RIGHT			;YES..
	HRLM	NXTPOS,TABLE(PRVPOS)	;MAKE PREVIOUS LL POINT TO NEW 
					; ENTRY
	HRRM	PRVPOS,TABLE(NXTPOS)
	SKIPN	LNCCNT		;IF THIS IS THE FIRST ENTRY SET SMLLIN
	HRRM	NXTPOS,SMLLIN	; FROM NXTPOS (BUT ONLY FOR FIRST ENTRY)
	JRST	NX
RIGHT:	HRRM	NXTPOS,TABLE(PRVPOS)	;DO APPROPRIATE THING TO
					; NEW ENTRY'S RL
	HRLM	PRVPOS,TABLE(NXTPOS)	;MAKE THE LL
	HRRM	NXTPOS,SMLLIN	;SAVE POSITION OF SMALLEST LINE ENTERED
				; IN THE STACKER
NX:	HRRE	T1,NXTLOC	;GET CURRENT FREE POSITION
					;JUST USED UP
	ADDI	T1,^D23			;GENERATE NEW FREE AREA REL LOC
	HRRM	T1,NXTLOC		;AND SAVE IT
	JRST	PAKLIN			;STACK LINE
;
; CODE TO ZAPP AN EXISTING LINE ENTRY AND REPLACE IT
; WITH A NEW ONE
;
EQUAL:	MOVE	T1,POSIT
	HRROM	T3,TABLE+1(POSIT)	;SAVE LINE LENGTH
	ADDI	T1,TABLE+2
	MOVSI	T2,XILB
	HRR	T2,T1
	IOR	T1,[JFCL 0,(T3)]
	BLT	T2,@T1
	SOS	LNCCNT		;SUBTRACT SO INC BY 1 IS NOT COUNTED
	JRST	STKEND
;
	MLOFF
	PAGE
;***************************************************************
;
;
; CODE FOR MERGING THE LINE STACKER WITH ###SRC.TMP
; OR TO MERGE ANY PROPERLY FORMATED FILE SPECIFIED IN A MERGE COMMAND
; WHEN FLAG 'STACKM' =1 MERGE IS WITH ###SRC.TMP & STACK	OTHERWISE
; THE SPECIFEID FILE IN A MERGE COMMAND WILL BE MERGED WITH SRC.TMP
; ON ENTRY:	AC'S 0-17 ARE SAVED ...AND RESTORED	ON EXIT
; NOTE SEE "LIG.1" CODE FOR STACK FORMAT
; CALL 	PUSHJ	P,MERGER
;
	MLON
;
; REGISTER DEFINITIONS
;
	GP==10		;GENERAL PURPOSE
	BLTPNT==T3	; POINTER USED TO XFER LINE TO OUTPUT BUFFER
	XFR==12		;INSTRUCTION TO XFER LINE TO OUTPUT BUFFER
	MF1==14		;SEQUENCE # OF MERGE FILE 1 (###SRC.TMP)
	MF2==15		;	"	"	"	"	"	2 (STACK OR SPEC/FILE)
	MF1LEN==16	;LENGTH OF LINE MF1
	MF2LEN==17	;	"	"	"	MF2
;
;
; FLAG BITS FL(RH)
;
	MRGLW==200	;SET =1 IF A MERGE FILE LINE IS ALREADY AVAILABLE
	SRCLW==20	;SET =1 IF AN SRC.TMP LINE IS ALREADY AVAILABLE
	EOFMRK==1	;GENERAL EOF MARKER
	STKEOF==2	; STACKER IS EMPTY
	EOFSRC==4	;###SRC.TMP IS EMPTY
	EOFMRG==10	; MERGE FILE IS EMPTY
	FRSTSL==40	; SET TO 1 TO INDICATE FIRST WORD SELECTION
	FRSTML==100	; INDICATES FIRST WORD SELECTION OF MERGE FILE
;
MERGER:	SETZM	NXTSN		;CLEAR IT
	SETZM	NXTSN1
	TLNN	FL,STACKM	;STACK MODE?
	JRST	MER.1		;NO..DONT BOTHER SAVING COMBUF..
	MOVEM	0,COMBUF		;SAVE AC'S IN 'COMBUF'
					; FOR LACK OF A BETTER PLACE
	MOVE	0,[XWD 1,COMBUF+1]
	BLT	0,COMBUF+17
	MOVE	0,[XWD 1,2]
	SETZM	1
	BLT	0,17
	MOVE	P,COMBUF+P		;RESTORE P
	HLLZ	FL,STRSAV+FL		;RESTORE LH OF FL FROM PRIME
					;REGISTER LEVEL (THAT IS..BEFORE
					;STACKER WAS EVEN CALLED)
MER.1:	TRO	FL,FRSTSL+FRSTML	;INIT FIRST SELECT FLAG
	TLNN	FL,STACKM		;STACK MERGE?
	PUSHJ	P,OPNMF			; NO... OPEN MERGE FILE..
MRG.1:	TRNE	FL,EOFSRC	;EOF REACHED?
	JRST	MRG.1B			;YES
	MOVEI	IOPNT,4		;SET CHANNAL POINTER FOR SRC.TMP
MRG.1A:	MOVEI	MF1LEN,0	;SET TO ZERO
	MOVE	T1,[POINT 36,XILB]
	SKIPE	MF1,NXTSN		;PROVIDE FOR FIRST CALL..
MRG.1D:	IDPB	MF1,T1
MRG.1C:	PUSHJ	P,SELECT	;GET A WORD FROM ###SRC.TMP
	TRZE	FL,EOFMRK		;EOF REACHED?
	JRST	[TRO FL,EOFSRC
		 TRZN FL,FRSTSL
		 JRST MRG.1E
		 SKIPE MF1LEN
		 JRST MRG.1E		;YES
		 JRST MRG.1B]
	SKIPN	NXTSN
	JRST	 [MOVE MF1,CH
		 MOVEM MF1,NXTSN
		 JRST MRG.1D]			;THIS WAS THE FIRST TIME ..
	TRNE	CH,1			;SEQ NUMBER?
	JRST	MRG.1E			;YES..
	IDPB	CH,T1			;SAVE WORD
	ADDI	MF1LEN,1		;COUNT IT
	JRST	MRG.1C			;KEEP FILLING 'XILB'
MRG.1E:	TRZ	FL,FRSTSL	;CLEAR FIRST SELECTION BIT
	MOVEM	CH,NXTSN	;SAVE VALUE OF NXT SN
	MOVE	0,XILB
	MOVEM	0,MF1		;SETUP C(SEQ #)
	ADDI	MF1LEN,1	; COUNT SEQ # WORD
	TRO	FL,SRCLW	; FLAG TO INDICATE	LINE IS WAITING
;
MRG.1B:	TLNN	FL,STACKM		;STACK MODE?
	PUSHJ	P,GSL.M		;GET MERGE LINE FROM SPECIFIED FILE
	PUSHJ	P,GSL			;GET THE NXT STACKED LINE
MRG.2:	TRNN	FL,EOFSRC+EOFMRG	;ANY EOF'S?
	JRST	MRG.5		;NOT YET..
	TRNN	FL,EOFSRC
MRG.2X:	JRST	[TRNE FL,MRGLW
		 JRST MRG.5
		 JRST MRGMF1]	;MF2 MAY BE EMPTY
	TRNN	FL,EOFMRG
MRG.2Y:	JRST	[TRNE FL,SRCLW
		 JRST MRG.5
		 JRST MRGMF2]	; MAKE SURE LAST LINE OF SRC.TMP IS GONE
MRG.4:	TRNE	FL,SRCLW	;IS THER STILL A LINE AVAILABLE?
	JRST	MRG.2X			;YES..IT MUST BE DUMPED.
	TRNE	FL,MRGLW	; STILL A LINE WAITING
	JRST	MRG.2Y		;IT MUST BE DUMPED
	RELEASE	SRCCHN,		;BOTH FILES EMPTY TIME TO CLOSE OUT
	RELEASE SROCHN,
	TLZN	FL,STACKM	;STACK MODE?
	JRST	MRGEX1		;NO
	FLUSH	(TABLE,1024)	;CLEAR OUT TABLE AREA
	SETZM	LNCCNT		;AND LINE COUNTER
	SETZM	NXTLOC		;AND CLEAR NXT LOCATION POINTER
MRGEX:	MOVE	0,[XWD COMBUF+1,1]	;RESTORE AC'S
	BLT	0,17
	MOVE	0,COMBUF
	TLZE	FL,STACKM
MRGEX1:	RELEASE	MRGCHN,
	MOVE	0,SVJOBF	;RECLAIM SPACE USED FOR BUFFERS
 	MOVEM	0,JOBFF
	POPJ	P,		;RETURN
;
;
;
; CODE TO MERGE STACK OR SPECIFIED FILE WITH ###SRC.TMP
;	REGISTER MF1 CONTAINS THE SEQ NUMBER OF LINE OPENED IN ###SRC.TMP
;	"	MF2	"	"	"	"	"	"	"	" STACK OR FILE
; EOF MARKS ARE DETECTED BY FLAGS	EOFSRC AND EOFMRG IN 'FL'(RH)
;
MRG.5:	CAMLE	MF2,MF1		;MF2< MF1 OR =TO MF1
	JRST	MRGMF1			;NO
					;GET SRC.TMP LINE
	CAMN	MF2,MF1		; IF = THEN MF1 IS DEAD
	TRZ	FL,SRCLW	; INDICATE NO LINE WAITING FROM SRC.TMP
;
MRGMF2:	MOVE	XFR,[BLT T3,0(MF2LEN)]	;INIT XFER INSTRUCTION
	MOVEM	MF2LEN,LSIZE		;LOAD LINE LENGTH
	HRRZ	GP,SMLLIN		;GET RELATIVE LOC OF STACK LINE
					;(SMALLEST) NOT YET OUTPUT
	HLLM	GP,TABLE+1(GP)		;SET THIS LINE TO USED MODE
	ADDI	GP,TABLE+2		;SETUP XFER REGISTER
	HRLZM	GP,BLTPNT
	TRZ	FL,MRGLW	; INDICATE NO LINE WAITING FROM MF2
	SETZM	MF2		; INDICATE NO LINE WAITING FROM MF2
	CAIN	MF2LEN,1	;IF LENGTH=1 THIS LINE WAS MEANT TO BE DELETED
	JRST	BUF.1		;...SO BE IT...
MRG.5A:	MOVE	T4,OSRCB+1	;GET C(LOC) TO FILL IN OUTPUT BUFFER
	ADDI	T4,1
	HRRM	T4,BLTPNT		;SAVE LOCATION IN XFER REGISTER
	HRRM	T4,XFR			;AND IN XFER INST.
;
BUFO:	PUSHJ	P,BUFROM	;ROOM TO DUMP THIS LINE IN THE
					; CURRENT BUFFER
					; IF NOT DUMP CURRENT AND RETURN
					; TO 'BUFO'
BUFFIL:	MOVE	T3,BLTPNT	; BLTPNT HAS XFER INFO IN IT
	SOS	XFR		;NECESSARY SO AS NOT TO XFER TO FAR
	XCT	XFR			;XFER THE LINE INTO OUTPUT BUFFER
BUF.1:	TRNE	FL,SRCLW		;LINE ALREADY THERE?
	JRST	MRG.1B			;YES
	JRST	MRG.1			; DO NEXT LINE
;
;
SPNTR:	POINT 17,@OSRCB,17	;BUFFER AREA POINTER
;
BUFROM:	LDB	T1,SPNTR	;GET REMAINING ROOM IN C(BUFFER)
	CAMG	T1,LSIZE			;ENOUGH ROOM FOR THIS LINE
	JRST	[MOVEI T4,201
		 HRLM T4,@OSRCB
		 PUSHJ	P,BUFOUT
		 POP P,
		 JRST BUFR1]
	SUB	T1,LSIZE
	DPB	T1,SPNTR
	MOVE	T1,LSIZE
	ADDM	T1,OSRCB+1
	POPJ	P,
BUFR1:	JRST	MRG.5A
;
BUFOUT:	OUT	SROCHN,
	POPJ	P,
	STATZ	SROCHN,740000
	HALT	.
	POPJ	P,
;
;
;
; CODE TO GET THE SMALLEST STACKER LINE NOT YET DUMPED TO MERGE FILE
; 'SMLLIN' CONTAINS THE RELATIVE ADDRESS(TO TABLE) OF THE NXT LINE
; TO LOOK AT.
;
GSL:	TRNE	FL,EOFMRG	;EOF REACHED?
	POPJ	P,		;YES..
	SKIPE	MF2		;IF NOT ZERO A LINE IS ALREADY WAITING
	POPJ	P,
	MOVE	GP,SMLLIN
	PUSHJ	P,GSL.1			;SEE IF THIS LINE WAS ALREADY USED
	MOVE	MF2,TABLE+2(GP)		; C(SEQ #) AVAIL TO MF2
	HRR	MF2LEN,TABLE+1(GP)	;GET LENGTH
	POPJ	P,			;RETURN
;
GSL.1:	HLRE	0,TABLE+1(GP)	;GET MODE INDICATOR
	SKIPE	0
	POPJ	P,			;RETURN IF LINE NOT YET USED
	MOVE	GP,TABLE(GP)		;GET LINKS
	HLRZ	0,GP		;IS NXT LINK TO SAME LOCATION?
	CAMN	0,SMLLIN	;YES.. THEN EOF REACHED.
	JRST	GSL.2			;YES
	HLRZM	GP,SMLLIN		;UPDATE SMLLIN POINTER
	TRZ	GP,777777		;MASK OFF RIGHT LINK
	MOVSS	0,GP		;SWAP HALVES TO GET POINTER IN E FIELD
	SKIPN	GP		; LINK=0
GSL.2:	TROA	FL,EOFMRG	; YES.. SET EOF FLAG
	POPJ	P,
	POP	P,
	POPJ	P,
;
;
; CODE TO OPEN THE ###SRC.TMP FILE FOR MERGING
;
OPNSRC:	INIT	SRCCHN,13
	SIXBIT	/DSK/
	XWD	,ISRCB
	HALT	.
	INIT	SROCHN,10		;INIT OUTPUT CHANNAL
	SIXBIT	/DSK/
	XWD	OSRCB,0
	HALT	.
	SETZM	TEMPN+2
	SETZM	TEMPN+3
	LOOKUP	SRCCHN,TEMPN
	JRST	NFOX
	SETZM	TEMPN+2
	SETZM	TEMPN+3
	ENTER	SROCHN,TEMPN
	HALT	.
	MOVE	0,JOBFF			;SAVE	BUFFER LOC
	MOVEM	0,SVJOBF		;SO IT CAN BE RECLAIMED LATER
	INBUF	SRCCHN,3
	OUTBUF	SROCHN,3
	OUTPUT	SROCHN,		;FIRST OUTPUT TO INIT BUFFER.
	POPJ	P,
;
;
; CODE TO GET A LINE FROM A FILE INTO THE MERGED FILE
;
MRGMF1:	MOVE	XFR,[BLT T3,(MF1LEN)]
	MOVE	BLTPNT,[XWD XILB,0]
	MOVEM	MF1LEN,LSIZE
	TRZ	FL,SRCLW		;CLEAR LINE WAITING FLAG
	JRST	MRG.5A
;
;
;
;
;
; CODE TO OPEN UP THE SPECIFIED FILE SO IT CAN BE MERGED WITH SRC.TMP
;
OPNMF:	INIT	MRGCHN,13
	SIXBIT	/DSK/
	XWD	,IMRGB
	HALT	.
	LOOKUP	MRGCHN,TMPBLK
	JRST	MFNF
	INBUF	MRGCHN,3
	POPJ	P,		;RETURN
;
MFNF:	TTCALL	3,[ASCIZ /? CANNOT FIND WEAVE FILE

/]
	JRST	PHAS7
;
	PAGE
; CODE TO GET A LINE FROM THE SPECIFIED MERGER FILE
;
; RETURNS LINE SEQ NUMBER IN MF2 AND LENGTH IN MF2LEN
; ON EOF	FL(RH) IS SET TO EOFMRG
GSL.M:	MOVEI	0,1		;LINE IS SETUP IN TABLE POSITION 1
	MOVEM	0,SMLLIN	;SO POINT TO IT 
	TRNN	FL,MRGLW	; IS THERE A LINE ALREADY AVALABLE?
	TRNE	FL,EOFMRG	;EOF REACHED?
	JRST	GS.M1B		;YES..
	MOVEI	IOPNT,5		; SELECT FROM MERGE CHANNAL POINTER
	MOVEI	MF2LEN,0	;CLEAR LENGTH
	MOVE	T1,[POINT 36,TABLE+3]
	SKIPE	MF2,NXTSN1	;PROVIDE FOR FIRST CALL
GS.M1D:	IDPB	MF2,T1
GS.M1C:	PUSHJ	P,SELECT	;GET A WORD
	TRZE	FL,EOFMRK	;EOF YET?
	JRST	[TRO FL,EOFMRG
		 TRZN FL,FRSTML
		 JRST GS.M1E
		 SKIPE MF2LEN
		 JRST GS.M1E
		 JRST GS.M1B]
	SKIPN	NXTSN1
	JRST	[MOVE MF2,CH
		 PUSHJ P,MFQCHK
		 MOVEM MF2,NXTSN1
		 JRST GS.M1D]
	TRNE	CH,1		;SEQ WRD?
	JRST	GS.M1E		;YES..
	IDPB	CH,T1		;SAVE WORD..
	ADDI	MF2LEN,1	; COUNT IT
	JRST	GS.M1C
;
GS.M1E:	TRZ	FL,FRSTML	;CLEAR FIRST SELECTION FLAG
	MOVEM	CH,NXTSN1	;SAVE VALUE OF NXT SEQ NUMBER
	MOVE	0,TABLE+3
	MOVEM	0,MF2
	ADDI	MF2LEN,1	;COUNT SEQ NUMBER
	TRO	FL,MRGLW	; SET FLAG TO INDICATE MERGE LINE AVAIL
GS.M1B:	AOS	(P)
	POPJ	P,		;RETURN
;
;
;
; CODE TO PRECHECK
; THE MERGE FILE MAKEING AN ATTEMPT TO SEE IF ITS THE PROPER FORMAT
; PROPER FORMAT IS A TEST ON THE FIRST WORD IN TO SEE IF IT HAS 
; A SEQ NUMBER BETWEEN ASCII 00001-99999 WITH BIT 35=1
;
MFQCHK:	TRNN	MF2,1		;BIT 35=1?
	JRST	BADFRM		;NO..
	CAML	MF2,[OCT 301406030143]	;LESS THAN '00001'
	CAML	MF2,[OCT 345627134563]	;LESS THAN '99999'
	JRST	BADFRM		;DOESNT LOOK RIGHT FOR MERGING
	POPJ	P,		;PRECHK LOOKS OK..PROCEDE
;
;
BADFRM:	TTCALL	3,[ASCIZ /WEAVE FILE DOES NOT HAVE PROPERLY
FORMATTED SEQUENCE NUMBERS.

WEAVE ABORTED..

/]
	JRST	PHAS7
	MLOFF
;	TITLE	LINED -A LINE EDITOR FOR DISK FILES
;	SUBTTL	BY LARRY WADE/LW/WJM/DR PRIVITERA	V.007 25 DECEMBER 1969
;		
;	COPYRIGHT 1969, DIGITAL EQUIPMENT CORP.
	;MAYNARD, MASS., USA
;	
;	GENERAL-
;	LINED IS A DERVIATIVE OF
;	DREDIT (DRUM EDITOR) WHICH WAS WRITTEN BY WILLIAM MEIR OF APPLIED
;	LOGIC CORPORATION AND EXISTS AS PART OF THE DECUS LIBRARY.
;	LINED'S COMMAND STRUCTURE IS SIMILIAR TO DEC'S "EDITOR"
;	BUT WORKS WITH FILES WHICH RESIDE ON THE DISK INSTEAD OF
;	ON DEC TAPE.
;
;	MODIFIED FOR SPECIAL USE BY D.R. PRIVITERA-OCT 69-.
;	TO BE USED WITH THE SIMPLIFIED USER DIALOGUE SYSTEM.
;
;	
;	
;	
;	PURPOSE-
;	
;	THIS MEMO WILL DESCRIBE THE CAPABILITIES AND RESTRICTIONS OF 
;	LINED AND OUTLINE THE CHANGES NECESSARY TO MAKE LINED A PART 
;	OF DEC'S SOFTWARE.
;	
;	A MAJOR FEATURE OF LINED IS THE ABILITY TO
;	REFERENCE ANY LINE AT ANY TIME, WITHOUT FIRST HAVING TO
;	CLOSE AND THEN REOPEN THE FILE.
;	
;	
;	COMMAND STRUCTURE-
;	
;	IN THE FOLLOWING DESCRIPTION, A POINT (.) OR DOT REFERS TO THE
;	LAST LINE WHICH WAS TYPED, THE LAST LINE DELETED, OR THE LAST
;	LINE INSERTED.
;	
;	
;	1. TO OPEN AN EXISTING FILE FOR EDITING.
;	
;		S<FILENAME.EXT><CR>	
;	
;	2. TO CREATE A NEW FILE.
;	
;		S<FILENAME.EXT><ALTMODE>
;	
;	3.TO INSERT TEXT
;	
;		I
;		I N1
;		I N1,N2		INSERT THE TEXT WHICH FOLLOWS AT LINE
;				N1 AND ASSIGN SEQUENCE NUMBERS IN
;				INCREMENTS OF N2 FOR EACH LINE AFTER THE
;				FIRST. IF LINE N1 EXISTS IT WILL BE
;				REPLACED. IF N1 IS NULL A VALUE OF 10 IS
;				ASSUMED. N1 MAY BE SPECIFIED AS A POINT (.).
;	
;				IF N2 IS NULL EITHER A VALUE OF 10
;				IS ASSUMED OR THE VALUE LAST SPECIFIED IN
;				AN INSERT COMMAND IS USED.
;				AN ALTMODE IS TYPED TO RETURN TO LINED
;				COMMAND LEVEL.
;	
;	4. TO PRINT TEXT.
;	
;		P N1
;		P N1,N2		PRINT LINE N1 OR LINES N1 THROUGH N2
;				N1 MAY BE SPECIFIED AS A POINT (.).
;				N2 MAY ALSO BE SPECIFIED AS A POINT AS
;				LONG AS N1<N2.
;	
;				AN ALTMODE WILL CAUSE THE NEXT LINE
;				TO BE TYPED.
;	
;	5. TO DELETE TEXT
;	
;		D N1
;		D N1,N2		DELETE LINE N1 OR LINES N1 THROUGH N2
;				N1 MAY BE SPECIFIED AS A POINT (.)
;				UNLESS AN INSERT WAS JUST PERFORMED.
;				N2 MAY ALSO BE SPECIFIED AS A POINT AS
;				LONG AS N1<N2.
;	
;	6. TO CLOSE A FILE
;	
;		E<CR>
;	
;	
;	RESTRICTIONS-
;	
;	1.	FILES ARE WRITTEN WITH STANDARD PROTECTION (55).
;	
;	2.	WHEN IN INSERTION MODE, THE ALTMODE RETURNS TO LINED 
;		COMMAND LEVEL. IF THE ALTMODE IS ALSO USED TO TERMINATE
;		A LINE OF TEXT TO BE INSERTED, THE LINE OF TEXT IS 
;		IGNORED.
;	
;	3.	LINED ASSUMES ALL BLOCKS ON THE DISK FILE HAVE AN
;		INTEGRAL NUMBER OF LINES. TO GUARANTEE THIS THE USER
;		SHOULD USE THE "A" SWITCH (LINE BLOCKING) WITH PIP TO PUT A FILE ON
;		THE DISK.
;	
;	4.	LINED AND TECO.15 AND UP ARE COMPATIBLE AND THE SAME	FILE
;		CAN BE EDITED BY BOTH
 ;
;	5.	LINE NUMBER 0 IS ILLEGAL.
;	
;	
;	ERROR HANDLING-
;	
;	WHEN AN ERROR IS DETECTED, A MESSAGE IS TYPED AND LEAVES THE 
;	USER AT COMMAND LEVEL. THE POSSIBLE ERRORS WHICH CAN OCCUR ARE 
;	AS FOLLOWS:
;	
;
;	THE FOLLOWING ERROR MESSAGES HAVE BEEN REVISED TO BE MORE EXPLANITORY.
;		DDE-DEVICE DATA ERROR
;		ILR-INDEX DURING INSERTION REFERENCES AN ALREADY EXISTING
;			LINE
;		ILS-INDEX DURING INSERTION SKIPS OVER AN
;			EXISTING LINE
;		NCF-NOT A CURRENT FILE
;		NLN-NON-EXISTENT LINE
;		UNA-UNAVAILABLE DEVICE
;		DCE-DEVICE DIRECTORY FULL
;		NFO-NO ACTIVE FILE
;		FAU-FILE NAME ALREADY IN USE
;		ILC-ILLEGAL COMMAND
;		CCL-EXPERIENCED AN ERROR WHILE REFERENCING THE CCL FILE
;	
;	
;	
;	SUDS (SIMPLIFIED USER DIAL. SYS.	INTERFACE
;	
;	THE SUDS CUSP WILL GENERATE A TEMPORARY DISK FILE WITH THE NAME
;	
;		###EDT.TMP
;	
;	IN ORDER TO PASS ANY LEGAL SLINED COMMANDS
;	
;	
;	
;
;	IMPLEMENTATION-
;	
;	EACH LINE OF TEXT IS STORED IN THE WORKING BUFFER WITH A LINE
;	HEADER WHICH HAS TWO ITEMS.	IN THE LEFT HALF IS THE SEQUENCE
;	NUMBER IN BINARY AND IN THE RIGHT HALF IS THE NUMBER OF WORDS
;	(INCLUDING THE LINE HEADER) WHICH ARE NEEDED TO STORE THE 
;	LINE OF TEXT.	THUS TO FIND THE NEXT LINE OF TEXT IT IS JUST
;	NECESSARY TO TAKE THE ADDRESS OF THE CURRENT LINE HEADER AND
;	ADD THE WORD COUNT.
;	
;	SEVERAL POINTER WORDS ARE USED TO KEEP TRACK OF THE LINES IN
;	THE WORKING BUFFER.	WRTLST CONTAINS THE SEQUENCE NUMBER OF THE
;	LAST LINE WHICH WAS JUST PASSED THROUGH THE WORKING BUFFER.
;	SEQLST CONTAINS THE HIGHEST SEQUENCE NUMBER OF THE HIGHEST
;	LINE IN THE BUFFER.	SN CONTAINS THE SEQUENCE NUMBER OF THE LINE
;	CURRENTLY BEING HANDLED IN A COMMAND.
;	
;	WHEN THE PROGRAM DISCOVERS THAT SN IS GREATER THAN WRTLST IT
;	KNOWS THAT THE LINE BEING SOUGHT HAS ALREADY PASSED THROUGH
;	THE WORKING BUFFER AND IS NOT DIRECTLY ACCESSIBLE SINCE THERE
;	IS NO WAY TO READ A FILE BACKWORDS.	CONSEQUENTLY, IT IS NEC-
;	ESSARY TO CLOSE THE FILE AND THEN REOPEN IT.	THIS PROCESS OF
;	GOING AROUND FROM WHERE WE ARE, TO THE END OF THE FILE, TO
;	THE BEGINNING OF THE FILE AND FINALLY TO THE LINE BEING SOUGHT
;	IS DONE.
;	
;	TO CLOSE THE FILE IT IS NECESSARY TO PASS ALL REMAINING TEXT
;	THROUGH THE WORKING BUFFER AND TO THE TEMPORARY OUTPUT FILE
;	(CALLED ###LIN.TMP).	THIS IS DONE BY GIVING THE SUBROUTINE
;	FNDLIN (FIND A LINE WHOSE SEQUENCE NUMBER IS IN SN) THE 
;	HIGHEST POSSIBLE SEQUENCE NUMBER, 99999.	NEXT THE ORIGINAL 
;	FILE IS RENAMED TO ###TMP.TMP, THE TEMPORARY OUTPUT FILE
;	IS RENAMED TO THE ORIGINAL FILE NAME, AND THE ###TMP.TMP FILE
;	IS DELETED.	THEN WE GIVE FNDLIN THE SEQUENCE NUMBER CURRENTLY
;	BEING LOOKED FOR AND THE PROGRAM CONTINUES WITH THE ORIGINAL
;	COMMAND.
;	
;	
;	; AC DEFINITIONS

FF=0	; FLAGS
CH=1	; ASCII CHARACTER
SN=2	; SEQUENCE NUMBER
Q=3	; PDL POINTER
TAC=4	; TEMPORARY AC'S
TAC1=5	;
LP=6	; ADDR OF CURRENT LINE
N1=7	; ARGS FOR COMMANDS
N2=10	;
NUM=11	; FOR NUMBER CONVERSION
AC1=12	; SCRATCH
AC2=13	;	"
AC3=14	;	"
AC4=15	;	"
OC=16	;
PS=17	;PREVIOUS SEQUENCE NUMBER

; MORE DEFINITIONS
	ALTMOD=33		;DEFINITION OF AN ALTMODE
	ETTY=0
	TTYMD=1		; ASCII LINE MODE
	EICHL=1		; EDIT DEVICE CHANNELS
	EOCHL=2
	EMDE=10		; EDIT DEVICE MODE
	CCLC=3		;CCL DSK CHANNEL
	PJOB=30		;CALLI ARGUMENTS
	RESET=0
	APRENB=16
	CORE=11
	NXM=20000	;FOR PROCESSOR TRAP CHECKING
	JOBAPR=125
	JOBTPC=127

;RH FLAGS (SET AND CLEARED DURING CMD EXECUTION)
	NARG=1		; 1=TWO ARGS SEEN
	TFLG=10		;AN EXISTING LINE HAS BEEN REPLACED ONCE ALREADY
	DARG=20		; USED IN DCOM
	FILFLG=40	;
	IFLG=100	;INSERTION CMD IN PROGRESS

;LH FLAGS (CLEARED ONLY BY E COMMAND)
	FILACT=1	;DENOTES ACTIVE FILE
	NFILE=2		; 0=NEW FILE ONLY
	ROUND=4		;=1 IF IN THE PROCESS OF GOING AROUND TO THE
			;BEGINNING OF THE FILE
	CCLF=100		;=1 IF ENTERED VIA CCL
	TYPRED=200		;=1 WHEN A READY MUST BE TYPED ON EXIT
	RETURN=1000		;=1 IF RETURN IS NOT TO TYPE #READY
	LEADR=2000		;=1 WHEN LEADING ZEROS NEED SUPPRESSING
	ERSW=4000		;=1 IF TYPEING ERROR MESSAGE
	TABFLG=10000		;=1 PRINT A TAB AFTER SEQ# (INSTEAD OF BLANK)
	PERIOD=20000		;=1 WHEN A PERIOD FOUND IN COMMAND STRING
	ALTON=40000		;=1 WHEN AN ALTMODE IS DETECTED IN
				; LINE INSERTION MODE

	IFNDEF CCLSW,<CCLSW=1>

;;;;;;;;;;;;;;;;;;;;;;;;;;;

	CCLSW=1

;;;;;;;;;;;;;;;;;;;;;;;;;;;


;EXTERNS

EXTERN JOBVER
EXTERN JOBFF,JOBSA,JOBREL,JOBREN


;	INITIALIZATION
	MLON

STARTX:	IFN CCLSW,<TLZA 	FF,CCLF
	TLO	FF,CCLF>

;	INIT LOW CORE
;
	EXTERNAL SVREL
	EXTERNAL DATBOT,DATTOP
;
;
	MOVE	TAC,[XWD DATVAR,DATBOT]
	BLT	TAC,DATTOP
	MOVE TAC,JOBREL
	MOVEM TAC,SVREL
;	MOVEI	TAC,DATTOP+1
;	HRLM	TAC,JOBSA
;
;
	MOVEI TAC,3	;GET PPN AND STORE IN DIRECTORYS
	CALLI AC1,PJOB
CCLHR1:	IDIVI	AC1,12
	ADDI	AC2,"0"-40
	LSHC	AC2,-6
	SOJG	TAC,CCLHR1
	HLLM	AC3,CCLDIR
	HLLM	AC3,ODIRC
	HLLM	AC3,ZDIRC
CSETUP:	CALLI RESET	;INITIALIZE 
	MOVEI	TAC,TRAPER	;ENABLE MEMORY VIOLATION TRAP
	MOVEM	TAC,JOBAPR
	HRRZI	TAC,NXM
	CALLI	TAC,APRENB
;
;
;	EXPAND AVAILABLE LOW SEG AREA TO 4K
;
;	MOVE TAC,JOBREL
;	ADDI TAC,3777
;	CALLI	TAC,CORE
;	JRST	SYSFLP		;IF CANT EXPAND INDICATE ERROR
;
;
	HRRI	COMCLR
	HRRM	JOBREN	;SET UP ALTERNATE ENTRY POINT
	INIT	ETTY,TTYMD
	SIXBIT /TTY/
	XWD	TYOBF,TYIBF
	JSR	UNA,ERROR
TIBUF:	INBUF	ETTY,1
TOBUF:	OUTBUF	ETTY,1
	MOVE TAC,JOBFF
	MOVEM TAC,SVJBFF
	SETZM	SN
IFN CCLSW,<	AND	FF,[XWD CCLF,0]
	TLNE 	FF,CCLF
	JSR	CCLHRX>


COMCLR:	MOVE Q,PDLPNT		;CLEAR PDL LIST

COMRET:	PUSH Q,.		; FOR RETURN (NEVER "JRST" HERE)
	TRZ FF,777777		;CLEAR FLAGS
IFN CCLSW,<
	TLNN	FF,CCLF	>
	PUSHJ Q,ASKOUT



; DISPATCH ROUTINE

COMRT1:	PUSHJ Q,GETCH1
	CAIN	CH,15
	PUSHJ	Q,GETCHR
	CAIN CH,12
	PUSHJ	Q,GETCHR
	CAIN	CH,"T"		;CHANGE TO TAB AFTER BLANK?
	JRST	TABCHG		;YES
	CAIE	CH,"S"
	CAIN CH,"E"
	JRST ARWCOM
	CAIN	CH,"X"
	JRST	DESDSP		;X COMAND DISPATCH FOR DESTROYING
	TLNN	FF,FILACT
	HALT			;***JIST TO TEST
;	JSR	NFO,ERROR
	CAIN CH,ALTMOD	;CHECK FOR ALTMODE
	JRST SPLPRT	;ALMODE SEEN
	MOVE AC4,CH
	PUSHJ Q,GETARG
	CAIN AC4,"I"
	JRST ICOM
	CAIN AC4,"D"
	JRST DCOM
	CAIN AC4,"P"
	JRST PCOM
	JSR	ILC,ERROR
;
;
;
; CODE TO CHANGE TO TAB FUNCTION FOR SPACING AFTER SEQ #
;
TABCHG:	TLO	FF,TABFLG	;SET TAB FLAG
	JRST	COMRT1		;GO READ NEXT COMMAND
;
;
DESDSP:	SETZM	INWRIT
	JRST	DESTROY
SPLPRT:	AOJG SN,PDCOM	;PRINT THE NEXT LINE
	JRST COMCLR	;ZERO SN



; ROUTINE TO PICK UP ARGS FROM COMMAND
;	RETURNED IN N1,N2 (N2=0 FOR 1 ARG)
;	SPACES IGNORED

GETARG:	SETZB N2,NUM
	MOVEI	TAC,5		; COUNT
GTARG2:	PUSHJ	Q,GETCHR	; GET INPUT CHAR.
	CAIG	CH,"9"		; NUMBER?
	CAIGE	CH,"0"
	JRST	GTARG3		; NO
	PUSHJ	Q,NCHCON	; YES - CONVERT
	SOJGE	TAC,GTARG2	; AGAIN
	JSR	ILC,ERROR		; NO
GTARG3:	CAIN	CH,40		; SPACE?
	JRST	GTARG2		; YES
	CAIE	CH,"."
	JRST	.+4
	TLO	FF,PERIOD	; SET PERIOD DETECTED FLAG.=1
	MOVE	NUM,SN
	PUSHJ	Q,GETCHR
	TRNE	FF,NARG		; 2ND TIME?
	SKIPA	N2,NUM		; YES
	MOVE	N1,NUM		; NO
	CAIN	CH,15		; CR
	JRST	GETCHR		; YES - DISCARD LF+RETURN
	CAIN CH,ALTMOD	;CHECK FOR ALTMODE
	POPJ Q,
	CAIN	CH,","		; TWO ARGS?
	TROE	FF,NARG		; YES - ALLOWABLE?
	JSR	ILC,ERROR	; NO OR BAD CHAR
	JRST	GETARG		; YES

CON033:	TLZ	FF,ALTON	;TURN OFF ALTMODE DETECTED FLAG
	TRZ	FF,NARG+TFLG+IFLG	;CLEAR FOR SAFETY SAKE
	SETZM	STPSIZ
	MOVEI CH,33	;MAKE IT STANDARD
	POPJ Q,	;RETURN
;	INSERT COMMAND

; N1=SN WANTED
; N2=STEP SIZE

ICOM:	SETOM	INWRIT		;SET IN INSERT MODE SWITCH
;	TLO	FF,RETURN		;DO NOT TYPE READY ON RETURN
	SKIPE	N2		; NEW STEP SIZE?
	MOVEM	N2,STPSIZ	; YES
	SKIPN SN,N1	;LINE 0 IS ILLEGAL
	PUSHJ Q,SET10	;ASSUME SN OF 10 IF NONE SPECIFIED
	PUSHJ	Q,LPSET		;SET PS AND LP
ICOM1:	TLO	FF,LEADR	;SUPPRESS LEADING ZEROS
	MOVEI	TAC1,AC4	; GET ASCII SEQ.#
	PUSHJ	Q,RDXA		; CONVERT TO ASCII
	PUSHJ	Q,FNDLIN	; FIND LOCATION OF LINE
	JRST	ICOM2		; NOT FOUND
	TRNE	FF,TFLG+IFLG	; EXISTS ALREADY
	JRST	BMPER		;REACHED AN EXISTING LINE #
	TRO	FF,TFLG		;SET "EXISTING" FLAG
ICOM2:	TROE	FF,IFLG
	CAMN	PS,AC3
	AOSA	TYOBF1
	JRST	SKPER		;BYPASSED AN EXISTING LINE # IN USE
	MOVEM	AC4,@TYOBF1	; SEQ # TO TYO BUFFER
	MOVEI	CH,40		; TAB
	TLNE	FF,TABFLG	;WANT A TAB INSTEAD OF BLANK?
	MOVEI	CH,11		;YES
	PUSHJ	Q,P1CHR		; OUTPUT BUFFER
	PUSHJ	Q,TYGTLN	; READ IN LINE
	JRST IFIX	;SPECIAL HANDLING FOR .
	CAMLE	SN,SEQLST	;UPDATE SEQLST ?
	MOVEM 	SN,SEQLST	;YES
	TRZE	FF,TFLG		;GOOD RETURN
	PUSHJ	Q,DCOM2		; DELETE EXISTING LINE
	AOS	TAC,AC3		; NEW WD COUNT (+1 FOR HEADER)
	CAMLE	TAC,FREWDS	; ROOM?
	PUSHJ	Q,ICOM3		; NO
	HRRM	LP,MOV1X
	HRRZ	AC1,LP
	ADD	AC1,AC3
	HRRM	AC1,MOV2X
	HRRZ	AC2,PNTR
	SUBI	AC2,(LP)
MOV1:	XCT	MOV1X
MOV2:	XCT	MOV2X
	SOJGE	AC2,MOV1


	ADDM	AC3,PNTR
	MOVN	TAC,AC3		; UPDATE POINTERS
	ADDM	TAC,FREWDS	; NEW FREE WD NUMBER
	HRL	AC3,SN		;HEADER WORD
	MOVSI	AC4,200000	;BLANK (REPLACES TAB) OCTAL 11 OR BIN 44000
	TLNE	FF,TABFLG	;WANT A TAB INSTEAD OF BLANK?
	MOVSI	AC4,44000	;YES
	MOVSI	AC2,(TAC)	;COUNT
	HRRM	LP,M2X		;"TO"
	HRRZ	TAC1,TYIBF1	;"FROM"
	HRRM	TAC1,M1X
	JRST	M2
M1:	XCT	M1X
	LSHC	AC3,^D28	;4 CHARS
	LSH	AC3,1		;CLEAR BIT 35
M2:	XCT	M2X
	LSHC	AC3,^D7
	SOSL	@TYIBF1		;CHECK "REAL" COUNT
	AOBJN	AC2,M1
	AOBJN	AC2,M1+1	;(IF LAST WORD HAS FIVE CHARS)
	MOVE	AC3,SN		;SAVE PREVIOUS SN
	SKIPN	STPSIZ
	JRST	[TRZ FF,NARG+TFLG+IFLG
		SETZM	STPSIZ
		JRST COMRT1]
		;*****************************PATCH****************
	ADD	SN,STPSIZ	; NEXT LINE
	MOVE	N1,SN		;FOR RDXA
	JRST	ICOM1
ICOM3:	PUSHJ	Q,FNLN2A	;OUTPUT AND DELETE
	JRST	FNDLIN		;FIND LOC AGAIN

IFIX:	CAME N2,SN	;ALLOWS FOR POSSIBILITY OF GIVING AN I
			;COMMAND BUT NOT GIVING ANY DATA
	SUB SN,STPSIZ
	JRST CARRET

SET10:	MOVEI SN,12
	MOVEI N1,12
	MOVEI AC4,"10"
	POPJ Q,
;
BMPER:	MOVE	A,SN		;BUMPED INT OTHIS #
	SKIPA
SKPER:	MOVE	A,PS		;TRIED TO BYPASS THIS #
	TRZ	FF,NARG+TFLG+IFLG		;CLEAR IN CASE MORE INSERT FOLLOW
	SETZM	STPSIZ
	TLO	FF,LEADR+ERSW		;SUPPRESS LEADING ZEROS
	MOVE	TAC1,[POINT 7,SBERR1]
	PUSHJ	Q,RDX1		;MAKE SEQ # ASCII
	TTCALL	3,SBERR
	JRST	DESTROY
;
;

; ROUTINE TO GET AND CHECK LINE
; IN TTY INPUT BUFFER
;	RETURNS
;		POPJ Q, ALTMODE
;		JRST CPOPJ1 - NORMAL

TYGTLN:	TLO	FF,ALTON	;TURN ON THE ALTMODE FLAG
TYG.2:	INPUT	ETTY,0
	MOVE	AC3,TYIBF1
	MOVEM	AC3,GETPOX
	SETZM	AC3		;CHARACTER COUNTER
	MOVE	AC1,[POINT 7,LINES]	;LINE BUFFER POINTER
TYG.1:	ILDB	CH,GETPOX
	CAIN	CH,"_"			;SPECIAL RUBOUT
	JRST	RUBCHR			;YES..PROCESS IT
	CAIN	CH,175		;ALTMODE?
	JRST	RUBLLN			;YES..PROCESS IT
	CAIN	CH,15		;<CR>
	JRST	PUT.LC			;END INPUT STREAM . FORSE CRLF
	CAIN	CH,12			;SAME FOR LF CHARACTER
	JRST	PUT.LC
	TLZ	FF,ALTON
	IDPB	CH,AC1			;SAVE INPUT CHARACTER
	ADDI	AC3,1			;INC CHARACTER COUNT
	JRST	TYG.1			;PROCESS NEXT INPUT CHARACTER
;
RUBCHR:	CAMN	AC1,[POINT 7,LINES]	;AT BEGINNING OF LINE?
	JRST	TYG.1			;YES RUBOUT LINE
	CAMN	AC1,[POINT 7,LINES-1,34]	;AT BEGINNING OF LINE?
	JRST	TYG.1
	SETZM	CH			;CLEAR LAST INPUT CHAR
	DPB	CH,AC1
	LDB	AC2,P.L			;GET BYTE POSITION POINTER
	ADDI	AC2,7		;BACK IT UP ONE
	CAIL	AC2,^D36		;AT BEGINNING OF CURRENT WORD
	JRST	RC.1			;YES..
RC.2:	DPB	AC2,P.L		;REPLACE BYTE POINTER POSITION
	SUBI	AC3,1			;DECREMENT CHARACTER COUNTER
	JRST	TYG.1
;
RC.1:	MOVEI	AC2,1		;SET 'P' POSITION TO 1
	SUBI	AC1,1			;BACKUP TO PREVIOS WORD.
	JRST	RC.2
;
P.L:	POINT 6,AC1,5
;
;
RUBLLN:	TLOE	FF,ALTON	;SET ALTMODE FLAG ON IF ITS 
	JRST	CON033			;ALREADY ON GO AWAY FROM LINE 
;					;INSERTION MODE
	TTCALL	3,[ASCIZ . DELETED
.]
	MOVEI	TAC1,AC1
	MOVE	A,SN
	TLO	FF,LEADR	;SUPPRESS LEADING ZEROS
	PUSHJ	Q,RDXA		;REPRINT LINE NUMBER
	MOVEI	CH,40
	TLNE	FF,TABFLG	;TAB INSTEAD OF BLANK?
	MOVEI	CH,11		;YES
	SETZM	AC2
	TTCALL	3,AC1
	TTCALL	1,CH
	FLUSH	(LINES,20)
	JRST	TYG.2			;RESTART LINE INPUTTING
;
PUT.LC:	MOVEI	CH,15		;FORSE OUT A <CR>
	IDPB	CH,AC1
	MOVEI	CH,12			;FORSE OUT <LF>
	IDPB	CH,AC1
	ADDI	AC3,2			;INCREMENT CHARACTER COUNTER
	IDIVI	AC3,5			;MAKE THE WORD COUNT
	SKIPE	AC4			;IF THERE IS A REMAINDER 
	ADDI	AC3,1
	MOVEM	AC3,@TYIBF1
	HRRZ	AC1,TYIBF1		;SETUP TO XFER EDITED LINE
	ADDI	AC1,1
	HRLI	AC1,LINES
	HRRM	AC1,PUT.X
	XCT	PUT.X
	FLUSH	(LINES,20)
	MOVE	AC1,TYIBF1
	ADD	AC1,(AC1)
	SOS	AC1
TYG1:	ILDB	CH,AC1
	TLNE	AC1,700000
	JRST	TYG1
	CAIG	CH,14
	CAIG	CH,11
	JRST	CPOPJ1
	AOJA	AC3,CPOPJ1		;RETURN
;
	PAGE

; ROUTINE TO DELETE A LINE (OR LINES)
; FROM TEXT BUFFER
;	N1	ADR	OF FIRST LINE TO DELETE
;	N2	ADR	OF LAST LINE TO DELETE

DCOM:	SETOM	INWRIT		;IN CASE ERROR DETECTED BEFORE FINISHED
	SKIPE	AC4,N2		;TEST AND SAVE
	CAMGE	N1,N2		;LEGAL ?
	SKIPN	SN,N1		;YES,SET FOR FNDLIN
	JSR	ILC,ERROR
	PUSHJ	Q,LPSET		;SET UP LP AND PS
	PUSHJ	Q,FNDLIN	;SEARCH (PS CONTAINS SN OF PREVIOUS LINE UPON RETURN)
	JRST	TLERR		;CANT FIND LINE
	JUMPE	AC4,DCOM5
	MOVE	SN,AC4
	MOVE	AC4,LP
	HRL	AC4,PS		;SAVE SN OF PREVIOUS LINE
	PUSHJ	Q,FDL1A	;FIND 2ND LINE(WON(T ADVANCE PAGE)
	SKIPA		;NOT FOUND
	JRST	DCOM1		;FOUND
	CAMLE	SN,SEQLST	;IN BUFFER?
	JRST	DCOM3		;NO DELETE ALL REST OF BUFFER
	SETZ	TAC,		;YES TEMOVE EXTRANEOUS

; BASIC ROUTINE TO REMOVE A LINE FROM
; TEXT BUFFER
;	GIVEN:	ADR OF LINE TO REMOVE
;	; # WORDS TO CLEAR
;	C(TAC)=WORD COUNT
;	C(LP)=ADR OF LINE

;AC1 CONTAINS SN OF LINE BEING DELETED

DCOM1:	CAMN	SN,SEQLST
	HLRM	AC4,SEQLST
	ADDI	TAC,(LP)
	SUBI	TAC,(AC4)
	MOVEI	LP,(AC4)
DCOM2:	HRRM	TAC,DCOM2X	; FROM=TO
	XCT	DCOM2X
	HRR	AC1,LP		; TO
	HRRZ	AC2,PNTR
	SUBI	AC2,(TAC)		; LAST DEST 
	BLT	AC1,(AC2)	; MOVE
	HRRM	AC2,PNTR
	ADDM	TAC,FREWDS
	POPJ	Q,		;RETURN
DCOM3:	HLRM	AC4,SEQLST
	HRRZ	AC1,PNTR	;REST. FIRST FREE
	SUBI	AC1,(AC4)
	HRRM	AC4,PNTR
	SETZM	(AC4)
	ADDM	AC1,FREWDS
DCOM4:	CAMGE	SN,NUM
	JRST	FNLN1A
	PUSHJ	Q,MVLIN1
	TLNE	FF,NFILE
	PUSHJ 	Q,GETLIN		;LOOK FOR LINE WE WANT
	POPJ 	Q,		;NO MORE
	JRST	DCOM4

DCOM5:	CAMN	SN,SEQLST	;LAST SN OF BUFFER ?
	MOVEM	PS,SEQLST
	JRST	DCOM2

; ROUTINE TO PRINT LINES
; WILL ONLY PRINT ONE LINE AT A TIME

PCOM:	SETOM	INWRIT		;JUST IN CASE YOU TRY TO LIST A NON-EXISTANT LINE
	TLO	FF,TYPRED	;INDICATE SUDS RETURN IS "PHAS7"
	SKIPN SN,N1
	MOVEI SN,1	;ASSUME 1 IF NONE SPECIFIED
	SKIPN AC4,N2
	JRST PCOM11
	CAMLE N1,N2
	JSR ILC,ERROR
PCOM11:	PUSHJ	Q,LPSET
	PUSHJ	Q,FNDLIN	;FIND IT
	JRST	TLERR
PCOM1:	PUSHJ	Q,FNDLIN
	MOVE	SN,AC1
	HLRZ	A,(LP)		;GET SEQ NUM
	JUMPE	A,ENDTTY
	MOVEI	TAC,5
	CAML	TAC,TYOBF2
	PUSHJ	Q,ENDTTY	;OUTPUT BUFFER
	MOVE	TAC1,TYOBF1
	TLO	FF,LEADR		;SUPPRESS LEADING ZEROS
	PUSHJ	Q,RDX1		;CONVERT & OUTPUT
	MOVEM	TAC1,TYOBF1	;FIX UP HDR POINTER
	MOVNI	TAC,5
	ADDM	TAC,TYOBF2
	HRRI	TAC1,1(LP)
	HRLI	TAC1,440700	;FORM BP
	ILDB	CH,TAC1
	PUSHJ	Q,PUTC
	CAIG	CH,14
	CAIG	CH,11
	JRST	.-4
	CAML	SN,AC4
	JRST	ENDTTY
	AOJA	SN,PCOM1
PDCOM:	XCT	PDCOMX
	JRST	PCOM1
TLERR:	MOVE	A,SN		;GET NONE EXISTANT LINE #
	PUSH	Q,TAC1
	MOVE	TAC1,[POINT 7,LERRN]
	TLO	FF,LEADR+ERSW	;SUPPRESS LEADING ZEROS
	PUSHJ	Q,RDX1		;MAKE SEQ# ASCII
	POP	Q,TAC1
	TTCALL	3,LERRM
	TTCALL	3,LERRN
	JRST	DESTROY
;

; BUFFER STRUCTURE
; TEXT IS STORED LINE BY LINE IN A SEQUENTIAL MANNER
; THE CONTENTS OF THE FIRST WORD OF LINE ARE AS FOLLOWS:
;	C(LH)= SEQUENCE NUM (BIN)
;	C(RH)= NUMBER OF WORDS
;	IN LINE (INCLUDING HDR WORD)

; LINE SEARCH ROUTINE
; GIVEN
;	ADR OF BEG. OF SEARCH IN LP
;	SEQ.NUMBER SOUGHT IN SN

; RETURN
;	A) FOUND: VIA A JRST CPOPJ1
;	LP CONTAINS ADR OF LINE
;	TAC CONTAINS WORD COUNT OF LINE
;	SN CONTAINS SEQ. NUM.

;	B) NOT FOUND - VIA A POPJ,
;	AS ABOVE EXCEPT LP CONTAINS	ADR OF THE FIRST LINE WITH
;	A SEQUENCE NUMBER HIGHER	THAN THE ONE SOUGHT
;		C(HEADER)=0 INDICATES END OF BUFFER

; A GENERAL ROUTINE TO 
; FIND A LINE AT ITS APPROXIMATE LOCATION
; UPON RETURN:
;	LP CONTAINS LOCATION
;	TAC CONTAINS WORD COUNT

; CALLED WITH SEQ. NUM. IN SN

FDL1A:	TRO	FF,FILFLG
FNDLIN:	PUSHJ	Q,QKCHEK	;CHECK LIMITS
	TLNN	FF,NFILE	;VIRGIN FILE ??
	JRST	LNSRCH		;FIND EXACT LOC.
				;AND RETURN
FNDLN1:	PUSHJ	Q,GETLIN	;GET WD COUNT
	JRST 	LNSRCH
FNLN1A:	CAMLE	TAC,FREWDS	;ROOM?
	PUSHJ	Q,FNDLN2	;NO - OUTPUT
FNDLNA:	HRL	AC1,TAC1		;FROM
	HRR	AC1,PNTR
	PUSHJ	Q,MOVLIN	;MOVE IT
	HRRM	TAC1,PNTR
	SETZM	(TAC1)
	MOVN	AC1,TAC
	ADDM	AC1,FREWDS
	MOVEM	NUM,SEQLST
	CAMG	SN,NUM		;THE ONE WE WANT?
	JRST	LSCHLP		;YES
	JRST	FNDLN1		;NO



; HERE WE OUTPUT AN ARBITRARY # OF
; LINES & THEN DELETE THEM

FNDLN2:	TRZE	FF,FILFLG
	JRST TPOPJ
FNLN2A:	SETZI	AC2,		;ZERO AC2
	PUSH	Q,TAC1		;SAVE COUNT
	PUSH	Q,TAC		;SAVE ADR
	HLRZ	TAC1,PNTR
FNDLN3:	PUSHJ	Q,OUTLIN
	ADD	AC2,TAC
	ADD	TAC1,TAC
	CAML	AC2,(Q)		;BIG ENOUGH HOLE ?
	CAMGE	AC2,OUTWRD	;OUTPUT ENOUGH?
	JRST	FNDLN3		;NO
	MOVE	TAC,AC2		;YES - FOR DCOM
	HLRZ	LP,PNTR
	PUSHJ	Q,DCOM2		;DELETE OUTPUT
	POP	Q,TAC
TPOPJ:	POP	Q,TAC1
	POPJ	Q,

;	LP - ADR OF LINE
;	SN - NUMBER WANTED
; C(RH)	TAC - WD COUNT
;	AC1 - SCRATCH - CONTAINS NEW SEQ.# ON RETURN

LSCHLP:	PUSHJ	Q,LPSET
LNSRCH:	SKIPN	TAC,(LP)	;GET & CHECK
	POPJ	Q,		;"VIRGIN" FILE RETURN
	HLRZ	AC1,TAC		;SEQ.#
	TLZE	FF,PERIOD	; PERIOD IN COMMAND STRING?
	MOVE	AC1,SN		;YES..
	CAMN	SN,AC1		;CHECK IT
	AOSA	(Q)		;FOUND
	CAMG	SN,AC1
	JRST	CLRTAC
	ADDI	LP,(TAC)	;UPDATE
	HRRZ	PS,AC1
	JRST	LNSRCH		;AROUND AGAIN
CLRTAC:	HRRZS	TAC
	POPJ	Q,

; QUICK CHECK ROUTINE TO DETERMINE WHETHER A LINE WITH SN
;	 LIES WITHIN THE BUFFER.

;	POPJ	Q,		;PAST END
;	JRST	CPOPJ1		;O.K.
; ALSO AN EXIT FOR BEFORE BUFFER
; CALL WITH SN SET

QKCHEK:	CAMG	SN,WRTLST	;LAST WRITTEN
	PUSHJ	Q,GORND
	CAMG	SN,SEQLST	;PAST END
CPOPJ1:	AOS	(Q)		;O.K.
CPOPJ:	POPJ	Q,		;RETURN

LPSET:	MOVE	PS,SEQLST
	HLRZ	LP,PNTR
	POPJ	Q,


GORND:	TRNN	FF,FILFLG
	JRST	.+3
	POP	Q,CH
	JRST	CPOPJ
	PUSH	Q,SN
	PUSH	Q,AC4
	MOVEM	Q,PSAVE
	TLO	FF,ROUND
	PUSHJ	Q,CLOSE
GORND1:	MOVE	Q,PSAVE
	MOVEI	CH,015
	PUSHJ	Q,ARWOLD
	TLZ	FF,ROUND
	PUSHJ	Q,LPSET
	POP	Q,AC4
	POP	Q,SN
	POPJ	Q,



; TELETYPE I/O ROUTINES
; (SHORT & SIMPLE)


IFN CCLSW,<

GETCHR:	TLNE	FF,CCLF
	JRST	CCLCHR
	JRST	TTYCHR

GETCH1:	TLNE	FF,CCLF
	JRST	CCLCH1
	JRST	TTYCH1


TTYCHR:	SOSGE	TYIBF2
TTYCH1:	INPUT	ETTY,
	ILDB	CH,TYIBF1
	JUMPE	CH,TTYCHR
	CAIN CH,032	;IGNORE ^Z
	JRST TTYCHR
	CAIE CH,176
	CAIN CH,175
	MOVEI CH,33	;CHANGE TO NEW FORM OF ALTMODE
	CAIL CH,140	;CONVERT LOWER TO UPPER CASE
	TRZ CH,40
	POPJ	Q,


>

IFE CCLSW,<
GETCHR:	SOSGE	TYIBF2		;MORE?
GETCH1:	INPUT	ETTY,0		;NO
	ILDB	CH,TYIBF1	;PICK UP CHAR
	JUMPE	CH,GETCHR	;NULL?
	POPJ	Q,		;RETURN
>

; NORMAL OUTPUT ROUTINE

PUTC:	SOSG	TYOBF2
PUTCH1:	OUTPUT	ETTY,0
	SKIPE	CH		;NULL?
	IDPB	CH,TYOBF1	;DEPOSIT
	POPJ	Q,

; ONE CHAR OUTPUT ROUTINE

ASKOUT:	MOVEI	CH,"*"
P1CHR:	PUSHJ	Q,PUTC
ENDTTY:	OUTPUT	ETTY,0
	POPJ	Q,


;GENERAL OUTPUT ROUTINE 

MESS:	HRLI TAC,440700
	ILDB CH,TAC
	JUMPE CH,ENDTTY
	PUSHJ Q,PUTC
	JRST .-3

ARWCOM:	TLZE	FF,FILACT
	JRST	CLOSE
	SETZM	IFILNM
	SETZM	IFILXT
	PUSHJ	Q,FILNAM
	SKIPN	IFILNM
	JSR	ILC,ERROR
	MOVE TAC1,IFILNM
	MOVEM TAC1,ZILCH
	CAIN	CH,"."
	PUSHJ	Q,FILEXT
	TLZ	FF,NFILE
ARWOLD:	PUSHJ	Q,SETUP
	CAIN CH,ALTMOD
	JRST INITO
	INIT	EICHL,EMDE
DV:	SIXBIT /DSK/
	EXP	EDIBF
	JSR	UNA,ERROR
	TLO	FF,NFILE
	LOOKUP	EICHL,IDIRC
	JSR	NCF,ERROR
	HLRZ	TAC,IDIRC+3
	SKIPN	TAC		;IS IT AN EMPTY FILE
	JRST	[TLNE FF,TABFLG
		 JRST EIBUF
		 TTCALL 3,[ASCIZ /EMPTY FILE..

/]
		JRST PHAS7]
EIBUF:	INBUF	EICHL,2
INITO:	INIT	EOCHL,EMDE
	SIXBIT /DSK/
	XWD	EDOBF,0
	JSR	UNA,ERROR
	TLNN	FF,NFILE
	PUSHJ	Q,[	LOOKUP EOCHL,IDIRC
			POPJ Q,
			JSR FAU,ERROR]
	ENTER	EOCHL,ODIRC
	JSR	DCE,ERROR
EOBUF:	OUTBUF	EOCHL,2
	PUSHJ	Q,EWRITE	;SET UP HEADER
	TLO	FF,FILACT
FILL1:	XCT	FILL1X	;INITIALIZATION FOLLOWS
	MOVEM	TAC,FREWDS
FILL2:	XCT	FILL2X
	HRRZM	TAC,PNTR
	HRLS	PNTR
	SETZM	(TAC)
	MOVEI	TAC,12
	TLNN	FF,ROUND
	JFCL
;	MOVEM	TAC,STPSIZ	******************REMOVE FOR STPSIZ TEST*******
	SETZM	WRTLST
	SETZM	SEQLST
	TLNE	FF,NFILE
	POPJ	Q,
CARRET:	JSP	TAC,MESS
	ASCIZ	/
/


CLOSE:	TLZE	FF,RETURN	;NEED TO TYPE READY?
	JRST	.+2
	TTCALL	3,[ASCIZ .
READY

.]
	SETZM	INWRIT		;CLEAR INSERT ENABLED FLAG
	SKIPN	IFILNM
	JSR	ILC,ERROR
	MOVEI	SN,^D99999
	PUSHJ	Q,FNDLIN
	HRRZ	TAC,FILL1X
	PUSHJ	Q,FNLN2A
CLOSE1:	RELEASE	EICHL,0
	RELEASE	EOCHL,0
	INIT	EICHL,0
	SIXBIT /DSK/
	0
	JSR	UNA,ERROR
	LOOKUP	EICHL,IDIRC
	JRST .+4
	CLOSE EICHL,0
	RENAME	EICHL,ZDIRC
	JSR	DDE,ERROR
	INIT	EOCHL,0
	SIXBIT /DSK/
	0
	JSR	UNA,ERROR
	LOOKUP	EOCHL,ODIRC
	JSR	DDE,ERROR
	CLOSE EOCHL,0
	HRLZI	TAC,55
	LSH	TAC,11
	IORM	TAC,IDIRC+2
	RENAME	EOCHL,IDIRC
	JSR	DDE,ERROR
	INIT EICHL,0
	SIXBIT /DSK/
	0
	JSR UNA,ERROR
	LOOKUP EICHL,ZILCH
	JRST .+4
	CLOSE EICHL,0
	RENAME	EICHL,BKNAM
	JSR DDE,ERROR
			;THIS AVOIDS A RENAME ERROR WITH .BAK
	INIT EICHL,0
	SIXBIT /DSK/
	0
	JSR UNA,ERROR
	LOOKUP	EICHL,ZDIRC
	JRST	NOIFIL
	CLOSE EICHL,0
	HRLZI	TAC,55
	LSH	TAC,11
	IORM	TAC,ZILCH+2
	RENAME	EICHL,ZILCH
	JSR	DDE,ERROR
NOIFIL:	RELEASE	EICHL,0
	RELEASE	EOCHL,0
	TLNE	FF,ROUND
	JRST	GORND1
	SETZM	IFILNM
	SETZM	IFILXT
	JRST	COMCLR

; A ROUTINE TO RETURN A DESCRIPTOR. RETURNS WITH
; FIRST NON LEGAL CHAR IN AC CH. NAME (IN SIXBIT) WILL
; BE IN AC TAC UPON RETURN.

FILNAM:	SKIPA	TAC1,[POINT 6,IFILNM]
FILEXT:	SKIPA	TAC1,[POINT 6,IFILXT]
	JRST	SXNAM4
	JRST	SXNAM2
SXNAM1:	SUBI	CH,40
	TLNE	TAC1,770000		;FULL ?
	IDPB	CH,TAC1			;STASH
SXNAM2:	PUSHJ	Q,GETCHR		;GET
	CAIN	CH," "			;SPACE ?
	JRST	.-2
SXNAM3:	CAIL	CH,60
	CAILE	CH,"Z"
	POPJ	Q,
	CAILE	CH,"9"
	CAIL	CH,101
	JRST	SXNAM1
	POPJ	Q,
DEVNUM:	SUBI	CH,40		;MAKE NUMBER SIXBIT
	DPB	CH,[POINT 6,DV,23]	;INIT DECTAPE CHANNEL INSTEAD OF DSK
	MOVSI	CH,446441	;MAKE 'DTA'
	HLLM	CH,DV
	PUSHJ	Q,GETCHR		;BETTER BE A ","
	CAIN	CH,","
	JRST	SXNAM2
	POPJ	Q,			;OR A FATE WORSE THAN DEATH
;
SXNAM4:	PUSHJ	Q,GETCHR	;LOOK FOR A DECTAPE #
	CAIN	CH," "	;IGNIRE BLANKS
	JRST	.-2
;	REMOVE FOR NOW	DECTAPE INPUT CODE*************
;	CAIL	CH,"0"		;FIND A NUMBER
;	CAILE	CH,"9"
;	JRST	.+2
;	JRST	DEVNUM		;MUST BE DECTAPE INPUT
	JRST	SXNAM3		;ORDINARY FILE NAME ENCOUNTERED

	
; ROUTINE TO MOVE LINE, FIX UP COUNT & POINTER (OF HEADER)
;	C(TAC1) = (FROM) TO
;	C(TAC)	= WD COUNT

MOVLIN:	MOVE	TAC1,TAC
	ADDI	TAC1,(AC1)
	BLT	AC1,-1(TAC1)
MVLIN1:	MOVN	N1,TAC
	ADDM	N1,EDIBF2
	ADDM	TAC,EDIBF1
	POPJ	Q,

;GETLIN ROUTINE

GETLIN:	HRRZ	TAC1,EDIBF1
	SKIPLE	N1,EDIBF2
	SKIPN	(TAC1)
	JRST	EDREAD
LNCNT:	MOVEI	TAC,1
	HRRM	TAC1,LNCNTY
	XCT	LNCNTY
	JRST	[CAME AC1,TECOFF
		JRST	LNCNT+5
		MOVEI	AC1,0
		MOVEM	AC1,@LNCNTY
		JRST	LNCNTX]
	JRST	LNCNTX
	TRNE	AC1,1
	JRST	.+2
	AOJA	TAC,.-5
LNCNTX:	CAMLE	TAC,N1
	MOVE	TAC,N1
	PUSHJ	Q,NUMCON
	HRRM	TAC,(TAC1)
	JRST	CPOPJ1

EDREAD:	INPUT	EICHL,0
	AOS	EDIBF1
	STATO	EICHL,760000
	JRST	GETLIN
	STATZ	EICHL,20000
	JRST	EOF
	TLZ	FF,FILACT
	JSR	DDE,ERROR
EOF:	TLZ	FF,NFILE
	POPJ	Q,		;"NO MORE" RETURN
TECOFF:	XWD	0,1

; OUTPUT
; GIVEN
; ADR OF LINE IN TAC1
; ACTION
; GET WORD COUNT
; CONVERT SEQ.# TO ASCII


OUTLIN:	SKIPN	(TAC1)		;END OF BUFFER
	JRST 	CLOSE1		;YES (END OF FILE)
	HRRZ	TAC, (TAC1)	; GET COUNT
	CAMLE	TAC,OC		; ROOM?
	PUSHJ	Q,EWRITE	; NO
	SUB	OC,TAC		; UPDATE COUNT
	HLRZ	A,(TAC1)	; SEQ. NUM.
	MOVEM	A,WRTLST
	PUSHJ	Q,RDX		; CONVERT SEQ #	TO ASCII
	HRL	AC1,TAC1		; FROM
	HRR	AC1,EDOBF1	; TO
	HRRM	AC1,BLTX	; END
	AOS	AC1
	XCT	BLTX		; MOVE
	ADDM	TAC,EDOBF1	; NEW LCN
	POPJ	Q,
EWRITE:	OUTPUT	EOCHL,0
	STATZ	EOCHL,740000
	JSR	DDE,ERROR
	HRRZ	OC,EDOBF2
	POPJ	Q,

; ASCII CHAR TO BIN NUMBER CONVERSION ROUTINE (NUMBER IS DECIMAL)
;	PUSHJ	Q,NUMCON
; WHERE
;	CH - CONTAINS CHAR. TO BE CONV.
;	NUM - CONTAINS BIN NUM. UPON RETURN

NCHCON:	IMULI	NUM,12		; DECIMAL
	ADDI	NUM,-60(CH)
	POPJ	Q,

; TO CONVERT 5 CHAR 7 BIT ASCII
; TO A BINARY NUMBER
NUMCON:	SETZI	NUM,
	HRLI	TAC1,440700
	ILDB	CH,TAC1
	PUSHJ	Q,NCHCON
	TLNE	TAC1,700000	; FINISHED
	JRST	NUMCON+2	; NO
	HRLM	NUM, (TAC1)	; YES
	POPJ	Q,		; RETURN

; BIN TO BCD CONVERSION ROUTINE
; ADAPTED FROM ANY RADIX PRINT IN MANUAL F-64PX	(8/64)

; A CALL TO THIS ROUTINE WILL CONVERT A BINARY SEQ. 
; NUMBER TO A	5 CHAR ASCII WORD WITH HEADING ZEROS

; CALL
;	PUSHJ	Q,RDX
; WHERE TAC1 CONTAINS ADR FOR RESULT
; A CONTAINS BINARY # TO BE CONVERTED

A=N1
A1=A+1

RDX:	SETOM	(TAC1)		; SET SN BIT
RDXA:	HRLI	TAC1,440700	; FORM POINTER
RDX1:	MOVEI	AC1,5		; 5 DIGITS
RADIX:	IDIVI	A,12		; RADIX IS DECIMAL
	HRLM	A1,(Q)		; SAVE REMAINDER
	SOSE	AC1		; FINISHED
	PUSHJ	Q,RADIX		; NO
	HLRZ	A,(Q)
	TLNE	FF,LEADR		;SUPPRESS LEADING ZEROS
	PUSHJ	Q,SUPS			;WHEN REQUIRED
	ADDI	A,60		; FORM ASCII
	IDPB	A,TAC1		; DEPOSIT IN WORD
	POPJ	Q,		; RETURN SOMEPLACE
SUPS:	SKIPE	A		;CHAR A ZERO
	JRST	SUPS1		;NO
	TLNN	FF,ERSW		;TYPING AN ERROR MESSAGE #
	JRST	.+3
	MOVEI	A,-30
	SKIPA
	MOVEI	A,-20		;MAKES THE CHAR A BLANK LATER
	POPJ	Q,
SUPS1:	TLZ	FF,LEADR+ERSW		;TURN OFF LEADER FLAG
	POPJ	Q,

; ERROR ROUTINE
; DECIPHERS ERROR # AND PRINTS	MESSAGE

ERROR1:	SOS ERROR
	LDB	AC1,EPNT1	; GET ERR #
	XCT	MES(AC1)
	SKIPN	AC1		;DDE ERROR?
	JRST	ERCLN		; YES TRY DELETEING ###TMP.TMP
	JRST	DESTROY
ERCLN:	INIT	15,0
	SIXBIT	/DSK/
	0
	JFCL
	SETZM	ZDIRC+2
	SETZM	ZDIRC+3
	LOOKUP	15,ZDIRC
	JFCL
	FLUSH	(ZDIRC,4)
	RENAME 	15,ZDIRC
	JFCL
	JRST	DESTROY

EPNT1:	POINT	4,@ERROR,12
;
;
;	PROCESSOR TRAP ROUTINE
;
TRAPER:;	TTCALL	3,[ASCIZ />/]
	PUSH	Q,TAC
	MOVE TAC,JOBREL
	ADDI TAC,2777
	CALLI	TAC,CORE
	JRST	SYSFLP		;CANT EXPAND
	MOVEI TAC,TRAPER
	MOVEM TAC,JOBAPR
	HRRZI TAC,NXM
	CALLI TAC,APRENB
	POP	Q,TAC
	JRST 2,@JOBTPC	;DATS WAT DEY SAID
;	JRST	START+1		;TRY STARTING AGAIN
;
SYSFLP:	TTCALL	3,[ASCIZ /SYSTEM FAILURE/]
	JRST	DESTROY
;






; ERROR MESSAGES


	DEFINE TYPE (A)
	<TTCALL 3,[ASCIZ /A/]>
;
;
MES:	TYPE <WHAT?

>
	TYPE <INDEX REACHED AN EXISTING LINE!>
	TYPE <INDEX SKIPPED OVER AN EXISTING LINE!>
	TYPE <YOUR SUDS TEMPORARY WORKING FILE 
MUST HAVE BEEN DELETED..
TO RECOVER,	A 'NEW' OR 'OLD' COMMAND MUST BE ISSUED.

>
	TYPE <NO SUCH LINE !
>
	TYPE <DEVICE UNAVAILABLE !>
	TYPE <DEVICE DIRECTORY FULL !>
	TYPE <SYSTEM FAILURE(NFO)>
	TYPE <FILE IN USE>
	TYPE <WHAT?

>
	TYPE <SYSTEM FAILURE(CCL)>
;
;
;
	DEFINE X(A)
	<A=ZZ
	ZZ=ZZ+1
	>
;
ZZ=0
	X DDE
	X ILR
	X ILS
	X NCF
	X NLN
	X UNA
	X DCE
	X NFO
	X FAU
	X ILC
	X CCL
;






LIT


; SUBROUTINE TO INITIALIZE PROGRAM



SETUP:	MOVE	TAC1,JOBFF
	MOVE	AC3,JOBREL
	MOVEI AC2,407*2
	ADD	TAC1,AC2
	HRRM	TAC1,FILL2X	;FIRST FREE LOC
	SUB	AC3,TAC1
	HRRM	AC3,FILL1X	;# FREE WORDS
	IDIVI	AC3,^D10
	MOVEM	AC3,OUTWRD
IFE CCLSW,<	POPJ	Q,
	>
IFN CCLSW,<
	TLNE	FF,ROUND
	POPJ	Q,
;************TEMP PATCH **********************10/20/69
	POPJ	Q,
;	::**********************
	TLNN	FF,CCLF
	POPJ	Q,
DESTROY:	TRZ	FF,777777	;CLEAR ALL TEMPORARY FLAGS
	SKIPE	INWRIT		;DO WE NEED TO KEEP READING COMMANDS
	JRST	COMRT1		;YES CAUSE WE ARE IN AN INSERT MODE
;	SETZM	CCLDIR
;	RENAME	CCLC,CCLDIR
;	JRST .+1
	MOVE TAC,SVREL
	CALL TAC,[SIXBIT/CORE/]
	JRST SYSFLP
	TLZE	FF,TYPRED	; RETURN TO "PHAS7" ?
	JRST	[TTCALL 3,[ASCIZ .
.]
		 JRST PHAS7]
DEST1:	JRST	STARTA		;RETURN TO START UP SUDS..
;	MOVSI	AC1,0
;	HRRI	AC1,RUNBLK
;	CALLI	AC1,35
;	TTCALL	3,[ASCIZ /SYSTEM FAILURE (DEST)/]
;	JRST	ERROR
;
RUNBLK:	SIXBIT	/SYS/
	SIXBIT	/SUDS/
	0
	0
	0
	0
;
;






	;	CCL INPUT CODE
;
CCLCHR:
CCLCH1:
;
;
CLD0:	ILDB	CH,CCLBF1	;GET A COMMAND CHARACTER FROM 'ORDERS'
	JUMPE	CH,DESTROY	;THIS SHOULD NEVER HAPPEN
	POPJ	Q,		;RETURN
;CLD0:	SOSG	CCLBF2
;	JRST	CLD1
;CLD2:	ILDB	CH,CCLBF1
;	JUMPE	CH,CLD0
;	POPJ	Q,
;CLD1:	IN	CCLC,
;	JRST	CLD2
;CLD4:	STATZ	CCLC,74000
;	TTCALL	3,[ASCIZ /SYSTEM ERR-CFTE/]
;	JRST	DESTROY
;CCLHRE:	INIT	CCLC,0
;	SIXBIT/DSK/
;	XWD	0,CCLBF
;	JSR	CCL,ERROR
;	SETZM	CCLDIR+3
;	LOOKUP	CCLC,CCLDIR
;	JSR	CCL,ERROR
;	MOVE TAC,JOBFF
;	MOVEM TAC,SVJBFF
;	INBUF	CCLC,1
CCLHRE:	MOVE	TAC,[POINT 7,ORDERS]	;SETUP COMMAND POINTER
	MOVEM	TAC,CCLBF1
	MOVEI	CH,0
	SOS	TYOBF2		;INITIALIZE OUTPUT BUFFER AND HEADER
	OUTPUT	ETTY,0
	JRST	@CCLHRX
>

	DATVAR:
;	BLOCK HEADERS FOR I/O BUFFERS
;
	BLOCK	^D15
;
;
;	MODIFIED INSTRUCTIONS THAT BELONG TO SHARABLE HIGH SEGMENT.
;
;	MOV1X:
	MOVE	AC1,(AC2)
;	MOV2X:
	MOVEM	AC1,(AC2)
;	M1X:
	MOVE	AC4,(AC2)
;	M2X:
	MOVEM	AC3,(AC2)
;	DCOM2X:
	HRLI	AC1,(LP)
;	PDCOMX:
	SETZI	AC4,
;	FILL1X:
	MOVEI	TAC,.
;	FILL2X:
	HRRZI	TAC,.
;	BLTX:
	BLT	AC1,(TAC)
;	LNCNTY:
	SKIPE	AC1,(TAC)
	JRST	LNCNT+3
	JRST	LNCNT+4
;	CCLHRX:
	0
	JRST	CCLHRE
;	SBERR:
	ASCII /LINE /
;	SBERR1:
	0
	ASCIZ / IN USE
/
;
;	LERRM:
	ASCIZ /NO LINE /
;	LERRN:
	0
	BYTE(7)15,12,12,0,0
;
;
;	ERROR:
	0
	JRST	ERROR1
;

;	INWRIT:
	0
;	PSAVE:
	0
	PDSIZ=^D25
	PDSIZ1=PDSIZ+1
;	FREWDS:
	0		; NUMBER OF FREE WDS
;	PNTR:
	0		;LH-FIRST FREE
			;RH-ADDR OF LAST HEADER
;	WRTLST:
	0		; SN LAST WRITTEN
;	STPSIZ:
	0		; STEP SIZE FOR INSERT
;	PDLPNT:
	XWD	-PDSIZ1,PDL-1
;	SVJBFF:
	0		; STORAGE
;	OUTWRD:
	0
;	SEQLST:
	0		;LAST SN IN CURRENT PAGE BUFFER
;
;
;	BKNAM:
	0
	0
	0
	0
;
;

;	IFILNM:

;	IDIRC:
	0
;	IFILXT:
	0
	0
	0

;	OFILNM:

;	ODIRC:
	SIXBIT	/###LIN/
	SIXBIT	/TMP/
	0
	0
;	ZDIRC:
	SIXBIT /###TMP/
	SIXBIT /TMP/
	0
	0
;	ZILCH:
	0
	SIXBIT /BAK/
	0
	0
;
;	CCLDIR:
	SIXBIT	/###EDT/
	SIXBIT	/TMP/
	0
	0
;	PUT.X:
	BLT	AC1,(AC3)
;	GETPOX:
	BLOCK 1
;	SVREL:
	BLOCK 1
;	LINES:
	BLOCK 24
;
;
;	PDL:
	BLOCK	PDSIZ
	END START

  0 