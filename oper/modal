      SUBROUTINE MODAL
      REAL  TITLE(70),CODE(80),WORD(3)
      DIMENSION D1( 1275),D2( 2500),A(50,8),VAL(50)
      DIMENSION VM(50),VK(50),VC(50)
C                  DIMENSION TO APPROPRIATE SIZE
C                  D1 IS ND*(ND+1)/2
C                  D2 IS ND * ND
C                  VAL, FMR, .... , NM, IFIN ARE ND
C                  D IS THE LARGER OF NVPR, ND, AND 30
C                  YF AND TF ARE EQUAL TO MAX NUMBER OF EXCITATION POINT
      COMMON IN,IOUT,NOUTP,IFPLT,NEXEC,NERR,NNT,NEQ,IFEQ(3),IFUNA,
     1       IFUPD(3),NREC(4),NFN(4),NS(10),NSTAT(600),TITLE,CODE
     1      ,IDMY(8),RDMY(334)
      COMMON         FMR(140),FMI(140),FR(140),FI(140)
     1,PC(140),DISP(140),ANGL(140),IFSP(140),
     1 NM(140),SD(140),S(140), YF(45),TF(45),
     2 IFIN(140),D(140),NUM(3),V(3),FREQ(3),TIME(4),NCMP(25,2),
     2 NVAR(25,2),
     3 NVPP(5,2),IFTYP(2),NPLOT(2),NPVAR(2),ND,NEQT,IFEXC,IFDSP,IFACC,
     4 IFDMP,IFMOD,IFFRQ,IFTIM,IFPRT,IPRNT(5),NEIG,JACOB
      COMMON/FILES/IDTFL,IPRCM,ITERM
      DATA WORD/'M','C','K'/
      ND=50
      NVPR=40
      NEQT=NEQ
      IF(NEQ-ND)20,20,2000
   20 IF(NERR)30,30,2010
C                  GET EQUATIONS OF MOTION
   30 NV=(NEQ*(NEQ+1))/2
      CALL VRDWT(D1,NV,NFN(1),NVPR,D,-1)
      CALL VRDWT(D2,NV,NFN(3),NVPR,D,-1)
C                  READ COMMAND DATA
      CALL REDAT(D1,D2)
C   -------------------------------------------------------------------
C                  EXECUTION BEGINS
C   -------------------------------------------------------------------
      IF(IFPRT)2050,1000,1000
 1000 IF(NERR)1005,1005,2050
 1005 IF(IFDSP)1010,1050,1010
C                  REMOVE SPECIFIED COORDINATES
 1010 NCT=0
      N1=0
      DO 1040 IC=1,NEQ
      IF(IFSP(IC)-1)1020,1040,1020
 1020 NCT=NCT+1
      FR(NCT)=FR(IC)
      FI(NCT)=FI(IC)
      FMR(NCT)=FMR(IC)
      FMI(NCT)=FMI(IC)
      IND=IC
      DO 1040 I=1,IND
      IF(IFSP(I)-1)1030,1040,1030
 1030 N2=NFND1(I,IC)
      N1=N1+1
      D1(N1)=D1(N2)
      D2(N1)=D2(N2)
 1040 CONTINUE
      NEQT=NCT
C                  COMPUTE MODAL EQUATIONS
 1050 CALL MSTOR(D2,ND,NEQT,NEQT,6)
      EPS=1.0E-06
      CALL MFACT(D1,NEQT,EPS,IER)
      CALL MFUNC(D2,NEQT,NEQT,D1,-1,IER)
      CALL MFUNC(D2,NEQT,NEQT,D1,2,IER)
      NV=(NEQT*(NEQT+1))/2
      CALL VRDWT(D1,NV,NFN(4),NVPR,D,1)
      NEIG=MIN0(NEIG,NEQT)
      IF(JACOB)1080,1080,1110
 1080 NCT=0
      DO 1090 I=1,NEQT
      NV=NEQT*(I-1)
      JST=I
      DO 1090 J=JST,NEQT
      NCT=NCT+1
 1090 D1(NCT)=-D2(NV+J)
      CALL EIGN2(D1,D2,VAL,NEQT,NEIG,NEIG)
      DO 1100 I=1,NEIG
 1100 VAL(I)=-VAL(I)
      GO TO 1140
C                  JACOBI METHOD
 1110 NCT=0
      DO 1120 I=1,NEQT
      JND=I
      NV=NEQT*(I-1)
      DO 1120 J=1,JND
      NCT=NCT+1
 1120 D1(NCT)=-D2(NV+J)
      CALL EIGEN(D1,D2,NEQT,0)
C                  GET VALUES
      NCT=0
      DO 1130 I=1,NEQT
      NCT=NCT+I
 1130 VAL(I)=-D1(NCT)
C                  READ U AND TRANSFORM VECTOR
 1140 NV=(NEQT*(NEQT+1))/2
      CALL VRDWT(D1,NV,NFN(4),NVPR,D,-1)
      CALL MFUNC(D2,NEQT,NEQT,D1,1,IER)
      DO 1160 I=1,NEIG
      IF(VAL(I))1160,1150,1150
 1150 VAL(I)=SQRT(VAL(I))
 1160 CONTINUE
C                  SCALE VECTOR
      IST=1-NEQT
      DO 1190 J=1,NEIG
      IST=IST+NEQT
      IND=IST+NEQT-1
      CON=0.
      DO 1170 I=IST,IND
 1170 CON=CON+D2(I)*D2(I)
      CON=1./SQRT(CON)
      DO 1180 I=IST,IND
 1180 D2(I)=D2(I)*CON
 1190 CONTINUE
C                  TRANSFORM TO MODAL COORDINATES
      NV=(NEQ*(NEQ+1))/2
      CALL VRDWT(D1,NV,NFN(3),NVPR,D,-1)
      IF(IFDSP)1225,1225,1195
C                  REMOVE SPECIFIED COORDINATES
 1195 N1=0
      DO 1220 IC=1,NEQ
      IF(IFSP(IC)-1)1200,1220,1200
 1200 IND=IC
      DO 1220 I=1,IND
      IF(IFSP(I)-1)1210,1220,1220
 1210 N1=N1+1
      N2=NFND1(I,IC)
      D1(N1)=D1(N2)
 1220 CONTINUE
 1225 DO 1240 ICOL=1,NEIG
      DO 1230 I=1,NEQT
      D(I)=0.
      N2=NEQT*(ICOL-1)
      DO 1230 J=1,NEQT
      N1=NFND1(I,J)
      N2=N2+1
 1230 D(I)=D(I)+D1(N1)*D2(N2)
      VK(ICOL)=0.
      N2=NEQT*(ICOL-1)
      DO 1240 I=1,NEQT
      N2=N2+1
 1240 VK(ICOL)=VK(ICOL)+D2(N2)*D(I)
C                  COMPUTE MASS AND DAMPING COEFFICIENTS
      DO 1250 I=1,NEIG
      VM(I)=VK(I)/(VAL(I)*VAL(I))
 1250 VC(I)=2.*PC(I)*SQRT(VK(I)*VM(I))
C                  CHANGE INITIAL DISPLACEMENTS TO NORMAL COORDINATES
      CALL MMOVE(A,ND,S,ND,NEQT,1,1)
      IF(NEQT-ND)1270,1260,2000
 1260 N1=1
      N2=2
      GO TO 1280
 1270 N1=1+NEQT
      N2=1
 1280 CALL MMOVE(A(N1,N2),ND,SD,ND,NEQT,1,1)
      NV=NEQT*NEQT
      CALL VRDWT(D2,NV,NFN(4),NVPR,D,1)
      CON=1.E-6
      CALL SIMQ1(A,D2,NEQT,2,CON,IER)
      CALL VRDWT(D2,NV,NFN(4),NVPR,D,-1)
      CALL MMOVE(S,ND,A,ND,NEQT,1,1)
      CALL MMOVE(SD,ND,A(N1,N2),ND,NEQT,1,1)
C                  CHANGE FORCE COMPONENTS
      CALL MLTIP(FR,1,D2,NEQT,D,1,NEQT,NEQT)
      IF(IFFRQ)1300,1300,1290
 1290 CALL MLTIP(FI,1,D2,NEQT,D,1,NEQT,NEQT)
      CALL MLTIP(FMR,1,D2,NEQT,D,1,NEQT,NEQT)
      CALL MLTIP(FMI,1,D2,NEQT,D,1,NEQT,NEQT)
C                  WRITE PLOT INFORMATION
 1300 IF(IFPLT)1400,1400,1310
 1310 NFILE=NFN(4)
      REWIND NFILE
      IF(IFDSP)1320,1320,1330
 1320 IFTYP(2)=0
 1330 NFD=IFTYP(1)+IFTYP(2)
      IF(NFD)1400,1400,1340
 1340 NREC(4)=NFD
      IFPLT=IFTYP(1)+2*IFTYP(2)
      NV=-1
      N1=1
      N2=2
      IF(IFTYP(1))1350,1350,1360
 1350 N1=2
 1360 IF(IFTYP(2))1370,1370,1380
 1370 N2=1
 1380 DO 1390 I=N1,N2
 1390 WRITE(NFILE)NV,NPLOT(I),NPVAR(I),(NVPP(J,I),J=1,5),(NCMP(L,I),L=1,
     125)
C                  WRITE TRANSFORMATION RESULTS
 1400 IF(IFPRT)1500,1500,1410
 1410 CALL EBLNK(TITLE,70,IEND)
      WRITE(IOUT,1420)(TITLE(II),II=1,IEND)
 1420 FORMAT( / 10X,70A1)
      IF(IPRNT(1))1435,1435,1425
 1425 WRITE(IOUT,1430)WORD(1)
 1430 FORMAT(//,2X,'MODAL VALUES FOR ',A1,' MATRIX'/)
      WRITE(IOUT,1460)(VM(J),J=1,NEIG)
      WRITE(IOUT,1430)WORD(2)
      WRITE(IOUT,1460)(VC(J),J=1,NEIG)
      WRITE(IOUT,1430)WORD(3)
      WRITE(IOUT,1460)(VK(J),J=1,NEIG)
 1435 IF(IPRNT(2))1500,1500,1438
 1438 IST=1-NEQT
      DO 1450 I=1,NEIG
      CON=VAL(I)/6.2831853
      WRITE(IOUT,1440)CON,VAL(I)
 1440 FORMAT(/2X,'MODE SHAPE AT ',E13.5,' CPS, ',E13.5,' RAD/SEC'/)
      IST=IST+NEQT
      IND=IST+NEQT-1
 1450 WRITE(IOUT,1460)(D2(J),J=IST,IND)
 1460 FORMAT(1X,6E12.3)
C   -------------------------------------------------------------------
C                  COMPUTE DYNAMIC RESPONSE
C   -------------------------------------------------------------------
C                  TIME DOMAIN RESPONSE
C   -------------------------------------------------------------------
 1500 NPRT=TIME(4)
      DT=TIME(3)/TIME(4)
      T=TIME(1)
      IST=1
      Y1=0.
      Y2=0.
      CALL TCOEF(A,ND,VM,VC,VK,NEIG,DT)
      IF(IFEXC)1560,1560,1510
 1510 Y2=0.
      Y4=0.
      VEL=0.
      CALL EXCIT(Y1,Y2,Y3,Y4,VEL,IST,TF,YF,T,0.,IFACC,IFEXC)
      GO TO 1560
 1520 DO 1550 I=1,NEIG
      IF(IFEXC)1530,1530,1540
 1530 D(I)=(A(I,1)*S(I)+A(I,2)*SD(I))*IFIN(I)
      SD(I)=(A(I,5)*S(I)+A(I,6)*SD(I))*IFIN(I)
      GO TO 1550
 1540 P1=FR(I)*Y1+FMR(I)*Y3
      P2=FR(I)*Y2+FMR(I)*Y4
      D(I)=(A(I,1)*S(I)+A(I,2)*SD(I)+A(I,3)*P1+A(I,4)*P2)*IFIN(I)
      SD(I)=(A(I,5)*S(I)+A(I,6)*SD(I)+A(I,7)*P1+A(I,8)*P2)*IFIN(I)
 1550 S(I)=D(I)
      IF(NPRT-NCT)1560,1560,1900
C    ------------------------------------------------------------------
C                  OUTPUT RESULTS
C    ------------------------------------------------------------------
 1560 NCT=0
      IF(IPRNT(3)+IPRNT(4)+IPRNT(5))1590,1590,1570
 1570 WRITE(IOUT,1580)T
 1580 FORMAT(/2X,'TIME =',2X,E12.4)
 1590 IF(IPRNT(3)+IFTYP(1))1700,1700,1600
C                  WRITE DISPLACEMENT COMPONENTS
 1600 CALL MLTPY(D,ND,D2,NEQT,S,ND,NEQT,NEIG,1)
      IF(IFDSP)1645,1645,1610
 1610 JJ=NEQT+1
      J=NEQ+1
      DO 1640 I=1,NEQ
      J=J-1
      IF(IFSP(J))1620,1620,1630
 1620 JJ=JJ-1
      D(J)=D(JJ)
      GO TO 1640
 1630 D(J)=DISP(J)*Y2
 1640 CONTINUE
 1645 IF(IPRNT(3))1670,1670,1650
 1650 WRITE(IOUT,1660)
 1660 FORMAT(/2X,'DISPLACEMENTS')
      WRITE(IOUT,1460)(D(J),J=1,NEQ)
 1670 IF(IFTYP(1))1700,1700,1680
 1680 IND=NPVAR(1)
      DO 1690 I=1,IND
      N1=NVAR(I,1)
 1690 VAL(I)=D(N1)
      WRITE(NFILE)T,(VAL(I),I=1,IND)
      NREC(4)=NREC(4)+1
 1700 IF(IPRNT(4))1800,1800,1710
 1710 CALL MLTPY(D,ND,D2,NEQT,SD,ND,NEQT,NEIG,1)
      WRITE(IOUT,1720)
 1720 FORMAT(/2X,'VELOCITY')
      IF(IFDSP)1760,1760,1725
 1725 JJ=NEQT+1
      J=NEQ+1
      DO 1750 I=1,NEQ
      J=J-1
      IF(IFSP(J))1730,1730,1740
 1730 JJ=JJ-1
      D(J)=D(JJ)
      GO TO 1750
 1740 D(J)=DISP(J)*(Y2-Y1)/DT
 1750 CONTINUE
 1760 WRITE(IOUT,1460)(D(J),J=1,NEQ)
 1800 IF(IPRNT(5))1900,1900,1810
 1810 IF(IFEXC)1820,1820,1830
 1820 P2=0.
 1830 DO 1840 I=1,NEIG
 1840 VAL(I)=(P2-VC(I)*SD(I)-VK(I)*S(I))/VM(I)
      CALL MLTPY(D,ND,D2,NEQT,VAL,ND,NEQT,NEIG,1)
      IF(IFDSP)1875,1875,1845
 1845 JJ=NEQT+1
      J=NEQ+1
      DO 1870 I=1,NEQ
      J=J-1
      IF(IFSP(J))1850,1850,1860
 1850 JJ=JJ-1
      D(J)=D(JJ)
      GO TO 1870
 1860 D(J)=DISP(J)*Y4
 1870 CONTINUE
 1875 WRITE(IOUT,1880)
 1880 FORMAT(/2X,'ACCELERATION')
      WRITE(IOUT,1460)(D(J),J=1,NEQ)
 1900 IF(T+DT-TIME(2))1910,1910,2060
 1910 IF(IFEXC)1930,1930,1920
 1920 CALL EXCIT(Y1,Y2,Y3,Y4,VEL,IST,TF,YF,T,DT,IFACC,IFEXC)
 1930 NCT=NCT+1
      T=T+DT
      GO TO 1520
 2000 WRITE(IOUT,2005)
 2005 FORMAT(/2X,'SOLUTION CANCELLED, TOO MANY EQUATIONS'/)
      GO TO 2050
 2010 CONTINUE
 2020 CONTINUE
 2030 CONTINUE
 2040 CONTINUE
 2050 IFPLT=0
 2060 NEXEC=0
      RETURN
      END
  