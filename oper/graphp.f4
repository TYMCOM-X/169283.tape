C************************************************************
C*
C*
C*
C*
C*
C*             THIS REPRESENTS THE
C*             GRAPHICS ROUTINES
C*             OF THE ADVANCED GRAPHICS
C*             PROGRAMMING PACKAGE..
C*
C*             COPYRIGHT.......
C*
C*             TEKTRONIX INC.
C*             BOX 500
C*             BEAVERTON, OREGON
C*
C*             RELEASE 2
C*             VERSION 1
C*             MODIFICATION 1
C*
C*      FILE 1 OF PRODUCT 062-1439-02    
C*
C*
C*
C*
C**************************************************************
C ROUTINE---AS-0010.05-01   GRAPH
        SUBROUTINE GRAPH(X,Y,DES)
        DIMENSION X(1),Y(1),DES(12,2)
        DATA IIOUT/5/
        CALL CSIZE(ISIZE,IVRT)
        IF(DES(1,1))10,10,11
11      CALL ERASE
10      SCALE=DES(1,2)
        NPNTS=IFIX(DES(2,1))
        XMIN=DES(3,1)
        YMIN=DES(3,2)
        XMAX=DES(4,1)
        YMAX=DES(4,2)
        IXOFF=IFIX(DES(5,1)*SCALE)
        IYOFF=IFIX(DES(5,2)*SCALE)
        LENX=IFIX(DES(6,1)*SCALE)
        LENY=IFIX(DES(6,2)*SCALE)
        XINT=DES(7,1)
        YINT=DES(7,2)
        DRAWX=DES(8,1)
        DRAWY=DES(8,2)
        LABTX=DES(9,1)
        LABTY=DES(9,2)
        WLABX=DES(10,1)
        WLABY=DES(10,2)
        XLOG=DES(11,1)
        YLOG=DES(11,2)
        IOPTX=IFIX(DES(12,1))
        IOPTY=IFIX(DES(12,2))
C       CREATE OPTION NUMBERS
        IF(XLOG)13,13,14
14      IOPTX=IOPTX+256
13      IF(YLOG)15,15,16
16      IOPTY=IOPTY+256
15      IF(WLABX)17,17,18
18      IF(LABTX)19,19,17
17      IOPTX=IOPTX+128
19      IF(WLABY)20,20,21
21      IF(LABTY)22,22,20
20      IOPTY=IOPTY+128
22      IF(DRAWX)23,23,24
23      IOPTX=IOPTX+32
        GO TO 25
24      IOPTX=IOPTX+24
25      IF(DRAWY)27,27,26
27      IOPTY=IOPTY+32
        GO TO 28
26      IOPTY=IOPTY+24
C       CALCULATE WINDOW INFORMATION
28      VLENX=XMAX-XMIN
        VLENY=YMAX-YMIN
        TICX=VLENX/XINT
        TICY=VLENY/YINT
        CALL SWINDO(IXOFF,LENX,IYOFF,LENY)
        CALL VWINDO(XMIN-TICX,VLENX+TICX,YMIN-TICY,VLENY+TICY)
        CALL AXIS(XMIN,YMIN,TICX,TICY,VLENX,VLENY,IOPTX,IOPTY)
C       PUT ON ALPHA LABELS IF NECESSARY
        IF(WLABX)29,29,34
34      IF(LABTX)29,29,30
30      CALL ALAB(X,XMIN,YMIN,IFIX(XINT)+1,VLENX,1)
29      IF(WLABY)31,31,12
12      IF(LABTY)31,31,32
32      CALL ALAB(Y,XMIN,YMIN,IFIX(YINT)+1,VLENY,9)
C       SORT OUT DASHES AND LITERALS FROM THE OTHERS
C********IS "X" NEGATIVE ON YOUR SYSTEM?
31      IF(DES(2,2))33,51,37
33      IF(DES(2,2)+888888)38,38,35
35      LDASH=-IFIX(DES(2,2))
        J=0
        LINE=6
        GO TO 36
38      CHAR=DES(2,2)
        LINE=5
        GO TO 36
37      LINE=IFIX(DES(2,2))
        J=0
C       DRAW THE CURVE
36      DO 50 I=1,NPNTS
C       FIND THE X VALUE FOR THIS TYPE
        IF(XLOG)40,40,41
41      XP=ALOG10(X(I))
        GO TO 42
40      IF(LABTX)43,43,44
44      XP=XMIN+(I-1)*TICX
        GO TO 42
43      XP=X(I)
C       FIND THE Y VALUE FOR THIS TYPE
42      IF(YLOG)45,45,46
46      YP=ALOG10(Y(I))
        GO TO 47
45      IF(LABTY)48,48,49
49      YP=YMIN+(I-1)*TICY
        GO TO 47
48      YP=Y(I)
47      GO TO (1,2,3,4,5,6),LINE
C       DASHED
6       IF(J)67,67,68
67      CALL MOVEA(XP,YP)
        J=1
        GO TO 50
68      CALL DASHA(XP,YP,LDASH)
        GO TO 50
C       LINE
1       CALL PLOTX(J,XP,YP)
        J=1
        GOTO 50
C       TRIANGLE MARKED
2       CALL PLOTX(J,XP,YP)
        CALL DRWREL(-5,5)
        CALL DRWREL(0,-10)
        CALL DRWREL(5,5)
        J=1
        GO TO 50
C       BAR GRAPH
3       XP=XP-TICX/2
        CALL MOVEA(XP,YMIN)
        CALL DRAWA(XP,YP)
        XP=XP+TICX
        CALL DRAWA(XP,YP)
        CALL DRAWA(XP,YMIN)
        GO TO 50
C       LITERAL POINT
5       CALL  MOVEA(XP,YP)
        CALL MOVREL(-ISIZE/2,-IVRT/2)
        CALL ANMODE
        WRITE (IIOUT,9)CHAR
9       FORMAT(1H+,$,A1)
        GO TO 50
C       HORIZONTAL BAR GRAPH
4       YP=YP-TICY/2
        CALL MOVEA(XMIN,YP)
        CALL DRAWA(XP,YP)
        YP=YP+TICY
        CALL DRAWA(XP,YP)
        CALL DRAWA(XMIN,YP)
50      CONTINUE
51      RETURN
        END
C ***************************************************************
C *      PDP-10   SYSTEM DEPENDANT   CODE                  *
C ***************************************************************
C *         FORMAT (1H+,$,A5)                                    *
C *            1H+,$  IS THE TAKE NO ACTION CARRIAGE CONTROL      *
C *            A5   THE PDP-10 STORES 5 CHARACTERS PER WORD        *
C *                 THE FORMAT SHOULD BE CHANGED TO REFLECT      *
C *                  THE WORD SIZE OF THE COMPUTER USED      *
C *      ICHARS            IS ALWAYS INITIALIZED TO THE NUMBER       *
C *                  OF CHARACTERS STORED PER WORD            *
C *     DECODE       IS A FORMATED TRANSFER IN CORE, FORMATING IS      *
C *                  THE SAME AS IF A READ WERE DONE INTO       *
C *                  THE VARIABLES IN THE LIST            *
C ***************************************************************
C ROUTINE---AS-0020.05-01   TITLE
        SUBROUTINE TITLE(X,DES)
        DIMENSION X(1),DES(12,2)
        IF (DES(1,1))10,10,11
11      CALL ERASE
10      SCALE=DES(1,2)
        NCHARS=IFIX(DES(2,1))
        XOFF=DES(5,1)
        YOFF=DES(5,2)
        XLOC=DES(3,1)
        YLOC=DES(3,2)
        IX=IFIX((XOFF+XLOC)*SCALE)
        IY=IFIX((YOFF+YLOC)*SCALE)
        CALL MOVABS(IX,IY)
        IF (DES(2,2))12,50,12
12      IF (DES(4,1))15,15,16
15      CALL HSTRNG(X,NCHARS)
        GO TO 50
16      CALL VSTRNG(X,NCHARS)
50      RETURN
        END
C ROUTINE---AS-0030.05-01   VSTRNG
        SUBROUTINE VSTRNG(STRING,NO)
        DATA IIOUT/5/
C       A VERY SYSTEM DEPENDANT ROUTINE
        INTEGER STRING(10),STR(10)
        DATA ICHARS/5/
        CALL CSIZE(IHOR,IVERT)
        ICOUNT=0
        NW=NO/ICHARS
        IF(NW*ICHARS-NO)4,5,5
4       NW=NW+1
5       K=1
        DO 6 I=K,NW
        DECODE(ICHARS,20,STRING(I))(STR(J),J=1,ICHARS)
20      FORMAT(7A1)
        DO 7 J=1,ICHARS
        CALL ANMODE
        WRITE (IIOUT,10)STR(J)
10      FORMAT(1H+,$,A1)
        ICOUNT=ICOUNT+1
        IF(ICOUNT-NO)1,2,2
1       CALL MOVREL(0,-IVERT)
7       CONTINUE
6       CONTINUE
2       CONTINUE
        END
C ROUTINE---AS-0050.05-01   ALAB
        SUBROUTINE ALAB(LABRY,XMIN,YMIN,NLABS,ALEN,IOPT)
        DIMENSION LABRY(10)
        DATA ICHARS/5/
C       OPTIONS
C       8=YAXIS
C       1-4=1-4 WORD LABELS
        CALL CSIZE(ISIZE,IVRT)
        TIC=ALEN/FLOAT(NLABS-1)
        IF (IOPT-8)1,2,2
C       X AXIS LABELING
1       X=XMIN
        LINT=NLABS*IOPT
        DO 3 I=1,LINT,IOPT
        CALL MOVEA(X,YMIN)
        CALL MOVREL(-2*ISIZE*IOPT,-IVRT/2-IVRT)
        CALL HSTRNG(LABRY(I),IOPT*ICHARS)
3       X=X+TIC
        RETURN
C       Y AXIS LABELING
2       IOPT=IOPT-8
        Y=YMIN
        LINT=NLABS*IOPT
        DO 5 I=1,LINT,IOPT
        CALL MOVEA(XMIN,Y)
        CALL MOVREL(-5*ISIZE*IOPT,0)
        CALL HSTRNG(LABRY(I),IOPT*ICHARS)
5       Y=Y+TIC
        RETURN
        END
C ROUTINE---AS-0040.05-01   HSTRNG
        SUBROUTINE HSTRNG(STRING,NO)
C       A SYSTEM DEPENDANT SUBROUTINE
        DIMENSION STRING(10)
C ***********************************************************
C *    ICHARS IS NUMBER OF CHARS PER WORD FOR MACHINE
C *  IF YOU CHANGE ICHAR YOU MUST CHANGE 3 FORMAT ALSO.
C ************************************************************
        DATA ICHARS/5/
C * IIOUT IS THE TERMINAL UNIT NUMBER
        DATA IIOUT/5/
        NW=NO/ICHARS
        IF(NW*ICHARS-NO)1,2,2
1       NW=NW+1
2       CALL ANMODE
        WRITE (IIOUT,3)(STRING(I),I=1,NW)
C ***** FORMAT A(NO OF CHARS/WORD)*****
3       FORMAT(1H+,$,10A5)
        RETURN
        END
C ROUTINE---AS-0070.05-01   AXIS
        SUBROUTINE AXIS(XMIN,YMIN,TICX,TICY,XLEN,YLEN,ITX,ITY)
        DATA IIOUT/5/
        CALL CSIZE(ISIZE,IVRT)
C * DONT DESTROY WHAT THEY GAVE US
        IOPTY=ITY
        IOPTX=ITX
        LOGX=0
        LOGY=0
        NOLABX=0
        NOLABY=0
C       STARTING PLACE FOR TIC MARKS
        STICX=YMIN
        STICY=XMIN
C       TOTAL LENGTH OF TICMARKS
        TICXL=0
        TICYL=0
C       LENGTH OF A SHORT TICMARK
        TICLX=YLEN/30
        TICLY=XLEN/30
C       DECODE OPTIONS
C       256=LOGARITMIC
C       128=NO LABELS
C       64=GRID OPTION
C       32=DONT DRAW THE AXIS LINE
C       16=TICS ON NEGATIVE SIDE OF AXIS
C       8=TICS ON POSITIVE SIDE OF AXIS
C       1-7=NO OF DECIMAL PLACES IN LABEL
C       X OPTIONS
        IF (IOPTX-256)3,4,4
4       LOGX=1
        IOPTX=IOPTX-256
3       IF (IOPTX-128)5,6,6
6       NOLABX=1
        IOPTX=IOPTX-128
5       IF (IOPTX-64)7,8,8
8       TICXL=TICXL+YLEN
        IOPTX=IOPTX-64
7       IF (IOPTX-32)10,31,31
C       DRAW THE X AXIS LINE
10      CALL MOVEA(XMIN+XLEN,YMIN)
        CALL DRAWA(XMIN,YMIN)
        GOTO 9
31      IOPTX=IOPTX-32
9       IF (IOPTX-16)11,12,12
12      STICX=YMIN-TICLX
        TICXL=TICXL+TICLX
        IOPTX=IOPTX-16
11      IF (IOPTX-8)13,14,14
14      TICXL=TICXL+TICLX
        IOPTX=IOPTX-8
C       Y OPTIONS
13      IF (IOPTY-256)17,18,18
18      LOGY=1
        IOPTY=IOPTY-256
17      IF (IOPTY-128)19,20,20
20      NOLABY=1
        IOPTY=IOPTY-128
19      IF (IOPTY-64)21,22,22
22      TICYL=TICYL+XLEN
        IOPTY=IOPTY-64
21      IF (IOPTY-32)24,29,29
C       DRAW THE Y AXIS LINE
24      CALL MOVEA(XMIN,YMIN+YLEN)
        CALL DRAWA(XMIN,YMIN)
        GO TO 23
29      IOPTY=IOPTY-32
23      IF (IOPTY-16)25,26,26
26      STICY=XMIN-TICLY
        TICYL=TICYL+TICLY
        IOPTY=IOPTY-16
25      IF (IOPTY-8)27,28,28
28      TICYL=TICYL+TICLY
        IOPTY=IOPTY-8
C       DRAW X ACCORDING TO OPTIONS
27      IF(NOLABX)81,81,83
83      IF(TICXL)81,37,81
81      X=XMIN
36      IF(X-XMIN-XLEN)33,33,37
33      IF(LOGX)34,34,30
34      CALL MOVEA(X,STICX)
        IF(TICXL)32,32,15
15      CALL DRAWR(0.,TICXL)
        GO TO 32
C       SPECIAL LONG TIC MARKS FOR LOG TYPE
30      CALL MOVEA(X,STICX-TICLX)
        CALL DRAWR(0.,TICXL+TICLX)
32      IF (NOLABX)40,40,41
C       FIND THE NUMBER TO PRINT
40      IF(LOGX)44,44,43
44      TYPEX=X
        GO TO 42
43      TYPEX=10.**IFIX(X)
C       GET BELOW THE TIC MARKS
42      CALL MOVER(0.,-TICXL-TICLX)
C       FIGURE OUT THE FORMAT AND PRINT THE LABEL
        IF(IOPTX-6)60,60,57
57      CALL MOVREL(-5*ISIZE,-IVRT)
        CALL ANMODE
        WRITE (IIOUT,77)TYPEX
77      FORMAT(1H+,$,E10.4)
        GO TO 41
60      IF(TYPEX)64,63,64
63      IPLACS=1+IOPTX
        GO TO 65
64      IPLACS=(IFIX(ALOG10(ABS(TYPEX)))+1+IOPTX)
65      CALL MOVREL((IPLACS/2-7)*ISIZE,-IVRT)
        CALL ANMODE
        IF(IOPTX-5)61,55,56
56      WRITE (IIOUT,76)TYPEX
76      FORMAT(1H+,$,F8.6)
        GO TO 41
61      IF(IOPTX-3)62,53,54
55      WRITE (IIOUT,75)TYPEX
75      FORMAT(1H+,$,F8.5)
        GO TO 41
54      WRITE (IIOUT,74)TYPEX
74      FORMAT(1H+,$,F8.4)
        GO TO 41
62      IF(IOPTX-1)50,51,52
53      WRITE (IIOUT,73)TYPEX
73      FORMAT(1H+,$,F8.3)
        GO TO 41
52      WRITE (IIOUT,72)TYPEX
72      FORMAT(1H+,$,F8.2)
        GO TO 41
51      WRITE (IIOUT,71)TYPEX
71      FORMAT(1H+,$,F8.1)
        GO TO 41
50      ITYPEX=IFIX(TYPEX)
        WRITE (IIOUT,70)ITYPEX
70      FORMAT(1H+,$,I8)
C       DONE?
41      IF (LOGX)35,35,38
38      IF(TICXL)35,35,16
C * IF ON THE LAST ONE DONT
16      IF(X+TICX-XMIN-XLEN)122,122,37
122     DO 39 J=2,9
C       DRAW THE LOGARITHMIC TIC MARKS
        CALL MOVEA(X+TICX*ALOG10(FLOAT(J)),STICX)
39      CALL DRAWR(0.,TICXL)
35      X=X+TICX
        GO TO 36
C       DRAW Y ACCORDING TO OPTIONS
37      IF(NOLABY)82,82,84
84      IF(TICYL)82,120,82
82      Y=YMIN
115     IF(Y-YMIN-YLEN)112,112,120
112     IF(LOGY)100,100,101
100     CALL MOVEA(STICY,Y)
        IF(TICYL)102,102,103
103     CALL DRAWR(TICYL,0.)
        GO TO 102
C       SPECIAL TIC MARKS FOR LOG TYPE 
101     CALL MOVEA(STICY-TICLY,Y)
        CALL DRAWR(TICYL+TICLY,0.)
102     IF (NOLABY)104,104,141
C       FIGURE OUT THE LABEL
104     IF(LOGY)106,106,107
106     TYPEY=Y
        GO TO 108
107     TYPEY=10.**IFIX(Y)
C       GET TO THE LEFT OF THE TIC MARKS
108     CALL MOVER(-TICYL,0.)
C       FIGURE OUT THE FORMAT AND PRINT THE LABEL
        IF(IOPTY-6)109,109,137
C       AND MOVE OVER MORE FOR THE CHARACTERS
137     CALL MOVREL(-11*ISIZE,0)
        CALL ANMODE
        WRITE (IIOUT,77)TYPEY
        GO TO 141
109     CALL MOVREL(-9*ISIZE,0)
        CALL ANMODE
        IF(IOPTY-5)110,135,136
136     WRITE (IIOUT,76)TYPEY
        GO TO 141
135     WRITE (IIOUT,75)TYPEY
        GOTO 141
110     IF(IOPTY-3)111,133,134
134     WRITE (IIOUT,74)TYPEY
        GO TO 141
133     WRITE (IIOUT,73)TYPEY
        GO TO 141
111     IF(IOPTY-1)130,131,132
132     WRITE (IIOUT,72)TYPEY
        GO TO 141
131     WRITE (IIOUT,71)TYPEY
        GO TO 141
130     ITYPEY=IFIX(TYPEY)
        WRITE (IIOUT,70)ITYPEY
C       DONE?
141     IF(LOGY)113,113,114
114     IF(TICYL)113,113,116
C * IF ON THE LAST ONE DONT
116     IF(Y+TICY-YMIN-YLEN)121,121,120
121     DO 117 J=2,9
C       DRAW THE LOGARITHMIC TIC MARKS ON Y
        CALL MOVEA(STICY,Y+TICY*ALOG10(FLOAT(J)))
117     CALL DRAWR(TICYL,0.)
113     Y=Y+TICY
        GO TO 115
120     RETURN
        END
C ROUTINE---AS-0080.05-01   PLOTX
        SUBROUTINE PLOTX(I,X,Y)
        IF (I)1,2,3
1      CALL POINTA(X,Y)
        GO TO 4
2      CALL MOVEA(X,Y)
        GO TO 4
3      CALL DRAWA(X,Y)
4      RETURN
        END
   