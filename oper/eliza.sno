*
*	ELIZA  IN SNOBOL    BY R.T. DUQUET   SUNYAB, FALL 1969.
*       
*       ANY QUESTIONS INSIDE GRAPHICS TO:  ADRIAN WALKER
*       PHONE 831-1351-EXT 33
*
*
	&ANCHOR = 1  
  	INPUT('INPUT',2,72)
	PRE.TRIM = SPAN(' ') ! NULL
	P.O = *KEY.
	P.1 = PRE.TRIM  BREAK(' ') . WORD. ' '
	P.2 = *LEN(NEWSIZE.) '/'
	P.3 = BREAK(':') . CONTENT. ':'
	P.4 = PRE.TRIM REM . CONTENT.
	P.5 = PRE.TRIM ANY('SLD') . WORD. ' '
	SHORTEN. = BREAK('.,') . PHRASE. ANY('.,') REM . TRAILER.
	X.REF = PRE.TRIM 'CF'
	BUMP. = PRE.TRIM 'NEWKEY'
	PAREN. = PRE.TRIM '/' BREAK('/') . CONTENT. '/'
	STASH. = PRE.TRIM '/' BREAK('/') . *$STORE. '/'
	CALL.TO.SNOBOL = PRE.TRIM 'SNOBOL'
	ATTENTION. = PRE.TRIM '*'
*
	INTRODUCTION = 'HOW DO YOU DO.'
	CLUELESS = '.. VERY INTERESTING'
	RETAIN = 'MY'
*
*  WE NOW READ THE SCRIPT AND FORM STRINGS AS FOLLOWS.
*  FOR EACH KEY WORD 'XXXX' WE FORM THE FOLLOWING VARIABLES:
*	RPL.XXXX	IS A REPLACEMENT WORD.(OPTIONAL)
*	LEV.XXXX	IS A LEVEL NUMBER (IF ABSENT KEY IS IGNORED)
*  	N.XXXXYA COUNT OF THE NUMBER OF DECOMPOSITIONS
*	DEC.I.XXXX	IS THE I'TH DECOMPOSITION PATTERN
*  	RULE.I.XXXX	IS A STRING OF RECOMPOSITION RULES FOR THE I'TH
*			DECOMPOSITION. RULES ARE SEPARATED BY ':'.
*
	OUTPUT = 'ENTER SCRIPT'
	KEYWORDS. = '.'
HIGGINS	SCRIPT. = TRIM(INPUT)		:F(END)
	SCRIPT. ATTENTION. =			:S(FLAG)
	SCRIPT. P.1 =			:F(HIGGINS)
	KEY. = '.'  WORD.
	KEYWORDS. P.O			:S(LESSON)
	KEYWORDS. KEY. KEYWORDS.
LESSON	SCRIPT. P.5 =			:F(HIGGINS)S($WORD.)
ERR	OUTPUT = 'SCRIPT ERROR: ' WORD. ' ' SCRIPT.   :(HIGGINS)
S	STORE. = 'RPL' KEY.
	SCRIPT. STASH. =		:F(ERR)S(LESSON)
L	SCRIPT.  PAREN. =		:F(ERR)
	$('LEV' KEY.) = INTEGER(CONTENT.) CONTENT.	:(LESSON)
D	N.N = $('N' KEY.) + 1
	$('N' KEY.) = N.N
	SCRIPT.  PAREN. =		:F(ERR)
	CONTENT. CALL.TO.SNOBOL =	:S(SPECIAL)
	$('DEC.' N.N KEY.) = ARB CONTENT. REM . POST
RULES	STORE. = 'RULE.' N.N KEY.
	$STORE. = DIFFER(SCRIPT.) SCRIPT.	:F(NEW.CARD)
LOOP	NEWSIZE. = SIZE($STORE.) - 1
	$STORE. P.2				:S(HIGGINS)
NEW.CARD $STORE. TRIM(INPUT)		:F(END)S(LOOP)
*
*  THE FOLLOWING ARE SPECIAL SCRIPT-HANDLING STATEMENTS
*
SPECIAL	$('DEC.' N.N KEY.) = EVAL(CONTENT.)	:(RULES)
ENCODE	SCRIPT. = ' :(HIGGINS) ;'	:[CODE(SCRIPT.)]
FLAG	SCRIPT. CALL.TO.SNOBOL =	:S(ENCODE)
*
*  WE NOW HOLD A CONVERSATION.  FIRST WE READ A SENTENCE AND
*  SEARCH FOR KEY WORDS REPLACING APPROPRIATE ONES
*  AND STACKING THE KEYS IN A QUASI-ORDERED LIST (STRING).
*
INTRO	OUTPUT = INTRODUCTION
HEAR	PHRASE. = TRIM(INPUT) '.'		:F(END)
HEARLESS	PHRASE. SHORTEN.  
	PHRASE. = PHRASE. ' '
	COPY. =  ;   CUES. =   ;   CUE.LEVEL = 0
SPLIT	&ANCHOR = 1 ;  PHRASE. P.1 = 	:F(REPLY)
	&ANCHOR = 0
	KEYWORDS. WORD.			:F(KEEP)
	NEW.WORD = DIFFER($('RPL.' WORD.))
.		$('RPL.' WORD.)			:S(REPLACE)
	COPY. = COPY. WORD. ' '		:(STACK)
REPLACE COPY. = COPY. NEW.WORD ' '
STACK	NEW.LEVEL = DIFFER($('LEV.' WORD.))
.		$('LEV.' WORD.)			:F(SPLIT)
	CUE.LEVEL = GT(NEW.LEVEL,CUE.LEVEL)
.		NEW.LEVEL		:F(LOCUE)
	CUES. = WORD. ':' CUES.			:(SPLIT)
LOCUE	CUES. = CUES. WORD. ':'			:(SPLIT)
KEEP	COPY. = COPY. WORD. ' '	:(SPLIT)
*
*  THIS PART FORMS OUR REPLY TO THE INPUT SENTENCE
*
REPLY	CUES. P.3 =			:F(NOCUE)
NEXTCUE	CUE. = '.' CONTENT.
	N.N = 0 ;  NMAX. = $('N' CUE.)
ANALYZE	N.N = LT(N.N,NMAX.) N.N + 1		:F(NOCUE)
	COPY. $('DEC.' N.N CUE.)		:F(ANALYZE)
	$('RULE.' N.N CUE.) P.3 =
	CONTENT. '/' =
	$('RULE.' N.N CUE.) = $('RULE.' N.N CUE.) CONTENT. ':'
	CONTENT. X.REF =			:S(NEWCUE)
	CONTENT. BUMP.				:S(REPLY)
	OUTPUT = '.. '  EVAL(CONTENT.)
	MEMORY. = IDENT(CUE.,'.' RETAIN)
.		LT(SIZE(MEMORY.),200)
.		MEMORY. COPY. ':'		:(HEAR)
*
*  THIS IS WHAT WE DO IF THERE ARE NO KEYWORDS IN THE INPUT
*
NEWCUE	CONTENT. P.4		:(NEXTCUE)
NOCUE	PHRASE. = DIFFER(TRAILER.) TRAILER.	:S(HEARLESS)
	MEMORY. P.3 = 			:F(ER.AH.UM)
	OUTPUT = '.. EARLIER YOU SAID ' CONTENT.	:(HEAR)
ER.AH.UM	OUTPUT = CLUELESS		:(HEAR)
*
END
*SNOBOL FAM1 = 'MOTHER' ! 'FATHER' ! 'SISTER' ! 'BROTHER' ! 'DAUGHTER'
*SNOBOL FAM2 = 'MOM' ! 'DAD' ! 'WIFE' ! 'CHILDREN' ! 'HUSBAND' ! 'SON'
*SNOBOL FAMILY = (FAM1 ! FAM2) . RELATIVE
*SNOBOL BELIEF = ('FEEL' ! 'THINK' ! 'BELIEVE' ! 'WISH') . PENSE
*SNOBOL HIGH = ('HAPPY' ! 'ELATED' ! 'GLAD' ! 'BETTER' ! 'HIGH') . BIEN
*SNOBOL ICKY = ('SAD' ! 'UNHAPPY' ! 'DEPRESSED' ! 'SICK') . MALADE
*SNOBOL MULTI = ('EVERYONE' ! 'EVERYBODY' ! 'NOBODY' ! 'NOONE') . ALLES
ALWAYS	   L /2/   D //
'CAN YOU THINK OF A SPECIFIC EXAMPLE ?':'WHEN ?':
'WHAT INCIDENT ARE YOU REALLY THINKING OF ?':'REALLY, ALWAYS?':/
AM	   L /2/ S /ARE/ D /ARE YOU/
'DO YOU BELIEVE YOU ARE' POST '?':
'WOULD YOU WANT TO BE' POST '?':
'YOU WISH I WOULD SAY YOU ARE' POST '?':CF WHAT:/
AM		D //
'WHY DO YOU SAY -AM- ?':'I DONT UNDERSTAND THAT':/
ALIKE  L /11/	D //CF DIT:/
ARE	L /2/ D /ARE I/
'WHY ARE YOU INTERESTED IN WHETHER I AM' POST 'OR NOT ?':
'WOULD YOU PREFER IT IF I WERENT' POST '?':
'PERHAPS I AM' POST 'IN YOUR DREAMS':
'DO YOU SOMETIMES THINK I AM' POST '?':?CF WHAT:/
ARE		D /ARE/
'DID YOU THINK THEY MIGHT NOT BE' POST '?':
'WOULD YOU LIKE IT IF THEY WERE NOT' POST '?':
'WHAT IF THEY WERE NOT' POST '?':'POSSIBLY THEY ARE' POST:/
AUTRE	L /1/   D //
   'PLEASE TELL ME ABOUT YOURSELF RATHER THAN ABOUT OTHERS':
'I AM MORE INTERESTED IN YOU THAN IN OTHERS.':
'DOES TALKING ABOUT OTHERS HELP YOU TO SOLVE YOUR PROBLEM?':/
BECAUSE	  L /2/   D //
   'IS THAT THE REAL REASON?':'DONT ANY OTHER QUESTIONS COME TO MIND?':
'DOES THAT REASON SEEM TO EXPLAIN ANYTHING ELSE?':
'WHAT OTHER REASONS MIGHT THERE BE?':/
CAN		L /2/ D //OAN I/
	'YOU BELIEVE I CAN' POST 'DONT YOU?':
'DO YOU WANT ME TO BE ABLE TO' POST '?':
 CF WHAT:'PERHAPS YOU WOULD LIKE TO BE ABLE TO' POST 'YOURSELF':/
CAN		D /CAN YOU/
'DO YOU WANT TO BE ABLE TO' POST '?':
'PERHAPS YOU DONT WANT TO' POST:
  'WHETHER OR NOT YOU CAN' POST 'DEPENDS ON YOU, NOT ME';/
CAN'T S /CANT/
CERTAINLY  L /2/    D //CF YES:/
COMPUTER   L /10/   D //
     'DO COMPUTERS WORRY YOU?':'WHY DO YOU MENTION COMPUTERS?':
'WHAT DO YOU THINK MACHIINES HAVE TO DO WITH YOUR PROBLEM':
'DONT YOU THINK COMPUTERS CAN HELP PEOPLE':
'WHAT IS IT ABOUT MACHINES THAT WORRIES YOU':
'WHAT DO YOU THINK ABOUT MACHINES?':/
COMPUTERS   L /10/ D //CF COMPUTER:/
DIT		     D //
    'IN WHAT WAY?':'WHAT RESEMBLANCE DO YOU SEE?':
'WHAT DOES THAT SIMILARITY SUGGEST TO YOU?:
'WHAT CONNECTIONS DO YOU SEE?':
'IS THAT THE REAL CONNECTION?':
'WHAT DO YOU SUPPOSE THAT RESEMBLANCE MEANS?':
'WHAT IS THE CONNECTION DO YOU SUPPOSE?':
'IS THERE REALLY SOME CONNECTION?':'HOW?':/
DON'T S /DONT/
DREAM	   L /4/   D //
     WHAT DOES THAT DREAM SUGGEST TO YOU?':'DO YOU DREAM OFTEN?':
'WHAT PERSONS APPEAR IN YOUR DREAMS?':
'DONT YOU THINK THAT DREAM IS RELATED TO YOUR PROBLEM?':NEWKEY:/
DREAMED L /5/ S /DREAMT/  D //CF DREAMT:/
DREAMS     L /4/ S /DREAM/  D //CF DREAM:/
DREAMT     L /5/ D /YOU DREAMT/
    'REALLY, '  POST '?':
'HAVE YOU EVER FANTASIED' POST 'WHILE YOU WERE AWAKE?':CF DREAM:/
EVERYBODY  L /3/   D //CF EVERYONE:/
EVERYONE   L /3/ D /SNOBOL ARB MULTI REM . POST/
     REALLY, ' ALLES '?':'SURELY NOT ' ALLES '!':'WHO FOR EXAMPLE?':
'CAN YOU THINK OF ANYONE IN PARTICULAR?':
'YOU ARE THINKING OF A VERY SPECIAL PERSON!':
'WHO MAY I ASK?':'SOMEONE SPECIAL PERHAPS?':
'YOU HAVE A PARTICULAR PERSON IN MIND DONT YOU ?':
'WHO DO YO THINK YO ARE TALKING ABOUT?':/
HE		L /1/   D //CF AUTRE:/
HOW		L /2/   D //CF WHAT:/
I		L /2/   /YOU/ D /YOU WAS/CF WAS:/
I	     D  /SNOBOL ARB 'YOU ' ('WANT ' ! 'NEED ') REM . POST/
    'WHAT WOULD IT MEAN TO YOU IF YOU GOT ' POST '?':
'WHY DO YOU WANT ' POST '?':'SUPPOSE YOU GOT ' POST 'SOON?':
'WHAT IF YOU NEVER GOT ' POST '?':'WHAT WOULD GETTING ' POST 'MEAN':
'WHAT IF YOU NEVER GOT ' POST '?':'WHAT WOULD GETTING ' POST 'MEAN':
'WHAT DOES WANTING' POST 'HAVE TO DO WITH THIS DISCUSSION?':/
I		D /SNOBOL ARB 'YOU ARE ' ARB ICKY/
     'I AM SORRY TO HEAR YOU ARE ' MALADE:
'DO YOU THINK THAT COMINGG HERE WILL HELP YOU NOT TO BE ' MALADE '?':
'I AM NOT SURE IT IS PLEASANT TO BE ' MALADE:
'CAN YOU EXPLAIN TO ME WHAT MADE YOU ' MALADE '?':/
I		D /SNOBOL ARB 'YOU ARE ' ARB HIGH/
    'HOW HAVE I HELPED YOU TO BE ' BIEN '?':
'HAS YOUR TREATMENT MADE YOU ' BIEN '?':
'WHAT MAKES YOU ' BIEN ' JUST NOW?':
'CAN YOU EXPLAIN WHY YOU ARE SUDDENLY ' BIEN '?':/
I		D /SNOBOL ARB 'YOU ' BELIEF ' YOU' REM . POST/
     'DO YOU REALLY THINK YOU' POST '?':
'BUT YOU ARE NOT SURE YOU' POST '?':'DO YOU REALLY DOUBT YOU' POST '?':/
I		D /YOU ARE/
'IS IT BECAUSE YOU ARE' POST 'THAT YOU CAME TO ME ?':
'HOW LONG HAVE YOU BEEN' POST '?':'DO YOU ENJOY BEING' POST '?':
'DO YOU BELIEVE IT NORMAL TO BE' POST '?':/
I		D /SNOBOL ARB 'YOU ' ('CANNOT' ! 'CANT') REM . POST/
    'HOW DO YOU KNOW YOU CANT' POST '?':'HAVE YOU TRIED TO' POST '?':
'PERHAPS YOU COULD' POST 'NOW?':
'DO YOU REALLY WANT TO BE ABLE TO' POST '?':/
I		D /YOU DONT/
'DONT YOU REALLY' POST '?':
'WHY DONT YOU' POST '?':'DO YOU WISH TO BE ABLE TO' POST '?':
'DOES THAT TROUBLE YOU ?':/
I		D /YOU FEEL/
'TELL ME MORE ABOUT SUCH FEELINGS.':
'DO YOU OFTEN FEEL' POST '?':'DO YOU ENJOY FEELING' POST '?':
'WHAT DOES FEELING' POST 'REMIND YOU OF ?':/
I		D /SNOBOL ARB 'YOU' ARB . POST 'I '/
    'PERHAPS IN YOUR FANTASY WE' POST 'EACH OTHER?':
'DO YOU WISH TO' POST 'ME?':'YOU SEEM TO NEED TO' POST ' ME.':
'DO YOU WISH TO' POST 'ANYONE ELSE ?':/
I		D //
'YOU SAY ' COPY '?':'CAN YOU ELABORATE ON THAT?':
'DO YOU SAY ' COPY ' FOR SOME SPECIAL REASON?':/
I'M		L /2/ S /YOU ARE/ D //CF I:/
LIKE L/11/
LIKE D /SNOBOL ARB (' AM ' ! ' IS ' ! ' ARE ' ! ' WAS ' ) ARB 'LIKE'/
CF DIT:/
LIKE		D //NEWKEY:/
MACHINE	  L /10/  D//CF COMPUTER:/
MACHINES  L /10/  D//CF COMPUTER:/
MAYBE	  L /2/   D //CF PERHAPS:/
ME   S /YOU/
MY		L /3/ S /YOUR/ D /SNOBOL ARB 'YOUR ' ARB FAMILY REM . POST/
   'TELL ME ABOUT YOUR FAMILY':'WHO ELSE IN YOUR FAMILY' POST '?':
'YOUR ' RELATIVE '?':
'WHAT ELSE COMES TO MIND WHEN YOU THINK OF YOUR ' RELATIVE '?':/
MY		D /YOUR/'YOUR' POST '?':
 RELATIVE '?':/D /YOUR/    'YOUR' POST '?':
'WHY DO YOU SAY YOUR' POST '?':'IS YOUR' POST ' IMPORTANT TO YOU?':
'DOES THAT SUGGEST ANYTHING ELSE THAT BELONGS TO YOU?';/
MYSELF  S /YOURSELF/
NAME     L /15/  D //
   'I AM NOT INTERESTED IN NAMES':'IVE TOLD YOU BEFORE, I AM NOT
 INTERESTED IN NAMES - PLEASE CONTINUE':/
NO	L /2/   D //
'WHY -NO- ?':'WHY NOT ?':
'ARE YOU SAYING NO JUST TO BE NEGATIVE?':
'YOU ARE BEING RATHER NEGATIVE!':/
NOBODY    L /3/   D //CF EVERYONE:/
NOONE	L /3/   D //CF EVERYONE:/
PERHAPS	  L /2/   D //
    'YOU DONT SEEM TO BE QUITE CERTAIN.':'WHY THE UNCERTAIN TONE?':
'CANT YOU BE MORE DEFINITE?':'YOU ARENT SURE?':'DONT YOU KNOW?':/
REMEMBER   L /6/ D /YOU REMEMBER/
    'DO YOU OFTEN THINK OF' POST '?':
'DOES THINKING OF' POST ' BRING ANYTHING ELSE TO MIND?':
'WHAT ELSE DO YOU REMEMBER?':'WHY DO YOU REMEMBER' POST ' JUST NOW?':
'WHAT IN THE PRESENT SITUATION REMINDS YOU OF' POST '?':
'IS THERE A CONNECTION BETWEEN ME AND' POST '?':/
REMEMBER		D /DO I REMEMBER/
'DID YOU THINK I WOULD FORGET' POST '?':
'WHY DO YOU THINK I SHOULD RECALL' POST ' JUST NOW?':
'WHAT ABOUT' POST '?':CF WHAT:'YOU MENTIONED' POST '!':/
REMEMBER		D //NEWKEY:/
SAME	L /11/	D //CF DIT:/
SHE	L /1/	D //CF AUTRE:/
SORRY	L /2/	D //
'PLEASE DO NOT FEEL APOLOGETIC.':'APOLOGIES ARE NOT NECESSARY.':
'WHAT FEELINGS DO YOU HAVE WHEN YOU APOLOGIZE?':
'IVE TOLD YOU THAT APOLOGIES ARE NOT REQUIRED!':/
WAS	L /3/ D /WAS YOU/
     'WHAT IF YOU WERE' POST '?':'DO YOU THINK YOU WERE' POST '?':
'WERE YOU' POST '?':'WHAT WOULD IT MEAN IF YOU WERE' POST '?':
'WHAT DOES' POST ' SUGGEST TO YOU?':CF WHAT:/
WAS		D /YOU WAS/	'WERE YOU REALL!':
'WHY DO YOU TELL ME YOU WERE' POST ' NOW?':
'PERHAPS I ALREADY KNEW YOU WERE' POST '>':/
WAS		D /WAS I/
'WOULD YOU LIKE TO BELIEVE I WAS' POST '?':
'WHAT IF I HAD BEEN' POST '?':'WHAT SUGGESTS THAT I WAS' POST '?':
'WHAT DO YOU THIINK?':'PERHAPS I WAS' POST '!':/
WERE		L /3/ S /WAS/  D //CF WAS:/
WHAT		L /2/   D //
    'WHY DO YOU ASK THAT?':'DOES THAT QUESTION INTEREST YOU?':
'WHAT IS IT YOU REALLY WANT TO KNOW?':'WHAT DO YOU THINK?':
'ARE SUCH QUESTIONS MUCH ON YOUR MIND?':
'WHAT ANSWER WOULD PLEASE YOU MOST?':'HAVE YOU ASKED ANYONE ELSE?':
'WHAT COMES TO MIND WHEN YOU ASK THAT?':
'HAVE YOU ASKED SUCH QUESTIONS BEFORE?':/
WHEN		L /2/   D //CF WHAT:/
WHY		L /2/   D /WHY DONT I/
'DO YOU BELIEVE THAT I DONT' POST '?':
'PERHAPS I WILL' POST ' IN GOOD TIME!':'SHOULD YOU' POST ' YOURSELF?':
'YOU WANT ME TO' POST ' DO YOU?':CF WHAT:/
WHY		D /WHY CANT YOU/
'DO YOU THINK YOU SHOULD BE ABLE TO' POST '?':
'DO YOU WANT TO BE ABLE TO' POST '?':
'DO YOU BELIEVE OUR CONVERSATION WILL HELP YOU TO' POST '?':
'HAVE YOU ANY IDEA WHY YOU CANT' POST '?':CF WHAT:/
WHY		D //CF WHAT:/
WON'T		S /WONT/
YES		L /2/   D //
   'YOU SEEM QUITE POSITIVE!':'ARE YOU SURE?':'I SEE!':
'I UNDERSTAND.':/
YOU		L /2/ S /I/   D /I ARE/
'WHAT MAKES YOU THINK I AM' POST '?':
'DOES IT PLEASE YOU TO BELIEVE I AM' POST '?':
'DO YOU SOMETIMES WISH THAT YOU WERE' POST '?':
'PERHAPS YOU WOULD LIKE TO BE' POST 'YOURSELF?':/
YOU	D /SNOBOL ARB 'I ' ARB . POST 'YOU'/
   'WHY DO YOU THINK I ' POST 'YOU?':
'DO YOU LIKE TO THINK I ' POST 'YOU?':
'WHAT MAKES YOU THINK I ' POST 'YOU?':
'REALLY? I ' POST 'YOU?':'DO YOU WISH TO BELIEVE I ' POST 'YOU?':
'SUPPOSE I DID ' POST 'YOU. WHAT WOULD THAT MEAN?':
'DOES ANYONE ELSE THINK I ' POST 'YOU?':/
YOU		D /I/
'WE WERE DISCUSSING YOU - NOT ME !':'OH! I' POST '?':
'ARE YOU REALLY TALKING ABOUT ME ?':/
YOUR	  L /2/ S /MY/  D /MY/
'WHY ARE YOU CONCERNED OVER MY' POST '?':
'WHAT ABOUT YOUR OWN' POST '?':
'ARE YOU WORRIED ABOUT SOMEONE ELSES' POST '?':/
YOURSELF    S /MYSELF/
*SNOBOL  INTRODUCTION = 'HOW DO YOU DO. PLEASE TELL ME YOUR PROBLEM.'
*    END OF SCRIPT
                                                                                                                                                                                                                                                                               