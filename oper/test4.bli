MODULE TEST4(STACK) =

BEGIN

MACRO OUTCHAR(S) =
   BEGIN
      MACHOP TTCALL = #51;
      TTCALL(1,S)
   END$;

STRUCTURE ARY2[I,J] = [I*J](.ARY2+(.I-1)*J+(.J-1));

OWN NUMBER,
    ROW,
    A,
    B,
    J,
    REMAIN,
    COUNT,
    ARY2 ARRAY[73,72];


!PROGRAM START

!FILL ARRAY WITH BLANKS

INCR ROW FROM 1 TO 73
   DO INCR COLUMN FROM 1 TO 72
         DO ARRAY[.ROW,.COLUMN] _ #40;

!INSERT ROW HEADERS INTO ARRAY

DECR ROW FROM 73 TO 1 BY 8
   DO INCR NUMBER FROM 1000 TO 9000 BY 1000
         DO DECR COLUMN FROM 5 TO 1
               DO IF .COLUMN EQL 2
                     THEN ARRAY[.ROW,.COLUMN] _ "."
                  ELSE BEGIN
                          REMAIN _ .NUMBER MOD 10 + "0";
                          NUMBER _ .NUMBER/10;
                          ARRAY[.ROW,.COLUMN] _ .REMAIN
                       END;

!INSERT DATA INTO ARRAY

J _9;
COUNT _ 0;
DECR I FROM 73 TO 1
   DO BEGIN
         COUNT _ .COUNT + 125;
         IF (.COUNT GEQ 5250) AND (.COUNT LEQ 6875)
            THEN BEGIN
                    ARRAY[.I,.J] _ "*"
                  END;
          IF .COUNT EQL 6375
             THEN BEGIN
                     ARRAY[.I,10] _ "*";
                     ARRAY[.I,11] _ "*"
                  END;
      END;

!INSERT CARRAIGE RETURNS AND LINE FEEDS INTO ARRAY

INCR I FROM 1 TO 73
   DO BEGIN LABEL PLACECRCF;
      PLACECRCF:
           DECR COLUMN FROM 72 TO 1
              DO BEGIN
                 IF .COLUMN EQL 1 OR .ARRAY[.I,.COLUMN] NEQ #40
                 THEN BEGIN
                         ARRAY[.I,.COLUMN+1] _ #15;
                         ARRAY[.I,.COLUMN+2] _ #12;
                         LEAVE PLACECRCF
                      END;
                 END;
      END;

!PRINT PAGE FROM ARRAY

INCR I FROM 1 TO 73
   DO BEGIN LABEL LINEPRINT;
      LINEPRINT:
         INCR J FROM 1 TO 72
            DO BEGIN
                  NUMBER _ .ARRAY[.I,.J];
                  OUTCHAR(NUMBER);
                  IF .ARRAY[.I,.J] EQL #12
                     THEN LEAVE LINEPRINT
               END;
      END;

END

ELUDOM
