C     PROGRAM PLSIM( INPUT,OUTPUT,TAPE5=INPUT,TAPE6=OUTPUT)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 001
C         THIS IS THE MAIN PROGRAM TO SIMULATE THE UNSTEADY STATE
C         THERMAL BEHAVIOR OF AN INSULATED AND BURIED PIPELINE SYSTEM.
C
C         INITIAL AUTHORS ARE REX L. ROWELL AND BEN L. MALEK OF
C         PETROCHEM CONSULTANTS, INC.-HOUSTON,TEXAS.
C
C   PROGRAM                  SEQUENCE
C    NUMBER    NAME           NUMBER
C   *********  ****         **********
C      001    PLSIM          00000010
C      002    MP2            00001130
C      003    MP3            00001310
C      004    MP4            00002230
C      005    ZEROV          00002660
C      006    READX          00002820
C      007    WTITL          00003220
C      008    TESTA          00003450
C      009    MOVE           00003570
C      010    SETUP          00003710
C      011    FITKC          00004950
C      012    XTPL           00005120
C      013    EQFLO          00005520
C      014    REYNO          00006820
C      015    RFILM          00007030
C      016    PRNO           00007430
C      017    RFOST          00007690
C      018    RINSO          00007900
C      019    FDELP          00008180
C      020    MOVER          00008570
C      021    DYFLO          00008750
C      022    TMATX          00009730
C      023    TPROF          00002180
C      024    TPIPE          00003170
C      025    TOIL           00003440
C      026    INTFAC         00003690
C      027    HTBAL          00004900
C      028    NOFLO          00005350
C      029    COOLR          00006360
C      030    OUTTP          00006860
C      031    OUTTM          00007980
C      032    TMHDR          00008550
C      033    PAGEX          00008820
C      034    PRNTV          00009140
C      035    PRNTM          00009560
C      036    DEFINE         NONE
C
C
C         CLEAR COMMON STATEMENT, IN TOTAL
    1 CALL ZEROV(TITLE(1),14196)
C         ASSIGN INPUT AND OUTPUT DEVICES
       M1 = 1
      OPEN (M1,'PLSIM.DAT',INPUT)
       M2 = 2
      OPEN (M2,'PLSIM.OUT',OUTPUT)
C         DEFINE T MATRIX BOUNDARIES
      NTR=22
      NTC=501
C         DEFINE MAX NO. OF OIL BATCHES AND P.L. SEGMENTS
      MNB=10
      MNP=20
C         NOTE TO REVISING PROGRAMMER-WHEN DIMENSIONED VECTORS OR
C         MATRICIES ARE CHANGED IN SIZE, DONOT FORGET TO CHANGE
C         ST. NO. 1 ABOVE.  ALSO, PLEASE CORRECT VECTOR LENGTHS
C         USED BY SR PAGEX WHEN IT CALLS PRNTV OR PRNTM ROUTINES.
C
C         READ THE 1ST PCA CARD
      READ (M1,900) (PCA(K),K=1,7),IDR,(TITLE(L),L=21,22)
  900 FORMAT(1H F9.3,6(F10.4),I2,2(A4))
C         TEST LISTING SWITCH
      IF(PCA(6)) 3,3,2
C         WRITE A HEADER CARD
    2 WRITE(M2,901)
  901 FORMAT(1H1 48H**** A LISTING OF PROGRAM CONSTANTS FOLLOWS ****)
      WRITE (M2,900) (PCA(K),K=1,7),IDR,(TITLE(L),L=21,22)
C         READ REMAINDER OF PCA DATA
    3 CALL READX(PCA(8),21,2,PCA(6))
C         READ PCB DATA (OIL BATCH PROPERTIES), 7/CARD.  USE PCA(27)
C         TO DETM LENGTH OF PCB TO BE ENTERED.
C         SET UP DO LOOP FOR READING COLUMNS INTO THE PCB MATRIX
      NBAT= PCA(27) + 0.001
      IA=0
      IDE=0
      DO 301 I=1,NBAT
      IDE = I  + IA
      CALL READX(PCB(1,I),21,IDE,PCA(6))
      IA = IA + 2
  301 CONTINUE
C         READ PCC DATA (PIPELINE SEGMENT DATA), 7/CARD. USE PCA(28)
C         TO DETM LENGTH OF PCC TO BE ENTERED.
      NSEG=PCA(28) + 0.001
      IA = 0
      IDE=0
      DO 302 I=1,NSEG
      IDE = I  + IA
      CALL READX(PCC(1,I),28,IDE,PCA(6))
      IA = IA + 3
  302 CONTINUE
C         READ BLANK CARD THAT FOLLOWS THE LAST PROGRAM CONSTANT CARD
      READ (M1,902)
  902 FORMAT(1H )
C         NOW WRITE IT IF PC-LIST SWITCH IS ON.
      IF(PCA(6))5,5,4
    4 WRITE(M2,902)
    5 CONTINUE
      CALL DEFINE
C         TRANSFER CONTROL TO MP2 FOR THE REMAINDER OF THIS COMP. ACCESS
      CALL MP2
      END
      SUBROUTINE MP2
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 002
C         CALLED BY MAIN PROGRAM AND, UPON ENGR FAILURE, BY SR,S-
C
C         THIS SR IS THE =RUN= EXECUTIVE.  IT PROCESSES AS MANY CASES
C         AS ARE READ IN, STOPPING ONLY WHEN SR MP3(INPUT) FINDS A STOP
C         CARD.
C
C         PROCESS INPUT FOR THE NEXT CASE
    1 CALL MP3
C         PERFORM ENGINEERING CALC,S--WRITING OUTPUT AS REQ,D.
    2 CALL MP4
C         NEXT CASE PLEASE. NOTE LACK OF RETURN STATEMENT.
    3 GO TO 1
      END
      SUBROUTINE MP3
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 003
C         CALLED BY MP2.
C         THIS SR READS CASE STUDY INPUT, AND LISTS IT WHEN REQ,D.
C
C         CLEAR ALL STORAGE FROM PREVIOUS RUN, EXCEPT FOR CERTAIN PC
C         VALUE READ IN JUST ONCE BEFORE CASE 1, AND EXCEPTING=T=AND=IT=
      DO 101 I=1,MNB
  101 CALL ZEROV(PCB(22,I),29)
      DO 102 J=1,MNP
  102 CALL ZEROV(PCC(29,J),21)
      CALL ZEROV(D(1),300)
      CALL ZEROV(Z(1),300)
      I=NTR*NTC
      CALL ZEROV(T(1,1),I)
      DO 1 J=1,NTC
      IT(1,J)=0
    1 IT(2,J)=0
C         PREPARE TO LIST INPUT AS IT,S READ, UNLESS PCA(7) IS =OFF=.
      IF(PCA(7) ) 3,3,2
    2 WRITE(M2,900)
  900 FORMAT(1H1 41H**** A LISTING OF CASE INPUT FOLLOWS ****)
C         READ 1ST TITLE CARD, TEST FOR =STOP= MESSAGE AND TEST ID
    3 IDE=1
      READ(M1,901) (TITLE(K),K=1,10),IDR,(TITLE(J),J=21,22)
  901 FORMAT(1H 10(A4),29X,I2,2(A4))
C         LIST IT, IF DESIRED BY USER
      IF(PCA(7))5,5,4
    4 WRITE(M2,901)(TITLE(K),K=1,10),IDR,(TITLE(J),J=21,22)
C         TEST FOR STOP CARD, WHICH WOULD TERMINATE THIS RUN
    5 CALL TESTA(TITLE(1),4HSTOP,JDIFF)
C         TEST =WORD READ= VS THE WORD =STOP= FOR A ZERO DIFFERENCE
      IF(JDIFF)7,6,7
C         SHUT IT ALL DOWN
    6 STOP
C         CARRY ON, AND TEST CARD ID FOR LEGALITY
    7 IF(IDE-IDR)8,9,8
C         INVALID ID, COMPLAIN LOUDLY
    8 CALL WTITL
      WRITE(M2,902) IDE, IDR
  902 FORMAT(1H1 40HINPUT CARDS OUT OF SEQUENCE.  WHEN CARD ,I3,14H WAS
     1EXPECTED,// 6H CARD ,I3,34H WAS RECEIVED.  RUN WILL CONTINUE.)
C         READ AND TEST ID ON SECOND TITLE CARD
    9 IDE=2
      READ(M1,901) (TITLE(K),K=11,20),IDR,(TITLE(J),J=21,22)
      IF(PCA(7))11,11,10
C         LIST IT
   10 WRITE(M2,901)(TITLE(K),K=11,20),IDR,(TITLE(J),J=21,22)
C         TEST ID
   11 IF(IDE-IDR)12,13,12
C         INVALID ID
   12 CALL WTITL
      WRITE(M2,902) IDE,IDR
C         READ TWO CONTROL CARDS (VECTOR STORAGE RESERVES ROOM FOR 7)
   13 CALL READX(D(1),14,3,PCA(7))
C         READ OIL BATCH CARDS (VARIABLE NO OF BATCHES, VARIABLE NO. OF
C         CARDS PER BATCH).
      NBAT= D(2) + 0.001
      IDE=10
      DO 17 I=1,NBAT
      IDE=IDE + 1
      CALL READX(D(51),7,IDE,PCA(7))
C         TRANSFER THIS OIL BATCH FLOW DATA TO PCB MATRIX
      D(I+60)=D(51)
      J=D(51)+ 0.001
      CALL MOVE(D(51),PCB(31,J),7)
C         SEE IF PROPS CARDS ARE NEXT
      IF(D(57))16,17,16
C         READ 3 PROP CARDS DIRECTLY INTO PCB MATRIX
   16 IDE=IDE+1
      CALL READX(PCB(1,J),21,IDE,PCA(7))
C         UPDATE ID FOR DO-LOOP
      IDE=IDE+2
   17 CONTINUE
C         NOW, TEST IF TEMP PROFILES FOR OIL AND STEEL ARE TO BE READ
      IF(D(3))18,19,18
C         READ 4 OIL TEMP CARDS AND 4 STEEL TEMP CARDS
   18 CALL READX(D(101),28,1,PCA(7))
      CALL READX(D(201),28,5,PCA(7))
C         FINALLY, READ A BLANK CARD TO FINISH THIS DATA SET.
   19 READ (M1,903)
  903 FORMAT(1H )
C         IF PCA(7) IS ON, LIST THE BLANK CARD
      IF(PCA(7))20,21,20
   20 WRITE(M2,903)
   21 CONTINUE
      RETURN
      END
      SUBROUTINE MP4
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 004
C         CALLED BY SR MP2
C         THIS SR IS THE ENGINEERING EXECUTIVE ROUTINE FOR A SINGLE
C         CASE STUDY INVOLVING ONE OR MORE OIL BATCHES BEING PUMPED
C         THROUGH ONE OR MORE PIPELINE SEGMENTS.
C
C         SET-UP INPUT AS REQ,D (TEMP PROFILES, PL SEGMENTS, ETC)
    1 CALL SETUP
C         TEST FOR FLOW DIRECTION
      IF(D(1)-1.0)2,2,3
C         CASE TYPE 1.0, SO USE FIRST BATCH TO STABILIZE FLOWING TEMPS
    2 I=D(61) + 0.001
      CALL EQFLO(I)
      K=0
      GO TO 4
C         PRINT PROFILE, BE IT  EQUIL. OR INPUT
    3 K=1
    4 CALL OUTTP(K)
C         SAVE INITIAL OIL TEMP AT TERMINUS FOR USE BY SR OUTTM
      Z(250)=T(1,NDX+1)
      Z(251)=0.0
      Z(253)=T(12,NDX+1)
C         SET UP THERMAL LOOP FOR 1-10 OIL BATCHES
      NTBAT=D(2) + 0.001
      DO 5 K=1,NTBAT
      I=D(K+60)+.001
C         PERFORM CALCS AND WRITE OUTPUT FOR FLOWING DYNAMIC STATE
      CALL DYFLO(I)
C         PERFORM CALCS AND WRITE OUTPUT FOR NO-FLOW DYNAMIC STATE
      CALL NOFLO(I)
    5 CONTINUE
C         SEE IF A VECTOR DUMP IS DESIRED
      IF(D(7))7,7,6
C         WRITE A DUMP OF THE DIMENSIONED VECTORS
    6 CALL PAGEX
    7 CONTINUE
C         RETURN CONTROL TO MP2 FOR NEXT CASE STUDY
      RETURN
      END
      SUBROUTINE ZEROV(REGV,LNGTH)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 005
      DIMENSION REGV(2)
C         CALLED BY MAIN PROGRAM,EQFLO
C         THIS SR SETS A REGION IN A GIVEN VECTOR TO ZERO.
C         ARGUMENTS-REGV=THE 1ST POSITION OF A REGION TO BE ZEROED.
C                  LNGTH=THE LENGTH TO BE CLEARED
C
C         HAVING SETUP DUMMY DIMENSIONING, EXECUTE THE ZEROING
    1 DO 2 I=1,LNGTH
    2 REGV(I)=0.0
      RETURN
      END
      SUBROUTINE READX(REGV,LNGTH,IDC,OUTKY)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 006
      DIMENSION REGV(2)
C         CALLED BY MAIN PROGRAM, MP3
C         THIS SR READS FLOATING POINT DATA, 7/CARD, PLUS AN ID/COMMENT.
C         THE STANDARD FORMAT IS (1H F9.4,6(F10.3),I2,2(A4)).
C         ARGUMENTS-REGV=1ST LOCATION TO BE READ
C                  LNGTH=LENGTH OF VECTOR TO BE READ.
C                    IDC=ID NO. OF 1ST CARD TO BE READ, WITH ALL FOLLOW-
C                        ING CARDS READ TO BE INCREMENT BY ONE.
C                  OUTKY=OUTPUT KEY, IF NON-ZERO, LIST ALL INPUT
    1 IF(LNGTH)10,10,101
  101 IDE=IDC-1
C         READ INPUT AS REQUIRED
    2 DO 7 I=1,LNGTH,7
      IDE=IDE + 1
      J=I + 6
    3 READ(M1,900) (REGV(K),K=I,J),IDR,(TITLE(L),L=21,22)
  900 FORMAT(1H F9.4,6(F10.3),I2,2(A4))
C         SEE IF LISTING IS DESIRED
      IF(OUTKY)5,5,4
C         LIST IT
    4 WRITE(M2,900)(REGV(K),K=I,J),IDR,(TITLE(L),L=21,22)
C         TEST CARD ID FOR LEGALITY
    5 IF(IDE-IDR)6,7,6
C         CARDS OUT OF ORDER, SAY SO AND CONTINUE THE RUN
    6 CALL WTITL
      WRITE (M2,901) IDE,IDR
  901 FORMAT(1H 40HINPUT CARDS OUT OF SEQUENCE.  WHEN CARD ,I3,14H WAS E
     1XPECTED,// 6H CARD ,I3,34H WAS RECEIVED.  RUN WILL CONTINUE.)
C         SKIP A LINE
      WRITE (M2,902)
  902 FORMAT(1H )
C         CONTINUE THE READ LOOP
    7 CONTINUE
   10 RETURN
      END
      SUBROUTINE WTITL
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 007
C         CALLED BY MP3,READX,PAGEX,OUTTP,TMHDR,TPROF
C         THIS SR SKIPS TO TOP OF PAGE, WRITES A HEADER, SKIPS A LINE,
C         WRITES TWO TITLES AND THEN SKIPS TWO LINES--ALL CENTERED FOR
C         72-COLUMN (8.5X11) WIDTH.
    1 WRITE(M2,900)
  900 FORMAT(1H1 8X,55HGULF INTERSTATE ENG. CO. -- PIPELINE SIMULATION P
     1ROGRAM)
      WRITE(M2,901)
  901 FORMAT(1H )
C         WRITE TWO CASE STUDY TITLE CARDS
      WRITE(M2,902) (TITLE(I),I=1,10)
      WRITE(M2,902) (TITLE(I),I=11,20)
  902 FORMAT(1H 15X,10(A4))
C         SKIP TWO LINES
      WRITE(M2,901)
      WRITE(M2,901)
      RETURN
      END
      SUBROUTINE TESTA(I1,I2,I3)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 008
C         CALLED BY MP3
C         THIS SR IS NEEDED TO SET UP AN IF STATEMENT BETWEEN TWO WORDS.
C         IT TAKES THE DIFFERENCE BETWEEN ARGUMENTS I1 AND I2 AND STORES
C         IT IN I3.
    1 I3=I1-I2
      RETURN
      END
      SUBROUTINE MOVE(REG1,REG2,N1)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 009
      DIMENSION REG1(2),REG2(2)
C         CALLED BY MP3, EQFLO
C         THIS SR MOVES N1 ITEMS FROM A REGION STARTING IN REG1 TO
C         A REGION STARTING IN REG2.
C
    1 DO 2 I=1,N1
    2 REG2(I)=REG1(I)
      RETURN
      END
      SUBROUTINE SETUP
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 010
C         CALLED BY SR MP4
C         THIS SR IS USED TO BRIDGE THE GAP BETWEEN RAW INPUT DATA
C         AND POLISHED DATA NEEDED BY THE VARIOUS ENGINEERING ROUTINES.
C         IT IS ALSO USED TO INITIALIZE MANY CONTROL VARIABLES.
C
C         COUNT THE NO. OF PL SEGMENTS BEING USED FOR THIS CASE
    1 NTSEG=0
      DO 3 K=1,MNP
      IF(D(K+7))3,3,2
C         NOTCH THE COUNTER
    2 NTSEG=NTSEG +1
    3 CONTINUE
C         CHECK FOR OVERRIDE NEED ON KEY INPUT. IF BLANK, OVERRIDE
      DO 5 K= 1,3
      IF(D(K+3))4,4,5
    4 D(K+3)=PCA(K)
    5 CONTINUE
C         DO NOT ALLOW INPUT TO EXCEED MAXIMUM-ALLOWABLE DX
      IF(D(4)-PCA(5))502,502,501
C         OVERRIDE EVEN A POSITIVE INPUT THAT,S TOO BIG
  501 D(4)=PCA(5)
C         NOW, BUILD THE NUMBER OF DX,S TO BE USED.  ROUND UP EACH PL
C         SEGMENT(WITH A FRAGMENT OF LENGTH LEFT OVER)TO THE NEXT DX.
  502 NDX=0
      DO 11 K=1,NTSEG
      J=D(K+7)+0.001
      Z(1)=PCC(2,J)
      Z(2)= Z(1)/D(4)
      I2=(Z(1)/D(4)) + 0.001
      Z(3)=I2
      IF(Z(2)-Z(3))6,6,7
    6 JD2=I2+NDX
      GO TO 8
    7 JD2=I2+NDX+1
    8 JD1=NDX + 1
    9 DO 10 KK=JD1,JD2
   10 IT(1,KK)=PCC(1,J) + 0.001
      NDX=I2+JD1-1
C         NOTE, IN ST. NO. 9 ABOVE, WE FILLED IN SEG NO. INTO IT MTX.
C         NOW, FOR EACH PL SEGMENT IN USE, CALC K AND CP EQN CONSTANTS
      CALL FITKC(PCC(8,J),PCC(10,J),PCC(32,J))
      CALL FITKC(PCC(8,J),PCC(12,J),PCC(34,J))
C         DO SAME FOR INSULATION
      CALL FITKC(PCC(8,J),PCC(18,J),PCC(36,J))
      CALL FITKC(PCC(8,J),PCC(20,J),PCC(38,J))
C         CALC ID, IN. AND FT.
      PCC(29,J)= PCC(3,J) -(2.0*PCC(4,J))
      PCC(30,J)= PCC(29,J)/12.0
C         CALC OD, FT
      PCC(31,J)=PCC(3,J)/12.0
C         CALC CS AREA-OIL FILLED
      PCC(40,J)=  3.14159*(PCC(30,J)**2)/4.0
C         CALC ANNULAR CS AREA-STEEL FILLED
      PCC(41,J)= (3.14159*  (PCC(31,J)**2)/4.0) - PCC(40,J)
C         VOL CAPACITY OF DX IN THIS SEGMENT, STD BBLS (AT 60 F)
      PCC(42,J)=  PCC(40,J)*(D(4)*5280.0) * 7.4805 / 42.0
C         END OF SEGMENT DO-LOOP
   11 CONTINUE
C         OIL BATCHING WILL NOW COMMENCE
C         FIRST, WE,LL COUNT THE BATCHES TO BE PROCESSED
      NTBAT=0
      DO 13 K=1,MNB
      IF(D(K+60))13,13,12
   12 NTBAT=NTBAT + 1
   13 CONTINUE
   14 DO 15 K=1,NTBAT
      I= D(K+60) + 0.001
C         SG AT  60 F
      PCB(22,I)=141.5/(131.5+PCB(2,I))
C         EQN CONSTANTS FOR VISCOSITY,  B THEN A IN  Z=A * E(B/T,DEG R)
      PCB(24,I)=  ALOG(PCB(6,I)/PCB(7,I)) / ( (1.0/(PCB(4,I)+460.0)) -
     1            (1.0/(PCB(5,I)+460.0)) )
      PCB(23,I)=  PCB(6,I)/ EXP(PCB(24,I)/(PCB(4,I)+460.0))
C         EQN CONSTANTS FOR K AND CP
      CALL FITKC(PCB(8,I),PCB(10,I),PCB(25,I))
   15 CALL FITKC(PCB(15,I),PCB(17,I),PCB(27,I))
C         PUT 1ST OIL BATCH INTO IT MATRIX, AND SET FRACTION=1.0
      DO 16 K=1,NDX
      IT(2,K)= D(61) + 0.001
   16 T(21,K)=1.0
C         ZERO THE MASTER CLOCK
      D(100)=0.0
C         TEST FOR CASE STUDY TYPE (WE MAY HAVE TO HANDLE INPUT TEMP
C         PROFILES FOR OIL AND STEEL.)
      IF(D(1)-1.0)50,50,20
C         UH-OHH, PROFILES MUST BE CURVE-FITTED AND TEMPS PUT TO T MTX
   20 DX=D(4)
      DO 40 KK=1,2
C         COUNT THE OIL (STEEL) TEMP POINTS THAT WERE INPUT(IF T=0,OUCH)
      NTM=0
      DO 23 J=1,14
      II=(100*KK) + (J*2)
      IF(D(II))21,22,21
   21 NTM=NTM + 1
C         NOW, SET UP A TABLE OF MILEAGE VS TEMP FOR SUBSEQUENT
C         INTERPOLATION AND OR EXTRAPOLATION BASIS DX,S
   22 D(J+170)= D(II)
      D(J+150)= D(II-1)
   23 CONTINUE
C         HAVING MILES IN D(151-164) AND TEMPS IN D(171-184)
C         RUN THE PL,DX BY DX, AND PICK OFF A TEMP THATS AT THE
C         START OF THAT DX,--ALL BY S-L INTERPOLATION OR EXTRAPOLATION.
C
C         BUT FIRST, GET ALL MILE MARKS BASED ON ZERO AT ORIGIN
      Z(6)=D(151)
      DO 24 J=1,NTM
   24 D(J+150)= D(J+150) - Z(6)
C         CLEAR MILES TO(-DX) NULL POSITION, THEN INTERPOLATE
      TDX= -DX
      I=NDX +1
      DO 30 J=1,I
      TDX=TDX + DX
   30 CALL XTPL(D(151),D(171),NTM,TDX,T(KK,J),1)
C         END OF OIL-THEN-STEEL DO LOOP FOR TEMP PROFILES
   40 CONTINUE
C         THAT,S ALL TO THE SETUP ACTIVITY
   50 CONTINUE
      RETURN
      END
      SUBROUTINE FITKC(REGT,REGK,REGA)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 011
      DIMENSION REGT(2),REGK(2),REGA(2)
C         CALLED BY SETUP
C         THIS SR CURVE FITS A TWO-POINT CURVE TO THE EQN FORM OF
C           ANSWER = A + B(X).  IT IS USED TO FIT K AND CP VALUES
C           FOR OIL, INSULATION AND STEEL IN TROGRAM APPLICATION.
C         ARGUMENTS REGT=1ST LOCN. OF FIT TEMPS (TWO IN A ROW ARE REQ,D)
C                   REGK=1ST LOCN. OF TWO K OR CP VALUES
C                   REGA=1ST LOCN. OF TWO ANSWERS, A AND B CONSTANTS.
    1 REGA(2)=(REGK(2)-REGK(1)) /(REGT(2) - REGT(1))
      REGA(1)= REGK(1) -  REGA(2)*REGT(1)
      RETURN
      END
      SUBROUTINE XTPL(RG1X,RG1Y,L1,RG2X,RG2Y,L2)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 012
      DIMENSION RG1X(2),RG1Y(2),RG2X(2),RG2Y(2)
C         CALLED BY SETUP
C         THIS SR INTERPOLATES OR EXTROPLATES IN A 2-COL TABLE, THAT
C         HAS  1ST LOCATIONS OF REG1X AND RG1Y AND IS OF LENGTH L1.
C         THE FIRST OF THE SEARCH VALUES IN RG2X AND THE 1ST OF
C         THE ANSWER VALUES IS IN RG2Y.  THESE TWO VALUE VECTORS
C         ARE BOTH OF LENGTH L2.
C
C         SET UP DO-LOOP FOR SEARCH VALUE OR VALUES
   50 DO 65 J=1,L2
C         SEE IF SEARCH VALUE (RG2X) IS BELOW LOWEST RG1X IN TABLE
   51 IF(RG2X(J) - RG1X(1)) 52,53,53
C         IF SO, SET UP INDEX FOR EXTRAPOLATION DOWNWARD AND DO IT
   52 K=2
      GO TO 64
C         SEE IF SEARCH VALUE (RG2X) IS ABOVE HIGHEST RG1X IN TABLE
   53 IF(RG2X(J) - RG1X(L1)) 55,55,54
C         IF SO, SET UP INDEX FOR EXTRAPOLATION UPWARD AND DO IT
   54 K=L1
      GO TO 64
C         NOW WE KNOW SEARCH VALUE IS BRACKETED. FIND OUT JUST WHERE.
   55 DO 59 N=1,L1
C         COMPARE RG2X TO EACH TABULAR POINT IN RG1X UNTIL BRACKETED
      IF(RG2X(J) - RG1X(N)) 63,61,59
   59 CONTINUE
C         PROCEDURE FOR RG2X = RG1X TABLE VALUE
   61 RG2Y(J) = RG1Y(N)
      GO TO 65
C         FINALLY, WE GET TO THE POINT AND INTERPOLATE OR EXTRAPOLATE
   63 K=N
   64 X1 = (RG2X(J)-RG1X(K-1)) / (RG1X(K) - RG1X(K-1))
      RG2Y(J) = RG1Y(K-1) +(X1 * (RG1Y(K) - RG1Y(K-1)))
   65 CONTINUE
      RETURN
      END
      SUBROUTINE EQFLO(IBAT)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 013
C         CALLED BY MP4
C         THIS SR CALCS AN EQUILIBRIUM TEMP PROFILE FOR BOTH OIL AND
C         STEEL UNDER FLOWING CONDITIONS OF A SINGLE OIL BATCH.
C         THE EQUATION USED IS EQUATION NO 4 IN THE MATHEMATICAL
C         DOCUMENTATION
C         ARGUMENT IBAT=BATCH NO OF EQUILIBRATION OIL
C         ASSIGN TEMP INTO PL FM PUMP. ALSO, SET ASSOCIATED PIPE TEMP.
    1 I=IBAT
      T(1,1)=PCB(33,I)
      T(2,1)=T(1,1)-1.0
C         EST. DELTA T ACROSS FIRST DELTA X LENGTH, FOR OIL AND PIPE
C         GUESS 1 DEG F PER MILE DROP AS FIRST GUESS
      DTB=(-1.0)  * D(4)
      DTP=DTB
C         SET DX LENGTH, FT
      DX=D(4)*5280.0
C         CLEAR HT BAL ERROR SUMMATION STORAGE FOR PREV. ITERATION
      CALL ZEROV(Z(61),6)
C         DENSITY, LB/FT3 AT INLET
      Z(49)=62.3*(PCB(22,I)+PCB(3,I)*(PCB(33,I)-60.))
C         VELOCITY AT INLET, FT/HR
      Z(50) = PCB(32,I)* 349.79 * PCB(22,I)/(24.0 * Z(49) * PCC(40,
     1IT(1,1)))
C         RATIO OF SG AT 60 TO SP GR AT TEMP
      Z(51)= PCB(22,I)/ (PCB(22,I) + PCB(3,I)*(PCB(33,I)-60.))
C         SET UP OUTER DO-LOOP TO WORK THE TOTAL PL 2-5 TIMES.
      PCA(8)=1.0
    2 DO 10 KK=1,5
C         SAVE LATEST SUMS
      CALL MOVE(Z(61),Z(64),3)
C         SET UP INNER DO-LOOP TO CALC FULL T MTX FOR EACH DELTA X
    3 DO  5 K=1,NDX
C         ASSIGN PL SEG AND OIL BATCH ID,S
      J= IT(1,K)
      I= IT(2,K)
C         CLEAR HT BAL SUMBOXES FOR THIS ITERATION
      CALL ZEROV(Z(61),3)
C         EST. T(O) INTO NEXT DX, AND CALC T-AVG FOR OIL AND PIPE
      TAB= T(1,K)+(DTB*0.5)
      TAP= T(2,K)+(DTP*0.5)
C         OIL DENSITY, LBS/CF AT T(AVG) FOR OIL BATCH IN THIS DELTA X
      T(9,K)= (PCB(22,I) + (PCB(3,I)*(TAB-60.0))) * 62.30
C         VOL CORR. FACTOR FOR TEMP  (SG-AT-60/SG AT TAB)
      T(20,K)= PCB(22,I) / (T(9,K)/62.30)
C         STD BBLS OIL IN DX, =(DX CAPACITY,STD BBLS)* (SG-T/SG-60)
      T(19,K)= PCC(42,J) / T(20,K)
C         OIL VELOCITY,FT/HR =(PUMPRATE-STD B/D)/24  * (DX/BBLS IN DX)
      IF(K.GT.1) GO TO 4
      T(5,K)= Z(50)*T(20,K) / Z(51)
      GO TO 40
    4 IF(IT(1,K).NE.IT(1,K-1)) GO TO 30
      VMP = 1.0
      GO TO 35
   30 VMP = PCC(40,IT(1,K-1))/PCC(40,IT(1,K))
   35 T(5,K)=T(5,K-1)*(T(20,K)/T(20,K-1))*VMP
C         R NUMBER (PSEUDO REYNOLDS NO.)
C         FIRST GET ACTUAL BBLS/DAY
   40 Z(52)=T(5,K)*PCC(40,J)*7.4805*24./42.
      CALL REYNO(Z(52),TAB,I,J,T(6,K))
C         R(F)--OIL FILM, SCALE AND METAL RESISTANCE
      CALL RFILM(T(6,K),TAB,I,J,T(7,K))
      CALL RFOST(TAP,J,Z(7))
C         COMBINE THEM (FILM + (SCALE + STEEL))
      T(7,K)= T(7,K) + Z(7)
C         R(T)--INSUL, SOIL RESISTANCE
      TIN=(TAP+ PCC(24,J)) * 0.5
      CALL RINSO(TIN,J,T(8,K))
C         CP OF OIL
      T(10,K)= PCB(27,I) +(PCB(28,I)*TAB)
C         CP OF STEEL
      T(11,K)= PCC(34,J) +(PCC(35,J)*TAP)
C         DELTA P, PSI DUE TO FRICTION IN DX LENGTH
      CALL FDELP(Z(52),T(5,K),TAB,DX,I,J,T(12,K))
C         FRICTIONAL HEAT GAIN, BTU/HR-FT
      T(13,K)=Z(52)*42.0*2544.48*T(12,K)/(1714.0*1440.0*DX)
C         OIL HT CONTENT, MMBTU
      T(14,K)=PCC(40,J)*DX*T(9,K)*T(10,K)*TAB*1.0E-6
C         HT CONTENT OF STEEL, MMBTU
      T(16,K)= PCC(5,J) * PCC(41,J) * DX * T(11,K) * TAP*1.0E-6
C         HT FLOW TO SOIL, MMBTU/HR
      T(18,K)=((TAP-PCC(24,J)) / T(8,K))*1.0E-6
C         SUM Q,S AS WE RUN THE P.L.
      Z(61)=Z(61) + T(14,K)
      Z(62)=Z(62) + T(16,K)
      Z(63)=Z(63) + T(18,K)
C         CALC ACTUAL T(OIL) OUT OF DX, I.E. INTO THE NEXT DX.
C         THE EQUATION USED IS EQUATION NO 4 IN THE MATHEMATICAL
C         DOCUMENTATION
      FR= T(13,K) * (T(7,K)+T(8,K))
      AR=PCC(40,J)*T(10,K)*T(9,K)*T(5,K)
     1    * (T(7,K)+T(8,K))
      T(1,K+1)=((T(1,K)-PCC(24,J)-FR)/(EXP(DX/AR))) + PCC(24,J) + FR
C         ESTIMATE DTB ACROSS NEXT DX, BASIS DT/DX EQN
      DTB=((T(13,K)/(AR/(T(7,K)+T(8,K))))+(PCC(24,J)/AR)-(T(1,K+1)/AR)
     1 )*DX
C         CALC ACTUAL T(PIPE) OUT OF THIS DX
      T(2,K+1)=(T(8,K)*T(1,K+1) + T(7,K)*PCC(24,J)) / (T(8,K) + T(7,K))
C         ESTIMATE DTP ACROSS NEXT DX, USING R,S OF THIS DX
      DTP=T(2,K+1)-T(2,K)
C         END OF INNER DO LOOP
    5 CONTINUE
C         DO SOME HOUSEKEEPING REGARDING 1ST PL STEEL TEMP
      T(2,1)=(T(8,1)*T(1,1)+T(7,1)*PCC(24,IT(1,1)))/(T(8,1)+T(7,1))
C         TEST FOR Q CONVERGENCE TO TOLERANCE DESIRED
C         SET AND TEST CONVERGENCE TOLERANCE TO 0.5 PER CENT OF
C         PREVIOUS ITERATION.
      IF(ABS(Z(61)-Z(64))-(0.5    *Z(61)/100.0))6,6,9
    6 IF(ABS(Z(62)-Z(65))-(0.5    *Z(62)/100.0))7,7,9
    7 IF(ABS(Z(63)-Z(66))-(0.5    *Z(63)/100.0))11,11,9
C         UPDATE LOOP COUNT BOX (EQUIL ITERATIONS)
    9 Z(197) = Z(197) + 1.0
C         OUTER DO-LOOP TERMINAL
   10 CONTINUE
C         EQUILIBRIUM AT LAST (OR END OF 5 SHOTS AT IT)
C         TRANSFER DATA AT(THETA)TO (THETA+DELTA THETA)
   11 CALL MOVER(T(1,1),T(3,1),NDX+1,22)
      CALL MOVER(T(2,1),T(4,1),NDX+1,22)
      CALL MOVER(T(14,1),T(15,1),NDX+1,22)
      CALL MOVER(T(16,1),T(17,1),NDX+1,22)
      T(12,NDX+1) = 0.0
      DO 12 J= 1,NDX
   12 T(12,NDX+1)=T(12,NDX+1) + T(12,J)
C         THAT,S ALL FOR EQ. FLOW CALCULATIONS
      RETURN
      END
      SUBROUTINE REYNO(QTY,TEMP,IBAT,JSEG,ANS)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 014
C         CALLED BY SR,S EQFLO, FDELP, TMATX
C         THIS SR CALCS A REYNOLDS NO. (R) THAT CAN BE CONVERTED TO A
C         CONVENTIONAL REYNOLDS NO. BY MULTIPLYING R BY 7742.0.
C         ARGUMENTS   QTY=QTY OF FLOW  IN ACTUAL BBLS/DAY
C                    TEMP=FLOWING OIL TEMP, DEG F
C                    JSEG=SEGMENT NO. OF P.L. BEING USED
C                    IBAT=BATCH NO. OF OIL FLOWING
C                    ANS =LOCN FOR ANSWER, R
    1 I=IBAT
      J=JSEG
C         CALC  VISCOSITY AT T
      Z(1)= PCB(23,I) *  EXP(PCB(24,I)/(TEMP+460.0))
C         CALC R
      ANS= (0.0119 * QTY) / (PCC(29,J)*Z(1))
      RETURN
      END
      SUBROUTINE RFILM(REN,TEMP,IBAT,JSEG,ANS)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 015
C         CALLED BY EQFLO, TMATX
C         THIS SR CALCS THE RESISTANCE OF THE FLOWING OIL FILM TO
C         THE TRANSFER OF HEAT THRU IT. REFERENCE= MC ADAMS, P-168,P-190
C          =HEAT TRANSMISSION=
C         THE EQN (VALID AT RE GT 2,000) IS AS FOLLOWS
C            H= (K/ID) * 0.023 * (RE**0.8) * (PR**0.4) ,BTU/(HR-SQFT-F)
C            R= 1/(H * PI * ID)
C         ARGUMENTS REN=REYNOLDS NO (R) IN SPECIAL UNITS, R=RE NO /7742
C                  TEMP=TEMP OF FLOWING OIL, DEG F
C                  JSEG=SEG. NO. OF PIPELINE
C                  IBAT=BATCH NO. OF FLOWING OIL
C                   ANS=LOCN FOR ANSWER, FILM RESISTANCE, HR-FT-F/BTU
C
    1 I=IBAT
      J=JSEG
C         CALC RE NO IN CONVENTIONAL SIZE
      Z(12)= REN  * 7742.0
C         CALC PRANDTL NO. (NOTE K-AT-T WILL BE LEFT IN Z(5) BY SR PRNO)
      CALL PRNO(TEMP,I,Z(13))
C         CALC (K/DIAM)
      Z(14)= Z(5) / PCC(30,J)
C         CHECK CALC ROUTING BASIS RE NO
      IF(Z(12)-2000)3,3,2
C         USE TURB. H EQN (MC ADAMS, PG 168) , BTU/(HR-SQFT-DEG F)
    2 Z(15)=(Z(14) * Z(12)**0.8 * Z(13)**0.4)*.023
      GO TO 4
C         USE LAMINAR H EQN (MC ADAMS, PG 190)
C         ASSUME THAT ((VIS AT BULK T)/(VIS AT WALL T))**0.14=1.0585
C         HD/K * (Z/Z)**0.14 = 1.86 ( (RENO)*(PRNO)* (D/L) )**0.3333
    3 Z(15)=((1.86/1.0585) * (Z(12)*Z(13)*(PCC(30,J)/(1.0)))**
     1       0.3333)*Z(14)
C         CALC R(FILM), HR-FT-DEG F/BTU
    4 ANS= 1.0 / (Z(15) * 3.14159 * PCC(30,J))
      RETURN
      END
      SUBROUTINE PRNO(TEMP,IBAT,ANS)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 016
C         CALLED BY SR,S RFILM
C         THIS SR CALCS A PRANDTL NUMBER  (PR =(CP*Z) / K )
C         ARGUMENTS ARE-TEMP=TEMP, DEG F, OF FLOWING OIL
C                       IBAT=BATCH NO. OF FLOWING OIL
C                        ANS=LOCN FOR ANSWER, PR NO.
C
    1 I=IBAT
C         GET C(P) AT T, BTU/LB-DEG F
      Z(1)= PCB(27,I) +  (PCB(28,I)*TEMP)
C         GET Z (VIS) AT T, CENTISTOKES
      Z(2)= PCB(23,I) * EXP(PCB(24,I)/(TEMP+460.0))
C         GET SG AT TEMP
      Z(3)= PCB(22,I) +(PCB(3,I)*(TEMP-60.0))
C         GET Z AT T, LB/(FT-HR)
      Z(4)= Z(2) * Z(3) *  2.42
C         GET K AT T
      Z(5)= PCB(25,I) +(PCB(26,I)*TEMP)
C         CALC PRANDTL NO
      ANS=(Z(1)*Z(4)) / Z(5)
      RETURN
      END
      SUBROUTINE RFOST(TEMP,JSEG,ANS)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 017
C         CALLED BY EQFLO, TMATX, COOLR
C         THIS SR CALCULATES THE FOULING FACTOR + PL STEEL RESISTANCE
C         TO HEAT FLOW.  EACH PL SEGMENT HAS IT,S OWN INPUT FOUL FACTOR.
C         ARGUMENT TEMP=TEMP, DEG F, OF STEEL
C                  JSEG=SEG. NO. OF PIPELINE
C                   ANS=LOCN OF ANSWER, R(FOULING+STEEL),HR-FT-F/BTU
      J=JSEG
C         FOULING FACTOR R
      Z(21)=PCC(6,J)/(3.14159* PCC(30,J))
C         STEEL R               R= (1/2PI*KSTL)*LN(RI/RO) (NOTE,D/D=R/R)
      Z(22)=     (1.0/(2.0*3.14159*(PCC(32,J)+PCC(33,J)*TEMP)))*ALOG
     1    (PCC(31,J)/PCC(30,J))
C         COMBINE THEM
      ANS= Z(21) + Z(22)
      RETURN
      END
      SUBROUTINE RINSO(TEMP,JSEG,ANS)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 018
C         CALLED BY EQFLO, TMATX, COOLR
C         THIS SR CALCS THE COMBINED RESISTANCE (TO HEAT FLOW) OF BOTH
C         INSULATION AND SOIL, CALLED RT.
C         ARGUMENTS TEMP=AVG TEMP, DEG F, OF INSULATION
C                   JSEG=SEGMENT NO. OF PL
C                    ANS=LOCN OF ANSWER, R(INSUL + SOIL), HR-FT-F/BTU
      J=JSEG
C         GET K OF INSUL
      Z(23)= PCC(36,J) + (PCC(37,J)*TEMP)
C         GET R OF INSUL ONLY  (NOTE, D/D=R/R)  R=(1/(2PIK))*LN(RO/RI)
      Z(24)= (1.0/(2.0*3.14159*Z(23))) * ALOG ((PCC(31,J)+(PCC(16,J)*
     1  2.0)/12.)/PCC(31,J))
C         GET R OF SOIL, REFERENCE=NATL BUR STDS REPORT 10194, PG 4.
C             R= (1/(2PIK)) * LN((DEPTH/RI0)+SQRT((D/RI0)**2 -1))
C         CALC(DEPTH OF COVER/RADIUS TO OUTER INSUL) (NOTE,IN/IN =FT/FT)
      Z(25)=PCC(23,J)/((PCC(3,J)/2.)+PCC(16,J))
C         R ITSELF (FOR SOIL)
      Z(26)= (1.0/(2.0*3.14159*PCC(25,J)))* ALOG(Z(25) +((Z(25)**2)-1.0)
     1       **0.5)
C         COMBINE TO GET RT, HR-FT-DEG F/BTU
      ANS= Z(24) + Z(26)
      RETURN
      END
      SUBROUTINE FDELP(QTY,VEL,TEMP,XLONG,IBAT,JSEG,ANS)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 019
C         CALLED BY SR,S EQFLO, TMATX
C         THIS SR CALCS DELTA P, PSI, FOR A LINE LENGTH
C         ARGUMENTS QTY=OIL FLOW, ACTUAL BBLS/DAY
C                   VEL=OIL VELOCITY, FT/HR
C                  TEMP=OIL TEMP, DEG F
C                 XLONG=LINE LENGTH, FT
C                  IBAT=OIL BATCH NO.
C                  JSEG=P.L. SEGMENT NO.
C                   ANS=LOCN FOR ANSWER, PSI
C         REFERENCE=WILSON-MCADAMS =CONTRIBUTION NO 19, DEPT CHE, MIT=
    1 I=IBAT
      J=JSEG
C         SG AT T
      Z(2)= PCB(22,I) + (PCB(3,I)*(TEMP-60.0))
C         R NO.
      CALL REYNO(QTY,TEMP,I,J,Z(3))
C         CHECK R NO  (R OF 0.2583=RE OF 2,000, R OF 0.5167=RE OF 4,000)
      IF(Z(3)-0.2583)2,2,3
C         LAMINAR FLOW FRICTION FACTOR
    2 Z(5)= 0.00207 * (1.0/Z(3))
      GO TO 6
C         CHECK R AGAIN
    3 IF(Z(3)-0.5167)4,5,5
C         INTERMEDIATE, SO USE S-L TIE LINE IN THIS UNCERTAIN ZONE
    4 Z(5)=0.005861 +(0.008335 * Z(3))
      GO TO 6
C         TURBULENT ZONE FRICTION FACTOR
    5 Z(5)= 0.0018 +(.00662 * (1.0/Z(3))**0.355)
C         CALC DELTA P PER MILE
    6 Z(6)=(0.241 * Z(5)*Z(2)* QTY**2) / (PCC(29,J)**5)
C         ADJUST DELTA P FOR LENGTH
      ANS= Z(6) *(XLONG/5280.0)
      RETURN
      END
      SUBROUTINE MOVER(REG1,REG2,NC,NR)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 020
      DIMENSION REG1(2),REG2(2)
C         CALLED BY EQFLO, DYFLO
C         THIS SR MOVES THE DATA IN ONE ROW OF A MTX TO ANOTHER ROW
C         IN THE SAME MATRIX, OR ONE OF IDENTICAL SIZE.
C         ARGUMENTS=REG1=1ST LOCN OF ROW TO BE MOVED
C                   REG2=1ST LOCN OF RECEIVING ROW
C                     NC=NO. OF COLUMNS IN THE SENDING AND RECEIVING MTX
C                     NR=NO. OF ROWS IN BOTH MATRICIES DIMEN ST.
    1 DO 2 I=1,NC
      II=I*NR - NR + 1
    2 REG2(II)= REG1(II)
      RETURN
      END
      SUBROUTINE  DYFLO(I)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 021
C         CALLED BY MP4
C         THIS SUBROUTINE CALCS NON-STEADY STATE TEMPERATURE PROFILES
C         FROM HEAT BALANCES AROUND A PIPE SEGMENT OF LENGTH DELTA X,
C         AND A TIME INTERVAL  DELTA TIME
C         ARGUMENTS, I = OIL BATCH NO.CURRENTLY BEING PUMPED
C
C         UPDATE NO OF TIMES THIS SUBROUTINE CALLED. THIS ALSO GIVES
C         NO OF DISTINCT BATCHES INTRODUCED INTO LINE SINCE TIME ZERO
      Z(170) = Z(170) + 1.0
C         CHECK IF PUMPING TIME FOR THIS BATCH IS ZERO AND IF THIS
C         IS BATCH NO 2 OR HIGHER
      IF(PCB(34,I) .GT. 0.) GO TO 20
C         CALC T MATRIX DATA IF REQUIRED FOR NO FLOW
      IF(D(1) .GT. 1.0) CALL TMATX(I)
C         CALC INITIAL PIPELINE FILL
      Z(105)=0.0
      DO 15  J= 1,NDX
   15 Z(105)  =  Z(105) + T(19,J)
      GO TO 100
C         INIT. TIME COUNT FOR PUMPING ON THIS BATCH
   20 Z(102) =0.0
C         INIT. PRINT CLOCK FOR TERMINUS OUTPUT
      PCT =0.0
C         INIT  PRINT CLOCK FOR TEMP. PROFILE OUTPUT
      PCP =0.0
C         CALC INITIAL T MATRIX DATA
      CALL TMATX(I)
C         DELTA TIME BASED ON VELOCITY OF INCOMING BATCH
      Z(103) = PCA(4) * D(4)*5280./ Z(160)
C
   30 CONTINUE
C         STEP BATCH CLOCK
      Z(102) = Z(102) +Z(103)
C         STEP MASTER CLOCK
      D(100) = D(100) +Z(103)
C         STEP OUTPUT CLOCK FOR TEMP PROFILE
      PCP = PCP + Z(103)
C         STEP OUTPUT CLOCK FOR TERMINUS OUTPUT
      PCT = PCT + Z(103)
C         STEP COUNTER ON TOTAL NO OF ITERATIONS W/ TIME
      Z(101) =Z(101) + 1.0
C         CALCULATE TEMPERATURE PROFILES
      CALL TPROF(I)
C         LOCATE BATCH INTERFACES AND MONITOR PIPELINE OUTPUT
      CALL INTFAC(I)
C         CALCULATE T MATRIX DATA
      CALL TMATX(I)
C         DO HEAT BALANCES
      CALL HTBAL(I)
C         ADJUST OIL TEMPS BEHIND INTERFACES
      DO 35 J=2,NDX
      IF(T(21,J).EQ.1.0) GO TO 35
      IF(J.NE.2) GO TO 36
      T(3,J) = T(3,1) +  (T(3,2) - T(3,1))*0.5
      GO TO 35
   36 T(3,J) = T(3,J-1) + T(3,J-1) - T(3,J-2)
   35 CONTINUE
C         CHECK FOR OUTPUT REQUEST FOR TEMP PROFILES
      IF(PCB(36,I).EQ.0.0) GO TO 40
      IF( PCP .LT. PCB(36,I)) GO TO 40
      CALL OUTTP(2)
      Z(251)=0.0
      PCP =0.0
C         CHECK FOR OUTPUT REQUEST FOR TERMINUS CONDITIONS
   40 IF( PCT .LT. D(5) )  GO TO 50
      CALL OUTTM(D(100),0)
      PCT =0.0
C         ZERO  T(19,NDX+1),T(20,NDX+1), AND Z(225) AT END OF OUTPUT
C
          T(19,NDX+1) =0.0
          T(20,NDX+1) =0.0
          Z(225) = 0.0
C         CHECK IF PUMPING TIME FOR THIS BATCH HAS BEEN REACHED
   50 IF( Z(102) .GE. PCB(34,I))  GO TO  60
C         MOVE  T(3,J) ROW TO  T(1,J) ROW
      CALL MOVER (T(3,1),T(1,1),NDX+1,22)
C         MOVE  T(4,J) ROW TO  T(2,J) ROW
      CALL MOVER (T(4,1),T(2,1),NDX+1,22)
C         LOOP AGAIN
      GO  TO  30
   60 CALL WTITL
C         CALC CUM. Q IN LESS Q GAIN IN OIL AND STEEL SINCE PUMPING
C         BEGAN ON THIS BATCH
      Z(159)=Z(152)+Z(155)+(Z(153)-T(14,NDX+1))+(Z(154)-T(16,NDX+1))
      PE= (Z(158)/Z(159))*100.
      WRITE(M2,900) PE,I
  900 FORMAT(1H 8X,31HHEAT OUT, ADJUSTED FOR GAIN, IS,F6.1,8H PERCENT,
     1 /,9X,37HOF HEAT IN AT END OF PUMPING ON BATCH,I3)
      DO 70 J=1,8
   70 Z(151+J)=0.0
      Z(251)=0.0
  100 RETURN
      END
      SUBROUTINE TMATX(I)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 022
C         CALLED BY DYFLO
C         THIS SUBROUTINE CALCULATES VALUES FOR PROPERTIES
C         IN T MATRIX
C         ARGUMENTS,I = OIL BATCH NO.
C
C
C         CALCULATE PROPERTIES OF FLUID CURRENTLY BEING PUMPED
C
C         DENSITY, LB/FT3 AT INLET
      Z(164)=62.3 *(PCB(22,I) + PCB(3,I)*(PCB(33,I)-60.0))
C         VELOCITY, FT/HR AT ZERO MILES
      Z(160)= PCB(32,I)* 349.79 * PCB(22,I)/(24.0 * Z(164)* PCC(40,
     1IT(1,1)))
C         RATIO OF SG AT 60F TO  SG AT TEMP
      Z(169)= 62.3*(PCB(22,I)) /Z(164)
C         REYNOLDS NO.
      Z(200)= PCB(32,I)* Z(169)
      CALL REYNO(Z(200),PCB(33,I),I,IT(1,1),Z(161))
C         COMBINED RESISTANCE OF FLUID FILM AND SCALE,HR-FT-DEG F/BTU
C
      CALL RFILM(Z(161),PCB(33,I),I,IT(1,1),Z(201))
      CALL RFOST(PCB(33,I),IT(1,1), Z(202))
      Z(162) = Z(201) +Z(202)
C         COMBINED RESISTANCE OF INSULATION AND SOIL,HR-FT-DEG F/BTU
C         FIRST GET AVG INSULATION TEMP
      Z(50)=(T(2,1)+PCC(24,IT(1,1)))/2.0
      CALL RINSO( Z(50)   ,IT(1,1),Z(163))
C         HEAT CAPACITY OF OIL, BTU/LB- DEG F
      Z(165)= PCB(27,I) +PCB(28,I)* PCB(33,I)
C         HEAT CAPACITY OF STEEL,BTU/LB-DEG F
      Z(166)=PCC(34,IT(1,1))+ PCC(35,IT(1,1))* T(2,1)
C         DELTA P ,PSI/DELTAX LENGTH
      Z(200)= PCB(32,I)* Z(169)
      Z(201)= D(4)* 5280.
      CALL FDELP(Z(200),Z(160),PCB(33,I),Z(201),I,IT(1,1),Z(167))
C         CALC FRICTIONAL HEAT GAIN F, BTU/HR-FT
      Z(168)=Z(200)*42.0*2544.48*Z(167)/(1714.*1440.* Z(201))
C         CALC  T MATRIX PROPERTIES , T(5,J) THRU  T(20,J)
C         VALUES ARE AVERAGES OVER THE SEGMENT ,X(J) TO X(J+1)
      DO 5  J = 1,NDX
C                  T(P)= TEMP OF PIPE OUTSIDE T(0)
      IF(T(21,J).NE. 1.0.AND.J.GT.1) GO TO 3
      K= IT(2,J)
      L= IT(1,J)
C         AVG OIL TEMP
      TOA = (T(1,J) + T(1,J+1))/2.0
C         AVG STEEL TEMP
      TSA = (T(2,J) + T(2,J+1))/2.0
C         DELTAX  LENGTH
      DELX = D(4)* 5280.
C
C               DENSITY , LB/CU FT
C
      T(9,J) =  62.3 *(PCB(22,K) + PCB(3,K) *(TOA -60.0))
C
C               RATIO OF SP GR AT 60F TO SP GR AT TEMP
C
      T(20,J)=  PCB(22,K)/ (PCB(22,K) +PCB(3,K)*(TOA-60.))
C
C               VELOCITY ,FT/HR
C
      IF(J.GT.1) GO TO 4
      T(5,J) = Z(160) * T(20,J) / Z(169)
      GO TO 6
    4 IF(IT(1,J).NE.IT(1,J-1)) GO TO 30
      VMP = 1.0
      GO TO 35
   30 VMP = PCC(40,IT(1,J-1))/PCC(40,IT(1,J))
   35 T(5,J)=T(5,J-1)*(T(20,J)/T(20,J-1))*VMP
C
C              REYNOLDS NO -(NON STANDARD UNITS)
C
C         FIRST GET ACTUAL BBLS/DAY
    6 Z(200)= T(5,J)*PCC(40,L)*7.4805*24.0/42.0
      CALL REYNO( Z(200),TOA,K,L,T(6,J))
C
C              COMBINED RESISTANCE OF FLUID FILM-SCALE,HR-FT-DEGF/BTU
C
      CALL RFILM(T(6,J),TOA,K,L,Z(203))
      CALL RFOST(TSA,L, Z(204))
      T(7,J) = Z(203) + Z(204)
C
C              COMBINED RESISTANCE OF INSULATION AND SOIL,HR-FT-DEGF/BTU
C
C         FIRST GET AVG INSULATION TEMPERATURE
      Z(205) =(TSA + PCC(24,L) )/2.0
      CALL RINSO(Z(205),L, T(8,J))
C
C              HEAT CAPACITY OF OIL, BTU/LB-DEGF
C
      T(10,J) = PCB(27,K) +(PCB(28,K) * TOA)
C
C              HEAT CAPACITY OF STEEL, BTU/LB-DEGF
C
      T(11,J) = PCC(34,L) +(PCC(35,L)*TSA)
C
C              FLUID FLOW PRESSURE DROP PER DELTAX LENGTH , PSI
C
      CALL FDELP(Z(200),T(5,J),TOA,DELX,K,L,T(12,J))
C
C              FRICTIONAL HEAT GAIN, F,  BTU/FT-HR
C
      T(13,J) =   Z(200)*42.0*2544.48 *T(12,J)/(1714. *1440.*DELX)
C
C              Q CONTAINED IN OIL, MM BTU
C
      T(14,J) = PCC(40,L)* DELX * T(9,J) * T(10,J)*TOA * 1.0E-6
C
C              Q CONTAINED IN STEEL, MMBTU
C
      T(16,J) = PCC(41,L)* DELX *PCC(5,L)* T(11,J)*TSA * 1.0E-6
C
C         Q PER HR TO SOIL, MMBTU/HR
C
      T(18,J)  =((TSA - PCC(24,L))/T(8,J))*DELX*1.0E-6
C
C         STANDARD BBL OF OIL PER SEGMENT
C
      T(19,J) = PCC(40,L) * DELX * 7.4805 /(T(20,J) *42.0)
      GO TO 5
C         THIS SEGMENT CONTAINS AN INTERFACE SO IT PROPERTIES ARE
C         AVERAGED ACROSS SEGMENT.
    3 KR=IT(2,J)
      KL=IT(2,J-1)
      L =IT(1,J)
      FR=T(21,J)
      FL= 1.0-T(21,J)
      DELXR = D(4)*5280.*FR
      DELXL = D(4)*5280.*FL
C         TEMP OF OIL TO RIGHT OF INTERFACE ,TORI
      IF( J .NE. NDX)    GO TO 40
      TORI= T(1,J+1)
C         TEMP OF STEEL TO RIGHT OF INTERFACE
      TSRI= T(2,J+1)
      GO TO 50
   40 TORI = T(1,J+1) +FR *(T(1,J+1) - T(1,J+2))
      TSRI = T(2,J+1) +FR *(T(2,J+1) - T(2,J+2))
C         TEMP OF OIL TO LEFT OF INTERFACE, TOLI
   50 TOLI = T(3,J-1)
C         TEMP OF STEEL TO LEFT OF INTERFACE, TSLI
      TSLI = T(4,J-1)
C
C         AVG OIL-STL TEMP TO RIGHT AND LEFT OF INTERFACE
      TOARI =(TORI + T(1,J+1))/2.0
      TOALI =(TOLI + T(1,J))/2.0
      TSARI =(TSRI + T(2,J+1))/2.0
      TSALI =(TSLI + T(2,J))/2.0
C              DENSITY
      T(9,J)=(62.3 *(PCB(22,KR) + PCB(3,KR) *(TOARI -60.)))* FR  +
     1       (62.3 *(PCB(22,KL) + PCB(3,KL) *(TOALI -60.)))* FL
C
C              RATIO OF SP GR AT 60F TO SP GR AT TEMP
C
      T(20,J)=(PCB(22,KR) / (PCB(22,KR) + PCB(3,KR) *(TOARI -60.)))*FR +
     1        (PCB(22,KL) / (PCB(22,KL) + PCB(3,KL) *(TOALI -60.)))*FL
C
C              VELOCITY
C
      IF(IT(1,J).NE.IT(1,J-1)) GO TO 55
      VMP = 1.0
      GO TO 56
   55 VMP = PCC(40,IT(1,J-1))/PCC(40,IT(1,J))
   56 T(5,J)=T(5,J-1)*(T(20,J)/T(20,J-1))*VMP
C
C              REYNOLDS NO
C
C         FIRST GET ACTUAL BBLS/DAY
      Z(200)= T(5,J) *PCC(40,L)*7.4805 *24.0/42.0
      CALL REYNO(Z(200),TOARI,KR,L,Z(205))
      CALL REYNO(Z(200),TOALI,KL,L,Z(206))
      T(6,J) = Z(205)*FR  + Z(206)* FL
C
C              COMBINED RESISTANCE OF FLUID FILM AND SCALE
C
      CALL RFILM(Z(205),TOARI,KR,L,Z(207))
      CALL RFILM(Z(206),TOALI,KL,L,Z(208))
      Z(209) = Z(207)*FR + Z(208)*FL
      CALL RFOST(TSARI,L,Z(210))
      CALL RFOST(TSALI,L,Z(211))
      Z(212) = Z(210)*FR + Z(211)*FL
      T(7,J) = Z(209) + Z(212)
C              COMBINED RESISTANCE OF INSULATION AND SOIL
C         FIRST GET AVG INSULATION TEMPERATURE
      Z(205)=((TSARI + PCC(24,L))/2.0  +(TSALI +PCC(24,L))/2.0)*0.5
      CALL RINSO(Z(205),L,T(8,J))
C
C              HEAT CAPACITY OF OIL
C
      T(10,J) =(PCB(27,KR) +(PCB(28,KR)*TOARI))*FR +
     1         (PCB(27,KL) +(PCB(28,KL)*TOALI))*FL
C
C              HEAT CAPACITY OF STEEL
C
      T(11,J) =(PCC(34,L) +(PCC(35,L)*TSARI))*FR +
     1         (PCC(34,L) +(PCC(35,L)*TSALI))*FL
C
C              FLUID FLOW PRESSURE DROP PER DELTAX LENGTH
C
      CALL FDELP(Z(200),T(5,J),TOARI,DELXR,KR,L,Z(205))
      CALL FDELP(Z(200),T(5,J),TOALI,DELXL,KL,L,Z(206))
      T(12,J) = Z(205)*FR + Z(206)*FL
C
C              FRICTIONAL HEAT GAIN
C
      T(13,J) = Z(200)* 42.0* 2544.48 *T(12,J)/(1714.* 1440.*(DELXR + DE
     1LXL))
C
C              Q CONTAINED IN OIL
C
      T(14,J)= PCC(40,L)*(DELXR +DELXL)* T(9,J)* T(10,J)*(TOARI +TOALI)*
     1 0.5 *1.0E-6
C
C              Q CONTAINED IN STEEL
C
      T(16,J)= PCC(41,L)* (DELXR +DELXL)*PCC(5,L)*T(11,J)*(TSARI +TSALI)
     1 *0.5 * 1.0E-6
C
C              Q PER HR TO SOIL
C
      T(18,J) = (((TSARI +TSALI)*.5 - PCC(24,L))/ T(8,J))*DELX*1.0E-6
C
C               STANDARD BBL OF OIL PER SEGMENT
C
      T(19,J) = PCC(40,L) *(DELXR +DELXL) *7.4805/(T(20,J)* 42.0)
    5 CONTINUE
C         SUM VALUES IN T MATRIX
      T(12,NDX+1) =0.0
      T(13,NDX+1) = 0.0
      T(14,NDX+1) = 0.0
      T(16,NDX+1) = 0.0
      T(18,NDX+1) =0.0
      DO 60  J=1,NDX
      T(12,NDX+1)= T(12,NDX +1) + T(12,J)
      T(13,NDX+1) = T(13,NDX+1) + T(13,J)
      T(14,NDX+1) = T(14,NDX+1) + T(14,J)
      T(16,NDX+1) = T(16,NDX+1) + T(16,J)
   60 T(18,NDX+1)= T(18,NDX +1) + T(18,J)
C         RETURN CONTROL TO DYFLO
      RETURN
      END
      SUBROUTINE TPROF(I)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 023
C         CALLED BY DYFLO
C         THIS SUBROUTINE CALCS UNKNOWN TEMPS IN PIPE MATERIAL AND OIL
C         THE TEMPERATURES ARE FOUND BY AN ITERATIVE SOLUTION TO
C         EQUATIONS NO 1 AND NO 2 IN THE MATHEMATICAL DOCUMENTATION
C         ARGUMENTS, I= OIL BATCH NO
C
C
C
C         LENGTH OF DELTA X SEGMENT
      DELX    = D(4)*5280.0
      NT=NDX+1
      DO 100 J=1,NT
C         IF THIS IS THE FIRST  DELX   T(3,1), = INCOMING OIL TEMP
      IF(J.GT.1 ) GO TO 2
      T(3,J) =PCB(33,I)
C         AVG OIL TEMP OVER INTERVAL
      TOA = T(3,J)
C     PIPE TEMP
      CALL TPIPE(TOA,J,T(4,J))
      GO TO 100
    2 CONTINUE
C         AVERAGE RATE OF CHANGE OF OIL TEMP WITH  RESPECT TO X
      S=(T(1,J)-T(1,J-1))/DELX
C         IF THE PRECEEDING DELX CONTAINS AN INTERFACE, SKIP
C         ITERATION AND USE AVG VALUES
      IF( T(21,J-1).EQ. 1.0) GO TO 5
      FL=1.0-T(21,J-1)
      FR=T(21,J-1)
      IF(J.NE.2) GO TO 20
      TOLI=T(1,J-1)
      GO TO 25
   20 TOLI=T(1,J-1) + FL*( T(1,J-1) - T(1,J-2))
   25 IF(J.NE.NT ) GO TO 30
      TORI= T(1,J)
      GO TO 35
   30 TORI=T(1,J) + FR*(T(1,J)-T(1,J+1))
   35 TOA = ((T(1,J-1)+TOLI)/2.0 + (T(1,J) + TORI)/2.0)/2.0
      CALL TPIPE(TOA,J,TPC)
      TPA=(T(2,J) + TPC)/2.0
C         AVG SLOPE
      S=((TOLI - T(1,J-1))+ (T(1,J) -TORI))/DELX
      CALL TOIL(TPA,S,J,TOC)
      T(4,J) = TPC
      T(3,J) = TOC
      GO TO 100
C     ASSUME TOE1   = T(1,J) AS FIRST GUESS FOR OIL TEMP AT END OF
C     TIME INTERVAL.
    5  TOE1   = T(1,J)
C         CALC  PIPE TEMP AT END OF INTERVAL
      CALL  TPIPE(TOE1,J,TPC)
C         CALC AVG  PIPE TEMP OVER INTERVAL
      TPA =(T(2,J) + TPC) /2.0
C         CALC OIL TEMP AT END OF INTERVAL
      CALL TOIL(TPA,S,J,TOC)
      E1 = TOE1 -TOC
C         GET A SECOND ESTIMATE
      TOE2 = TOC
      TOA =( TOE2  + T(1,J))/2.0
      CALL  TPIPE(TOA,J,TPC)
      TPA =(T(2,J) + TPC) /2.0
      CALL TOIL(TPA,S,J,TOC)
C         ITERATE TO CONVERGENCE
      IL = PCA(9) + .001
      DO 50  K= 1,IL
C         COUNT NO. OF ITERATIONS SINCE TIME ZERO
      Z(199) = Z(199) + 1.0
      E2 =  TOE2 -TOC
      IF( ABS (E2) .LE. PCA(10)) GO TO 60
      DELTA=(-E2)*(TOE1-TOE2)/(E1-E2)
      TOE1 = TOE2
      E1=E2
      TOE2 = TOE1 +DELTA
      TOA =( TOE2  + T(1,J))/2.0
      CALL  TPIPE(TOA,J,TPC)
      TPA=(T(2,J) + TPC)/2.0
      CALL TOIL(TPA,S,J,TOC)
   50 CONTINUE
C         COUNT NO OF TIME LOOP DIMENSIONS EXCEEDED
      Z(198) = Z(198) + 1.0
      IF(Z(198) .GT. 10.0) GO TO 60
      X= FLOAT(J-1)
      FMIL = X* D(4)
      CALL WTITL
      WRITE(M2,900)FMIL,TOE2,TOE1,TOE1
  900 FORMAT(1H 60HTEMPROF SUBROUTINE UNABLE TO CONVERGE TEMPERATURE AT
     1A POINT/,F6.1,51H MILES ALONG THE PIPELINE. LAST OIL TEMPERATURE W
     2AS,F6.1,/,29H NEW ESTIMATE WOULD HAVE BEEN,F6.1,16H OIL TEMP SET T
     3O,F6.1)
   60 T(3,J)=TOE2
      T(4,J)=TPC
C         INCREMENT TOTAL ITERATIONS
  100 CONTINUE
      RETURN
      END
      SUBROUTINE TPIPE(TA,J,ANS)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 024
C         CALLED BY TPROF
C         THIS SUBROUTINE CALCULATES THE PIPE TEMPERATURE AT THE
C         END OF A TIME INTERVAL WHEN THE AVERAGE OIL TEMPERATURE
C         OVER THE TIME INTERVAL IS GIVEN
C         THE EQUATION USED IS EQUATION NO 6 IN THE
C         MATHEMATICAL DOCUMENTATION
C
C         ARGUMENTS, TA= AVERAGE OIL TEMPERATURE OVER TIME INTERVAL
C                     J= POSITION ALONG PIPELINE
C                    ANS=LOCATION OF PIPE TEMPERATURE
      IF(J.GT. 1) GO  TO  10
      Z(201) = (1.0/Z(163)) + (1.0/Z(162))
      Z(202) = PCC(41,IT(1,J))*Z(166)*PCC(5,IT(1,J))
      Z(203) = TA/Z(162)  +PCC(24,IT(1,J))/Z(163)
      GO TO 20
   10 Z(201) = (1.0/ T(7,J-1)) +(1.0 / T(8,J-1))
      Z(202) = PCC(41,IT(1,J-1))* T(11,J-1) *PCC(5,IT(1,J-1))
      Z(203) = TA/T(7,J-1)  + PCC(24,IT(1,J-1)) /T(8,J-1)
   20 Z(204) =  EXP(-Z(201)*Z(103)/Z(202))
      ANS =((Z(201)* T(2,J)- Z(203))*Z(204) + Z(203))/Z(201)
      RETURN
      END
      SUBROUTINE  TOIL(TA ,S,J,ANS)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 025
C         CALLED BY TPROF
C         THIS SUBROUTINE CALCULATES THE OIL TEMPERATURE AT THE
C         END OF A TIME INTERVAL WHEN THE AVERAGE PIPE TEMPERATURE
C         AND THE AVERAGE RATE OF CHANGE IN OIL TEMPERATURE WITH X
C         IS GIVEN.
C         THE EQUATION USED IS EQUATION NO 7 IN THE MATHEMATICAL
C         DOCUMENTATION
C
C         ARGUMENTS TA= AVERAGE PIPE TEMPERATURE OVER TIME INTERVAL
C                    S= AVG RATE OF CHANGE IN OIL TEMP WITH X
C                    J= POSITION ALONG PIPELINE
C                  ANS= LOCATION OF OIL TEMP.
C
      Z(201)= PCC(40,IT(1,J-1)) *(T(10,J-1))* (T(9,J-1))
      Z(202)= EXP(-Z(103)/(Z(201)* T(7,J-1)))
      Z(203)= TA/T(7,J-1) - Z(201)* T(5,J-1)*S  + T(13,J-1)
      Z(204)= T(1,J)/T(7,J-1) - Z(203)
      ANS =(Z(202)* Z(204) + Z(203)) * T(7,J-1)
      RETURN
      END
      SUBROUTINE  INTFAC(I)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 026
C         CALLED BY DYFLO
C         THIS SUBROUTINE LOCATES INTERFACES AND MONITORS OUTPUT
C         AT END OF PIPELINE
C         ARGUMENTS, I = BATCH NO CURRENTLY BEING PUMPED
C
C
C         SUM STANDARD BBLS OF INPUT DURING TIME INTERVAL
      Z(200) = PCB(32,I)/24.0 * Z(103)
      K= 104 + I
      Z(K) = Z(K) + Z(200)
C         SET L = CUM NO OF INTERFACES EXITED FROM PIPELINE
      L = Z(104) + .001
C         DEFINE BATCH NO CURRENTLY EXITING ;  LL
      LL = D(61+L) +.001
C         SET IT(2,NDX+1) = TO EXITING BATCH NO FOR USE BY OUTTM
      IT(2,NDX+1) =LL
C         CALC STD BBLS OUT DURING THIS TIME INTERVAL
      N= 124 +LL
      K= 114 +LL
      Z(200)=(T(5,NDX) *Z(103)* PCC(40,IT(1,NDX))*7.4805 )
     1 /( T(20,NDX) * 42.0)
C         ACCUMULATE TOTAL STD BBLS OUT SINCE LAST OUTPUT CALL IN
C         T(19,NDX+1) FOR USE BY OUTTM
      T(19,NDX+1) = T(19,NDX+1) + Z(200)
C         ACCUMULATE TOTAL STD BBLS OUT FOR THIS BATCH SINCE
C         TIME ZERO
      Z(K) = Z(K)+Z(200)
C         AVERAGE TEMP OF MATERIAL OUT DURING THIS INTERVAL
      TAG = (T(1,NDX+1) + T( 3, NDX +1)) /2.0
C         BULK TEMPERATURE OF BATCH OUT UP TO THIS TIME
      Z(N)=((Z(K)- Z(200))*Z(N) + Z(200)* TAG)/Z(K)
C         BULK TEMPERATURE OF EXITING BATCH SINCE LAST OUTPUT CALL
      Z(225) =((T(19,NDX+1)- Z(200))* Z(225) + Z(200)*TAG)/T(19,NDX+1)
C         CALC  SG AT 60 / SG AT T  FOR  Z(225). THIS VALUE IS USED
C         BY OUTTM
      T(20,NDX+1) = PCB(22,LL)  /(PCB(22,LL)+ PCB(3,LL)*(Z(225)-60.))
C         CHECK IF CUMULATIVE TOTAL OF BATCH NOW EXITING EXCEEDS
C         INPUT. IF SO, INTERFACE HAS JUST CLEARED END OF PIPELINE
      IF(Z(K) .LE. Z(K-10)) GO TO 2
C         INCREMENT INTERFACE COUNT AT PIPELINE EXIT
      Z(104)=  Z(104) + 1.0
C         DETERMINE AMOUNT OF BATCH OVERLAP AT EXIT AT END OF TIME
      Z(201)= Z(K)- Z(K-10)
C         CORRECT CUMULATIVE AMOUNTS OUT PER BATCH
      T(19,NDX+1) = T(19,NDX+1) - Z(201)
      Z(K) = Z(K)- Z(201)
C         ACTUAL ARRIVAL TIME NOW IS TO BE CORRECTED BY AMOUNT OF
C         TIME CORRESPONDING TO ABOVE BATCH OVERLAP
      M= 134 + LL
C         DELTA TIME , Z(202)
      Z(202) =(Z(201) * T(20,NDX)*42.0)/(T(5,NDX)*PCC(40,IT(1,NDX))
     1 *7.4805)
C         INTERFACE ARRIVAL TIME
      Z(M)= D(100)- Z(202)
C         CALL OUTPUT FOR INTERFACE ENCOUNTER
      CALL OUTTM(Z(M),1)
C         ZERO  T(19,NDX+1) AND T(20,NDX+1) AND Z(225) AT END OF OUTPUT
      T(19,NDX+1) =0.0
      T(20,NDX+1) =0.0
      Z(225) = 0.0
C         BATCH OVERLAP BELONGS IN NEXT ACCUMULATION
      LL = D(61+(L+1)) +.001
      Z(114+LL) = Z(114+LL) + Z(201)
      T(19,NDX+1) = T(19,NDX+1) + Z(201)
      T(20,NDX+1) = T(20,NDX-1)
      IT(2,NDX+1) = LL
C         CALL PROFILE OUTPUT AFTER INTERFACE CLEARS
      CALL OUTTP(2)
      Z(151)=0.0
      PCP=0.0
C         UPDATE VALUE OF L ,CUM NO OF INTERFACES EXITED FROM LINE
      L=  Z(104) + .001
C         DETERMINE NO OF INTERFACES REMAINING IN PIPELINE
    2 XK=Z(170)-Z(104)-1.0
C         IF NO INTERFACES REMAIN IN PIPELINE, SKIP SEARCH
      IF(XK.LE.0.0) GO TO 15
      IK=XK+.001
C         INITIALIZE COUNTER FOR INTERFACES FOUND DURING SEARCH
      INC =0
      Z(201)=0.0
C         CALC REMAINING AMOUNT OF BATCH IN LINE
C         INITIAL VALUE OF LL, BATCH CURRENTLY EXITING HAS PREVIOUSLY
C         BEEN SET
      DO 5  J = 1,NDX
      JJ =(NDX +1)- J
      IF(INC.EQ.IK) GO TO 10
      K= 104 + LL
      N= 114 + LL
      Z(200) = Z(K)- Z(N)
C         NOW ACCUMULATE STD BBLS FROM BACK END OF PIPELINE UNTIL
C         INTERFACE IS FOUND
      Z(201) = Z(201) + T(19,JJ)
C         CHECK FOR INTERFACE
      IF ( Z(200) .LE. Z(201)) GO TO 6
C         THIS DELTA X SEGMENT DOESNT HAVE INTERFACE
   10 IT(2,JJ) = LL
      T(21,JJ) = 1.0
      GO  TO  5
C
C         INTERFACE HAS BEEN FOUND, INCREMENT INTERFACE COUNTER
    6 INC =INC +1
C         CALC OVERLAP
      Z(202) = Z(201) -Z(200)
      T(21,JJ)=1.-Z(202)/T(19,JJ)
      IT(2,JJ)= LL
C         UPDATE BATCH ACCUMULATOR
      Z(201) = Z(202)
C
C         UPDATE BATCH IDENTIFIER ,LL . NOTE THAT CUM NO OF INTERFACES
C         EXITED FROM PIPELINE, L, HAS PREVIOUSLY BEEN SET.
C
      LL = D(61+L +INC) + .001
    5 CONTINUE
C         RETURN CONTROL TO DYFLO
   15 RETURN
      END
      SUBROUTINE HTBAL(I)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 027
C         CALLED BY DYFLO
C         THIS SUBROUTINE CALCS HEAT BALANCES OVER TIME INTERVAL
C         DELTA AND  ACCUMULATES  TOTAL  HEAT BALANCE
C         ARGUMENTS,I  = OIL BATCH NO
C
C
C         CALC  OIL AND STEEL HEAT CONTENT AT END OF TIME INTERVAL,MMBTU
      DO 10  J= 1,NDX
      T(15,J) =(PCC(40,IT(1,J)) * D(4)* 5280. * T(9,J) * T(10,J)*
     1 ((T(3,J) + T(3,J+1))/2.0))* 1.0E-6
   10 T(17,J) =(PCC(41,IT(1,J))* D(4)*  5280. * T(11,J)*PCC(5,IT(1,J))*
     1(T(4,J)+ T(4,J+1))/2.0) * 1.0 E -6
      T(15,NDX+1) = 0.0
      T(17,NDX+1) = 0.0
      DO 15 J=1,NDX
      T(15,NDX+1) = T(15,NDX+1) + T(15,J)
   15 T(17,NDX+1) = T(17,NDX+1) + T(17,J)
C         Q IN AT ORIGIN OVER THIS TIME INTERVAL,MMBTU
      Z(147) =(PCB(32,I)*349.8*PCB(22,I)*PCB(33,I)* Z(103)*Z(165)
     1 /24.0)*1.0E-6
      IF(Z(102)-Z(103).GT.0.0) GO TO 1
      Z(153)=T(14,NDX+1)
      Z(154)=T(16,NDX+1)
C         TOTAL HEAT DUE TO FRICTION OVER THIS TIME INTERVAL, MMBTU
    1 Z(148) = T(13,NDX+1)*Z(103)*D(4)*5280.  *1.0E-6
C         Q OUT AT TERMINUS OVER THIS TIME INTERVAL,MMBTU
      Z(149) =(T(5,NDX)* PCC(40,IT(1,NDX))* T(9,NDX)* T(10,NDX)*Z(103)
     1*(T(1,NDX+1)+ T(3,NDX+1)) /2.0) *1.0 E-6
C         Q OUT TO SOIL OVER THIS TIME INTERVAL
      Z(150) = T(18,NDX+1)*Z(103)
C         TOTAL Q OUT OVER  THIS TIME INTERVAL,MMBTU
      Z(151) = Z(149) + Z(150)
C         ACCUMULATE TOTALS OF EACH OF THE ABOVE WITH TIME,MMBTU
      Z(152)=Z(152)+Z(147)
      Z(155)=Z(155)+Z(148)
      Z(156)=Z(156)+Z(149)
      Z(157)=Z(157)+Z(150)
      Z(158)=Z(158)+Z(151)
      RETURN
      END
      SUBROUTINE NOFLO(IBAT)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 028
C         CALLED BY MP4
C         THIS SR RUNS A TIME-TEMP STUDY OF THE PIPELINE DURING NON-FLOW
C         OPERATIONS.  IT ASSUMES THAT STEEL AND OIL TEMP EQUILIBRATE
C         TO EQUAL VALUES WITHIN NIL TIME (ACTUAL TIME EST. AT 1 MIN.).
C         IT FURTHER ASSUMES THAT THE BULK OIL TEMP IS HOMOGENEOUSLY
C         MIXED.  THE LATTER IS ADJUSTABLE,HOWEVER, BY A FACTOR
C         ALLOWED IN THE BASIC EQUATION.  THUS, THE VALUE OF THIS
C         FACTOR MAY BE DETERMINED WHEN THE PIPELINE IS OPERATIONAL
C         FROM OPERATING DATA.
C         THE EQUATION USED IS EQUATION NO 5 IN THE MATHEMATICAL
C         DOCUMENTATION
C         ARGUMENT IBAT=BATCH NO. OF LAST OIL PUMPED INTO PIPELINE
C         TEST FOR ZERO COOLING TIME
    1 IF(PCB(35,IBAT))25,25,2
    2 DX=D(4)*5280.0
C         EQUALIZE OIL AND STEEL TEMPS VIA HT BALANCES ON EACH DX
      Z(73)=0.0
      Z(74)=0.0
      DO 3 K=1,NDX
C         SET FLOW-ORIENTED TERMS TO ZERO
      T(5,K)=0.0
      T(6,K)=0.0
      T(12,K)=0.0
      T(13,K)=0.0
C         SET Q TERMS TO ZERO BECAUSE WE,RE NOT KEEPING,EM UPDATED
      T(14,K)=0.0
      T(15,K)=0.0
      T(16,K)=0.0
      T(17,K)=0.0
      T(18,K)=0.0
      J=IT(1,K)
C         SUM STD BBLS IN P. L. WHILE HOT
      Z(73)=Z(73) + T(19,K)
C         OIL WT * CP
      Z(71)= (PCC(40,J)*DX*T(9,K)) * T(10,K)
C         STL WT * CP
      Z(72)= (PCC(41,J)*DX*PCC(5,J)) * T(11,K)
C         TEMP FOR OIL AND STL AT HEAT BALANCED TIME  (1 MINUTE OR LESS)
      T(1,K)=  (Z(71)*T(1,K) + Z(72)*T(2,K)) / (Z(71)+Z(72))
      T(2,K)= T(1,K)
    3 CONTINUE
       T(1,NDX+1)=T(1,NDX)+T(1,NDX)-T(1,NDX-1)
      T(2,NDX+1)=T(1,NDX+1)
      Z(74)=Z(73)
C         OUTPUT THE PROFILE THUS DETERMINED AT TIME ZERO
      CALL OUTTP(3)
C       **INITIALIZE DO-LOOP SET-UP**
C         SAVE STARTING TIME FROM MASTER CLOCK
      Z(99)=D(100)
      Z(98)=0.0
      Z(97)=0.0
C         SET METRONOME TO PCA(12) VALUE
      DTM= PCA(12)
C         IF COOLING IS LESS THAN ABOVE, CHANGE ROUTING
      IF(PCB(35,IBAT).GT.DTM) GO TO 40
      DTM=PCB(35,IBAT)
      ET=DTM
      GO TO 160
   40 J2=PCB(35,IBAT)/DTM +0.001
C         NOW, LET THE 0.5 HR METRONOME CLICK AWAY. EVERY FEW HRS
C         WE,LL UPDATE OIL AND STEEL PROPERTIES AND CALCULATE A NEW
C         TEMP PROFILE.  AND EVERY SO OFTEN, WE,LL CALC TEMPS FOR
C         OUTPUT PURPOSES.
      DO 15 KK=1,J2
      D(100)=D(100) +DTM
      Z(98)=Z(98) + DTM
      Z(97)=Z(97)+ DTM
C         CHECK FOR UPDATE TIME
      IF(Z(98).GE.PCA(11)) GO TO 100
C         CHECK FOR OUTPUT TIME
   50 IF(Z(97).GE.PCB(36,IBAT)) GO TO 150
C         CHECK FOR END OF NO FLOW TIME
   60 IF((D(100)-Z(99)).GE.(PCB(35,IBAT)-DTM)) GO TO 155
      GO TO 15
  100 CALL COOLR(Z(98))
      Z(98)=0.0
      GO TO 50
  150 CALL COOLR(Z(98))
      CALL OUTTP(3)
      Z(97)=0.0
      Z(98)=0.0
      GO TO 60
C         END OF METRONOME DO-LOOP
   15 CONTINUE
  155 DTM=PCB(35,IBAT)-(D(100)-Z(99))
      ET=Z(98)+DTM
      IF(ET .LE.0.0) GO TO 25
  160 D(100)=D(100) + DTM
      CALL COOLR(ET )
      CALL OUTTP(3)
C         SAVE OIL TEMP AT END OF LINE AND MASTER CLOCK TIME
C         AFTER COOLING FOR USE BY SR OUTTM
      Z(250)=T(1,NDX+1)
      Z(251)=0.0
   25 RETURN
      END
      SUBROUTINE COOLR(THETA)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 029
C         CALLED BY NOFLO
C         THIS SR IMPLEMENTS THE COOLING EQN DESCRIBED IN SR NOFLO.
C         IT ALSO UPDATES CERTAIN OIL AND STEEL PROPERTIES, AND
C         KEEPS TRACK OF OIL,S VOLUMETRIC EXPANSION AND CONTRACTION.
C         STEEL,S THERMAL EXPANSION IS IGNORED.
C         ARGUMENT THETA=TIME,HRS, SINCE TEMPS WERE LAST CALCULATED
C         CLEAR SUMBOX FOR EXPANSION/CONTRACTION TANK
      Z(74)=0.0
    1 DO 2 K=1,NDX
      J=IT(1,K)
      I=IT(2,K)
C         CALC B TERM IN EQN, USING OLD PROPS
      Z(81)=T(11,K)*PCC(5,J)*PCC(41,J) + T(9,K)*T(10,K)*PCC(40,J)
C         CALC TOTAL RESISTANCE
      Z(85)=T(7,K) + T(8,K)
C         CALC EXPONENTIAL TERM IN EQN
      Z(82)= EXP(THETA/(Z(81)*(Z(85) +PCA(15))))
C         CALC NEW OIL TEMP FOR EACH DX
      T(1,K)=((T(1,K)-PCC(24,J))/Z(82)) + PCC(24,J)
      T(2,K)=T(1,K)
C         CALC NEW RF (W/ONLY FOULING AND STEEL COMPONENT)
      CALL RFOST(T(2,K),J,T(7,K))
C         CALC NEW RT
      TIN = (T(2,K) + PCC(24,J)) * 0.5
      CALL RINSO(TIN,J,T(8,K))
C         CALC SG AT TEMP
      Z(83)= PCB(22,I) + (PCB(3,I)*(T(1,K)-60.0))
C         CALC VOL EXPAN FACTOR  SG-60/SG-T
      T(20,K)= PCB(22,I)/Z(83)
C         CALC DENSITY, LBS/CF
      T(9,K)= Z(83) * 62.3
C         CALC NEW C(O) FOR OIL AT NEW TEMP
      T(10,K)= PCB(27,I) + (PCB(28,I)*T(1,K))
C         CALC NEW C(P) FOR STL AT NEW TEMP
      T(11,K)= PCC(34,J) + (PCC(35,J)*T(2,K))
C         CALC NEW STD BBLS WITHIN A DX SECTION
C         BBLS=(DX CAPACITY, STD BBLS) / (SG-60/SG-T)
      T(19,K)= PCC(42,J) / T(20,K)
C          ADD THESE STD BBLS OF OIL INTO SUMER
      Z(74)=Z(74) + T(19,K)
    2 CONTINUE
       T(1,NDX+1)=T(1,NDX)+T(1,NDX)-T(1,NDX-1)
      T(2,NDX+1)=T(1,NDX+1)
      RETURN
      END
      SUBROUTINE OUTTP(KEY)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 030
C         CALLED BY MP4,DYFLO,INTFAC, NOFLO
C         THIS SR WRITES A TEMP PROFILE FOR THE WHOLE PIPELINE.  IN
C         SOME INSTANCES, IT ALSO WRITES A DELTA P PROFILE.
C         ARGUMENT KEY=ID OF CALLING SR
C                     0=MP4 AFTER EQFLO CALCS
C                      1=MP4 W/ONLY T DATA FM INPUT
C                      2=DYFLO OR INTFAC
C                      3=NOFLO
C         WRITE TITLES
      CALL WTITL
C         SETUP COMPUTED GO-TO INDEX
      I=KEY + 1
      GO TO(10,11,12,13),I
C         WRITE HEADER LINE
   10 WRITE (M2,900)
      GO TO 14
   11 WRITE (M2,901)
      GO TO 14
   12 WRITE (M2,902)
      GO TO 14
   13 WRITE (M2,903)
  900 FORMAT(1H 17X,38HPIPELINE PROFILE DATA-EQUILIBRIUM FLOW)
  901 FORMAT(1H 17X,33HPIPELINE PROFILE DATA-INPUT TEMPS)
  902 FORMAT(1H 17X,34HPIPELINE PROFILE DATA-DYNAMIC FLOW)
  903 FORMAT(1H 17X,33HPIPELINE PROFILE DATA-NON FLOWING)
C         SKIP A LINE
   14 WRITE (M2,999)
  999 FORMAT(1H )
C         WRITE MORE HEADING DATA
      WRITE (M2,904) D(100)
  904 FORMAT(1H 17X,18HCLOCK TIME, HOURS-,10X,F7.2)
      WRITE (M2,905) IT(2,1)
  905 FORMAT(1H 17X,29HOIL BATCH ID. AT P.L. ORIGIN-,3X,I3)
      WRITE (M2,906) IT(2,NDX)
  906 FORMAT(1H 17X,31HOIL BATCH ID. AT P.L. TERMINUS-,1X,I3)
C         DELETE FLOW LINE IF TYPE 3 ARGUMENT
      IF(KEY-3)15,16,15
   15 K=IT(2,1)
      WRITE (M2,907) PCB(32,K)
  907 FORMAT(1H 17X,26HOIL PUMPING RATE, STD B/D-,F9.1)
      GO TO 17
C         FOR COOLING TEMP PROFILE, PRINT VOL SHRINKAGE, STD BBLS
   16 VOL= Z(74) - Z(73)
      WRITE (M2,950) VOL
  950 FORMAT(1H 17X,28HLINEFILL SHRINKAGE,STD BBLS-,F7.1)
C         CARRYING ON BRAVELY, SKIP A LINE
   17 WRITE (M2,999)
C         AGAIN, PERFORM COMPUTED GO-TO
      GO TO(25,20,25,20),I
C         FOR KEY=1 AND KEY=3, ZERO DELTA P LINE IN T MTX
   20 DO 21 K=1,NDX
   21 T(12,K)=0.0
C         NOW WRITE COL HEADINGS
   25 WRITE (M2,909)
      WRITE (M2,910)
      WRITE (M2,911)
      WRITE (M2,912)
      WRITE (M2,999)
  909 FORMAT(1H 7X,9HPL LENGTH)
  910 FORMAT(1H 7X,10HFM ORIGIN,,3X,9HOIL TEMP,,3X,11HSTEEL TEMP,3X,
     1      10HSOIL TEMP,3X,10HCUMULATIVE)
  911 FORMAT(1H 9X,5HMILES,8X,5HDEG F,7X,5HDEG F,9X,5HDEG F,6X,
     1 12HDELTA P, PSI)
  912 FORMAT(1H 7X,10H----------,3X,9H---------,3X,11H-----------,3X,9H-
     1--------,3X,12H------------)
C         CALC RATIO, DELTA MILES FOR OUTPUT/DX
      DXR= D(6)/D(4)
      IF(DXR-1.0)30,30,31
C         RESET TO ONCE EVERY DX
   30 DXR=1.0
C         TRUNCATE TO SMOOTH VALUE
   31 IXR=DXR + 0.001
C         CLEAR DX SUMER AND DP SUMER
      SDX=0.0
      SDP=0.0
C         PRINT START OF P.L. DATA (AT ORIGIN)
      J=IT(1,1)
      WRITE (M2,913) SDX,T(1,1),T(2,1),PCC(24,J),SDP
  913 FORMAT(1H 6X,F8.2,6X,F8.2,4X,F8.2,6X,F8.2,5X,F8.1)
C         SET UP DO-LOOP FOR PRINTING EVERY DELTA-M MILES
      JJ= 0
      KK=NDX-1
      DO 35 I=1,KK
      JJ = JJ + 1
      SDX=SDX + D(4)
      SDP=SDP + T(12,I)
      IF(I.EQ.KK) GO TO 32
      IF(IT(2, I).EQ.IT(2, I+1)) GO TO 32
      X=  I - 1
      XM=(X+(1.0-T(21, I)))*D(4)
      WRITE(M2,914) XM
  914 FORMAT(1H 8X,9HINTERFACE,F6.1,19H MILES FROM ORIGIN.)
C         SEE IF IT,S TIME TO PRINT YET
   32 IF(IXR.NE.JJ) GO TO 35
C         PRINT A LINE
   33 J=IT(1,I)
      WRITE (M2,913) SDX,T(1,I+1),T(2,I+1),PCC(24,J),SDP
      JJ = 0
   35 CONTINUE
C         NOW, PRINT AT THE TERMINUS
      SDX=SDX + D(4)
      SDP=SDP + T(12,NDX)
      J=IT(1,NDX)
      WRITE (M2,913) SDX,T(1,I+1),T(2,I+1),PCC(24,J),SDP
C         THAT,S ALL
      RETURN
      END
      SUBROUTINE OUTTM(CTIME,KEY)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 031
C         CALLED BY EQFLO, DYFLO, INTFAC
C         THIS SR WRITES TERMINUS OUTPUT. THE ARGUMENT =KEY= DEFINES
C         (WHEN ZERO) A NORMAL CALL, AND (WHEN NON-ZERO) AN INTERFACE
C         OUTPUT.  THIS SR IS CALLED ONCE FOR EACH LINE OF OUTPUT.
C
C         SEE IF THIS IS THE VERY FIRST TIME FOR THIS OUTPUT ON THIS
C         OIL BATCH
      IF(Z(251).GT.0.0) GO TO 3
C         WRITE PAGE TITLE AND COLUMN HEADERS
    2 CALL TMHDR
C         WRITE ZERO TIME AND TEMP FLOWING AT TERMINUS-1ST DROP OF OIL
      WRITE (M2,901)Z(252), Z(250),Z(253)
  901 FORMAT(1H F7.3,2X,F7.2,49X,F7.1)
C         NOW FOR ROUTINE USAGE OF THIS SR
C         1ST SEE IF 18 CALLS FOR OUTPUT ON THIS BATCH HAVE OCCURRED
    3 IF(Z(251)-18.0)5,5,4
C         IF SO WRITE FRESH HEADER
    4 CALL TMHDR
      Z(251) = 0.0
C         NOW, WRITE 1 LINE OF DATA
      WRITE (M2,901)Z(252), Z(250),Z(253)
    5 I=NDX +1
      VOL1=T(19,I) * T(20,I)
      II= 114 + IT(2,I)
      VOL2= (T(19,I) / (CTIME - Z(252))) * 24.0
      WRITE (M2,902) IT(2,I),VOL1,Z(II),VOL2
  902 FORMAT(1H 22X,I2,5X,F9.1,4X,F8.1,3X,F9.1)
C         BEFORE THE 2ND LINE, SEE IF ITS AN INTERFACE LINE
      IF(KEY-1)6,7,7
C         ROUTINE LINE PRINT AT END OF DELTA VOLUME
    6 WRITE (M2,903) CTIME, T(1,I), T(12,I)
  903 FORMAT(1H F7.3,2X,F7.2,49X,F7.1)
C         SAVE CLOCKTIME
      Z(252)= CTIME
      GO TO 9
C         END OF INTER FACE FOR THIS BATCH
    7 WRITE (M2,904) CTIME, T(1,I), T(12,I)
  904 FORMAT(1H F7.3,2X,F7.2,13X,30HTRAILING INTERFACE AT TERMINUS,6X,
     1 F7.1)
C         SAVE CLOCKTIME
      Z(252)= CTIME
      Z(250)=T(1,NDX+1)
      Z(251)=0.0
      Z(253)=T(12,NDX+1)
      GO TO 10
C         UPDATE THE CALL COUNTER FOR THIS EXITING BATCH  SAVE CLOCKTIME
    9 Z(251)=Z(251) + 1.0
      Z(253) = T(12,NDX+1)
      Z(250) = T(1,NDX+1)
   10 CONTINUE
      RETURN
      END
      SUBROUTINE TMHDR
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 032
C         CALLED BY SR OUTTM
C         THIS SR WRITES HEADING FOR THE TERMINUS OUTPUT ROUTINE
    1 CALL WTITL
      WRITE (M2,900)
      WRITE (M2,999)
      WRITE (M2,901)
      WRITE (M2,902)
      WRITE (M2,903)
      WRITE (M2,904)
      WRITE (M2,999)
  900 FORMAT(1H 20X,31HOIL EXIT CONDITIONS AT TERMINUS)
  999 FORMAT(1H )
  901 FORMAT(1H 2X,5HCLOCK,22X,9HAVG DELTA,4X,10HCUMUL EXIT,3X,8HAVG EXI
     1T)
  902 FORMAT(1H 2X,5HTIME,,2X,8HOIL EXIT,2X,8HEXIT OIL,4X,7HVOLUME,,6X,
     1 7HVOLUME,,3X,9HFLOWRATE,,2X,8HFRICTION)
  903 FORMAT(1H 3X,3HHRS,3X,7HTEMP, F,3X,8HBATCH ID,2X,11HACTUAL BBLS,
     13X,8HSTD BBLS,3X,7HSTD B/D,4X,7HDELTA P)
  904 FORMAT(1H 1X,6H------,2X,8H--------,2X,8H--------,2X,11H----------
     1-,2X,9H---------,2X,10H----------,2X,8H--------)
      RETURN
      END
      SUBROUTINE PAGEX
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 033
C         CALLED BY SR MP4
C         THIS SR DUMPS THE DIMENSION STATEMENT UPON DEMAND
C
    1 CALL WTITL
      WRITE (M2,900)
  900 FORMAT(1H 34H**** A DIMENSION DUMP FOLLOWS ****)
C         SKIP 2 SPACES
      WRITE(M2,998)
  998 FORMAT(1H )
      WRITE(M2,998)
C         DUMP PCA VECTOR
      CALL PRNTV(PCA(1),50,4HPCA ,1,1)
C         DUMP PCB MATRIX
      CALL PRNTM(PCB(1,1),50,1,10,4HPCB ,1)
C         DUMP PCC MATRIX
      CALL PRNTM(PCC(1,1),50,1,20,4HPCC ,1)
      WRITE(M2,999)
  999 FORMAT(1H1)
C         DUMP D VECTOR
      CALL PRNTV(D(1),300,4HD   ,1,1)
C         DUMP Z VECTOR
      CALL PRNTV(Z(1),300,4HZ   ,1,1)
C         DUMP T AND IT MTX
      CALL PRNTM(T(1,1),22,1,NDX+1,4HT   ,2)
C         THAT,S A DIMENSION DUMP
      RETURN
      END
      SUBROUTINE PRNTV(REGV,LNGTH,ITITL,IPOS,KEY)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 034
      DIMENSION REGV(2),IREG(5),WREG(5)
C         CALLED BY SR PAGEX
C         THIS SR DUMPS A VECTOR IN E-FORMAT, 5 DATA POINTS/LINE.
C         ARGUMENTS REGV=1ST LOCN TO BE DUMPED
C                  LNGTH=LENGTH OF VECTOR TO BE DUMPED
C                  ITITL=A 4-HOLLERITH VECTOR NAME
C                   IPOS=SUBSCRIPT NO. OF 1ST VECTOR (FOR LABELING
C                        OUTPUT
C                    KEY=ON-OFF KEY FOR PRINTING. IF ZERO, ITS OFF.
C                        IF NONZERO, IT,S ON AND OUTPUT WILL BE LISTED.
C
    1 IF(KEY)2,30,2
    2 IF(LNGTH)30,30,3
C        SPACE BEFORE PRINTING
    3 WRITE (M2,901)
  901 FORMAT(1H )
      DO 10 I=1,LNGTH,5
      DO 4  K=1,5
      I3=I+K-1
      IREG(K)=(IPOS-1) + I3
    4 WREG(K)= REGV(I3)
      IF(I+4-LNGTH)5,5,6
    5 I2=5
      GO TO 7
    6 I2=LNGTH-I+1
C        OMIT WRITING IF ALL 5 VALUES ON A LINE ARE ZERO
    7 DO 8 J=1,5
      I3=J + I2 -5
      IF(WREG(I3))9,8,9
    8 CONTINUE
      GO TO 10
C        OKAY,SO WRITE 5
    9 WRITE (M2,902) ITITL,((IREG(L),WREG(L)),L=1,I2)
  902 FORMAT(1H A4,1X,5(I4,E17.8,1X))
   10 CONTINUE
   30 RETURN
      END
      SUBROUTINE PRNTM(REGM,NR,NC1,NC2,ITITL,KEY)
      DIMENSION TITLE(22),PCA(50),PCB(50,10),PCC(50,20),D(300),Z(300)
      DIMENSION T(22,501), IT(2,501)
      COMMON TITLE,PCA,PCB,PCC,D,Z,T,IT,M1,M2,NTR,NTC,MNB,MNP,NDX
C 035
C         CALLED BY PAGEX
      DIMENSION REGM(2),REGV(10),IPOS(10)
C         THIS SUBROUTINE PRINTS A MATRIX DUMP FOR ANY MATRIX
C         IT HANDLES 10 COLS PER PAGE AND PRINTS IN E-FORMAT.
C         ARGUMENTS REGM= THE FIRST MATRIX LOCN TO BE DUMPED. IT
C                         MUST REFER TO ROW 1, IN ANY COLUMN, E.G.
C                         ARE- T(1,1), A(1,5), ... ETC.
C                     NR=NO. OF ROWS USED IN DIMEN.ST.FOR THIS MTX
C                    NC1=THE NO OF THE 1ST COLUMN TO BE DUMPED
C                    NC2=THE NO OF THE LAST COL TO BE DUMPED
C                  ITITL=THE 4-HOLLERITH NAME OF THE MTX
C                    KEY=ON-OFF KEY. 0=OFF, 1=ON, 2=ON WITH SPEC DUMP
C
C         TEST FOR KEY OFF
      IF(KEY)30,30,1
C         SET-UP DO-LOOP FOR 10 COLS PER PG
    1 NC=NC2-NC1 + 1
      DO 20 IC=1,NC,10
C         SEE HOW MANY COLUMNS REMAIN
      IF(NC - (IC+9))2,3,3
    2 LEN= NC - IC + 1
      GO TO 4
    3 LEN=10
C         WRITE A FRESH PAGE HEADER, W/COL NUMBERS
    4 WRITE (M2,900) ITITL
  900 FORMAT(1H110X,19H**** A DUMP OF THE ,A4,20H MATRIX FOLLOWS ****)
      WRITE (M2,999)
      WRITE (M2,999)
  999 FORMAT(1H )
      I1=NC1 +  IC  - 1
      I2=I1 +LEN - 1
      I4 = 0
      DO 5 I3=I1,I2
      I4 = I4 + 1
    5 IPOS(I4)=I3
      WRITE (M2,901) (IPOS(I),I=1,LEN)
  901 FORMAT(1H 5H COL=,10(4X,I4,4X))
      WRITE (M2,999)
C         SET UP THE  WRITE-LOOP FOR ALL ROWS
      DO 10 K1=1,NR
C         TRANSFER ONE ROW OF DATA INTO REGV
      DO 6 K2=1,LEN
      K3=K1+(K2*NR)-NR + (I1-1)*NR
    6 REGV(K2)= REGM(K3)
C         TEST THE ROW FOR ALL-ZERO CONDITION
      DO 7 K2=1,LEN
      IF(REGV(K2))8,7,8
    7 CONTINUE
C         IF WE GET TO HERE, ROW IS ALL-ZERO, SKIP IT
      GO TO 10
C         WRITE A ROW
    8 WRITE (M2,902) K1,(REGV(K4),K4=1,LEN)
  902 FORMAT(1H I4,1X,10(E12.5))
C         END OF ROW WRITING
   10 CONTINUE
C         SEE IF SPECIAL KEY SETTING IS ON
   11 IF(KEY-2)20,12,20
C         IT IS.  PRINT CONTENTS OF THE TWO ROWS OF THE IT MTX
C         THIS IS A NON-GENERAL FEATURE AND MAY BE PULLED FOR
C         USE OF THIS SR IN OTHER PROGRAMS.
   12 WRITE (M2,999)
      WRITE (M2,999)
      WRITE (M2,904) (IT(1,K4),K4=I1,I2)
      WRITE (M2,905) (IT(2,K4),K4=I1,I2)
  904 FORMAT(1H 4HIT-1,10(4X,I4,4X))
  905 FORMAT(1H 4HIT-2,10(4X,I4,4X))
C         END OF THIS PAGE OF 1-10 COLUMNS
   20 CONTINUE
   30 CONTINUE
      RETURN
      END
      SUBROUTINE DEFINE
C
C 036
C         CALLED BY MAIN PROGRAM
C         THIS SUBROUTINE IS A DUMMY THAT IS USED TO DEFINE PROGRAM
C         STORAGE ASSIGNMENTS.
C
C   ***** PCA VECTOR (LENGTH=50)
C   ***** MISC PROGRAM CONSTANTS
C PCA    1     DIFFERENTIAL P.L. LENGTH (FOR CALCULUS), MILES
C PCA    2     TIME SPAN TO BE USED FOR TERMINUS OUTPUT, HOURS
C PCA    3     P.L. SPAN TO BE USED FOR PROFILE OUTPUT, MILES
C PCA    4     MULTIPLYING FACTOR FOR DELTA THETA MANIPULATION
C PCA    5     MAX. ALLOWABLE VALUE OF DIFF. P.L. LENGTH, MILES
C PCA    6     PC INPUT LISTING SWITCH. ZERO=OFF  1.0=ON
C PCA    7     CASE INPUT LISTING SWITCH.ZERO=OFF 1.0=ON
C PCA    8     ITERATION COUNT-BOX IN SR EQFLO HEAT BALANCING
C PCA    9     MAX ITERATIONS ALLOWED IN SR TPROF TEMP SEARCH
C PCA   10     MAX TEMP ERROR ALLOWED IN SR TPROF TEMP SEARCH, DEG F
C PCA   11     TIME FREQUENCY FOR UPDATING PROPERTIES IN SR NOFLO, HRS
C PCA   12     TIME INCREMENT OF METRONONE IN SR NOFLO, HRS
C PCA   13     SPARE
C PCA   14
C PCA   15     SR NOFLO COR FACTOR, FF(SEE MATH DOC. EQN NO. 5)
C PCA   16     SPARE
C PCA   17
C PCA   18
C PCA   19
C PCA   20
C PCA   21
C PCA   22
C PCA   23
C PCA   24
C PCA   25
C PCA   26
C PCA   27     NO. OF OIL BATCHES TO FOLLOW IN INPUT (3 CARDS/BATCH)
C PCA   28     NO. OF P.L. SEGMENTS TO FOLLOW IN INPUT (4/SEGMENT)
C PCA 29-50    SPARE
C
C
C   ***** PCB MATRIX  (SIZE-50 ROWS, BY I OIL BATCHES,I=10)
C   ***** OIL BATCH DATA, ONE BATCH PER COLUMN
C PCB  1,I     OIL BATCH CODE NUMBER
C PCB  2,I     GRAVITY, DEG API
C PCB  3,I     COEFFICIENT OF VOLUMETRIC EXPANSION
C PCB  4,I     TEMPERATURE, DEG F, FOR FIRST VISCOSITY DATA
C PCB  5,I     TEMPERATURE, DEG F, FOR SECOND VISCOSITY DATA
C PCB  6,I     VISCOSITY AT FIRST TEMPERATURE, CENTISTOKES
C PCB  7,I     VISCOSITY AT SECOND TEMPERATURE, CENTISTOKES
C PCB  8,I     TEMP.,DEG F FOR FIRST THERMAL COND. DATA
C PCB  9,I     TEMP.,DEG F FOR SECOND THERMAL COND. DATA
C PCB 10,I     THERMAL COND. AT FIRST TEMP., BTU-FT/HR-SQFT-DEG F
C PCB 11,I     THERMAL COND. AT SECOND TEMP.,BTU-FT/HR-SQFT-DEG F
C PCB 12,I     SPARE
C PCB 13,I
C PCB 14,I
C PCB 15,I     TEMP.,DEG F FOR FIRST HEAT CAPACITY DATA
C PCB 16,I     TEMP.,DEG F FOR SECOND HEAT CAPACITY DATA
C PCB 17,I     HEAT CAPACITY AT FIRST TEMP., BTU/LB-DEG F
C PCB 18,I     HEAT CAPACITY AT SECOND TEMP.,BTU/LB-DEG F
C PCB 19,I     POUR POINT, DEG F (INPUT NOT USED BY PROGRAM)
C PCB 20,I     SULFUR CONTENT, WT PERCENT (INPUT NOT USED BY PROGRAM)
C PCB 21,I     REID VAPOR PRESSURE, PSIA (INPUT NOT USED BY PROGRAM)
C PCB 22,I     SPECIFIC GRAVITY AT 60 DEG F
C PCB 23,I     TERM A IN VISCOSITY EQN., Z=A*EXP(B/T), T IN DEG R
C PCB 24,I     TERM B IN VISCOSITY EQN., Z=A*EXP(B/T), T IN DEG R
C PCB 25,I     TERM A IN THERMAL COND EQN., K=A+B*T, T IN DEG F
C PCB 26,I     TERM B IN THERMAL COND EQN., K=A+B*T, T IN DEG F
C PCB 27,I     TERM A IN HEAT CAPACITY EQN., C(P)=A+B*T, T IN DEG F
C PCB 28,I     TERM B IN HEAT CAPACITY EQN., C(P)=A+B*T, T IN DEG F
C PCB 29,I     SPARE
C PCB 30,I
C PCB 31,I     OIL BATCH CODE NO
C PCB 32,I     FLOWRATE DURING PUMPING, STD BBL/DAY
C PCB 33,I     PIPELINE ENTRY TEMPERATURE, DEG F
C PCB 34,I     PUMPING TIME FOR THIS BATCH, HOURS
C PCB 35,I     NO-FLOW TIME AT END OF THIS BATCH, HOURS
C PCB 36,I     FREQUENCY OF TEMPERATURE PROFILE OUTPUT, HOURS
C PCB 37,I     KEY FOR OIL PROPERTY DATA
C PCB 38-50    SPARE
C
C     C
C   ***** PCC MATRIX  (SIZE-50 ROWS, BY J PIPELINE SEGMENTS,J=20)
C   ***** PIPELINE SEGMENT DATA, ONE SEGMENT PER COLUMN
C PCC  1,J     SEGMENT ID CODE NUMBER
C PCC  2,J     LENGTH OF SEGMENT, MILES
C PCC  3,J     OUTSIDE DIAMETER OF PIPE, INCHES
C PCC  4,J     WALL THICKNESS OF PIPE, INCHES
C PCC  5,J     DENSITY OF PIPE MATERIAL, LBS/CUFT
C PCC  6,J     FOULING FACTOR OF SCALE OR DIRT, HR-SQFT-DEG F/BTU
C PCC  7,J     SPARE
C PCC  8,J     TEMP. ,DEG F,FOR FIRST DATA ON PIPELINE PROPERTIES
C PCC  9,J     TEMP. ,DEG F,FOR SECOND DATA ON PIPELINE PROPERTIES
C PCC 10,J     THERMAL COND OF PIPE AT FIRST TEMP.,BTU-FT/HR-SQFT-DEG F
C PCC 11,J     THERMAL COND OF PIPE AT SECOND TEMP.,BTU-FT/HR-SQFT-DEG F
C PCC 12,J     HEAT CAPACITY OF PIPE AT FIRST TEMP., BTU/LB-DEG F
C PCC 13,J     HEAT CAPACITY OF PIPE AT SECOND TEMP, BTU/LB-DEG F
C PCC 14,J     SPARE
C PCC 15,J     INSULATION TYPE CODE (INPUT NOT USED BY PROGRAM)
C PCC 16,J     THICKNESS OF INSULATION, INCHES
C PCC 17,J     BULK DENSITY OF INSULATION, LBS/CUFT (NOT USED BY PROG)
C PCC 18,J     THER COND OF INSL AT FIRST TEMP., BTU-FT/HR-SQFT-DEG F
C PCC 19,J     THER COND OF INSL AT SECOND TEMP.,BTU-FT/HR-SQFT-DEG F
C PCC 20,J     HEAT CAP OF INSL AT FIRST TEMP., BTU/LB-DEG F
C PCC 21,J     HEAT CAP OF INSL AT SECOND TEMP.,BTU/LB-DEG F
C PCC 22,J     SOIL TYPE CODE (INPUT NOT USED BY PROGRAM)
C PCC 23,J     DEPTH OF SOIL COVER, INCHES
C PCC 24,J     GROUND TEMPERATURE, DEG F
C PCC 25,J     THER COND OF SOIL AT GROUND TEMP,BTU-FT/HR-SQFT-DEG F
C PCC 26,J     SPARE
C PCC 27,J
C PCC 28,J
C PCC 29,J     INSIDE DIAMETER OF PIPE, INCHES
C PCC 30,J     INSIDE DIAMETER OF PIPE, FEET
C PCC 31,J     OUTSIDE DIAMETER OF PIPE, FEET
C PCC 32,J     TERM A IN PIPE THER COND EQN., K=A+B*T,T IN DEG F
C PCC 33,J     TERM B IN PIPE THER COND EQN., K=A+B*T,T IN DEG F
C PCC 34,J     TERM A IN PIPE HEAT CAP  EQN., C(P)=A+B*T,T IN DEG F
C PCC 35,J     TERM B IN PIPE HEAT CAP  EQN., C(P)=A+B*T,T IN DEG F
C PCC 36,J     TERM A IN INSL THER COND EQN., K=A+B*T,T IN DEG F
C PCC 37,J     TERM B IN INSL THER COND EQN., K=A+B*T,T IN DEG F
C PCC 38,J     TERM A IN INSL HEAT CAP  EQN., C(P)=A+B*T, T IN DEG F
C PCC 39,J     TERM B IN INSL HEAT CAP  EQN., C(P)=A+B*T, T IN DEG F
C PCC 40,J     CROSS SECTIONAL AREA OF INSIDE PIPE, SQFT
C PCC 41,J     CROSS SECTIONAL AREA OF PIPE ANNULUS, SQFT
C PCC 42,J     CAPACITY OF A DELTA X LENGTH OF THIS SEGMENT, STD BBLS
C PCC 43-50,J  SPARE
C
C
C   ***** D VECTOR  (LENGTH=300)
C   ***** MISC INPUT VALUES AND CALCULATED VALUES
C D      1     STUDY TYPE KEY - EQUILIBRIUM OR DYNAMIC
C D      2     NUMBER OF OIL BATCHES TO BE PUMPED DURING THIS RUN
C D      3     KEY DENOTING IF TEMP PROFILE DATA IS TO BE INPUT
C D      4     DIFFERENTIAL PIPELINE LENGTH TO BE USED IN CALCS, MILES
C D      5     TIME SPAN USED FOR OUTPUT OF TERMINUS CONDITIONS, HOURS
C D      6     PIPELINE SPAN TO BE USED FOR PROFILE OUTPUT, MILES
C D      7     KEY DENOTING IF VECTOR DUMP IS DESIRED
C D    8-27    SEGMENT ID CODE NO FOR SUCCESSIVE PIPELINE CONNECTIONS
C D   28-50    SPARE
C D   51-57    SCRATCH AREA FOR INPUTING OIL BATCH DATA
C D   58-60    SPARE
C D     61     OIL BATCH CODE NUMBER OF 1ST  OIL PUMPED
C D     62     OIL BATCH CODE NUMBER OF 2ND  OIL PUMPED
C D     63     OIL BATCH CODE NUMBER OF 3RD  OIL PUMPED
C D     64     OIL BATCH CODE NUMBER OF 4TH  OIL PUMPED
C D     65     OIL BATCH CODE NUMBER OF 5TH  OIL PUMPED
C D     66     OIL BATCH CODE NUMBER OF 6TH  OIL PUMPED
C D     67     OIL BATCH CODE NUMBER OF 7TH  OIL PUMPED
C D     68     OIL BATCH CODE NUMBER OF 8TH  OIL PUMPED
C D     69     OIL BATCH CODE NUMBER OF 9TH  OIL PUMPED
C D     70     OIL BATCH CODE NUMBER OF 10TH OIL PUMPED
C D   71-80    SPARE
C D   81-99    SPARE
C D    100     MASTER CLOCK TIME SINCE PUMPING BEGAN, HOURS
C D  101-128   MILEAGE AND TEMP FOR INPUT OIL TEMP PROFILES
C D  129-150   SPARE
C D  151-164   CALC MILEAGE -  OIL AND STEEL PROFILE DATA
C D  165-170   SPARE
C D  171-184   CALC TEMPERATURES - OIL AND STEEL PROFILE DATA
C D  185-200   SPARE
C D  201-228   MILEAGE AND TEMP FOR INPUT STEEL TEMP PROFILES
C D  229-300   SPARE
C
C
C   ***** Z VECTOR  (LENGTH= 300)
C   ***** MISC SCRATCH STORAGE FOR INTERMEDIATE CALCULATED VALUES
C Z    1-100   MISC SCRATCH - TEMPORARY VALUES
C Z    101     TOTAL ITERATIONS IN TIME INCREMENT
C Z    102     TIME ELASPED SINCE PUMPING BEGAN ON CURRENT BATCH, HOURS
C Z    103     TIME INTERVAL USED IN CALCULATIONS, HOURS
C Z    104     TOTAL NO OF BATCHES EXITED SINCE THIS RUN BEGAN
C Z  105-114   TOTAL STD BBLS IN OF BATCH I, I=1,10
C Z  115-124   TOTAL STD BBLS OUT OF BATCH I ,I=1,10
C Z  125-134   AVG TEMP OF BATCH I AT OUTLET, I=1,10  , DEG F
C Z  145-146   SPARE
C Z    147     Q IN AT INLET OVER THIS TIME INTERVAL, MMBTU
C Z    148     TOTAL Q DUE TO FRICTION OVER THIS TIME INTERVAL, MMBTU
C Z    149     Q OUT IN OIL AT TERMINUS OVER THIS TIME INTERVAL, MMBTU
C Z    150     Q OUT TO SOIL OVER THIS TIME INTERVAL, MMBTU
C Z    151     TOTAL Q OUT OVER THIS TIME INTERVAL, MMBTU
C Z    152     CUM. Q IN AT INLET SINCE PUMPING ON THIS BATCH
C Z    153     Q CONTENT IN OIL AT START OF PUMPING ON THIS BATCH
C Z    154     Q CONTENT IN STEEL AT START OF PUMPING ON THIS BATCH
C Z    155     CUM. Q DUE TO FRICTION SINCE PUMPING ON THIS BATCH
C Z    156     CUM. Q OUT AT TERM. SINCE PUMPING BEGAN ON THIS BATCH
C Z    157     CUM. Q OUT TO SOIL SINCE PUMPING BEGAN ON THIS BATCH
C Z    158     CUM. Q OUT (TOTAL) SINCE PUMPING BEGAN ON THIS BATCH
C Z    159     CUM. Q IN LESS GAIN IN OIL AND STEEL
C Z    160     VELOCITY        OF OIL CURRENTLY BEING PUMPED AT INLET
C Z    161     REYNOLDS NO     OF OIL CURRENTLY BEING PUMPED AT INLET
C Z    162     R(F)            OF OIL CURRENTLY BEING PUMPED AT INLET
C Z    163     R(T)            OF OIL CURRENTLY BEING PUMPED AT INLET
C Z    164     DENSITY         OF OIL CURRENTLY BEING PUMPED AT INLET
C Z    165     HEAT CAPACITY   OF OIL CURRENTLY BEING PUMPED AT INLET
C Z    166     PIPE HEAT CAPACITY AT INLET
C Z    167     PRESSURE DROP AT INLET CONDITIONS
C Z    168     FRICTIONAL HEAT AT INLET CONDITIONS
C Z    169     RATIO OF SG AT 60 TO SG AT TEMP AT INLET CONDITIONS
C Z    170     NO OF DIFFERENT BATCHES PUMPED UP TO CURRENT TIME
C Z  171-196   SPARE
C Z    197     ITER COUNT BOX IN SR EQFLO HEAT BALANCE CONVERGENCE
C Z    198     CUM. NO. OF TIMES ITERATION LOOP EXCEEDED IN SR TPROF
C Z    199     CUM. ITERATIONS FOR TEMP SEARCH IN SR TPROF
C Z  200-224   MISC SCRATCH -TEMPORARY VALUES
C Z    225     AVG TEMP OF CURRENTLY EXITING BATCH SINCE LAST OUTPUT
C Z  226-249   SPARE
C Z    250     OIL TEMP AT OUTLET AT TIME ZERO OR AT END OF COOLDOWN
C Z    251     CALL COUNTER FOR SR OUTTM
C Z    252     CLOCK TIME AT PREVIOUS CALL OF SR OUTTM
C Z    253     CUMULATIVE DELTA P AT LAST CALL OF SR OUTTM
C Z  254-300   SPARE
C
C
C   ***** T MATRIX (SIZE-22 ROWS BY K (DELTA X+1) INCREMENTS, K=NTR)
C   ***** TEMP AND PROPERTIES OF PIPE AND CONTENTS AT A POINT IN TIME
C   ***** AVERAGE PROPERTIES OVER DELTA X LENGTH ,ONE DELTA X PER
C   ***** COLUMN
C T    1,K     OIL TEMP AT START OF DELTA X AND START OF TIME INTERVAL
C T    2,K     PIPE TEMP AT START OF DELTA X AND START OF TIME INTERVAL
C T    3,K     OIL TEMP AT START OF DELTA X AND END OF TIME INTERVAL
C T    4,K     PIPE TEMP AT START OF DELTA X AND END OF TIME INTERVAL
C T    5,K     VELOCITY, FT/HR
C T    6,K     REYNOLDS NO IN SPECIAL UNITS
C T    7,K     COMB FLUID FILM AND SCALE RESISTANCE, HR-FT-DEG F/BTU
C T    8,K     COMB INSL AND SOIL RESISTANCE, HR-FT-DEG F/BTU
C T    9,K     OIL DENSITY, LB/CUFT
C T   10,K     OIL HEAT CAPACITY, BTU/LB-DEG F
C T   11,K     PIPE HEAT CAPACITY, BTU/LB-DEG F
C T   12,K     PRESSURE DROP OVER DELTA X , PSI
C T   13,K     FRICTIONAL HEAT, BTU/FT-HR
C T   14,K     HEAT CONTAINED IN OIL AT START OF TIME INTERVAL, MMBTU
C T   15,K     HEAT CONTAINED IN OIL AT END   OF TIME INTERVAL, MMBTU
C T   16,K     HEAT CONTAINED IN PIPE AT START OF TIME INTERVAL, MMBTU
C T   17,K     HEAT CONTAINED IN PIPE AT END   OF TIME INTERVAL, MMBTU
C T   18,K     RATE OF HEAT LOSS TO SOIL, MMBTU/HR
C T   19,K     STANDARD BBL OF OIL CONTAINED IN DELTA X
C T   20,K     RATIO OF OIL SP GR AT 60 DEG F TO SP GR AT TEMP
C T   21,K     KEY DENOTING FRACTION OF BATCH REMAINING IN DELTA X
C T   22,K     SPARE
C
C
C   ***** IT MATRIX  (SIZE-2 ROWS BY L (DELTA X+1) INCREMENTS, L=NTR)
C   ***** BATCH ID CODE AND PIPE SEGMENT CODE IN DELTA X, ONE DELTA X
C   ***** PER COLUMN
C IT   1,L     PIPE SEGMENT CODE NUMBER
C IT   2,L     OIL BATCH CODE NUMBER
C
C
C   ***** NON SUBSCRIPTED VARIABLES
C M1           ID FOR INPUT DEVICE
C M2           ID FOR OUTPUT DEVICE
C NTR          MAX NO OF ROWS IN T AND IT MATRICES
C NTC          MAX NO OF COLUMNS IN T MATRIX
C MNB          MAX NO OF OIL BATCHES
C MNP          MAX NO OF PIPELINE SEGMENTS
C NDX          NO OF DELTA X INCREMENTS ALONG PIPELINE FOR CALCS
      A= 1.0 * 1.0
      RETURN
      END
   d @