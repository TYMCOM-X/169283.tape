      SUBROUTINE FOURAN
        COMMON/MISCEL/NOGO,IGOOF,IDI,IDO
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/TRAN/JTRFLG,TSTEP,TSTOP,TSTART,NOTINT,STEPS(5),ENDPTS(5),
     1   KFROUT,FORFRE,KFPTS
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
C
C
      DIMENSION SINCO(9),COSCO(9)
C
C
      XN=KFPTS
      DCCO=0.0
      DO 10 I=1,9
      SINCO(I)=0.0
   10 COSCO(I)=0.0
      ISTART=ICALC-KFPTS
      IKNT=ISTART
   20 IKNT=IKNT+1
      IF (IKNT.GE.ICALC) GO TO 40
      DCCO=DCCO+ROUT(IKNT,KFROUT)
      DO 30 J=1,9
      ARG=TWOPI*FLOAT(IKNT-ISTART)*FLOAT(J)/XN
      SINCO(J)=SINCO(J)+ROUT(IKNT,KFROUT)*SIN(ARG)
      COSCO(J)=COSCO(J)+ROUT(IKNT,KFROUT)*COS(ARG)
   30 CONTINUE
      GO TO 20
   40 DCCO=(DCCO+0.5*(ROUT(ISTART,KFROUT)+ROUT(ICALC,KFROUT)))/XN
      DO 50 J=1,9
      SINCO(J)=2.0*SINCO(J)/XN
      COSCO(J)=(2.0*COSCO(J)+ROUT(ISTART,KFROUT)+ROUT(ICALC,KFROUT))/XN
   50 CONTINUE
      WRITE (IDO,61)TEMPS(ITEMNO)
61      FORMAT(/1X,'*****FOURIER ANALYSIS     TEMPERATURE',F10.3,
     1  ' DEG C'/)
      IKNT=IOVAR(KFROUT,2)
      WRITE (IDO,71) IONAM(IKNT)
71      FORMAT(1X,'FOURIER COMPONENTS OF TRANSIENT RESPONSE',
     1  ' OF: ',A5)
        WRITE (IDO,72)DCCO
72      FORMAT(1X,'DC COMPONENT: ',1PE10.3/)
        WRITE (IDO,73)
73      FORMAT(1X,'HARMONIC  FREQUENCY  FOURIER  NORMALIZED  ',
     1  ' PHASE  NORMALIZED')
        WRITE (IDO,74)
74      FORMAT(1X,' NUMBER      (HZ)   COMPONENT COMPONENT',
     1  '    (DEG)  PHASE(DEG)')
      IKNT=1
      FREQ1=FORFRE
      XNORM=SQRT(SINCO(1)*SINCO(1)+COSCO(1)*COSCO(1))
      XNHARM=1.0
      IF (XNORM.LT.1.0E-20) GO TO 75
      PNORM=RAD*ATAN2(COSCO(1),SINCO(1))
      GO TO 76
75    PNORM=0.0
76    PHASEN=0.0
      WRITE (IDO,81) IKNT,FREQ1,XNORM,XNHARM,PNORM,PHASEN
81      FORMAT(5X,I1,5X,1PE9.2,1X,E9.2,1X,E9.2,1X,0F8.1,1X,F8.1)
      IF (XNORM.LT.1.0E-20) XNORM=1.0E-20
      THD=0.0
   90 IKNT=IKNT+1
      IF (IKNT.GT.9) GO TO 100
      FREQ1=FLOAT(IKNT)*FORFRE
      HARM=SQRT(SINCO(IKNT)*SINCO(IKNT)+COSCO(IKNT)*COSCO(IKNT))
      XNHARM=HARM/XNORM
      IF (HARM.LT.1.0E-20) GO TO 92
      PHASE=RAD*ATAN2(COSCO(IKNT),SINCO(IKNT))
      GO TO 95
   92 PHASE=0.0
   95 PHASEN=PHASE-PNORM
      THD=THD+XNHARM*XNHARM
      WRITE (IDO,81) IKNT,FREQ1,HARM,XNHARM,PHASE,PHASEN
      GO TO 90
  100 THD=100.0*SQRT(THD)
      WRITE (IDO,101) THD
101   FORMAT (/1X,'TOTAL HARMONIC DISTORTION = ',F10.3,' PERCENT'/)
      RETURN
      END
