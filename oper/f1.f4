CCC
CCC      Y(1)=Z              DERY(1)=Z-DOT
CCC      Y(2)=Z-DOT          DERY(2)=Z-DDOT
CCC      Y(3)=THETA          DERY(3)=THETA-DOT
CCC      Y(4)=THETA-DOT      DERY(4)=THETA-DDOT
CCC      Y(5)=X              DERY(5)=X-DOT
CCC      Y(6)=X-DOT          DERY(6)=X-DDOT
CCC      Y(7)=ZS(1)          DERY(7)=ZS(1)-DOT
CCC      Y(8)=ZS(1)-DOT      DERY(8)=ZS(1)-DDOT
CCC      Y(9)=ZS(2)          DERY(9)=ZS(2)-DOT
CCC      Y(10)=ZS(2)-DOT     DERY(10)=ZS(2)-DDOT
CCC   FOR A SINGLE REAR AXLE...
CCC   KEY=0
CCC   Y(11)=Y(12)=0
CCC   FOR THE WALKING BEAM...
CCC   KEY=1
CCC       Y(11)=THETAT             DERY(11)=THETAT-DOT
CCC       Y(12)=THETAT-DOT         DERY(12)=THETAT-DDOT
CCC   WHERE ZS(2) LOCATES THE CM OF THE WALKING BEAM ASSY
CCC   FOR THE 4 ELLIPTIC LEAF SPRING...
CCC   KEY=2
CCC   Y(11)=ZS(3)                 DERY(11)=ZS(3)-DOT
CCC   Y(12)=ZS(3)-DOT             DERY(12)=ZS(3)-DDOT
CCC   WHERE ZS2 AND ZS3 LOCATE THE AXLE CENTERS
CCC
      SUBROUTINE FCT1
C
C NOTE:  THE VALUES OF W AND WS IN INPUT ARE IDENTICAL WITH THE VALUES
C        OF M AND MS IN FCT1 AND OUTPUT.  THIS MEANS THAT M AND MS ARE
C        IN POUNDS, NOT MASS UNITS
C
      INTEGER R,W
      REAL J,JS,KF,K,KT,L,M,MLOCAL,MS,MU,MUZERO,N,NS
      REAL MAXAX
      COMMON /KEY/ KEY,KAXLE,KROAD
      COMMON /PAR/ AA1,AA2,AA4,AA5,AA6,AA7,AA8,A1,A2,ALPHA,C,CF,CS,
     1  DELTA,FA,J,JS,K,KT,MUZERO,PERCNT,         M,MS,WP,PJ,PX,PZ
     2 ,PM,W1,WS
      DIMENSION WS(3)
      COMMON/BLOCK1/ R,W
      COMMON/BLOCK2/XPRINT,XDOT,FX,SZERO,SLOPE,MLOCAL,XDD
      COMMON/BLOCK5/N,SLIP,XLAM,MU,SLO,TT,OMEGAD,L
      COMMON /BLOCK6/ TQ,XX,YY,NUM
      COMMON /BLOCK7/ NS,SF,P,POLD,T,PROP,RR
      COMMON /BLOCK8/ KPROP,MAXAX,EMPTY
      DIMENSION FA(3)
      DIMENSION WW(3)
      DIMENSION ROADZ(5)
      DIMENSION ARM(3)
      DIMENSION LHS(16),RHS(4),TN(4)
      DIMENSION SLOPE(3),MLOCAL(3),SZERO(3),FX(3)
      DIMENSION C(4),CF(2),CS(3),JS(3),K(2),KT(3),MS(3),NS(3),ALPHA(2),
     1DELTA(2),MUZERO(3)
      DIMENSION MU(3),N(3),OMEGAD(3),RR(3),SF(3),SLIP(3),SLO(3),TT(3),
     1 ZS(3)
      DIMENSION S(3),T(5),SD(3),CCF(3),XLAM(3),L(3)
      DIMENSION PRMT(5),Y(12),AUX(16,12),DERY(12)
      DIMENSION XX(150),YY(150),NUM(6),TQ(3,2),POLD(3),P(5)
      DIMENSION PROP(3),MAXAX(3),EMPTY(3)
      DIMENSION FEET(3)
      RADIAN=57.295779
      KEY1=KEY+1
      XPRIME=-1.0
      DO 3800 I=1,5
 3800 ROADZ(I) = 0.0
      DO 4817 I =1,3
C     SET PROP(I) FOR NO PROPORTIONING
4817  PROP(I) = 1.
C
      IF(KROAD.EQ.-1) CALL ROAD1(A1,A2,AA1,AA2,AA4,AA5,KEY)
      CT2 =  0.04*SQRT(KT(2)*MS(2))
      GO TO (19000,19001,19002), KEY1
C     FOR THE SINGLE AXLE......
19000 FX(3)=0.
      P(3) = 0.
      SLIP(3) = 0.0
      T(3) = 0.
      AA6=0.
      GO TO 19003
C     FOR THE 4 LEAF......
19002 CT3 =  0.04*SQRT(KT(3)*MS(3))
      TN1DEN=(1.+AA1/AA2+AA4/AA5*AA1/AA2+((AA1/AA2)**2)*(AA4/AA5))
      COSAA7=COS(AA7)
      SINAA7=SIN(AA7)
      ARM(1) = COSAA7*AA6 + SINAA7*AA8
      GO TO 19003
C     FOR THE WALKING BEAM......
19001 CT3 =  0.04*SQRT(KT(3)*MS(3))
      IF(PERCNT.LE..0001)PERCNT=.0001
      P1 = (100. - PERCNT)/PERCNT
      XBAR = MS(3)*(AA1 + AA2)/(MS(2) + MS(3))
      AA3 = XBAR - AA1
      AA6=AA1+AA3
      AA7=AA2-AA3
      AA8 = (MS(2)*AA1*AA1 + MS(3)*AA2*AA2)
19003 CONTINUE
      IXYZ = 0
C     FOR NO PROPORTIONING GO TO 4811
      IF(KPROP.NE.-1) GO TO 4811
C     IXYZ=1 SIGNIFIES FIRST TIME THROUGH TO CALCULATE EMPTY AXLE LOADS
      IXYZ = 1
      GO TO 4812
4811  CONTINUE
C     CALCULATE CHANGES IN MASS AND INERTIA DUE TO THE PAYLOAD
      APLD = ALPHA(1) + DELTA(1)
      FEET(1) = A2*12.
      FEET(2) = APLD*12.
      FEET(3) = J*12.
      WRITE (W,4800) FEET
 4800 FORMAT(' THE EMPTY SPRUNG MASS CENTER IS',F11.3,' INCHES AHEAD OF
     1THE REAR AXLE CENTERLINE,',F11.3,' INCHES ABOVE THE GROUND'/'  ...
     2THE EMPTY POLAR MOMENT OF THE SPRUNG MASS IS',F15.3,' INCH LB SEC*
     3*2'//)
      IF (WP.EQ.0.0) GO TO 4810
      XA2 = A2
      WB = A1 + A2
      A2 = (M*A2 + PM*PX)/(M + PM)
      A1 = WB - A2
      DBAR = (((ALPHA(1)+DELTA(1))*M + PM*PZ)/(M+PM) - ALPHA(1)) -
     1  DELTA(1)
      DELTA(1) = DELTA(1) + DBAR
      DELTA(2) = DELTA(2) + DBAR
      APLUSD = ALPHA(1)+DELTA(1)
      J = J + M*((XA2 - A2)**2 + (APLUSD - APLD)**2) + PJ +
     1  PM*((A2 - PX)**2 + (APLD - PZ)**2)
      W1 = W1 + WP
      M = M + PM
      FEET(1) = A2*12.
      FEET(2) = APLUSD*12.
      FEET(3) = J*12.
      WRITE (W,4820) FEET
 4820 FORMAT(' THE LOADED SPRUNG MASS CENTER IS',F11.3,' INCHES AHEAD OF
     1 THE REAR AXLE CENTERLINE,',F11.3,' INCHES ABOVE THE GROUND'/'  ..
     2.THE POLAR MOMENT IS ',F14.3,' INCH LB SEC**2'//)
 4810 CONTINUE
C
C CALCULATE THE COULOMB FRICTION "BRAKE POINTS"
      ABOUNC = (CF(1) + CF(2))/M
      APITCH = (CF(1)*A1 + CF(2)*A2)/J
      DEL1 = 0.0025*((CF(1)/MS(1)) + ABOUNC +APITCH*A1)
      DEL2 = 0.0025*((CF(2)/MS(2)) + ABOUNC + APITCH*A2)
4812  CONTINUE
      GVW = W1
      DO 19004 I=1,KAXLE
19004 GVW = GVW + WS(I)
      GVWM = GVW/32.1725
      IF(KEY.EQ.2)GO TO 20001
      XXX = W1*A1/(A1+A2)
      NS(1) = WS(1) + W1 - XXX
C     CALCULATE THE STATIC SUSPENSION DEFLECTION
      IF(K(1).GT.0.) GO TO 501
      IF(IXYZ.EQ.1)GO TO 501
      NUM1 = 100+NUM(5)
      Z1 = NS(1) - WS(1)
      CALL TABLE(101,NUM1,YY,XX,Z1,SP1)
      FT = SP1*12.
      WRITE (W,510) FT,Z1
510   FORMAT('0','TABLE LOOKUP FOR THE FRONT SPRING...IN THE STATIC CONF
     1IGURATION THE DEFLECTION IS',F5.2,' INCHES FOR',F7.0,' POUNDS')
501   CONTINUE
      IF(KEY.EQ.0)GO TO 234
C     FOR THE WALKING BEAM......
      NS(3) = (XXX*AA1 + (WS(2)+WS(3))*AA6)/(AA1+AA2)
      NS(2) = GVW - NS(1) - NS(3)
      IF(IXYZ.EQ.1)GO TO 4813
C     CALCULATE THE STATIC SUSPENSION DEFLECTION
      IF(K(2).GT.0.0) GO TO 233
      NUM2=125+NUM(6)
      Z2 = NS(2) + NS(3) -WS(2) - WS(3)
      CALL TABLE(126,NUM2,YY,XX,Z2,SP2)
      FT = SP2*12.
      WRITE (W,516) FT,Z2
  516 FORMAT('0','TABLE LOOKUP FOR THE WALKING BEAM SPRING...IN THE STAT
     1IC CONFIGURATION THE DEFLECTION IS',F5.2,' INCHES FOR',F7.0,' POUN
     3DS')
      GO TO 233
234   CONTINUE
C     FOR THE SINGLE AXLE......
      NS(2) = GVW - NS(1)
      IF(IXYZ.EQ.1)GO TO 4813
C     CALCULATE THE STATIC SUSPENSION DEFLECTION
      IF(K(2).GT.0.0) GOTO 233
      NUM2 = 125+NUM(6)
      Z2 = NS(2) - WS(2)
      CALL TABLE(126,NUM2,YY,XX,Z2,SP2)
      FT = SP2*12.
      WRITE (W,515) FT,Z2
  515 FORMAT('0','TABLE LOOKUP FOR THE REAR SPRING...IN THE STATIC CONFI
     1GURATION THE DEFLECTION IS',F5.2,' INCHES FOR',F7.0,' POUNDS')
      GO TO 233
20001 CONTINUE
      ARM(2) = A2 - AA4 - AA2
      ARM(3) = A2 + AA5 + AA1
      XN3=W1*A1/(A1+ARM(3)+((AA5/AA4)*((1+AA2/AA1)/(1+AA1/AA2)))*
     1  (A1 + ARM(2)))
      XN2 = XN3*((1+AA2/AA1)/((AA4/AA5)*(1+AA1/AA2)))
      TN1S = XN2*AA2/(AA1 + AA2)
      TN2S = XN2 - TN1S
      TN4S = XN3*AA1/(AA1 + AA2)
      TN3S = XN3 - TN4S
      NS(2) = XN2 + WS(2)
      NS(3) = XN3 + WS(3)
      NS(1) = GVW - NS(2) - NS(3)
      IF(IXYZ.EQ.1)GO TO 4813
C     CALCULATE THE STATIC SUSPENSION DEFLECTION
      IF(K(1).GT.0.0) GO TO 520
      NUM1 = 100+NUM(5)
      Z1 = NS(1) - WS(1)
      CALL TABLE(101,NUM1,YY,XX,Z1,SP1)
      FT = SP1*12.
      WRITE (W,510) FT,Z1
  520 CONTINUE
      IF(K(2).GT.0.0) GO TO 233
      WRITE (W,526)
  526 FORMAT('0','ERROR ...NO TABLE LOOKUP ALLOWED ON REAR 4-LEAF TANDEM
     1 AXLE')
      CALL EXIT
 4813 WRITE (W,7500)
 7500 FORMAT('0THE STATIC LOADS WITH ZERO PAYLOAD WOULD BE'/
     1  ' AXLE NUMBER      LOAD')
      DO 4818 I=1,KAXLE
      WW(I) = NS(I)
 4818 WRITE (W,9001) I,WW(I)
 9001 FORMAT(T5,I1,T15,F10.3)
      WRITE (W,9002) GVW
 9002 FORMAT(T15,'__________'/T 3,'TOTAL',T15,F10.3)
      IXYZ = 0
C     NOW GO THROUGH AND CALCULATE THE LOADED STATIC LOADS
      GO TO 4811
  233 CONTINUE
      WRITE (W,9003)
 9003 FORMAT('0THE STATIC LOADS ON THE TIRES ARE'/
     1  ' AXLE NUMBER      LOAD')
      DO 9004 I=1,KAXLE
 9004 WRITE (W,9001) I,NS(I)
      WRITE (W,9002) GVW
4819  IF (KPROP.NE.-1)GO TO 7520
      WRITE (W,7510)
 7510 FORMAT('0','THE PROPORTIONING VALUES FOR EACH AXLE ARE:')
      DO 4815 I=1,KAXLE
      PROP(I) = EMPTY(I)+(1.-EMPTY(I))*(NS(I)-WW(I))/(MAXAX(I)-WW(I))
      IF(PROP(I).GT.1.)PROP(I)=1.
      WRITE (W,7511) I,PROP(I)
 7511 FORMAT(T10,'AXLE NUMBER',I2,', PROPORTIONING =',F10.3)
4815  CONTINUE
 7520 CONTINUE
      RETURN
      ENTRY FCT(X,Y,DERY)
      IF(X.NE.XPRIME) CALL TORQUE(X)
      XPRIME=X
      IF(KROAD.EQ.-1) CALL ROAD2(Y(5),ROADZ,KAXLE)
C     THE FRONT SUSPENSION IS COMMON TO ALL THREE KEYS
      S(1)=-Y(1) +Y(7 )+A1*Y(3)
      SD(1)=Y(8 )-Y(2)+A1*Y(4)
      CC1 = CF(1)*SD(1)/DEL1
      IF(ABS(SD(1)).GT.DEL1)CC1 = CF(1)*SD(1)/ABS(SD(1))
      CCCC = C(1)
      IF(SD(1).GT.0.)CCCC=C(2)
      IF(K(1).GT.0.)GO TO 504
      Z = SP1 - S(1)
      CALL TABLE(101,NUM1,XX,YY,Z,XKS)
      XKS = XKS - Z1
      GO TO 505
504   XKS =-K(1)*S(1)
505   SF(1) =-XKS + CCCC*SD(1) + CC1
      IF(KEY.NE.2)GO TO 1
C     4 LEAF TIRE POSITIONS
      YT2 = Y(9)
      YT3=Y(11)
      YT2D= Y(10)
      YT3D = Y(12)
C     AVERAGE THE VELOCITY AND POSITION OF THE AXLES
      S(2) = -Y(1) +(Y(11) + Y(9))/2. - A2*Y(3)
      SD(2) = -Y(2) + (Y(12) + Y(10))/2. - A2*Y(4)
      GO TO 2
1     CONTINUE
C     WALKING BEAM TIRE POSITIONS
      YT2=Y(9)-AA6*Y(11)
      YT3=Y(9)+AA7*Y(11)
      YT3D = Y(10) + AA7*Y(12)
      YT2D = Y(10) - AA6*Y(12)
      SD(2)=Y(10)-Y(2)-A2*Y(4)      -AA3*Y(12)
      S(2)=Y(9 )-Y(1)-A2*Y(3)       -AA3*Y(11)
2     CONTINUE
      IF(K(2).GT.0.0) GO TO 506
      Z=SP2-S(2)
      CALL TABLE(126,NUM2,XX,YY,Z,XKS)
      XKS=XKS-Z2
      GO TO 507
  506 XKS=-K(2)*S(2)
  507 CONTINUE
      CC2=CF(2)*SD(2)/DEL2
      IF (ABS(SD(2)).GT.DEL2) CC2=CF(2)*SD(2)/ABS(SD(2))
      CCCC = C(3)
      IF(SD(2).GT.0.)CCCC = C(4)
      SF(2) = -XKS      + SD(2)*CCCC + CC2
      RR(1) = ALPHA(1) - Y(9)
      RR(2) = ALPHA(2)-YT2
      RR(3) = ALPHA(2) - YT3
C     NORMAL FORCE ON THE TIRES...SMOOTH ROAD ONLY
      N(1) = NS(1) + KT(1)*(Y(7) + ROADZ(1))
      N(2) = NS(2) + KT(2)*(YT2  + ROADZ(2))
      N(3) = NS(3) + KT(3)*(YT3  + ROADZ(3))
C     ADD TWO PERCENT CRITICAL DAMPING TO THE TANDEM TIRES
      N(2) = N(2) + CT2*YT2D
      N(3) = N(3) + CT3*YT3D
      DO 5 I=1,KAXLE
      IF(N(I).LE.0.0) N(I) = 0.001
C     BEGIN SLIP CALCULATION
      AF = FA(I)*XDOT
      ETA = -T(I)/JS(I)
      B = -RR(I)/JS(I)
      ETABAR =-N(I)*(MLOCAL(I) - SLOPE(I)*SZERO(I))
      F =-RR(I)*(ETA + B*ETABAR)/XDOT
      F = F + XDD/XDOT
      BBAR =-SLOPE(I)*N(I)
      Q = RR(I)*B*BBAR/XDOT
      Q = Q + XDD/XDOT
      IF(ABS(Q).LT..0001) Q = .0001
      TIME = X - XPRINT
      A = -Q*TIME
      IF(ABS(A).LT.150.) GO TO 31
      A=150.*A/ABS(A)
   31 SLIP(I) = (SZERO(I) - F/Q)*EXP(A) + F/Q
      IF(SLIP(I).GT.0.0) GO TO 996
      IF (SZERO(I).GT.0.1) GO TO 991
      SLIP(I)=SZERO(I)/2.0
      GO TO 999
  991 SLIP(I)=0.05
      GO TO 999
  996 IF(SLIP(I).LT.1.0) GO TO 999
CCCC  WHEEL LOCKUP....
      SLIP(I) = 1.
      XLAM(I) = 0.0
      MU(I) = MUZERO(I)*(1. - AF)
      FX(I) = -MU(I)*N(I)
      TT(I) = -FX(I)*RR(I)
      OMEGAD(I)=0.0
      GO TO 5
999   IF(SLIP(I).EQ.0.0)GO TO 900
      XLAM(I) = .5*MUZERO(I)*N(I)*(1.-AF*SLIP(I))*(1. - SLIP(I))
     1/(CS(I)*SLIP(I))
      IF(XLAM(I).GT.1.)GO TO 900
      FOFS = XLAM(I)/2.
      FPOFS = MUZERO(I)*N(I)*(AF*SLIP(I)**2-1.)/(4.*CS(I)*SLIP(I)**2)
      MU(I) = MUZERO(I)*(1. - AF*SLIP(I))*(1. - FOFS)
      SLO(I) = -MUZERO(I)*(AF*(1. - FOFS) +(1. - AF*SLIP(I))*FPOFS)
      GO TO 998
900   MU(I) = CS(I)*SLIP(I)/(N(I)*(1. - SLIP(I)))
      SLO(I) = CS(I)/(N(I)*(1. - SLIP(I))**2)
998   TT(I) = T(I)
      FX(I) = -MU(I)*N(I)
      OMEGAD(I) = -(T(I) + FX(I)*RR(I))/JS(I)
5     CONTINUE
C    FORWARD MOTION X     .....NOTE X,XDOT AND XDD ARE IN FT NOT INCHES
      DERY(6) = (FX(1) + FX(2) + FX(3))/GVWM
      DO 10 I=1,11,2
   10 DERY(I) = Y(I+1)
      DERY(8) = (-SF(1) + NS(1) - N(1))/MS(1)
      L(1) = -FX(1) + MS(1)*DERY(6)
      IF(KEY.NE.2)GO TO 675
CC    THE 4 LEAF EQUATIONS
      TR2 = (MS(2)*DERY(6) - FX(2))/COSAA7
      TR3 = (MS(3)*DERY(6) - FX(3))/COSAA7
C     EQ3,EQ4 AND EQ6 ARE THE RIGHT HAND SIDE OF EQUATIONS GIVEN IN THE
C     DOCUMENTATION
      EQ3 = -TT(2) + TR2*ARM(1)
      EQ4 = -TT(3) + TR3*ARM(1)
      EQ6=-1.*SF(2)+TN1S+TN2S+TN3S+TN4S
      TN1=(EQ6+EQ3/AA2+EQ3/AA5*AA4/AA2+EQ4/AA2+AA1/AA5*AA4/AA2*EQ3/AA2)/
     1TN1DEN
      TN2=(-EQ3+AA1*TN1)/AA2
      TN3=(AA4/AA5)*((-EQ3+AA1*TN1)/AA2)
      TN4=(-EQ4+(AA1*(AA4/AA5*((-EQ3+AA1*TN1)/AA2))))/AA2
      DERY(2) = (SF(1) + SF(2))/M
      DERY(2) = DERY(2) + (TR2+TR3)*SINAA7/M
      DERY(4) = (-L(1)*DELTA(1) - SF(1)*A1 - TT(1) - ((TN1-TN1S)*(ARM(2)
     1  -AA1) + (TN2-TN2S)*(ARM(2)+AA2)) - ((TN3-TN3S)*(ARM(3)-AA1) +
     2  (TN4-TN4S)*(ARM(3)+AA2)) - (TR2+TR3)*COSAA7*DELTA(2) +
     3  TR2*SINAA7*ARM(2) + TR3*SINAA7*ARM(3) - (TR2+TR3)*ARM(1))/J
      DERY(10) = (TN1 + TN2 + WS(2) - N(2) - TR2*SINAA7)/MS(2)
      DERY(12) = (TN3 + TN4 + WS(3) - N(3) - TR3*SINAA7)/MS(3)
      RETURN
  675 CONTINUE
CC    EQUATIONS FOR THE WALKING BEAM
      DERY(2) = (SF(2) + SF(1))/M
      IF(KEY.EQ.0)GO TO 102
      DERY(10) = (-SF(2) + NS(2) - N(2) + NS(3) - N(3))/(MS(2)+MS(3))
      TR2 = (TT(2) - AA4*(MS(2)*DERY(6)-FX(2)))/(AA4 + (1.+P1)*AA5)
      TR3 = (TT(3) - AA4*(MS(3)*DERY(6)-FX(3)))/(AA4 + (1.+P1)*AA5)
      VA2=P1*TR2*AA5
      VA3=P1*TR3*AA5
      L(2) = -FX(2) - FX(3) + (MS(2)+MS(3))*DERY(6) + TR2 + TR3
      DERY(4) = (-SF(1)*A1 + SF(2)*A2 + DELTA(2)*(FX(2)+FX(3)-
     1  (MS(2)+MS(3))*DERY(6))- TT(2) - TT(3) + (VA2+VA3) - TT(1) -
     2  L(1)*DELTA(1))/J
      DERY(12)=(N(2)*AA6-N(3)*AA7-VA2-VA3+(SF(2)-XXX)*AA3)/AA8
      RETURN
102   CONTINUE
CCC   EQUATIONS FOR THE SINGLE AXLE
      L(2) = -FX(2) + MS(2)*DERY(6)
      DERY(4) = (-SF(1)*A1 + SF(2)*A2 - TT(1) - TT(2) - L(1)*(DELTA(1)
     1 +S(1)) - L(2)*(DELTA(2)+ S(2)))/J
      DERY(10) = (-SF(2) + NS(2) - N(2))/MS(2)
      DERY(11) = 0.
      DERY(12) = 0.
      RETURN
      END
    