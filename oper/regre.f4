C
C     ..................................................................
C
C        SAMPLE MAIN PROGRAM FOR MULTIPLE REGRESSION - REGRE
C
C        PURPOSE
C           (1) READ THE PROBLEM PARAMETER CARD FOR A MULTIPLE REGRES-
C           SION, (2) READ SUBSET SELECTION CARDS, (3) CALL THE SUB-
C           ROUTINES TO CALCULATE MEANS, STANDARD DEVIATIONS, SIMPLE
C           AND MULTIPLE CORRELATION COEFFICIENTS, REGRESSION COEFFI-
C           CIENTS, T-VALUES, AND ANALYSIS OF VARIANCE FOR MULTIPLE
C           REGRESSION, AND (4) PRINT THE RESULTS.
C
C        REMARKS
C           THE NUMBER OF OBSERVATIONS, N, MUST BE GREATER THAN M+1,
C           WHERE M IS THE NUMBER OF VARIABLES.  IF SUBSET SELECTION
C           CARDS ARE NOT PRESENT, THE PROGRAM CAN NOT PERFORM MULTIPLE
C           REGRESSION.
C           AFTER RETURNING FROM SUBROUTINE MINV, THE VALUE OF DETER-
C           MINANT (DET) IS TESTED TO CHECK WHETHER THE CORRELATION
C           MATRIX IS SINGULAR.  IF DET IS COMPARED AGAINST A SMALL
C           CONSTANT, THIS TEST MAY ALSO BE USED TO CHECK NEAR-
C           SINGULARITY.
C
C        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
C           CORRE  (WHICH, IN TURN, CALLS THE SUBROUTINE NAMED DATA)
C           ORDER
C           MINV
C           MULTR
C
C        METHOD
C           REFER TO B. OSTLE, 'STATISTICS IN RESEARCH', THE IOWA STATE
C           COLLEGE PRESS', 1954, CHAPTER 8.
C
C     ..................................................................
C
C     THE FOLLOWING  DIMENSIONS MUST BE GREATER THAN OR EQUAL TO THE
C     NUMBER OF VARIABLES, M..
C
         DIMENSION XBAR(40),STD(40),D(40),RY(40),ISAVE(40),B(40),
     1             SB(40),T(40),W(40)
C
C     THE FOLLOWING DIMENSION MUST BE GREATER THAN OR EQUAL TO THE
C     PRODUCT OF M*M..
C
         DIMENSION RX(1600)
C
C     THE FOLLOWING DIMENSION MUST BE GREATER THAN OR EQUAL TO
C     (M+1)*M/2..
C
         DIMENSION R(820)
C
C     THE FOLLOWING DIMENSION MUST BE GREATER THAN OR EQUAL TO 10..
C
         DIMENSION ANS(10)
C
C     ..................................................................
C
C        IF A DOUBLE PRECISION VERSION OF THIS ROUTINE IS DESIRED, THE
C        C IN COLUMN 1 SHOULD BE REMOVED FROM THE DOUBLE PRECISION
C        STATEMENT WHICH FOLLOWS.
C
C     DOUBLE PRECISION XBAR,STD,RX,R,D,B,T,RY,DET,SB,ANS,SUM
C
C        THE C MUST ALSO BE REMOVED FROM DOUBLE PRECISION STATEMENTS
C        APPEARING IN OTHER ROUTINES USED IN CONJUNCTION WITH THIS
C        ROUTINE.
C
C        ...............................................................
C
    1 FORMAT(A4,A2,I5,2I2)
    2 FORMAT(25H1MULTIPLE REGRESSION.....A4,A2//6X,14HSELECTION.....I2//
     1)
    3 FORMAT(9H0VARIABLE,5X,4HMEAN,6X,8HSTANDARD,6X,11HCORRELATION,4X,
     110HREGRESSION,4X,10HSTD. ERROR,5X,8HCOMPUTED/6H   NO.,18X,9HDEVIAT
     2ION,7X,6HX VS Y,7X,11HCOEFFICIENT,3X,12HOF REG.COEF.,3X,7HT VALUE)
    4 FORMAT(1H ,I4,6F14.5)
    5 FORMAT(10H DEPENDENT)
    6 FORMAT(1H0/10H INTERCEPT,10X,F16.5//23H MULTIPLE CORRELATION  ,F13
     1.5//23H STD. ERROR OF ESTIMATE,F13.5//)
    7 FORMAT(1H0,21X,39HANALYSIS OF VARIANCE FOR THE REGRESSION//5X,19HS
     1OURCE OF VARIATION,7X,7HDEGREES,7X,6HSUM OF,10X,4HMEAN,12X,7HF VAL
     2UE/30X,10HOF FREEDOM,4X,7HSQUARES,9X,7HSQUARES)
    8 FORMAT(30H ATTRIBUTABLE TO REGRESSION   ,I6,3F16.5/30H DEVIATION F
     1ROM REGRESSION    ,I6,2F16.5)
    9 FORMAT(1H ,5X,5HTOTAL,19X,I6,F16.5)
   10 FORMAT(36I2)
   11 FORMAT(1H ,15X,18HTABLE OF RESIDUALS//9H CASE NO.,5X,7HY VALUE,5X,
     110HY ESTIMATE,6X,8HRESIDUAL)
   12 FORMAT(1H ,I6,F15.5,2F14.5)
   13 FORMAT(53H1NUMBER OF SELECTIONS NOT SPECIFIED.  JOB TERMINATED.)
   14 FORMAT(52H0THE MATRIX IS SINGULAR.  THIS SELECTION IS SKIPPED.)
C
C     ..................................................................
C
C     READ PROBLEM PARAMETER CARD
C
  100 READ (5,1) PR,PR1,N,M,NS
C        PR........PROBLEM NUMBER (MAY BE ALPHAMERIC)
C        PR1.......PROBLEM NUMBER (CONTINUED)
C        N.........NUMBER OF OBSERVATIONS
C        M.........NUMBER OF VARIABLES
C        NS........NUMBER OF SELECTIONS
C
C     LOGICAL TAPE 13 IS USED AS INTERMEDIATE STORAGE TO HOLD INPUT
C     DATA.  THE INPUT DATA ARE WRITTEN ON LOGICAL TAPE 13 BY THE
C     SPECIAL INPUT SUBROUTINE NAMED DATA.  THE STORED DATA MAY BE USED
C     FOR RESIDUAL ANALYSIS.
C
      REWIND 13
C
      IO=0
      X=0.0
C
      CALL CORRE (N,M,IO,X,XBAR,STD,RX,R,D,B,T)
C
      REWIND 13
C
C     TEST NUMBER OF SELECTIONS
C
      IF(NS) 108, 108, 109
  108 WRITE (6,13)
      GO TO 300
C
  109 DO 200 I=1,NS
      WRITE (6,2) PR,PR1,I
C
C     READ SUBSET SELECTION CARD
C
      READ (5,10) NRESI,NDEP,K,(ISAVE(J),J=1,K)
C        NRESI.....OPTION CODE FOR TABLE OF RESIDUALS
C                    0  IF IT IS NOT DESIRED.
C                    1  IF IT IS DESIRED.
C        NDEP......DEPENDENT VARIABLE
C        K.........NUMBER OF INDEPENDENT VARIABLES INCLUDED
C        ISAVE.....A VECTOR CONTAINING THE INDEPENDENT VARIABLES
C                       INCLUDED
C
      CALL ORDER (M,R,NDEP,K,ISAVE,RX,RY)
C
      CALL MINV (RX,K,DET,B,T)
C
C     TEST SINGULARITY OF THE MATRIX INVERTED
C
      IF(DET) 112, 110, 112
  110 WRITE (6,14)
      GO TO 200
C
  112 CALL MULTR (N,K,XBAR,STD,D,RX,RY,ISAVE,B,SB,T,ANS)
C
C     PRINT MEANS, STANDARD DEVIATIONS, INTERCORRELATIONS BETWEEN
C     X AND Y, REGRESSION COEFFICIENTS, STANDARD DEVIATIONS OF
C     REGRESSION COEFFICIENTS, AND COMPUTED T-VALUES
C
      MM=K+1
      WRITE (6,3)
      DO 115 J=1,K
      L=ISAVE(J)
  115 WRITE (6,4) L,XBAR(L),STD(L),RY(J),B(J),SB(J),T(J)
      WRITE (6,5)
      L=ISAVE(MM)
      WRITE (6,4) L,XBAR(L),STD(L)
C
C     PRINT INTERCEPT, MULTIPLE CORRELATION COEFFICIENT, AND STANDARD
C     ERROR OF ESTIMATE
C
      WRITE (6,6) ANS(1),ANS(2),ANS(3)
C
C     PRINT ANALYSIS OF VARIANCE FOR THE REGRESSION
C
      WRITE (6,7)
      L=ANS(8)
      WRITE (6,8) K,ANS(4),ANS(6),ANS(10),L,ANS(7),ANS(9)
      L=N-1
      SUM=ANS(4)+ANS(7)
      WRITE (6,9) L,SUM
      IF(NRESI) 200, 200, 120
C
C     PRINT TABLE OF RESIDUALS
C
  120 WRITE (6,2) PR,PR1,I
      WRITE (6,11)
      MM=ISAVE(K+1)
      DO 140 II=1,N
      READ (13) (W(J),J=1,M)
      SUM=ANS(1)
      DO 130 J=1,K
      L=ISAVE(J)
  130 SUM=SUM+W(L)*B(J)
      RESI=W(MM)-SUM
  140 WRITE (6,12) II,W(MM),SUM,RESI
      REWIND 13
  200 CONTINUE
      GO TO 100
  300 CONTINUE
      END
