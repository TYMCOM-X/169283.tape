
TITLE PPCHK.MAC - N. CHURCHILL, DECEMBER 19700

	LOC 137
	XWD 0,1
	RELOC

;CALLIS

RESET=0
EXIT=12
WAIT=10
GETPPN=24
GETTTY=34
GETTAB=41
SLEEP=31

;AC'S

A=1
B=2
C=3
D=4
AC=5
PPPTR=6
CODPTR=7
BP=10
WD=11
M=12
CH=13
TRYCTR=14
P=15

;IO CHANNELS

INP=17

;MISCELLANEOUS PARAMETERS

EOF=20000	;FOR STATZ'S


ENTRY PPCHK

PPCHK:	0
	MOVE M,@0(16)		;SAVE PRODUCT CODE ARGUMENT
	MOVEM	M,INCODE
	MOVE	B,[XWD 777760,0]		;REMOVE UNWANTED BITS
	ANDM	B,INCODE		;ALLOW 2-DIGIT CODE ONLY
	MOVE P,PDP
	CALLI A,GETPPN		;GET USER NO. IN SIXBIT
	JRST .+1
	MOVE AC,A
	CALL AC,[SIXBIT/UNSQZE/]
	MOVEM AC,	INPP

	INIT	INP,0		;INIT CUST2.DAT FILE
	SIXBIT/SYS/
	XWD 0,DSKBUF
	JRST	INITER
	INBUF	INP,1
	MOVE TRYCTR,^D20		;TRY TO ACCESS 20 TIMES
TRYGIN:	MOVE	A,[SIXBIT/CUST2/]
	MOVSI B,(SIXBIT/DAT/)
	SETZB	C,D
	MOVE D,SYSPPN
	LOOKUP	INP,A
	JRST LERROR
GETBUF:	MOVEI	AC,6
	MOVE	PPPTR,[XWD 440600,PPNUM]
PP:	PUSHJ	P,INPUT		;READ IN USER NO. AND CONVERT TO ASCII
	SUBI	WD,40
	IDPB	WD,PPPTR
	SOSLE	AC
	JRST	PP
	MOVEI	AC,2
	MOVE	CODPTR,[XWD 440700,PCODE]
PC:	PUSHJ	P,INPUT		;READIN 2-DIGIT PROD. CODE
	IDPB	WD,CODPTR
	SOSLE	AC
	JRST PC
	PUSHJ	P,INPUT
	CAIE	WD,15		;FIND LF BEFORE GOING ON
	JRST	.-2
	PUSHJ	P,INPUT

COMPAR:	MOVE	M,PPNUM		;LOOK FOR MATCH ON USER NO.
	CAME	M,INPP
	JRST GETBUF
	MOVE	M,PCODE		;LOOK FOR SECOND MATCH ON PROD. CODE
	CAME	M,INCODE
	JRST 	GETBUF

MATCH:	JRA	16,1(16)		;MATCH. JUST RETURN TO CALLER

NOMATCH:	MOVEI	AC,MSG1		;KICK BAD CALLER OUT O CALLING PROGRAM
MESSAG:	CALL	AC,[SIXBIT/DDTOUT/]
	CALLI	EXIT

INITER:	MOVEI	AC,MSG3	;FAILED TO INIT CUST2.DAT
	JRST MESSAG

LERROR:	MOVEI	A,1
	CALLI	A,SLEEP
	SOSLE	TRYCTR
	JRST	TRYGIN
	MOVEI	AC,MSG2
	JRST	MESSAGE

INPUT:	SOSLE	DSKBUF+2		;TEST IF BUFFER EMPTY YET
	JRST DSKOK		;NO
	INPUT	INP,0		;YES, SO REFILL
	STATZ	INP,760000		;TEST FOR EOF OR OTHER ERROR RETURN
	JRST NOMATCH

DSKOK:	ILDB	WD,DSKBUF+1
	JUMPE	WD,INPUT		;IGNORE NULLS
	POPJ P,

MSG1:	ASCIZ/CANNOT ACCESS REQUESTED PROGRAM. PLEASE
CALL YOUR ACCOUNT REPRESENTATIVE.
/
MSG2:	ASCIZ/VALIDATION FILE IS CAUSING FAILURE. PLEASE CALL 716-854-0242.
/
MSG3:	ASCIZ/CANNOT INITIALIZE VALIDATION FILE. PLEASE CALL 716-
854-0242.
/

DSKBUF:	BLOCK 3
SYSPPN:	XWD 1,4
PDP:	XWD -20,.
	BLOCK 21
PPNUM:	0
PCODE:	0
INCODE:	0
INPP:	0
ENTSIZ=25
ENTRY:	BLOCK ENTSIZ

	END
