TITLE GETACT - RETRIEVE AND WRITE ACCOUNTING INTO ACCTG. FILES
SUBTTL J. MARCIN 5/19/72

LOC 137
1
RELOC

;AC'S
F=0
A=1
B=2
C=3
D=4
E=5
INDEX=6
CH=7
BP=10
BP1=11 
N=12
N1=13
P=14

;FLAGS IN F
SPUSER==1	;SPECIFIC USER REQUESTED
ASCO==2	;ASCOUT OUTPUT

EXIT==12

EXTERNAL USER,CUST,ICODE,IER,MODE,PPN

ENTRY GETACT

GETACT:	0
	MOVEM 0,ACBLK	;SAVE AC'S
	MOVE 0,[XWD 1,ACBLK+1]
	BLT 0,ACBLK+16
	MOVE P,PDP
	SETZB F,IER
	MOVE A,ICODE
	CAIE A,1
	CAIN A,2
	JRST INITIT	;IF 1 OR 2, OPEN FILE SPECIFIED
	CAIN A,6
	JRST CLSIT	;IF 6, CLOSE FILE SPECIFIED
	CAIN A,4
	JRST INPTIT	;IF 4,READ NEXT RECORD
	CAIN A,5
	JRST WRITIT	;IF 5, WRITE SPECIFIED RECORD
	CAIE A,3
	JRST ERR4
	TRO F,SPUSER
	JRST INPTIT	;IF 3, GO LOOK FOR SPCIFIC RECORD
RETURN:	MOVE 16,[XWD ACBLK,0]
	BLT 16,16
	JRA 16,(16)

INITIT:	SETZ ,ZERST
	MOVE A,[XWD ZERST,ZERST+1]
	BLT A,ZEREND
	SKIPL ,MODE
	JRST INIT2
	INIT 17,17
	SIXBIT /DSK/
	0
	CALLI EXIT
	MOVE B,[SIXBIT/USERNA/]
	HRLZI C,(SIXBIT/MES/)
	SETZ D,
	MOVE E,PPN
	LOOKUP 17,B
	JRST NOFIL1
	HLRE N,E
	MOVMS N
	SUBI N,1
	ROT N,-7
	ADDI N,1
	MOVEM N,LASBL1
	CAIE A,1
	JRST INITA
	SETZM ,BLKNO1	;SET UP INCASE USER WILL READ SEQUENTIALLY
	SETZM ,INDEX1
	USETI 17,@BLKNO1
	INPUT 17,USRLST
	STATZ 17,760000
	JRST ERR3
	JRST RETURN

INITA:	MOVE B,[SIXBIT/USERNA/]
	HRLZI C,(SIXBIT/MES/)
	SETZ D,
	MOVE E,PPN
	ENTER 17,B
	JRST ERR3
	JRST RETURN

INIT2:	INIT 16,17
	SIXBIT /DSK/
	0
	CALLI EXIT
	MOVE B,[SIXBIT/CUSTOM/]
	HRLZI C,(SIXBIT/ERS/)
	SETZ D,
	MOVE E,PPN
	LOOKUP 16,B
	JRST NOFIL1
	HLRE N,E
	MOVMS N
	SUBI N,1
	ROT N,-7
	ADDI N,1
	MOVEM N,LASBL2
	CAIE A,1
	JRST INIT3
	SETZM ,BLKNO2
	AOS ,BLKNO2
	SETZM ,INDEX2
	USETI 16,@BLKNO2
	INPUT 16,USRLST
	STATZ 16,760000
	JRST ERR3
	JRST RETURN

INIT3:	MOVE B,[SIXBIT/CUSTOM/]
	HRLZI C,(SIXBIT/ERS/)
	SETZ D,
	MOVE E,PPN
	ENTER 16,B
	JRST ERR3
	JRST RETURN

CLSIT:	SKIPL ,MODE
	JRST CLS2
	CLOSE 17,0
	RELEASE 17,0
	JRST RETURN

CLS2:	CLOSE 16,0
	RELEASE 16,0
	JRST RETURN

WRITIT:	SKIPL ,MODE
	JRST WRIT2
	MOVE A,USER
	IDIVI A,^D101
	ADDI B,1
WRIT3:	USETI 17,@B
	MOVEM B,BLKNO1
	INPUT 17,USRLST
	STATZ 17,760000
	JRST ERR3
	SETZ INDEX,
WRIT4:	SKIPG A,USRBLK(INDEX)
	JRST WRIT7
	CAMN A,USER
	JRST WRIT10+1
	ADDI INDEX,3
	JRST WRIT4
WRIT7:	JUMPE A,WRIT10
	HRRZ B,A
	JRST WRIT3
WRIT10:	MOVE A,USER
	MOVEM A,USRBLK(INDEX)
	MOVE A,USER+1
	DPB A,[POINT 6,USRBLK+1(INDEX),17]
	MOVE A,USER+6
	DPB A,[POINT 7,USRBLK+1(INDEX),24]
	MOVE A,USER+2
	DPB A,[POINT 7,USRBLK+1(INDEX),35]
	MOVE A,USER+3
	DPB A,[POINT 14,USRBLK+2(INDEX),17]
	MOVE A,USER+4
	DPB A,[POINT 17,USRBLK+2(INDEX),35]
	SETZ A,
	SKIPE ,USER+5
	MOVEI A,1
	DPB A,[POINT 1,USRBLK+1(INDEX),1]
	JRST WROUT1

WRIT2:	MOVE A,CUST
	IDIVI A,^D53
	ADDI B,1
	IMULI B,^D10
WRITB1:	USETI 16,@B
	MOVEM B,BLKNO2
	INPUT 16,CUSLST
	STATZ 16,76000
	JRST ERR3
	SETZ INDEX,
WRITB2:	SKIPG A,CUSBLK(INDEX)
	JRST WRITB3
	LDB C,SIZBP
	HRRZS A,A
	CAMN A,CUST
	JRST WRITB6
	ADD INDEX,C
	JRST WRITB2
WRITB3:	JUMPE A,WRITB5
	HRRZ B,A
	JRST WRITB1
WRITB5:	CAIL INDEX,1176
	JRST WRITB7
	MOVE A,CUST
	DPB A,[POINT 17,CUSBLK(INDEX),35]
	MOVE A,CUST+1
	DPB A,[POINT 14,CUSBLK(INDEX),17]
	MOVE A,CUST+2
	DPB A,[POINT 17,CUSBLK+1(INDEX),35]
SIXIN:	MOVE BP,[POINT  6,CUST+3]
	MOVE BP1,[POINT 6,CUSBLK+2(INDEX)]
	MOVEI N,^D30
SIXI1:	ILDB CH,BP
	IDPB CH,BP1
	SOJG N,SIXI1
	ADDI BP,1
	MOVEI N,^D30
SIXI2:	ILDB CH,BP
	IDPB CH,BP1
	SOJG N,SIXI2
	MOVEI C,^D13
	SETZ N1,
	MOVEI N,^D180
SIXI3:	MOVEI A,77
	MOVE BP,[POINT 6,CUST(N1),35]
	ADDI BP,^D44
	PUSHJ P,DECBYT
	LDB CH,BP
	JUMPN CH,SIXI4
	SOJG N,.-3
	IDPB A,BP
	JRST TAKIN
SIXI4:	IDPB A,BP
	PUSHJ P,DECBYT
	JUMPE N1,.+3
	HRLZI A,400000
	IORM A,CUSBLK+1(INDEX)
	MOVEI A,76
SIXI7:	SOJE N,TAKIN
	CAIG N,^D29
	JRST TAKIN
	MOVE D,N
	IDIVI D,^D30
	IMULI D,^D30
	SUBI D,1
	MOVE B,D
	CAMN B,N
	JRST .+4
	PUSHJ P,DECBYT
	SOJA N,.-3
SIXI6:	PUSHJ P,DECBYT
	LDB CH,BP
	JUMPN CH,SIXI5
	SOJG N,SIXI6
	IDPB A,BP
	JRST TAKIN
SIXI5:	IDPB A,BP
	PUSHJ P,DECBYT
	JRST SIXI7
TAKIN:	MOVE BP,[POINT 6,CUST(N1)]
	ADDI BP,^D15
	MOVEI N,^D180
	MOVEI B,5
	SKIPE ,N1
	MOVE B,SAVB
TAK1:	ILDB CH,BP
	IDPB CH,BP1
	SOJ N,.+1
	SOJGE B,.+3
	ADDI C,1
	MOVEI B,5
	CAIN CH,76
	JRST TAK2
	CAIN CH,77
	JRST TAK1A
	JUMPG N,TAK1
TAK1A:	MOVEM B,SAVB
	JUMPE N1,.+3
	DPB C,SIZBP
	JRST WROUT2
	ADDI N1,^D30
	JRST SIXI3-1
TAK2:	ILDB CH,BP
	SOJ N,.+1
	JUMPE CH,TAK2
	JRST TAK1+1


WRITB7:	AOS ,LASBL2
	HRRZ A,LASBL2
	TRO A,400000
	MOVEM A,CUSBLK(INDEX)
	OUTPUT 16,CUSLST
	STATZ 16,760000
	JRST ERR3
	USETI 16,@LASBL2
	INPUT 16,CUSLST
	STATZ 16,760000
	JRST ERR3
	JRST WRITB2-1

WRITB6:	TTCALL 3,[ASCIZ/FOUND A MATCH IN CUSTOMERS.
/]
	CALLI 12

WROUT2:	USETO 16,@BLKNO2
	OUTPUT 16,CUSLST
	STATZ 16,760000
	JRST ERR3
	JRST RETURN

WROUT1:	USETO 17,@BLKNO1
	OUTPUT 17,USRLST
	STATZ 17,760000
	JRST ERR3
	JRST RETURN

;INPUT ENTRY
INPTIT:	SKIPL ,MODE
	JRST INPT2
	TRNE F,SPUSER	;SPECIFIC USER?
	JRST INP4	;YES, GO LOOK FOR IT
	MOVE INDEX,INDEX1	;NO, TAKE NEXT ON IN ORDER
INP1:	SKIPE C,USRBLK(INDEX)
	JRST INP2
	AOS BLKNO1
	MOVE B,BLKNO1
INP1A:	USETI 17,@B
	INPUT 17,USRLST
	STATZ 17,740000
	JRST ERR3
	STATZ 17,20000
	JRST ERR2
	SETZB INDEX,INDEX1
	JRST INP1

INP2:	JUMPG C,INP3	;OVERFLOW BLK
	HLLZ B,C
	JRST INP1A

INP3:	MOVE A,USRBLK(INDEX)
	MOVEM A,USER	;SAVE PPN
	LDB A,[POINT 6,USRBLK+1(INDEX),17]
	MOVEM A,USER+1	;PCODE
	LDB A,[POINT 7,USRBLK+1(INDEX),24]
	MOVEM A,USER+6	;SUBSET NO.
	LDB A,[POINT 7,USRBLK+1(INDEX),35]
	MOVEM A,USER+2	;TCODE
	LDB A,[POINT 14,USRBLK+2(INDEX),17]
	MOVEM A,USER+3
	LDB A,[POINT 17,USRBLK+2(INDEX),35]
	MOVEM A,USER+4
	MOVEI A,1
	SETZM ,USER+5
	SKIPGE ,USRBLK+1(INDEX)
	MOVEM A,USER+5
	ADDI INDEX,3
	MOVEM INDEX,INDEX1
	JRST RETURN

INP4:	MOVE A,USER
	IDIVI A,^D101
	ADDI B,1
INP4A:	USETI 17,@B
	INPUT 17,USRLST
	STATZ 17,760000
	JRST ERR3
	SETZ INDEX,
INP6:	SKIPG A,USRBLK(INDEX)
	JRST INP7
	CAMN A,USER
	JRST INP3+2
	ADDI INDEX,3
	JRST INP6
INP7:	JUMPE A,ERR1
	HRRZ B,A
	JRST INP4A

INPT2:	TRNE F,SPUSER	;SPECIFIC CUSTOMER?
	JRST INPTB
	MOVE INDEX,INDEX2
INPT1:	SKIPE C,CUSBLK(INDEX)
	JRST INPB3
	AOS ,BLKNO2
	MOVE B,BLKNO2
INPTBA:	USETI 16,@B
	INPUT 16,CUSLST
	STATZ 16,200000
	JRST ERR2
	STATZ 16,740000
	JRST ERR3
	SETZB INDEX,INDEX2
	JRST INPT1
INPB3:	JUMPG C,INPB2
	HRRZ B,C
	JRST INPTBA

INPB2:	LDB A,[POINT 17,CUSBLK(INDEX),35]
	MOVEM A,CUST
	LDB A,SIZBP
	MOVEM A,SIZENT
	LDB A,[POINT 14,CUSBLK(INDEX),17]
	MOVEM A,CUST+1
	LDB A,[POINT 17,CUSBLK+1(INDEX),35]
	MOVEM A,CUST+2
	SKIPG ,MODE
	TRO F,ASCO
SIXOUT:	MOVE BP1,[POINT 6,CUST+3]
	TRNE F,ASCO
	MOVE BP1,[POINT 7,CUST+3]
	MOVE BP,[POINT 6,CUSBLK+2(INDEX)]
	MOVEI N,^D30
SIX1:	ILDB CH,BP
	TRNE F,ASCO
	ADDI CH,40
	IDPB CH,BP1
	SOJG N,SIX1
	SETZ CH,
	TRNE F,ASCO
	JRST SIX2-1
	MOVEI N,6
	IDPB CH,BP1
	SOJG N,.-1	;FILL OUT WITH ZEROES
	MOVEI N,^D30
SIX2:	ILDB CH,BP
	TRNE F,ASCO
	ADDI CH,40
	IDPB CH,BP1
	SOJG N,SIX2
	SETZ CH,
	TRNE F,ASCO
	JRST NXTAD1-1
	MOVEI N,6
	IDPB CH,BP1
	SOJG N,.-1
	MOVEI B,2
NXTAD1:	MOVEI A,5	;NUMBER OF LINES
NEWL:	MOVEI N,^D30	;NO OF CHARS IN A LINE

SIX3:	ILDB CH,BP
	CAIE CH,76
	JRST SIX4
	SETZ CH,
	TRNE F,ASCO
	MOVEI CH,40
	IDPB CH,BP1
	SOJG N,.-1
	SOJG A,NEWL
	JRST SIX6

SIX4:	CAIE CH,77
	JRST SIX5
	SETZ CH,
	TRNE F,ASCO
	MOVEI CH,40
	SOJA A,.+1
	IDPB CH,BP1
	SOJG N,.-1
	MOVEI N,^D30
	SOJG A,.-3
SIX6:	SOJE B,INCDEX
	SKIPE ,CUSBLK+1(INDEX)
	JRST NXTADR
	MOVEI N,^D150
	SETZ CH,
	TRNE F,ASCO
	MOVEI CH,40
	IDPB CH,BP1
	SOJG N,.-1
INCDEX:	ADD INDEX,SIZENT
	MOVEM INDEX,INDEX2
	JRST RETURN

SIX5:	TRNE F,ASCO
	ADDI CH,40
	IDPB CH,BP1
	SOJG N,SIX3
	SOJG A,NEWL
	JRST SIX6

NXTADR:	SETZM ,CUST+44
	SETZM ,CUST+74
	JRST NXTAD1

INPTB:	MOVE A,CUST
	IDIVI A,^D53
	ADDI B,1
	IMULI B,^D10
INPTB1:	USETI 16,@B
	INPUT 16,CUSLST
	STATZ 16,760000
	JRST ERR3
	SETZ INDEX,
INPTB2:	SKIPG A,CUSBLK(INDEX)
	JRST INPTB3
	LDB C,SIZBP
	CAMN A,CUST
	JRST INPB2+2
	ADD INDEX,C
	JRST INPTB2
INPTB3:	JUMPE A,ERR1
	HRRZ B,A
	JRST INPTB1


DECBYT:	PUSH P,C
	LDB C,[POINT 6,BP,11]
	LDB D,[POINT 6,BP,5]
	ADD D,C
	CAIG D,44
	JRST DECDON
	SUBI BP,1
	MOVEI D,6
DECDON:	DPB D,[POINT 6,BP,5]
	POP P,C
	POPJ P,0

ERROR:	MOVEM A,IER
	JRST RETURN
ERR1:	MOVEI A,1
	JRST ERROR
ERR2:	MOVEI A,2
	JRST ERROR
ERR3:	MOVEI A,3
	JRST ERROR
ERR4:	MOVEI A,4
	JRST ERROR

NOFIL1:	TTCALL 3,[ASCIZ/
CANNOT FIND USERNA.MES.
/]
	CALLI EXIT

NOFIL2:	TTCALL 3,[ASCIZ/
CANNOT FIND CUSTOM.ERS.
/]
	CALLI EXIT

;STORAGE
ZERST:	0
SAVB:	0
SIZENT:	0
BLKNO1:	0
LASBL1:	0
BLKNO2:	0
LASBL2:	0
INDEX1:	0
INDEX2:	0
USRBLK:	BLOCK 200
CUSBLK:	BLOCK 1200
ZEREND:	0
PDP:	XWD -20,.
	BLOCK 20
ACBLK:	BLOCK 17
USRLST:	IOWD 200,USRBLK
	0
CUSLST:	IOWD 1200,CUSBLK
	0
SIZBP:	POINT 7,CUSBLK+1(INDEX),7
	END

 