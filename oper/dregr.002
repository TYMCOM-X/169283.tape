        TYPE 1
1       FORMAT(' 000000 000002')
        CALL DREGR
        END
	SUBROUTINE DREGR
         DIMENSION XBAR(40),STD(40),D(40),RY(40),ISAVE(40),B(40),
     1             SB(40),T(40),W(40),X(30000),IDUM(40),
     2 NAMRG(40),RESI(1000)
         DIMENSION RX(1600)
         DIMENSION R(820)
         DIMENSION ANS(10)
    2 FORMAT(25H1MULTIPLE REGRESSION.....,'Y= ',A5,//)
    3 FORMAT(9H0VARIABLE,3X,4HMEAN,4X,8HSTANDARD,4X,11HCORRELATION,1X,
     110HREGRESSION,2X,10HSTD. ERROR,3X,3HCAL/1X6H  NAME,12X,9HDEVIAT
     2ION,5X,6HX VS Y,4X,11HCOEFFICIENT,1X,11HOF REG.COEF,1X,5HT VAL)
    4 FORMAT(1H ,A5,3F12.3,3F10.3)
    5 FORMAT(10H DEPENDENT)
    6 FORMAT(1H0/10H INTERCEPT,10X,G//23H MULTIPLE CORRELATION  ,G
     1//23H STD. ERROR OF ESTIMATE,G//)
    7 FORMAT(1H0,21X,39HANALYSIS OF VARIANCE FOR THE REGRESSION//1X,'S
     1OURCE OF VARIATION',7X,7HDEGREES,5X,6HSUM OF,8X,4HMEAN,6X,'F VAL
     2'/25X,10HOF FREEDOM,4X,7HSQUARES,6X,7HSQUARES)
    8 FORMAT(1X,'DUE TO REGRESSION'   ,9X,I4,3G14.6/1X,
     1'DEVIATION FROM REGRESSION',2X,I4,2X,2G14.6)
    9 FORMAT(1H ,5X,5HTOTAL,17X,I4,G)
   10 FORMAT(9I)
   11 FORMAT(1H ,15X,18HTABLE OF RESIDUALS//9H CASE NO.,3X,7HY VALUE,5X,
     110HY ESTIMATE,6X,8HRESIDUAL)
   12 FORMAT(1H ,I6,3G14.6)
   14 FORMAT(52H0THE MATRIX IS SINGULAR.  THIS SELECTION IS SKIPPED.)
16	FORMAT(1X,'ENTER NAME OF DATA FILE?',$)
17	FORMAT(A5)
18	FORMAT(/1X,'NAME OF DEPENDENT VARIABLE?',$)
19	FORMAT(1X,'ENTER NAMES OF INDEPENDENT VARIABLES,ONE TO A ROW
     1 IN RESPONSE TO ?.'/1X,' TYPE STOP WHEN DONE.'/)
20	FORMAT(1X,'?',$)
104	FORMAT(1X,'NO SUCH VARIABLE . TRY AGAIN.'/)
99	FORMAT(1X,'VARIABLE ALREADY IN REGRESSION.TRY AGAIN.'/)
100	FORMAT(1X,'TOO FEW OBSERVATIONS.REGRESSION TERMINATED.'/)
306	FORMAT(//1X,'NEED TABLE OF RESIDUALS.?',/,1X,
     +'TYPE TTY,DSK,BOTH OR NONE.'/)
118	FORMAT(1X,'ENTER OUTPUT FILENAME.?',$)
311	FORMAT(2I)
308	FORMAT(6G)
117	FORMAT(1X,'ILLEGAL OPTION.TRY AGAIN.'/)
317	FORMAT(1X,'NO SUCH FILE ',A5,'. TRY AGAIN.'/)
186	FORMAT(1X,'NAME OF RESIDUAL ARRAY?.',$)
320	FORMAT(A5,2X,A5)
335     FORMAT(/1X,' TOO MANY VARIABLES OR OBSERVATIONS.'/)
318	FORMAT(A5,25X,'RESIDUAL DATA',2X,A5)
	CALL ERRSET(0)
	IO=1
	IWFIL=0
316	TYPE 16
	ACCEPT 17,IDATA
	CALL LOCATE(IDATA,IERR)
	IF(IERR)314,315,315
314	TYPE 317,IDATA
	GO TO 316
        DO 21 I=1,30000
21      X(I)=0.0
315	CALL INPUT(IND1,IND2,IDUM,X,IDATA)
        IF(IND1.LE.1000.AND.IND2.LE.40) GO TO 330
        TYPE 335
        GO TO 205
330     N=IND1
26	I=1
	DO 22 J=1,40
22	NAMRG(J)='     '
29  	TYPE 18
	ACCEPT 17,NAME1
	IF(NAME1.EQ.5HSTOP ) GO TO 305
	GO TO 25
23	TYPE 19
24	TYPE 20
	ACCEPT 17,NAME1
	IF(NAME1.EQ.5HSTOP ) GO TO 107
25	DO 101 J=1,IND2
	ITEMP=IDUM(J)
	IF(NAME1.EQ.ITEMP) GO TO 102
101	CONTINUE
105	TYPE 104
	IF(I.EQ.1) GO TO 29
	GO TO 24
102	NAMRG(I)=IDUM(J)
	IF(I.EQ.1) GO TO 27
	ITEMP=I-1
	DO 98 IDOO=1,ITEMP
	IF(NAMRG(I).NE.NAMRG(IDOO)) GO TO 98
	TYPE 99
	GO TO 24
98	CONTINUE
27	INDEX1=N*(J-1)
	INDEX2=N*(I-1)
	DO 103 K=1,N
        XSAVE=X(INDEX2+K)
        X(INDEX2+K)=X(INDEX1+K)
103     X(INDEX1+K)=XSAVE
        ITEMP=IDUM(I)
        IDUM(I)=IDUM(J)
        IDUM(J)=ITEMP
	I=I+1
	IF(I.EQ.2) GO TO 23
	GO TO 24
107	NS=1
	NDEP=1
	K=I-2
	M=I-1
	DO 108 J=1,K
108	ISAVE(J)=J+1
	ITEMP=M+1
	IF(N.GT.ITEMP) GO TO 111
	TYPE 100
	GO TO 305
C
111      CALL CORRE (N,M,IO,X,XBAR,STD,RX,R,D,B,T)
C
C
C
  109 DO 200 I=1,NS
      CALL ORDER (M,R,NDEP,K,ISAVE,RX,RY)
C
      CALL MINV (RX,K,DET,B,T)
C
C     TEST SINGULARITY OF THE MATRIX INVERTED
C
      IF(DET) 112, 110, 112
  110 WRITE (5,14)
      GO TO 200
C
  112 CALL MULTR (N,K,XBAR,STD,D,RX,RY,ISAVE,B,SB,T,ANS)
C
C     PRINT MEANS, STANDARD DEVIATIONS, INTERCORRELATIONS BETWEEN
C     X AND Y, REGRESSION COEFFICIENTS, STANDARD DEVIATIONS OF
C     REGRESSION COEFFICIENTS, AND COMPUTED T-VALUES
C
      MM=K+1
      WRITE (5,3)
      DO 115 J=1,K
      L=ISAVE(J)
  115 WRITE (5,4) NAMRG(L),XBAR(L),STD(L),RY(J),B(J),SB(J),T(J)
      WRITE (5,5)
      L=ISAVE(MM)
      WRITE (5,4) NAMRG(L),XBAR(L),STD(L)
C
C     PRINT INTERCEPT, MULTIPLE CORRELATION COEFFICIENT, AND STANDARD
C     ERROR OF ESTIMATE
C
      WRITE (5,6) ANS(1),ANS(2),ANS(3)
C
C     PRINT ANALYSIS OF VARIANCE FOR THE REGRESSION
C
      WRITE (5,7)
      L=ANS(8)
      WRITE (5,8) K,ANS(4),ANS(6),ANS(10),L,ANS(7),ANS(9)
      L=N-1
      SUM=ANS(4)+ANS(7)
      WRITE (5,9) L,SUM
307	TYPE 306
	ACCEPT 17,NRESI
	IF(NRESI.EQ.'NONE ') GO TO 201
	IF(NRESI.EQ.'TTY  '.OR.NRESI.EQ.'BOTH ') GO TO 120
	IF(NRESI.EQ.'DSK  ') GO TO 120
	TYPE 117
	GO TO 307
C
C     PRINT TABLE OF RESIDUALS
C
120	NOFCNT=NAMRG(ISAVE(MM))
	IF(NRESI.NE.'DSK  ') WRITE(5,2)NAMRG(ISAVE(MM))
	IF(NRESI.NE.'DSK  ')      WRITE (5,11)
      MM=ISAVE(K+1)
      DO 140 II=1,N
	DO 132 J=1,M
	ITEMP=II+N*(J-1)
132	W(J)=X(ITEMP)
      SUM=ANS(1)
      DO 130 J=1,K
      L=ISAVE(J)
  130 SUM=SUM+W(L)*B(J)
      RESI(II)=W(MM)-SUM
  140 IF(NRESI.NE.'DSK  ')WRITE (5,12) II,W(MM),SUM,RESI(II)
  200 CONTINUE
	IF(NRESI.EQ.'TTY  ') GO TO 201
116	IWFIL=IWFIL+1
	IF(IWFIL.GT.1) GO TO 119
	TYPE 118
	ACCEPT 17,NOFIL
	CALL OFILE(1,NOFIL)
119	TYPE 186
	ACCEPT 17,IREASR
	WRITE(1,320)IREASR,NOFCNT
	WRITE(1,308)(RESI(J),J=1,N)
201      GO TO 26
  300 CONTINUE
305	IF(IWFIL.EQ.0)GO TO 205
	ENDFILE 1
	CALL IFILE(1,NOFIL)
	CALL OFILE(21,NOFIL)
	WRITE(21,311)N,IWFIL
	DO 310 J=1,IWFIL
	READ(1,320)IREASR,NOFCNT
	READ(1,308)(RESI(L),L=1,N)
	WRITE(21,318)IREASR,NOFCNT
310	WRITE(21,308)(RESI(L),L=1,N)
	ENDFILE 21
205     CONTINUE
	RETURN
	END
	SUBROUTINE DATA
	RETURN
	END
C	STATPAK VERSION OF CORRE
	SUBROUTINE CORRE(N,M,IO,X,XBAR,STD,RX,R,B,D,T)
	DIMENSION X(1),XBAR(1),STD(1),RX(1),R(1),B(1),D(1),T(1)
C     INITIALIZATION
C
      DO 100 J=1,M
      B(J)=0.0
  100 T(J)=0.0
      K=(M*M+M)/2
      DO 102 I=1,K
  102 R(I)=0.0
      FN=N
      L=0
C
      IF(IO) 105, 127, 105
C
C     DATA ARE ALREADY IN CORE
C
  105 DO 108 J=1,M
      DO 107 I=1,N
      L=L+1
  107 T(J)=T(J)+X(L)
      XBAR(J)=T(J)
  108 T(J)=T(J)/FN
C
      DO 115 I=1,N
      JK=0
      L=I-N
      DO 110 J=1,M
      L=L+N
      D(J)=X(L)-T(J)
  110 B(J)=B(J)+D(J)
      DO 115 J=1,M
      DO 115 K=1,J
      JK=JK+1
  115 R(JK)=R(JK)+D(J)*D(K)
      GO TO 205
C
C     READ OBSERVATIONS AND CALCULATE TEMPORARY
C     MEANS FROM THESE DATA IN T(J)
C
  127 IF(N-M) 130, 130, 135
  130 KK=N
      GO TO 137
  135 KK=M
  137 DO 140 I=1,KK
      CALL DATA (M,D)
      DO 140 J=1,M
      T(J)=T(J)+D(J)
      L=L+1
  140 RX(L)=D(J)
      FKK=KK
      DO 150 J=1,M
      XBAR(J)=T(J)
  150 T(J)=T(J)/FKK
C
C     CALCULATE SUMS OF CROSS-PRODUCTS OF DEVIATIONS
C     FROM TEMPORARY MEANS FOR M OBSERVATIONS
C
      L=0
      DO 180 I=1,KK
      JK=0
      DO 170 J=1,M
      L=L+1
  170 D(J)=RX(L)-T(J)
      DO 180 J=1,M
      B(J)=B(J)+D(J)
      DO 180 K=1,J
      JK=JK+1
  180 R(JK)=R(JK)+D(J)*D(K)
C
      IF(N-KK) 205, 205, 185
C
C     READ THE REST OF OBSERVATIONS ONE AT A TIME, SUM
C     THE OBSERVATION, AND CALCULATE SUMS OF CROSS-
C     PRODUCTS OF DEVIATIONS FROM TEMPORARY MEANS
C
  185 KK=N-KK
      DO 200 I=1,KK
      JK=0
      CALL DATA (M,D)
      DO 190 J=1,M
      XBAR(J)=XBAR(J)+D(J)
      D(J)=D(J)-T(J)
  190 B(J)=B(J)+D(J)
      DO 200 J=1,M
      DO 200 K=1,J
      JK=JK+1
  200 R(JK)=R(JK)+D(J)*D(K)
C
C     CALCULATE MEANS
C
  205 JK=0
      DO 210 J=1,M
      XBAR(J)=XBAR(J)/FN
C
C     ADJUST SUMS OF CROSS-PRODUCTS OF DEVIATIONS
C     FROM TEMPORARY MEANS
C
      DO 210 K=1,J
      JK=JK+1
  210 R(JK)=R(JK)-B(J)*B(K)/FN
C
C     CALCULATE CORRELATION COEFFICIENTS
C
      JK=0
      DO 220 J=1,M
      JK=JK+J
  220 STD(J)= SQRT( ABS(R(JK)))
      DO 230 J=1,M
      DO 230 K=J,M
      JK=J+(K*K-K)/2
      L=M*(J-1)+K
      RX(L)=R(JK)
      L=M*(K-1)+J
      RX(L)=R(JK)
      IF(STD(J)*STD(K)) 225, 222, 225
  222 R(JK)=0.0
      GO TO 230
  225 R(JK)=R(JK)/(STD(J)*STD(K))
  230 CONTINUE
C
C     CALCULATE STANDARD DEVIATIONS
C
      FN=SQRT(FN-1.0)
      DO 240 J=1,M
  240 STD(J)=STD(J)/FN
C
C     COPY THE DIAGONAL OF THE MATRIX OF SUMS OF CROSS-PRODUCTS OF
C     DEVIATIONS FROM MEANS.
C
      L=-M
      DO 250 I=1,M
      L=L+M+1
  250 B(I)=RX(L)
      RETURN
      END
C	STATPAK VERSION OF MINV
      SUBROUTINE MINV(A,N,D,L,M)
      DIMENSION A(1),L(1),M(1)
C	SEARCH FOR LARGEST ELEMENT
      D=1.0
      NK=-N
      DO 80 K=1,N
      NK=NK+N
      L(K)=K
      M(K)=K
      KK=NK+K
      BIGA=A(KK)
      DO 20 J=K,N
      IZ=N*(J-1)
      DO 20 I=K,N
      IJ=IZ+I
   10 IF( ABS(BIGA)- ABS(A(IJ))) 15,20,20
   15 BIGA=A(IJ)
      L(K)=I
      M(K)=J
   20 CONTINUE
C
C        INTERCHANGE ROWS
C
      J=L(K)
      IF(J-K) 35,35,25
   25 KI=K-N
      DO 30 I=1,N
      KI=KI+N
      HOLD=-A(KI)
      JI=KI-K+J
      A(KI)=A(JI)
   30 A(JI) =HOLD
C
C        INTERCHANGE COLUMNS
C
   35 I=M(K)
      IF(I-K) 45,45,38
   38 JP=N*(I-1)
      DO 40 J=1,N
      JK=NK+J
      JI=JP+J
      HOLD=-A(JK)
      A(JK)=A(JI)
   40 A(JI) =HOLD
C
C        DIVIDE COLUMN BY MINUS PIVOT (VALUE OF PIVOT ELEMENT IS
C        CONTAINED IN BIGA)
C
   45 IF(BIGA) 48,46,48
   46 D=0.0
      RETURN
   48 DO 55 I=1,N
      IF(I-K) 50,55,50
   50 IK=NK+I
      A(IK)=A(IK)/(-BIGA)
   55 CONTINUE
C
C        REDUCE MATRIX
C
      DO 65 I=1,N
      IK=NK+I
      HOLD=A(IK)
      IJ=I-N
      DO 65 J=1,N
      IJ=IJ+N
      IF(I-K) 60,65,60
   60 IF(J-K) 62,65,62
   62 KJ=IJ-I+K
      A(IJ)=HOLD*A(KJ)+A(IJ)
   65 CONTINUE
C
C        DIVIDE ROW BY PIVOT
C
      KJ=K-N
      DO 75 J=1,N
      KJ=KJ+N
      IF(J-K) 70,75,70
   70 A(KJ)=A(KJ)/BIGA
   75 CONTINUE
C
C        PRODUCT OF PIVOTS
C
      D=D*BIGA
C
C        REPLACE PIVOT BY RECIPROCAL
C
      A(KK)=1.0/BIGA
   80 CONTINUE
C
C        FINAL ROW AND COLUMN INTERCHANGE
C
      K=N
  100 K=(K-1)
      IF(K) 150,150,105
  105 I=L(K)
      IF(I-K) 120,120,108
  108 JQ=N*(K-1)
      JR=N*(I-1)
      DO 110 J=1,N
      JK=JQ+J
      HOLD=A(JK)
      JI=JR+J
      A(JK)=-A(JI)
  110 A(JI) =HOLD
  120 J=M(K)
      IF(J-K) 100,100,125
  125 KI=K-N
      DO 130 I=1,N
      KI=KI+N
      HOLD=A(KI)
      JI=KI-K+J
      A(KI)=-A(JI)
  130 A(JI) =HOLD
      GO TO 100
  150 RETURN
      END
C	STATPAK VERSION OF MULTR
	SUBROUTINE MULTR(N,K,XBAR,STD,D,RX,RY,ISAVE,B,SB,T,ANS)
      DIMENSION XBAR(1),STD(1),D(1),RX(1),RY(1),ISAVE(1),B(1),SB(1),
     1          T(1),ANS(1)
      MM=K+1
C
C        BETA WEIGHTS
C
      DO 100 J=1,K
  100 B(J)=0.0
      DO 110 J=1,K
      L1=K*(J-1)
      DO 110 I=1,K
      L=L1+I
  110 B(J)=B(J)+RY(I)*RX(L)
      RM=0.0
      BO=0.0
      L1=ISAVE(MM)
C
C        COEFFICIENT OF DETERMINATION
C
      DO 120 I=1,K
      RM=RM+B(I)*RY(I)
C
C        REGRESSION COEFFICIENTS
C
      L=ISAVE(I)
      B(I)=B(I)*(STD(L1)/STD(L))
C
C        INTERCEPT
C
  120 BO=BO+B(I)*XBAR(L)
      BO=XBAR(L1)-BO
C
C        SUM OF SQUARES ATTRIBUTABLE TO REGRESSION
C
      SSAR=RM*D(L1)
C
C        MULTIPLE CORRELATION COEFFICIENT
C
  122 RM= SQRT( ABS(RM))
C
C        SUM OF SQUARES OF DEVIATIONS FROM REGRESSION
C
      SSDR=D(L1)-SSAR
C
C        VARIANCE OF ESTIMATE
C
      FN=N-K-1
      SY=SSDR/FN
C
C        STANDARD DEVIATIONS OF REGRESSION COEFFICIENTS
C
      DO 130 J=1,K
      L1=K*(J-1)+J
      L=ISAVE(J)
  125 SB(J)= SQRT( ABS((RX(L1)/D(L))*SY))
C
C        COMPUTED T-VALUES
C
  130 T(J)=B(J)/SB(J)
C
C        STANDARD ERROR OF ESTIMATE
C
  135 SY= SQRT( ABS(SY))
C
C        F VALUE
C
      FK=K
      SSARM=SSAR/FK
      SSDRM=SSDR/FN
      F=SSARM/SSDRM
C
      ANS(1)=BO
      ANS(2)=RM
      ANS(3)=SY
      ANS(4)=SSAR
      ANS(5)=FK
      ANS(6)=SSARM
      ANS(7)=SSDR
      ANS(8)=FN
      ANS(9)=SSDRM
      ANS(10)=F
      RETURN
      END
C	STATPAK VERSION OF ORDER
      SUBROUTINE ORDER (M,R,NDEP,K,ISAVE,RX,RY)
      DIMENSION R(1),ISAVE(1),RX(1),RY(1)
C     COPY INTERCORRELATIONS OF INDEPENDENT VARIABLES
C     WITH DEPENDENT VARIABLE
C
      MM=0
      DO 130 J=1,K
      L2=ISAVE(J)
      IF(NDEP-L2) 122, 123, 123
  122 L=NDEP+(L2*L2-L2)/2
      GO TO 125
  123 L=L2+(NDEP*NDEP-NDEP)/2
  125 RY(J)=R(L)
C
C     COPY A SUBSET MATRIX OF INTERCORRELATIONS AMONG
C     INDEPENDENT VARIABLES
C
      DO 130 I=1,K
      L1=ISAVE(I)
      IF(L1-L2) 127, 128, 128
  127 L=L1+(L2*L2-L2)/2
      GO TO 129
  128 L=L2+(L1*L1-L1)/2
  129 MM=MM+1
  130 RX(MM)=R(L)
C
C     PLACE THE SUBSCRIPT NUMBER OF THE DEPENDENT
C     VARIABLE IN ISAVE(K+1)
C
      ISAVE(K+1)=NDEP
      RETURN
      END
	SUBROUTINE INPUT(IND1,IND2,IDUM,DUMMY,IDATA)
	DIMENSION IDUM(1),DUMMY(1)
	CALL IFILE(1,IDATA)
	READ(1,100) IND1,IND2
	DO 200 J=1,IND2
	READ(1,105) IDUM(J)
	INDEX1=IND1*(J-1)+1
	INDEX2=INDEX1+IND1-1
200	READ(1,106) (DUMMY(L),L=INDEX1,INDEX2)
100	FORMAT(2I)
105	FORMAT(A5)
106	FORMAT(6G)
	RETURN
	END
