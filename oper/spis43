      SUBROUTINE DIODE
      COMMON NODPLC(200),YNL(501),TSTORE(501),TRACUR(500),VN(101),
     1   VNIM1(101),IORDER(101),IUR(102),IUC(200),MATLOC(500)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(101),NAME(100),LOCAL(100),MNAME(100)
      COMMON/PARAM/VALUE(100),SOURCE(150),SYMVAL(25,25)
C
        DOUBLE PRECISION MODNAM,MNAME,NAME
C
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      DIMENSION VDO(1),CJN(1)
      EQUIVALENCE (VDO(1),TRACUR(1)),(CJN(1),TRACUR(2))
C
C
      ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) RETURN
      ISPOT=ICURNT(8)
      DO 1000 I=ISTART,ISTOP
C
C  LOOKUP MATRIX POINTERS
C
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      IF (MODE.EQ.2) GO TO 50
      LOC4=MATLOC(IFIND+1)
      LOC5=MATLOC(IFIND+2)
      LOC6=MATLOC(IFIND+3)
      LOC7=MATLOC(IFIND+4)
      IFIND=IFIND+4
C
C
C  DC ANALYSIS
C
   50 GSPR=SYMVAL(MNAM,1)*VALUE(I)
      CSAT=SYMVAL(MNAM,4)*VALUE(I)
      VTE=SYMVAL(MNAM,5)
C
C  INITIALIZE ON FIRST DC PASS
C
      IF (IPASS1.EQ.0) GO TO 60
      IF (MODE.GT.1) GO TO 60
      VD=VTE*ALOG(VTE/(1.414*CSAT))
      GO TO 70
C
C  COMPUTE NEW JUNCTION VOLTAGE
C
   60 VD=VNIM1(LOC3)-VNIM1(LOC2)
      IF (IPASS1.EQ.1) GO TO 70
      CALL JUNCT(VD,VDO(ISPOT),CSAT,VTE)
   70 VDO(ISPOT)=VD
C
C  COMPUTE EQUIVALENT DC CONDUCTANCES
C
   80 IF (VD.GT.0.0) GO TO 82
      EVD=1.0
      GEQ=CSAT/VTE+GMIN
      CD=GEQ*VD
      CDEQ=0.0
      GO TO 84
   82 EVD=EXP(VD/VTE)
      CD=CSAT*(EVD-1.0)+GMIN*VD
      GEQ=CSAT*EVD/VTE+GMIN
      CDEQ=GEQ*VD-CD
   84 IF (MODE.EQ.1) GO TO 500
C
C  CHARGE STORAGE ELEMENTS
C
  310 TAU=SYMVAL(MNAM,2)
      CZERO=SYMVAL(MNAM,3)*VALUE(I)
      PHIB=SYMVAL(MNAM,6)
      IF (VD.GE.0.0) GO TO 312
      SARG=SQRT(1.0-VD/PHIB)
      CTEMP=2.0*(TAU*CSAT*VD/VTE+2.0*PHIB*CZERO*(1.0-SARG))/DELTA
      CAPD=TAU*CSAT/VTE+CZERO/SARG
      GO TO 320
  312 CTEMP=2.0*(TAU*CSAT*(EVD-1.0)+VD*CZERO*(1.0+VD/(4.0*PHIB)))/DELTA
      CAPD=TAU*CSAT*EVD/VTE+CZERO*(1.0+VD/(2.0*PHIB))
C
C  SMALL SIGNAL PARAMETERS
C
  320 IF (MODE.GT.2) GO TO 350
      TRACUR(ISPOT+1)=CD
      TRACUR(ISPOT+2)=GEQ
      TRACUR(ISPOT+3)=CAPD
      GO TO 900
C
C  TRANSIENT ANALYSIS
C
  350 IF (IPASS1.EQ.0) GO TO 360
      CJN(ISPOT)=CTEMP
C
C  UPDATE AT FIRST ITERATION OF NEW TIMEPOINT
C
  360 IF (MODE.EQ.3) GO TO 370
      CJN(ISPOT)=CTEMP*(1.0+DELTA/DELOLD)-CJN(ISPOT)
C
C  COMPUTE CAPACITOR CONDUCTANCE AND EQUIVALENT CURRENT SOURCE
C
  370 GTEMP=2.0*CAPD/DELTA
      GEQ=GEQ+GTEMP
      CDEQ=CDEQ+GTEMP*VD+CJN(ISPOT)-CTEMP
C
C  LOAD CURRENT VECTOR
C
  500 VN(LOC2)=VN(LOC2)-CDEQ
      VN(LOC3)=VN(LOC3)+CDEQ
C
C  LOAD MATRIX
C
      YNL(LOC1)=YNL(LOC1)+GSPR
      YNL(LOC2)=YNL(LOC2)+GEQ
      YNL(LOC3)=YNL(LOC3)+GEQ+GSPR
      YNL(LOC4)=YNL(LOC4)-GSPR
      YNL(LOC5)=YNL(LOC5)-GEQ
      YNL(LOC6)=YNL(LOC6)-GSPR
      YNL(LOC7)=YNL(LOC7)-GEQ
  900 ISPOT=ISPOT+5
 1000 CONTINUE
      RETURN
      END
