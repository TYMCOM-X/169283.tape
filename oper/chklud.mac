TITLE LISLUD - LIST THE CUPERTINO LUD AND ACCOUNTING
SUBTTL J. MARCIN -1/4/72


VLISLD==10
JOBVER=137
LOC JOBVER
XWD 0,VLISLD
RELOC

EXTERNAL GETACT

INTERNAL USER,CUST,ICODE,MODE,PPN,IER

MLON
;AC'S
F=0
A=1
B=2
C=3
D=4
E=5
INDX2=6
N=7
N1=10
N2=11
N3=12
CH=13
BP=14
CH=13

WD=15
INDEX=12
P=17

;FLAGS IN RIGHT HALF OF F
TTY==1	;OUTPUT GOES TO TTY
NOSPC==2	;DON'T TYPE SPACE
LEAD==4	;DON'T TYPE LEADING ZEROES
DULOUT==10	;DUL IS BEING OUTPUT
NOLOK==40	;DON'T CHECK USERS.DAT
INITF==100	;THERE IS AN INIT FILE


;I/O CHANNELS
DSK=14
LPT=13
DSK1==15

;UUO'S
RESET=0
EXIT=12
LICTAB==-20	;GETTAB FOR LICENSE
SETLIC==-10	;SET LICENSE

OPRLIC==100000	;OPERATOR LICENSE

LSTBUF:	JFCL
	CALLI RESET
	MOVE P,PDP
	SETZ F,
	HRROI A,LICTAB
	GETTAB A,
	MOVEI A,0	;SAY NO LICENSE
	TLNE A,OPRLIC	;DOES HE HAVE OPER LIC??
	JRST LICOK
	HRLS A
	CALLI A,SETLIC
LICOK:	PUSHJ P,GETDSK
	TTCALL 3,[ASCIZ/TYPE "H" FOR HELP.
/]
LISLUD:	TTCALL 3,[ASCIZ /*/]
	TTCALL 4,CH
	TTCALL 11,0
	CAIN CH,"L"
	JRST LISTIT
	CAIN CH,"D"
	JRST LISDUL
	CAIN CH,"S"
	JRST SORTIT
	CAIN CH,"T"
	JRST TYPIT
	CAIN CH,"P"
	JRST TYPPN
	CAIN CH,"C"
	JRST CHKIT
	CAIN CH,"A"
	JRST ALLOUT
	CAIN CH,"N"
	JRST NAMOUT
	CAIN CH,"H"
	JRST HELP
	CAIN CH,"Q"
	CALLI EXIT
	CAIN CH,"E"
	CALLI EXIT
	JRST LISLUD-1

HELP:	TTCALL 3,HELMSG
	JRST LISLUD
HELMSG:	ASCIZ / YOU HAVE THE FOLLOWING OPTIONS:
L	LISTS LUD.SYS AND ACCOUNTING FILES ON LPT
	(OR ON LUD.LST IF DSK ASSIGNED LPT)
D	LISTS DUL.SYS ON THE LPT (OR DUL.LST IF DSK ASSIGNED LPT)
S	AFTER L OR D WRITES A FILE ON THE DISK, TO SORT:
	 LUD BY PPN, TYPE SYS:@LUDPPN(WRITES ON FILE LUD.PPN)
	 DUL BY NAME, TYPE SYS:@DULNAM(WRITES ON FILE DUL.NAM)
	 DUL BY PPN, TYPE SYS:@DULPPN(WRITES ON FILE DUL.PPN)
	NOTE:  TO EXIT, TYPE ^C.
		TO CONTINUE IN LISLUD, TYPE SYS:LISLUD!
T	TYPES THE LUD AND ACCOUNTING ENTRY FOR THE NAME
	(TYPE NAME ON NEXT LINE)
P	TYPES THE LUD AND ACCOUNTING ENTRY FOR THE PPN
	(TYPE PPN ON NEXT LINE)
N	TYPES THE NAME AND ADDRESS ENTRY FOR THE CUSTOMER NUMBER
	(TYPE CUSTOMER NUMBER ON THE NEXT LINE)
A	WRITES ALL NAMES AND ADDRESSES IN A DSK FILE CALLED ADRES.DAT
C	CONSISTENCY CHECK BETWEEN ACCTG. FILES,LUD.SYS AND DUL.SYS
	PROGRAM EXITS WHEN COMPLETE
H	TYPES THIS TEXT
E(OR Q)	EXITS
/

LISTIT:	PUSHJ P,GETLUD
	SETOM ,MODE
	PUSHJ P,INTACT
	SKIPE A,IER
	JRST USRERR
	PUSHJ P,GETLPT
	MOVEI A,1
	CAIL A,^D888
	JRST LISEND
	USETI DSK,@A
	INPUT DSK,LUDLST
	STATZ DSK,760000
	JRST DSKERR
LIST1:	SETZ INDEX,
	SKIPN B,LUDBLK(INDEX)
	AOJA A,LISTIT+7
	JUMPG B,LIST2
	PUSHJ P,ROVBLK
	JRST LIST1
LIST2:	SKIPN ,LUDBLK+4(INDEX)
	JRST .+4
	PUSH P,A
	PUSHJ P,ENTOUT
	POP P,A
	LDB C,NENTP
	ADD INDEX,C
	JRST LIST1+1
LISEND:	CLOSE LPT,
	RELEASE LPT,
	JRST LISLUD

GETDSK:	INIT DSK,17
	SIXBIT /SYS/
	0
	CALLI EXIT
	POPJ P,0

ENTOUT:	MOVE WD,LUDBLK+2(INDEX)
	ANDI WD,177	;GET SIZ OF ENTRY
	SUBI WD,10
	MOVEM WD,WDTOGO
	MOVE N,LUDBLK(INDEX)
	MOVEM N,USER
	MOVE BP,[POINT 3,LUDBLK(INDEX)]	;OUTPUT PPN
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	MOVE  N,LUDBLK+1(INDEX)
	ANDI N,377
	MOVEI A,4
	PUSHJ P,DECPRT
	MOVE BP,[POINT 2,LUDBLK+3(INDEX),1]
	MOVEI N,1
	PUSHJ P,OCTOUT	;OUTPUT LANGUAGE BITS
	HRRZ A,LUDBLK+3(INDEX)
	PUSHJ P,SPACE
	MOVEI N,1
	MOVEI CH,"S"
	TLNE A,100000
	MOVEI CH,"D"	;DAYLIGHT SAVINGS(OR STANDARD)
	PUSHJ P,SIXOUT+5
	HLRZ N,LUDBLK+3(INDEX)
	ANDI N,76000
	LSH N,-12
	MOVEI A,5
	PUSHJ P,DECPRT+1	;TIME ZONE
	MOVE N,LUDBLK+1(INDEX)
	ANDI N,377
	MOVEI A,4
	PUSHJ P,DECPRT+1	;DISTRICT
	MOVE BP,[POINT 3,LUDBLK+3(INDEX),17]
	MOVEI N,6
	PUSHJ P,OCTOUT	;OUTPUT PRIV. BITS
	PUSH P,INDEX
	LDB CH,INITP
	JUMPE CH,NXTST1
	TRO F,INITF
	ADDI INDEX,3
NXTST1:	PUSHJ P,SPACE
	MOVE BP,[POINT 6,LUDBLK+5(INDEX)]
	MOVEI N,4
	PUSHJ P,SIXOUT+3	;STR NAME
	MOVE N,LUDBLK+6(INDEX)
	PUSHJ P,DECPRT	;QUOTA
	MOVE N,LUDBLK+7(INDEX)
	PUSHJ P,DECPRT	;QUOTA
	POP P,INDEX
	MOVEI A,3
	MOVEM A,ICODE
	JSA 16,GETACT
	SKIPN A,IER
	JRST .+4
	CAIN A,1
	JRST NOOUT
	JRST USRERR
	MOVE BP,[POINT 3,USER+1,29]
	MOVEI N,2
	PUSHJ P,OCTOUT
	MOVE N,USER+2
	MOVEI A,4
	PUSHJ P,DECPRT+1
	MOVE N,USER+4
	MOVEI A,3
	PUSHJ P,DECPRT+1
	MOVE N,USER+3
	PUSHJ P,SPACE
	MOVEI A,3
	PUSHJ P,DECPRT+1
INIT01:	TRZN	F,INITF
	JRST SP21OUT
	MOVE A,WDTOGO
	SUBI A,3
	MOVEM A,WDTOGO
	PUSHJ P,SPACE
	MOVEI CH,"("
	PUSHJ P,OUCH
	MOVEI N,^D12
	MOVE BP,[POINT 6,LUDBLK+6(INDEX)]
	PUSHJ P,SIXOUT+3
	MOVEI CH,")"
	PUSHJ P,OUCH
	MOVEI N,6
	MOVE BP,[POINT 6,LUDBLK+5(INDEX)]
	TRO F,NOSPC
	PUSHJ P,SIXOUT
CHKSTR:	PUSHJ P,CRLF
	SKIPN A,WDTOGO
	POPJ P,0
	SUBI A,3
	MOVEM A,WDTOGO
	MOVE INDX2,INDEX
	LDB CH,INITP
	ADDI INDEX,5
	SKIPE ,CH
	ADDI INDEX,3
	ADDI INDEX,3
	MOVE BP,[POINT 3,LUDBLK(INDX2)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	MOVEI C,^D33
	PUSHJ P,SPACE
	SOJG C,.-1
	MOVE BP,[POINT 6,LUDBLK(INDEX)]
	MOVEI N,4
	PUSHJ P,SIXOUT+3
	MOVE N,LUDBLK+1(INDEX)
	PUSHJ P,SPACE
	PUSHJ P,DECPRT
	MOVE N,LUDBLK+2(INDEX)
	PUSHJ P,SPACE
	PUSHJ P,DECPRT
	MOVEI C,^D20
	PUSHJ P,SPACE
	SOJG C,.-1
	MOVE INDEX,INDX2
	JRST SP21OUT

NOOUT:	PUSHJ P,SPACE
	PUSHJ P,SPACE
	MOVE BP,[POINT 7,MSG3]
	PUSHJ P,MSGOUT
	JRST INIT01

SP21OUT:	MOVEI C,^D21
	PUSHJ P,SPACE
	SOJG C,.-1
	JRST CHKSTR

MSGOUT:	ILDB CH,BP
	JUMPE CH,ENDOUT
	PUSHJ P,OUCH
	JRST MSGOUT
ENDOUT:	POPJ P,0

LISDUL:	TRO F,DULOUT
	PUSHJ P,GETLPT
	PUSHJ P,GETDUL
	MOVEI A,1
LISD5:	CAIL A,^D102
	JRST LISEND
	USETI DSK1,@A
	INPUT DSK1,DULLST
	STATZ DSK1,760000
	JRST DSKER1
LISD1:	SETZ C,
	SKIPN B,DULBLK(C)
	AOJA A,LISD5
	JUMPG  B,LISD2
	PUSHJ P,RBLK1
	JRST LISD1
LISD2:	PUSHJ P,ENTD
	PUSHJ P,CRLF
	ADDI C,3
	JRST LISD1+1
ENTD:	MOVEI N,^D12
	MOVE BP,[POINT 3,DULBLK(C)]
	PUSHJ P,OCTOUT
	MOVE BP,[POINT 6,DULBLK+1(C)]
	PUSHJ P,SIXOUT
	TRO F,NOSPC
	PUSHJ P,SIXOUT
	POPJ P,0

;DO A RUN UUO ON SORT
SORTIT:	HRRZI A,RUNBLK
	CALLI A,35
	CALLI 12

TYPIT:	PUSHJ P,GETUSR
	MOVEM N,NAME
	PUSH P,INDEX
	PUSHJ P,HASHER
	POP P,INDEX
	PUSHJ P,GETLUD
	TRO F,NOLOK
	PUSHJ P,FINNAM
	JRST NOENTRY
	TRO F,TTY
	TTCALL 3,[ASCIZ/PPN AND AUN: /]
	PUSHJ P,OUT1
	JRST LISLUD

OUT1:	MOVE N,LUDBLK(INDEX)
	MOVEM N,USER
	MOVE BP,[POINT 3,LUDBLK(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
	MOVE BP,[POINT 7,DISMSG]
	PUSHJ P,MSGOUT
	MOVEI A,4
	MOVE N,LUDBLK+1(INDEX)
	ANDI N,377
	PUSHJ P,DECPRT+1
	PUSHJ P,CRLF
	MOVE B,LUDBLK+2(INDEX)
	TLNN B,200000
	JRST .+3
	MOVE BP,[POINT 7,NOMSG]
	PUSHJ P,MSGOUT
	MOVE B,LUDBLK+3(INDEX)
	TLNN B,100000
	JRST .+3
	MOVE BP,[POINT 7,DAYL]
	PUSHJ P,MSGOUT
	LDB N,[POINT 5,B,8]
	JUMPE N,.+6
	MOVE BP,[POINT 7,GMTMSG]
	PUSHJ P,MSGOUT
	MOVEI A,5
	PUSHJ P,DECPRT+1
	PUSHJ P,CRLF
	TRNN B,100000
	JRST .+3
	MOVE BP,[POINT 7,DETMSG]
	PUSHJ P,MSGOUT
	TRNN B,40000
	JRST .+3
	MOVE BP,[POINT 7,PROP]
	PUSHJ P,MSGOUT
	TRNN B,20000
	JRST .+3
	MOVE BP,[POINT 7,ACTSUP]
	PUSHJ P,MSGOUT
	SETZ N,
	LDB N,[POINT 2,B,28]
	MOVE BP,[POINT 7,@MODTAB(N)]
	PUSHJ P,MSGOUT
	MOVE WD,LUDBLK+2(INDEX)
	ANDI WD,177
	SUBI WD,5
	MOVEM WD,N1
	PUSH P,INDEX
	LDB CH,INITP
	JUMPE CH,NXTONE
	TRO F,INITF
	ADDI INDEX,3
	SUBI N1,3
NXTONE:	MOVE BP,[POINT 6,LUDBLK+5(INDEX)]
	MOVEI N,4
	PUSHJ P,SIXOUT+1
	PUSH P,N1
	PUSHJ P,SPACE
	MOVE N,LUDBLK+6(INDEX)
	PUSHJ P,DECPRT
	PUSHJ P,SPACE
	MOVE N,LUDBLK+7(INDEX)
	PUSHJ P,DECPRT
	POP P,N1
	MOVEI CH,"."
	PUSHJ P,OUCH
	SUBI N1,3
	ADDI INDEX,3
	JUMPG N1,NXTONE
	POP P,INDEX
	SETOM ,MODE
	MOVEI A,1
	MOVEM A,ICODE
	JSA 16,GETACT
	SKIPE A,IER
	JRST USRERR
	MOVEI A,3
	MOVEM A,ICODE
	JSA 16,GETACT
	SKIPN A,IER
	JRST RESOUT
	CAIN A,1
	JRST NOACTG
	JRST USRERR
RESOUT:	MOVE BP,[POINT 7,PRC]
	PUSHJ P,MSGOUT
	MOVE BP,[POINT 3,USER+1,29]
	MOVEI N,2
	PUSHJ P,OCTOUT
	MOVE BP,[POINT 7,TRC]
	PUSHJ P,MSGOUT
	MOVE N,USER+2
	MOVEI A,4
	PUSHJ P,DECPRT+1
	MOVE BP,[POINT 7,CUS]
	PUSHJ P,MSGOUT
	MOVE N,USER+4
	MOVEI A,3
	PUSHJ P,DECPRT+1
	MOVE BP,[POINT 7,SLS]
	PUSHJ P,MSGOUT
	MOVE N,USER+3
	MOVEI A,3
	PUSHJ P,DECPRT+1
INITOU:	TRZN F,INITF
	JRST DONTYP
	MOVE BP,[POINT 7,INIT1]
	PUSHJ P,MSGOUT
	MOVE BP,[POINT 6,LUDBLK+6(INDEX)]
	MOVEI N,^D12
	PUSHJ P,SIXOUT+3
	MOVE BP,[POINT 7,INIT2]
	PUSHJ P,MSGOUT
	MOVE BP,[POINT 6,LUDBLK+5(INDEX)]
	TRO F,NOSPC
	PUSHJ P,SIXOUT
DONTYP:	PUSHJ P,CRLF
	POPJ P,0

;OUTPUT SALESMAN CODE

NOENTRY:	TTCALL 3,[ASCIZ /NO SUCH USER.
/]
	JRST LISLUD

MSG3:	ASCIZ/
NO ACCOUNTING ENTRY./
NOACTG:	MOVE BP,[POINT 7,MSG3]
	PUSHJ P,MSGOUT
	POPJ P,0

ALLOUT:	TRZ F,TTY
	INIT LPT,1
	SIXBIT /DSK/
	LPTO,,0
	CALLI 12
	OUTBUF LPT,2
	MOVE A,[SIXBIT/ADRES/]
	HRLZI B,(SIXBIT/LST/)
	SETZB C,D
	ENTER LPT,A
	CALLI EXIT
	SETZM ,MODE
	PUSHJ P,INTACT
	SKIPE A,IER
	JRST CUSERR
	MOVEI A,4
	MOVEM A,ICODE
NXTCUS:	JSA 16,GETACT
	SKIPN N,IER
	JRST .+6
	CAIE N,2
	JRST CUSERR
	CLOSE LPT,
	RELEASE LPT,
	JRST LISLUD
	PUSHJ P,ONEOUT
	JRST NXTCUS

NAMOUT:	TRO F,TTY
	PUSHJ P,GETCUS
	SETZM ,MODE
	PUSHJ P,INTACT
	SKIPE A,IER
	JRST CUSERR
	MOVEI A,3
	MOVEM A,ICODE
	JSA 16,GETACT
	SKIPN N,IER
	JRST .+5
	CAIE N,1
	JRST CUSERR
	TTCALL 3,[ASCIZ/NO SUCH CUSTOMER.
/]
	JRST LISLUD
	PUSHJ P,ONEOUT
	JRST LISLUD

ONEOUT:	MOVE BP,[POINT 7,CUST+11]
	MOVEI N,^D30
	PUSHJ P,ASCOUT
	MOVE BP,[POINT 7,POMSG]
	PUSHJ P,MSGOUT
	MOVE BP,[POINT 7,CUST+3]
	MOVEI N,^D30
	PUSHJ P,ASCOUT
	MOVE BP,[POINT 7,DISMSG]
	PUSHJ P,MSGOUT
	MOVE N,CUST+1
	PUSHJ P,DECPRT
	PUSHJ P,CRLF
	SKIPN N,CUST+116
	JRST .+4
	MOVE BP,[POINT 7,NMDIS]
	PUSHJ P,MSGOUT
	PUSHJ P,DECPRT
	MOVE N,CUST+113
	JUMPE N,.+4
	MOVE BP,[POINT 7,MINMSG]
	PUSHJ P,MSGOUT
	MOVE N,CUST+115
	JUMPE N,.+3
	MOVE BP,[POINT 7,TIMSG]
	PUSHJ P,MSGOUT
	MOVE BP,[POINT 7,CUST+17]
	PUSHJ P,ADDOUT
	MOVE BP,[POINT 7,ZIPMSG]
	PUSHJ P,MSGOUT
	MOVE N,CUST+114
	PUSHJ P,DECPRT+1
	PUSHJ P,CRLF
	MOVE A,CUST+55
	AOJE A,ENDOUT
	MOVE BP,[POINT 7,CUST+55]
	PUSHJ P,ADDOUT
	POPJ P,0

ADDOUT:	MOVEI D,5
	MOVE C,BP
	ILDB CH,C
	CAIE CH,40
	JRST LINOUT
	MOVEI N,^D29
	ILDB CH,C
	CAIE CH,40
	JRST LINOUT
	SOJG N,.-3
	POPJ P,0
LINOUT:	MOVEI N,^D30
	PUSHJ P,ASCOUT
	SOJG D,ADDOUT+1
	POPJ P,0

ASCOUT:	ILDB CH,BP
	PUSHJ P,OUCH
	SOJG N,ASCOUT
	PUSHJ P,CRLF
	POPJ P,0
	
POMSG:	ASCIZ/P.O.: /
DISMSG:	ASCIZ/DISTRICT: /
NMDIS:	ASCIZ/NEXT MONTH'S DISTRICT: /
MINMSG:	ASCIZ/MONTHLY MINIMUM APPLIES.
/
TIMSG:	ASCIZ/NON-TIMESHARING CUSTOMER.
/
ZIPMSG:	ASCIZ/ZIP CODE: /
NOMSG:	ASCIZ/NO 'TYMSHARE' MESSAGE AT LOGIN.
/
DAYL:	ASCIZ/DAYLIGHT SAVINGS APPLIES.
/
GMTMSG:	ASCIZ/HOURS FROM GMT= /
DETMSG:	ASCIZ/DETACH ON DISCONNECT.
/
PROP:	ASCIZ/TYMSHARE PROPRIETARY.
/
ACTSUP:	ASCIZ/ACCOUNT SUPERVISOR.
/
PRC:	ASCIZ/PRICING CODE: /
TRC:	ASCIZ/TRACKING CODE: /
CUS:	ASCIZ/CUSTOMER: /
SLS:	ASCIZ/SALESMAN: /
INIT1:	ASCIZ/INIT: (/
INIT2:	ASCIZ/)/

;RETURN IF NOT FOUND
;RETURN+1 IF FOUND
FINNAM:	USETI DSK,@N1
	INPUT DSK,LUDLST
	STATZ DSK,760000
	JRST DSKERR
TYP1:	SETZ INDEX,
	SKIPN B,LUDBLK(INDEX)
	JRST CHKDEL
	JUMPG B,TYP2
	PUSH P,C
	PUSHJ P,ROVBLK
	POP P,C
	JRST TYP1
TYP2:	CAMN N,LUDBLK+4(INDEX)
	JRST TYPEND
	LDB B,NENTP
	ADD INDEX,B
	JRST	TYP1+1
TYPEND:	AOS (P)
	POPJ P,0
CHKDEL:	TRZE F,NOLOK
	POPJ P,0	;NOT FOUND
	MOVE N,DULBLK(C)
	MOVEM N,USER
	MOVEI N,3
	MOVEM N,ICODE
	SETOM ,MODE
	JSA 16,GETACT
	SKIPE A,IER
	JRST .+3
	AOS (P)
	POPJ P,0
	CAIN A,1
	POPJ P,0
	JRST	USRERR

TYPPN:	TRO F,TTY
	PUSHJ P,GETPPN
	PUSHJ P,GETDUL
	PUSHJ P,FINPPN
	JRST	NOENTRY
	TTCALL 3,[ASCIZ /USER NAME: /]
	MOVE BP,[POINT 6,DULBLK+1(C)]
	PUSHJ P,SIXOUT
	TRO F,NOSPC
	PUSHJ P,SIXOUT
	MOVE N,DULBLK+1(C)
	MOVE N1,DULBLK+2(C)
	PUSHJ P,CRLF
	JRST TYPIT+2

GETUSR:	TTCALL 3,[ASCIZ /USER: /]
	SETZB N,N1
	MOVE BP,[POINT 6,N]
	MOVEI N2,^D12
	TTCALL 4,CH
	CAIN CH,15
	JRST GOTUSR
	SUBI CH,40
	IDPB CH,BP
	SOJG N2,.-5
GOTUSR:	TTCALL 11,0
	POPJ P,0

GETPPN:	TTCALL 3,[ASCIZ /PPN: /]
	SETZ A,
	MOVEI N2,6
	MOVE BP,[POINT 3,A,17]
	PUSHJ P,GETCHR
	JRST GOTSEC
	JRST FIRSTIN
	IDPB CH,BP
	SOJG N2,.-4
	PUSHJ P,GETCHR
	JRST GOTSEC
	SKIPA
	JRST .-3
FIRSTIN: JUMPE N2,SECOND
	LSH A,-3
	SOJG N2,.-1
SECOND:	HRLZM A,N
	SETZ A,
	MOVEI N2,6
	MOVE BP,[POINT 3,A,17]
	PUSHJ P,GETCHR
	JRST GOTSEC
	JRST BADCHR
	IDPB CH,BP
	SOJG N2,.-4
	PUSHJ P,GETCHR
	JRST GOTSEC
	JRST BADCHR
	JRST .-3
GOTSEC:	JUMPE N2,.+3
	LSH A,-3
	SOJG N2,.-1
	HRRM A,N
	TTCALL 11,0
	POPJ P,0

GETCHR:	TTCALL 4,CH
	CAIN CH,15
	POPJ P,0
	CAIE CH,54
	JRST .+3
	AOS (P)
	POPJ P,0
	SUBI CH,60
	CAILE CH,7
	JRST BADCHR
	JUMPL CH,BADCHR
	AOS (P)
	AOS (P)
	POPJ P,0

GETCUS:	TTCALL 3,[ASCIZ/CUSTOMER NUMBER: /]
	SETZB A,CUST
	MOVEI N2,4
	MOVEI N,^D1000
NXTCH1:	TTCALL 4,CH
	CAIN CH,15
	JRST GOTCUS
	SUBI CH,60
	CAILE CH,11
	JRST BADCH1
	JUMPL CH,BADCH1
	IMUL CH,N
	ADD A,CH
	IDIVI N,^D10
	SOJG N2,NXTCH1
GOTCUS:	JUMPE N2,.+3
	IDIVI A,^D10
	SOJG N2,.-1
	MOVEM A,CUST
	TTCALL 11,0
	POPJ P,0

BADCH1:	TTCALL 3,[ASCIZ /ILLEGAL CHARACTER.
/]
	TTCALL 11,0
	JRST GETCUS

BADCHR:	TTCALL 3,[ASCIZ /ILLEGAL CHARACTER.
/]
	TTCALL 11,0
	JRST LSTBUF

FINPPN:	MOVE D,N
	IDIVI N,^D101
	ADDI N1,1
	USETI DSK1,@N1
	INPUT DSK1,DULLST
	STATZ DSK1,760000
	JRST DSKER1
	SETZ C,
PPN1:	SKIPN ,DULBLK(C)
	POPJ P,0
	SKIPG ,DULBLK(C)
	JRST PPN2
	CAMN D,DULBLK(C)
	JRST	PPNEND
	ADDI C,3
	CAIGE C,200
	JRST PPN1
	POPJ P,0
PPN2:	PUSHJ P,RBLK1
	JRST PPN1-1
PPNEND:	MOVE N1,DULBLK+1(C)
	MOVEM N1,NAME
	MOVE N1,DULBLK+2(C)
	MOVEM N1,NAME+1
	AOS (P)
	POPJ P,0

CHKIT:	TRO F,TTY
	PUSHJ P,GETLUD
	PUSHJ P,GETDUL
	SETZ INDX2,
	SETZM ,MODE
	PUSHJ P,INTACT
	SETOM ,MODE
	PUSHJ P,INTACT
	TTCALL 3,[ASCIZ/CHECKING LUD.
/]
	MOVEI A,1
NXTBLK:	CAIL A,^D888
	JRST CHKDUL
	USETI DSK,@A
	INPUT DSK,LUDLST
	STATZ DSK,760000
	JRST DSKERR
LUD1:	SETZ INDEX,
	SKIPN B,LUDBLK(INDEX)
	AOJA A,NXTBLK
	JUMPG B,LUD2
	PUSHJ P,ROVBLK
	JRST LUD1
LUD2:	SKIPN ,LUDBLK+4(INDEX)
	JRST NXTLUD
	MOVE N,LUDBLK(INDEX)
	PUSHJ P,FINPPN
	JRST NODUL
	MOVE N,LUDBLK(INDEX)
	MOVEM N,USER
	MOVEM N,USERS1(INDX2)
	LDB N,[POINT 8,LUDBLK+1(INDEX),35]
	MOVEM N,USERS2(INDX2)
	MOVEI N,3
	MOVEM N,ICODE
	SETOM ,MODE
	JSA 16,GETACT
	SKIPN N,IER
	JRST NXTLUD-1
	CAIN N,1
	JRST NOACT3
	JRST USRERR
	AOJA INDX2,.+1
NXTLUD:	LDB C,NENTP
	ADD INDEX,C
	JRST LUD1+1

NODUL:	TTCALL 3,[ASCIZ /NO ENTRY IN DUL FOR /]
	MOVE BP,[POINT 3,LUDBLK(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
	PUSHJ P,CRLF
	JRST BLKNO

NODUL1:	TTCALL 3,[ASCIZ /HASH LOC OF DUL NAME =/]
	MOVE BP,[POINT 3,N1,17]
	MOVEI N,6
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
BLKNO:	TTCALL 3,[ASCIZ /LUD BLK NO. =/]
	MOVEI N,6
	MOVE BP,[POINT 3,A,17]
	MOVEI N,6
	PUSHJ P,OCTOUT
DONMSG:	PUSHJ P,CRLF
	PUSHJ P,CRLF
	JRST NXTLUD

	TTCALL 3,[ASCIZ /HASH OF DUL NAME =/]
	MOVE N1,N
	MOVE BP,[POINT 3,N1]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
	TTCALL 3,[ASCIZ /ENTRY IN LUD =/]
	MOVE BP,[POINT 3,LUDBLK+4(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	JRST DONMSG

NOACT3:	TTCALL 3,[ASCIZ/ NO ACCOUNTING ENTRY FOR/]
	MOVE BP,[POINT 6,DULBLK+1(C)]
	PUSHJ P,SIXOUT
	TRO F,NOSPC
	PUSHJ P,SIXOUT
	TTCALL 3,[ASCIZ/(/]
	MOVE BP,[POINT 3,LUDBLK(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT+1
	TTCALL 3,[ASCIZ/)
/]
	JRST NXTLUD

BADDUL:	TTCALL 3,[ASCIZ /PPN /]
	PUSHJ P,DULPPN
	TTCALL 3,[ASCIZ / IS IN BLOCK /]
	MOVE BP,[POINT 3,A,17]
	MOVEI N,6
	PUSHJ P,OCTOUT
	TTCALL 3,[ASCIZ /.
(IT SHOULD BE IN BLOCK /]
	MOVE BP,[POINT 3,N1,17]
	MOVEI N,6
	PUSHJ P,OCTOUT
	TTCALL 3,[ASCIZ /.)
/]
	PUSHJ P,CRLF
	JRST NXTDUL

BAD2:	TTCALL 3,[ASCIZ/NO ENTRY IN LUD FOR /]
	MOVE BP,[POINT 6,DULBLK+1(C)]
	PUSHJ P,SIXOUT
	TRO F,NOSPC
	PUSHJ P,SIXOUT
	TTCALL 3,[ASCIZ/ (/]
	PUSHJ P,DULPPN
	TTCALL 3,[ASCIZ/)
/]
	JRST NXTDUL

DISDIF:	TTCALL 3,[ASCIZ /PPN: /]
	MOVE BP,[POINT 3,USER]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	TTCALL 3,[ASCIZ/  LUD DIST. = /]
	MOVE BP,[POINT 3,N1,29]
	MOVEI N,2
	PUSHJ P,OCTOUT
	TTCALL 3,[ASCIZ/ ACCTG. DIST. = /]
	MOVE BP,[POINT 3,USERS2(INDEX),29]
	MOVEI N,2
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
	POPJ P,0

DULPPN:	MOVE BP,[POINT 3,DULBLK(C)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	POPJ P,0

BAD3:	TTCALL 3,[ASCIZ /DUL PPN =/]
	PUSHJ P,DULPPN
	PUSHJ P,CRLF
	TTCALL 3,[ASCIZ /LUD PPN =/]
	MOVEI N,^D12
	MOVE BP,[POINT 3,LUDBLK(INDEX)]
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
	PUSHJ P,CRLF
	JRST NXTDUL

CHKDUL:	TTCALL 3,[ASCIZ/CHECKING DUL.
/]
	MOVEI A,1
	CAIL A,^D102
	JRST ENDLUD
	USETI DSK1,@A
	INPUT DSK1,DULLST
	STATZ DSK1,760000
	JRST DSKER1
DUL1:	SETZ C,
	SKIPN B,DULBLK(C)
	AOJA A,CHKDUL+2
	JUMPG B,.+3
	PUSHJ P,RBLK1
	JRST DUL1
	MOVE N,DULBLK(C)
	IDIVI N,^D101
	ADDI N1,1
	CAME N1,A
	JRST BADDUL
DUL2:	MOVE N,DULBLK+1(C)
	MOVE N1,DULBLK+2(C)
	PUSH P,C
	PUSH P,A
	PUSH P,INDEX
	PUSHJ P,HASHER
	POP P,INDEX
	POP P,A
	POP P,C
	PUSHJ P,FINNAM
	JRST BAD2	;NOT FOUND
	MOVE N,DULBLK(C)
	CAME N,LUDBLK(INDEX)
	JRST BAD3
NXTDUL:	ADDI C,3
	JRST DUL1+1

ENDLUD:	TTCALL 3,[ASCIZ/CHECKING USERNA.MES.
/]
	TLZ F,400000
	PUSHJ P,INTACT
	MOVEI A,4
	MOVEM A,ICODE
CHKUSR:JSA 16,GETACT
	SKIPE A,IER
	JRST USRERR
	MOVE INDEX,INDX2
	MOVE N,USER
	CAMN N,USERS1(INDEX)
	JRST .+3
	SOJG INDEX,.-2
	PUSHJ P,USET
	MOVE N1,USER+3
	IDIVI N1,^D100
	CAME N1,USERS2(INDEX)
	PUSHJ P,DISDIF
	JRST CHKUSR
	TTCALL 3,[ASCIZ/DONE CHECKING.
/]
	CALLI 1,EXIT

USET:	TLON	F,400000
	TTCALL 3,[ASCIZ/
THE FOLLOWING USERS ARE IN USERS.DAT
BUT NOT IN THE LUD:
/]
	PUSHJ P,SPACE
	POPJ P,0

GETLPT:	INIT LPT,1
	SIXBIT /LPT/
	LPTO,,0
	JRST NOLPT
	OUTBUF LPT,2
	MOVE A,[SIXBIT/LUD   /]
	TRZE F,DULOUT
	MOVE A,[SIXBIT/DUL/]
	HRLZI B,(SIXBIT/LST/)
	SETZB C,D
	ENTER LPT,A
	CALLI EXIT
	POPJ P,0

NOLPT:	TTCALL 3,[ASCIZ /LPT NOT AVAILABLE.
/]
	JRST LISLUD

OUCH:	TRNE F,TTY
	JRST TOUCH
	SOSG ,LPTO+2
	OUTPUT LPT,0
	IDPB CH,LPTO+1
	POPJ P,0

TOUCH:	TTCALL 1,CH
	POPJ P,0

RBLK1:	HRRZ C,DULBLK(C)
	USETI DSK1,@C
	INPUT DSK1,DULLST
	STATZ DSK1,760000
	JRST DSKER1
	POPJ P,0

ROVBLK:	HRRZ C,LUDBLK(INDEX)
	USETI DSK,@C
	INPUT DSK,LUDLST
	STATZ DSK,760000
	JRST DSKERR
	POPJ P,0

GETDUL:	INIT DSK1,17
	SIXBIT /SYS/
	0
	CALLI EXIT
	MOVE A,[SIXBIT/DUL/]
	HRLZI B,(SIXBIT/SYS/)
	SETZB C,D
	LOOKUP DSK1,A
	SKIPA
	POPJ P,0
DSKER1:	TTCALL 3,[ASCIZ/ERROR IN DUL.
/]
	CALLI EXIT

GETLUD:	MOVE A,[SIXBIT /LUD/]
	HRLZI	B,(SIXBIT/SYS/)
	SETZB C,D
	LOOKU,A
	SKIPA
	POPJ P,0
DSKERR:	TTCALL 3,[ASCIZ/ERROR IN LUD.
/]
	CALLI EXIT

SIXOUT:	MOVEI N,6
	TRZN F,NOSPC
	PUSHJ P,SPACE
	ILDB CH,BP
	ADDI CH,40
	PUSHJ P,OUCH
	SOJG N,SIXOUT+3
	POPJ P,0

DECPRT:	SETZ A,
	MOVEI D,12
	CAIG N,0
	JRST ZEROUT
CHECKD:	CAMLE N,TABD(A)
	JRST RDXPRT
	PUSHJ P,SPACE
	ADDI A,1
	JRST CHECKD
RDXPRT:	MOVMS N
	IDIVI N,(D)
	HRLM N1,0(P)
	SKIPN N
	PUSHJ P,SPC
	PUSHJ P,RDXPRT
	HLRZ CH,0(P)
	ADDI CH,"0"
	JRST OUCH
ZEROUT:	MOVEI N,7
	SUB N,A
	PUSHJ P,SPACE
	SOJG N,.-1
	MOVEI CH,"0"
	PUSHJ P,OUCH
	POPJ P,0

OCTOUT:	PUSHJ P,SPACE
	MOVE B,N
OCT2:	ILDB CH,BP
	JUMPN CH,OCT3
	TRNE F,LEAD
	SOJA N,OCT2
OCT3:	TRZ F,LEAD
	ADDI CH,60
	PUSHJ P,OUCH
	CAIN N,7
	JRST .+3
	SOJG N,OCT2
	POPJ P,0
	CAIG B,6
	SOJA N,OCT2
	MOVEI CH,54
	PUSHJ P,OUCH
	SOJA N,OCT2

CRLF:	MOVEI CH,15
	PUSHJ P,OUCH
	MOVEI CH,12
	PUSHJ P,OUCH
	POPJ P,0

SPC:	PUSHJ P,SPACE
	AOS (P)
	POPJ P,0

SPACE:	TRNE F,TTY
	JRST TSPACE
	MOVEI CH,40
	PUSHJ P,OUCH
	POPJ P,0

TSPACE:	TTCALL 3,[ASCIZ / /]
	POPJ P,0

INTACT:	MOVEI A,1
	MOVEM A,ICODE
	JSA 16,GETACT
	SKIPN A,IER
	POPJ P,0
	SKIPL ,MODE
	JRST CUSERR
USRERR:	TTCALL 3,[ASCIZ/
ERROR IN USERNA.MES.
/]
	CALLI EXIT
CUSERR:	TTCALL 3,[ASCIZ/
ERROR IN CUSTOM.ERS.
/]
	CALLI EXIT

HASHER:	MOVEI B,0
	MOVE C,[555555555555]
	MOVE D,[361275431652]
	MOVE E,[612754316523]
	JSR RND
	JSR RND
	JSR RND
	JSR RND
	XOR E,D
	MOVE N,E
	TLZ N,400000
	IDIVI N,^D887
	MOVE N,E
	XOR N,C
	ADDI N1,1
	POPJ P,0
RND:	0
	ADD D,N
	ROTC N,-22
	MOVEI A,5
RND1:	MOVE N2,C+1(B)
	MUL N2,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
	ADDM N3,D+1(B)
	AOJE B,RND2
	MOVNI B,1
	TRNE D,1
	SKIPL E
	MOVEI B,0
	EXCH C,E
RND2:	SOJG A,RND1
	JRST @RND
	POPJ P,0

LPTO:	BLOCK 3
	0
DULLST:	IOWD 200,DULBLK
	0
DULBLK:	BLOCK 200
LUDLST:	IOWD 200,LUDBLK
	0
LUDBLK:	BLOCK 200
PDP:	XWD	-20,.
	BLOCK 21

INITP:	POINT 1,LUDBLK+2(INDEX),27
WDTOGO:	0
NAME:	BLOCK 2
UNAME:	BLOCK 2
USER:	BLOCK 17
CUST:	BLOCK 130
ICODE:	0
IER:	0
MODE:	0
PPN:	0	;CUD10 PPN
USERS1:	BLOCK 4000
USERS2:	BLOCK 4000
RUNBLK:	SIXBIT/SYS/
	SIXBIT/SORT/
	0
	0
	0
	0
	0
MODTAB:	ASCIZ/PDP/
	ASCIZ/TYMX/
	ASCIZ/GE/
	ASCIZ/SUDS/

MODMSG:	ASCIZ/ MODE./

TABD:	^D999999;A=0(7 PLACES)
	^D99999	;A=1(6 PLACES)
	^D9999	;A=2(5 PLACES)
	^D999	;A=3(4 PLACES)
	^D99	;A=4(3 PLACES)
	^D9	;A=5(2 PLACES)
	0	;A=6(1 PLACE)
NENTP:	POINT 7,LUDBLK+2(INDEX),35
	END LSTBUF
   HOIï