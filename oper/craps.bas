10 REMARK "CRAPS": 
20 REMARK VER. A2.00  10/30/"71
30GOSUB 1790
40T1=1  
50I=1
60P1 = 0
70LET A=-2 
80 RANDOM
90IF T1>1 THEN 140
100PRINT "HOW MUCH TO BET"; 
110INPUT B  
120T1=T1 + 1
130GO TO 210
140PRINT 
150PRINT"HAVE YOU LOST ENOUGH TO QUIT YET";
160INPUT Q  
170IF Q=1 THEN 2160
180 REM GET ANOTHER BET 
190 PRINT "WELL, HOW MUCH NOW";
200 GOTO 110
210IF B<0 THEN 240
220IF B>100 THEN 240
230GO TO 300
240IF Z>0 THEN280
250PRINT"PLEASE OBSERVE THE HOUSE RULES OF BETS FROM $1 TO $100"  
260Z=Z+1 
270GO TO 100
280PRINT"COME ON NOW THE LIMIT IS 100 BUCKS" 
290GO TO 100
300IF T1>5 THEN 340
310PRINT"TYPE OF BET: 1 FOR PASS, 0 FOR NOPASS"; 
320INPUT T  
330GO TO 360
340PRINT"TYPE OF BET";
350INPUT T  
360Z=0
370I=1
380PRINT 
390IF T<0 THEN 430
400IF T>1 THEN 430
410IF T <> INT(T) THEN 430
420GO TO 470
430PRINT "INVALID BET OPTION" 
440GO TO 310
450I=1
460P1=0  
470PRINT "THEY COME,THEY ROLL"
480 D1=INT( ( ( (RND(0)+RND(TIM))/2) *6)+1)
490 D2=INT( ( ( (RND(-1)+RND(TIM)+RND(DAT))/3) *6)+1)
500D3=D1 + D2  
510 PRINT "YOU ROLL  ";D1;TAB(15)"AND"TAB(20)D2;TAB(25);
520IF D3=2 THEN 1050
530IF D3=3 THEN 1090
540IF D3=4 THEN 1130
550IF D3=5 THEN 1210
560IF D3=6 THEN 1250
570IF D3=7 THEN 1330
580IF D3=8 THEN 1390
590IF D3=9 THEN 1470
600IF D3=10 THEN 1510
610IF D3=11 THEN 1590
620IF D3=12 THEN 1630
630GO TO 480
640P1=D3 
650I = I+1  
660REMARK.  
670PRINT "SO YOUR POINT IS"P1 
680GO TO 480
690IF D3=7 THEN 740
700IF D3 = P1 THEN 900
710I= I + 1 
720PRINT "...ROLL AGAIN"
730GO TO 480
740IF T=1 THEN 850
750IF D3 = 2 THEN 830
760PRINT 
770PRINT 
780PRINT "YOU WIN..  YOU NOW HAVE $"B1"." 
790GOSUB 1670
800GO TO 90
810PRINT 
820PRINT 
830PRINT "A ROLL OF TWO IS A STANDOFF ON A DONT PASS BET"
840GO TO 480
850B1 = B1 - B 
860PRINT "AND CRAP OUT."
870PRINT 
880PRINT "YOU LOSE!  YOU NOW HAVE $"B1"." 
890GO TO 90
900IF I=1 THEN 930
910PRINT "AND MAKE YOUR POINT."  
920GOTO 940
930PRINT "AND CRAP OUT."  
940IF T=0 THEN 1000
950B1 = B1 + B
960PRINT
970PRINT
980PRINT "YOU WIN??  YOU NOW HAVE $"B1"."
990GO TO 90
1000B1 = B1 - B
1010PRINT
1020PRINT
1030PRINT "YOU LOSE** YOU NOW HAVE $"B1"."
1040GO TO 90
1050REM  
1060REMARK. "TWO SNAKE-EYES***CRAPS TWO***"  
1070IF I=1 THEN 740
1080GO TO 690
1090REM  
1100REMARK. "THREE***CRAPS THREE***"
1110IF I=1 THEN 740
1120GO TO 690
1130REM  
1140IF D1=D2 THEN 1180
1150REMARK.  "FOUR EASY WAY***FOUR***" 
1160IF I=1 THEN 640
1170GO TO 690
1180REMARK. "TWO TWOS***HARD WAY FOUR***" 
1190IF I=1 THEN 640
1200GO TO 690
1210REM  
1220REMARK. "FIVER FIVE***FIVE****" 
1230IF I=1 THEN 640
1240GO TO 690
1250REM  
1260IF D1=D2 THEN 1300
1270REMARK. "SIX EASY WAY***SOFT SIX***"  
1280IF I=1 THEN 640
1290GO TO 690
1300REMARK. "TWO THREES***SIX HARD WAY***"
1310IF I=1 THEN 640
1320GO TO 690
1330REM  
1340IF I>1 THEN 1370
1350REMARK. "*******SEVEN WINNER FRONT LINE******" 
1360GO TO 900
1370REMARK. "*****SEVEN WINNER COME AND THE DONT SIDE*****" 
1380GO TO 690
1390REM  
1400IF D1=D2 THEN 1440
1410REMARK. "EIGHT EASY WAY*****SOFT EIGHT*****"
1420IF I=1 THEN 640
1430GO TO 690
1440REMARK. "TWO FOURS*****EIGHT HARD WAY****"  
1450IF I=1 THEN 640
1460GO TO 690
1470REM  
1480REMARK. "NINER NINE*****NINE*****" 
1490IF I=1 THEN 640
1500GO TO 690
1510REM  
1520IF D1=D2 THEN 1560
1530REMARK. "TEN EASY WAY*****SOFT TEN*****" 
1540IF I=1 THEN 640
1550GO TO 690
1560REMARK. "TWO FIVES****HARD WAY TEN*****" 
1570IF I=1 THEN 640
1580GO TO 690
1590REM  
1600REMARK. "ELEVEN-ELEVEN****ELEVEN DID COME***"  
1610IF I=1 THEN 900
1620GO TO 690
1630REM  
1640REMARK. "BOXCARS-TWO SIXES****CRAPS TWELVE****"
1650IF I=1 THEN 740
1660GO TO 690
1670I=INT(5*RND(0))+1
1680ON I GO TO 1690,1710,1730,1750,1770
1690PRINT "THE DICE MUST BE LOADED" 
1700RETURN  
1710PRINT "MONTE-CARLO IS WAITING FOR YOU!!" 
1720RETURN  
1730PRINT "STICKY FINGERS!!"  
1740RETURN  
1750PRINT "SUCH LUCK FOR BEGINNERS!!!!!"  
1760RETURN  
1770 PRINT "WAIT UNTIL NEXT TIME, THEN I'LL WIN"
1780RETURN  
1790PRINT
1800PRINT
1810REM  
1820PRINT "WELCOME TO TYMSHARE'S REAL-TIME CRAP GAME"
1830PRINT
1840 PRINT "TO ANSWER QUESTION WITH YES OR NO: 1= YES, 0= NO." 
1850 PRINT  
1860PRINT"DO YOU NEED INSTRUCTIONS? (YES OR NO)";
1870INPUT S1
1880 PRINT  
1890IF S1 = 1 THEN 1910
1900RETURN  
1910REM INSTRUCTIONS 
1920PRINT"THE GAME IS PLAYED THE SAME AS DICE GAMES AT ALL THE MAJOR"
1930PRINT"GAMBLING HOUSES IN THE WORLD"
1940PRINT
1950PRINT"YOU CAN BET ANYTHING WITHIN THE HOUSE LIMITS OF 100 DOLLARS." 
1960PRINT
1970PRINT"THERE ARE TWO WAYS TO BET PRESENTLY IN THE GAME,PASS OR NOPASS." 
1980PRINT
1990PRINT"A PASS BET WINS IF THE PERSON ROLLING THE DICE,"  
2000PRINT"NAMELY YOU, ROLLS A 7 OR 11 ON THE FIRST ROLL, OR"
2010PRINT "MACHINE MAKES HIS POINT" 
2020PRINT
2030PRINT"A NOPASS BET WINS IF THE PERSON ROLLING THE DICE ROLLS A 3 OR 12"
2040PRINT"ON THE FIRST PASS OR FAILS TO MAKE HIS OR HER POINT" 
2050PRINT
2060PRINT "A POINT IS MADE BY BEING ROLLED AGAIN BEFORE A 7 IS ROLLED," 
2070PRINT "IF THE 7 COMES UP BEFORE THE POINT ROLLS AGAIN THE POINT" 
2080PRINT "IS NOT MADE" 
2090PRINT
2100PRINT"GOOD LUCK AND REMEMBER THAT WE ARE NOT PLAYING FOR MONEY"  
2110PRINT"BUT ANY LOSSES WILL BE ADDED TO YOUR MONTHLY COMPUTER STATEMENT" 
2120PRINT
2130PRINT
2140PRINT
2150RETURN  
2160STOP 
2170END  
