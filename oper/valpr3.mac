TITLE	VALPR3	ACCOUNTING RECORD BUILDER
; VERSION 3.0 - CHRIS NEUSTRUP - JUNE 12, 1973

SUBTTL C. NEUSTRUP - JUNE 12, 1973
	;FORTRAN CALLABLE BY CALL VALPR3(RTYPE,PCODE,FILENAME,PPN)
	;WHERE RTYPE IS THE RECORD TYPE
	;	4 FOR FINAL EXIT
	;	2 FOR INTERMEDIATE CHECKPOINT
	;	1 FOR CHECKPOINT (ALTERNATE)
	;	3 FOR INITIAL ENTRY
	;WHERE PCODE IS A PROGRAM IDENTIFICATION NUMBER
	;	IN THE RANGE 1 TO 777777 (OCTAL)
        ;WHERE FILENAME IS UP TO 5 ASCII CHARACTERS OF FILENAME WHERE
                ;ACCOUNTING IS TO BE WRITTEN
        ; WHERE PPN = WHERE BILLING FILE RESIDES

	ENTRY	VALPR3
	EXTERN JOBJDA,JOBHCU,	JOBFF,JOBREL
	INTERN JOBVER
JOBVER=137
LOC	JOBVER
XWD	0,6
RELOC

	;ACCUMULATORS

	A=1
	B=2
	C=3
	D=4
	BP=14
	BP1=15
	Q=16
	P=17

;CHANNELS(REDEFINED TO OTHER THAN ZERO DURING EXECUTION)
	Z=0
;CONSTTANTS
FILPPN=-25

VALPR3:	0			;ENTER HERE
	MOVEM	17,SAVE+17
	MOVEI	17,SAVE
	BLT	17,SAVE+16	;SAVE THE CALLERS AC'S
	MOVE	17,[XWD -20,PDP]
	PUSH	P,JOBFF		;SAVE JOBFF
	PUSH	P,JOBREL
	MOVE	A,JOBHCU
	CAIN	A,17		;HIGHEST CHANNEL ALREADY 17?
	PUSHJ	P,GETCHN	;YES,GO LOOK FOR FREE CHANNEL
	ADDI	A,1		;USE THE NEXT HIGHER CHANNEL THEN
	HRRZ	Z,A		;THIS IS THE CHANNEL NO.
	MOVSI	A,-TABSIZ	;NUMBER OF ITEMS IN UUO TABLE
	DPB	Z,[POINT 4,TABTOP(A),12]
	AOBJN	A,.-1
	SETZ	A,		;SET FOR THE CURRENT JOB
	CALLI	A,24		;GET PROJECT/PROGRAMMER NUMBER
	MOVEM	A,LINE		;SAVE IN THE FIRST WORD
	CALLI	A,14		;GET THE DATE
DTIME:	CALLI	B,22	;GET TIME OF DAY IN JIFFIES
	DPB	A,[POINT 12,B,11]
	MOVEM	B,LINE+1	;SAVE THE SECOND WORD
	SETZ	C,		;SET TO THE CURRENT JOB
TRUS:	CALLI	C,27		;GET THE TRU
	IDIVI	C,^D<10*10*10*10>	;CONVERT
	MOVEM	C,LINE+2	;SAVE THE THIRD WORD
	HRRZ	A,@(Q)		;GET RECORD TYPE 
	TRZ	A,777770	;REMOVE EXTRANEOUS BITS
	HRL	A,@1(Q)		;GET PROGRAM CODE
	SETZ	B,		;SET FOR CURRENT JOB
	CALLI	B,30		;GET THE JOB NO.
	DPB	B,[POINT 9,A,26]	;9 BITS,RH
	MOVEM	A,LINE+3	;SAVE IN FIFTH WORD
ARG3:	MOVE	A,@2(Q)	;GET CALLER'S FILENNME
	TRNE	A,1		;TRY TO CHECK IF READDY ASCII
	JRST FILERR		;BIT 35 SET, CANT BE ASCII
	SETZ	SAVFIL
	MOVE	BP1,[POINT 6,SAVFIL]	;SAVE IT
	MOVE	BP,[POINT 7,A]
	MOVEI	C,5
NXTCHR:	ILDB	B,BP
	SUBI	B,40	;IN SIXBIT
	IDPB	B,BP1
	SOJG	C,NXTCHR
	MOVSI	A,(SIXBIT/DAT/)
	MOVEM	A,SAVEXT
        MOVE    A,@3(Q)         ; GET PPN
        CAIN    A,0             ; IS = ZERO?
        JRST    ERROR           ; YES, ABORT
        MOVEM   A,SAVPPN        ; NO, STORE IT FOR LOOKUP
GETCOR:	HRRZ	C,JOBREL
	MOVEI	B,1(C)	;NEED 1K CORE
	CORE	B,
	JRST	[TTCALL 3,[ASCIZ/
NO USER CORE AVAILABLE FOR ACCOUNTING.
/]
	PUSHJ	P,NOTIFY]	;EXITS
	ADDI	C,1
	MOVEM	C,JOBFF		;GET OUR BUFFER SPACE IN NEW CORE
				;SO WE DON'T MESS UP CHAIN
	MOVEI	C,12		;SET RETRY LIMIT FOR ENTER
REINIT:	XCT OPN			;INIT IN ASCII MODE
	JRST	ERROR		;BAD NEWS
	PUSHJ	P,SETFIL	;SET UP FILE INFO
	XCT LKUP		;LOOKUP THE FILE
	JRST	NOFILE		;NOT THERE SO CREATE IT
	HLRE	B,FILE+3	;GET THE NEGATIVE WORD COUNT
	SKIPL	FILE+3	;SKIP IF NEGATIVE

	JRST	.+3
	MOVNS	B		;MAKE IT POSITIVE
	LSH	B,-7		;CONVERT TO BLOCK NUMBER
	ADDI	B,1		;BECAUSE NUMBERING FROM 1
	PUSHJ	P,SETFIL		;SET UP FILE INFO AGAIN
	XCT ENTR		;ENTER THE FILE
	JRST	INUSE		;CAN'T ENTER, PROBABLY IN USE
	XCT	INBF		;SET UP INPUT BUFFER
	XCT OUTBF		;SET UP AN OUTPUT BUFFER
	HLL	B,USTI		;FORM USETI COMMAND
	XCT	B
	XCT INPT		;INITIAL READ OF THE LAST BLOCK
	XCT STZ34		;CHECK STATUS
	JRST ERROR
	XCT OUTPT		;DUMMY OUTPUT TO CLEAR BUFFER
	HLL	B,USTO		;FORM USETO COMMAND
	XCT	B		;WRITE INTO THE LAST BLOCK
	MOVEI	C,14		;RECORD COUNT IN A BLOCK
NEXREC:	PUSHJ	P,GETCHR	;GET A CHARACTER
	CAIE	B,32		;END OF FILE?
	JRST	MOVREC+1	;NO SO GO DEPOSIT THE RECORD
	MOVEI	C,60		;NUMBER OF CHARACTERS IN A MESSAGE
	HRLZI	A,(POINT 3,0)	;SET UP MESSAGE BYTE POINTER
	HRRI	A,LINE		;PUT IN MESSAGE ADDRESS
OUTMES:	ILDB	B,A		;GET OCTAL DIGIT
	ADDI	B,60		;CONVERT TO ASCII
	PUSHJ	P,PUTCHR	;OUTPUT CHARACTER
	SOJG	C,OUTMES	;DO THE WHOLE MESSAGE
	MOVEI	C,3		;NUMBER OF CHARACTERS ON THE END
	MOVE	A,[POINT 7,ENDLIN];BYTE POINTER TO END OF LINE
OUTEND:	ILDB	B,A		;GET A CHARACTER
	PUSHJ	P,PUTCHR	;OUTPUT CHARACTER
	SOJG	C,OUTEND	;DO THE WHOLE END
	XCT	CLS		;CLOSE THE FILE
	XCT	STZ74		;CHECK STATUS
	JRST	ERROR		;BAD NEWS
FINISH:	XCT	RELS		;RELEASE THE BUFFERS
	POP	P,C	;JOBREL
	CORE	C,
	JRST [TTCALL 3,[ASCIZ /
CANNOT RESTORE JOBREL.
/]
	PUSHJ	P,NOTIFY]	;EXITS
	POP	P,JOBFF		;AND RECOVER THE CALLER'S STORAGE
	HRLZI	17,SAVE		;RESTORE THE CALLERS AC'S
	BLT	17,17
	JRA	Q,3(Q)		;RETURN


MOVREC:	PUSHJ	P,GETCHR	;GET A CHARACTER
	CAIN	B,32	;EOF
	JRST	[TTCALL	3,[ASCIZ/
ACCOUNTING FILE OUT OF PHASE.
/]
	PUSHJ	P,NOTIFY]
	PUSHJ	P,PUTCHR	;OUTPUT CHARCTER
	CAIE	B,12		;LINE FEED?
	JRST	MOVREC		;NO MOVE ANOTHER CHARACTER
	SOJG	C,NEXREC	;HAS A WHOLE BLOCK BEEN MOVED??
	XCT OUTPT		;YES SO WRITE
	XCT	STZ74		;CHECK STATUS
	JRST	ERROR		;BAD NEWS
	JRST	OUTMES-3	;NOW GO WRITE THE MESSAGE


SETFIL:	MOVE	A,SAVFIL
	MOVEM	A,FILE
	MOVE	A,SAVEXT
	MOVEM	A,FILE+1
	MOVE	A,SAVPPN
	MOVEM	A,FILE+3
        MOVE    A,PROT
        MOVEM   A,FILE+2
	POPJ	P,0

NOFILE:	HRRZ	A,FILE+1
	JUMPN	A,CHKERR	;IF NOT FILE NOT FOUND ERROR, GO CHECK
	PUSHJ	P,SETFIL		;SET UP FILE INFO
	XCT	ENTR		;ENTER A NEW ACCOUNTING FILE
	JRST CHKERR
	XCT	OUTBF		;SET UP AN OUTPUT BUFFER
	XCT OUTPT		;DUMMY OUTPUT TO CLEAR BUFFER
	JRST	OUTMES-3	;NOW GO WRITE THE MESSAGE

CHKERR:	HRRZ	A,FILE+1
	CAIN	A,3		;FILE IN USE?
	JRST INUSE		;YES
	CAIE	A,2		;PROTECTION FAILUSE?
	JRST ERROR		;NO. ABORT
	TTCALL	3,[ASCIZ/
BILLING FILE MUST BE DECLARED PUBLIC.
/]
	PUSHJ	P,NOTIFY	;EXITS
NOTIFY:	TTCALL 3,[ASCIZ/
PLEASE NOTIFY YOUR ACCOUNT REPRESENTATIVE.
/]
	CALLI 12

INUSE:	SOJLE	C,ERROR		;ERROR OFF IF COUNT RUNS OUT
	MOVEI	A,12		;TEN SECONDS
	CALLI	A,31		;GO TO SLEEP
	JRST	REINIT		;AWAKE AGAIN SO RETRY INIT SEQUENCE


GETCHR:	SOSG	IBUF+2		;BUFFER EMPTY?
	JRST	.+3		;YES RETURN EOF CONDITION
	ILDB	B,IBUF+1	;PICK UP CHARACTER
	POPJ	P,		;RETURN
	MOVEI	B,32		;EOF CHARACTER
	POPJ	P,		;RETURN


PUTCHR:	SOSG	OBUF+2		;BUFFER FULL?
	JRST	.+3		;YES GO EMPTY
	IDPB	B,OBUF+1	;DEPOSIT CHARACTER
	POPJ	P,		;RETURN
	XCT OUTPT		;WRITE OUT A RECORD
	XCT STZ74		;CHECK STATUS
	JRST	ERROR		;BAD NEWS
	JRST	.-5		;GO PROCESS


GETCHN:	MOVSI	A,-17
NXTJDA:	SKIPE	JOBJDA(A)	;THIS CHANNEL IN USE?
	JRST	NXTCHN		;TRY NEXT CHANNEL
	AOS	(P)		;NO. INDEX RETURN TO AVOID INDEXING CHAN.
	POPJ	P,0		;NOW RETURN
NXTCHN:	AOBJN	A,NXTJDA		;TRY NEXT CHANNEL
	TTCALL	3,[ASCIZ/NO CHANNELS AVAILABLE FOR ACCOUNTING IO.
/]
	PUSHJ	P,NOTIFY
	CALLI 12		;EXIT. NO CHANNELS

FILERR:	TTCALL 3,[ASCIZ/FILE NAME NOT 5 OR LESS ASCII CHARS./]
	CALLI	12
ERROR:	TTCALL 3,[ASCIZ/PREMIUM ACCOUNTING SYSTEM FAILURE.
PLEASE NOTIFY THE OPERATOR IMMEDIATELY.
RUN ABORTED.
/]
	XCT RELS		;RELEASE THE DSK CHANNEL
	CALLI	12		;AND EXIT

ENDLIN:	BYTE	(7)15,12,32	;CR LF EOF
OPNBLK:	EXP	0
	SIXBIT/DSK/
	XWD OBUF,IBUF
SAVFIL:	BLOCK 1
SAVEXT:	BLOCK 1
SAVPPN:	BLOCK 1
FILE:	BLOCK 4	;SET UP 4-WORD FILE INFO HERE

IBUF:	BLOCK	3		;INPUT BUFFER HEADER
OBUF:	BLOCK	3		;OUTPUT BUFFER HEADER
LINE:	BLOCK	5		;ACCOUNTING RECORD BUFFER
PDP:	BLOCK 20
SAVE:	BLOCK 20	;SAVE CALLER'S AC'S HERE

TABTOP:		;TABLE OF UUO'S, CHANNEL DEFINED DURING EXEC.
CLS:	CLOSE	0,
RELS:	RELEASE	0,
STZ34:	STATZ 0,340000
STZ74:	STATZ	0,740000
OPN:	OPEN 0,OPNBLK
LKUP:	LOOKUP	0,FILE
ENTR:	ENTER	0,FILE
RENM:	RENAME	0,FILE
INBF:	INBUF	0,1
OUTBF:	OUTBUF	0,1
INPT:	INPUT	0,
OUTPT:	OUTPUT	0,
USTI:	USETI	0,
USTO:	USETO	0,
TABSIZ=	.-TABTOP
PROT:   100000,,0

CODEND:	END
