      PROGRAM SPICE (INPUT,OUTPUT,TAPE5=INPUT,TAPE6=OUTPUT)
C
C  SPICE IS AN ELECTRONIC CIRCUIT SIMULATION PROGRAM THAT WAS DEVELOPED
C  BY THE INTEGRATED CIRCUITS GROUP OF THE ELECTRONICS RESEARCH
C  LABORATORY AND THE DEPARTMENT OF ELECTRICAL ENGINEERING AND
C  COMPUTER SCIENCES AT THE UNIVERSITY OF CALIFORNIA, BERKELEY,
C  CALIFORNIA.  THE PROGRAM SPICE IS AVAILABLE FREE OF CHARGE TO ANY
C  INTERESTED PARTY.  THE SALE, RESALE, OR USE OF THIS PROGRAM FOR
C  PROFIT WITHOUT THE EXPRESS WRITTEN CONSENT OF THE DEPARTMENT OF
C  ELECTRICAL ENGINEERING AND COMPUTER SCIENCES, UNIVERSITY OF
C  CALIFORNIA, BERKELEY, CALIFORNIA, IS AN INFRINGEMENT OF COPYRIGHT
C  LAW AND IS STRICTLY FORBIDDEN.
C
C  VERSION 1
C
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/CARDS/ID,NUMFLD,ICARD1,KEOF
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/OCNTRL/IPRCOM(10,2),IPLCOM(10,2),PLTLIM(10,2,2),
     1   IACPRT(5,4),IACPLT(5,4),ACPLIM(5,4,2)
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
      COMMON/DC/ICVFLG,ITCELM,TCSTAR,TCSTOP,TCINCR,KSSOP,KINEL,KOVAR
      COMMON/TRAN/JTRFLG,TSTEP,TSTOP,TSTART,NOTINT,STEPS(5),ENDPTS(5),
     1   KFROUT,FORFRE,KFPTS
      COMMON/AC/JACFLG,FSTART,FSTOP,IDFREQ,FINCR,INOISE,NOSPRT,
     1   NOSOUT,NOSIN
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
      COMMON/SENVAR/NSENS,KSNOUT(10)
      COMMON/DESIGN/JDSFLG,NSPEC,NDES,ERROR,NOELEM(30),NELTPE(30),
     1   NOUNUM(10),DESVAL(10),WEIGHT(10)
C
C
      DATA IVERSE /4H 1G /
C
C  CONSTANTS AND INITIALIZATION
C
      IACCT=1
      KEOF=0
      CALL DATE(KDATE,KTIME)
      VNIM1(1)=0.0
      JUNODE(1)=0
      IUS=402
      ILS=1202
      MIRROR=800
      BOLTZ=1.38053E-23
      CHARGE=1.602095E-19
      GMIN=1.0E-12
      PERTOL=0.001
      VNTOL=50.0E-6
      TEMPS(1)=27.0
      TWOPI=8.0*ATAN2(1.0,1.0)
      RAD=360.0/TWOPI
      XLOG2=ALOG(2.0)
      XLOG10=ALOG(10.0)
C
C  BEGIN JOB  ...  READ AND PRINT TITLE CARD
C
   10 CALL SECOND(T1)
      NOGO=0
      IF (KEOF.EQ.1) GO TO 100
      READ (5,11) (JOBNAM(I),I=1,8)
   11 FORMAT (8A10)
      IF (EOF,5) 100,20
   20 WRITE (6,21) (JOBNAM(I),I=1,8),KDATE,KTIME,IVERSE
   21 FORMAT (1H6///5(1X,125(1H*)/)///1X,8A10,26X,*-----  SPICE  -----*
     1   ///21X,*DATE  *,A9,12X,*CLOCK  *,A9,12X,*VERSION  *,A4///
     2   1X,125(1H*)/////)
      DO 30 I=1,15
   30 RTIMES(I)=0.0
C
C  BEGIN THE JOB
C
      CALL READIN
      IF (NOGO.EQ.1) GO TO 50
      CALL ERRCHK
      IF (NOGO.EQ.1) GO TO 50
      CALL ANLYZE
      IF (NOGO.EQ.1) GO TO 50
C
C  FINISHED
C
      WRITE (6,41)
   41 FORMAT (////5X,*JOB CONCLUDED*//)
      GO TO 60
   50 WRITE (6,51)
   51 FORMAT (////5X,*JOB ABORTED*//)
   60 CALL SECOND(T2)
      ET=T2-T1
      COST=0.11667*ET
      WRITE (6,61) ET,COST
   61 FORMAT (5X,*TOTAL JOB TIME *,F10.3,* SEC*//20X,F10.3,* $$$*//)
C
C  JOB ACCOUNTING
C
      IF (IACCT.EQ.0) GO TO 10
      WRITE (6,66) (JOBNAM(I),I=1,8),KDATE,KTIME,IVERSE
   66 FORMAT (1H6/////1X,8A10//5X,*DATE  *,A9,10X,*CLOCK  *,A9,10X,
     1   *VERSION  *,A4)
      WRITE (6,71) NUNODS,NUMNOD,NUMEL,(JELCNT(I),I=7,10)
   71 FORMAT (///3X,*NUNODS*,1X,*NUMNOD*,2X,*NUMEL*,2X,*BJTS*,2X,
     1   *DIODES*,2X,*JFETS*,2X,*MFETS*//7(4X,I3))
      WRITE (6,76) NUMTEM,ICVFLG,JTRFLG,JACFLG,NOSOUT,NOGO
   76 FORMAT (///3X,*NUMTEM*,1X,*ICVFLG*,1X,*JTRFLG*,1X,*JACFLG*,1X,
     1   *NOSOUT*,3X,*NOGO*//6(4X,I3))
      WRITE (6,81) (RTIMES(I),I=1,10),ET
   81 FORMAT (////10X,6HREADIN,14X,F10.3//10X,5HSETUP,15X,F10.3//
     1   10X,6HTRCURV,14X,F10.3,10X,F10.3//
     2   10X,4HDCAN,16X,F10.3,10X,F10.3//10X,4HACAN,16X,F10.3//
     3   10X,6HTRANAN,14X,F10.3,10X,F10.3//10X,6HOUTPUT,14X,F10.3//
     4   10X,5HTOTAL,15X,F10.3)
      GO TO 10
  100 CALL EXIT
      END
      SUBROUTINE READIN
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/CARDS/ID,NUMFLD,ICARD1,KEOF
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/OCNTRL/IPRCOM(10,2),IPLCOM(10,2),PLTLIM(10,2,2),
     1   IACPRT(5,4),IACPLT(5,4),ACPLIM(5,4,2)
      COMMON/DC/ICVFLG,ITCELM,TCSTAR,TCSTOP,TCINCR,KSSOP,KINEL,KOVAR
      COMMON/TRAN/JTRFLG,TSTEP,TSTOP,TSTART,NOTINT,STEPS(5),ENDPTS(5),
     1   KFROUT,FORFRE,KFPTS
      COMMON/AC/JACFLG,FSTART,FSTOP,IDFREQ,FINCR,INOISE,NOSPRT,
     1   NOSOUT,NOSIN
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
      COMMON/SENVAR/NSENS,KSNOUT(10)
      COMMON/DESIGN/JDSFLG,NSPEC,NDES,ERROR,NOELEM(30),NELTPE(30),
     1   NOUNUM(10),DESVAL(10),WEIGHT(10)
C
C  READIN AND ERRCHK USE YNL, TSTORE, AND MATLOC FOR TEMPORARY STORAGE
C
C        TSTORE
C
C            1          NAME1(400)
C          401          MNAME1(400)
C          801          LOCAL1(400)
C         1201          IDNO(400)
C         1601          VALUE1(400)
C
C           YNL
C
C            1          FIELD(100)     ITABLE(2001)
C          101          ICODE(100)
C          201          IFIELD(10)
C
C        MATLOC
C
C            1          NODPL1(800)
C
      DIMENSION FIELD(1),ICODE(1)
      EQUIVALENCE (FIELD(1),YNL(1)),(ICODE(1),YNL(101))
      DIMENSION NAME1(1),MNAME1(1),LOCAL1(1),IDNO(1),VALUE1(1)
      EQUIVALENCE (NAME1(1),TSTORE(1)),(MNAME1(1),TSTORE(401)),
     1   (LOCAL1(1),TSTORE(801)),(IDNO(1),TSTORE(1201)),
     2   (VALUE1(1),TSTORE(1601))
      DIMENSION NODPL1(1)
      EQUIVALENCE (NODPL1(1),MATLOC(1))
C
C  KEYWORDS AND FORMAT INFORMATION IS STORED IN LOCAL ARRAYS
C
      DATA IBLNK /55555555555555B/
      DATA ISHIFT /1000000000000B/
      DATA JSHIFT /10000000000B/
      DATA KSHIFT /100000000B/
      DATA ILETI,ILETN,ILETV/11B,16B,26B/
      DATA LSAC,LSDC,LSDE,LSEX,LSFO,LSIM,LSIN,LSLI,LSMA,LSNO,LSOC,LSOP,
     1   LSOU,LSPH,LSPL,LSPR,LSPU,LSRE,LSSI,LSTC,LSTR/103B,403B,405B,
     2   530B,617B,1115B,1116B,1411B,1501B,1617B,1703B,1720B,1725B,
     3   2010B,2014B,2022B,2025B,2205B,2311B,2403B,2422B/
      DIMENSION NNODS(10)
      DATA NNODS/2,2,2,2,4,2,3,2,3,4/
      DIMENSION MODTYP(11),MTYPE(10),MOTYPE(10),TCODE(10),NAMPAR(25,5)
      DATA MODTYP /162016B,201620B,160720B,200720B,045555B,230204B,
     1   161206B,201206B,161517B,201517B,305555B/
      DATA MTYPE /1,1,2,2,3,3,4,4,5,5/
      DATA MOTYPE /1,1,2,2,3,4,5,5,6,6/
      DATA TCODE /1.0,-1.0,1.0,-1.0,0.0,0.0,1.0,-1.0,1.0,-1.0/
      DATA NAMPAR /      0,020655B,022255B,220255B,220355B,220555B,
     1   030323B,240655B,242255B,031205B,031203B,112355B,200555B,
     2   200355B,260155B,050755B,9*0,
     3         0,020615B,022215B,220255B,220355B,220555B,030323B,
     4   240655B,242255B,031205B,031203B,112355B,260155B,260255B,
     5   033555B,111355B,160555B,033755B,111322B,160355B,200555B,
     6   150555B,200355B,150355B,050755B,
     7   222355B,242455B,031217B,112355B,165555B,201011B,050755B,18*0,
     8         0,262417B,020524B,140115B,220455B,222355B,030723B,
     9   030704B,200255B,112355B,15*0,
     A         0,262417B,201011B,020524B,070115B,140115B,220455B,
     B   222355B,030723B,030704B,030702B,030204B,030223B,200255B,
     C   112355B,10*0/
C
C  INITIALIZE VARIABLES
C
      CALL SECOND(T1)
      VT=BOLTZ*(TEMPS(1)+273.0)/CHARGE
      ICARD1=1
      ICVFLG=0
      INOISE=0
      IONUM=0
      JACFLG=0
      JDSFLG=0
      JTRFLG=0
      KEXMOD=0
      KFROUT=0
      KINEL=0
      KSSOP=0
      LOCSOR=0
      NDES=0
      NOPRNT=0
      NOSOUT=0
      NOSPRT=0
      NOSTOP=0
      NOTINT=0
      NSENS=0
      NSPEC=0
      NUMEL=0
      NUMMOD=0
      NUMNOD=0
      NUMTEM=1
      NUNODS=0
      DO 1 I=1,3
    1 NUMOR(I)=0
      DO 10 I=1,20
   10 JELCNT(I)=0
C
C GET A CARD
C
   25 IF (KEOF.EQ.1) GO TO 5000
      IGOOF=0
      CALL CARD
      IF (IGOOF.EQ.0) GO TO 50
      NOGO=1
      GO TO 25
C
C  ELEMENT AND DEVICE CARDS
C
   50 IF (ID.GT.20) GO TO 300
      NUMEL=NUMEL+1
      IF (NUMEL.GT.400) GO TO 260
      IKNT=1
      IF (ID.NE.4) GO TO 60
      IF (ICODE(2).EQ.0) GO TO 60
      ID=5
      IKNT=2
   60 IF (KEXMOD.EQ.0) GO TO 70
      IF (ID.EQ.20) GO TO 280
      IDNO(NUMEL)=ID+20
      GO TO 80
   70 IDNO(NUMEL)=ID
      JELCNT(ID)=JELCNT(ID)+1
   80 NAME1(NUMEL)=FIELD(1)
      LOCAL1(NUMEL)=NOSTOP+1
      MNAME1(NUMEL)=IBLNK
      VALUE1(NUMEL)=0.0
      IF (ID.EQ.20) GO TO 230
      ISTOP=NNODS(ID)
      IF ((IKNT+ISTOP).GT.NUMFLD) GO TO 4000
      IF ((NOSTOP+ISTOP).GT.800) GO TO 260
      DO 90 I=1,ISTOP
   90 NODPL1(NOSTOP+I)=FIELD(IKNT+I)
      NOSTOP=NOSTOP+ISTOP
      GO TO (100,100,100,110,105,110,200,200,200,200),ID
C
C  RESISTORS, CAPACITORS, AND INDUCTORS
C
  100 IF (NUMFLD.LT.4) GO TO 250
      VALUE1(NUMEL)=FIELD(4)
      IF (VALUE1(NUMEL).LE.0.0) GO TO 250
      GO TO 25
C
C  CONTROLLED SOURCES
C
  105 MNAM=LOCSOR+1
      MNAME1(NUMEL)=MNAM
      LOCSOR=LOCSOR+1
      IF (LOCSOR.GT.150) GO TO 260
      SOURCE(MNAM)=0.0
      IF (NUMFLD.LT.7) GO TO 25
      VALUE1(NUMEL)=FIELD(7)
      IF (NUMFLD.LT.8) GO TO 25
      SOURCE(MNAM)=FIELD(8)
      GO TO 25
C
C  INDEPENDENT SOURCES
C
  110 MNAM=LOCSOR+1
      MNAME1(NUMEL)=MNAM
      LOCSOR=LOCSOR+5
      IF (LOCSOR.GT.150) GO TO 260
      DO 120 I=MNAM,LOCSOR
  120 SOURCE(I)=0.0
      IKNT=3
  130 IKNT=IKNT+1
      IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).EQ.1) GO TO 140
      IF (IKNT.GT.4) GO TO 270
      SOURCE(MNAM)=FIELD(IKNT)
      GO TO 25
  140 ITEMP=IFIX(FIELD(IKNT))/JSHIFT
      JKNT=0
      IF (ITEMP.EQ.LSDC) JKNT=1
      IF (ITEMP.EQ.LSAC) JKNT=2
      IF (ITEMP.EQ.LSPU) JKNT=3
      IF (ITEMP.EQ.LSEX) JKNT=4
      IF (ITEMP.EQ.LSSI) JKNT=5
      IF (JKNT.EQ.0) GO TO 270
      IKNT=IKNT+1
      IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).EQ.1) GO TO 140
      GO TO (150,160,170,170,170),JKNT
  150 SOURCE(MNAM)=FIELD(IKNT)
      GO TO 130
  160 SOURCE(MNAM+1)=FIELD(IKNT)
      IKNT=IKNT+1
      IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).EQ.1) GO TO 140
      SOURCE(MNAM+4)=FIELD(IKNT)
      GO TO 130
  170 IF (SOURCE(MNAM+3).NE.0.0) GO TO 270
      SOURCE(MNAM+3)=JKNT-2
      JKNT=LOCSOR+1
      LOCSOR=LOCSOR+7
      IF (LOCSOR.GT.150) GO TO 260
      DO 180 I=JKNT,LOCSOR
  180 SOURCE(I)=0.0
  190 SOURCE(JKNT)=FIELD(IKNT)
      IKNT=IKNT+1
      IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).EQ.1) GO TO 140
      JKNT=JKNT+1
      IF (JKNT.GT.LOCSOR) GO TO 270
      GO TO 190
C
C  DEVICE CARDS
C
  200 VALUE1(NUMEL)=1.0
      IKNT=NNODS(ID)+2
      IF (IKNT.GT.NUMFLD) GO TO 25
      MNAME1(NUMEL)=FIELD(IKNT)
      IF ((IKNT+1).GT.NUMFLD) GO TO 25
      VALUE1(NUMEL)=FIELD(IKNT+1)
      IF (VALUE1(NUMEL).LE.0.0) GO TO 250
      GO TO 25
C
C  EXTERNAL MODEL ELEMENT
C
  230 IF (NUMFLD.LT.3) GO TO 4000
      MNAME1(NUMEL)=FIELD(NUMFLD)
      ISTOP=NUMFLD-2
      IF ((NOSTOP+ISTOP).GT.800) GO TO 260
      DO 240 I=1,ISTOP
  240 NODPL1(NOSTOP+I)=FIELD(I+1)
      NOSTOP=NOSTOP+ISTOP
      VALUE1(NUMEL)=ISTOP
      GO TO 25
C
C  ELEMENT CARD ERRORS
C
  250 NOGO=1
      WRITE (6,251)
  251 FORMAT (//5X,*----------  VALUE IS NOT GREATER THAN ZERO*//)
      GO TO 25
  260 NOGO=1
      WRITE (6,261)
  261 FORMAT (//5X,*----------  ELEMENT LIMIT EXCEEDED*//)
      GO TO 25
  270 NOGO=1
      WRITE (6,271)
  271 FORMAT (//5X,*----------  SOURCE VALUES SPECIFIED INCORRECTLY*//)
      GO TO 25
  280 NOGO=1
      WRITE (6,281)
  281 FORMAT (//5X,*----------  EXTERNAL MODELS CANNOT BE NESTED*//)
      GO TO 25
C
C  CONTROL CARDS
C
  300 ID=ID-20
      GO TO (5000,400,800,1200,1400,1600,1800,2800,3000,
     1   3200,3400,3600),ID
C
C  MODEL CARD
C
  400 IF (NUMFLD.LT.2) GO TO 4100
      IF (ICODE(1).EQ.0) GO TO 600
      NAM=FIELD(1)
      IKNT=0
  410 IKNT=IKNT+1
      IF (IKNT.GT.NUMMOD) GO TO 420
      IF (NAM.NE.MODNAM(IKNT)) GO TO 410
      GO TO 600
  420 IF (NUMMOD.GE.20) GO TO 610
      NUMMOD=NUMMOD+1
      MODNAM(NUMMOD)=NAM
      IF (ICODE(2).EQ.0) GO TO 620
      NAM=FIELD(2)/KSHIFT
      IKNT=0
  430 IKNT=IKNT+1
      IF (IKNT.GT.11) GO TO 620
      IF (NAM.NE.MODTYP(IKNT)) GO TO 430
      IF (IKNT.EQ.11) GO TO 700
      SYMVAL(NUMMOD,1)=TCODE(IKNT)
      ITYPE=MTYPE(IKNT)
      KIND(NUMMOD)=MOTYPE(IKNT)
      DO 440 I=2,25
  440 SYMVAL(NUMMOD,I)=0.0
      ISPOT=0
      IF (SYMVAL(NUMMOD,1).NE.0.0) ISPOT=1
      IPOINT=2
  450 IPOINT=IPOINT+1
      IF (IPOINT.GT.NUMFLD) GO TO 25
      IF (IPOINT.GT.26) GO TO 630
      IF (ICODE(IPOINT).EQ.1) GO TO 460
      ISPOT=ISPOT+1
      SYMVAL(NUMMOD,ISPOT)=FIELD(IPOINT)
      GO TO 450
  460 NAM=FIELD(IPOINT)/KSHIFT
      ISPOT=0
  470 ISPOT=ISPOT+1
      IF (ISPOT.GT.25) GO TO 630
      IF (NAM.NE.NAMPAR(ISPOT,ITYPE)) GO TO 470
      IPOINT=IPOINT+1
      IF (IPOINT.GT.NUMFLD) GO TO 25
      SYMVAL(NUMMOD,ISPOT)=FIELD(IPOINT)
      IPOINT=IPOINT+1
      IF (IPOINT.GT.NUMFLD) GO TO 25
      IF (ICODE(IPOINT).EQ.1) GO TO 460
      GO TO 630
  600 NOGO=1
      WRITE (6,601)
  601 FORMAT (//5X,*----------  ILLEGAL OR PREVIOUSLY DEFINED MODEL*//)
      GO TO 25
  610 NOGO=1
      WRITE (6,611)
  611 FORMAT (//5X,*----------  MODEL LIMIT EXCEEDED*//)
      GO TO 25
  620 NOGO=1
      WRITE (6,621)
  621 FORMAT (//5X,*----------  INCORRECT MODEL TYPE*//)
      GO TO 25
  630 NOGO=1
      WRITE (6,631)
  631 FORMAT (//5X,*----------  UNDEFINED PARAMETER NAME OR INCORRECT *
     1   *PARAMETER LIST*//)
      GO TO 25
C
C  EXTERNAL MODELS
C
  700 IF (KEXMOD.NE.0) GO TO 720
      KEXMOD=NUMMOD
      KIND(KEXMOD)=20
      SYMVAL(KEXMOD,1)=NUMEL+1
      SYMVAL(KEXMOD,3)=NUMFLD-2
      IF (NUMFLD.LT.3) GO TO 730
      IF (NUMFLD.GT.22) GO TO 730
      DO 710 I=3,NUMFLD
      IF (FIELD(I).EQ.0.0) GO TO 730
  710 SYMVAL(KEXMOD,I+3)=FIELD(I)
      GO TO 25
  720 NOGO=1
      WRITE (6,721)
  721 FORMAT (//5X,*----------  .FINIS CARD MISSING*//)
      GO TO 25
  730 NOGO=1
      WRITE (6,731)
  731 FORMAT (//5X,*----------  EXTERNAL MODEL NODE REFERANCES ARE *
     1   *INCORRECT*//)
      GO TO 25
C
C  OUTPUT CARD
C
  800 IF (IONUM.GT.9) GO TO 1020
      IF (NUMFLD.LT.1) GO TO 4100
      NAM=FIELD(1)
      IKNT=0
  810 IKNT=IKNT+1
      IF (IKNT.GT.IONUM) GO TO 820
      IF (NAM.NE.IONAM(IKNT)) GO TO 810
      GO TO 1030
  820 ITEMP=NAM/ISHIFT
      IPOINT=0
      IF (ITEMP.EQ.ILETN) IPOINT=1
      IF (ITEMP.EQ.ILETI) IPOINT=2
      IF (ITEMP.EQ.ILETV) IPOINT=3
      IF (IPOINT.EQ.0) GO TO 1030
      IF (IPOINT.GT.NUMFLD) GO TO 4100
      IONUM=IONUM+1
      IONAM(IONUM)=NAM
      IOFLG(IONUM)=3
      IF (IPOINT.EQ.1) GO TO 830
      IOPND(IONUM)=FIELD(2)
      IOFLG(IONUM)=2
      IF (IPOINT.EQ.2) GO TO 830
      IONND(IONUM)=FIELD(3)
      IOFLG(IONUM)=1
  830 IGOOF=0
      IFLAG=0
  840 IPOINT=IPOINT+1
      IF (IPOINT.GT.NUMFLD) GO TO 1010
      IF (ICODE(IPOINT).EQ.0) GO TO 1000
  850 ITEMP=FIELD(IPOINT)/JSHIFT
      IKNT=0
      IF (ITEMP.EQ.LSPR) IKNT=1
      IF (ITEMP.EQ.LSPL) IKNT=2
      IF (ITEMP.EQ.LSDC) IKNT=3
      IF (ITEMP.EQ.LSTR) IKNT=4
      IF (ITEMP.EQ.LSRE) IKNT=5
      IF (ITEMP.EQ.LSIM) IKNT=6
      IF (ITEMP.EQ.LSMA) IKNT=7
      IF (ITEMP.EQ.LSPH) IKNT=8
      IF (ITEMP.EQ.LSOU) IKNT=9
      IF (ITEMP.EQ.LSIN) IKNT=10
      IF (IKNT.EQ.0) GO TO 1000
      IF (IKNT.GT.2) GO TO 860
      IFLAG=IKNT
      GO TO 840
  860 IF (IFLAG.EQ.0) GO TO 1000
      IKNT=IKNT-2
      IF (IKNT.GT.2) GO TO 900
      KNTR=NUMOR(IKNT)
      IF (KNTR.EQ.0) GO TO 870
      IF (IOVAR(KNTR,IKNT).EQ.IONUM) GO TO 880
  870 NUMOR(IKNT)=NUMOR(IKNT)+1
      KNTR=NUMOR(IKNT)
      IOVAR(KNTR,IKNT)=IONUM
      IPRCOM(KNTR,IKNT)=0
      IPLCOM(KNTR,IKNT)=0
  880 IF (IFLAG.EQ.2) GO TO 890
      IPRCOM(KNTR,IKNT)=1
      GO TO 840
  890 IPLCOM(KNTR,IKNT)=1
      IPOINT=IPOINT+1
      IF (IPOINT.GT.NUMFLD) GO TO 1010
      IF (ICODE(IPOINT).EQ.1) GO TO 850
      PLTLIM(KNTR,IKNT,1)=FIELD(IPOINT)
      IPOINT=IPOINT+1
      IF (IPOINT.GT.NUMFLD) GO TO 1010
      IF (ICODE(IPOINT).EQ.1) GO TO 850
      PLTLIM(KNTR,IKNT,2)=FIELD(IPOINT)
      IPLCOM(KNTR,IKNT)=2
      GO TO 840
  900 KNTR=NUMOR(3)
      IF (KNTR.EQ.0) GO TO 910
      IF (IACVAR(KNTR).EQ.IONUM) GO TO 930
  910 KNTR=KNTR+1
      IF (KNTR.GT.5) GO TO 1020
      NUMOR(3)=KNTR
      IACVAR(KNTR)=IONUM
      DO 920 I=1,4
      IACPRT(KNTR,I)=0
  920 IACPLT(KNTR,I)=0
  930 IKNT=IKNT-2
      IF (IKNT.LT.5) GO TO 940
      IF (IOFLG(IONUM).NE.3) GO TO 1000
      IKNT=IKNT-4
      INOISE=KNTR
  940 IF (IFLAG.EQ.2) GO TO 950
      IACPRT(KNTR,IKNT)=1
      GO TO 840
  950 IACPLT(KNTR,IKNT)=1
      IPOINT=IPOINT+1
      IF (IPOINT.GT.NUMFLD) GO TO 1010
      IF (ICODE(IPOINT).EQ.1) GO TO 850
      ACPLIM(KNTR,IKNT,1)=FIELD(IPOINT)
      IPOINT=IPOINT+1
      IF (IPOINT.GT.NUMFLD) GO TO 1010
      IF (ICODE(IPOINT).EQ.1) GO TO 850
      ACPLIM(KNTR,IKNT,2)=FIELD(IPOINT)
      IACPLT(KNTR,IKNT)=2
      GO TO 840
 1000 IGOOF=1
      GO TO 840
 1010 IF (IGOOF.EQ.0) GO TO 25
      WRITE (6,1011)
 1011 FORMAT (//5X WARNING ---  UNDEFINED OUTPUT SPECIFICATION*//)
      GO TO 25
 1020 WRITE (6,1021)
 1021 FORMAT (//5X,*----------  OUTPUT LIMIT EXCEEDED*//)
      GO TO 25
 1030 WRITE (6,1031)
 1031 FORMAT (//5X,*----------  OUTPUT NAME IS ILLEGAL OR HAS BEEN *
     1  *DEFINED PREVIOUSLY*//)
      GO TO 25
C
C  DC ANALYSIS CARD
C
 1200 IF (NUMFLD.GT.0) GO TO 1210
      KSSOP=1
      GO TO 25
 1210 IKNT=1
 1220 IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).EQ.0) GO TO 1240
      ITEMP=IFIX(FIELD(IKNT))/JSHIFT
      IF (ITEMP.EQ.LSTC) GO TO 1230
      IF (ITEMP.EQ.LSOP) GO TO 1225
      IF (IKNT.GT.1) GO TO 1240
      IKNT=0
      GO TO 1230
 1225 KSSOP=1
      IKNT=IKNT+1
      IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).NE.1) GO TO 1240
      ITEMP=IFIX(FIELD(IKNT))/JSHIFT
      IF (ITEMP.EQ.LSTC) GO TO 1230
      IF ((IKNT+1).GT.NUMFLD) GO TO 4100
      KOVAR=FIELD(IKNT)
      KINEL=FIELD(IKNT+1)
      IKNT=IKNT+2
      GO TO 1220
 1230 IF ((IKNT+4).GT.NUMFLD) GO TO 4100
      ICVFLG=1
      ITCELM=FIELD(IKNT+1)
      TCSTAR=FIELD(IKNT+2)
      TCSTOP=FIELD(IKNT+3)
      TCINCR=FIELD(IKNT+4)
      IKNT=IKNT+5
      GO TO 1220
 1240 WRITE (6,1241)
 1241 FORMAT (//5X,*----------  DC ANALYSIS PARAMETERS ARE *
     1  *SPECIFIED INCORRECTLY*//)
      GO TO 25
C
C  AC ANALYSIS CARD
C
 1400 IKNT=1
 1410 IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).EQ.0) GO TO 1440
 1420 ITEMP=IFIX(FIELD(IKNT))/JSHIFT
      JKNT=0
      IF (ITEMP.EQ.LSLI) JKNT=1
      IF (ITEMP.EQ.LSDE) JKNT=2
      IF (ITEMP.EQ.LSOC) JKNT=3
      IF (ITEMP.EQ.LSNO) JKNT=4
      IF (JKNT.EQ.0) GO TO 1440
      IF (JKNT.GT.3) GO TO 1430
      IF ((IKNT+3).GT.NUMFLD) GO TO 4100
      JACFLG=1
      IDFREQ=JKNT
      FINCR=FIELD(IKNT+1)
      FSTART=FIELD(IKNT+2)
      FSTOP=FIELD(IKNT+3)
      IKNT=IKNT+4
      GO TO 1410
 1430 IF ((IKNT+2).GT.NUMFLD) GO TO 4100
      NOSOUT=FIELD(IKNT+1)
      NOSIN=FIELD(IKNT+2)
      IKNT=IKNT+3
      IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).EQ.1) GO TO 1420
      NOSPRT=FIELD(IKNT)
      IF (NOSPRT.LT.10) NOSPRT=10
      IKNT=IKNT+1
      GO TO 1410
 1440 WRITE (6,1441)
 1441 FORMAT (//5X,*----------  AC ANALYSIS PARAMETERS ARE DEFINED *
     1  *INCORRECTLY*//)
      GO TO 25
C
C  TRAN ANALYSIS CARD
C
 1600 IF (NUMFLD.LT.2) GO TO 4100
      TSTEP=FIELD(1)
      TSTOP=FIELD(2)
      TSTART=0.0
      NOTINT=0
      IKNT=3
      IF (NUMFLD.EQ.2) GO TO 1620
      IF (ICODE(3).EQ.1) GO TO 1620
      TSTART=FIELD(3)
      IKNT=4
 1610 IF ((IKNT+1).GT.NUMFLD) GO TO 1620
      IF (ICODE(IKNT).EQ.1) GO TO 1620
      IF (ICODE(IKNT+1).EQ.1) GO TO 1670
      NOTINT=NOTINT+1
      STEPS(NOTINT)=FIELD(IKNT)
      ENDPTS(NOTINT)=FIELD(IKNT+1)
      IKNT=IKNT+2
      GO TO 1610
 1620 IF (NOTINT.GT.0) GO TO 1630
      NOTINT=1
      STEPS(1)=TSTEP
 1630 ENDPTS(NOTINT)=TSTOP
      TOTPTS=0.0
      JKNT=0
 1640 JKNT=JKNT+1
      IF (JKNT.GT.NOTINT) GO TO 1650
      IF (JKNT.GT.1) GO TO 1645
      TEMP=ENDPTS(1)
      GO TO 1648
 1645 TEMP=ENDPTS(JKNT)-ENDPTS(JKNT-1)
 1648 IF (TEMP.LE.0.0) GO TO 1670
      IF (STEPS(JKNT).LE.0.0) GO TO 1670
      TOTPTS=TOTPTS+TEMP/STEPS(JKNT)
      GO TO 1640
 1650 IF (TOTPTS.GT.1001.0) GO TO 1680
      TEMP=(TSTOP-TSTART)/100.0
      IF (TEMP.LE.0.0) GO TO 1690
      IF (1.01*TSTEP.GE.TEMP) GO TO 1660
      TSTEP=TEMP
      WRITE (6,1651) TSTEP
 1651 FORMAT (//5X,*--- WARNING ---  PRINT INCREMENT INCREASED TO *,
     1   1PE10.3//)
 1660 JTRFLG=IFIX((TSTOP-TSTART)/TSTEP+0.5)+1
      IF (JTRFLG.LT.2) JTRFLG=2
      GO TO 1700
 1670 WRITE (6,1671)
 1671 FORMAT (//5X,*----------  TRANSIENT INTERVALS ARE SPECIFIED *
     1   *INCORRECTLY OR NUMBER OF INTERVALS EXCEEDS 5*//)
      GO TO 25
 1680 WRITE (6,1681)
 1681 FORMAT (//5X,*----------  TOTAL NUMBER OF TIMEPOINTS EXCEEDS *
     1   *1001*//)
      GO TO 25
 1690 WRITE (6,1691)
 1691 FORMAT (//5X,*----------  FINAL TIME IS LESS THAN START TIME, *
     1   *OR FINAL TIME IS LESS THAN ZERO*//)
      GO TO 25
 1700 IF (IKNT.GT.NUMFLD) GO TO 25
      ITEMP=IFIX(FIELD(IKNT))/JSHIFT
      IF (ITEMP.NE.LSFO) GO TO 25
      IF ((IKNT+2).GT.NUMFLD) GO TO 25
      KFROUT=FIELD(IKNT+1)
      FORFRE=FIELD(IKNT+2)
      GO TO 25
C
C  TEMPERATURE CARD
C
 1800 IF (NUMFLD.LT.1) GO TO 4100
      IF (NUMFLD.LT.6) GO TO 1810
      NUMFLD=5
      WRITE (6,1801)
 1801 FORMAT (//5X,*----------  ONLY FIRST FIVE TEMPERATURES USED*//)
 1810 KNTR=1
      DO 1820 I=1,NUMFLD
      IF (FIELD(I).LT.-223.0) GO TO 1820
      KNTR=KNTR+1
      TEMPS(KNTR)=FIELD(I)
 1820 CONTINUE
      NUMTEM=KNTR
      GO TO 25
C
C  SENSITIVITY CARD
C
 2800 IF (NUMFLD.LT.1) GO TO 4100
      IKNT=0
 2810 IKNT=IKNT+1
      IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).EQ.0) GO TO 2820
      IF (NSENS.GE.10) GO TO 2820
      NSENS=NSENS+1
      KSNOUT(NSENS)=FIELD(IKNT)
      GO TO 2810
 2820 NSENS=0
      WRITE (6,2821)
 2821 FORMAT (//5X,*----------  SENSITIVITY VARIABLES ARE INCORRECTLY *
     1   *SPECIFIED OR SENSITIVITY OUTPUT LIMIT EXCEEDED*//)
      GO TO 25
C
C  DESIGN CARD
C
 3000 IF (NUMFLD.LT.1) GO TO 4100
      JDSFLG=1
      IKNT=0
 3010 IKNT=IKNT+1
      IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).EQ.0) GO TO 3030
      IF (NDES.GE.30) GO TO 3030
      NDES=NDES+1
      NOELEM(NDES)=FIELD(IKNT)
      GO TO 3010
 3030 JDSFLG=0
      NDES=0
      WRITE (6,3031)
 3031 FORMAT (//5X,*----------  DESIGN PARAMETERS ARE INCORRECTLY *
     1   *SPECIFIED OR NUMBER OF PARAMETERS EXCEEDS 30*//)
      GO TO 25
C
C  SPEC CARD
C
 3200 IF (NUMFLD.LT.2) GO TO 4100
      IKNT=0
 3210 IKNT=IKNT+1
      IF (IKNT.GT.NUMFLD) GO TO 25
      IF (ICODE(IKNT).EQ.0) GO TO 3220
      IF (NSPEC.GE.10) GO TO 3220
      NSPEC=NSPEC+1
      NOUNUM(NSPEC)=FIELD(IKNT)
      IKNT=IKNT+1
      IF (IKNT.GT.NUMFLD) GO TO 3220
      IF (ICODE(IKNT).EQ.1) GO TO 3220
      DESVAL(NSPEC)=FIELD(IKNT)
      WEIGHT(NSPEC)=1.0
      IF (IKNT.EQ.NUMFLD) GO TO 25
      IF (ICODE(IKNT+1).EQ.1) GO TO 3210
      IKNT=IKNT+1
      WEIGHT(NSPEC)=FIELD(IKNT)
      GO TO 3210
 3220 NSPEC=0
      WRITE (6,3221)
 3221 FORMAT (//5X,*----------  DESIGN OUTPUTS ARE INCORRECTLY *
     1   *SPECIFIED OR OUTPUT LIMIT EXCEEDED*//)
      GO TO 25
C
C  PRINT INHIBIT CARD
C
 3400 NOPRNT=1
      GO TO 25
C
C  FINIS CARD
C
 3600 IF (KEXMOD.EQ.0) GO TO 25
      SYMVAL(KEXMOD,2)=NUMEL
      IF (SYMVAL(KEXMOD,1).GT.SYMVAL(KEXMOD,2)) GO TO 3610
      KEXMOD=0
      GO TO 25
 3610 KEXMOD=0
      NOGO=1
      WRITE (6,3611)
 3611 FORMAT (//5X,*----------  EXTERNAL MODEL CONTAINS NO ELEMENTS*//)
      GO TO 25
C
C  CARD ERROR
C
 4000 NOGO=1
 4100 WRITE (6,4101)
 4101 FORMAT (//5X,*----------  INSUFFICIENT NUMBER OF PARAMETERS ON *
     1  *ABOVE CARD*//)
      GO TO 25
C
C  END
C
 5000 IF (KEXMOD.EQ.0) GO TO 5020
      NOGO=1
      WRITE (6,5011)
 5011 FORMAT (//5X,*----------  NO .FINIS CARD*//)
 5020 CALL SECOND(T2)
      RTIMES(1)=T2-T1
      RETURN
      END
      SUBROUTINE CARD
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/CARDS/ID,NUMFLD,ICARD1,KEOF
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
C
C
      DIMENSION ID1(20),ID2(12),FIELD(1),ICODE(1),IFIELD(1)
      EQUIVALENCE (FIELD(1),YNL(1)),(ICODE(1),YNL(101)),
     1   (IFIELD(1),YNL(201))
C
C  ID1    R , C , L , I , * , V , Q , D , J , M , 9* , X
C
      DATA ID1 /22B,3B,14B,11B,0,26B,21B,4B,12B,15B,9*0,30B/
C
C  ID2  END , MODEL , OUTPUT , DC , AC , TRAN , TEMP , SENSE ,
C       DESIGN , SPEC , NP , FINIS
C
      DATA ID2 /516B,1517B,1725B,403B,103B,2422B,2405B,2305B,405B,
     1   2320B,1620B,611B/
C
C
      DATA IBLNK /5555555555555555B/
      DATA ISPACE,IASTK,IPER,ICOMMA,IZERO,IPLUS,IMINUS,ILETE,ILETM,
     1   ILETG,ILETK,ILETU,ILETN,ILETP/55B,47B,57B,56B,33B,45B,46B,
     2   5B,15B,7B,13B,25B,16B,20B/
      DATA ISHIFT /100B/
      DATA JSHIFT /100000000000000B/
      DATA IEQUAL /54B/
C
C
      NUMFLD=0
      ID=0
      IF (ICARD1.EQ.1) GO TO 400
C
C  FIRST PASS
C
   20 WRITE (6,21) (IFIELD(I),I=1,ISTOP)
   21 FORMAT (5X,10(R8))
      NOFLD=0
      JUMP=1
      GO TO 301
   25 IF (ICHAR.EQ.ISPACE) GO TO 300
      IF (ICHAR.NE.IASTK) GO TO 28
      ICARD1=1
      GO TO 400
C
C  ELEMENT CARD  GET ID
C
   28 IF (ICHAR.EQ.IPER) GO TO 31
      DO 30 I=1,20
      IF (ICHAR.NE.ID1(I)) GO TO 30
      ID=I
      GO TO 95
   30 CONTINUE
      GO TO 50
C
C  CONTROL CARD  PACK LEAD PERIOD AND GET ID
C
   31 JUMP=16
      GO TO 300
   32 JCHAR=ICHAR
      JUMP=2
      GO TO 300
   35 ICHAR=JCHAR*ISHIFT+ICHAR
      DO 38 I=1,12
      IF (ICHAR.NE.ID2(I)) GO TO 38
      ID=I+20
      GO TO 39
   38 CONTINUE
      GO TO 50
   39 JUMP=3
      GO TO 300
   40 IF (ICHAR.EQ.ISPACE) GO TO 55
      IF (ICHAR.EQ.ICOMMA) GO TO 61
      IF (ICHAR.EQ.IEQUAL) GO TO 61
      GO TO 300
C
C  ERROR   UNKNOWN CARD
C
   50 IGOOF=1
      WRITE (6,51)
   51 FORMAT (//5X,*----------  ABOVE CARD IS UNRECOGNIZABLE*//)
      GO TO 400
C
C  THIS LOOP USED FOR ALL SUCEEDING PASSES
C  IF LAST FIELD WAS ENDED BY A SPACE IGNORE SPACES AND FIRST COMMA
C
   55 JUMP=4
      GO TO 300
   60 IF (NOFLD.GT.ISTOP) GO TO 400
      IF (ICHAR.EQ.ISPACE) GO TO 300
      IF (ICHAR.EQ.ICOMMA) GO TO 61
      IF (ICHAR.EQ.IEQUAL) GO TO 61
      GO TO 65
C
C  IF LAST FIELD WAS ENDED BY A COMMA IGNORE ONLY SPACES
C
   61 IF (NUMFLD.GT.59) GO TO 400
      JUMP=5
      GO TO 300
   62 IF (NOFLD.GT.ISTOP) GO TO 400
      IF (ICHAR.EQ.ISPACE) GO TO 300
      IF (ICHAR.EQ.ICOMMA) GO TO 63
      IF (ICHAR.EQ.IEQUAL) GO TO 63
      GO TO 65
C
C  TWO COMMAS SEPARATED ONLY BY SPACE (S) IMPLIES A VACANT (ZERO) FIELD
C
   63 NUMFLD=NUMFLD+1
      ICODE(NUMFLD)=0
      FIELD(NUMFLD)=0.0
      GO TO 61
C
C  NAME FIELD
C  ICODE = 1  SIGNIFIES A NAME
C
   65 IF (ICHAR.GE.IZERO) GO TO 125
   95 IF (NUMFLD.GT.59) GO TO 400
      NUMFLD=NUMFLD+1
      NAM=0
      ICHK=0
      ICODE(NUMFLD)=1
      ICOUNT=1
  100 NAM=NAM*ISHIFT+ICHAR
      ICOUNT=ICOUNT+1
      IF (ICOUNT.GT.7) GO TO 110
      JUMP=6
      GO TO 300
  105 IF (ICHAR.EQ.ISPACE) GO TO 118
      IF (ICHAR.EQ.ICOMMA) GO TO 117
      IF (ICHAR.EQ.IEQUAL) GO TO 117
      GO TO 100
C
C  FINISH THE FIELD
C
  110 FIELD(NUMFLD)=NAM
      JUMP=7
      GO TO 300
  115 IF (ICHAR.EQ.ISPACE) GO TO 55
      IF (ICHAR.EQ.ICOMMA) GO TO 61
      IF (ICHAR.EQ.IEQUAL) GO TO 61
      GO TO 300
C
C  PACK FIELD WITH BLANKS
C
  117 ICHK=1
  118 DO 120 I=ICOUNT,7
  120 NAM=NAM*ISHIFT+ISPACE
      FIELD(NUMFLD)=NAM
      IF (ICHK) 55,55,61
C
C  NUMBER FIELD
C  ICODE = 0   SIGNIFIES A NUMBER FIELD
C
  125 IF (NUMFLD.GT.59) GO TO 400
      ICHK=0
      NUMFLD=NUMFLD+1
      NUM=0
      IEXP=0
      ISET=0
      ICODE(NUMFLD)=0
C
C  CHECK FOR A SIGN
C
      ISIN=0
      IF (ICHAR.EQ.IPLUS) ISIN=1
      IF (ICHAR.EQ.IMINUS) ISIN=-1
      IF (ISIN.NE.0) GO TO 130
      ISIN=1
      JUMP=9
      GO TO 138
  130 JUMP=8
      GO TO 300
  135 IF (ICHAR.EQ.ISPACE) GO TO 300
      JUMP=9
  138 IF (ICHAR.EQ.ICOMMA) GO TO 205
      IF (ICHAR.EQ.IEQUAL) GO TO 205
      IDIGIT=ICHAR-IZERO
      IF (IDIGIT.LT.0) GO TO 140
      IF (IDIGIT.GT.9) GO TO 140
      NUM=NUM*10+IDIGIT
      IF (ISET.EQ.0) GO TO 300
      IEXP=IEXP-1
      GO TO 300
C
C  CHARACTER NOT A DIGIT
C  CHECK FOR SPACE, DEC POINT, E NOTATION, OR SCALE FACTOR
C
  140 IF (ICHAR.EQ.ISPACE) GO TO 206
      IF (ICHAR.EQ.IPER) GO TO 155
      IF (ICHAR.EQ.ILETE) GO TO 160
      GO TO 185
C
C  DECIMAL POINT
C
  155 IF (ISET.EQ.1) GO TO 210
      ISET=1
      GO TO 300
C
C  E FORMAT
C
  160 ITEMP=0
      JUMP=11
      GO TO 300
  165 IF (ICHAR.EQ.ISPACE) GO TO 300
      IEXSIN=0
      IF (ICHAR.EQ.IPLUS) IEXSIN=1
      IF (ICHAR.EQ.IMINUS) IEXSIN=-1
      IF (IEXSIN.NE.0) GO TO 170
      IEXSIN=1
      JUMP=13
      GO TO 178
  170 JUMP=12
      GO TO 300
  175 IF (ICHAR.EQ.ISPACE) GO TO 300
      JUMP=13
  178 IF (ICHAR.EQ.ICOMMA) GO TO 179
      IF (ICHAR.EQ.IEQUAL) GO TO 179
      IF (ICHAR.EQ.ISPACE) GO TO 180
      IDIGIT=ICHAR-IZERO
      IF (IDIGIT.LT.0) GO TO 182
      IF (IDIGIT.GT.9) GO TO 182
      ITEMP=ITEMP*10+IDIGIT
      GO TO 300
  179 ICHK=1
  180 IEXP=IEXP+IEXSIN*ITEMP
      GO TO 206
  182 IEXP=IEXP+IEXSIN*ITEMP
      JUMP=15
      GO TO 202
C
C  SCALE FACTOR
C
  185 IF (ICHAR.NE.ILETM) GO TO 200
      JUMP=14
      GO TO 300
  190 IF (ICHAR.EQ.ILETE) GO TO 195
      ITEMP=-3
      GO TO 201
  195 ITEMP=6
      GO TO 201
  200 ITEMP=0
      IF (ICHAR.EQ.ILETG) ITEMP=9
      IF (ICHAR.EQ.ILETK) ITEMP=3
      IF (ICHAR.EQ.ILETU) ITEMP=-6
      IF (ICHAR.EQ.ILETN) ITEMP=-9
      IF (ICHAR.EQ.ILETP) ITEMP=-12
  201 IEXP=IEXP+ITEMP
      JUMP=15
  202 IF (ICHAR.EQ.ICOMMA) GO TO 205
      IF (ICHAR.EQ.IEQUAL) GO TO 205
      IF (ICHAR.EQ.ISPACE) GO TO 206
      GO TO 300
C
C  PUT THE NUMBER TOGETHER
C
  205 ICHK=1
  206 IF (NUM) 207,209,207
  207 IF (IEXP.GT.40) GO TO 210
      IF (IEXP.LT.-40) GO TO 209
      IF (IEXP.EQ.0) GO TO 208
      VAL=NUM*EXP(IEXP*XLOG10)
      IF (VAL.GT.1.0E20) GO TO 210
      IF (VAL.LT.1.0E-40) GO TO 209
      FIELD(NUMFLD)=ISIN*VAL
      IF (ICHK) 55,55,61
  208 FIELD(NUMFLD)=ISIN*NUM
      IF (ICHK) 55,55,61
  209 FIELD(NUMFLD)=0.0
      IF (ICHK) 55,55,61
C
C  ERROR
C
  210 IGOOF=1
      WRITE (6,211) NUMFLD
  211 FORMAT (//5X,*----------  ABOVE CARD HAS ERROR IN FIELD  *,I5//)
      GO TO 400
C
C  GET NEXT CHARACTER
C  THIS LOOP IS A LOCAL SUBROUTINE
C  RETURN POINT DETERMINED BY JUMP
C
  300 IFDPOS=IFDPOS+1
      IF (IFDPOS.LT.9) GO TO 305
      IF (IFDPOS.GT.11) GO TO 210
  301 NOFLD=NOFLD+1
      IF (NOFLD.GT.ISTOP) GO TO 310
      IFLD=IFIELD(NOFLD)
      IFDPOS=1
  305 ICHAR=IFLD/JSHIFT
      IFLD=(IFLD-ICHAR*JSHIFT)*ISHIFT
      GO TO 315
  310 ICHAR=ISPACE
  315 GO TO (25,35,40,60,62,105,115,135,138,210,165,175,178,190,
     1   202,32,435),JUMP
C
C  READ A CARD
C
  400 IF (ID.EQ.21) RETURN
      READ (5,401) (IFIELD(I),I=1,10)
  401 FORMAT (10R8)
      IF (EOF,5) 500,410
C
C  ELIMINATE BLANK FIELDS
C
  410 ISTOP=10
  415 IF (IFIELD(ISTOP).NE.IBLNK) GO TO 420
      ISTOP=ISTOP-1
      IF (ISTOP.LT.1) GO TO 400
      GO TO 415
  420 IF (ICARD1.EQ.0) GO TO 430
      ICARD1=0
      GO TO 20
  430 NOFLD=0
      JUMP=17
      GO TO 301
  435 IF (ICHAR.EQ.ISPACE) GO TO 300
      IF (ICHAR.EQ.IPLUS) GO TO 440
      RETURN
  440 WRITE (6,21) (IFIELD(I),I=1,ISTOP)
      IF (IGOOF.EQ.1) GO TO 400
      GO TO 55
  500 KEOF=1
      IGOOF=1
      WRITE (6,501)
  501 FORMAT (//5X,*----------  DATA END OF FILE ENCOUNTERED BEFORE *
     1   *END CARD*//)
      RETURN
      END
      SUBROUTINE ERRCHK
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
C
C
      DIMENSION ITABLE(1)
      EQUIVALENCE (ITABLE(1),YNL(1))
      DIMENSION NAME1(1),MNAME1(1),LOCAL1(1),IDNO(1),VALUE1(1)
      EQUIVALENCE (NAME1(1),TSTORE(1)),(MNAME1(1),TSTORE(401)),
     1   (LOCAL1(1),TSTORE(801)),(IDNO(1),TSTORE(1201)),
     2   (VALUE1(1),TSTORE(1601))
      DIMENSION NODPL1(1)
      EQUIVALENCE (NODPL1(1),MATLOC(1))
      DIMENSION NNODS(10),NINO(10)
      DATA NNODS/2,2,2,2,4,2,3,2,3,4/
      DATA NINO/0,0,3,0,0,3,3,1,2,2/
      DATA ISHFT4,ICOMMA/100000000B,56000000B/
C
C
      CALL SECOND(T1)
      IF (NUMEL.EQ.0) GO TO 610
      IF (NUMEL.GE.400) GO TO 630
      LOCAL1(NUMEL+1)=NOSTOP+1
C
C  CONSTRUCT ORDERED LIST OF USER SPECIFIED NODES
C
      NUNODS=1
      DO 110 I=1,NUMEL
      ID=IDNO(I)
      IF (ID.GT.20) GO TO 110
      JSTART=LOCAL1(I)
      JSTOP=LOCAL1(I+1)-1
      DO 100 J=JSTART,JSTOP
      NODE1=NODPL1(J)
      JKNT=0
   60 JKNT=JKNT+1
      IF (JKNT.GT.NUNODS) GO TO 70
      IF (NODE1-JUNODE(JKNT)) 70,100,60
   70 K=NUNODS+1
      IF (K.GT.401) GO TO 620
   80 IF (K.LE.JKNT) GO TO 90
      JUNODE(K)=JUNODE(K-1)
      K=K-1
      GO TO 80
   90 JUNODE(K)=NODE1
      NUNODS=NUNODS+1
  100 CONTINUE
  110 CONTINUE
C
C  TRANSFER NONMODEL NODES TO FINAL NODE LIST AND ASSIGN PROGRAM NODES
C
      NOSTOP=0
      DO 180 I=1,NUMEL
      ID=IDNO(I)
      IF (ID.GT.20) GO TO 180
      LOC=LOCAL1(I)-1
      JSTOP=LOCAL1(I+1)-LOC-1
      IF ((NOSTOP+JSTOP).GT.800) GO TO 630
      LOCAL1(I)=NOSTOP+1
      DO 160 J=1,JSTOP
      NODE1=NODPL1(LOC+J)
      JKNT=0
  150 JKNT=JKNT+1
      IF (JKNT.GT.NUNODS) GO TO 620
      IF (JUNODE(JKNT).NE.NODE1) GO TO 150
      NODPLC(NOSTOP+J)=JKNT
  160 CONTINUE
      NOSTOP=NOSTOP+JSTOP
      IF (ID.EQ.20) GO TO 180
      JSTOP=NINO(ID)
      IF (JSTOP.EQ.0) GO TO 180
      IF ((NOSTOP+JSTOP).GT.800) GO TO 630
      DO 170 J=1,JSTOP
  170 NODPLC(NOSTOP+J)=0
      NOSTOP=NOSTOP+JSTOP
  180 CONTINUE
      NUMNOD=NUNODS
C
C  PROCESS EXTERNAL MODELS
C
      IF (NUMMOD.EQ.0) GO TO 300
      DO 250 I=1,NUMMOD
      IF (KIND(I).NE.20) GO TO 250
      JSTOP=SYMVAL(I,3)
      DO 210 J=1,JSTOP
  210 ITABLE(J)=SYMVAL(I,J+5)
      LSTOP=JSTOP
      JSTART=SYMVAL(I,1)
      JSTOP=SYMVAL(I,2)
      DO 240 J=JSTART,JSTOP
      ID=IDNO(J)-20
      LOC=LOCAL1(J)-1
      KSTOP=NNODS(ID)
      DO 230 K=1,KSTOP
      NODE1=NODPL1(LOC+K)
      IF (NODE1.EQ.0) GO TO 230
      DO 220 L=1,LSTOP
      IF (NODE1.NE.ITABLE(L)) GO TO 220
      NODPL1(LOC+K)=L
      GO TO 230
  220 CONTINUE
      LSTOP=LSTOP+1
      ITABLE(LSTOP)=NODE1
      NODPL1(LOC+K)=LSTOP
  230 CONTINUE
  240 CONTINUE
      SYMVAL(I,4)=LSTOP
  250 CONTINUE
C
C  EXPLODE MODELS
C
  300 IF (JELCNT(20).EQ.0) GO TO 500
      DO 450 I=1,NUMEL
      IF (IDNO(I).NE.20) GO TO 450
      MNAM=MNAME1(I)
      DO 310 J=1,NUMMOD
      IF (MODNAM(J).NE.MNAM) GO TO 310
      IF (KIND(J).NE.20) GO TO 310
      MNAM=J
      MNAME1(I)=J
      GO TO 320
  310 CONTINUE
      NOGO=1
      WRITE (6,311) MNAM
  311 FORMAT (//5X,*----------  EXTERNAL MODEL *,R7,* HAS NOT BEEN *
     1  *DEFINED PROPERLY*//)
      GO TO 450
C
C  CONSTRUCT NODE CORRESPONDANCE TABLE
C
  320 ITEMP=VALUE1(I)
      JSTOP=SYMVAL(MNAM,3)
      IF (ITEMP.EQ.JSTOP) GO TO 340
      NOGO=1
      WRITE (6,321) NAME1(I),MODNAM(MNAM)
  321 FORMAT (//5X,*----------  NUMBER OF NODES SPECIFIED FOR *,R7,
     1  * AND NUMBER OF NODES FOR MODEL *,R7,* DO NOT AGREE*//)
      GO TO 450
  340 LOC=LOCAL1(I)-1
      DO 350 J=1,JSTOP
  350 ITABLE(J)=NODPLC(LOC+J)
      LSTART=JSTOP+1
      LSTOP=SYMVAL(MNAM,4)
      IF (LSTART.GT.LSTOP) GO TO 370
      DO 360 L=LSTART,LSTOP
      NUMNOD=NUMNOD+1
      IF (NUMNOD.GT.401) GO TO 620
  360 ITABLE(L)=NUMNOD
C
C  INSERT ELEMENTS INTO TEMPORARY STACK
C
  370 JSTART=SYMVAL(MNAM,1)
      JSTOP=SYMVAL(MNAM,2)
      DO 410 J=JSTART,JSTOP
      ID=IDNO(J)-20
      JELCNT(ID)=JELCNT(ID)+1
      NUMEL=NUMEL+1
      IF (NUMEL.GT.400) GO TO 630
      IDNO(NUMEL)=ID
      NAME1(NUMEL)=(NAME1(I)/ISHFT4)*ISHFT4+ICOMMA+NAME1(J)/ISHFT4
      MNAME1(NUMEL)=MNAME1(J)
      VALUE1(NUMEL)=VALUE1(J)
      LOCAL1(NUMEL)=NOSTOP+1
      KSTOP=NNODS(ID)
      IF ((NOSTOP+KSTOP).GT.800) GO TO 630
      LOC=LOCAL1(J)-1
      DO 390 K=1,KSTOP
      NODE1=NODPL1(LOC+K)
      IF (NODE1.NE.0) GO TO 380
      NODPLC(NOSTOP+K)=1
      GO TO 390
  380 NODPLC(NOSTOP+K)=ITABLE(NODE1)
  390 CONTINUE
      NOSTOP=NOSTOP+KSTOP
      KSTOP=NINO(ID)
      IF (KSTOP.EQ.0) GO TO 410
      IF ((NOSTOP+KSTOP).GT.800) GO TO 630
      DO 400 K=1,KSTOP
  400 NODPLC(NOSTOP+K)=0
      NOSTOP=NOSTOP+KSTOP
  410 CONTINUE
  450 CONTINUE
C
C  ASSIGN LOCATIONS IN PERMANENT STORAGE
C
  500 IF (NOGO.EQ.1) GO TO 1000
      LOCATE(1)=1
      DO 510 I=1,20
      LOCATE(I+1)=LOCATE(I)+JELCNT(I)
  510 JELCNT(I)=0
      IF (LOCATE(21).GT.201) GO TO 630
C
C  MOVE FINAL CIRCUIT DATA TO PERMANENT STACK
C
      DO 520 I=1,NUMEL
      ID=IDNO(I)
      IF (ID.GT.20) GO TO 520
      LOC=LOCATE(ID)+JELCNT(ID)
      JELCNT(ID)=JELCNT(ID)+1
      NAME(LOC)=NAME1(I)
      LOCAL(LOC)=LOCAL1(I)
      MNAME(LOC)=MNAME1(I)
      VALUE(LOC)=VALUE1(I)
  520 CONTINUE
      NUMEL=LOCATE(21)-1
C
C  ASSIGN USER NODE NUMBERS TO INTERNAL NODES
C
      IF (NUMNOD.EQ.NUNODS) GO TO 540
      NODE1=JUNODE(NUNODS)
      ISTART=NUNODS+1
      DO 530 I=ISTART,NUMNOD
      NODE1=NODE1+1
  530 JUNODE(I)=NODE1
      NUNODS=NUMNOD
C
C  ASSIGN TRACUR LOCATIONS
C
  540 DO 550 I=1,20
  550 ICURNT(I)=1
      ICURNT(3)=1+2*JELCNT(2)
      ICURNT(6)=ICURNT(3)+2*JELCNT(3)
      ICURNT(7)=ICURNT(6)+2*JELCNT(6)
      ICURNT(8)=ICURNT(7)+15*JELCNT(7)
      ICURNT(9)=ICURNT(8)+5*JELCNT(8)
      ICURNT(10)=ICURNT(9)+10*JELCNT(9)
      ICURNT(11)=ICURNT(10)+15*JELCNT(10)
      IF (ICURNT(11).GT.1700) GO TO 630
C
C  CALL ROUTINES TO PRINT CIRCUIT DATA, CHECK MODELS, AND CHECK
C  RUN CONTROL PARAMETERS
C
      CALL ELPRNT
      CALL MODCHK
      CALL CIRCHK
      IF (NUMNOD.GT.401) GO TO 620
      GO TO 1000
C
C  ERROR MESSAGES
C
  610 NOGO=1
      WRITE (6,611)
  611 FORMAT (//5X,*----------  CIRCUIT HAS NO ELEMENTS*//)
      GO TO 1000
  620 NOGO=1
      WRITE (6,621)
  621 FORMAT (//5X,*----------  MAXIMUM NODE LIMIT EXCEEDED*//)
      GO TO 1000
  630 NOGO=1
      WRITE (6,631)
  631 FORMAT (//5X,*----------  MAXIMUM ELEMENT LIMIT EXCEEDED*//)
 1000 CALL SECOND(T2)
      RTIMES(1)=RTIMES(1)+T2-T1
      RETURN
      END
      SUBROUTINE ELPRNT
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/TRAN/JTRFLG,TSTEP,TSTOP,TSTART,NOTINT,STEPS(5),ENDPTS(5),
     1   KFROUT,FORFRE,KFPTS
C
C
      DIMENSION ITABLE(1),NNODS(10)
      EQUIVALENCE (ITABLE(1),YNL(1))
      DATA NNODS /2,2,2,2,4,2,3,2,3,4/
      DATA IPULSE,ISINE,IEXP,IBLNK/5HPULSE,8HSINUSOID,3HEXP,1H /
C
C  PRINT LISTING OF ELEMENTS
C
      IF (NOPRNT.EQ.1) GO TO 250
      WRITE (6,11) (JOBNAM(I),I=1,8)
   11 FORMAT (1H7/////1X,125(1H*)///1X,8A10,26X,*-----  SPICE  -----*
     1   ///21X,*CIRCUIT SUMMARY*///1X,125(1H*))
C
C  PRINT EXTERNAL MODELS
C
      ISTART=LOCATE(20)
      ISTOP=LOCATE(21)-1
      IF (ISTART.GT.ISTOP) GO TO 50
      WRITE (6,21)
   21 FORMAT (///1X,5H**** ,*EXTERNAL MODEL ELEMENTS*//6X,*NAME*,
     1   4X,*MODEL*,4X,*NODES*//)
      DO 40 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)-1
      JSTOP=VALUE(I)
      DO 30 J=1,JSTOP
      NODE1=NODPLC(LOC+J)
   30 ITABLE(J)=JUNODE(NODE1)
      WRITE (6,31) NAME(I),MODNAM(MNAM),(ITABLE(J),J=1,JSTOP)
   31 FORMAT (6X,R7,1X,R7,1X,20(I3,2X))
   40 CONTINUE
C
C  PRINT RESISTORS, CAPACITORS, AND INDUCTORS
C
   50 ISTOP=LOCATE(4)-1
      IF (ISTOP.LT.1) GO TO 80
      WRITE (6,51)
   51 FORMAT (///1X,5H**** ,*RESISTORS, CAPACITORS, AND INDUCTORS*//6X,
     1   *NAME*,9X,*NODES*,15X,*VALUE*//)
      DO 70 I=1,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      WRITE (6,61) NAME(I),JUNODE(NODE1),JUNODE(NODE2),VALUE(I)
   61 FORMAT (6X,R7,4X,I3,2X,I3,12X,1PE9.2)
   70 CONTINUE
C
C  PRINT VOLTAGE CONTROLLED CURRENT SOURCES
C
   80 ISTART=LOCATE(5)
      ISTOP=LOCATE(6)-1
      IF (ISTART.GT.ISTOP) GO TO 110
      WRITE (6,81)
   81 FORMAT (///1X,5H**** ,*VOLTAGE CONTROLLED CURRENT SOURCES*//6X,
     1   *NAME*,9X,1H+,4X,1H-,3X,2H+C,3X,2H-C,4X,*VALUE*,6X,*DELAY*//)
      DO 100 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE4=NODPLC(LOC+3)
      MNAM=MNAME(I)
      WRITE (6,91) NAME(I),JUNODE(NODE1),JUNODE(NODE2),JUNODE(NODE3),
     1   JUNODE(NODE4),VALUE(I),SOURCE(MNAM)
   91 FORMAT (6X,R7,4X,4(I3,2X),1PE9.2,2X,E9.2)
  100 CONTINUE
C
C  PRINT TRANSISTORS
C
  110 ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) GO TO 140
      WRITE (6,111)
  111 FORMAT (///1X,5H**** ,*BIPOLAR JUNCTION TRANSISTORS*//6X,
     1   *NAME*,9X,1HC,4X,1HB,4X,1HE,9X,*MODEL*,8X,*AREA*//)
      DO 130 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      WRITE (6,121) NAME(I),JUNODE(NODE1),JUNODE(NODE2),JUNODE(NODE3),
     1   MNAME(I),VALUE(I)
  121 FORMAT (6X,R7,4X,3(I3,2X),7X,R7,2X,F9.3)
  130 CONTINUE
C
C  PRINT DIODES
C
  140 ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) GO TO 170
      WRITE (6,141)
  141 FORMAT (///1X,5H**** ,*DIODES*//6X,*NAME*,9X,1H+,4X,1H-,14X,
     1   *MODEL*,8X,*AREA*//)
      DO 160 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      WRITE (6,151) NAME(I),JUNODE(NODE1),JUNODE(NODE2),MNAME(I),
     1   VALUE(I)
  151 FORMAT (6X,R7,4X,2(I3,2X),12X,R7,2X,F9.3)
  160 CONTINUE
C
C  PRINT JFETS
C
  170 ISTART=LOCATE(9)
      ISTOP=LOCATE(10)-1
      IF (ISTART.GT.ISTOP) GO TO 200
      WRITE (6,171)
  171 FORMAT (///1X,5H**** ,*JFETS*//6X,*NAME*,9X,1HD,4X,1HG,4X,1HS,
     1   9X,*MODEL*,8X,*AREA*//)
      DO 190 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      WRITE (6,121) NAME(I),JUNODE(NODE1),JUNODE(NODE2),JUNODE(NODE3),
     1   MNAME(I),VALUE(I)
  190 CONTINUE
C
C  PRINT MOSFETS
C
  200 ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) GO TO 230
      WRITE (6,201)
  201 FORMAT (///1X,5H**** ,*MOSFETS*//6X,*NAME*,9X,1HD,4X,1HG,4X,
     1   1HS,4X,1HB,4X,*MODEL*,8X,*AREA*//)
      DO 220 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE4=NODPLC(LOC+3)
      WRITE (6,211) NAME(I),JUNODE(NODE1),JUNODE(NODE2),JUNODE(NODE3),
     1   JUNODE(NODE4),MNAME(I),VALUE(I)
  211 FORMAT (6X,R7,4X,4(I3,2X),2X,R7,2X,F9.3)
  220 CONTINUE
C
C  PROCESS INDEPENDENT SOURCES
C
  230 WRITE (6,241)
  241 FORMAT (///1X,5H**** ,*INDEPENDENT SOURCES*//6X,*NAME*,
     1   9X,1H+,4X,1H-,13X,*DC VALUE*,3X,*AC VALUE*,3X,*AC PHASE*,
     2   4X,*TRANSIENT*//)
  250 DO 370 ID=4,6,2
      ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 370
      DO 360 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      MNAM=MNAME(I)
      ITYPE=SOURCE(MNAM+3)
      IF (NOPRNT.EQ.1) GO TO 270
      NAM=IBLNK
      IF (ITYPE.EQ.1) NAM=IPULSE
      IF (ITYPE.EQ.2) NAM=IEXP
      IF (ITYPE.EQ.3) NAM=ISINE
      WRITE (6,261) NAME(I),JUNODE(NODE1),JUNODE(NODE2),SOURCE(MNAM),
     1   SOURCE(MNAM+1),SOURCE(MNAM+4),NAM
  261 FORMAT (6X,R7,4X,I3,2X,I3,12X,1PE9.2,2X,E9.2,2X,E9.2,4X,A8)
  270 IF (JTRFLG.EQ.0) GO TO 350
      IF (ITYPE.EQ.0) GO TO 350
      GO TO (280,300,320),ITYPE
C
C  PULSE SOURCE
C
  280 IF (SOURCE(MNAM+2).EQ.1.0) GO TO 290
      IF (SOURCE(MNAM+7).LT.TSTEP) SOURCE(MNAM+7)=TSTEP
      IF (SOURCE(MNAM+8).LT.TSTEP) SOURCE(MNAM+8)=TSTEP
      IF (SOURCE(MNAM+9).LT.TSTEP) SOURCE(MNAM+9)=TSTEP
      IF (SOURCE(MNAM+10).EQ.0.0) SOURCE(MNAM+10)=TSTOP
      IF (SOURCE(MNAM+10).LT.TSTEP) SOURCE(MNAM+10)=TSTEP
      TEMP=TSTEP+SOURCE(MNAM+8)+SOURCE(MNAM+9)+SOURCE(MNAM+10)
      IF (SOURCE(MNAM+11).EQ.0.0) SOURCE(MNAM+11)=TSTOP
      IF (SOURCE(MNAM+11).LT.TEMP) SOURCE(MNAM+11)=TEMP
  290 IF (NOPRNT.EQ.1) GO TO 350
      JSTART=MNAM+5
      JSTOP=MNAM+11
      WRITE (6,291) (SOURCE(J),J=JSTART,JSTOP)
  291 FORMAT (72X,*VALUES*,4X,1PE9.2,3X,E9.2/72X,*DELAY*,5X,E9.2/
     1   72X,*RISETIME*,2X,E9.2/72X,*FALLTIME*,2X,E9.2/72X,*WIDTH*,
     2   5X,E9.2/72X,*PERIOD*,4X,E9.2)
      GO TO 350
C
C  EXPONENTIAL SOURCE
C
  300 IF (SOURCE(MNAM+2).EQ.1.0) GO TO 310
      IF (SOURCE(MNAM+7).LT.TSTEP) SOURCE(MNAM+7)=TSTEP
      IF (SOURCE(MNAM+8).LT.TSTEP) SOURCE(MNAM+8)=TSTEP
      TEMP=SOURCE(MNAM+7)+TSTEP
      IF (SOURCE(MNAM+9).LT.TEMP) SOURCE(MNAM+9)=TEMP
      IF (SOURCE(MNAM+10).LT.TSTEP) SOURCE(MNAM+10)=TSTEP
  310 IF (NOPRNT.EQ.1) GO TO 350
      JSTART=MNAM+5
      JSTOP=MNAM+10
      WRITE (6,311) (SOURCE(J),J=JSTART,JSTOP)
  311 FORMAT (72X,*VALUES*,4X,1PE9.2,3X,E9.2/72X,*DELAY1*,4X,E9.2/
     1   72X,*TAU1*,6X,E9.2/72X,*DELAY2*,4X,E9.2/72X,*TAU2*,6X,E9.2)
      GO TO 350
C
C  SINUSOIDAL SOURCE
C
  320 IF (SOURCE(MNAM+2).EQ.1.0) GO TO 330
      IF (SOURCE(MNAM+7).EQ.0.0) SOURCE(MNAM+7)=1.0/TSTOP
      IF (SOURCE(MNAM+8).LT.TSTEP) SOURCE(MNAM+8)=TSTEP
  330 IF (NOPRNT.EQ.1) GO TO 350
      JSTART=MNAM+5
      JSTOP=MNAM+9
      WRITE (6,331) (SOURCE(J),J=JSTART,JSTOP)
  331 FORMAT (72X,*OFFSET*,4X,1PE9.2/72X,*AMPLITUDE*,1X,E9.2/72X,
     1   *FREQUENCY*,1X,E9.2/72X,*DELAY*,5X,E9.2/72X,*THETA*,5X,E9.2)
  350 SOURCE(MNAM+2)=1.0
  360 CONTINUE
  370 CONTINUE
      DO 420 ID=4,6,2
      ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 420
      DO 410 I=ISTART,ISTOP
      MNAM=MNAME(I)
      ITYPE=SOURCE(MNAM+3)
      IF (SOURCE(MNAM+2).EQ.0.0) GO TO 410
      TEMP=SOURCE(MNAM+4)/RAD
      SOURCE(MNAM+4)=SOURCE(MNAM+1)*SIN(TEMP)
      SOURCE(MNAM+1)=SOURCE(MNAM+1)*COS(TEMP)
      IF (JTRFLG.EQ.0) GO TO 400
      IF (ITYPE.EQ.0) GO TO 400
      GO TO (380,400,390),ITYPE
  380 SOURCE(MNAM+8)=SOURCE(MNAM+7)+SOURCE(MNAM+8)
      TEMP=SOURCE(MNAM+9)
      SOURCE(MNAM+9)=SOURCE(MNAM+8)+SOURCE(MNAM+10)
      SOURCE(MNAM+10)=SOURCE(MNAM+9)+TEMP
      GO TO 400
  390 TEMP=SOURCE(MNAM+7)*TWOPI
      SOURCE(MNAM+7)=SOURCE(MNAM+8)
      SOURCE(MNAM+8)=SOURCE(MNAM+9)
      SOURCE(MNAM+9)=TEMP
  400 SOURCE(MNAM+2)=0.0
  410 CONTINUE
  420 CONTINUE
C
C  INVERT RESISTANCE AND INDUCTANCE VALUES
C
      ISTOP=LOCATE(2)-1
      IF (ISTOP.LT.1) GO TO 480
      DO 470 I=1,ISTOP
  470 VALUE(I)=1.0/VALUE(I)
  480 ISTART=LOCATE(3)
      ISTOP=LOCATE(4)-1
      IF (ISTART.GT.ISTOP) GO TO 500
      DO 490 I=ISTART,ISTOP
      VALUE(I)=1.0/VALUE(I)
  490 CONTINUE
C
C  CONSTRUCT NODE TABLE
C
  500 ISTOP=NUNODS+1
      DO 510 I=1,ISTOP
  510 IUR(I)=1
      DO 570 ID=1,10
      ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 570
      DO 560 I=ISTART,ISTOP
      JSTART=LOCAL(I)
      JSTOP=JSTART+NNODS(ID)-1
      DO 550 J=JSTART,JSTOP
      NODE=NODPLC(J)
      ISPOT=IUR(NODE+1)
      K=IUR(NUNODS+1)
  520 K=K-1
      IF (K.LT.ISPOT) GO TO 530
      ITABLE(K+1)=ITABLE(K)
      GO TO 520
  530 ITABLE(ISPOT)=I
      K=NODE
      KSTOP=NUNODS+1
  540 K=K+1
      IF (K.GT.KSTOP) GO TO 550
      IUR(K)=IUR(K)+1
      GO TO 540
  550 CONTINUE
  560 CONTINUE
  570 CONTINUE
C
C  CHECK FOR SINGULAR NODES AND CUTSETS OF CAPACITORS AND/OR
C  CURRENT SOURCES
C
      DO 630 I=2,NUNODS
      JSTART=IUR(I)
      JSTOP=IUR(I+1)-1
      IF (JSTART.GE.JSTOP) GO TO 620
      DO 610 J=JSTART,JSTOP
      IELNUM=ITABLE(J)
      IF (IELNUM.LT.LOCATE(2)) GO TO 630
      IF (IELNUM.LT.LOCATE(3)) GO TO 610
      IF (IELNUM.LT.LOCATE(4)) GO TO 630
      IF (IELNUM.LT.LOCATE(6)) GO TO 610
      GO TO 630
  610 CONTINUE
      NOGO=1
      WRITE (6,611) JUNODE(I)
  611 FORMAT (//5X,*----------  INPUT DATA CONTAINS CUTSET OF *
     1   *CAPACITORS AND/OR CURRENT SOURCES AT NODE *,I5//)
      GO TO 630
  620 NOGO=1
      WRITE (6,621) JUNODE(I)
  621 FORMAT (//5X,*----------  LESS THAN TWO ELEMENTS ARE CONNECTED *
     1   *TO NODE *,I5//)
  630 CONTINUE
C
C  PRINT NODE TABLE
C
      IF (NOPRNT.EQ.1) RETURN
      WRITE (6,801)
  801 FORMAT (1H1,5H**** ,*NODE TABLE*//5X,*NODE*,5X,
     1   *ELEMENTS CONNECTED*)
      ISTOP=IUR(NUNODS+1)-1
      DO 810 I=1,ISTOP
      IELNUM=ITABLE(I)
      ITABLE(I)=NAME(IELNUM)
  810 CONTINUE
      DO 830 I=1,NUNODS
      JSTART=IUR(I)
      JSTOP=IUR(I+1)-1
      IF (JSTART.GT.JSTOP) GO TO 830
      WRITE (6,821) JUNODE(I),(ITABLE(J),J=JSTART,JSTOP)
  821 FORMAT (/3X,I5,3X,15(1X,R7)/(11X,15(1X,R7)))
  830 CONTINUE
      RETURN
      END
      SUBROUTINE MODCHK
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
C
C
      DIMENSION MOTYP(6),MODEF(4),DEFEM(16),DEFGP(25),DEFD(7),DEFJ(10),
     1   DEFM(15)
      DATA MOTYP /7,7,8,8,9,10/
      DATA MODEF /1,3,5,6/
      DATA DEFEM /1.0,100.0,1.0,8*0.0,1.0E-14,2*1.0,0.0,1.11/
      DATA DEFGP /1.0,100.0,1.0,8*0.0,1.0E-14,4*0.0,2.0,2*0.0,2.0,1.0,
     1   0.5,1.0,0.5,1.11/
      DATA DEFD /3*0.0,1.0E-14,2*1.0,1.11/
        DATA SBDEG /0.69/
      DATA DEFJ /1.0,-2.0,1.0E-4,5*0.0,1.0,1.0E-14/
      DATA DEFM /-1.0,2.0,0.5,1.0E-4,9*0.0,1.0,1.0E-14/
      DATA IBLNK,IDEF,JLETP,JLETN,LSJUN,LSSBD /55555555555555B,
     1   10H   DEFAULT,1HP,1HN,3HD  ,3HSBD/
C
C  ASSIGN MODEL NAMES
C
      ID=6
    5 ID=ID+1
      IF (ID.GT.10) GO TO 60
      IKNT=LOCATE(ID)-1
      ISTOP=LOCATE(ID+1)-1
   10 IKNT=IKNT+1
      IF (IKNT.GT.ISTOP) GO TO 5
      JKNT=0
   20 JKNT=JKNT+1
      IF (JKNT.GT.NUMMOD) GO TO 30
      IF (MNAME(IKNT).NE.MODNAM(JKNT)) GO TO 20
      MTYPE=KIND(JKNT)
      IF (ID.NE.MOTYP(MTYPE)) GO TO 20
      MNAME(IKNT)=JKNT
      GO TO 10
   30 IF (MNAME(IKNT).EQ.IBLNK) GO TO 40
      WRITE (6,31) MNAME(IKNT)
   31 FORMAT (//5X,*---  WARNING  ---  MODEL  *,R7,* HAS NOT BEEN *
     1   *DEFINED CORRECTLY*//)
   40 IF (NUMMOD.GE.25) GO TO 55
      NUMMOD=NUMMOD+1
      MODNAM(NUMMOD)=MNAME(IKNT)
      KIND(NUMMOD)=MODEF(ID-6)
      MNAME(IKNT)=NUMMOD
      DO 50 I=1,25
   50 SYMVAL(NUMMOD,I)=0.0
      GO TO 10
   55 NOGO=1
      WRITE (6,56)
   56 FORMAT (//5X,*----------  MODEL LIMIT EXCEEDED*//)
      GO TO 10
C
C  PROCESS MODEL PARAMETERS
C
   60 IF (NUMMOD.EQ.0) RETURN
      IF (NOPRNT.EQ.1) GO TO 70
      WRITE (6,61)
   61 FORMAT (1H1)
   70 IF (JELCNT(7).EQ.0) GO TO 100
C
C  EBERS-MOLL MODELS
C
      IPRINT=NOPRNT
      DO 90 I=1,NUMMOD
      IF (KIND(I).NE.1) GO TO 90
      IF (IPRINT.EQ.1) GO TO 75
      IPRINT=1
      WRITE (6,71)
   71 FORMAT (///1X,5H**** ,*EBERS-MOLL MODEL PARAMETERS*//1X,*NAME*,
     1   3X,*TYPE*,4X,*BF*,6X,*BR*,6X,*RB*,5X,*RC*,4X,*RE*,
     2   5X,*CCS*,6X,*TF*,7X,*TR*,7X,*CJE*,6X,*CJC*,6X,*IS*,
     3   6X,*PE*,4X,*PC*,5X,*VA*,5X,*EG*)
   75 IF (SYMVAL(I,1).EQ.0.0) SYMVAL(I,1)=DEFEM(1)
      DO 80 J=2,16
      IF (SYMVAL(I,J).LE.0.0) SYMVAL(I,J)=DEFEM(J)
   80 CONTINUE
      IF (NOPRNT.EQ.1) GO TO 85
      NAM=JLETN
      IF (SYMVAL(I,1).EQ.-1.0) NAM=JLETP
      MNAM=MODNAM(I)
      IF (MNAM.EQ.IBLNK) MNAM=IDEF
      WRITE (6,81) MNAM,NAM,(SYMVAL(I,J),J=2,16)
   81 FORMAT (/1X,R7,2X,A1,2X,F7.2,1X,F7.3,1X,F6.1,1X,F6.1,1X,F5.1,
     1   1X,1PE8.2,1X,E8.2,1X,E8.2,1X,E8.2,1X,E8.2,1X,E8.2,1X,
     2   0PF5.2,1X,F5.2,1X,F7.2,1X,F5.2)
   85 IF (SYMVAL(I,4).NE.0.0) SYMVAL(I,4)=1.0/SYMVAL(I,4)
      IF (SYMVAL(I,5).NE.0.0) SYMVAL(I,5)=1.0/SYMVAL(I,5)
      IF (SYMVAL(I,6).NE.0.0) SYMVAL(I,6)=1.0/SYMVAL(I,6)
      IF (SYMVAL(I,13).LT.0.1) SYMVAL(I,13)=0.1
      SYMVAL(I,10)=SYMVAL(I,10)*SQRT(SYMVAL(I,13))
      IF (SYMVAL(I,14).LT.0.1) SYMVAL(I,14)=0.1
      SYMVAL(I,11)=SYMVAL(I,11)*SQRT(SYMVAL(I,14))
      IF (SYMVAL(I,15).NE.0.0) SYMVAL(I,15)=1.0/SYMVAL(I,15)
      IF (SYMVAL(I,16).LT.0.1) SYMVAL(I,16)=0.1
   90 CONTINUE
C
C  GUMMEL-POON MODELS
C
      IPRINT=NOPRNT
      DO 490 I=1,NUMMOD
      IF (KIND(I).NE.2) GO TO 490
      IF (IPRINT.EQ.1) GO TO 475
      IPRINT=1
      WRITE (6,471)
  471 FORMAT(////1X,5H**** ,*GUMMEL-POON MODEL PARAMETERS*)
  475 IF (SYMVAL(I,1).EQ.0.0) SYMVAL(I,1)=DEFGP(1)
      DO 480 J=2,25
      IF (SYMVAL(I,J).LE.0.0) SYMVAL(I,J)=DEFGP(J)
  480 CONTINUE
      IF (NOPRNT.EQ.1) GO TO 485
      NAM=JLETN
      IF (SYMVAL(I,1).EQ.-1.0) NAM=JLETP
      MNAM=MODNAM(I)
      IF (MNAM.EQ.IBLNK) MNAM=IDEF
      WRITE (6,481) MNAM,NAM,(SYMVAL(I,J),J=2,25)
  481 FORMAT (//1X,*NAME*,3X,*TYPE*,4X,*BFM*,5X,*BRM*,5X,*RB*,5X,
     1   *RC*,4X,*RE*,5X,*CCS*,6X,*TF*,7X,*TR*,7X,*CJE*,6X,*CJC*,6X,
     2   *IS*,7X,*VA*,6X,*VB*//1X,R7,2X,A1,2X,F7.2,1X,F7.3,1X,F6.1,1X,
     3   F6.1,1X,F5.1,1X,1PE8.2,1X,E8.2,1X,E8.2,1X,E8.2,1X,E8.2,1X,
     4   E8.2,1X,0PF7.2,1X,F7.2//16X,*C2*,6X,*IK*,6X,*NE*,10X,
     5   *C4*,6X,*IKR*,5X,*NC*,14X,*PE*,3X,*ME*,7X,*PC*,3X,*MC*,
     6   9X,*EG*//12X,1PE8.2,1X,E8.2,2X,0PF5.3,4X,1PE8.2,1X,E8.2,
     7   2X,0PF5.3,10X,F5.2,1X,F5.3,3X,F5.2,1X,F5.3,5X,F5.2)
  485 CSAT=SYMVAL(I,12)
      SYMVAL(I,2)=CSAT/SYMVAL(I,2)
      SYMVAL(I,3)=CSAT/SYMVAL(I,3)
      IF (SYMVAL(I,4).NE.0.0) SYMVAL(I,4)=1.0/SYMVAL(I,4)
      IF (SYMVAL(I,5).NE.0.0) SYMVAL(I,5)=1.0/SYMVAL(I,5)
      IF (SYMVAL(I,6).NE.0.0) SYMVAL(I,6)=1.0/SYMVAL(I,6)
      IF (SYMVAL(I,21).LT.0.1) SYMVAL(I,21)=0.1
      IF (SYMVAL(I,22).GT.0.9) SYMVAL(I,22)=0.9
      SYMVAL(I,10)=SYMVAL(I,10)*EXP(SYMVAL(I,22)*ALOG(SYMVAL(I,21)))
      IF (SYMVAL(I,23).LT.0.1) SYMVAL(I,23)=0.1
      IF (SYMVAL(I,24).GT.0.9) SYMVAL(I,24)=0.9
      SYMVAL(I,11)=SYMVAL(I,11)*EXP(SYMVAL(I,24)*ALOG(SYMVAL(I,23)))
      IF (SYMVAL(I,13).NE.0.0) SYMVAL(I,13)=1.0/SYMVAL(I,13)
      IF (SYMVAL(I,14).NE.0.0) SYMVAL(I,14)=1.0/SYMVAL(I,14)
      SYMVAL(I,15)=CSAT*SYMVAL(I,15)
      IF (SYMVAL(I,16).NE.0.0) SYMVAL(I,16)=1.0/SYMVAL(I,16)
      SYMVAL(I,17)=VT*SYMVAL(I,17)
      SYMVAL(I,18)=CSAT*SYMVAL(I,18)
      IF (SYMVAL(I,19).NE.0.0) SYMVAL(I,19)=1.0/SYMVAL(I,19)
      SYMVAL(I,20)=VT*SYMVAL(I,20)
      IF (SYMVAL(I,25).LT.0.1) SYMVAL(I,25)=0.1
  490 CONTINUE
C
C  DIODES
C
  100 IF (JELCNT(8).EQ.0) GO TO 130
      IF (NOPRNT.EQ.1) GO TO 105
      WRITE (6,101)
  101 FORMAT (///1X,5H**** ,*DIODE MODEL PARAMETERS*//1X,*NAME*,4X,
     1   *TYPE*,8X,*RS*,7X,*TT*,8X,*CJO*,7X,*IS*,8X,*N*,5X,*PHI*,
     2   6X,*EG*)
  105 DO 120 I=1,NUMMOD
      IF (KIND(I).EQ.3) GO TO 108
      IF (KIND(I).NE.4) GO TO 120
      IF (SYMVAL(I,7).EQ.0.0) SYMVAL(I,7)=SBDEG
  108 DO 110 J=1,7
      IF (SYMVAL(I,J).LE.0.0) SYMVAL(I,J)=DEFD(J)
  110 CONTINUE
      IF (NOPRNT.EQ.1) GO TO 115
      MNAM=MODNAM(I)
      IF (MNAM.EQ.IBLNK) MNAM=IDEF
      NAM=LSJUN
      IF (KIND(I).EQ.4) NAM=LSSBD
      WRITE (6,111) MNAM,NAM,(SYMVAL(I,J),J=1,7)
  111 FORMAT (/1X,R7,2X,A3,3X,F7.1,3X,1PE8.2,3X,E7.1,3X,E8.2,3X,0PF4.1,
     1   3X,F5.2,3X,F5.2)
  115 IF (SYMVAL(I,1).NE.0.0) SYMVAL(I,1)=1.0/SYMVAL(I,1)
      SYMVAL(I,5)=SYMVAL(I,5)*VT
      IF (SYMVAL(I,6).LT.0.1) SYMVAL(I,6)=0.1
      SYMVAL(I,3)=SYMVAL(I,3)*SQRT(SYMVAL(I,6))
  120 CONTINUE
C
C  JFETS
C
  130 IF (JELCNT(9).EQ.0) GO TO 160
      IF (NOPRNT.EQ.1) GO TO 135
      WRITE (6,131)
  131 FORMAT (///1X,5H**** ,*JFET MODEL PARAMETERS*//1X,*NAME*,3X,
     1   *TYPE*,4X,*VTO*,7X,*BETA*,5X,*LAMBDA*,8X,*RD*,8X,*RS*,7X,
     2   *CGS*,8X,*CGD*,7X,*PB*,7X,*IS*)
  135 DO 150 I=1,NUMMOD
      IF (KIND(I).NE.5) GO TO 150
      IF (SYMVAL(I,1).EQ.0.0) SYMVAL(I,1)=DEFJ(1)
      IF (SYMVAL(I,2).EQ.0.0) SYMVAL(I,2)=DEFJ(2)
      DO 140 J=3,10
      IF (SYMVAL(I,J).LE.0.0) SYMVAL(I,J)=DEFJ(J)
  140 CONTINUE
      IF (NOPRNT.EQ.1) GO TO 145
      NAM=JLETN
      IF (SYMVAL(I,1).EQ.-1.0) NAM=JLETP
      MNAM=MODNAM(I)
      IF (MNAM.EQ.IBLNK) MNAM=IDEF
      WRITE (6,141) MNAM,NAM,(SYMVAL(I,J),J=2,10)
  141 FORMAT (/1X,R7,2X,A1,2X,F8.3,3X,1PE8.2,3X,E7.1,3X,0PF7.1,3X,
     1   F7.1,3X,1PE8.2,3X,E8.2,3X,0PF5.2,3X,1PE8.2)
  145 IF (SYMVAL(I,5).NE.0.0) SYMVAL(I,5)=1.0/SYMVAL(I,5) 
      IF (SYMVAL(I,6).NE.0.0) SYMVAL(I,6)=1.0/SYMVAL(I,6) 
      IF (SYMVAL(I,9).LT.0.1) SYMVAL(I,9)=0.1 
      SYMVAL(I,7)=SYMVAL(I,7)*SQRT(SYMVAL(I,9))
      SYMVAL(I,8)=SYMVAL(I,8)*SQRT(SYMVAL(I,9))
  150 CONTINUE
C 
C  MOSFETS
C 
  160 IF (JELCNT(10).EQ.0) GO TO 200
      IF (NOPRNT.EQ.1) GO TO 165
      WRITE (6,161)
  161 FORMAT (///1X,5H**** ,*MOSFET MODEL PARAMETERS*//1X,*NAME*,3X,
     1   *TYPE*,4X,*VTO*,4X,*PHI*,4X,*BETA*,4X,*GAMMA*,2X,*LAMBDA*,6X,
     2   *RD*,6X,*RS*,5X,*CGS*,6X,*CGD*,6X,*CGB*,6X,*CBD*,6X,*CBS*,
     3   5X,*PB*,5X,*IS*) 
  165 DO 180 I=1,NUMMOD
      IF (KIND(I).NE.6) GO TO 180 
      IF (SYMVAL(I,1).EQ.0.0) SYMVAL(I,1)=DEFM(1) 
      IF (SYMVAL(I,2).EQ.0.0) SYMVAL(I,2)=DEFM(2) 
      DO 170 J=3,15
      IF (SYMVAL(I,J).LE.0.0) SYMVAL(I,J)=DEFM(J) 
  170 CONTINUE
      IF (NOPRNT.EQ.1) GO TO 175
      NAM=JLETN
      IF (SYMVAL(I,1).EQ.-1.0) NAM=JLETP
      MNAM=MODNAM(I)
      IF (MNAM.EQ.IBLNK) MNAM=IDEF
      WRITE (6,171) MNAM,NAM,(SYMVAL(I,J),J=2,15) 
  171 FORMAT (/1X,R7,2X,A1,2X,F8.3,1X,F5.2,1X,1PE8.2,1X,E7.1,1X,
     1   E7.1,1X,0PF7.1,1X,F7.1,1X,1PE8.2,1X,E8.2,1X,E8.2,1X,E8.2,1X, 
     2   E8.2,1X,0PF5.2,1X,1PE8.2)
  175 IF (SYMVAL(I,7).NE.0.0) SYMVAL(I,7)=1.0/SYMVAL(I,7) 
      IF (SYMVAL(I,8).NE.0.0) SYMVAL(I,8)=1.0/SYMVAL(I,8) 
      IF (SYMVAL(I,3).LT.0.1) SYMVAL(I,3)=0.1 
      IF (SYMVAL(I,14).LT.0.1) SYMVAL(I,14)=0.1
      SYMVAL(I,12)=SYMVAL(I,12)*SQRT(SYMVAL(I,14))
      SYMVAL(I,13)=SYMVAL(I,13)*SQRT(SYMVAL(I,14))
  180 CONTINUE
C 
C  RESERVE ADDITIONAL NODES
C 
C  TRANSISTORS
C 
  200 IF (NOGO.EQ.1) RETURN
      ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) GO TO 240
      DO 230 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      IF (SYMVAL(MNAM,4).EQ.0.0) GO TO 205
      NUMNOD=NUMNOD+1 
      NODPLC(LOC+4)=NUMNOD
      GO TO 210
  205 NODPLC(LOC+4)=NODPLC(LOC+1) 
  210 IF (SYMVAL(MNAM,5).EQ.0.0) GO TO 215
      NUMNOD=NUMNOD+1 
      NODPLC(LOC+3)=NUMNOD
      GO TO 220
  215 NODPLC(LOC+3)=NODPLC(LOC)
  220 IF (SYMVAL(MNAM,6).EQ.0.0) GO TO 225
      NUMNOD=NUMNOD+1 
      NODPLC(LOC+5)=NUMNOD
      GO TO 230
  225 NODPLC(LOC+5)=NODPLC(LOC+2) 
  230 CONTINUE
C 
C  DIODES 
C 
  240 ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) GO TO 260
      DO 250 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      IF (SYMVAL(MNAM,1).EQ.0.0) GO TO 245
      NUMNOD=NUMNOD+1 
      NODPLC(LOC+2)=NUMNOD
      GO TO 250
  245 NODPLC(LOC+2)=NODPLC(LOC)
  250 CONTINUE
C 
C  JFETS
C 
  260 ISTART=LOCATE(9)
      ISTOP=LOCATE(10)-1
      IF (ISTART.GT.ISTOP) GO TO 290
      DO 280 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      IF (SYMVAL(MNAM,5).EQ.0.0) GO TO 265
      NUMNOD=NUMNOD+1 
      NODPLC(LOC+3)=NUMNOD
      GO TO 270
  265 NODPLC(LOC+3)=NODPLC(LOC)
  270 IF (SYMVAL(MNAM,6).EQ.0.0) GO TO 275
      NUMNOD=NUMNOD+1 
      NODPLC(LOC+4)=NUMNOD
      GO TO 280
  275 NODPLC(LOC+4)=NODPLC(LOC+2) 
  280 CONTINUE
C 
C  MOSFETS
C 
  290 ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) RETURN 
      DO 310 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      IF (SYMVAL(MNAM,7).EQ.0.0) GO TO 295
      NUMNOD=NUMNOD+1 
      NODPLC(LOC+4)=NUMNOD
      GO TO 300
  295 NODPLC(LOC+4)=NODPLC(LOC)
  300 IF (SYMVAL(MNAM,8).EQ.0.0) GO TO 305
      NUMNOD=NUMNOD+1 
      NODPLC(LOC+5)=NUMNOD
      GO TO 310
  305 NODPLC(LOC+5)=NODPLC(LOC+2) 
  310 CONTINUE
      RETURN
      END 
      SUBROUTINE CIRCHK
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21), 
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10), 
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/OCNTRL/IPRCOM(10,2),IPLCOM(10,2),PLTLIM(10,2,2), 
     1   IACPRT(5,4),IACPLT(5,4),ACPLIM(5,4,2)
      COMMON/DC/ICVFLG,ITCELM,TCSTAR,TCSTOP,TCINCR,KSSOP,KINEL,KOVAR
      COMMON/TRAN/JTRFLG,TSTEP,TSTOP,TSTART,NOTINT,STEPS(5),ENDPTS(5),
     1   KFROUT,FORFRE,KFPTS
      COMMON/AC/JACFLG,FSTART,FSTOP,IDFREQ,FINCR,INOISE,NOSPRT,
     1   NOSOUT,NOSIN 
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
      COMMON/SENVAR/NSENS,KSNOUT(10)
      COMMON/DESIGN/JDSFLG,NSPEC,NDES,ERROR,NOELEM(30),NELTPE(30),
     1   NOUNUM(10),DESVAL(10),WEIGHT(10) 
C 
C 
      DATA IBLNK,ILLIN,ILDEC,ILOCT /1H ,6HLINEAR,6HDECADE,6HOCTAVE/
C 
C  CHECK OUTPUT REQUESTS
C 
      IF (IONUM.EQ.0) GO TO 400
      IF (NOPRNT.EQ.1) GO TO 25
      WRITE (6,21)
   21 FORMAT (///1X,5H**** ,*OUTPUT REQUESTS*//8X,*NAME*,5X,
     1   *ELEMENT*,5X,*+NODE*,5X,*-NODE*) 
   25 IKNT=0
   30 IKNT=IKNT+1 
      IF (IKNT.GT.IONUM) GO TO 400
      IF (IOFLG(IKNT).EQ.0) GO TO 30
      IF (IOFLG(IKNT).EQ.3) GO TO 30
      IPNOD=IOPND(IKNT)
      IF (IOFLG(IKNT).EQ.2) GO TO 100 
C 
C  VOLTAGE OUTPUT 
C 
      INNOD=IONND(IKNT)
      IF (NOPRNT.EQ.1) GO TO 55
      WRITE (6,51) IONAM(IKNT),IPNOD,INNOD
   51 FORMAT (/8X,R7,13X,I5,5X,I5)
   55 IF (IPNOD.EQ.INNOD) GO TO 200
      JKNT=0
   60 JKNT=JKNT+1 
      IF (JKNT.GT.NUNODS) GO TO 200
      IF (IPNOD.NE.JUNODE(JKNT)) GO TO 60 
      IOPND(IKNT)=JKNT
      JKNT=0
   70 JKNT=JKNT+1 
      IF (JKNT.GT.NUNODS) GO TO 200
      IF (INNOD.NE.JUNODE(JKNT)) GO TO 70 
      IONND(IKNT)=JKNT
      GO TO 30
C 
C  CURRENT OUTPUT 
C 
  100 IF (NOPRNT.EQ.1) GO TO 105
      WRITE (6,101) IONAM(IKNT),IPNOD 
  101 FORMAT (/8X,R7,3X,R7)
  105 CALL IDEN (IPNOD,ID,ITEMP)
      IF (ID.NE.6) GO TO 200
      IOPND(IKNT)=ITEMP
      IONND(IKNT)=1
      GO TO 30
C 
C  OUTPUT VARIABLE ERROR
C 
  200 IOFLG(IKNT)=0
      IOPND(IKNT)=1
      IONND(IKNT)=1
      WRITE (6,201) IONAM(IKNT)
  201 FORMAT (//5X,*----------  OUTPUT VARIABLE  *,R7,* HAS NOT BEEN *
     1   *DEFINED CORRECTLY*//)
      GO TO 30
C 
C  CHECK DC ANALYSIS REQUESTS 
C 
  400 IF (ICVFLG.EQ.0) GO TO 500
      IF (NOPRNT.EQ.1) GO TO 425
      WRITE (6,421) ITCELM,TCSTAR,TCSTOP,TCINCR
  421 FORMAT (///1X,5H**** ,*TRANSFER CURVES*//5X,*VARIABLE SOURCE*,5X,
     1   *FIRST VALUE*,6X,*LAST VALUE*,6X,*INCREMENT*//9X,R7,10X,1PE9.2,
     2   7X,E9.2,7X,E9.2) 
C 
C  TRANSFER CURVES
C 
  425 CALL IDEN (ITCELM,ID,ITEMP) 
      IF (ID.EQ.4) GO TO 430
      IF (ID.EQ.6) GO TO 430
      GO TO 480
  430 ITCELM=ITEMP
      IF (TCINCR.EQ.0.0) GO TO 445
      TEMP=(TCSTOP-TCSTAR)/TCINCR 
      IF (TEMP.GT.0.0) GO TO 440
      TCINCR=-TCINCR
      TEMP=-TEMP
  440 ICVFLG=IFIX(TEMP+0.5)+1 
      IF (ICVFLG.LT.2) ICVFLG=2
      IF (ICVFLG.LT.102) GO TO 450
  445 ICVFLG=101
      TCINCR=(TCSTOP-TCSTAR)/100.0
      WRITE (6,446) TCINCR
  446 FORMAT (//5X,*---  WARNING  ---  SOURCE INCREMENT INCREASED TO *,
     1   1PE8.2)
  450 IF (NUMOR(1).GT.0) GO TO 500
      IF (JDSFLG.GT.0) GO TO 500
      ICVFLG=0
      WRITE (6,471)
  471 FORMAT (//5X,*----------  TRANSFER CURVE REQUESTED BUT NO DC *
     1   *OUTPUTS HAVE BEEN DEFINED*//)
      GO TO 500
  480 ICVFLG=0
      WRITE (6,481)
  481 FORMAT (//5X,*----------  TRANSFER CURVE VARIABLE SOURCE HAS NOT *
     1   *BEEN DEFINED*//)
C 
C  SMALL SIGNAL TRANSFER FUNCTION 
C 
  500 IF (KINEL.EQ.0) GO TO 800
      IF (NOPRNT.EQ.1) GO TO 505
      WRITE (6,501) KOVAR,KINEL
  501 FORMAT (///1X,5H**** ,*TRANSFER FUNCTION*//5X,*OUTPUT*,5X,*INPUT*
     2   //5X,R7,4X,R7)
  505 CALL IDEN(KINEL,ID,ITEMP)
      IF (ID.EQ.4) GO TO 510
      IF (ID.EQ.6) GO TO 510
      GO TO 550
  510 KINEL=ITEMP 
      IKNT=0
  520 IKNT=IKNT+1 
      IF (IKNT.GT.IONUM) GO TO 550
      IF (KOVAR.NE.IONAM(IKNT)) GO TO 520 
      IF (IOFLG(IKNT).EQ.0) GO TO 550 
      KOVAR=IKNT
      GO TO 800
  550 KINEL=0 
      WRITE (6,551)
  551 FORMAT (//5X,*----------  TRANSFER FUNCTION INPUT SOURCE AND/OR *
     1   *OUTPUT VARIABLE HAVE NOT BEEN DEFINED CORRECTLY*//) 
C 
C  CHECK AC ANALYSIS REQUESTS 
C 
  800 IF (JACFLG.EQ.0) GO TO 1200 
      JACFLG=FINCR
      IF (NOPRNT.EQ.1) GO TO 815
      NAM=IBLNK
      IF (IDFREQ.EQ.1) NAM=ILLIN
      IF (IDFREQ.EQ.2) NAM=ILDEC
      IF (IDFREQ.EQ.3) NAM=ILOCT
      WRITE (6,811) FSTART,FSTOP,NAM,JACFLG
  811 FORMAT (///1X,5H**** ,*FREQUENCY VARIATION*//5X,*FIRST FREQ*,5X,
     1   *LAST FREQ*,5X,*VARIATION*,5X,*POINTS*//5X,1PE9.2,6X,E9.2,7X,
     2   A8,3X,I5)
  815 IF (IDFREQ.EQ.0) GO TO 860
      IF (FSTART.LE.0.0) GO TO 860
      IF (FSTOP.LE.0.0) GO TO 860 
      IF (FSTART.GT.FSTOP) GO TO 860
      IF (IDFREQ.GT.1) GO TO 840
      IF (JACFLG.LT.2) JACFLG=2
      IF (JACFLG.LT.102) GO TO 830
      JACFLG=101
      WRITE (6,821) JACFLG
  821 FORMAT (//5X,*---  WARNING  ---  NUMBER OF AC FREQUENCY POINTS *
     1   *REDUCED TO *,I5)
  830 FINCR=(FSTOP-FSTART)/FLOAT(JACFLG-1)
      GO TO 900
  840 FACTOR=XLOG10
      IF (IDFREQ.EQ.3) FACTOR=XLOG2
      TEMP=ALOG(FSTOP/FSTART)/FACTOR
      JACFLG=IFIX(TEMP*JACFLG+0.5)+1
      IF (JACFLG.LT.2) JACFLG=2
      IF (JACFLG.LT.102) GO TO 850
      JACFLG=101
      NUMPTS=101/IFIX(TEMP+0.5)
      WRITE (6,821) NUMPTS
  850 FINCR=EXP(FACTOR*TEMP/FLOAT(JACFLG-1))
      GO TO 900
  860 JACFLG=0
      WRITE (6,861)
  861 FORMAT (//5X,*----------  AC FREQUENCY VARIATION HAS NOT BEEN * 
     1   *DEFINED CORRECTLY*//)
C 
C  NOISE ANALYSIS 
C 
  900 IF (NOSOUT.NE.0) GO TO 910
      INOISE=0
      GO TO 1000
  910 IF (NOPRNT.EQ.1) GO TO 915
      WRITE (6,911) NOSOUT,NOSIN,NOSPRT
  911 FORMAT (///1X,5H**** ,*NOISE ANALYSIS*//5X,*OUTPUT*,5X,*INPUT*, 
     1   5X,*SUMMARY INTERVAL*//5X,R7,4X,R7,5X,I5)
  915 CALL IDEN (NOSIN,ID,ITEMP)
      IF (ID.EQ.4) GO TO 920
      IF (ID.EQ.6) GO TO 920
      GO TO 970
  920 NOSIN=ITEMP 
      IKNT=0
  930 IKNT=IKNT+1 
      IF (IKNT.GT.IONUM) GO TO 970
      IF (NOSOUT.NE.IONAM(IKNT)) GO TO 930
      IF (IOFLG(IKNT).NE.1) GO TO 970 
      NOSOUT=IKNT 
      IF (NOSPRT.GT.JACFLG) NOSPRT=JACFLG 
      IKNT=NOSPRT 
      IF (INOISE.EQ.0) GO TO 965
      IKNT=IKNT+IACPRT(INOISE,1)+IACPRT(INOISE,2)+IACPLT(INOISE,1)
     1   +IACPLT(INOISE,2)
  965 IF (IKNT.GT.0) GO TO 1000
  970 NOSOUT=0
      NOSPRT=0
      INOISE=0
      WRITE (6,971)
  971 FORMAT (//5X,*----------  NOISE ANALYSIS INPUT SOURCE AND/OR *
     1   *OUTPUT VARIABLE HAVE NOT BEEN DEFINED CORRECTLY*//) 
 1000 IF ((NUMOR(3)+NOSPRT).GT.0) GO TO 1200
      JACFLG=0
      WRITE (6,1001)
 1001 FORMAT (//5X,*----------  SMALL SIGNAL ANALYSIS REQUESTED BUT NO *
     1   *AC OUTPUTS HAVE BEEN DEFINED*//)
C 
C  CHECK TRANSIENT ANALYSIS REQUESTS
C 
 1200 IF (JTRFLG.EQ.0) GO TO 1400 
      IF (NOPRNT.EQ.1) GO TO 1240 
      WRITE (6,1211) TSTEP,TSTOP,TSTART,(STEPS(I),ENDPTS(I),I=1,NOTINT)
 1211 FORMAT (///1X,5H**** ,*TRANSIENT TIME VARIATION*//5X,
     1   *TIME INCREMENT*,5X,*LAST TIME*,5X,*START TIME*//7X,1PE9.2,
     2   8X,E9.2,6X,E9.2//5X,*INTERVALS*,5(5X,*DELTA*,3X,*END POINT*)//
     3   14X,5(3X,E9.2,1X,E9.2))
C 
C  FOURIER ANALYSIS
C 
 1240 IF (KFROUT.EQ.0) GO TO 1350 
      IF (NOPRNT.EQ.1) GO TO 1245 
      WRITE (6,1241) KFROUT,FORFRE
 1241 FORMAT (///1X,5H**** ,*FOURIER ANALYSIS*//5X,*OUTPUT*,5X,
     1   *FUNDAMENTAL*//5X,R7,5X,1PE9.2)
 1245 IKNT=0
 1250 IKNT=IKNT+1 
      IF (IKNT.GT.IONUM) GO TO 1310
      IF (KFROUT.NE.IONAM(IKNT)) GO TO 1250
      IF (IOFLG(IKNT).EQ.0) GO TO 1310
      JKNT=0
 1260 JKNT=JKNT+1 
      IF (JKNT.GT.NUMOR(2)) GO TO 1270
      IF (IOVAR(JKNT,2).NE.IKNT) GO TO 1260
      GO TO 1280
 1270 NUMOR(2)=JKNT
      IOVAR(JKNT,2)=IKNT
 1280 KFROUT=JKNT 
      IF (FORFRE.EQ.0.0) GO TO 1300
      KFPTS=IFIX(1.0/(FORFRE*TSTEP)+0.5)
      IF (KFPTS.LT.10) GO TO 1300 
      IF (KFPTS.GE.JTRFLG) GO TO 1300 
      GO TO 1350
 1300 KFROUT=0
      WRITE (6,1301)
 1301 FORMAT (//5X,*----------  FOURIER ANALYSIS FREQUENCY IS *
     1   *INCOMPATIBLE WITH TRANSIENT TIME VARIATION*//)
      GO TO 1350
 1310 KFROUT=0
      WRITE (6,1311)
 1311 FORMAT (//5X,*----------  FOURIER ANALYSIS OUTPUT HAS NOT BEEN *
     1   *DEFINED CORRECTLY*//)
 1350 IF (NUMOR(2).GT.0) GO TO 1400
      JTRFLG=0
      WRITE (6,1351)
 1351 FORMAT (//5X,*----------  TRANSIENT ANALYSIS REQUESTED BUT NO * 
     1   *TRANSIENT OUTPUTS HAVE BEEN DEFINED*//) 
C 
C  CHECK SENSITIVITY REQUESTS 
C 
 1400 IF (NSENS.EQ.0) GO TO 1600
      IF (NOPRNT.EQ.1) GO TO 1405 
      WRITE (6,1401) (KSNOUT(I),I=1,NSENS)
 1401 FORMAT (///1X,5H**** ,*SENSITIVITY OUTPUTS*//10(2X,R7)) 
      IFLAG=0 
 1405 IKNT=0
 1410 IKNT=IKNT+1 
      IF (IKNT.GT.NSENS) GO TO 1440
      JKNT=0
 1420 JKNT=JKNT+1 
      IF (JKNT.GT.IONUM) GO TO 1430
      IF (KSNOUT(IKNT).NE.IONAM(JKNT)) GO TO 1420 
      KSNOUT(IKNT)=JKNT
      GO TO 1410
 1430 IFLAG=1 
      WRITE (6,1431) KSNOUT(IKNT) 
 1431 FORMAT (//5X,*----------  SENSITIVITY OUTPUT *,R7,* HAS NOT *
     1   *BEEN CORRECTLY DEFINED*//)
      GO TO 1410
 1440 IF (IFLAG.EQ.0) GO TO 1600
      NSENS=0 
C 
C  CHECK DESIGN PARAMETER SPECIFICATIONS
C 
 1600 IF (JDSFLG.EQ.0) GO TO 3000 
      IF (NOPRNT.EQ.1) GO TO 1620 
      WRITE (6,1611) (NOELEM(I),I=1,NDES) 
 1611 FORMAT (///1X,5H**** ,*DC DESIGN PARAMETERS*//(10(2X,R7)/)) 
 1620 IKNT=0
 1630 IKNT=IKNT+1 
      IF (IKNT.GT.NDES) GO TO 1800
      CALL IDEN(NOELEM(IKNT),ID,ITEMP)
      IF (ID.EQ.0) GO TO 1660 
      NOELEM(IKNT)=ITEMP
      NELTPE(IKNT)=ID 
      GO TO 1630
 1660 JDSFLG=0
      NOGO=1
      WRITE (6,1661) NOELEM(IKNT) 
 1661 FORMAT (//5X,*----------  DESIGN PARAMETER *,R7,* HAS NOT BEEN *
     1   *CORRECTLY DEFINED*//)
      GO TO 1630
C 
C  CHECK DESIGN OUTPUT SPECIFICATIONS 
C 
 1800 IF (NSPEC.EQ.0) GO TO 1870
      IF (NOPRNT.EQ.1) GO TO 1810 
      WRITE (6,1801) (NOUNUM(I),DESVAL(I),WEIGHT(I),I=1,NSPEC)
 1801 FORMAT (///1X,5H**** ,*DC DESIGN OUTPUT SPECIFICATIONS*//1X,
     1  *NAME*,13X,*VALUE*,10X,*WEIGHT*//10(1X,R7,6X,1PE10.3,6X,E10.3/))
 1810 IKNT=0
 1820 IKNT=IKNT+1 
      IF (IKNT.GT.NSPEC) GO TO 3000
      JKNT=0
 1830 JKNT=JKNT+1 
      IF (JKNT.GT.IONUM) GO TO 1860
      IF (NOUNUM(IKNT).NE.IONAM(JKNT)) GO TO 1830 
      NOUNUM(IKNT)=JKNT
      GO TO 1820
 1860 JDSFLG=0
      NOGO=1
      WRITE (6,1861) NOUNUM(IKNT) 
 1861 FORMAT (//5X,*----------  DESIGN OUTPUT VARIABLE *,R7,* HAS NOT *
     1   *BEEN DEFINED CORRECTLY*//)
      GO TO 1820
 1870 JDSFLG=0
      NOGO=1
      WRITE (6,1871)
 1871 FORMAT (//5X,*----------  NO DESIGN OUTPUTS SPECIFIED*//)
C 
C  CHECK TEMP COEFFICIENTS
C 
 3000 IF (NOPRNT.EQ.1) RETURN 
      ISTART=1
      IF (NUMTEM.GT.1) ISTART=2
      WRITE (6,3001) (TEMPS(I),I=ISTART,NUMTEM)
 3001 FORMAT (//1X,5H**** ,*CIRCUIT TEMPERATURES (DEG C)*,5(5X,F8.2)//)
      RETURN
      END 
      SUBROUTINE IDEN (KELNAM,KID,KELNO)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21), 
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
C 
C 
      DIMENSION ID1(10)
      DATA ID1 /22B,3B,14B,11B,0,26B,21B,4B,12B,15B/
      DATA ISHIFT /1000000000000B/
C 
C 
      ITEMP=KELNAM/ISHIFT 
      KID=0
   10 KID=KID+1
      IF (KID.GT.10) GO TO 50 
      IF (ITEMP.NE.ID1(KID)) GO TO 10 
   20 IKNT=LOCATE(KID)-1
      ISTOP=LOCATE(KID+1)-1
   30 IKNT=IKNT+1 
      IF (IKNT.GT.ISTOP) GO TO 60 
      IF (NAME(IKNT).NE.KELNAM) GO TO 30
      KELNO=IKNT
   40 IKNT=IKNT+1 
      IF (IKNT.GT.ISTOP) RETURN
      IF (NAME(IKNT).NE.KELNAM) GO TO 40
   50 KID=0
      RETURN
   60 IF (KID.NE.4) GO TO 50
      KID=5
      GO TO 20
      END 
      SUBROUTINE SETUP
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401), 
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21), 
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
C 
C 
      DIMENSION JMNODE(1) 
      EQUIVALENCE (JMNODE(1),VN(1))
      DIMENSION ITROW(1)
      EQUIVALENCE (ITROW(1),YNL(1))
C 
C  INITIALIZE, ORDER GROUND NODE LAST 
C 
      CALL SECOND(T1) 
      IGOOF=0 
      DO 10 I=1,NUMNOD
      IUR(I)=0
   10 JMNODE(I)=0 
      JMNODE(1)=NUMNOD
      IORDER(NUMNOD)=1
      NEXNOD=NUMNOD-1 
C 
C  DETERMINE ORDER IN WHICH SOURCES (AND INDUCTORS IN DC ANALYSIS)
C  MUST BE LOADED 
C 
      NUMVS=JELCNT(6) 
      IF (MODE.GT.1) GO TO 20 
      NUMVS=NUMVS+JELCNT(3)
   20 IF (NUMVS.EQ.0) GO TO 150
C 
C  IUR(I) TEMPORARILY CONTAINS THE NUMBER OF SOURCES CONNECTED TO 
C  NODE I 
C 
      ID=6
   30 ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 50
      DO 40 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      IUR(NODE1)=IUR(NODE1)+1 
      IUR(NODE2)=IUR(NODE2)+1 
      NODPLC(LOC+2)=0 
   40 CONTINUE
   50 IF (MODE.GT.1) GO TO 60 
      ID=ID-3 
      IF (ID.GT.0) GO TO 30
C 
C  SEARCH THROUGH SOURCES AND LOAD THOSE WITH A NODE THAT HAS NO
C  OTHER SOURCES CONNECTED
C 
   60 ILOAD=1 
   70 IFLAG=0 
      ID=6
   80 ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 130
      DO 120 I=ISTART,ISTOP
      LOC=LOCAL(I)
      IF (NODPLC(LOC+2).NE.0) GO TO 120
      NODE1=NODPLC(LOC)
      ITRY=0
   90 IF (NODE1.EQ.1) GO TO 110
      IF (JMNODE(NODE1).GT.0) GO TO 110
      IF (IUR(NODE1).GT.1) GO TO 110
      JMNODE(NODE1)=NEXNOD
      IORDER(NEXNOD)=NODE1
      NEXNOD=NEXNOD-1 
      NODPLC(LOC+2)=1 
      IF (ITRY.EQ.0) GO TO 100
      NODPLC(LOC+2)=-1
      NODPLC(LOC+1)=NODPLC(LOC)
      NODPLC(LOC)=NODE1
  100 NODE2=NODPLC(LOC+1) 
C 
C  AFTER A SOURCE HAS BEEN LOADED, IT IS DELETED FROM THE IUR COUNTER 
C 
      IUR(NODE1)=IUR(NODE1)-1 
      IUR(NODE2)=IUR(NODE2)-1 
      IFLAG=1 
      MATLOC(ILOAD+800)=I 
      ILOAD=ILOAD+1
      IF (ILOAD.GT.NUMVS) GO TO 150
      GO TO 120
  110 IF (ITRY.EQ.1) GO TO 120
      ITRY=1
      NODE1=NODPLC(LOC+1) 
      GO TO 90
  120 CONTINUE
C 
C  IN DC ANALYSIS, CYCLE THROUGH INDUCTORS ALSO
C 
  130 IF (MODE.GT.1) GO TO 140
      ID=ID-3 
      IF (ID.GT.0) GO TO 80
C 
C  IF NO ELEMENT WAS LOADED ON ABOVE SEARCH, THE CIRCUIT CONTAINS A
C  LOOP OF SOURCES AND/OR INDUCTORS
C 
  140 IF (IFLAG.EQ.1) GO TO 70
      NOGO=1
      WRITE (6,141)
  141 FORMAT (//5X,*----------  INPUT DATA CONTAINS A LOOP OF VOLTAGE *
     1  *SOURCES AND/OR INDUCTORS*//) 
      GO TO 1100
C 
C  ORDER REMAINING NODES IN THE CIRCUIT ARBITRARILY
C 
  150 NEXNOD=1
      DO 160 I=1,NUMNOD
  160 IUR(I)=1
      IUR(NUMNOD+1)=1 
      DO 170 I=2,NUMNOD
      IF (JMNODE(I).NE.0) GO TO 170
      JMNODE(I)=NEXNOD
      IORDER(NEXNOD)=I
      NEXNOD=NEXNOD+1 
  170 CONTINUE
      NSTOP=NEXNOD-1
C 
C  RESERVE MATRIX LOCATIONS IN IUR,IUC
C 
C  RESISTORS, CAPACITORS, INDUCTORS
C 
      ISTOP=LOCATE(4)-1
      IF (MODE.NE.1) GO TO 210
      ISTOP=LOCATE(3)-1
  210 IF (ISTOP.LT.1) GO TO 230
      DO 220 I=1,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      CALL RESERV(NODE1,NODE2)
  220 CONTINUE
C 
C  VOLTAGE CONTROLLED CURRENT SOURCES 
C 
  230 ISTART=LOCATE(5)
      ISTOP=LOCATE(6)-1
      IF (ISTART.GT.ISTOP) GO TO 250
      DO 240 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      NODE4=NODPLC(LOC+3) 
      CALL RESERV(NODE1,NODE3)
      CALL RESERV(NODE1,NODE4)
      CALL RESERV(NODE2,NODE3)
      CALL RESERV(NODE2,NODE4)
  240 CONTINUE
C 
C  TRANSISTORS
C 
  250 ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) GO TO 270
      DO 260 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      NODE4=NODPLC(LOC+3) 
      NODE5=NODPLC(LOC+4) 
      NODE6=NODPLC(LOC+5) 
      CALL RESERV(NODE1,NODE4)
      CALL RESERV(NODE2,NODE5)
      CALL RESERV(NODE3,NODE6)
      CALL RESERV(NODE4,NODE5)
      CALL RESERV(NODE4,NODE6)
      CALL RESERV(NODE5,NODE6)
  260 CONTINUE
C 
C  DIODES 
C 
  270 ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) GO TO 290
      DO 280 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      CALL RESERV(NODE1,NODE3)
      CALL RESERV(NODE2,NODE3)
  280 CONTINUE
C 
C  JFETS
C 
  290 ISTART=LOCATE(9)
      ISTOP=LOCATE(10)-1
      IF (ISTART.GT.ISTOP) GO TO 310
      DO 300 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      NODE4=NODPLC(LOC+3) 
      NODE5=NODPLC(LOC+4) 
      CALL RESERV(NODE1,NODE4)
      CALL RESERV(NODE2,NODE4)
      CALL RESERV(NODE2,NODE5)
      CALL RESERV(NODE3,NODE5)
      CALL RESERV(NODE4,NODE5)
  300 CONTINUE
C 
C  MOSFETS
C 
  310 ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) GO TO 350
      DO 320 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      NODE4=NODPLC(LOC+3) 
      NODE5=NODPLC(LOC+4) 
      NODE6=NODPLC(LOC+5) 
      CALL RESERV(NODE1,NODE5)
      CALL RESERV(NODE2,NODE4)
      CALL RESERV(NODE2,NODE5)
      CALL RESERV(NODE2,NODE6)
      CALL RESERV(NODE3,NODE6)
      CALL RESERV(NODE4,NODE5)
      CALL RESERV(NODE4,NODE6)
      CALL RESERV(NODE5,NODE6)
  320 CONTINUE
C 
C  SOURCE TERMS ... IF SOURCE HAS POSITIVE NODE N+ AND NEGATIVE NODE
C  N-, THEN FOR EVERY (N+,I) TERM (WITH I LT N+) THAT EXISTS, RESERVE 
C  A (N-,I) TERM
C 
  350 IF (NUMVS.EQ.0) GO TO 400
      DO 380 I=1,NUMVS
      IELNUM=MATLOC(I+800)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      IF (NODE2.EQ.1) GO TO 380
      JSTART=IUR(NODE1)
      JSTOP=IUR(NODE1+1)-1
      IF (JSTART.GT.JSTOP) GO TO 380
      NTERMS=0
      DO 360 J=JSTART,JSTOP
      NTERMS=NTERMS+1 
      ITROW(NTERMS)=IUC(J)
  360 CONTINUE
      DO 370 J=1,NTERMS
      CALL RESERV(NODE2,ITROW(J)) 
  370 CONTINUE
  380 CONTINUE
C 
C  CALL POINT TO REORDER NODES FOR MINIMAL FILLIN 
C 
  400 IF (IGOOF.EQ.1) GO TO 1000
      NTTB=IUR(NUMNOD+1)-1
      CALL POINT
      IF (IGOOF.EQ.1) GO TO 1000
C 
C  STORE MATRIX LOCATIONS FOR EACH DEVICE 
C 
      IFIND=NUMVS+2*(JELCNT(1)+JELCNT(2)+JELCNT(3))+4*JELCNT(5)
     1  +12*JELCNT(7)+4*JELCNT(8)+10*JELCNT(9)+16*JELCNT(10)
      IF (IFIND.GT.1800) GO TO 1000
      IF (NUMVS.EQ.0) GO TO 410
      DO 405 I=1,NUMVS
  405 MATLOC(I)=MATLOC(I+800) 
  410 IFIND=NUMVS 
C 
C  RESISTORS, CAPACITORS, INDUCTORS
C 
      ISTOP=LOCATE(4)-1
      IF (MODE.NE.1) GO TO 420
      ISTOP=LOCATE(3)-1
  420 IF (ISTOP.LT.1) GO TO 440
      DO 430 I=1,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      MATLOC(IFIND+1)=INDEX(NODE1,NODE2)
      MATLOC(IFIND+2)=INDEX(NODE2,NODE1)
      IFIND=IFIND+2
  430 CONTINUE
C 
C  VOLTAGE DEPENDENT CURRENT SOURCES
C 
  440 ISTART=LOCATE(5)
      ISTOP=LOCATE(6)-1
      IF (ISTART.GT.ISTOP) GO TO 460
      DO 450 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      NODE4=NODPLC(LOC+3) 
      MATLOC(IFIND+1)=INDEX(NODE1,NODE3)
      MATLOC(IFIND+2)=INDEX(NODE1,NODE4)
      MATLOC(IFIND+3)=INDEX(NODE2,NODE3)
      MATLOC(IFIND+4)=INDEX(NODE2,NODE4)
      IFIND=IFIND+4
  450 CONTINUE
C 
C  TRANSISTORS
C 
  460 ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) GO TO 480
      DO 470 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      NODE4=NODPLC(LOC+3) 
      NODE5=NODPLC(LOC+4) 
      NODE6=NODPLC(LOC+5) 
      MATLOC(IFIND+1)=INDEX(NODE1,NODE4)
      MATLOC(IFIND+2)=INDEX(NODE2,NODE5)
      MATLOC(IFIND+3)=INDEX(NODE3,NODE6)
      MATLOC(IFIND+4)=INDEX(NODE4,NODE1)
      MATLOC(IFIND+5)=INDEX(NODE4,NODE5)
      MATLOC(IFIND+6)=INDEX(NODE4,NODE6)
      MATLOC(IFIND+7)=INDEX(NODE5,NODE2)
      MATLOC(IFIND+8)=INDEX(NODE5,NODE4)
      MATLOC(IFIND+9)=INDEX(NODE5,NODE6)
      MATLOC(IFIND+10)=INDEX(NODE6,NODE3) 
      MATLOC(IFIND+11)=INDEX(NODE6,NODE4) 
      MATLOC(IFIND+12)=INDEX(NODE6,NODE5) 
      IFIND=IFIND+12
  470 CONTINUE
C 
C  DIODES 
C 
  480 ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) GO TO 500
      DO 490 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      MATLOC(IFIND+1)=INDEX(NODE1,NODE3)
      MATLOC(IFIND+2)=INDEX(NODE2,NODE3)
      MATLOC(IFIND+3)=INDEX(NODE3,NODE1)
      MATLOC(IFIND+4)=INDEX(NODE3,NODE2)
      IFIND=IFIND+4
  490 CONTINUE
C 
C  JFETS
C 
  500 ISTART=LOCATE(9)
      ISTOP=LOCATE(10)-1
      IF (ISTART.GT.ISTOP) GO TO 520
      DO 510 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      NODE4=NODPLC(LOC+3) 
      NODE5=NODPLC(LOC+4) 
      MATLOC(IFIND+1)=INDEX(NODE1,NODE4)
      MATLOC(IFIND+2)=INDEX(NODE2,NODE4)
      MATLOC(IFIND+3)=INDEX(NODE2,NODE5)
      MATLOC(IFIND+4)=INDEX(NODE3,NODE5)
      MATLOC(IFIND+5)=INDEX(NODE4,NODE1)
      MATLOC(IFIND+6)=INDEX(NODE4,NODE2)
      MATLOC(IFIND+7)=INDEX(NODE4,NODE5)
      MATLOC(IFIND+8)=INDEX(NODE5,NODE2)
      MATLOC(IFIND+9)=INDEX(NODE5,NODE3)
      MATLOC(IFIND+10)=INDEX(NODE5,NODE4) 
      IFIND=IFIND+10
  510 CONTINUE
C 
C  MOSFETS
C 
  520 ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) GO TO 550
      DO 530 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      NODE4=NODPLC(LOC+3) 
      NODE5=NODPLC(LOC+4) 
      NODE6=NODPLC(LOC+5) 
      MATLOC(IFIND+1)=INDEX(NODE1,NODE5)
      MATLOC(IFIND+2)=INDEX(NODE2,NODE4)
      MATLOC(IFIND+3)=INDEX(NODE2,NODE5)
      MATLOC(IFIND+4)=INDEX(NODE2,NODE6)
      MATLOC(IFIND+5)=INDEX(NODE3,NODE6)
      MATLOC(IFIND+6)=INDEX(NODE4,NODE2)
      MATLOC(IFIND+7)=INDEX(NODE4,NODE5)
      MATLOC(IFIND+8)=INDEX(NODE4,NODE6)
      MATLOC(IFIND+9)=INDEX(NODE5,NODE1)
      MATLOC(IFIND+10)=INDEX(NODE5,NODE2) 
      MATLOC(IFIND+11)=INDEX(NODE5,NODE4) 
      MATLOC(IFIND+12)=INDEX(NODE5,NODE6) 
      MATLOC(IFIND+13)=INDEX(NODE6,NODE2) 
      MATLOC(IFIND+14)=INDEX(NODE6,NODE3) 
      MATLOC(IFIND+15)=INDEX(NODE6,NODE4) 
      MATLOC(IFIND+16)=INDEX(NODE6,NODE5) 
      IFIND=IFIND+16
  530 CONTINUE
C 
C  VOLTAGE SOURCES (AND INDUCTORS IN DC ANALYSIS) 
C 
  550 IF (NUMVS.EQ.0) GO TO 600
      DO 560 I=1,NUMVS
      IELNUM=MATLOC(I)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODPLC(LOC+3)=JMNODE(NODE1) 
      NODPLC(LOC+4)=JMNODE(NODE2) 
  560 CONTINUE
C 
C  FINISHED
C 
  600 LASTUT=IUS+IUR(NUMNOD+1)-2
      LASTLT=LASTUT+MIRROR
      IF (IACCT.EQ.0) GO TO 1100
      NUTAR=IUR(NSTOP+1)-1
      NSTERM=IUR(NUMNOD+1)-1-NUTAR
      NUTBR=(NTTB-NSTERM)/2
      IFILL=NUTAR-NUTBR
      NTTERM=NUMNOD+2*(NUTAR+NSTERM)-1
      NNODE=NUMNOD-1
      PERSPA=100.0*(1.0-FLOAT(NTTERM)/FLOAT(NNODE*NNODE)) 
      IOPS=0
      IF (NSTOP.EQ.0) GO TO 620
      DO 610 I=1,NSTOP
      NOPS=IUR(I+1)-IUR(I)
      IOPS=IOPS+NOPS*NOPS 
  610 CONTINUE
  620 WRITE (6,621) NNODE,NSTOP,NUTBR,NUTAR,IFILL,NSTERM,NTTERM,IOPS, 
     1  PERSPA
  621 FORMAT (//5X,*MATRIX STATISTICS*//6X,*NODES*,3X,*NSTOP*,3X, 
     1   *NUTBR*,3X,*NUTAR*,3X,*IFILL*,2X,*NSTERM*,2X,*NTTERM*,3X,
     2  *IOPS*,4X,*PERSPA*//6X,8(I4,4X),F6.3) 
      GO TO 1100
 1000 NOGO=1
      WRITE (6,1001) IUR(NUMNOD+1),IFIND
 1001 FORMAT (1H1,4X,*----------  PROGRAM ERROR ... SETUP*//
     1   5X,*ARRAY SIZE EXCEEDED ...  IUTOT = *,I4,3X,*IFIND = *,I4)
 1100 CALL SECOND(T2) 
      RTIMES(2)=RTIMES(2)+T2-T1
      RETURN
      END 
      SUBROUTINE RESERV (NODE1,NODE2) 
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401), 
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21), 
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
C 
C 
      DIMENSION JMNODE(1) 
      EQUIVALENCE (JMNODE(1),VN(1))
C 
C 
      IF (NODE1.EQ.1) RETURN
      IF (NODE2.EQ.1) RETURN
      IF (NODE1.EQ.NODE2) RETURN
      IF (JMNODE(NODE1).GT.JMNODE(NODE2)) GO TO 10
      N1=NODE2
      N2=NODE1
      GO TO 20
   10 N1=NODE1
      N2=NODE2
C 
C  LOOKUP LOWER TRIANGLE (N1,N2) TERM 
C 
   20 II=IUR(N1)
      ISTOP=IUR(N1+1)-1
   30 IF (II.GT.ISTOP) GO TO 40
      IF (IUC(II).EQ.N2) RETURN
      II=II+1 
      GO TO 30
C 
C  LOWER TRIANGLE TERM DOES NOT EXIST, RESERVE IT 
C 
   40 JJ=IUR(NUMNOD+1)
      IF (JJ.GT.1600) GO TO 1000
   50 IF (JJ.LE.II) GO TO 60
      IUC(JJ)=IUC(JJ-1)
      JJ=JJ-1 
      GO TO 50
   60 IUC(II)=N2
      JJ=N1+1 
      JSTOP=NUMNOD+1
   70 IF (JJ.GT.JSTOP) GO TO 100
      IUR(JJ)=IUR(JJ)+1
      JJ=JJ+1 
      GO TO 70
C 
C  RESERVE UPPER TRIANGLE (N2,N1) TERM ONLY IF N1 IS NOT A SOURCE NODE
C 
  100 IF (JMNODE(N1).GT.NSTOP) RETURN 
      II=IUR(N2+1)
      JJ=IUR(NUMNOD+1)
      IF (JJ.GT.1600) GO TO 1000
  110 IF (JJ.LE.II) GO TO 120 
      IUC(JJ)=IUC(JJ-1)
      JJ=JJ-1 
      GO TO 110
  120 IUC(II)=N1
      JJ=N2+1 
  130 IF (JJ.GT.JSTOP) RETURN 
      IUR(JJ)=IUR(JJ)+1
      JJ=JJ+1 
      GO TO 130
 1000 IGOOF=1 
      RETURN
      END 
      FUNCTION INDEX(NODE1,NODE2) 
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401), 
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
C 
C 
      DIMENSION JMNODE(1) 
      EQUIVALENCE (JMNODE(1),VN(1))
C 
C  IN THE FINAL POINTER SYSTEM, ONLY THE UPPER TRIANGLE TERMS ... 
C  (N1,N2) WITH N2 GT N1 ... ARE RETAINED IN THE IUC POINTERS, EXCEPT 
C  FOR SOURCE NODES, WHERE ONLY THE LOWER TRIANGLE TERMS ... (N2,N1)
C  WITH N2 GT N1 ... ARE RETAINED 
C 
      IF (NODE1.EQ.1) GO TO 100
      IF (NODE2.EQ.1) GO TO 100
      N1=JMNODE(NODE1)
      N2=JMNODE(NODE2)
      IF (N1-N2) 30,10,20 
   10 INDEX=NODE1 
      RETURN
   20 ISPOT=ILS-1 
      IF (N1-NSTOP) 40,40,50
   30 ISPOT=IUS-1 
      IF (N2-NSTOP) 50,50,40
   40 ITEMP=N1
      N1=N2
      N2=ITEMP
   50 ISTART=IUR(N1)
      ISTOP=IUR(N1+1)-1
      IF (ISTART.GT.ISTOP) GO TO 100
      DO 60 I=ISTART,ISTOP
      IF (IUC(I).NE.N2) GO TO 60
      INDEX=ISPOT+I
      RETURN
   60 CONTINUE
  100 INDEX=1 
      RETURN
      END 
      SUBROUTINE POINT
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401), 
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21), 
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
C 
C 
      DIMENSION JMNODE(1),NUMOFF(1),NOTOLO(1),IURTMP(1),IUCTMP(1),
     1   ITROW(1),ITCOL(1)
      EQUIVALENCE (JMNODE(1),VN(1))
      EQUIVALENCE (NUMOFF(1),IURTMP(1),YNL(1))
      EQUIVALENCE (NOTOLO(1),IUCTMP(1),YNL(402))
      EQUIVALENCE (ITROW(1),YNL(802)) 
      EQUIVALENCE (ITCOL(1),YNL(1002))
C 
C 
      IGOOF=0 
      NEXNOD=1
      IF (NSTOP.LT.2) GO TO 300
C 
C  COMPUTE NUMBER OF OFF DIAGONAL TERMS FOR EACH ROW AND CONSTRUCT
C  LIST OF ROWS WITH ONE OFF DIAGONAL TERM
C 
      NTERMS=0
      DO 20 I=1,NUMNOD
      NUMOFF(I)=IUR(I+1)-IUR(I)
      IF (JMNODE(I).GT.NSTOP) GO TO 20
      IF (NUMOFF(I).GT.1) GO TO 20
      NTERMS=NTERMS+1 
      NOTOLO(NTERMS)=I
   20 CONTINUE
C 
C  LOAD ROWS CONTAINED IN NOTOLO ARRAY.  A ROW IS LOADED BY SWAPPING
C  ITS IORDER AND JMNODE ENTRIES WITH THE ENTRIES CORRESPONDING TO
C  ROW NEXNOD 
C 
   50 ITERM=0 
   60 ITERM=ITERM+1
      IF (ITERM.GT.NTERMS) GO TO 100
      LOAD=NOTOLO(ITERM)
      NODE=IORDER(NEXNOD) 
      IROW=JMNODE(LOAD)
      JMNODE(NODE)=IROW
      JMNODE(LOAD)=NEXNOD 
      IORDER(IROW)=NODE
      IORDER(NEXNOD)=LOAD 
      NEXNOD=NEXNOD+1 
      IF (NEXNOD.GE.NSTOP) GO TO 300
C 
C  REDUCE THE NUMBER OF OFF DIAGONAL TERMS BY ONE FOR EACH COLUMN 
C  ELEMENT OF THE ROW JUST LOADED 
C 
      JSTART=IUR(LOAD)
      JSTOP=IUR(LOAD+1)-1 
      IF (JSTART.GT.JSTOP) GO TO 60
      DO 70 J=JSTART,JSTOP
      IROW=IUC(J) 
      IF (JMNODE(IROW).LT.NEXNOD) GO TO 70
      NUMOFF(IROW)=NUMOFF(IROW)-1 
C 
C  IF THIS SUBTRACTION CAUSES THE NUMBER OF OFF DIAGONAL TERMS IN IROW
C  TO BE ONE, ADD IROW TO THE NOTOLO ARRAY
C 
      IF (NUMOFF(IROW).NE.1) GO TO 70 
      NTERMS=NTERMS+1 
      NOTOLO(NTERMS)=IROW 
   70 CONTINUE
      GO TO 60
C 
C  ALL REMAINING ROWS HAVE AT LEAST TWO OFF DIAGONAL TERMS.  DETERMINE
C  THE ROW WHICH WILL CAUSE THE LEAST AMOUNT OF FILLIN.  IF TWO OR MORE
C  ROWS CAUSE THE SAME FILLIN, SELECT THE ONE WITH THE LARGEST NUMBER 
C  OF OFF DIAGONAL TERMS
C 
  100 IFILL=160000
      LOAD=IORDER(NEXNOD) 
      DO 150 I=NEXNOD,NSTOP
      IRFILL=0
      NODE=IORDER(I)
C 
C  THE FILLIN IS DETERMINED BY A MOCK DECOMPOSITION OF EACH ROW
C  ASSUMING IT IS LOADED NEXT 
C 
      JSTART=IUR(NODE)
      JSTOP=IUR(NODE+1)-1 
      DO 130 J=JSTART,JSTOP
      JO=IUC(J)
      IF (JMNODE(JO).LT.NEXNOD) GO TO 130 
      LSTART=IUR(JO)
      LSTOP=IUR(JO+1)-1
      K=J 
  110 K=K+1
      IF (K.GT.JSTOP) GO TO 130
      KO=IUC(K)
      IF (JMNODE(KO).LT.NEXNOD) GO TO 110 
C 
C  TEST FOR (JO,KO) MATRIX TERM.  IF THIS TERM DOESNT EXIST, IT WILL
C  BE CREATED IN DECOMPOSING THIS ROW 
C 
      DO 120 L=LSTART,LSTOP
      IF (IUC(L).EQ.KO) GO TO 110 
  120 CONTINUE
      IRFILL=IRFILL+1 
      IF (IRFILL.GT.IFILL) GO TO 150
      GO TO 110
  130 CONTINUE
      IF (IRFILL.LT.IFILL) GO TO 140
      IF (NUMOFF(NODE).LE.NUMOFF(LOAD)) GO TO 150 
  140 LOAD=NODE
      IFILL=IRFILL
      IF (IFILL.EQ.0) GO TO 200
  150 CONTINUE
C 
C  LOAD IS NOW THE ROW WHICH WILL CAUSE THE MINIMUM FILLIN.  RESERVE
C  THE FILLIN TERMS IN IUR AND IUC ARRAYS AND PUT LOAD IN LIST OF ROWS
C  TO BE LOADED
C 
  200 NTERMS=1
      NOTOLO(1)=LOAD
      IF (IFILL.EQ.0) GO TO 50
      IRFILL=0
      JSTART=IUR(LOAD)
      JSTOP=IUR(LOAD+1)-1 
      DO 230 J=JSTART,JSTOP
      JO=IUC(J)
      IF (JMNODE(JO).LT.NEXNOD) GO TO 230 
      LSTART=IUR(JO)
      LSTOP=IUR(JO+1)-1
      K=J 
  210 K=K+1
      IF (K.GT.JSTOP) GO TO 230
      KO=IUC(K)
      IF (JMNODE(KO).LT.NEXNOD) GO TO 210 
      DO 220 L=LSTART,LSTOP
      IF (IUC(L).EQ.KO) GO TO 210 
  220 CONTINUE
C 
C  ALL FILLIN TERMS ARE STORED TEMPORARILY TO AVOID ALTERING THE
C  POINTERS IN THE MIDDLE OF THE MOCK DECOMPOSITION
C 
      IRFILL=IRFILL+1 
      IF (IRFILL.GT.200) GO TO 1000
      ITROW(IRFILL)=JO
      ITCOL(IRFILL)=KO
      GO TO 210
  230 CONTINUE
C 
C  NOW INSERT ALL FILLIN TERMS IN THE POINTERS
C 
      DO 240 I=1,IRFILL
      JO=ITROW(I) 
      KO=ITCOL(I) 
      NUMOFF(JO)=NUMOFF(JO)+1 
      NUMOFF(KO)=NUMOFF(KO)+1 
      CALL RESERV(JO,KO)
  240 CONTINUE
      GO TO 50
C 
C  RELOAD IUR,IUC ARRAYS WITH USING THE REORDERED NODE NUMBERS.  RETAIN
C  ONLY THE UPPER TRIANGLE TERMS, EXCEPT FOR SOURCE NODES, WHERE ONLY 
C  THE LOWER TRIANGLE TERM IS RETAINED
C 
  300 IF (IGOOF.EQ.1) RETURN
      IUTOT=0 
      IF (NSTOP.EQ.0) GO TO 350
      DO 320 I=1,NSTOP
      IURTMP(I)=IUTOT+1
      NODE=IORDER(I)
      JSTART=IUR(NODE)
      JSTOP=IUR(NODE+1)-1 
      IF (JSTART.GT.JSTOP) GO TO 320
      DO 310 J=JSTART,JSTOP
      IROW=IUC(J) 
      LOAD=JMNODE(IROW)
      IF (LOAD.LT.I) GO TO 310
      IUTOT=IUTOT+1
      IUCTMP(IUTOT)=LOAD
  310 CONTINUE
  320 CONTINUE
  350 ISTART=NSTOP+1
      IF (ISTART.GT.NUMNOD) GO TO 400 
      DO 370 I=ISTART,NUMNOD
      IURTMP(I)=IUTOT+1
      NODE=IORDER(I)
      JSTART=IUR(NODE)
      JSTOP=IUR(NODE+1)-1 
      IF (JSTART.GT.JSTOP) GO TO 370
      DO 360 J=JSTART,JSTOP
      IROW=IUC(J) 
      LOAD=JMNODE(IROW)
      IUTOT=IUTOT+1
      IUCTMP(IUTOT)=LOAD
  360 CONTINUE
  370 CONTINUE
  400 IUR(NUMNOD+1)=IUTOT+1
      IF (IUTOT.GT.800) GO TO 1000
      DO 410 I=1,NUMNOD
  410 IUR(I)=IURTMP(I)
      IF (IUTOT.EQ.0) RETURN
      DO 420 I=1,IUTOT
  420 IUC(I)=IUCTMP(I)
      RETURN
 1000 IGOOF=1 
      RETURN
      END 
      SUBROUTINE ANLYZE
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401), 
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21), 
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10), 
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
      COMMON/DC/ICVFLG,ITCELM,TCSTAR,TCSTOP,TCINCR,KSSOP,KINEL,KOVAR
      COMMON/TRAN/JTRFLG,TSTEP,TSTOP,TSTART,NOTINT,STEPS(5),ENDPTS(5),
     1   KFROUT,FORFRE,KFPTS
      COMMON/AC/JACFLG,FSTART,FSTOP,IDFREQ,FINCR,INOISE,NOSPRT,
     1   NOSOUT,NOSIN 
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
      COMMON/SENVAR/NSENS,KSNOUT(10)
      COMMON/DESIGN/JDSFLG,NSPEC,NDES,ERROR,NOELEM(30),NELTPE(30),
     1   NOUNUM(10),DESVAL(10),WEIGHT(10) 
C 
C 
      DATA LPRN /1H(/ 
C 
C  IF CIRCUIT CONTAINS NO INDUCTORS, SETUP MATRIX ONCE
C 
      MODE=0
      KSETUP=0
      IF (JELCNT(3).GT.0) GO TO 5 
      MODE=1
      KSETUP=1
      CALL SETUP
      IF (NOGO.EQ.1) RETURN
C 
C  DC DESIGN
C 
    5 IF (JDSFLG.EQ.0) GO TO 20
      MODE=1
      IF (KSETUP.EQ.1) GO TO 10
      CALL SETUP
      IF (NOGO.EQ.1) RETURN
   10 CALL DSIGN
      RETURN
C 
C  CYCLE THROUGH TEMPERATURES 
C 
   20 ITEMNO=1
      IF (NUMTEM.EQ.1) GO TO 40
   30 ITEMNO=ITEMNO+1 
      IF (ITEMNO.GT.NUMTEM) RETURN
      CALL TMPUPD 
C 
C  DC TRANSFER CURVES 
C 
   40 IF (ICVFLG.EQ.0) GO TO 200
      MODE=1
      IF (KSETUP.EQ.1) GO TO 50
      CALL SETUP
      IF (NOGO.EQ.1) RETURN
   50 CALL SECOND(T1) 
      LOC1=MNAME(ITCELM)
      TEMVAL=SOURCE(LOC1) 
      SOURCE(LOC1)=TCSTAR 
      KOUNT=0 
      IPROB=0 
      ICALC=0 
   60 IGOOF=0 
      IPASS1=1
      DO 70 I=2,NUMNOD
   70 VNIM1(I)=0.0
   80 CALL ITER8
      KOUNT=KOUNT+ITERNO
      IF (IGOOF.EQ.1) GO TO 150
C 
C  STORE OUTPUTS
C 
      ICALC=ICALC+1
      FREQ(ICALC)=SOURCE(LOC1)
      IKNT=0
   90 IKNT=IKNT+1 
      IF (IKNT.GT.NUMOR(1)) GO TO 110 
      JKNT=IOVAR(IKNT,1)
      IPNOD=IOPND(JKNT)
      IF (IOFLG(JKNT).EQ.2) GO TO 100 
      INNOD=IONND(JKNT)
      ROUT(ICALC,IKNT)=VNIM1(IPNOD)-VNIM1(INNOD)
      GO TO 90
  100 CALL CALCUR(IPNOD,CREAL)
      ROUT(ICALC,IKNT)=CREAL
      GO TO 90
C 
C  LINEAR INTERPOLATION FOR MISSING TRANSFER CURVE VALUE
C 
  110 IF (IPROB.LT.2) GO TO 130
      IKNT=0
  120 IKNT=IKNT+1 
      IF (IKNT.GT.NUMOR(1)) GO TO 130 
      ROUT(ICALC-1,IKNT)=(ROUT(ICALC-2,IKNT)+ROUT(ICALC,IKNT))/2.0
      GO TO 120
C 
C  INCREMENT SOURCE VALUE 
C 
  130 IPROB=0 
      SOURCE(LOC1)=SOURCE(LOC1)+TCINCR
      IF (ICALC.LT.ICVFLG) GO TO 80
      SOURCE(LOC1)=TEMVAL 
      GO TO 190
C 
C  NONCONVERGENCE RECOVERY
C 
  150 IF (ICALC.EQ.0) GO TO 170
      IF (IPROB.GT.0) GO TO 160
      IPROB=1 
      WRITE (6,151) SOURCE(LOC1)
  151 FORMAT (//5X,*--- WARNING ---  TRANSFER CURVES RESTARTED AT *
     1   *SOURCE VALUE *,1PE9.2)
      GO TO 60
  160 IF (IPROB.GT.1) GO TO 165
      IPROB=2 
      ICALC=ICALC+1
      FREQ(ICALC)=SOURCE(LOC1)
      SOURCE(LOC1)=SOURCE(LOC1)+TCINCR
      WRITE (6,151) SOURCE(LOC1)
      GO TO 60
  165 ICALC=ICALC-1
  170 NOGO=1
      WRITE (6,171) SOURCE(LOC1)
  171 FORMAT (1H1,4X,*----------  NO CONVERGENCE ON DC TRANSFER * 
     1   *AT SOURCE VALUE *,1PE9.2//) 
      WRITE (6,176) ITERNO,IPASS1,IFINAL
  176 FORMAT (5X,*ITERNO =  *,I3,5X,*IPASS1 =  *,I3,5X,*IFINAL =  *,I3//
     1   5X,*LAST NODE VOLTAGES*//1X,5(* NODE    VOLTAGE    *)//) 
      WRITE (6,181) (LPRN,JUNODE(I),VNIM1(I),I=2,NUNODS)
  181 FORMAT (5(1X,A1,I3,1H),1X,F10.4,3X)/)
  190 CALL SECOND(T2) 
      RTIMES(3)=RTIMES(3)+T2-T1
      RTIMES(4)=RTIMES(4)+KOUNT
      CALL OVTPVT 
      IF (NOGO.EQ.1) RETURN
C 
C  SMALL SIGNAL OPERATING POINT
C 
  200 CALL SECOND(T1) 
      ITEMP=NSENS+KSSOP
      IF (ITEMP.GT.0) GO TO 220
      IF (JACFLG.EQ.0) GO TO 210
      ITEMP=JELCNT(7)+JELCNT(8)+JELCNT(9)+JELCNT(10)
      IF (ITEMP.GT.0) GO TO 220
      GO TO 240
  210 ITEMP=JTRFLG+JDSFLG+ICVFLG
      IF (ITEMP.GT.0) GO TO 300
  220 IF (MODE.EQ.1) GO TO 230
      MODE=1
      IF (KSETUP.EQ.1) GO TO 230
      CALL SETUP
      IF (NOGO.EQ.1) RETURN
  230 WRITE (6,231) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
  231 FORMAT (1H7/////1X,125(1H*)///1X,8A10,26X,*-----  SPICE  -----* 
     1   ///21X,*SMALL SIGNAL BIAS SOLUTION*,30X,*TEMPERATURE  *, 
     2   F10.3,*  DEG C*///1X,125(1H*)///)
      CALL DCOP(1)
      IF (KINEL.EQ.0) GO TO 235
      CALL DCTRFN 
  235 IF (NSENS.EQ.0) GO TO 238
      CALL SENCAL 
  238 CALL SECOND(T2) 
      RTIMES(5)=RTIMES(5)+T2-T1
      IF (NOGO.EQ.1) RETURN
C 
C  AC SMALL SIGNAL ANALYSIS
C 
      IF (JACFLG.EQ.0) GO TO 300
  240 MODE=2
      IF (KSETUP.EQ.1) GO TO 250
      CALL SETUP
      IF (NOGO.EQ.1) RETURN
  250 CALL ACAN
      CALL OVTPVT 
      IF (NOGO.EQ.1) RETURN
C 
C  TRANSIENT DC INITIAL CONDITIONS
C 
  300 IF (JTRFLG.EQ.0) GO TO 30
      IF (MODE.EQ.1) GO TO 310
      MODE=1
      IF (KSETUP.EQ.1) GO TO 310
      CALL SETUP
      IF (NOGO.EQ.1) RETURN
  310 CALL SECOND(T1) 
      DO 330 ID=4,6,2 
      ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 330
      DO 320 I=ISTART,ISTOP
      MNAM=MNAME(I)
      SOURCE(MNAM+2)=SOURCE(MNAM) 
      IF (SOURCE(MNAM+3).EQ.0.0) GO TO 320
      SOURCE(MNAM)=SOURCE(MNAM+5) 
  320 CONTINUE
  330 CONTINUE
      WRITE (6,341) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
  341 FORMAT (1H7/////1X,125(1H*)///1X,8A10,26X,*-----  SPICE  -----* 
     1   ///21X,*INITIAL TRANSIENT SOLUTION*,30X,*TEMPERATURE  *, 
     2   F10.3,*  DEG C*///1X,125(1H*)///)
      CALL DCOP(2)
      IF (NOGO.EQ.1) GO TO 440
C 
C  STORE DC INDUCTOR CURRENTS 
C 
      ISTART=LOCATE(3)
      ISTOP=LOCATE(4)-1
      IF (ISTART.GT.ISTOP) GO TO 360
      ISPOT=ICURNT(3) 
      DO 350 I=ISTART,ISTOP
      CALL CALCUR(I,CREAL)
      TRACUR(ISPOT)=0.0
      TRACUR(ISPOT+1)=CREAL
      ISPOT=ISPOT+2
  350 CONTINUE
C 
C  STORE DC CAPACITOR VOLTAGES
C 
  360 ISTART=LOCATE(2)
      ISTOP=LOCATE(3)-1
      IF (ISTART.GT.ISTOP) GO TO 380
      ISPOT=ICURNT(2) 
      DO 370 I=ISTART,ISTOP
      LOC=LOCAL(I)
      IPNOD=NODPLC(LOC)
      INNOD=NODPLC(LOC+1) 
      VCAP=VNIM1(IPNOD)-VNIM1(INNOD)
      TRACUR(ISPOT)=VCAP
      TRACUR(ISPOT+1)=0.0 
      ISPOT=ISPOT+2
  370 CONTINUE
C 
C  STORE INITIAL OUTPUTS
C 
  380 FREQ(1)=0.0 
      IKNT=0
  390 IKNT=IKNT+1 
      IF (IKNT.GT.NUMOR(2)) GO TO 410 
      JKNT=IOVAR(IKNT,2)
      IPNOD=IOPND(JKNT)
      IF (IOFLG(JKNT).EQ.2) GO TO 400 
      INNOD=IONND(JKNT)
      ROUT(1,IKNT)=VNIM1(IPNOD)-VNIM1(INNOD)
      GO TO 390
  400 CALL CALCUR(IPNOD,CREAL)
      ROUT(1,IKNT)=CREAL
      GO TO 390
C 
C  RESET SOURCE VALUES
C 
  410 DO 430 ID=4,6,2 
      ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 430
      DO 420 I=ISTART,ISTOP
      MNAM=MNAME(I)
      IF (SOURCE(MNAM+3).EQ.0.0) GO TO 420
      SOURCE(MNAM)=SOURCE(MNAM+2) 
  420 CONTINUE
  430 CONTINUE
  440 CALL SECOND(T2) 
      RTIMES(5)=RTIMES(5)+T2-T1
      IF (NOGO.EQ.1) RETURN
C 
C  TRANSIENT ANALYSIS 
C 
      MODE=3
      IF (KSETUP.EQ.1) GO TO 450
      CALL SETUP
      IF (NOGO.EQ.1) RETURN
  450 CALL TRANAN 
      CALL OVTPVT 
      IF (NOGO.EQ.1) RETURN
      GO TO 30
      END 
      SUBROUTINE TMPUPD
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
C 
C 
      TEMPD=TEMPS(ITEMNO)+273.0
      VT=BOLTZ*TEMPD/CHARGE
      IF (NUMMOD.EQ.0) RETURN 
      RATIO=TEMPD/(TEMPS(ITEMNO-1)+273.0) 
      RATIO2=RATIO*RATIO
      RATIO3=RATIO2*RATIO 
      XNUM=-(1.0-RATIO)/VT
      DO 100 I=1,NUMMOD
      INUM=KIND(I)
      IF (INUM.EQ.0) GO TO 100
      GO TO (10,20,30,40,50,60),INUM
C 
C  EBERS-MOLL TRANSISTOR MODELS
C 
   10 FACTOR=RATIO3*EXP(SYMVAL(I,16)*XNUM)
      SYMVAL(I,12)=SYMVAL(I,12)*FACTOR
      GO TO 100
C 
C  GUMMEL-POON TRANSISTOR MODELS
C 
   20 FACTOR=RATIO3*EXP(SYMVAL(I,25)*XNUM)
      SYMVAL(I,2)=SYMVAL(I,2)*FACTOR
      SYMVAL(I,3)=SYMVAL(I,3)*FACTOR
      SYMVAL(I,12)=SYMVAL(I,12)*FACTOR
      SYMVAL(I,17)=SYMVAL(I,17)*RATIO 
      SYMVAL(I,15)=SYMVAL(I,15)*EXP(VT*ALOG(FACTOR)/SYMVAL(I,17)) 
      SYMVAL(I,20)=SYMVAL(I,20)*RATIO 
      SYMVAL(I,18)=SYMVAL(I,18)*EXP(VT*ALOG(FACTOR)/SYMVAL(I,20)) 
      GO TO 100
C 
C  JUNCTION DIODE MODELS
C 
   30 FACTOR=RATIO3*EXP(SYMVAL(I,7)*XNUM) 
      SYMVAL(I,5)=SYMVAL(I,5)*RATIO
      SYMVAL(I,4)=SYMVAL(I,4)*EXP(VT*ALOG(FACTOR)/SYMVAL(I,5))
      GO TO 100
C 
C  SHOTTKY BARRIER DIODE MODELS
C 
   40 FACTOR=RATIO2*EXP(SYMVAL(I,7)*XNUM) 
      SYMVAL(I,5)=SYMVAL(I,5)*RATIO
      SYMVAL(I,4)=SYMVAL(I,4)*EXP(VT*ALOG(FACTOR)/SYMVAL(I,5))
      GO TO 100
C 
C  JFET MODEL 
C 
   50 FACTOR=RATIO3*EXP(1.11*XNUM)
      SYMVAL(I,10)=SYMVAL(I,10)*FACTOR
      GO TO 100
C 
C  MOSFET MODEL
C 
   60 FACTOR=RATIO3*EXP(1.11*XNUM)
      SYMVAL(I,15)=SYMVAL(I,15)*FACTOR
  100 CONTINUE
      RETURN
      END 
      SUBROUTINE DSIGN
      RETURN
      END 
      SUBROUTINE DCOP(IFLAG)
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401), 
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21), 
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
C 
C 
      DATA LPRN/1H(/
C 
C  IFLAG=1 FOR AC SMALL SIGNAL OPERATING POINT
C  IFLAG=2 FOR INITIAL TRANSIENT OPERATING POINT
C 
      IGOOF=0 
      IPASS1=1
      DO 10 I=2,NUMNOD
   10 VNIM1(I)=0.0
      CALL ITER8
      RTIMES(6)=RTIMES(6)+ITERNO
      IF (IGOOF.EQ.0) GO TO 100
      NOGO=1
      WRITE (6,21)
   21 FORMAT (1H1,4X,*----------  NO CONVERGENCE ON DC *
     1   *OPERATING POINT*//) 
      WRITE (6,31) ITERNO,IPASS1,IFINAL
   31 FORMAT (5X,*ITERNO =  *,I3,5X,*IPASS1 =  *,I3,5X,*IFINAL =  *,I3//
     1   5X,*LAST NODE VOLTAGES*//1X,5(* NODE    VOLTAGE    *)//) 
      WRITE (6,41) (LPRN,JUNODE(I),VNIM1(I),I=2,NUNODS)
   41 FORMAT (5(1X,A1,I3,1H),1X,F10.4,3X)/)
      RETURN
C 
C  PRINT DC OPERATING POINT
C 
  100 WRITE (6,101)
  101 FORMAT (1X,5(* NODE    VOLTAGE    *)//) 
      WRITE (6,41) (LPRN,JUNODE(I),VNIM1(I),I=2,NUNODS)
C 
C  VOLTAGE SOURCE CURRENTS
C 
      POWER=0.0
      ISTART=LOCATE(6)
      ISTOP=LOCATE(7)-1
      IF (ISTART.GT.ISTOP) GO TO 160
      WRITE (6,131)
  131 FORMAT (////5X,*VOLTAGE SOURCE CURRENTS*//8X,*NAME*,10X,
     1   *CURRENT*/)
      DO 150 I=ISTART,ISTOP
      MNAM=MNAME(I)
      CALL CALCUR(I,CREAL)
      POWER=POWER-SOURCE(MNAM)*CREAL
      WRITE (6,141) NAME(I),CREAL 
  141 FORMAT (/8X,R7,5X,1PE10.3,*  AMPS*) 
  150 CONTINUE
  160 ISTART=LOCATE(4)
      ISTOP=LOCATE(5)-1
      IF (ISTART.GT.ISTOP) GO TO 180
      DO 170 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      MNAM=MNAME(I)
      POWER=POWER-SOURCE(MNAM)*(VNIM1(NODE1)-VNIM1(NODE2))
  170 CONTINUE
  180 WRITE (6,181) POWER 
  181 FORMAT (//5X,*TOTAL POWER DISSIPATION  *,1PE9.2,*  WATTS*)
C 
C  SMALL SIGNAL DEVICE PARAMETERS 
C 
      ISTART=JELCNT(7)+JELCNT(8)+JELCNT(9)+JELCNT(10) 
      IF (ISTART.EQ.0) RETURN 
      WRITE (6,201)
  201 FORMAT (1H1)
      IF (IFLAG.EQ.2) GO TO 210
      IPASS1=1
      MODE=2
      CALL BJT
      CALL DIODE
      CALL JFET
      CALL MOSFET 
      MODE=1
      IPASS1=0
C 
C  TRANSISTORS
C 
  210 ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) GO TO 300
      IF (IFLAG.EQ.2) GO TO 220
      WRITE (6,211)
  211 FORMAT (/////5X,*TRANSISTOR OPERATING POINTS*///1X,*NAME*,
     1   4X,*MODEL*,7X,*IB*,8X,*IC*,6X,*VBE*,5X,*VBC*,5X,*VCE*,7X,
     2   *GM*,7X,*RPI*,6X,*RO*,7X,*CPI*,6X,*CMU*,5X,*BETADC*,2X,
     3   *BETAAC*,3X,*FT*/)
      GO TO 230
  220 WRITE (6,221)
  221 FORMAT (/////5X,*TRANSISTOR OPERATING POINTS*///1X,*NAME*,
     1   4X,*MODEL*,7X,*IB*,8X,*IC*,6X,*VBE*,5X,*VBC*,5X,*VCE*,5X,
     2   *BETADC*/)
  230 ISPOT=ICURNT(7) 
      DO 280 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      TYPE=SYMVAL(MNAM,1) 
      VBE=VNIM1(NODE2)-VNIM1(NODE3)
      VBC=VNIM1(NODE2)-VNIM1(NODE1)
      VCE=VBE-VBC 
      CC=TYPE*TRACUR(ISPOT+2) 
      CB=TYPE*TRACUR(ISPOT+3) 
      ARG=TRACUR(ISPOT+3) 
      IF (ABS(ARG).LT.1.0E-20) ARG=1.0E-20
      BETADC=TRACUR(ISPOT+2)/ARG
      IF (IFLAG.EQ.1) GO TO 250
      WRITE (6,241) NAME(I),MODNAM(MNAM),CB,CC,VBE,VBC,VCE,BETADC 
  241 FORMAT (/1X,R7,1X,R7,1X,1PE9.2,1X,E9.2,1X,0PF7.3,1X,F7.3,1X,
     1   F7.3,1X,F7.1)
      GO TO 270
  250 RPI=1.0/TRACUR(ISPOT+4) 
      CPI=TRACUR(ISPOT+5) 
      CMU=TRACUR(ISPOT+7) 
      GM=TRACUR(ISPOT+8)
      ARG=TRACUR(ISPOT+9) 
      IF (ARG.LT.1.0E-20) ARG=1.0E-20 
      RO=1.0/ARG
      BETAAC=GM*RPI
      ARG=CPI+CMU 
      IF (ARG.LT.1.0E-20) ARG=1.0E-20 
      FT=GM/(TWOPI*ARG)
      WRITE (6,261) NAME(I),MODNAM(MNAM),CB,CC,VBE,VBC,VCE,GM,RPI,
     1   RO,CPI,CMU,BETADC,BETAAC,FT
  261 FORMAT (/1X,R7,1X,R7,1X,1PE9.2,1X,E9.2,1X,0PF7.3,1X,F7.3,1X,
     1   F7.3,1X,1PE9.2,1X,E8.2,1X,E8.2,1X,E8.2,1X,E8.2,1X,0PF7.1,1X, 
     2   F7.1,1X,1PE8.2)
  270 ISPOT=ISPOT+15
  280 CONTINUE
C 
C  DIODES 
C 
  300 ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) GO TO 400
      IF (IFLAG.EQ.2) GO TO 310
      WRITE (6,301)
  301 FORMAT (/////5X,*DIODE OPERATING POINTS*///1X,*NAME*,4X,
     1   *MODEL*,10X,*ID*,11X,*VJ*,12X,*R*,12X,*C*/)
      GO TO 320
  310 WRITE (6,311)
  311 FORMAT (/////5X,*DIODE OPERATING POINTS*///1X,*NAME*,4X,
     1   *MODEL*,10X,*ID*,11X,*VJ*/)
  320 ISPOT=ICURNT(8) 
      DO 370 I=ISTART,ISTOP
      LOC=LOCAL(I)
      MNAM=MNAME(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      VBE=VNIM1(NODE1)-VNIM1(NODE2)
      CC=TRACUR(ISPOT+1)
      IF (IFLAG.EQ.1) GO TO 340
      WRITE (6,331) NAME(I),MODNAM(MNAM),CC,VBE
  331 FORMAT (/1X,R7,1X,R7,2(3X,1PE10.3)) 
      GO TO 360
  340 RPI=1.0/TRACUR(ISPOT+2) 
      WRITE (6,351) NAME(I),MODNAM(MNAM),CC,VBE,RPI,TRACUR(ISPOT+3)
  351 FORMAT (/1X,R7,1X,R7,4(3X,1PE10.3)) 
  360 ISPOT=ISPOT+5
  370 CONTINUE
C 
C  JFETS
C 
  400 ISTART=LOCATE(9)
      ISTOP=LOCATE(10)-1
      IF (ISTART.GT.ISTOP) GO TO 500
      IF (IFLAG.EQ.2) GO TO 410
      WRITE (6,401)
  401 FORMAT (/////5X,*JFET OPERATING POINTS*///1X,*NAME*,4X,*MODEL*, 
     1   10X,*ID*,11X,*VGS*,10X,*VDS*,10X,*GM*,11X,*GDS*,10X,*CGS*,
     2   10X,*CGD*/)
      GO TO 420
  410 WRITE (6,411)
  411 FORMAT (/////5X,*JFET OPERATING POINTS*///1X,*NAME*,4X,*MODEL*, 
     1   10X,*ID*,11X,*VGS*,10X,*VDS*/)
  420 ISPOT=ICURNT(9) 
      DO 460 I=ISTART,ISTOP
      LOC=LOCAL(I)
      MNAM=MNAME(I)
      TYPE=SYMVAL(MNAM,1) 
      CSAT=SYMVAL(MNAM,10)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      VGD=TRACUR(ISPOT+1) 
      CDRAIN=TYPE*(TRACUR(ISPOT+2)-CSAT*(EXP(VGD/VT)-1.0))
      VGS=VNIM1(NODE2)-VNIM1(NODE3)
      VDS=VNIM1(NODE1)-VNIM1(NODE3)
      IF (IFLAG.EQ.1) GO TO 440
      WRITE (6,431) NAME(I),MODNAM(MNAM),CDRAIN,VGS,VDS
  431 FORMAT (/1X,R7,1X,R7,3(3X,1PE10.3)) 
      GO TO 450
  440 WRITE (6,441) NAME(I),MODNAM(MNAM),CDRAIN,VGS,VDS,
     1   TRACUR(ISPOT+3),TRACUR(ISPOT+4),TRACUR(ISPOT+6), 
     2   TRACUR(ISPOT+8)
  441 FORMAT (/1X,R7,1X,R7,7(3X,1PE10.3)) 
  450 ISPOT=ISPOT+10
  460 CONTINUE
C 
C  MOSFETS
C 
  500 ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) RETURN 
      IF (IFLAG.EQ.2) GO TO 510
      WRITE (6,501)
  501 FORMAT (/////5X,*MOSFET OPERATING POINTS*///1X,*NAME*,4X,
     1   *MODEL*,10X,*ID*,11X,*VGS*,10X,*VDS*,10X,*VBS*,10X,*GM*,11X, 
     2   *GDS*,9X,*GMBS*,10X,*CBD*,10X,*CBS*/)
      GO TO 520
  510 WRITE (6,511)
  511 FORMAT (/////5X,*MOSFET OPERATING POINTS*///1X,*NAME*,4X,
     1   *MODEL*,10X,*ID*,11X,*VGS*,10X,*VDS*,10X,*VBS*/) 
  520 ISPOT=ICURNT(10)
      DO 560 I=ISTART,ISTOP
      LOC=LOCAL(I)
      MNAM=MNAME(I)
      TYPE=SYMVAL(MNAM,1) 
      CSAT=SYMVAL(MNAM,15)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      NODE3=NODPLC(LOC+2) 
      NODE4=NODPLC(LOC+3) 
      VGS=VNIM1(NODE2)-VNIM1(NODE3)
      VDS=VNIM1(NODE1)-VNIM1(NODE3)
      VBS=VNIM1(NODE4)-VNIM1(NODE3)
      VBD=TRACUR(ISPOT)
      CDRAIN=TYPE*(TRACUR(ISPOT+2)-CSAT*(EXP(VBD/VT)-1.0))
      IF (IFLAG.EQ.1) GO TO 540
      WRITE (6,531) NAME(I),MODNAM(MNAM),CDRAIN,VGS,VDS,VBS
  531 FORMAT (/1X,R7,1X,R7,4(3X,1PE10.3)) 
      GO TO 550
  540 WRITE (6,541) NAME(I),MODNAM(MNAM),CDRAIN,VGS,VDS,VBS,
     1   TRACUR(ISPOT+3),TRACUR(ISPOT+4),TRACUR(ISPOT+5), 
     2   TRACUR(ISPOT+7),TRACUR(ISPOT+9)
  541 FORMAT (/1X,R7,1X,R7,9(3X,1PE10.3)) 
  550 ISPOT=ISPOT+15
  560 CONTINUE
      RETURN
      END 
      SUBROUTINE DCTRFN
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401), 
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21), 
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10), 
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
      COMMON/DC/ICVFLG,ITCELM,TCSTAR,TCSTOP,TCINCR,KSSOP,KINEL,KOVAR
C 
C 
      DIMENSION U(1)
      EQUIVALENCE (U(1),YNL(402)) 
C 
C 
      LOC=LOCAL(KINEL)
      NOPOSI=NODPLC(LOC)
      NONEGI=NODPLC(LOC+1)
      NOPOSO=IOPND(KOVAR) 
      NONEGO=IONND(KOVAR) 
      IPASS1=-1
C 
C  SETUP CURRENT VECTOR FOR INPUT IMPEDENCE AND TRANSFER FUNCTION 
C 
      DO 10 I=1,NUMNOD
   10 VN(I)=0.0
      IF (KINEL.GE.LOCATE(6)) GO TO 20
      VN(NOPOSI)=-1.0 
      VN(NONEGI)=1.0
   20 KELNO=KINEL 
C 
C  LOAD IN VOLTAGE SOURCES TO CURRENT VECTOR
C 
   30 IF (NUMVS.EQ.0) GO TO 60
      DO 50 I=1,NUMVS 
      IELNUM=MATLOC(I)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      SIGN=NODPLC(LOC+2)
      IF (IELNUM.NE.KELNO) SIGN=0.0
      VN(NODE2)=VN(NODE2)+VN(NODE1)-YNL(NODE1)*SIGN
      IF (SIGN.EQ.0.0) GO TO 50
      IPNOD=NODPLC(LOC+3) 
      JSTART=IUR(IPNOD)
      JSTOP=IUR(IPNOD+1)-1
      IF (JSTART.GT.JSTOP) GO TO 50
      DO 40 J=JSTART,JSTOP
      JO=IUC(J)
      NODE3=IORDER(JO)
      VN(NODE3)=VN(NODE3)-U(J)*SIGN
   40 CONTINUE
   50 CONTINUE
C 
C  PERFORM NEW FORWARD AND BACK SUBSTITUTION
C 
   60 CALL ITER8
      VN(1)=0.0
      IF (NUMVS.EQ.0) GO TO 100
      DO 90 I=1,NUMVS 
      IELNUM=MATLOC(NUMVS-I+1)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1) 
      SIGN=NODPLC(LOC+2)
      IF (IELNUM.NE.KELNO) SIGN=0.0
      IF (IELNUM.LT.LOCATE(6)) GO TO 80
      ISPOT=ICURNT(6)+2*(IELNUM-LOCATE(6))
      TRACUR(ISPOT)=VN(NODE1) 
   80 VN(NODE1)=VN(NODE2)+SIGN
   90 CONTINUE
  100 IF (KELNO.NE.KINEL) GO TO 200
C 
C  EVALUATE TRANSFER FUNCTION 
C 
      IF (IOFLG(KOVAR).EQ.2) GO TO 110
      TRFN=VN(NOPOSO)-VN(NONEGO)
      GO TO 120
  110 CALL CALCUR(NOPOSO,TRFN)
C 
C  EVALUATE INPUT IMPEDENCE
C 
  120 IF (KINEL.GE.LOCATE(6)) GO TO 130
      ZIN=VN(NONEGI)-VN(NOPOSI)
      GO TO 150
  130 CALL CALCUR(KINEL,CREAL)
      IF (CREAL.GT.1.0E-20) GO TO 140 
      IF (CREAL.LT.-1.0E-20) GO TO 140
      CREAL=-1.0E-20
  140 ZIN=-1.0/CREAL
C 
C  SETUP CURRENT VECTOR FOR OUTPUT IMPEDENCE
C 
  150 KELNO=0 
      DO 160 I=1,NUMNOD
  160 VN(I)=0.0
      IF (IOFLG(KOVAR).EQ.2) GO TO 170
      VN(NOPOSO)=-1.0 
      VN(NONEGO)=1.0
      GO TO 30
  170 KELNO=NOPOSO
      IF (KELNO.NE.KINEL) GO TO 30
      ZOUT=ZIN
      GO TO 250
C 
C  EVALUATE OUTPUT IMPEDENCE
C 
  200 IF (IOFLG(KOVAR).EQ.2) GO TO 210
      ZOUT=VN(NONEGO)-VN(NOPOSO)
      GO TO 250
  210 CALL CALCUR(NOPOSO,CREAL)
      IF (CREAL.GT.1.0E-20) GO TO 220 
      IF (CREAL.LT.-1.0E-20) GO TO 220
      CREAL=-1.0E-20
  220 ZOUT=-1.0/CREAL 
  250 WRITE (6,251) IONAM(KOVAR),NAME(KINEL),NAME(KINEL),IONAM(KOVAR),
     1   TRFN,ZIN,ZOUT
  251 FORMAT (/////5X,*SMALL SIGNAL CHARACTERISTICS*///6X,R7,3H/  ,
     1   R7,4X,*INPUT IMPEDENCE*,7X,*OUTPUT IMPEDENCE*/31X,*AT  *,R7, 
     2   11X,*AT  *,R7//8X,1PE10.3,12X,E10.3,12X,E10.3)
      RETURN
      END 
      SUBROUTINE SENCAL
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401), 
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
      COMMON/SENVAR/NSENS,KSNOUT(10)
C
C
      DATA LSRB,LSRC,LSRE,LSIS,LSBF,LSBR,LSVA,LSRS,LSN /6H   RB ,
     1   6H   RC ,6H   RE ,6H   IS ,6H   BF ,6H   BR ,6H   VA ,
     2   6H   RS ,6H   N  /
C
C
      DO 1000 N=1,NSENS
C
C  SMALL SIGNAL OPERATING POINT HAS BEEN OBTAINED.  SMALL SIGNAL
C  Y MATRIX IS STORED IN YNL ARRAY.  OBTAIN ADJOINT SOLUTION BY
C  CONSTRUCTING ADJOINT EXCITATION VECTOR AND DOING A FORWARD AND
C  BACK SUBSTITUTION ON THE TRANSPOSE OF Y MATRIX
C
      DO 10 I=1,NUMNOD
   10 VN(I)=0.0
      ISTART=LOCATE(6)
      ISTOP=LOCATE(7)-1
      IF (ISTART.GT.ISTOP) GO TO 30
      DO 20 I=ISTART,ISTOP
   20 VALUE(I)=0.0
   30 IKNT=KSNOUT(N)
      IPNOD=IOPND(IKNT)
      IF (IOFLG(IKNT).EQ.2) GO TO 40
      INNOD=IONND(IKNT)
      VN(IPNOD)=-1.0
      VN(INNOD)=1.0
      GO TO 50
   40 VALUE(IPNOD)=-1.0
   50 CALL ASOL
C
C  REAL SOLUTION IS STORED IN VNIM1 ARRAY, AND ADJOINT SOLUTION IS
C  STORED IN VN ARRAY.  COMPUTE SENSITIVITIES
C
      WRITE (6,51) IONAM(IKNT)
   51 FORMAT (1H1,*SENSITIVITIES OF OUTPUT *,R7///8X,*ELEMENT*,9X,
     1   *ELEMENT*,7X,*ELEMENT*,7X,*NORMALIZED*/10X,*NAME*,11X,*VALUE*,
     2   6X,*SENSITIVITY*,4X,*SENSITIVITY*/35X,*(VOLTS/UNIT)*,2X,
     3   *(VOLTS/PERCENT)*)
C
C  RESISTORS
C
      ISTOP=LOCATE(2)-1
      IF (ISTOP.LT.1) GO TO 100
      WRITE (6,61)
   61 FORMAT (//)
      DO 80 I=1,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      VAL=1.0/VALUE(I)
      SENS=-(VNIM1(NODE1)-VNIM1(NODE2))*(VN(NODE1)-VN(NODE2))/(VAL*VAL)
      SENSN=VAL*SENS/100.0
      WRITE (6,71) NAME(I),VAL,SENS,SENSN
   71 FORMAT (10X,R7,5X,1PE10.3,5X,E10.3,5X,E10.3)
   80 CONTINUE
C
C  CURRENT SOURCES
C
  100 ISTART=LOCATE(4)
      ISTOP=LOCATE(5)-1
      IF (ISTART.GT.ISTOP) GO TO 150
      WRITE (6,61)
      DO 120 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      MNAM=MNAME(I)
      VAL=SOURCE(MNAM)
      SENS=VN(NODE1)-VN(NODE2)
      SENSN=VAL*SENS/100.0
      WRITE (6,71) NAME(I),VAL,SENS,SENSN
  120 CONTINUE
C
C  VOLTAGE DEPENDENT CURRENT SOURCES
C
  150 ISTART=LOCATE(5)
      ISTOP=LOCATE(6)-1
      IF (ISTART.GT.ISTOP) GO TO 200
      WRITE (6,61)
      DO 170 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE4=NODPLC(LOC+3)
      VAL=VALUE(I)
      SENS=(VNIM1(NODE3)-VNIM1(NODE4))*(VN(NODE1)-VN(NODE2))
      SENSN=VAL*SENS/100.0
      WRITE (6,71) NAME(I),VAL,SENS,SENSN
  170 CONTINUE
C
C  VOLTAGE SOURCES
C
  200 ISTART=LOCATE(6)
      ISTOP=LOCATE(7)-1
      IF (ISTART.GT.ISTOP) GO TO 300
      WRITE (6,61)
      DO 230 I=ISTART,ISTOP
      MNAM=MNAME(I)
      VAL=SOURCE(MNAM)
      CALL ACALC(I,CREAL)
      SENS=-CREAL
      SENSN=VAL*SENS/100.0
      WRITE (6,71) NAME(I),VAL,SENS,SENSN
  230 CONTINUE
C
C  TRANSISTORS
C
  300 ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) GO TO 400
      WRITE (6,61)
      DO 380 I=ISTART,ISTOP
      MNAM=MNAME(I)
      WRITE (6,301) NAME(I)
  301 FORMAT (1X,R7)
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE4=NODPLC(LOC+3)
      NODE5=NODPLC(LOC+4)
      NODE6=NODPLC(LOC+5)
C
C  BASE RESISTANCE (RB)
C
      IF (SYMVAL(MNAM,4).NE.0.0) GO TO 320
      WRITE (6,311) LSRB
  311 FORMAT (10X,R7,5X,4H 0.0,11X,4H 0.0,11X,4H 0.0)
      GO TO 330
  320 VAL=1.0/SYMVAL(MNAM,4)
      SENS=-(VNIM1(NODE2)-VNIM1(NODE5))*(VN(NODE2)-VN(NODE5))/(VAL*VAL)
      SENSN=VAL*SENS/100.0
      WRITE (6,71) LSRB,VAL,SENS,SENSN
C
C  COLLECTOR RESISTANCE (RC)
C
  330 IF (SYMVAL(MNAM,5).NE.0.0) GO TO 340
      WRITE (6,311) LSRC
      GO TO 350
  340 VAL=1.0/SYMVAL(MNAM,5)
      SENS=-(VNIM1(NODE1)-VNIM1(NODE4))*(VN(NODE1)-VN(NODE4))/(VAL*VAL)
      SENSN=VAL*SENS/100.0
      WRITE (6,71) LSRC,VAL,SENS,SENSN
C
C  EMITTER RESISTANCE (RE)
C
  350 IF (SYMVAL(MNAM,6).NE.0.0) GO TO 360
      WRITE (6,311) LSRE
      GO TO 370
  360 VAL=1.0/SYMVAL(MNAM,6)
      SENS=-(VNIM1(NODE3)-VNIM1(NODE6))*(VN(NODE3)-VN(NODE6))/(VAL*VAL)
      SENSN=VAL*SENS/100.0
      WRITE (6,71) LSRE,VAL,SENS,SENSN
C
C  INTRINSIC PARAMETERS
C
  370 IF (KIND(MNAM).NE.1) GO TO 380
      TYPE=SYMVAL(MNAM,1)
      BF=SYMVAL(MNAM,2)
      BR=SYMVAL(MNAM,3)
      CSAT=SYMVAL(MNAM,12)*VALUE(I)
      OVA=SYMVAL(MNAM,15)
      VBE=TYPE*(VNIM1(NODE5)-VNIM1(NODE6))
      VBC=TYPE*(VNIM1(NODE5)-VNIM1(NODE4))
      EVBE=EXP(VBE/VT)
      EVBC=EXP(VBC/VT)
      VABE=TYPE*(VN(NODE5)-VN(NODE6))
      VABC=TYPE*(VN(NODE5)-VN(NODE4))
      VACE=VABE-VABC
C
C  SATURATION CURRENT (IS)
C
      SENS=VACE*((EVBE-EVBC)*(1.0-VBC*OVA)-(EVBC-1.0)/BR)
     1   +VABE*((EVBE-1.0)/BF+(EVBC-1.0)/BR)
      SENSN=CSAT*SENS/100.0
      WRITE (6,71) LSIS,CSAT,SENS,SENSN
C
C  CURRENT GAINS (BF,BR)
C
      SENS=-VABE*CSAT*(EVBE-1.0)/(BF*BF)
      SENSN=BF*SENS/100.0
      WRITE (6,71) LSBF,BF,SENS,SENSN
      SENS=-VABC*CSAT*(EVBC-1.0)/(BR*BR)
      SENSN=BR*SENS/100.0
      WRITE (6,71) LSBR,BR,SENS,SENSN
C
C  EARLY VOLTAGE (VA)
C
C
      IF (OVA.NE.0.0) GO TO 375
      WRITE (6,311) LSVA
      GO TO 380
  375 SENS=VACE*VBC*CSAT*(EVBE-EVBC)*OVA*OVA
      IF (ABS(SENS).LT.1.0E-50) SENS=0.0
      VA=1.0/OVA
      SENSN=VA*SENS/100.0
      WRITE (6,71) LSVA,VA,SENS,SENSN
  380 CONTINUE
C
C  DIODES
C
  400 ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) GO TO 1000
      WRITE (6,61)
      DO 500 I=ISTART,ISTOP
      WRITE (6,301) NAME(I)
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      MNAM=MNAME(I)
C
C  SERIES RESISTANCE (RS)
C
      IF (SYMVAL(MNAM,1).NE.0.0) GO TO 410
      WRITE (6,311) LSRS
      GO TO 420
  410 VAL=1.0/SYMVAL(MNAM,1)
      SENS=-(VNIM1(NODE1)-VNIM1(NODE3))*(VN(NODE1)-VN(NODE3))/(VAL*VAL)
      SENSN=VAL*SENS/100.0
      WRITE (6,71) LSRS,VAL,SENS,SENSN
C
C  INTRINSIC PARAMETERS
C
  420 CSAT=SYMVAL(MNAM,4)*VALUE(I)
      VTE=SYMVAL(MNAM,5)
      XNE=VTE/VT
      VBE=VNIM1(NODE3)-VNIM1(NODE2)
      EVBE=EXP(VBE/VTE)
      VABE=VN(NODE3)-VN(NODE2)
C
C  SATURATION CURRENT (IS)
C
      SENS=VABE*(EVBE-1.0)
      SENSN=CSAT*SENS/100.0
      WRITE (6,71) LSIS,CSAT,SENS,SENSN
C
C  IDEALITY FACTOR (N)
C
      SENS=-VABE*(CSAT/XNE)*(VBE/VTE)*EVBE
      IF (ABS(SENS).LT.1.0E-50) SENS=0.0
      SENSN=XNE*SENS/100.0
      WRITE (6,71) LSN,XNE,SENS,SENSN
  500 CONTINUE
 1000 CONTINUE
      RETURN
      END
      SUBROUTINE ASOL
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
C
C
      DIMENSION U(1),UL(1)
      EQUIVALENCE (U(1),YNL(402)),(UL(1),YNL(1202))
C
C  LOAD VOLTAGE SOURCES IN ADJOINT EXCITATION VECTOR
C
      IF (NUMVS.EQ.0) GO TO 100
      DO 90 I=1,NUMVS
      IELNUM=MATLOC(I)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      SIGN=NODPLC(LOC+2)
      IPNOD=NODPLC(LOC+3)
      IF (IELNUM.LT.LOCATE(6)) GO TO 10
      VAL=SIGN*VALUE(IELNUM)
      GO TO 20
   10 VAL=0.0
   20 VN(NODE2)=VN(NODE2)+VN(NODE1)-YNL(NODE1)*VAL
      IF (VAL) 30,90,30
   30 JSTART=IUR(IPNOD)
      JSTOP=IUR(IPNOD+1)-1
      IF (JSTART.GT.JSTOP) GO TO 90
      DO 50 J=JSTART,JSTOP
      JO=IUC(J)
      NODE3=IORDER(JO)
      VN(NODE3)=VN(NODE3)-UL(J)*VAL
   50 CONTINUE
   90 CONTINUE
C
C  OBTAIN ADJOINT SOLUTION
C
  100 LE=NSTOP-1
      IF (LE) 200,110,120
  110 IB=IORDER(1)
      VN(IB)=VN(IB)/YNL(IB)
      GO TO 200
C
C  FORWARD SUBSTITUTION
C
  120 DO 150 I=1,LE
      L=IORDER(I)
      VN(L)=VN(L)/YNL(L)
      IUST=IUR(I)
      IUE=IUR(I+1)-1
      IF (IUST.GT.IUE) GO TO 150
      DO 140 IU=IUST,IUE
      II=IUC(IU)
      IC=IORDER(II)
      VN(IC)=VN(IC)-VN(L)*U(IU)
  140 CONTINUE
  150 CONTINUE
C
C  BACK SUBSTITUTION
C
      IB=IORDER(NSTOP)
      VN(IB)=VN(IB)/YNL(IB)
      DO 190 I=1,LE
      IB=IORDER(NSTOP-I)
      IUST=IUR(NSTOP-I)
      IUE=IUR(NSTOP-I+1)-1
      IF (IUST.GT.IUE) GO TO 190
      DO 180 IU=IUST,IUE
      JJ=IUC(IU)
      JB=IORDER(JJ)
      VN(IB)=VN(IB)-UL(IU)*VN(JB)
  180 CONTINUE
  190 CONTINUE
C
C  SET SOURCES TO VOLTAGE VALUE
C
  200 VN(1)=0.0
      IF (NUMVS.EQ.0) RETURN
      DO 240 I=1,NUMVS
      IELNUM=MATLOC(NUMVS-I+1)
      IF (IELNUM.LT.LOCATE(6)) GO TO 220
      ISPOT=ICURNT(6)+2*(IELNUM-LOCATE(6))
      VAL=VALUE(IELNUM)
      GO TO 230
  220 VAL=0.0
      ISPOT=ICURNT(3)+2*(IELNUM-LOCATE(3))
  230 LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      SIGN=NODPLC(LOC+2)
      TRACUR(ISPOT)=VN(NODE1)
      VN(NODE1)=VN(NODE2)+SIGN*VAL
  240 CONTINUE
      RETURN
      END
      SUBROUTINE ACALC (IELNUM,CREAL)
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
C
C
      DIMENSION U(1)
      EQUIVALENCE (U(1),YNL(402))
C
C
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      SIGN=NODPLC(LOC+2)
      IPNOD=NODPLC(LOC+3)
      ISPOT=ICURNT(6)+2*(IELNUM-LOCATE(6))
      CREAL=TRACUR(ISPOT)-YNL(NODE1)*VN(NODE1)
      ISTART=IUR(IPNOD)
      ISTOP=IUR(IPNOD+1)-1
      IF (ISTART.GT.ISTOP) GO TO 20
      DO 10 I=ISTART,ISTOP
      IO=IUC(I)
      NODE3=IORDER(IO)
      CREAL=CREAL-U(I)*VN(NODE3)
   10 CONTINUE
   20 CREAL=SIGN*CREAL
      RETURN
      END
      SUBROUTINE TRANAN
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
      COMMON/TRAN/JTRFLG,TSTEP,TSTOP,TSTART,NOTINT,STEPS(5),ENDPTS(5),
     1   KFROUT,FORFRE,KFPTS
C
C
      DATA LPRN /1H(/
C
C  INITIALIZE
C
      CALL SECOND(T1)
      ICALC=1
      KOUNT=0
      IGOOF=0
      IPASS1=1
      INTER=1
      TNEXT=TSTART
      IF (TNEXT.LT.TSTEP) TNEXT=TSTEP
      DELTA=STEPS(1)
      TIME=0.0
      GO TO 110
C
C  INCREMENT TIME, UPDATE SOURCES, AND SOLVE NEXT TIMEPOINT
C
  100 MODE=4
  110 TIME=TIME+DELTA
      CALL SORUPD
      CALL ITER8
      KOUNT=KOUNT+ITERNO
      IF (IGOOF.EQ.1) GO TO 1000
C
C  CHANGE DELTA IF NEXT INTERVAL IS ENTERED
C
      DELOLD=DELTA
      DEL1=1.01*(ENDPTS(INTER)-TIME)
      IF (DEL1.GT.DELTA) GO TO 300
      IF (INTER.GE.NOTINT) GO TO 300
      INTER=INTER+1
      DELTA=STEPS(INTER)
C
C  STORE OUTPUTS
C
  300 DEL1=1.01*(TNEXT-TIME)
      IF (DEL1.GT.DELTA) GO TO 100
      ICALC=ICALC+1
      FREQ(ICALC)=TNEXT
      IKNT=0
  310 IKNT=IKNT+1
      IF (IKNT.GT.NUMOR(2)) GO TO 350
      JKNT=IOVAR(IKNT,2)
      IPNOD=IOPND(JKNT)
      IF (IOFLG(JKNT).EQ.2) GO TO 320
      INNOD=IONND(JKNT)
      OUTVAR=VNIM1(IPNOD)-VNIM1(INNOD)
      GO TO 330
  320 CALL CALCUR(IPNOD,OUTVAR)
C
C  LINEAR INTERPOLATION FOR TIMEPOINT VALUE
C
  330 VAROLD=ROUT(ICALC-1,IKNT)
      FRACT=1.0-TSTEP/(TIME-FREQ(ICALC-1))
      IF (FRACT.LT.1.0E-6) FRACT=0.0
      ROUT(ICALC,IKNT)=OUTVAR+FRACT*(VAROLD-OUTVAR)
      GO TO 310
  350 TNEXT=TNEXT+TSTEP
      IF (ICALC-JTRFLG) 300,1100,1100
C
C  NONCONVERGENCE TRAP
C
 1000 WRITE (6,1001) TIME
 1001 FORMAT (1H1,4X,*----------  NO CONVERGENCE IN TRANSIENT *
     1   *ANALYSIS AT TIMEPOINT  *,1PE9.2//)
      WRITE (6,1011) ITERNO,IPASS1,IFINAL
 1011 FORMAT (5X,*ITERNO =  *,I3,5X,*IPASS1 =  *,I3,5X,*IFINAL =  *,I3//
     1   5X,*LAST NODE VOLTAGES*//1X,5(* NODE    VOLTAGE    *)//)
      WRITE (6,1021) (LPRN,JUNODE(I),VNIM1(I),I=2,NUNODS)
 1021 FORMAT (5(1X,A1,I3,1H),1X,F10.4,3X)/)
 1100 CALL SECOND(T2)
      RTIMES(8)=RTIMES(8)+T2-T1
      RTIMES(9)=RTIMES(9)+KOUNT
      RETURN
      END
      SUBROUTINE SORUPD
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
C
C
      DO 350 ID=4,6,2
      ISTART=LOCATE(ID)
      ISTOP=LOCATE(ID+1)-1
      IF (ISTART.GT.ISTOP) GO TO 350
      DO 300 I=ISTART,ISTOP
      MNAM=MNAME(I)
      ITYPE=SOURCE(MNAM+3)
      IF (ITYPE.EQ.0) GO TO 300
      GO TO (110,150,200),ITYPE
C
C  PULSE SOURCE
C
  110 V1=SOURCE(MNAM+5)
      V2=SOURCE(MNAM+6)
      T1=SOURCE(MNAM+7)
      T2=SOURCE(MNAM+8)
      T3=SOURCE(MNAM+9)
      T4=SOURCE(MNAM+10)
      PERIOD=SOURCE(MNAM+11)
      TIME1=TIME
  115 IF (TIME1.LT.T1+PERIOD) GO TO 120
      TIME1=TIME1-PERIOD
      GO TO 115
  120 IF (TIME1.LT.T4) GO TO 125
      SOURCE(MNAM+2)=V1
      GO TO 300
  125 IF (TIME1.LT.T3) GO TO 130
      SOURCE(MNAM+2)=V2+(TIME1-T3)*(V1-V2)/(T4-T3)
      GO TO 300
  130 IF (TIME1.LT.T2) GO TO 135
      SOURCE(MNAM+2)=V2
      GO TO 300
  135 IF (TIME1.LT.T1) GO TO 140
      SOURCE(MNAM+2)=V1+(TIME1-T1)*(V2-V1)/(T2-T1)
      GO TO 300
  140 SOURCE(MNAM+2)=V1
      GO TO 300
C
C  EXPONENTIAL SOURCE
C
  150 V1=SOURCE(MNAM+5)
      V2=SOURCE(MNAM+6)
      T1=SOURCE(MNAM+7)
      TAU1=SOURCE(MNAM+8)
      T2=SOURCE(MNAM+9)
      TAU2=SOURCE(MNAM+10)
      TIME1=TIME
      IF (TIME1.GT.T1) GO TO 155
      SOURCE(MNAM+2)=V1
      GO TO 300
  155 IF (TIME1.GT.T2) GO TO 160
      SOURCE(MNAM+2)=V1+(V2-V1)*(1.0-EXP((T1-TIME1)/TAU1))
      GO TO 300
  160 SOURCE(MNAM+2)=V1+(V2-V1)*(1.0-EXP((T1-TIME1)/TAU1))
     1   +(V1-V2)*(1.0-EXP((T2-TIME1)/TAU2))
      GO TO 300
C
C  SINUSOIDAL SOURCE
C
  200 V1=SOURCE(MNAM+5)
      V2=SOURCE(MNAM+6)
      T1=SOURCE(MNAM+7)
      THETA=SOURCE(MNAM+8)
      OMEG=SOURCE(MNAM+9)
      TIME1=TIME-T1
      IF (TIME1.GT.0.0) GO TO 220
      SOURCE(MNAM+2)=V1
      GO TO 300
  220 SOURCE(MNAM+2)=V1+V2*(EXP(-TIME1*THETA))*SIN(OMEG*TIME1)
  300 CONTINUE
  350 CONTINUE
      RETURN
      END
      SUBROUTINE ITER8
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      DIMENSION U(1),UL(1)
      EQUIVALENCE (U(1),YNL(402)),(UL(1),YNL(1202))
C
C
      LE=NSTOP-1
      IF (IPASS1.NE.-1) GO TO 40
      IF (LE) 220,175,155
   40 IFINAL=1
      ITERNO=0
   50 NONCON=0
      DO 60 I=1,NUMNOD
      VN(I)=0.0
   60 YNL(I)=0.0
      DO 70 I=IUS,LASTUT
   70 YNL(I)=0.0
      DO 80 I=ILS,LASTLT
   80 YNL(I)=0.0
      CALL LOAD
      IF (IGOOF.EQ.1) GO TO 1000
      IF (LE) 220,175,90
C
C  DECOMPOSITION
C
   90 DO 150 L=1,LE
      KK=IORDER(L)
      IF (ABS(YNL(KK)).GT.GMIN) GO TO 130
      YNL(KK)=GMIN
      WRITE (6,121)
  121 FORMAT (5X,*--- WARNING ---  UNDERFLOW ENCOUNTERED*)
  130 IS=IUR(L)
      IE=IUR(L+1)-1
      IF (IS.GT.IE) GO TO 150
      DO 140 IL=IS,IE
      UL(IL)=UL(IL)/YNL(KK)
      IO=IUC(IL)
      IDIAG=IORDER(IO)
      YNL(IDIAG)=YNL(IDIAG)-UL(IL)*U(IL)
      DO 135 IU=IS,IE
      JO=IUC(IU)
      IF (IO-JO) 131,135,133
C
C  FIND (IO,JO) MATRIX TERM  (UPPER TRIANGLE)
C
  131 J=IUR(IO+1)
      JE=IUR(IO)
  132 J=J-1
      IF (J.LT.JE) GO TO 1000
      IF (IUC(J).NE.JO) GO TO 132
      U(J)=U(J)-UL(IL)*U(IU)
      GO TO 135
C
C  FIND (IO,JO) MATRIX TERM (LOWER TRIANGLE)
C
  133 J=IUR(JO+1)
      JE=IUR(JO)
  134 J=J-1
      IF (J.LT.JE) GO TO 1000
      IF (IUC(J).NE.IO) GO TO 134
      UL(J)=UL(J)-UL(IL)*U(IU)
  135 CONTINUE
  140 CONTINUE
  150 CONTINUE
C
C  FORWARD SUBSTITUTION
C
  155 DO 170 J=1,LE
      JCS=IUR(J)
      JCE=IUR(J+1)-1
      IF (JCE.LT.JCS) GO TO 170
      JB=IORDER(J)
      DO 160 I=JCS,JCE
      II=IUC(I)
      IB=IORDER(II)
      VN(IB)=VN(IB)-VN(JB)*UL(I)
  160 CONTINUE
  170 CONTINUE
C
C  BACK SUBSTITUTION
C
  175 IO=IORDER(NSTOP)
      IF (ABS(YNL(IO)).GT.GMIN) GO TO 178
      YNL(IO)=GMIN
      WRITE (6,121)
  178 VN(IO)=VN(IO)/YNL(IO)
      IF (LE.EQ.0) GO TO 220
      DO 190 I=1,LE
      IO=IORDER(NSTOP-I)
      JS=IUR(NSTOP-I)
      JE=IUR(NSTOP-I+1)-1
      IF (JE.LT.JS) GO TO 185
      DO 180 J=JS,JE
      JJ=IUC(J)
      JO=IORDER(JJ)
      VN(IO)=VN(IO)-U(J)*VN(JO)
  180 CONTINUE
  185 VN(IO)=VN(IO)/YNL(IO)
  190 CONTINUE
C
C  SET SOURCE NODES TO THEIR VOLTAGE VALUE
C
  220 VN(1)=0.0
      IF (IPASS1.EQ.-1) RETURN
      IF (NUMVS.EQ.0) GO TO 250
      DO 240 I=1,NUMVS
      IELNUM=MATLOC(NUMVS-I+1)
      IF (IELNUM.LT.LOCATE(6)) GO TO 225
      ISPOT=ICURNT(6)+2*(IELNUM-LOCATE(6))
      MNAM=MNAME(IELNUM)
      VAL=SOURCE(MNAM+MODE-1)
      GO TO 230
  225 VAL=0.0
      ISPOT=ICURNT(3)+2*(IELNUM-LOCATE(3))
  230 LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      SIGN=NODPLC(LOC+2)
      TRACUR(ISPOT)=VN(NODE1)
      VN(NODE1)=VN(NODE2)+SIGN*VAL
  240 CONTINUE
C
C  CHECK CONVERGENCE
C
  250 ITERNO=ITERNO+1
      IPASS1=0
      IF (IFINAL.EQ.0) GO TO 265
      DO 260 I=2,NUMNOD
      TEMP1=ABS(VN(I))
      IF (TEMP1.GT.1.0E20) IGOOF=1
      TEMP2=ABS(VNIM1(I))
      TEMP3=ABS(VN(I)-VNIM1(I))
      VMIN=TEMP1
      IF (TEMP2.LT.VMIN) VMIN=TEMP2
      VMIN=VMIN*PERTOL
      IF (VMIN.LT.VNTOL) VMIN=VNTOL
      IF (TEMP3.GT.VMIN) NONCON=NONCON+1
      VNIM1(I)=VN(I)
  260 CONTINUE
      IF (IGOOF.EQ.1) RETURN
      IF (NONCON.GT.0) GO TO 275
      RETURN
  265 IFINAL=1
      DO 270 I=2,NUMNOD
      IF (ABS(VN(I)).GT.1.0E20) IGOOF=1
      VNIM1(I)=VN(I)
  270 CONTINUE
      IF (IGOOF.EQ.1) RETURN
  275 IF (ITERNO.LT.101) GO TO 50
      IGOOF=1
      RETURN
 1000 IGOOF=1
      WRITE (6,1001)
 1001 FORMAT (//5X,*----------  PROGRAM ERROR IN MATRIX INVERSION*//)
      RETURN
      END
      SUBROUTINE CALCUR(IELNUM,CREAL)
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
C
C
      DIMENSION UL(1)
      EQUIVALENCE (UL(1),YNL(1202))
C
C
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      IPNOD=NODPLC(LOC+3)
      IF (IELNUM.LT.LOCATE(6)) GO TO 10
      ISPOT=ICURNT(6)+2*(IELNUM-LOCATE(6))
      SIGN=NODPLC(LOC+2)
      GO TO 20
   10 ISPOT=ICURNT(3)+2*(IELNUM-LOCATE(3))
      SIGN=1.0
   20 CREAL=TRACUR(ISPOT)-YNL(NODE1)*VNIM1(NODE1)
      ISTART=IUR(IPNOD)
      ISTOP=IUR(IPNOD+1)-1
      IF (ISTART.GT.ISTOP) GO TO 40
      DO 30 I=ISTART,ISTOP
      J=IUC(I)
      JO=IORDER(J)
      CREAL=CREAL-UL(I)*VNIM1(JO)
   30 CONTINUE
   40 CREAL=SIGN*CREAL
      RETURN
      END
      SUBROUTINE LOAD
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      DIMENSION U(1),UL(1)
      EQUIVALENCE (U(1),YNL(402)),(UL(1),YNL(1202))
      DIMENSION VCAP(1),VIND(1),CCAP(1),CIND(1)
      EQUIVALENCE (VCAP(1),VIND(1),TRACUR(1))
      EQUIVALENCE (CCAP(1),CIND(1),TRACUR(2))
C
C
      IFIND=NUMVS
C
C  RESISTORS
C
      ISTOP=LOCATE(2)-1
      IF (ISTOP.LT.1) GO TO 20
      DO 10 I=1,ISTOP
      LOC=LOCAL(I)
      VAL=VALUE(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=MATLOC(IFIND+1)
      LOC4=MATLOC(IFIND+2)
      IFIND=IFIND+2
      YNL(LOC1)=YNL(LOC1)+VAL
      YNL(LOC2)=YNL(LOC2)+VAL
      YNL(LOC3)=YNL(LOC3)-VAL
      YNL(LOC4)=YNL(LOC4)-VAL
   10 CONTINUE
C
C  CAPACITORS
C
   20 IF (MODE.GT.1) GO TO 25
      IFIND=IFIND+2*JELCNT(2)
      GO TO 100
   25 ISTART=LOCATE(2)
      ISTOP=LOCATE(3)-1
      IF (ISTART.GT.ISTOP) GO TO 60
      ISPOT=ICURNT(2)
      DO 50 I=ISTART,ISTOP
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=MATLOC(IFIND+1)
      LOC4=MATLOC(IFIND+2)
      IFIND=IFIND+2
      IF (MODE.EQ.3) GO TO 40
      GEQ=VALUE(I)*2.0/DELOLD
      CEQ=VCAP(ISPOT)*GEQ+CCAP(ISPOT)
      VCAP(ISPOT)=VNIM1(LOC1)-VNIM1(LOC2)
      CCAP(ISPOT)=VCAP(ISPOT)*GEQ-CEQ
   40 GEQ=VALUE(I)*2.0/DELTA
      CEQ=VCAP(ISPOT)*GEQ+CCAP(ISPOT)
      ISPOT=ISPOT+2
      VN(LOC1)=VN(LOC1)+CEQ
      VN(LOC2)=VN(LOC2)-CEQ
      YNL(LOC1)=YNL(LOC1)+GEQ
      YNL(LOC2)=YNL(LOC2)+GEQ
      YNL(LOC3)=YNL(LOC3)-GEQ
      YNL(LOC4)=YNL(LOC4)-GEQ
   50 CONTINUE
C
C  INDUCTORS
C
   60 ISTART=LOCATE(3)
      ISTOP=LOCATE(4)-1
      IF (ISTART.GT.ISTOP) GO TO 100
      ISPOT=ICURNT(3)
      DO 90 I=ISTART,ISTOP
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=MATLOC(IFIND+1)
      LOC4=MATLOC(IFIND+2)
      IFIND=IFIND+2
      IF (MODE.EQ.3) GO TO 80
      GEQ=VALUE(I)*DELOLD/2.0
      CEQ=-VIND(ISPOT)*GEQ-CIND(ISPOT)
      VIND(ISPOT)=VNIM1(LOC1)-VNIM1(LOC2)
      CIND(ISPOT)=VIND(ISPOT)*GEQ-CEQ
   80 GEQ=VALUE(I)*DELTA/2.0
      CEQ=-VIND(ISPOT)*GEQ-CIND(ISPOT)
      ISPOT=ISPOT+2
      VN(LOC1)=VN(LOC1)+CEQ
      VN(LOC2)=VN(LOC2)-CEQ
      YNL(LOC1)=YNL(LOC1)+GEQ
      YNL(LOC2)=YNL(LOC2)+GEQ
      YNL(LOC3)=YNL(LOC3)-GEQ
      YNL(LOC4)=YNL(LOC4)-GEQ
   90 CONTINUE
C
C  VOLTAGE DEPENDENT CURRENT SOURCES
C
  100 ISTART=LOCATE(5)
      ISTOP=LOCATE(6)-1
      IF (ISTART.GT.ISTOP) GO TO 500
      DO 120 I=ISTART,ISTOP
      LOC1=MATLOC(IFIND+1)
      LOC2=MATLOC(IFIND+2)
      LOC3=MATLOC(IFIND+3)
      LOC4=MATLOC(IFIND+4)
      IFIND=IFIND+4
      VAL=VALUE(I)
      YNL(LOC1)=YNL(LOC1)+VAL
      YNL(LOC2)=YNL(LOC2)-VAL
      YNL(LOC3)=YNL(LOC3)-VAL
      YNL(LOC4)=YNL(LOC4)+VAL
  120 CONTINUE
C
C  CALL DEVICE MODEL ROUTINES
C
  500 CALL BJT
      CALL DIODE
      CALL JFET
      CALL MOSFET
C
C  CURRENT SOURCES
C
      IF (MODE.GT.3) MODE=3
      ISTART=LOCATE(4)
      ISTOP=LOCATE(5)-1
      IF (ISTART.GT.ISTOP) GO TO 600
      DO 520 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      VAL=SOURCE(MNAM+MODE-1)
      VN(NODE1)=VN(NODE1)-VAL
      VN(NODE2)=VN(NODE2)+VAL
  520 CONTINUE
C
C  VOLTAGE SOURCES (AND INDUCTORS IN DC ANALYSIS)
C
  600 IF (NUMVS.EQ.0) RETURN
      DO 900 I=1,NUMVS
      IELNUM=MATLOC(I)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      SIGN=NODPLC(LOC+2)
      IPNOD=NODPLC(LOC+3)
      INNOD=NODPLC(LOC+4)
C
C  ALTER Y MATRIX
C
      JSTART=IUR(IPNOD)
      JSTOP=IUR(IPNOD+1)-1
      IF (NODE2.EQ.1) GO TO 800
      YNL(NODE2)=YNL(NODE2)+YNL(NODE1)
      IF (JSTART.GT.JSTOP) GO TO 800
      DO 700 J=JSTART,JSTOP
C
C  FOR EACH (N+,I) TERM, FIND THE CORRESPONDING (N-,I) TERM
C
      JO=IUC(J)
      IF (INNOD-JO) 650,610,620
C
C  DIAGONAL (N-,N-) TERM
C
  610 YNL(NODE2)=YNL(NODE2)+U(J)+UL(J)
      GO TO 700
C
C  UPPER TRIANGLE (INNOD,JO) TERM  INNOD.LT.JO OR INNOD A SOURCE NODE
C
  620 KFLAG=1
      IF (INNOD.LE.NSTOP) GO TO 660
  630 K=IUR(INNOD+1)
      KSTOP=IUR(INNOD)
  640 K=K-1
      IF (K.LT.KSTOP) GO TO 1000
      IF (IUC(K).NE.JO) GO TO 640
      IF (KFLAG) 690,1000,680
C
C  LOWER TRIANGLE (JO,INNOD) TERM  JO.LT.INNOD OR JO A SOURCE NODE
C
  650 KFLAG=-1
      IF (JO.LE.NSTOP) GO TO 630
  660 K=IUR(JO+1)
      KSTOP=IUR(JO)
  670 K=K-1
      IF (K.LT.KSTOP) GO TO 1000
      IF (IUC(K).NE.INNOD) GO TO 670
      IF (KFLAG) 690,1000,680
C
C  ADD LOWER N+ TERM TO LOWER N- TERM AND UPPER N+ TERM TO
C  UPPER N- TERM
C
  680 U(K)=U(K)+U(J)
      UL(K)=UL(K)+UL(J)
      GO TO 700
C
C  ADD LOWER N+ TERM TO UPPER N- TERM AND UPPER N+ TERM TO
C  LOWER N- TERM
C
  690 U(K)=U(K)+UL(J)
      UL(K)=UL(K)+U(J)
  700 CONTINUE
C
C  ALTER CURRENT VECTOR
C
  800 IF (IELNUM.LT.LOCATE(6)) GO TO 810
      MNAM=MNAME(IELNUM)
      VAL=SIGN*SOURCE(MNAM+MODE-1)
      GO TO 820
  810 VAL=0.0
  820 VN(NODE2)=VN(NODE2)+VN(NODE1)-YNL(NODE1)*VAL
      IF (VAL) 830,900,830
  830 IF (JSTART.GT.JSTOP) GO TO 900
      DO 850 J=JSTART,JSTOP
      JO=IUC(J)
      NODE3=IORDER(JO)
      VN(NODE3)=VN(NODE3)-U(J)*VAL
  850 CONTINUE
  900 CONTINUE
      RETURN
 1000 IGOOF=1
      WRITE (6,1001)
 1001 FORMAT (//5X,*----------  PROGRAM ERROR ... LOAD*//)
      RETURN
      END
      SUBROUTINE JUNCT(VNEW,VOLD,CSAT,VTE)
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      VLIM1=10.0*VTE
      VLIM2=2.0*VTE
      DELV=VNEW-VOLD
      IF (ABS(DELV).LT.VLIM2) GO TO 30
      IF (DELV.LT.0.0) GO TO 10
      IF (VNEW.LT.VLIM1) GO TO 30
      VNEW=VOLD+VLIM2
      IF (VNEW.LT.VLIM1) VNEW=VLIM1
      GO TO 20
   10 IF (VOLD.LT.VLIM1) GO TO 30
      VNEW=VOLD-VLIM2
   20 IFINAL=0
   30 RETURN
      END
      SUBROUTINE BJT
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MODELS/NUMMOD,MODNAM(25),KIND(25)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      DIMENSION VBEO(1),VBCO(1),CJBE(1),CJBC(1),VCCS(1),CCCS(1)
      EQUIVALENCE (VBEO(1),TRACUR(1)),(VBCO(1),TRACUR(2)),
     1   (CJBE(1),TRACUR(3)),(CJBC(1),TRACUR(4)),(VCCS(1),TRACUR(5)),
     2   (CCCS(1),TRACUR(6))
C
C
      ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) RETURN
      ISPOT=ICURNT(7)
      DO 1000 I=ISTART,ISTOP
C
C  LOOKUP MATRIX POINTERS
C
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      LOC4=NODPLC(LOC+3)
      LOC5=NODPLC(LOC+4)
      LOC6=NODPLC(LOC+5)
      IF (MODE.EQ.2) GO TO 50
      LOC7=MATLOC(IFIND+1)
      LOC8=MATLOC(IFIND+2)
      LOC9=MATLOC(IFIND+3)
      LOC10=MATLOC(IFIND+4)
      LOC11=MATLOC(IFIND+5)
      LOC12=MATLOC(IFIND+6)
      LOC13=MATLOC(IFIND+7)
      LOC14=MATLOC(IFIND+8)
      LOC15=MATLOC(IFIND+9)
      LOC16=MATLOC(IFIND+10)
      LOC17=MATLOC(IFIND+11)
      LOC18=MATLOC(IFIND+12)
      IFIND=IFIND+12
C
C  DC ANALYSIS
C
   50 TYPE=SYMVAL(MNAM,1)
      GBPR=SYMVAL(MNAM,4)
      GCPR=SYMVAL(MNAM,5)
      GEPR=SYMVAL(MNAM,6)
      CSAT=SYMVAL(MNAM,12)*VALUE(I)
C
C  INITIALIZE JUNCTIONS ON FIRST DC PASS
C
      IF (IPASS1.EQ.0) GO TO 60
      IF (MODE.GT.1) GO TO 55
      VBE=VT*ALOG(VT/(1.414*CSAT))
      VBC=-1.0
      GO TO 70
   55 VBE=VBEO(ISPOT)
      VBC=VBCO(ISPOT)
      GO TO 80
   60 VBE=TYPE*(VNIM1(LOC5)-VNIM1(LOC6))
      VBC=TYPE*(VNIM1(LOC5)-VNIM1(LOC4))
      CALL JUNCT(VBE,VBEO(ISPOT),CSAT,VT)
      CALL JUNCT(VBC,VBCO(ISPOT),CSAT,VT)
   70 VBEO(ISPOT)=VBE
      VBCO(ISPOT)=VBC
C
C  EBERS-MOLL TRANSISTOR MODEL
C
   80 IF (KIND(MNAM).EQ.2) GO TO 200
      BF=SYMVAL(MNAM,2)
      BR=SYMVAL(MNAM,3)
      OVA=SYMVAL(MNAM,15)
C
C  COMPUTE EQUIVALENT DC CONDUCTANCES
C
      EVBE=EXP(VBE/VT)
      EVBC=EXP(VBC/VT)
      FVA=1.0-OVA*VBC
      CBE=CSAT*(EVBE-1.0)
      CBC=CSAT*(EVBC-1.0)
      GSAT=CSAT/VT
      GPI=(GSAT/BF)*EVBE
      IF (GPI.LT.GMIN) GPI=GMIN
      GMU=(GSAT/BR)*EVBC
      IF (GMU.LT.GMIN) GMU=GMIN
      GO=(CBE-CBC)*OVA+GSAT*EVBC*FVA
      GM=GSAT*EVBE*FVA-GO
      GMPGO=GM+GO
      CC=(CBE-CBC)*FVA-CBC/BR
      CB=CBE/BF+CBC/BR
      CEQBC=TYPE*(CC-VBE*(GMPGO)+VBC*(GMU+GO))
      CEQBE=TYPE*(-CC-CB+VBE*GMPGO+VBE*GPI-VBC*GO)
C
C  ZERO TRANSIENT CONDUCTANCES IN DC ANALYSIS
C
      IF (MODE.GT.1) GO TO 110
      TRACUR(ISPOT+2)=CC
      TRACUR(ISPOT+3)=CB
      GCCS=0.0
      CEQCS=0.0
      GO TO 500
C
C  CHARGE STORAGE ELEMENTS
C
  110 CCS=SYMVAL(MNAM,7)
      TF=SYMVAL(MNAM,8)
      TR=SYMVAL(MNAM,9)
      CZBE=SYMVAL(MNAM,10)*VALUE(I)
      CZBC=SYMVAL(MNAM,11)*VALUE(I)
      PE=SYMVAL(MNAM,13)
      PC=SYMVAL(MNAM,14)
C
C  COMPUTE DEPLETION LAYER VOLTAGES
C
      ARGBE=PE-VBE
      IF (ARGBE.LT.0.1) ARGBE=0.1
      SARGBE=SQRT(ARGBE)
      ARGBC=PC-VBC
      IF (ARGBC.LT.0.1) ARGBC=0.1
      SARGBC=SQRT(ARGBC)
C
C  SMALL SIGNAL PARAMETERS
C
      IF (MODE.GT.2) GO TO 150
      TRACUR(ISPOT+4)=GPI
      TRACUR(ISPOT+5)=TF*(CBE+CSAT)/VT+CZBE/SARGBE
      TRACUR(ISPOT+6)=GMU
      TRACUR(ISPOT+7)=TR*(CBC+CSAT)/VT+CZBC/SARGBC
      TRACUR(ISPOT+8)=GM
      TRACUR(ISPOT+9)=GO
      GO TO 900
C
C  TRANSIENT ANALYSIS
C
  150 CTBE=(2.0*TF*CBE-4.0*CZBE*SARGBE)/DELTA
      CTBC=(2.0*TR*CBC-4.0*CZBC*SARGBC)/DELTA
C
C  INITIALIZE ON FIRST TRANSIENT PASS
C
      IF (IPASS1.EQ.0) GO TO 160
      CJBE(ISPOT)=CTBE
      CJBC(ISPOT)=CTBC
      VCCS(ISPOT)=VNIM1(LOC4)
      CCCS(ISPOT)=0.0
C
C  UPDATE AT FIRST ITERATION OF NEW TIMEPOINT
C
  160 IF (MODE.EQ.3) GO TO 170
      CJBE(ISPOT)=CTBE*(1.0+DELTA/DELOLD)-CJBE(ISPOT)
      CJBC(ISPOT)=CTBC*(1.0+DELTA/DELOLD)-CJBC(ISPOT)
      CCCS(ISPOT)=(2.0*CCS/DELOLD)*(VNIM1(LOC4)-VCCS(ISPOT))-CCCS(ISPOT)
      VCCS(ISPOT)=VNIM1(LOC4)
C
C  COMPUTE CAPACITOR EQUIVALENT CONDUCTANCES AND CURRENT SOURCES
C
  170 GTEMP=(2.0*TF*(CSAT+CBE)/VT+2.0*CZBE/SARGBE)/DELTA
      GPI=GPI+GTEMP
      CEQBE=CEQBE+TYPE*(GTEMP*VBE-CTBE+CJBE(ISPOT))
      GTEMP=(2.0*TR*(CSAT+CBC)/VT+2.0*CZBC/SARGBC)/DELTA
      GMU=GMU+GTEMP
      CEQBC=CEQBC+TYPE*(GTEMP*VBC-CTBC+CJBC(ISPOT))
      GCCS=2.0*CCS/DELTA
      CEQCS=VCCS(ISPOT)*GCCS+CCCS(ISPOT)
      GO TO 500
C
C  GUMMEL-POON TRANSISTOR MODEL
C
  200 C1=SYMVAL(MNAM,2)*VALUE(I)
      C3=SYMVAL(MNAM,3)*VALUE(I)
      OVA=SYMVAL(MNAM,13)
      OVB=SYMVAL(MNAM,14)
      C2=SYMVAL(MNAM,15)*VALUE(I)
      OIK=SYMVAL(MNAM,16)/VALUE(I)
      VTE=SYMVAL(MNAM,17)
      C4=SYMVAL(MNAM,18)*VALUE(I)
      OIKR=SYMVAL(MNAM,19)/VALUE(I)
      VTC=SYMVAL(MNAM,20)
      EVE=EXP(VBE/VT)
      EVEN=EXP(VBE/VTE)
      EVC=EXP(VBC/VT)
      EVCN=EXP(VBC/VTC)
      CBE=CSAT*(EVE-1.0)
      CBC=CSAT*(EVC-1.0)
C
C  DETERMINE BASE CHARGE TERMS
C
      Q1=1.0+OVA*VBC+OVB*VBE
      Q2=OIK*CBE+OIKR*CBC
      SQARG=SQRT(Q1*Q1+4.*Q2)
      QB=(Q1+SQARG)/2.
      DQBDVE=(OVB+(1.0/SQARG)*(Q1*OVB+2.0*OIK*(CSAT+CBE)/VT))/2.0
      DQBDVC=(OVA+(1.0/SQARG)*(Q1*OVA+2.0*OIKR*(CSAT+CBC)/VT))/2.0
      IF (QB.GT.1.0E-5) GO TO 210
      QB=1.E-5
      DQBDVE=0.
      DQBDVC=0.
C
C  DETERMINE DC INCREMENTAL CONDUCTANCES
C
  210 CS=CSAT/QB
      CC=CS*(EVE-EVC)-C3*(EVC-1.0)-C4*(EVCN-1.0)
      CB=C1*(EVE-1.0)+C2*(EVEN-1.0)+C3*(EVC-1.0)+C4*(EVCN-1.0)
      GPI=(C1/VT)*EVE+(C2/VTE)*EVEN
      IF (GPI.LT.GMIN) GPI=GMIN
      GMU=(C3/VT)*EVC+(C4/VTC)*EVCN
      IF (GMU.LT.GMIN) GMU=GMIN
      GO=CS*(EVC/VT+(EVE-EVC)*DQBDVC/QB)
      GM=CS*(EVE/VT-(EVE-EVC)*DQBDVE/QB)-GO
      GMPGO=GM+GO
      CEQBC=TYPE*(CC-VBE*(GMPGO)+VBC*(GMU+GO))
      CEQBE=TYPE*(-CC-CB+VBE*GMPGO+VBE*GPI-VBC*GO)
C
C  ZERO TRANSIENT CONDUCTANCES IN DC ANALYSIS
C
      IF (MODE.GT.1) GO TO 310
      TRACUR(ISPOT+2)=CC
      TRACUR(ISPOT+3)=CB
      GCCS=0.0
      CEQCS=0.0
      GO TO 500
C
C  CHARGE STORAGE ELEMENTS
C
  310 CCS=SYMVAL(MNAM,7)
      TF=SYMVAL(MNAM,8)
      TR=SYMVAL(MNAM,9)
      CZBE=SYMVAL(MNAM,10)*VALUE(I)
      CZBC=SYMVAL(MNAM,11)*VALUE(I)
      PE=SYMVAL(MNAM,21)
      XME=SYMVAL(MNAM,22)
      PC=SYMVAL(MNAM,23)
      XMC=SYMVAL(MNAM,24)
C
C  COMPUTE DEPLETION LAYER VOLTAGES
C
      ARGBE=PE-VBE
      IF (ARGBE.LT.0.1) ARGBE=0.1
      SARGBE=EXP(XME*ALOG(ARGBE))
      ARGBC=PC-VBC
      IF (ARGBC.LT.0.1) ARGBC=0.1
      SARGBC=EXP(XMC*ALOG(ARGBC))
C
C  SMALL SIGNAL PARAMETERS
C
      IF (MODE.GT.2) GO TO 350
      TRACUR(ISPOT+4)=GPI
      TRACUR(ISPOT+5)=TF*(CBE+CSAT)/VT+CZBE/SARGBE
      TRACUR(ISPOT+6)=GMU
      TRACUR(ISPOT+7)=TR*(CBC+CSAT)/VT+CZBC/SARGBC
      TRACUR(ISPOT+8)=GM
      TRACUR(ISPOT+9)=GO
      GO TO 900
C
C  TRANSIENT ANALYSIS
C
  350 CTBE=(2.0*TF*CBE-2.0*CZBE*ARGBE/(SARGBE*(1.0-XME)))/DELTA
      CTBC=(2.0*TR*CBC-2.0*CZBC*ARGBC/(SARGBC*(1.0-XMC)))/DELTA
C
C  INITIALIZE ON FIRST TRANSIENT PASS
C
      IF (IPASS1.EQ.0) GO TO 360
      CJBE(ISPOT)=CTBE
      CJBC(ISPOT)=CTBC
      VCCS(ISPOT)=VNIM1(LOC4)
      CCCS(ISPOT)=0.0
C
C  UPDATE ON FIRST ITERATION OF EACH TIMEPOINT
C
  360 IF (MODE.EQ.3) GO TO 370
      CJBE(ISPOT)=CTBE*(1.0+DELTA/DELOLD)-CJBE(ISPOT)
      CJBC(ISPOT)=CTBC*(1.0+DELTA/DELOLD)-CJBC(ISPOT)
      CCCS(ISPOT)=(2.0*CCS/DELOLD)*(VNIM1(LOC4)-VCCS(ISPOT))-CCCS(ISPOT)
      VCCS(ISPOT)=VNIM1(LOC4)
C
C  COMPUTE CAPACITOR EQUIVALENT CONDUCTANCES AND CURRENT SOURCES
C
  370 GTEMP=(2.0*TF*(CSAT+CBE)/VT+2.0*CZBE/SARGBE)/DELTA
      GPI=GPI+GTEMP
      CEQBE=CEQBE+TYPE*(GTEMP*VBE-CTBE+CJBE(ISPOT))
      GTEMP=(2.0*TR*(CSAT+CBC)/VT+2.0*CZBC/SARGBC)/DELTA
      GMU=GMU+GTEMP
      CEQBC=CEQBC+TYPE*(GTEMP*VBC-CTBC+CJBC(ISPOT))
      GCCS=2.0*CCS/DELTA
      CEQCS=GCCS*VCCS(ISPOT)+CCCS(ISPOT)
C
C  LOAD CURRENT EXCITATION VECTOR
C
  500 VN(LOC4)=VN(LOC4)+CEQCS-CEQBC
      VN(LOC5)=VN(LOC5)+CEQBC+CEQBE
      VN(LOC6)=VN(LOC6)-CEQBE
C
C  LOAD Y MATRIX, REAL TERMS
C
      YNL(LOC1)=YNL(LOC1)+GCPR
      YNL(LOC2)=YNL(LOC2)+GBPR
      YNL(LOC3)=YNL(LOC3)+GEPR
      YNL(LOC4)=YNL(LOC4)+GMU+GO+GCPR+GCCS
      YNL(LOC5)=YNL(LOC5)+GBPR+GPI+GMU
      YNL(LOC6)=YNL(LOC6)+GPI+GEPR+GMPGO
      YNL(LOC7)=YNL(LOC7)-GCPR
      YNL(LOC8)=YNL(LOC8)-GBPR
      YNL(LOC9)=YNL(LOC9)-GEPR
      YNL(LOC10)=YNL(LOC10)-GCPR
      YNL(LOC11)=YNL(LOC11)-GMU+GM
      YNL(LOC12)=YNL(LOC12)-GMPGO
      YNL(LOC13)=YNL(LOC13)-GBPR
      YNL(LOC14)=YNL(LOC14)-GMU
      YNL(LOC15)=YNL(LOC15)-GPI
      YNL(LOC16)=YNL(LOC16)-GEPR
      YNL(LOC17)=YNL(LOC17)-GO
      YNL(LOC18)=YNL(LOC18)-GPI-GM
  900 ISPOT=ISPOT+15
 1000 CONTINUE
      RETURN
      END
      SUBROUTINE DIODE
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      DIMENSION VDO(1),CJN(1)
      EQUIVALENCE (VDO(1),TRACUR(1)),(CJN(1),TRACUR(2))
C
C
      ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) RETURN
      ISPOT=ICURNT(8)
      DO 1000 I=ISTART,ISTOP
C
C  LOOKUP MATRIX POINTERS
C
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      IF (MODE.EQ.2) GO TO 50
      LOC4=MATLOC(IFIND+1)
      LOC5=MATLOC(IFIND+2)
      LOC6=MATLOC(IFIND+3)
      LOC7=MATLOC(IFIND+4)
      IFIND=IFIND+4
C
C
C  DC ANALYSIS
C
   50 GSPR=SYMVAL(MNAM,1)
      CSAT=SYMVAL(MNAM,4)*VALUE(I)
      VTE=SYMVAL(MNAM,5)
C
C  INITIALIZE ON FIRST DC PASS
C
      IF (IPASS1.EQ.0) GO TO 60
      IF (MODE.GT.1) GO TO 55
      VD=VTE*ALOG(VTE/(1.414*CSAT))
      GO TO 70
   55 VD=VDO(ISPOT)
      GO TO 80
C
C  COMPUTE NEW JUNCTION VOLTAGE
C
   60 VD=VNIM1(LOC3)-VNIM1(LOC2)
      CALL JUNCT(VD,VDO(ISPOT),CSAT,VTE)
   70 VDO(ISPOT)=VD
C
C  COMPUTE EQUIVALENT DC CONDUCTANCES
C
   80 EVD=EXP(VD/VTE)
      CD=CSAT*(EVD-1.0)
      GEQ=(CSAT/VTE)*EVD
      IF (GEQ.LT.GMIN) GEQ=GMIN
      CDEQ=GEQ*VD-CD
      IF (MODE.GT.1) GO TO 310
      TRACUR(ISPOT+1)=CD
      GO TO 500
C
C  CHARGE STORAGE ELEMENTS
C
  310 TAU=SYMVAL(MNAM,2)
      CZERO=SYMVAL(MNAM,3)*VALUE(I)
      PHIB=SYMVAL(MNAM,6)
C
C  COMPUTE DEPLETION LAYER VOLTAGE
C
      ARG=PHIB-VD
      IF (ARG.LT.0.1) ARG=0.1
      SARG=SQRT(ARG)
C
C  SMALL SIGNAL PARAMETERS
C
      IF (MODE.GT.2) GO TO 350
      TRACUR(ISPOT+2)=GEQ
      TRACUR(ISPOT+3)=TAU*(CD+CSAT)/VTE+CZERO/SARG
      GO TO 900
C
C  TRANSIENT ANALYSIS
C
  350 CTEMP=(2.0*TAU*CD-4.0*CZERO*SARG)/DELTA
C
C  INITIALIZE ON FIRST TRANSIENT PASS
C
      IF (IPASS1.EQ.0) GO TO 360
      CJN(ISPOT)=CTEMP
C
C  UPDATE AT FIRST ITERATION OF NEW TIMEPOINT
C
  360 IF (MODE.EQ.3) GO TO 370
      CJN(ISPOT)=CTEMP*(1.0+DELTA/DELOLD)-CJN(ISPOT)
C
C  COMPUTE CAPACITOR CONDUCTANCE AND EQUIVALENT CURRENT SOURCE
C
  370 GTEMP=(2.0*TAU*(CD+CSAT)/VTE+2.0*CZERO/SARG)/DELTA
      GEQ=GEQ+GTEMP
      CDEQ=CDEQ+GTEMP*VD+CJN(ISPOT)-CTEMP
C
C  LOAD CURRENT VECTOR
C
  500 VN(LOC2)=VN(LOC2)-CDEQ
      VN(LOC3)=VN(LOC3)+CDEQ
C
C  LOAD MATRIX
C
      YNL(LOC1)=YNL(LOC1)+GSPR
      YNL(LOC2)=YNL(LOC2)+GEQ
      YNL(LOC3)=YNL(LOC3)+GEQ+GSPR
      YNL(LOC4)=YNL(LOC4)-GSPR
      YNL(LOC5)=YNL(LOC5)-GEQ
      YNL(LOC6)=YNL(LOC6)-GSPR
      YNL(LOC7)=YNL(LOC7)-GEQ
  900 ISPOT=ISPOT+5
 1000 CONTINUE
      RETURN
      END
      SUBROUTINE JFET
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      DIMENSION VGSO(1),VGDO(1),CDRO(1),CJGS(1),CJGD(1)
      EQUIVALENCE (VGSO(1),TRACUR(1)),(VGDO(1),TRACUR(2)),
     1   (CDRO(1),TRACUR(3)),(CJGS(1),TRACUR(4)),(CJGD(1),TRACUR(5))
C
C
      ISTART=LOCATE(9)
      ISTOP=LOCATE(10)-1
      IF (ISTART.GT.ISTOP) RETURN
      ISPOT=ICURNT(9)
      DO 1000 I=ISTART,ISTOP
C
C  LOOKUP MATRIX POINTERS
C
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      LOC4=NODPLC(LOC+3)
      LOC5=NODPLC(LOC+4)
      IF (MODE.EQ.2) GO TO 50
      LOC6=MATLOC(IFIND+1)
      LOC7=MATLOC(IFIND+2)
      LOC8=MATLOC(IFIND+3)
      LOC9=MATLOC(IFIND+4)
      LOC10=MATLOC(IFIND+5)
      LOC11=MATLOC(IFIND+6)
      LOC12=MATLOC(IFIND+7)
      LOC13=MATLOC(IFIND+8)
      LOC14=MATLOC(IFIND+9)
      LOC15=MATLOC(IFIND+10)
      IFIND=IFIND+10
C
C  DC ANALYSIS
C
   50 TYPE=SYMVAL(MNAM,1)
      VTO=SYMVAL(MNAM,2)
      BETA=SYMVAL(MNAM,3)*VALUE(I)
      XLAMB=SYMVAL(MNAM,4)
      GDPR=SYMVAL(MNAM,5)
      GSPR=SYMVAL(MNAM,6)
      CSAT=SYMVAL(MNAM,10)
C
C  INITIALIZE ON FIRST DC PASS
C
      IF (IPASS1.EQ.0) GO TO 60
      IF (MODE.GT.1) GO TO 55
      CDRO(ISPOT)=0.0
      VGS=-1.0
      VGD=-1.0
      GO TO 70
   55 VGS=VGSO(ISPOT)
      VGD=VGDO(ISPOT)
      GO TO 80
C
C  COMPUTE INTERNAL BRANCH VOLTAGES AND GATE JUNCTION MODELS
C
   60 VGS=TYPE*(VNIM1(LOC2)-VNIM1(LOC5))
      VGD=TYPE*(VNIM1(LOC2)-VNIM1(LOC4))
      CALL JUNCT(VGS,VGSO(ISPOT),CSAT,VT)
      CALL JUNCT(VGD,VGDO(ISPOT),CSAT,VT)
   70 VGSO(ISPOT)=VGS
      VGDO(ISPOT)=VGD
   80 CTEMP=CSAT*(EXP(VGS/VT)-1.0)
      GGS=(CSAT+CTEMP)/VT
      IF (GGS.LT.GMIN) GGS=GMIN
      CEQGS=TYPE*(VGS*GGS-CTEMP)
      CTEMP=CSAT*(EXP(VGD/VT)-1.0)
      GGD=(CSAT+CTEMP)/VT
      IF (GGD.LT.GMIN) GGD=GMIN
      CEQGD=TYPE*(VGD*GGD-CTEMP)
      VDS=VGS-VGD
C
C  COMPUTE DRAIN CURRENT AND DERIVITIVES FOR NORMAL MODE
C
      IF (VDS.LT.0.0) GO TO 200
      VGST=VGS-VTO
C
C  NORMAL MODE, CUTOFF REGION
C
      IF (VGST.GT.0.0) GO TO 110
      CDRAIN=0.0
      GM=0.0
      GDS=0.0
      GO TO 300
C
C  NORMAL MODE, SATURATION REGION
C
  110 IF (VGST.GT.VDS) GO TO 120
      CDRAIN=BETA*VGST*(VGST+XLAMB*VDS)
      GM=BETA*(2.0*VGST+XLAMB*VDS)
      GDS=BETA*XLAMB*VGST
      GO TO 300
C
C  NORMAL MODE, LINEAR REGION
C
  120 CDRAIN=BETA*VDS*((XLAMB+2.0)*VGST-VDS)
      GM=BETA*(XLAMB+2.0)*VDS
      GDS=BETA*((XLAMB+2.0)*VGST-2.0*VDS)
      GO TO 300
C
C  COMPUTE DRAIN CURRENT AND DERIVITIVES FOR INVERSE MODE
C
  200 VGST=VGD-VTO
C
C  INVERSE MODE, CUTOFF REGION
C
      IF (VGST.GT.0.0) GO TO 210
      CDRAIN=0.0
      GM=0.0
      GDS=0.0
      GO TO 300
C
C  INVERSE MODE, SATURATION REGION
C
  210 IF (VGST.GT.-VDS) GO TO 220
      CDRAIN=-BETA*VGST*(VGST-XLAMB*VDS)
      GM=-BETA*(2.0*VGST-XLAMB*VDS)
      GDS=BETA*XLAMB*VGST-GM
      GO TO 300
C
C  INVERSE MODE, LINEAR REGION
C
  220 CDRAIN=BETA*VDS*((XLAMB+2.0)*VGST+VDS)
      GM=BETA*VDS*(2.0+XLAMB)
      GDS=BETA*((XLAMB+2.0)*VGST+2.0*VDS)-GM
C
C  COMPUTE EQUIVALENT DRAIN CURRENT SOURCE, CHECK CHANGE IN DRAIN
C  CURRENT FROM LAST ITERATION, AND STORE DC RESULTS
C
  300 CDREQ=TYPE*(VGS*GM+VDS*GDS-CDRAIN)
      TOL=PERTOL*ABS(CDRAIN)
      IF (TOL.LT.1.0E-9) TOL=1.0E-9
      DIFF=ABS(CDRAIN-CDRO(ISPOT))
      IF (DIFF.GT.TOL) IFINAL=0
      CDRO(ISPOT)=CDRAIN
C
C  CHARGE STORAGE ELEMENTS
C
      IF (MODE.EQ.1) GO TO 500
      CZGS=SYMVAL(MNAM,7)*VALUE(I)
      CZGD=SYMVAL(MNAM,8)*VALUE(I)
      PHIB=SYMVAL(MNAM,9)
C
C  COMPUTE CAPACITOR VOLTAGES
C
      ARGGS=PHIB-VGS
      IF (ARGGS.LT.0.1) ARGGS=0.1
      SARGGS=SQRT(ARGGS)
      ARGGD=PHIB-VGD
      IF (ARGGD.LT.0.1) ARGGD=0.1
      SARGGD=SQRT(ARGGD)
C
C  SMALL SIGNAL PARAMETERS
C
      IF (MODE.GT.2) GO TO 350
      TRACUR(ISPOT+3)=GM
      TRACUR(ISPOT+4)=GDS
      TRACUR(ISPOT+5)=GGS
      TRACUR(ISPOT+6)=CZGS/SARGGS
      TRACUR(ISPOT+7)=GGD
      TRACUR(ISPOT+8)=CZGD/SARGGD
      GO TO 900
C
C  TRANSIENT ANALYSIS
C
  350 CTGS=-4.0*CZGS*SARGGS/DELTA
      CTGD=-4.0*CZGD*SARGGD/DELTA
C
C  INITIALIZE ON FIRST PASS
C
      IF (IPASS1.EQ.0) GO TO 360
      CJGS(ISPOT)=CTGS
      CJGD(ISPOT)=CTGD
C
C  UPDATE AT FIRST ITERATION OF EACH TIMEPOINT
C
  360 IF (MODE.EQ.3) GO TO 370
      CJGS(ISPOT)=CTGS*(1.0+DELTA/DELOLD)-CJGS(ISPOT)
      CJGD(ISPOT)=CTGD*(1.0+DELTA/DELOLD)-CJGD(ISPOT)
C
C  COMPUTE CAPACITOR CONDUCTANCES AND EQUIVALENT CURRENT SOURCES
C
  370 GTEMP=2.0*CZGS/(SARGGS*DELTA)
      GGS=GGS+GTEMP
      CEQGS=CEQGS+TYPE*(GTEMP*VGS-CTGS+CJGS(ISPOT))
      GTEMP=2.0*CZGD/(SARGGD*DELTA)
      GGD=GGD+GTEMP
      CEQGD=CEQGD+TYPE*(GTEMP*VGD-CTGD+CJGD(ISPOT))
C
C  LOAD CURRENT VECTOR
C
  500 VN(LOC2)=VN(LOC2)+CEQGS+CEQGD
      VN(LOC4)=VN(LOC4)+CDREQ-CEQGD
      VN(LOC5)=VN(LOC5)-CDREQ-CEQGS
C
C  LOAD Y MATRIX
C
      YNL(LOC1)=YNL(LOC1)+GDPR
      YNL(LOC2)=YNL(LOC2)+GGD+GGS
      YNL(LOC3)=YNL(LOC3)+GSPR
      YNL(LOC4)=YNL(LOC4)+GDPR+GDS+GGD
      YNL(LOC5)=YNL(LOC5)+GSPR+GDS+GM+GGS
      YNL(LOC6)=YNL(LOC6)-GDPR
      YNL(LOC7)=YNL(LOC7)-GGD
      YNL(LOC8)=YNL(LOC8)-GGS
      YNL(LOC9)=YNL(LOC9)-GSPR
      YNL(LOC10)=YNL(LOC10)-GDPR
      YNL(LOC11)=YNL(LOC11)+GM-GGD
      YNL(LOC12)=YNL(LOC12)-GDS-GM
      YNL(LOC13)=YNL(LOC13)-GGS-GM
      YNL(LOC14)=YNL(LOC14)-GSPR
      YNL(LOC15)=YNL(LOC15)-GDS
  900 ISPOT=ISPOT+10
 1000 CONTINUE
      RETURN
      END
      SUBROUTINE MOSFET
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      DIMENSION VBDO(1),VBSO(1),CDRO(1),CJBD(1),CJBS(1),VCGS(1),
     1   CCGS(1),VCGD(1),CCGD(1),VCGB(1),CCGB(1)
      EQUIVALENCE (VBDO(1),TRACUR(1)),(VBSO(1),TRACUR(2)),
     1   (CDRO(1),TRACUR(3)),(CJBD(1),TRACUR(4)),(CJBS(1),TRACUR(5)),
     2   (VCGS(1),TRACUR(6)),(CCGS(1),TRACUR(7)),(VCGD(1),TRACUR(8)),
     3   (CCGD(1),TRACUR(9)),(VCGB(1),TRACUR(10)),(CCGB(1),TRACUR(11))
C
C
      ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) RETURN
      ISPOT=ICURNT(10)
      DO 1000 I=ISTART,ISTOP
C
C  LOOKUP MATRIX POINTERS
C
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      LOC4=NODPLC(LOC+3)
      LOC5=NODPLC(LOC+4)
      LOC6=NODPLC(LOC+5)
      IF (MODE.EQ.2) GO TO 50
      LOC7=MATLOC(IFIND+1)
      LOC8=MATLOC(IFIND+2)
      LOC9=MATLOC(IFIND+3)
      LOC10=MATLOC(IFIND+4)
      LOC11=MATLOC(IFIND+5)
      LOC12=MATLOC(IFIND+6)
      LOC13=MATLOC(IFIND+7)
      LOC14=MATLOC(IFIND+8)
      LOC15=MATLOC(IFIND+9)
      LOC16=MATLOC(IFIND+10)
      LOC17=MATLOC(IFIND+11)
      LOC18=MATLOC(IFIND+12)
      LOC19=MATLOC(IFIND+13)
      LOC20=MATLOC(IFIND+14)
      LOC21=MATLOC(IFIND+15)
      LOC22=MATLOC(IFIND+16)
      IFIND=IFIND+16
C
C  DC ANALYSIS
C
   50 TYPE=SYMVAL(MNAM,1)
      VTO=SYMVAL(MNAM,2)
      PHI=SYMVAL(MNAM,3)
      BETA=SYMVAL(MNAM,4)*VALUE(I)
      GAMMA=SYMVAL(MNAM,5)
      XLAMB=SYMVAL(MNAM,6)
      GDPR=SYMVAL(MNAM,7)
      GSPR=SYMVAL(MNAM,8)
      CSAT=SYMVAL(MNAM,15)
C
C  INITIALIZE ON FIRST DC PASS
C
      IF (IPASS1.EQ.0) GO TO 60
      IF (MODE.GT.1) GO TO 55
      CDRO(ISPOT)=0.0
      VBS=-1.0
      VBD=-1.0
      GO TO 70
   55 VBS=VBSO(ISPOT)
      VBD=VBDO(ISPOT)
      GO TO 80
C
C  DETERMINE INTERNAL BRANCH VOLTAGES AND SUBSTRATE JUNCTION MODELS
C
   60 VBD=TYPE*(VNIM1(LOC4)-VNIM1(LOC5))
      VBS=TYPE*(VNIM1(LOC4)-VNIM1(LOC6))
      CALL JUNCT(VBD,VBDO(ISPOT),CSAT,VT)
      CALL JUNCT(VBS,VBSO(ISPOT),CSAT,VT)
   70 VBDO(ISPOT)=VBD
      VBSO(ISPOT)=VBS
   80 CTEMP=CSAT*(EXP(VBD/VT)-1.0)
      GBD=(CSAT+CTEMP)/VT
      IF (GBD.LT.GMIN) GBD=GMIN
      CEQBD=TYPE*(VBD*GBD-CTEMP)
      CTEMP=CSAT*(EXP(VBS/VT)-1.0)
      GBS=(CSAT+CTEMP)/VT
      IF (GBS.LT.GMIN) GBS=GMIN
      CEQBS=TYPE*(VBS*GBS-CTEMP)
      VDS=VBS-VBD
      VGS=TYPE*(VNIM1(LOC2)-VNIM1(LOC6))
      VGD=VGS-VDS
      VGB=VGS-VBS
C
C  COMPUTE DRAIN CURRENT AND DERIVITIVES FOR NORMAL MODE
C
      IF (VDS.LT.0.0) GO TO 200
      ARG=PHI-VBS
      IF (ARG.LT.0.1) ARG=0.1
      SARG=SQRT(ARG)
      VGST=VGS-VTO-GAMMA*(SARG-SQRT(PHI))
C
C  NORMAL MODE, CUTOFF REGION
C
      IF (VGST.GT.0.0) GO TO 110
      CDRAIN=0.0
      GM=0.0
      GDS=0.0
      GMBS=0.0
      GO TO 300
C
C  NORMAL MODE, SATURATION REGION
C
  110 IF (VGST.GT.VDS) GO TO 120
      CDRAIN=BETA*VGST*(VGST+XLAMB*VDS)
      GM=BETA*(2.0*VGST+XLAMB*VDS)
      GDS=BETA*XLAMB*VGST
      GMBS=GM*GAMMA/(2.0*SARG)
      GO TO 300
C
C  NORMAL MODE, LINEAR REGION
C
  120 CDRAIN=BETA*VDS*((XLAMB+2.0)*VGST-VDS)
      GM=BETA*(XLAMB+2.0)*VDS
      GDS=BETA*((XLAMB+2.0)*VGST-2.0*VDS)
      GMBS=GM*GAMMA/(2.0*SARG)
      GO TO 300
C
C  COMPUTE DRAIN CURRENT AND DERIVITIVES FOR INVERSE MODE
C
  200 ARG=VDS+PHI-VBS
      IF (ARG.LT.0.1) ARG=0.1
      SARG=SQRT(ARG)
      VGST=VGS-VDS-VTO-GAMMA*(SARG-SQRT(PHI))
C
C  INVERSE MODE, CUTOFF REGION
C
      IF (VGST.GT.0.0) GO TO 210
      CDRAIN=0.0
      GM=0.0
      GDS=0.0
      GMBS=0.0
      GO TO 300
C
C  INVERSE MODE, SATURATION REGION
C
  210 IF (VGST.GT.-VDS) GO TO 220
      CDRAIN=-BETA*VGST*(VGST-XLAMB*VDS)
      GM=-BETA*(2.0*VGST-XLAMB*VDS)
      GDS=BETA*XLAMB*VGST-GM*(1.0+GAMMA/(2.0*SARG))
      GMBS=GM*GAMMA/(2.0*SARG)
      GO TO 300
C
C  INVERSE MODE, LINEAR REGION
C
  220 CDRAIN=BETA*VDS*((2.0+XLAMB)*VGST+VDS)
      GM=BETA*VDS*(2.0+XLAMB)
      GDS=BETA*((2.0+XLAMB)*VGST+2.0*VDS)-GM*(1.0-GAMMA/(2.0*SARG))
      GMBS=GM*GAMMA/(2.0*SARG)
C
C  COMPUTE EQUIVALENT DRAIN CURRENT SOURCE, CHECK CHANGE IN DRAIN
C  CURRENT FROM LAST ITERATION, STORE DC RESULTS, AND ZERO TRANSIENT
C  CONDUCTANCES
C
  300 CDREQ=TYPE*(VGS*GM+VDS*GDS+VBS*GMBS-CDRAIN)
      TOL=PERTOL*ABS(CDRAIN)
      IF (TOL.LT.1.0E-9) TOL=1.0E-9
      DIFF=ABS(CDRAIN-CDRO(ISPOT))
      IF (DIFF.GT.TOL) IFINAL=0
      CDRO(ISPOT)=CDRAIN
      IF (MODE.GT.1) GO TO 310
      GCGS=0.0
      CEQGS=0.0
      GCGD=0.0
      CEQGD=0.0
      GCGB=0.0
      CEQGB=0.0
      GO TO 500
C
C  CHARGE STORAGE ELEMENTS
C
  310 CGS=SYMVAL(MNAM,9)*VALUE(I)
      CGD=SYMVAL(MNAM,10)*VALUE(I)
      CGB=SYMVAL(MNAM,11)*VALUE(I)
      CZBD=SYMVAL(MNAM,12)*VALUE(I)
      CZBS=SYMVAL(MNAM,13)*VALUE(I)
      PHIB=SYMVAL(MNAM,14)
C
C  COMPUTE CAPACITOR VOLTAGES
C
      ARGBD=PHIB-VBD
      IF (ARGBD.LT.0.1) ARGBD=0.1
      SARGBD=SQRT(ARGBD)
      ARGBS=PHIB-VBS
      IF (ARGBS.LT.0.1) ARGBS=0.1
      SARGBS=SQRT(ARGBS)
C
C  SMALL SIGNAL PARAMETERS
C
      IF (MODE.GT.2) GO TO 350
      TRACUR(ISPOT+3)=GM
      TRACUR(ISPOT+4)=GDS
      TRACUR(ISPOT+5)=GMBS
      TRACUR(ISPOT+6)=GBD
      TRACUR(ISPOT+7)=CZBD/SARGBD
      TRACUR(ISPOT+8)=GBS
      TRACUR(ISPOT+9)=CZBS/SARGBS
      GO TO 900
C
C  TRANSIENT ANALYSIS
C
  350 CTBD=-4.0*CZBD*SARGBD/DELTA
      CTBS=-4.0*CZBS*SARGBS/DELTA
C
C  INITIALIZE ON FIRST PASS
C
      IF (IPASS1.EQ.0) GO TO 360
      VCGS(ISPOT)=VGS
      CCGS(ISPOT)=0.0
      VCGD(ISPOT)=VGD
      CCGD(ISPOT)=0.0
      VCGB(ISPOT)=VGB
      CCGB(ISPOT)=0.0
      CJBD(ISPOT)=CTBD
      CJBS(ISPOT)=CTBS
C
C  UPDATE AT FIRST ITERATION OF EACH TIMEPOINT
C
  360 IF (MODE.EQ.3) GO TO 370
      CCGS(ISPOT)=(2.0*CGS/DELOLD)*(VGS-VCGS(ISPOT))-CCGS(ISPOT)
      VCGS(ISPOT)=VGS
      CCGD(ISPOT)=(2.0*CGD/DELOLD)*(VGD-VCGD(ISPOT))-CCGD(ISPOT)
      VCGD(ISPOT)=VGD
      CCGB(ISPOT)=(2.0*CGB/DELOLD)*(VGB-VCGB(ISPOT))-CCGB(ISPOT)
      VCGB(ISPOT)=VGB
      CJBD(ISPOT)=CTBD*(1.0+DELTA/DELOLD)-CJBD(ISPOT)
      CJBS(ISPOT)=CTBS*(1.0+DELTA/DELOLD)-CJBS(ISPOT)
C
C  COMPUTE CAPACITOR CONDUCTANCES AND EQUIVALENT CURRENT SOURCES
C
  370 GCGS=2.0*CGS/DELTA
      CEQGS=TYPE*(VCGS(ISPOT)*GCGS+CCGS(ISPOT))
      GCGD=2.0*CGD/DELTA
      CEQGD=TYPE*(VCGD(ISPOT)*GCGD+CCGD(ISPOT))
      GCGB=2.0*CGB/DELTA
      CEQGB=TYPE*(VCGB(ISPOT)*GCGB+CCGB(ISPOT))
      GTEMP=2.0*CZBD/(SARGBD*DELTA)
      GBD=GBD+GTEMP
      CEQBD=CEQBD+TYPE*(GTEMP*VBD-CTBD+CJBD(ISPOT))
      GTEMP=2.0*CZBS/(SARGBS*DELTA)
      GBS=GBS+GTEMP
      CEQBS=CEQBS+TYPE*(GTEMP*VBS-CTBS+CJBS(ISPOT))
C
C  LOAD CURRENT VECTOR
C
  500 VN(LOC2)=VN(LOC2)+CEQGS+CEQGD+CEQGB
      VN(LOC4)=VN(LOC4)+CEQBS+CEQBD-CEQGB
      VN(LOC5)=VN(LOC5)+CDREQ-CEQGD-CEQBD
      VN(LOC6)=VN(LOC6)-CDREQ-CEQGS-CEQBS
C
C  LOAD Y MATRIX
C
      YNL(LOC1)=YNL(LOC1)+GDPR
      YNL(LOC2)=YNL(LOC2)+GCGD+GCGS+GCGB
      YNL(LOC3)=YNL(LOC3)+GSPR
      YNL(LOC4)=YNL(LOC4)+GBD+GBS+GCGB
      YNL(LOC5)=YNL(LOC5)+GDPR+GDS+GCGD+GBD
      YNL(LOC6)=YNL(LOC6)+GSPR+GDS+GCGS+GBS+GM+GMBS
      YNL(LOC7)=YNL(LOC7)-GDPR
      YNL(LOC8)=YNL(LOC8)-GCGB
      YNL(LOC9)=YNL(LOC9)-GCGD
      YNL(LOC10)=YNL(LOC10)-GCGS
      YNL(LOC11)=YNL(LOC11)-GSPR
      YNL(LOC12)=YNL(LOC12)-GCGB
      YNL(LOC13)=YNL(LOC13)-GBD
      YNL(LOC14)=YNL(LOC14)-GBS
      YNL(LOC15)=YNL(LOC15)-GDPR
      YNL(LOC16)=YNL(LOC16)-GCGD+GM
      YNL(LOC17)=YNL(LOC17)-GBD+GMBS
      YNL(LOC18)=YNL(LOC18)-GDS-GM-GMBS
      YNL(LOC19)=YNL(LOC19)-GCGS-GM
      YNL(LOC20)=YNL(LOC20)-GSPR
      YNL(LOC21)=YNL(LOC21)-GBS-GMBS
      YNL(LOC22)=YNL(LOC22)-GDS
  900 ISPOT=ISPOT+15
 1000 CONTINUE
      RETURN
      END
      SUBROUTINE ACAN
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
      COMMON/AC/JACFLG,FSTART,FSTOP,IDFREQ,FINCR,INOISE,NOSPRT,
     1   NOSOUT,NOSIN
C
C
      DIMENSION PUTOUT(2,101,5)
      EQUIVALENCE (PUTOUT(1,1,1),ROUT(1,1))
      DIMENSION YPARTS(2,1),VNPART(2,1)
      COMPLEX CVAL,CYNL(1),CVN(1),CU(1),CUL(1)
      EQUIVALENCE (CYNL(1),YPARTS(1,1),YNL(1))
      EQUIVALENCE (CVN(1),VNPART(1,1),VN(1))
      EQUIVALENCE (CU(1),CYNL(402)),(CUL(1),CYNL(1202))
C
C
      CALL SECOND(T1)
      LE=NSTOP-1
      ICALC=0
      FREQ1=FSTART
C
C  LOAD MATRIX
C
    1 OMEGA=TWOPI*FREQ1
      DO 2 I=1,NUMNOD
      VNPART(1,I)=0.0
      VNPART(2,I)=0.0
      YPARTS(1,I)=0.0
    2 YPARTS(2,I)=0.0
      DO 3 I=IUS,LASTUT
      YPARTS(1,I)=0.0
    3 YPARTS(2,I)=0.0
      DO 4 I=ILS,LASTLT
      YPARTS(1,I)=0.0
    4 YPARTS(2,I)=0.0
      CALL ACLOAD
      IF (IGOOF.EQ.1) GO TO 1000
      IF (LE) 100,75,10
C
C  DECOMPOSITION
C
   10 DO 50 L=1,LE
      KK=IORDER(L)
      RY=ABS(YPARTS(1,KK))
      XY=ABS(YPARTS(2,KK))
      IF ((RY+XY).GT.GMIN) GO TO 30
      YPARTS(1,KK)=GMIN
      YPARTS(2,KK)=0.0
      WRITE (6,21)
   21 FORMAT (5X,*--- WARNING ---  UNDERFLOW ENCOUNTERED*)
   30 IS=IUR(L)
      IE=IUR(L+1)-1
      IF (IS.GT.IE) GO TO 50
      DO 40 IL=IS,IE
      CUL(IL)=CUL(IL)/CYNL(KK)
      IO=IUC(IL)
      IDIAG=IORDER(IO)
      CYNL(IDIAG)=CYNL(IDIAG)-CUL(IL)*CU(IL)
      DO 35 IU=IS,IE
      JO=IUC(IU)
      IF (IO-JO) 31,35,33
C
C  FIND (IO,JO) MATRIX TERM (UPPER TRIANGLE)
C
   31 J=IUR(IO+1)
      JE=IUR(IO)
   32 J=J-1
      IF (J.LT.JE) GO TO 1000
      IF (IUC(J).NE.JO) GO TO 32
      CU(J)=CU(J)-CUL(IL)*CU(IU)
      GO TO 35
C
C  FIND (IO,JO) MATRIX TERM (LOWER TRIANGLE)
C
   33 J=IUR(JO+1)
      JE=IUR(JO)
   34 J=J-1
      IF (J.LT.JE) GO TO 1000
      IF (IUC(J).NE.IO) GO TO 34
      CUL(J)=CUL(J)-CUL(IL)*CU(IU)
   35 CONTINUE
   40 CONTINUE
   50 CONTINUE
C
C  FORWARD SUBSTITUTION
C
      DO 70 J=1,LE
      JCS=IUR(J)
      JCE=IUR(J+1)-1
      IF (JCE.LT.JCS) GO TO 70
      JB=IORDER(J)
      DO 60 I=JCS,JCE
      II=IUC(I)
      IB=IORDER(II)
      CVN(IB)=CVN(IB)-CVN(JB)*CUL(I)
   60 CONTINUE
   70 CONTINUE
C
C  BACK SUBSTITUTION
C
   75 IO=IORDER(NSTOP)
      RY=ABS(YPARTS(1,IO))
      XY=ABS(YPARTS(2,IO))
      IF ((RY+XY).GT.GMIN) GO TO 78
      YPARTS(1,IO)=GMIN
      YPARTS(2,IO)=0.0
      WRITE (6,21)
   78 CVN(IO)=CVN(IO)/CYNL(IO)
      IF (LE.EQ.0) GO TO 100
      DO 90 I=1,LE
      IO=IORDER(NSTOP-I)
      JS=IUR(NSTOP-I)
      JE=IUR(NSTOP-I+1)-1
      IF (JE.LT.JS) GO TO 85
      DO 80 J=JS,JE
      JJ=IUC(J)
      JO=IORDER(JJ)
      CVN(IO)=CVN(IO)-CU(J)*CVN(JO)
   80 CONTINUE
   85 CVN(IO)=CVN(IO)/CYNL(IO)
   90 CONTINUE
C
C  SET SOURCE NODES TO THEIR VOLTAGE VALUE
C
  100 VNPART(1,1)=0.0
      VNPART(2,1)=0.0
      IF (NUMVS.EQ.0) GO TO 135
      DO 110 I=1,NUMVS
      IELNUM=MATLOC(NUMVS-I+1)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      SIGN=NODPLC(LOC+2)
      MNAM=MNAME(IELNUM)
      ISPOT=ICURNT(6)+2*(IELNUM-LOCATE(6))
      TRACUR(ISPOT)=VNPART(1,NODE1)
      TRACUR(ISPOT+1)=VNPART(2,NODE1)
      CVAL=SIGN*CMPLX(SOURCE(MNAM+1),SOURCE(MNAM+4))
      CVN(NODE1)=CVN(NODE2)+CVAL
  110 CONTINUE
C
C  STORE OUTPUT
C
  135 ICALC=ICALC+1
      FREQ(ICALC)=FREQ1
      IKNT=0
  140 IKNT=IKNT+1
      IF (IKNT.GT.NUMOR(3)) GO TO 160
      JKNT=IACVAR(IKNT)
      IF (IOFLG(JKNT).EQ.3) GO TO 140
      IPNOD=IOPND(JKNT)
      IF (IOFLG(JKNT).EQ.2) GO TO 150
      INNOD=IONND(JKNT)
      PUTOUT(1,ICALC,IKNT)=VNPART(1,IPNOD)-VNPART(1,INNOD)
      PUTOUT(2,ICALC,IKNT)=VNPART(2,IPNOD)-VNPART(2,INNOD)
      GO TO 140
  150 CALL ACCAL(IPNOD,CREAL,CIMAG)
      PUTOUT(1,ICALC,IKNT)=CREAL
      PUTOUT(2,ICALC,IKNT)=CIMAG
      GO TO 140
  160 IF (NOSOUT.NE.0) CALL NOISE
C
C  INCREMENT FREQUENCY
C
      IF (IDFREQ.GT.1) GO TO 180
      FREQ1=FREQ1+FINCR
      GO TO 190
  180 FREQ1=FREQ1*FINCR
  190 IF (ICALC-JACFLG) 1,1100,1100
C
C  FINISHED
C
 1000 NOGO=1
      WRITE (6,1001)
 1001 FORMAT (//5X,*----------  PROGRAM ERROR IN MATRIX INVERSION*//)
 1100 CALL SECOND(T2)
      RTIMES(7)=RTIMES(7)+T2-T1
      RETURN
      END
      SUBROUTINE ACLOAD
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
      COMMON/ITER/GMIN,PERTOL,VNTOL,IPASS1,IFINAL,ITERNO,IFIND
C
C
      COMPLEX CVAL,CYNL(1),CVN(1),CU(1),CUL(1)
      DIMENSION YPARTS(2,1),VNPART(2,1)
      EQUIVALENCE (CYNL(1),YPARTS(1,1),YNL(1))
      EQUIVALENCE (CVN(1),VNPART(1,1),VN(1))
      EQUIVALENCE (CU(1),CYNL(402)),(CUL(1),CYNL(1202))
C
C
      IFIND=NUMVS
C
C  RESISTORS
C
      ISTOP=LOCATE(2)-1
      IF (ISTOP.LT.1) GO TO 20
      DO 10 I=1,ISTOP
      LOC=LOCAL(I)
      VAL=VALUE(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=MATLOC(IFIND+1)
      LOC4=MATLOC(IFIND+2)
      IFIND=IFIND+2
      YPARTS(1,LOC1)=YPARTS(1,LOC1)+VAL
      YPARTS(1,LOC2)=YPARTS(1,LOC2)+VAL
      YPARTS(1,LOC3)=YPARTS(1,LOC3)-VAL
      YPARTS(1,LOC4)=YPARTS(1,LOC4)-VAL
   10 CONTINUE
C
C  CAPACITORS
C
   20 ISTART=LOCATE(2)
      ISTOP=LOCATE(3)-1
      DO 30 I=ISTART,ISTOP
      IF (ISTART.GT.ISTOP) GO TO 40
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=MATLOC(IFIND+1)
      LOC4=MATLOC(IFIND+2)
      IFIND=IFIND+2
      VAL=VALUE(I)*OMEGA
      YPARTS(2,LOC1)=YPARTS(2,LOC1)+VAL
      YPARTS(2,LOC2)=YPARTS(2,LOC2)+VAL
      YPARTS(2,LOC3)=YPARTS(2,LOC3)-VAL
      YPARTS(2,LOC4)=YPARTS(2,LOC4)-VAL
   30 CONTINUE
C
C  INDUCTORS
C
   40 ISTART=LOCATE(3)
      ISTOP=LOCATE(4)-1
      IF (ISTART.GT.ISTOP) GO TO 60
      DO 50 I=ISTART,ISTOP
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=MATLOC(IFIND+1)
      LOC4=MATLOC(IFIND+2)
      IFIND=IFIND+2
      VAL=-VALUE(I)/OMEGA
      YPARTS(2,LOC1)=YPARTS(2,LOC1)+VAL
      YPARTS(2,LOC2)=YPARTS(2,LOC2)+VAL
      YPARTS(2,LOC3)=YPARTS(2,LOC3)-VAL
      YPARTS(2,LOC4)=YPARTS(2,LOC4)-VAL
   50 CONTINUE
C
C  VOLTAGE DEPENDENT CURRENT SOURCES
C
   60 ISTART=LOCATE(5)
      ISTOP=LOCATE(6)-1
      IF (ISTART.GT.ISTOP) GO TO 80
      DO 70 I=ISTART,ISTOP
      LOC1=MATLOC(IFIND+1)
      LOC2=MATLOC(IFIND+2)
      LOC3=MATLOC(IFIND+3)
      LOC4=MATLOC(IFIND+4)
      IFIND=IFIND+4
      VAL=VALUE(I)
      MNAM=MNAME(I)
      ARG=OMEGA*SOURCE(MNAM)
      XVAL=-VAL*SIN(ARG)
      VAL=VAL*COS(ARG)
      CVAL=CMPLX(VAL,XVAL)
      CYNL(LOC1)=CYNL(LOC1)+CVAL
      CYNL(LOC2)=CYNL(LOC2)-CVAL
      CYNL(LOC3)=CYNL(LOC3)-CVAL
      CYNL(LOC4)=CYNL(LOC4)+CVAL
   70 CONTINUE
C
C  BJTS
C
   80 ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) GO TO 100
      ISPOT=ICURNT(7)
      DO 90 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      LOC4=NODPLC(LOC+3)
      LOC5=NODPLC(LOC+4)
      LOC6=NODPLC(LOC+5)
      LOC7=MATLOC(IFIND+1)
      LOC8=MATLOC(IFIND+2)
      LOC9=MATLOC(IFIND+3)
      LOC10=MATLOC(IFIND+4)
      LOC11=MATLOC(IFIND+5)
      LOC12=MATLOC(IFIND+6)
      LOC13=MATLOC(IFIND+7)
      LOC14=MATLOC(IFIND+8)
      LOC15=MATLOC(IFIND+9)
      LOC16=MATLOC(IFIND+10)
      LOC17=MATLOC(IFIND+11)
      LOC18=MATLOC(IFIND+12)
      IFIND=IFIND+12
      GBPR=SYMVAL(MNAM,4)
      GCPR=SYMVAL(MNAM,5)
      GEPR=SYMVAL(MNAM,6)
      GPI=TRACUR(ISPOT+4)
      XCPI=TRACUR(ISPOT+5)*OMEGA
      GMU=TRACUR(ISPOT+6)
      XCMU=TRACUR(ISPOT+7)*OMEGA
      GM=TRACUR(ISPOT+8)
      GO=TRACUR(ISPOT+9)
      GMPGO=GM+GO
      XCCS=SYMVAL(MNAM,7)*OMEGA
      ISPOT=ISPOT+15
      YPARTS(1,LOC1)=YPARTS(1,LOC1)+GCPR
      YPARTS(1,LOC2)=YPARTS(1,LOC2)+GBPR
      YPARTS(1,LOC3)=YPARTS(1,LOC3)+GEPR
      YPARTS(1,LOC4)=YPARTS(1,LOC4)+GMU+GO+GCPR
      YPARTS(1,LOC5)=YPARTS(1,LOC5)+GBPR+GPI+GMU
      YPARTS(1,LOC6)=YPARTS(1,LOC6)+GPI+GEPR+GMPGO
      YPARTS(1,LOC7)=YPARTS(1,LOC7)-GCPR
      YPARTS(1,LOC8)=YPARTS(1,LOC8)-GBPR
      YPARTS(1,LOC9)=YPARTS(1,LOC9)-GEPR
      YPARTS(1,LOC10)=YPARTS(1,LOC10)-GCPR
      YPARTS(1,LOC11)=YPARTS(1,LOC11)-GMU+GM
      YPARTS(1,LOC12)=YPARTS(1,LOC12)-GMPGO
      YPARTS(1,LOC13)=YPARTS(1,LOC13)-GBPR
      YPARTS(1,LOC14)=YPARTS(1,LOC14)-GMU
      YPARTS(1,LOC15)=YPARTS(1,LOC15)-GPI
      YPARTS(1,LOC16)=YPARTS(1,LOC16)-GEPR
      YPARTS(1,LOC17)=YPARTS(1,LOC17)-GO
      YPARTS(1,LOC18)=YPARTS(1,LOC18)-GPI-GM
      YPARTS(2,LOC4)=YPARTS(2,LOC4)+XCMU+XCCS
      YPARTS(2,LOC5)=YPARTS(2,LOC5)+XCPI+XCMU
      YPARTS(2,LOC6)=YPARTS(2,LOC6)+XCPI
      YPARTS(2,LOC11)=YPARTS(2,LOC11)-XCMU
      YPARTS(2,LOC14)=YPARTS(2,LOC14)-XCMU
      YPARTS(2,LOC15)=YPARTS(2,LOC15)-XCPI
      YPARTS(2,LOC18)=YPARTS(2,LOC18)-XCPI
   90 CONTINUE
C
C  DIODES
C
  100 ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) GO TO 120
      ISPOT=ICURNT(8)
      DO 110 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      LOC4=MATLOC(IFIND+1)
      LOC5=MATLOC(IFIND+2)
      LOC6=MATLOC(IFIND+3)
      LOC7=MATLOC(IFIND+4)
      IFIND=IFIND+4
      GSPR=SYMVAL(MNAM,1)
      GEQ=TRACUR(ISPOT+2)
      XCEQ=TRACUR(ISPOT+3)*OMEGA
      ISPOT=ISPOT+5
      YPARTS(1,LOC1)=YPARTS(1,LOC1)+GSPR
      YPARTS(1,LOC2)=YPARTS(1,LOC2)+GEQ
      YPARTS(1,LOC3)=YPARTS(1,LOC3)+GEQ+GSPR
      YPARTS(1,LOC4)=YPARTS(1,LOC4)-GSPR
      YPARTS(1,LOC5)=YPARTS(1,LOC5)-GEQ
      YPARTS(1,LOC6)=YPARTS(1,LOC6)-GSPR
      YPARTS(1,LOC7)=YPARTS(1,LOC7)-GEQ
      YPARTS(2,LOC2)=YPARTS(2,LOC2)+XCEQ
      YPARTS(2,LOC3)=YPARTS(2,LOC3)+XCEQ
      YPARTS(2,LOC5)=YPARTS(2,LOC5)-XCEQ
      YPARTS(2,LOC7)=YPARTS(2,LOC7)-XCEQ
  110 CONTINUE
C
C  JFETS
C
  120 ISTART=LOCATE(9)
      ISTOP=LOCATE(10)-1
      IF (ISTART.GT.ISTOP) GO TO 140
      ISPOT=ICURNT(9)
      DO 130 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      LOC4=NODPLC(LOC+3)
      LOC5=NODPLC(LOC+4)
      LOC6=MATLOC(IFIND+1)
      LOC7=MATLOC(IFIND+2)
      LOC8=MATLOC(IFIND+3)
      LOC9=MATLOC(IFIND+4)
      LOC10=MATLOC(IFIND+5)
      LOC11=MATLOC(IFIND+6)
      LOC12=MATLOC(IFIND+7)
      LOC13=MATLOC(IFIND+8)
      LOC14=MATLOC(IFIND+9)
      LOC15=MATLOC(IFIND+10)
      IFIND=IFIND+10
      GDPR=SYMVAL(MNAM,5)
      GSPR=SYMVAL(MNAM,6)
      GM=TRACUR(ISPOT+3)
      GDS=TRACUR(ISPOT+4)
      GGS=TRACUR(ISPOT+5)
      XGS=TRACUR(ISPOT+6)*OMEGA
      GGD=TRACUR(ISPOT+7)
      XGD=TRACUR(ISPOT+8)*OMEGA
      ISPOT=ISPOT+10
      YPARTS(1,LOC1)=YPARTS(1,LOC1)+GDPR
      YPARTS(1,LOC2)=YPARTS(1,LOC2)+GGD+GGS
      YPARTS(1,LOC3)=YPARTS(1,LOC3)+GSPR
      YPARTS(1,LOC4)=YPARTS(1,LOC4)+GDPR+GDS+GGD
      YPARTS(1,LOC5)=YPARTS(1,LOC5)+GSPR+GDS+GM+GGS
      YPARTS(1,LOC6)=YPARTS(1,LOC6)-GDPR
      YPARTS(1,LOC7)=YPARTS(1,LOC7)-GGD
      YPARTS(1,LOC8)=YPARTS(1,LOC8)-GGS
      YPARTS(1,LOC9)=YPARTS(1,LOC9)-GSPR
      YPARTS(1,LOC10)=YPARTS(1,LOC10)-GDPR
      YPARTS(1,LOC11)=YPARTS(1,LOC11)+GM-GGD
      YPARTS(1,LOC12)=YPARTS(1,LOC12)-GDS-GM
      YPARTS(1,LOC13)=YPARTS(1,LOC13)-GGS-GM
      YPARTS(1,LOC14)=YPARTS(1,LOC14)-GSPR
      YPARTS(1,LOC15)=YPARTS(1,LOC15)-GDS
      YPARTS(2,LOC2)=YPARTS(2,LOC2)+XGS+XGD
      YPARTS(2,LOC4)=YPARTS(2,LOC4)+XGD
      YPARTS(2,LOC5)=YPARTS(2,LOC5)+XGS
      YPARTS(2,LOC7)=YPARTS(2,LOC7)-XGD
      YPARTS(2,LOC8)=YPARTS(2,LOC8)-XGS
      YPARTS(2,LOC11)=YPARTS(2,LOC11)-XGD
      YPARTS(2,LOC13)=YPARTS(2,LOC13)-XGS
  130 CONTINUE
C
C  MOSFETS
C
  140 ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) GO TO 160
      ISPOT=ICURNT(10)
      DO 150 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      LOC1=NODPLC(LOC)
      LOC2=NODPLC(LOC+1)
      LOC3=NODPLC(LOC+2)
      LOC4=NODPLC(LOC+3)
      LOC5=NODPLC(LOC+4)
      LOC6=NODPLC(LOC+5)
      LOC7=MATLOC(IFIND+1)
      LOC8=MATLOC(IFIND+2)
      LOC9=MATLOC(IFIND+3)
      LOC10=MATLOC(IFIND+4)
      LOC11=MATLOC(IFIND+5)
      LOC12=MATLOC(IFIND+6)
      LOC13=MATLOC(IFIND+7)
      LOC14=MATLOC(IFIND+8)
      LOC15=MATLOC(IFIND+9)
      LOC16=MATLOC(IFIND+10)
      LOC17=MATLOC(IFIND+11)
      LOC18=MATLOC(IFIND+12)
      LOC19=MATLOC(IFIND+13)
      LOC20=MATLOC(IFIND+14)
      LOC21=MATLOC(IFIND+15)
      LOC22=MATLOC(IFIND+16)
      IFIND=IFIND+16
      GDPR=SYMVAL(MNAM,7)
      GSPR=SYMVAL(MNAM,8)
      GM=TRACUR(ISPOT+3)
      GDS=TRACUR(ISPOT+4)
      GMBS=TRACUR(ISPOT+5)
      XCGS=SYMVAL(MNAM,9)*OMEGA*VALUE(I)
      XCGD=SYMVAL(MNAM,10)*OMEGA*VALUE(I)
      XCGB=SYMVAL(MNAM,11)*OMEGA*VALUE(I)
      GBD=TRACUR(ISPOT+6)
      XBD=TRACUR(ISPOT+7)*OMEGA
      GBS=TRACUR(ISPOT+8)
      XBS=TRACUR(ISPOT+9)*OMEGA
      ISPOT=ISPOT+15
      YPARTS(1,LOC1)=YPARTS(1,LOC1)+GDPR
      YPARTS(1,LOC3)=YPARTS(1,LOC3)+GSPR
      YPARTS(1,LOC4)=YPARTS(1,LOC4)+GBD+GBS
      YPARTS(1,LOC5)=YPARTS(1,LOC5)+GDPR+GDS+GBD
      YPARTS(1,LOC6)=YPARTS(1,LOC6)+GSPR+GDS+GBS+GM+GMBS
      YPARTS(1,LOC7)=YPARTS(1,LOC7)-GDPR
      YPARTS(1,LOC11)=YPARTS(1,LOC11)-GSPR
      YPARTS(1,LOC13)=YPARTS(1,LOC13)-GBD
      YPARTS(1,LOC14)=YPARTS(1,LOC14)-GBS
      YPARTS(1,LOC15)=YPARTS(1,LOC15)-GDPR
      YPARTS(1,LOC16)=YPARTS(1,LOC16)+GM
      YPARTS(1,LOC17)=YPARTS(1,LOC17)-GBD+GMBS
      YPARTS(1,LOC18)=YPARTS(1,LOC18)-GDS-GM-GMBS
      YPARTS(1,LOC19)=YPARTS(1,LOC19)-GM
      YPARTS(1,LOC20)=YPARTS(1,LOC20)-GSPR
      YPARTS(1,LOC21)=YPARTS(1,LOC21)-GBS-GMBS
      YPARTS(1,LOC22)=YPARTS(1,LOC22)-GDS
      YPARTS(2,LOC2)=YPARTS(2,LOC2)+XCGD+XCGS+XCGB
      YPARTS(2,LOC4)=YPARTS(2,LOC4)+XBD+XBS+XCGB
      YPARTS(2,LOC5)=YPARTS(2,LOC5)+XCGD+XBD
      YPARTS(2,LOC6)=YPARTS(2,LOC6)+XCGS+XBS
      YPARTS(2,LOC8)=YPARTS(2,LOC8)-XCGB
      YPARTS(2,LOC9)=YPARTS(2,LOC9)-XCGD
      YPARTS(2,LOC10)=YPARTS(2,LOC10)-XCGS
      YPARTS(2,LOC12)=YPARTS(2,LOC12)-XCGB
      YPARTS(2,LOC13)=YPARTS(2,LOC13)-XBD
      YPARTS(2,LOC14)=YPARTS(2,LOC14)-XBS
      YPARTS(2,LOC16)=YPARTS(2,LOC16)-XCGD
      YPARTS(2,LOC17)=YPARTS(2,LOC17)-XBD
      YPARTS(2,LOC19)=YPARTS(2,LOC19)-XCGS
      YPARTS(2,LOC21)=YPARTS(2,LOC21)-XBS
  150 CONTINUE
C
C  CURRENT SOURCES
C
  160 ISTART=LOCATE(4)
      ISTOP=LOCATE(5)-1
      IF (ISTART.GT.ISTOP) GO TO 200
      DO 170 I=ISTART,ISTOP
      MNAM=MNAME(I)
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      CVAL=CMPLX(SOURCE(MNAM+1),SOURCE(MNAM+4))
      CVN(NODE1)=CVN(NODE1)-CVAL
      CVN(NODE2)=CVN(NODE2)+CVAL
  170 CONTINUE
C
C  VOLTAGE SOURCES
C
  200 IF (NUMVS.EQ.0) RETURN
      DO 500 I=1,NUMVS
      IELNUM=MATLOC(I)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      SIGN=NODPLC(LOC+2)
      IPNOD=NODPLC(LOC+3)
      INNOD=NODPLC(LOC+4)
C
C  ALTER Y MATRIX
C
      JSTART=IUR(IPNOD)
      JSTOP=IUR(IPNOD+1)-1
      IF (NODE2.EQ.1) GO TO 400
      CYNL(NODE2)=CYNL(NODE2)+CYNL(NODE1)
      IF (JSTART.GT.JSTOP) GO TO 400
      DO 300 J=JSTART,JSTOP
C
C  FOR EACH (N+,I) TERM, FIND THE CORRESPONDING (N-,I) TERM
C
      JO=IUC(J)
      IF (INNOD-JO) 250,210,220
C
C  DIAGONAL (N-,N-) TERM
C
  210 CYNL(NODE2)=CYNL(NODE2)+CU(J)+CUL(J)
      GO TO 300
C
C  UPPER TRIANGLE (INNOD,JO) TERM  INNOD.LT.JO OR INNOD A SOURCE NODE
C
  220 KFLAG=1
      IF (INNOD.LE.NSTOP) GO TO 260
  230 K=IUR(INNOD+1)
      KSTOP=IUR(INNOD)
  240 K=K-1
      IF (K.LT.KSTOP) GO TO 1000
      IF (IUC(K).NE.JO) GO TO 240
      IF (KFLAG) 290,1000,280
C
C  LOWER TRIANGLE (JO,INNOD) TERM  JO.LT.INNOD OR JO A SOURCE NODE
C
  250 KFLAG=-1
      IF (JO.LE.NSTOP) GO TO 230
  260 K=IUR(JO+1)
      KSTOP=IUR(JO)
  270 K=K-1
      IF (K.LT.KSTOP) GO TO 1000
      IF (IUC(K).NE.INNOD) GO TO 270
      IF (KFLAG) 290,1000,280
C
C  ADD LOWER N+ TERM TO LOWER N- TERM AND UPPER N+ TERM TO
C  UPPER N- TERM
C
  280 CU(K)=CU(K)+CU(J)
      CUL(K)=CUL(K)+CUL(J)
      GO TO 300
C
C  ADD LOWER N+ TERM TO UPPER N- TERM AND UPPER N+ TERM TO
C  LOWER N- TERM
C
  290 CU(K)=CU(K)+CUL(J)
      CUL(K)=CUL(K)+CU(J)
  300 CONTINUE
C
C  ALTER CURRENT VECTOR
C
  400 MNAM=MNAME(IELNUM)
      CVAL=SIGN*CMPLX(SOURCE(MNAM+1),SOURCE(MNAM+4))
      CVN(NODE2)=CVN(NODE2)+CVN(NODE1)-CYNL(NODE1)*CVAL
      IF (SOURCE(MNAM+1)) 410,500,410
  410 IF (JSTART.GT.JSTOP) GO TO 500
      DO 420 J=JSTART,JSTOP
      JO=IUC(J)
      NODE3=IORDER(JO)
      CVN(NODE3)=CVN(NODE3)-CU(J)*CVAL
  420 CONTINUE
  500 CONTINUE
      RETURN
 1000 IGOOF=1
      WRITE (6,1001)
 1001 FORMAT (//5X,*----------  PROGRAM ERROR ... ACLOAD*//)
      RETURN
      END
      SUBROUTINE NOISE
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/PARAM/VALUE(200),SOURCE(150),SYMVAL(25,25)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/AC/JACFLG,FSTART,FSTOP,IDFREQ,FINCR,INOISE,NOSPRT,
     1   NOSOUT,NOSIN
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
C
C
      DIMENSION PUTOUT(2,101,5)
      EQUIVALENCE (PUTOUT(1,1,1),ROUT(1,1))
      DIMENSION VNPART(2,1)
      EQUIVALENCE (VNPART(1,1),VN(1))
C
C
      KILL=0
      IF (ICALC.GT.1) GO TO 10
      FOURKT=4.0*CHARGE*VT
      TWOQ=2.0*CHARGE
      IPNOD=IOPND(NOSOUT)
      INNOD=IONND(NOSOUT)
      KNTR=1
   10 IF (NOSPRT.EQ.0) GO TO 30
      IF (KNTR.GT.ICALC) GO TO 30
      KILL=1
      KNTR=KNTR+NOSPRT
      WRITE (6,11) (JOBNAM(I),I=1,8),TEMPS(ITEMNO),FREQ(ICALC)
   11 FORMAT (1H7/////1X,125(1H*)///1X,8A10,26X,*-----  SPICE  -----*
     1   ///21X,*NOISE ANALYSIS*,41X,*TEMPERATURE  *,F10.3,*  DEG C*///
     2   1X,125(1H*)///5X,*FREQUENCY  *,1PE10.3,*  HZ*)
      WRITE (6,21)
   21 FORMAT (1HX)
C
C  OBTAIN THE ADJOINT CIRCUIT SOLUTION
C
   30 IF (KILL.EQ.1) GO TO 40
      IF (INOISE.EQ.0) RETURN
   40 VNRMS=0.0
      VREAL=VNPART(1,IPNOD)-VNPART(1,INNOD)
      VIMAG=VNPART(2,IPNOD)-VNPART(2,INNOD)
      VOUT=SQRT(VREAL*VREAL+VIMAG*VIMAG)
      IF (VOUT.LT.1.0E-20) VOUT=1.0E-20
      DO 50 I=1,NUMNOD
      VNPART(1,I)=0.0
   50 VNPART(2,I)=0.0
      VNPART(1,IPNOD)=-1.0
      VNPART(1,INNOD)=1.0
      IF (NUMVS.EQ.0) GO TO 70
      DO 60 I=1,NUMVS
      IELNUM=MATLOC(I)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      VNPART(1,NODE2)=VNPART(1,NODE2)+VNPART(1,NODE1)
      VNPART(2,NODE2)=VNPART(2,NODE2)+VNPART(2,NODE1)
   60 CONTINUE
   70 CALL ACASOL
      VNPART(1,1)=0.0
      VNPART(2,1)=0.0
      IF (NUMVS.EQ.0) GO TO 100
      DO 80 I=1,NUMVS
      IELNUM=MATLOC(NUMVS-I+1)
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      VNPART(1,NODE1)=VNPART(1,NODE2)
      VNPART(2,NODE1)=VNPART(2,NODE2)
   80 CONTINUE
C
C  RESISTORS
C
  100 ISTOP=LOCATE(2)-1
      IF (ISTOP.LT.1) GO TO 200
      IF (KILL.EQ.0) GO TO 110
      WRITE (6,101)
  101 FORMAT (////5X,*RESISTOR SQUARED NOISE VOLTAGES (SQ V/HZ)*//
     1   5X,*NAME*,9X,*TOTAL*//)
  110 DO 130 I=1,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      VREAL=VNPART(1,NODE1)-VNPART(1,NODE2)
      VIMAG=VNPART(2,NODE1)-VNPART(2,NODE2)
      VNO1=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*VALUE(I)
      VNRMS=VNRMS+VNO1
      IF (KILL.EQ.0) GO TO 130
      WRITE (6,121) NAME(I),VNO1
  121 FORMAT (5X,R7,6(3X,1PE9.3))
  130 CONTINUE
C
C  BIPOLAR JUNCTION TRANSISTORS
C
  200 ISTART=LOCATE(7)
      ISTOP=LOCATE(8)-1
      IF (ISTART.GT.ISTOP) GO TO 300
      IF (KILL.EQ.0) GO TO 210
      WRITE (6,201)
  201 FORMAT (////5X,*TRANSISTOR SQUARED NOISE VOLTAGES (SQ V/HZ)*//
     1   5X,*NAME*,10X,*RB*,10X,*RC*,10X,*RE*,10X,*IB*,10X,*IC*,9X,
     2   *TOTAL*//)
  210 ISPOT=ICURNT(7)
      DO 220 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE4=NODPLC(LOC+3)
      NODE5=NODPLC(LOC+4)
      NODE6=NODPLC(LOC+5)
      MNAM=MNAME(I)
C
C  BASE RESISTANCE
C
      VREAL=VNPART(1,NODE2)-VNPART(1,NODE5)
      VIMAG=VNPART(2,NODE2)-VNPART(2,NODE5)
      VNO1=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,4)
C
C  COLLECTOR RESISTANCE
C
      VREAL=VNPART(1,NODE1)-VNPART(1,NODE4)
      VIMAG=VNPART(2,NODE1)-VNPART(2,NODE4)
      VNO2=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,5)
C
C  EMITTER RESISTANCE
C
      VREAL=VNPART(1,NODE3)-VNPART(1,NODE6)
      VIMAG=VNPART(2,NODE3)-VNPART(2,NODE6)
      VNO3=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,6)
C
C  BASE CURRENT SHOT NOISE
C
      VREAL=VNPART(1,NODE5)-VNPART(1,NODE6)
      VIMAG=VNPART(2,NODE5)-VNPART(2,NODE6)
      VNO4=(VREAL*VREAL+VIMAG*VIMAG)*TWOQ*ABS(TRACUR(ISPOT+3))
C
C  COLLECTOR CURRENT SHOT NOISE
C
      VREAL=VNPART(1,NODE4)-VNPART(1,NODE6)
      VIMAG=VNPART(2,NODE4)-VNPART(2,NODE6)
      VNO5=(VREAL*VREAL+VIMAG*VIMAG)*TWOQ*ABS(TRACUR(ISPOT+2))
      ISPOT=ISPOT+15
      VNTOT=VNO1+VNO2+VNO3+VNO4+VNO5
      VNRMS=VNRMS+VNTOT
      IF (KILL.EQ.0) GO TO 220
      WRITE (6,121) NAME(I),VNO1,VNO2,VNO3,VNO4,VNO5,VNTOT
  220 CONTINUE
C
C  JUNCTION DIODES
C
  300 ISTART=LOCATE(8)
      ISTOP=LOCATE(9)-1
      IF (ISTART.GT.ISTOP) GO TO 400
      IF (KILL.EQ.0) GO TO 310
      WRITE (6,301)
  301 FORMAT (////5X,*DIODE SQUARED NOISE VOLTAGES (SQ V/HZ)*//
     1   5X,*NAME*,10X,*RS*,10X,*ID*,9X,*TOTAL*//)
  310 DO 320 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      MNAM=MNAME(I)
C
C  OHMIC RESISTANCE
C
      VREAL=VNPART(1,NODE1)-VNPART(1,NODE3)
      VIMAG=VNPART(2,NODE1)-VNPART(2,NODE3)
      VNO1=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,1)
C
C  JUNCTION CURRENT SHOT NOISE
C
      VREAL=VNPART(1,NODE3)-VNPART(1,NODE2)
      VIMAG=VNPART(2,NODE3)-VNPART(2,NODE2)
      VNO2=(VREAL*VREAL+VIMAG*VIMAG)*TWOQ*ABS(TRACUR(ISPOT+1))
      ISPOT=ISPOT+5
      VNTOT=VNO1+VNO2
      VNRMS=VNRMS+VNTOT
      IF (KILL.EQ.0) GO TO 320
      WRITE (6,121) NAME(I),VNO1,VNO2,VNTOT
  320 CONTINUE
C
C  JFETS
C
  400 ISTART=LOCATE(9)
      ISTOP=LOCATE(10)-1
      IF (ISTART.GT.ISTOP) GO TO 500
      IF (KILL.EQ.0) GO TO 410
      WRITE (6,401)
  401 FORMAT (////5X,*JFET SQUARED NOISE VOLTAGES (SQ V/HZ)*//
     1   5X,*NAME*,10X,*RD*,10X,*RS*,10X,*ID*,9X,*TOTAL*//)
  410 ISPOT=ICURNT(9)
      DO 420 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE4=NODPLC(LOC+3)
      NODE5=NODPLC(LOC+4)
      MNAM=MNAME(I)
C
C  DRAIN RESISTANCE
C
      VREAL=VNPART(1,NODE1)-VNPART(1,NODE4)
      VIMAG=VNPART(2,NODE1)-VNPART(2,NODE4)
      VNO1=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,5)
C
C  SOURCE RESISTANCE
C
      VREAL=VNPART(1,NODE3)-VNPART(1,NODE5)
      VIMAG=VNPART(2,NODE3)-VNPART(2,NODE5)
      VNO2=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,6)
C
C  DRAIN CURRENT SHOT NOISE
C
      VREAL=VNPART(1,NODE4)-VNPART(1,NODE5)
      VIMAG=VNPART(2,NODE4)-VNPART(2,NODE5)
      VNO3=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*2.0*ABS(TRACUR(ISPOT+3))/3.0
      ISPOT=ISPOT+10
      VNTOT=VNO1+VNO2+VNO3
      VNRMS=VNRMS+VNTOT
      IF (KILL.EQ.0) GO TO 420
      WRITE (6,121) NAME(I),VNO1,VNO2,VNO3,VNTOT
  420 CONTINUE
C
C  MOSFETS
C
  500 ISTART=LOCATE(10)
      ISTOP=LOCATE(11)-1
      IF (ISTART.GT.ISTOP) GO TO 600
      IF (KILL.EQ.0) GO TO 510
      WRITE (6,501)
  501 FORMAT (////5X,*MOSFET SQUARED VOISE VOLTAGES (SQ V/HZ)*//
     1   5X,*NAME*,10X,*RD*,10X,*RS*,10X,*ID*,9X,*TOTAL*//)
  510 ISPOT=ICURNT(10)
      DO 520 I=ISTART,ISTOP
      LOC=LOCAL(I)
      NODE1=NODPLC(LOC)
      NODE2=NODPLC(LOC+1)
      NODE3=NODPLC(LOC+2)
      NODE5=NODPLC(LOC+4)
      NODE6=NODPLC(LOC+5)
      MNAM=MNAME(I)
C
C  DRAIN RESISTANCE
C
      VREAL=VNPART(1,NODE1)-VNPART(1,NODE5)
      VIMAG=VNPART(2,NODE1)-VNPART(2,NODE5)
      VNO1=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,7)
C
C  SOURCE RESISTANCE
C
      VREAL=VNPART(1,NODE3)-VNPART(1,NODE6)
      VIMAG=VNPART(2,NODE3)-VNPART(2,NODE6)
      VNO2=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*SYMVAL(MNAM,8)
C
C  DRAIN CURRENT SHOT NOISE
C
      VREAL=VNPART(1,NODE5)-VNPART(1,NODE6)
      VIMAG=VNPART(2,NODE5)-VNPART(2,NODE6)
      VNO3=(VREAL*VREAL+VIMAG*VIMAG)*FOURKT*2.0*ABS(TRACUR(ISPOT+3))/3.0
      ISPOT=ISPOT+15
      VNTOT=VNO1+VNO2+VNO3
      VNRMS=VNRMS+VNTOT
      IF (KILL.EQ.0) GO TO 520
      WRITE (6,121) NAME(I),VNO1,VNO2,VNO3,VNTOT
  520 CONTINUE
C
C  COMPUTE EQUIVALENT INPUT NOISE VOLTAGE
C
  600 VNOUT=SQRT(VNRMS)
      VNIN=VNOUT/VOUT
      IF (KILL.EQ.0) GO TO 620
      WRITE (6,601) VNRMS,VNOUT,IONAM(NOSOUT),NAME(NOSIN),VOUT,
     1   NAME(NOSIN),VNIN
  601 FORMAT (////////5X,*TOTAL OUTPUT NOISE VOLTAGE*,4X,1PE10.3,
     1   *  SQ V/HZ*,6X,*=*,4X,E10.3,*  V/RT HZ*//5X,*TRANSFER *
     2   *FUNCTION VALUE  ( *,R7,*/ *,R7,*)*,16X,E10.3//5X,
     3   *EQUIVALENT INPUT NOISE   (AT  *,R7,*)*,22X,E10.3,
     4   *   /RT HZ*)
      WRITE (6,611)
  611 FORMAT (1HY)
  620 IF (INOISE.EQ.0) RETURN
      PUTOUT(1,ICALC,INOISE)=VNOUT
      PUTOUT(2,ICALC,INOISE)=VNIN
      RETURN
      END
      SUBROUTINE ACASOL
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/POINTS/IUS,ILS,MIRROR,NSTOP,NUMVS,LASTUT,LASTLT
C
C
      COMPLEX CYNL(1),CVN(1),CU(1),CUL(1)
      EQUIVALENCE (CYNL(1),YNL(1)),(CVN(1),VN(1))
      EQUIVALENCE (CU(1),CYNL(402)),(CUL(1),CYNL(1202))
C
C
      LE=NSTOP-1
      IF (LE) 60,5,10
    5 IB=IORDER(1)
      CVN(IB)=CVN(IB)/CYNL(IB)
      RETURN
C
C  FORWARD SUBSTITUTION
C
   10 DO 30 I=1,LE
      L=IORDER(I)
      CVN(L)=CVN(L)/CYNL(L)
      IUST=IUR(I)
      IUE=IUR(I+1)-1
      IF (IUST.GT.IUE) GO TO 30
      DO 20 IU=IUST,IUE
      II=IUC(IU)
      IC=IORDER(II)
      CVN(IC)=CVN(IC)-CVN(L)*CU(IU)
   20 CONTINUE
   30 CONTINUE
C
C  BACK SUBSTITUTION
C
      IB=IORDER(NSTOP)
      CVN(IB)=CVN(IB)/CYNL(IB)
      DO 50 I=1,LE
      IB=IORDER(NSTOP-I)
      IUST=IUR(NSTOP-I)
      IUE=IUR(NSTOP-I+1)-1
      IF (IUST.GT.IUE) GO TO 50
      DO 40 IU=IUST,IUE
      JJ=IUC(IU)
      JB=IORDER(JJ)
      CVN(IB)=CVN(IB)-CUL(IU)*CVN(JB)
   40 CONTINUE
   50 CONTINUE
   60 RETURN
      END
      SUBROUTINE ACCAL(IELNUM,CREAL,CIMAG)
      COMMON NODPLC(800),YNL(2001),TSTORE(2001),TRACUR(1700),VN(401),
     1   VNIM1(401),IORDER(401),IUR(402),IUC(800),MATLOC(1800)
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
C
C
      COMPLEX CUR,CYNL(1),CVN(1),CUL(1)
      EQUIVALENCE (CYNL(1),YNL(1)),(CVN(1),VN(1))
      EQUIVALENCE (CUL(1),CYNL(1202))
C
C
      CREAL=0.0
      CIMAG=0.0
      LOC=LOCAL(IELNUM)
      NODE1=NODPLC(LOC)
      SIGN=NODPLC(LOC+2)
      IPNOD=NODPLC(LOC+3)
      ISPOT=ICURNT(6)+2*(IELNUM-LOCATE(6))
      ISTART=IUR(IPNOD)
      ISTOP=IUR(IPNOD+1)-1
      CUR=CMPLX(TRACUR(ISPOT),TRACUR(ISPOT+1))-CYNL(NODE1)*CVN(NODE1)
      IF (ISTART.GT.ISTOP) GO TO 40
      DO 30 I=ISTART,ISTOP
      J=IUC(I)
      JO=IORDER(J)
      CUR=CUR-CUL(I)*CVN(JO)
   30 CONTINUE
   40 CREAL=SIGN*REAL(CUR)
      CIMAG=SIGN*AIMAG(CUR)
      RETURN
      END
      SUBROUTINE OVTPVT
      COMMON/INDATA/NUMEL,NUNODS,NUMNOD,NOSTOP,JELCNT(20),LOCATE(21),
     1   ICURNT(21),JUNODE(401),NAME(200),LOCAL(200),MNAME(200)
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/OCNTRL/IPRCOM(10,2),IPLCOM(10,2),PLTLIM(10,2,2),
     1   IACPRT(5,4),IACPLT(5,4),ACPLIM(5,4,2)
      COMMON/DC/ICVFLG,ITCELM,TCSTAR,TCSTOP,TCINCR,KSSOP,KINEL,KOVAR
      COMMON/TRAN/JTRFLG,TSTEP,TSTOP,TSTART,NOTINT,STEPS(5),ENDPTS(5),
     1   KFROUT,FORFRE,KFPTS
      COMMON/AC/JACFLG,FSTART,FSTOP,IDFREQ,FINCR,INOISE,NOSPRT,
     1   NOSOUT,NOSIN
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
C
C
      DIMENSION PUTOUT(2,101,5)
      EQUIVALENCE (PUTOUT(1,1,1),ROUT(1,1))
      DIMENSION ITAB(10),INAM(10),IDID(10),VOUT(10)
      DIMENSION ITITLE(4),IHEAD(10)
      DATA ITITLE /10H REAL PART,10H IMAG PART,
     1             10H MAGNITUDE,10H   PHASE  /
      DATA LSNOIS,LSTIME /10H   NOISE  ,10H    TIME  /
C
C
      IF (ICALC.LT.2) RETURN
      CALL SECOND(T1)
      GO TO (10,200,20),MODE
C
C  DC TRANSFER CURVES
C
   10 IFLAG=1
      NAMHDR=NAME(ITCELM)
      GO TO 50
C
C  TRANSIENT ANALYSIS
C
   20 IFLAG=2
      NAMHDR=LSTIME
C
C  PRINTS
C
   50 KNTR=0
      IKNT=0
   60 IKNT=IKNT+1
      IF (IKNT.GT.NUMOR(IFLAG)) GO TO 70
      IF (IPRCOM(IKNT,IFLAG).EQ.0) GO TO 60
      ITEMP=IOVAR(IKNT,IFLAG)
      KNTR=KNTR+1
      ITAB(KNTR)=IKNT
      INAM(KNTR)=IONAM(ITEMP)
      GO TO 60
   70 IF (KNTR.EQ.0) GO TO 140
      IF (IFLAG.EQ.2) GO TO 90
      WRITE (6,81) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
   81 FORMAT (1H7/////1X,125(1H*)///1X,8A10,26X,*-----  SPICE  -----*
     1   ///21X,*DC TRANSFER CURVES*,37X,*TEMPERATURE *,F10.3,
     2   * DEG C*///1X,125(1H*)//)
      GO TO 100
   90 WRITE (6,91) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
   91 FORMAT (1H7/////1X,125(1H*)///1X,8A10,26X,*-----  SPICE  -----*
     1   ///21X,*TRANSIENT ANALYSIS*,37X,*TEMPERATURE *,F10.3,
     2   * DEG C*///1X,125(1H*)//)
  100 WRITE (6,101) NAMHDR,(INAM(I),I=1,KNTR)
  101 FORMAT (3X,R7,2X,10(5X,R7))
      WRITE (6,111)
  111 FORMAT (//1HX)
      DO 130 I=1,ICALC
      DO 120 J=1,KNTR
      IKNT=ITAB(J)
  120 VOUT(J)=ROUT(I,IKNT)
      WRITE (6,121) FREQ(I),(VOUT(J),J=1,KNTR)
  121 FORMAT (1X,1PE9.2,2X,10(2X,E10.3))
  130 CONTINUE
      WRITE (6,131)
  131 FORMAT (1HY)
C
C  PLOTS
C
  140 IKNT=0
  150 IKNT=IKNT+1
      IF (IKNT.GT.NUMOR(IFLAG)) GO TO 1000
      IF (IPLCOM(IKNT,IFLAG).EQ.0) GO TO 150
      ITEMP=IOVAR(IKNT,IFLAG)
      IF (IFLAG.EQ.2) GO TO 160
      WRITE (6,81) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
      GO TO 170
  160 WRITE (6,91) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
  170 WRITE (6,171) NAMHDR,IONAM(ITEMP)
  171 FORMAT (3X,R7,5X,R7//)
      IF (IPLCOM(IKNT,IFLAG).EQ.2) GO TO 180
      CALL PLOT (0,IKNT,0,1,0.0,0.0)
      GO TO 150
  180 CALL PLOT (0,IKNT,0,0,PLTLIM(IKNT,IFLAG,1),PLTLIM(IKNT,IFLAG,2))
      GO TO 150
C
C  AC ANALYSIS
C
C  PRINTS
C
  200 KNTR=0
      IF (INOISE.EQ.0) GO TO 220
      IF (IACPRT(INOISE,1).EQ.0) GO TO 210
      KNTR=KNTR+1
      INAM(KNTR)=IONAM(NOSOUT)
      IHEAD(KNTR)=LSNOIS
      IDID(KNTR)=1
      ITAB(KNTR)=INOISE
  210 IF (IACPRT(INOISE,2).EQ.0) GO TO 220
      KNTR=KNTR+1
      INAM(KNTR)=NAME(NOSIN)
      IHEAD(KNTR)=LSNOIS
      IDID(KNTR)=2
      ITAB(KNTR)=INOISE
  220 IKNT=0
      IFLAG=0
  230 IKNT=IKNT+1
      IF (IKNT.GT.NUMOR(3)) GO TO 250
      ITEMP=IACVAR(IKNT)
      IF (IOFLG(ITEMP).EQ.3) GO TO 230
      JKNT=0
  240 JKNT=JKNT+1
      IF (JKNT.GT.4) GO TO 230
      IF (IACPRT(IKNT,JKNT).EQ.0) GO TO 240
      KNTR=KNTR+1
      INAM(KNTR)=IONAM(ITEMP)
      IHEAD(KNTR)=ITITLE(JKNT)
      IDID(KNTR)=JKNT
      ITAB(KNTR)=IKNT
      IF (KNTR.LT.10) GO TO 240
      IF (IFLAG.EQ.1) GO TO 260
      IFLAG=1
      GO TO 270
  250 IF (KNTR.EQ.0) GO TO 400
      IF (IFLAG.EQ.0) GO TO 270
      IFLAG=0
  260 WRITE (6,261)
  261 FORMAT (1H1/////)
      GO TO 280
  270 WRITE (6,271) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
  271 FORMAT (1H7/////1X,125(1H*)///1X,8A10,26X,*-----  SPICE  -----*
     1   ///21X,*AC ANALYSIS*,44X,*TEMPERATURE *,F10.3,
     2   * DEG C*///1X,125(1H*)//)
  280 WRITE (6,281) (INAM(I),I=1,KNTR)
  281 FORMAT (//2X,*FREQUENCY *,10(5X,R7))
      WRITE (6,291) (IHEAD(I),I=1,KNTR)
  291 FORMAT (4X,4H(HZ),4X,10(2X,A10))
      WRITE (6,111)
      DO 370 I=1,ICALC
      KKNT=0
  300 KKNT=KKNT+1
      IF (KKNT.GT.KNTR) GO TO 360
      ITEMP=ITAB(KKNT)
      ITYPE=IDID(KKNT)
      GO TO (310,320,330,340),ITYPE
  310 VOUT(KKNT)=PUTOUT(1,I,ITEMP)
      GO TO 300
  320 VOUT(KKNT)=PUTOUT(2,I,ITEMP)
      GO TO 300
  330 VOUT(KKNT)=SQRT(PUTOUT(1,I,ITEMP)*PUTOUT(1,I,ITEMP)
     1   +PUTOUT(2,I,ITEMP)*PUTOUT(2,I,ITEMP))
      GO TO 300
  340 XMAG=SQRT(PUTOUT(1,I,ITEMP)*PUTOUT(1,I,ITEMP)
     1   +PUTOUT(2,I,ITEMP)*PUTOUT(2,I,ITEMP))
      IF (XMAG.GT.1.0E-20) GO TO 350
      VOUT(KKNT)=0.0
      GO TO 300
  350 VOUT(KKNT)=RAD*ATAN2(PUTOUT(2,I,ITEMP),PUTOUT(1,I,ITEMP))
      GO TO 300
  360 WRITE (6,121) FREQ(I),(VOUT(J),J=1,KNTR)
  370 CONTINUE
      WRITE (6,131)
      IF (IFLAG.EQ.0) GO TO 400
      KNTR=0
      GO TO 240
C
C  PLOTS
C
  400 IF (INOISE.EQ.0) GO TO 500
      IF (IACPLT(INOISE,1).EQ.0) GO TO 450
      DO 410 I=1,ICALC
      XMAG=PUTOUT(1,I,INOISE)
      IF (XMAG.LT.1.0E-20) XMAG=1.0E-20
      PUTOUT(1,I,INOISE)=ALOG10(XMAG)
  410 CONTINUE
      WRITE (6,421) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
  421 FORMAT (1H7/////1X,125(1H*)///1X,8A10,26X,*-----  SPICE  -----*
     1   ///21X,*NOISE ANALYSIS*,41X,*TEMPERATURE *,F10.3,
     2   * DEG C*///1X,125(1H*)//)
      WRITE (6,431) IONAM(NOSOUT)
  431 FORMAT (2X,*FREQUENCY*,4X,*NOISE AT *,R7,*   (/RT HZ)*//)
      IF (IACPLT(INOISE,1).EQ.2) GO TO 440
      CALL PLOT (1,INOISE,1,1,0.0,0.0)
      GO TO 450
  440 CALL PLOT (1,INOISE,1,0,ACPLIM(INOISE,1,1),ACPLIM(INOISE,1,2))
  450 IF (IACPLT(INOISE,2).EQ.0) GO TO 500
      DO 460 I=1,ICALC
      XMAG=PUTOUT(2,I,INOISE)
      IF (XMAG.LT.1.0E-20) XMAG=1.0E-20
      PUTOUT(2,I,INOISE)=ALOG10(XMAG)
  460 CONTINUE
      WRITE (6,421) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
      WRITE (6,471) NAME(NOSIN)
  471 FORMAT (2X,*FREQUENCY*,4X,*EQU NOISE AT  *,R7,*  (/RT HZ)*//)
      IF (IACPLT(INOISE,2).EQ.2) GO TO 480
      CALL PLOT (2,INOISE,1,1,0.0,0.0)
      GO TO 500
  480 CALL PLOT(2,INOISE,1,0,ACPLIM(INOISE,2,1),ACPLIM(INOISE,2,2))
  500 IKNT=0
  510 IKNT=IKNT+1
      IF (IKNT.GT.NUMOR(3)) GO TO 1000
      ITEMP=IACVAR(IKNT)
      IF (IOFLG(ITEMP).EQ.3) GO TO 510
      IF (IACPLT(IKNT,1).EQ.0) GO TO 530
      WRITE (6,271) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
      WRITE (6,511) IONAM(ITEMP)
  511 FORMAT (2X,*FREQUENCY*,4X,*REAL PART OF *,R7//)
      IF (IACPLT(IKNT,1).EQ.2) GO TO 520
      CALL PLOT (1,IKNT,0,1,0.0,0.0)
      GO TO 530
  520 CALL PLOT (1,IKNT,0,0,ACPLIM(IKNT,1,1),ACPLIM(IKNT,1,2))
  530 IF (IACPLT(IKNT,2).EQ.0) GO TO 560
      WRITE (6,271) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
      WRITE (6,541) IONAM(ITEMP)
  541 FORMAT (2X,*FREQUENCY*,4X,*IMAGINARY PART OF *,R7//)
      IF (IACPLT(IKNT,2).EQ.2) GO TO 550
      CALL PLOT (2,IKNT,0,1,0.0,0.0)
      GO TO 560
  550 CALL PLOT (2,IKNT,0,0,ACPLIM(IKNT,2,1),ACPLIM(IKNT,2,2))
  560 IF (IACPLT(IKNT,3).NE.0) GO TO 570
      IF (IACPLT(IKNT,4).NE.0) GO TO 570
      GO TO 510
  570 DO 590 I=1,ICALC
      XMAG=SQRT(PUTOUT(1,I,IKNT)*PUTOUT(1,I,IKNT)
     1   +PUTOUT(2,I,IKNT)*PUTOUT(2,I,IKNT))
      IF (XMAG.GT.1.0E-20) GO TO 580
      PUTOUT(1,I,IKNT)=-20.0
      PUTOUT(2,I,IKNT)=0.0
      GO TO 590
  580 PUTOUT(2,I,IKNT)=RAD*ATAN2(PUTOUT(2,I,IKNT),PUTOUT(1,I,IKNT))
      PUTOUT(1,I,IKNT)=ALOG10(XMAG)
  590 CONTINUE
      IF (IACPLT(IKNT,3).EQ.0) GO TO 630
      WRITE (6,271) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
      WRITE (6,611) IONAM(ITEMP)
  611 FORMAT (2X,*FREQUENCY*,4X,*MAGNITUDE OF *,R7//)
      IF (IACPLT(IKNT,3).EQ.2) GO TO 620
      CALL PLOT (1,IKNT,1,1,0.0,0.0)
      GO TO 630
  620 CALL PLOT (1,IKNT,1,0,ACPLIM(IKNT,3,1),ACPLIM(IKNT,3,2))
  630 IF (IACPLT(IKNT,4).EQ.0) GO TO 510
      WRITE (6,271) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
      WRITE (6,641) IONAM(ITEMP)
  641 FORMAT (2X,*FREQUENCY*,4X,*PHASE OF *,R7,* (DEGREES)*//)
      IF (IACPLT(IKNT,4).EQ.2) GO TO 650
      CALL PLOT (2,IKNT,0,0,-180.0,180.0)
      GO TO 510
  650 CALL PLOT (2,IKNT,0,0,ACPLIM(IKNT,4,1),ACPLIM(IKNT,4,2))
      GO TO 510
 1000 IF (MODE.LT.3) GO TO 1010
      IF (KFROUT.EQ.0) GO TO 1010
      CALL FOURAN
 1010 CALL SECOND(T2)
      RTIMES(10)=RTIMES(10)+T2-T1
      RETURN
      END
      SUBROUTINE PLOT (ISPOT,INUM,ILOG,IAUTO,PMIN,PMAX)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
C
C
      DIMENSION PUTOUT(2,101,5)
      EQUIVALENCE (PUTOUT(1,1,1),ROUT(1,1))
      DIMENSION COOR(5),IPOINT(13)
      DATA IPOINT /5755555555555555B,2*5555555555555555B,
     1             5557555555555555B,2*5555555555555555B,
     1             5555575555555555B,2*5555555555555555B,
     1             5555555755555555B,2*5555555555555555B,
     1             5555555557555555B/
      DATA ILET1,ILET2,ISHIFT /10B,12B,100B/
C
C
      IF (IAUTO.EQ.1) GO TO 10
      IF (ILOG.EQ.1) GO TO 5
      XMIN=PMIN
      DEL=PMAX-PMIN
      IF (DEL.GT.0.0) GO TO 2
      XMIN=PMAX
      DEL=-DEL
    2 IF (DEL.LT.1.0E-20) DEL=1.0E-20
      DEL=DEL/4.0
      GO TO 185
    5 XMIN=PMIN
      IF (XMIN.LT.1.0E-20) XMIN=1.0E-20
      XMIN=ALOG10(XMIN)
      XMAX=PMAX
      IF (XMAX.LT.1.0E-20) XMAX=1.0E-20
      XMAX=ALOG10(XMAX)
      DEL=XMAX-XMIN
      IF (DEL.GT.0.0) GO TO 8
      XMIN=XMAX
      DEL=-DEL
    8 IF (DEL.LT.0.0001) DEL=0.0001
      DEL=DEL/4.0
      GO TO 185
C
C  DETERMINE MAX AND MIN VALUES
C
   10 IF (ISPOT.EQ.0) GO TO 22
      XMAX=PUTOUT(ISPOT,1,INUM)
      XMIN=XMAX
      DO 20 I=2,ICALC
      IF (PUTOUT(ISPOT,I,INUM).LT.XMIN) XMIN=PUTOUT(ISPOT,I,INUM)
      IF (PUTOUT(ISPOT,I,INUM).GT.XMAX) XMAX=PUTOUT(ISPOT,I,INUM)
   20 CONTINUE
      GO TO 25
   22 XMAX=ROUT(1,INUM)
      XMIN=XMAX
      DO 24 I=2,ICALC
      IF (ROUT(I,INUM).LT.XMIN) XMIN=ROUT(I,INUM)
      IF (ROUT(I,INUM).GT.XMAX) XMAX=ROUT(I,INUM)
   24 CONTINUE
C
C  SCALING
C
   25 AVER=(XMAX+XMIN)/2.0
      DEL=XMAX-XMIN
      DELMIN=ABS(AVER)*1.0E-3
      IF (ILOG.EQ.0) GO TO 26
      IF (DELMIN.LT.0.0001) DELMIN=0.0001
      GO TO 27
   26 IF (DELMIN.LT.1.0E-20) DELMIN=1.0E-20
   27 IF (DEL.LT.DELMIN) DEL=DELMIN
      IEXP=-IFIX(ALOG10(DEL))
      FACTOR=EXP(IEXP*XLOG10)
      DEL=DEL*FACTOR
      IF (DEL.LT.0.75) FACTOR=FACTOR*10.0
      IF (DEL.GT.8.0) FACTOR=FACTOR/10.0
      IAVER=IFIX(FACTOR*AVER+0.5)
      AVER=FLOAT(IAVER)/FACTOR
      DEL1=XMAX-AVER
      DEL=AVER-XMIN
      IF (DEL1.GT.DEL) DEL=DEL1
      DEL=DEL*FACTOR
      IF (DEL.GT.1.0) GO TO 28
      DEL=1.0
      GO TO 40
   28 IF (DEL.GT.2.0) GO TO 30
      DEL=2.0
      GO TO 40
   30 IF (DEL.GT.4.0) GO TO 35
      DEL=4.0
      GO TO 40
   35 DEL=8.0
   40 DEL=DEL/FACTOR
      XMIN=AVER-DEL
      DEL=DEL/2.0
C
C  DETERMINE COORDINATES
C
  185 COOR(1)=XMIN
      SMALL=DEL*1.0E-4
      DO 190 I=1,4
      COOR(I+1)=COOR(I)+DEL
      IF (ABS(COOR(I+1)).LT.SMALL) COOR(I+1)=0.0
  190 CONTINUE
      IF (ILOG.EQ.0) GO TO 210
      DO 200 I=1,5
  200 COOR(I)=EXP(XLOG10*COOR(I))
  210 DEL=DEL/25.0
C
C  PRINT COORDINATES
C
      WRITE (6,211)
  211 FORMAT (1HX)
      WRITE (6,216) (COOR(I),I=1,5)
  216 FORMAT (/8X,5(15X,1PE10.3)//26X,51(1X,1H-))
C
C  BEGIN PLOTTING
C
      DO 250 I=1,ICALC
      IF (ISPOT.GT.0) GO TO 232
      VOUT=ROUT(I,INUM)
      GO TO 235
  232 VOUT=PUTOUT(ISPOT,I,INUM)
  235 JPOINT=IFIX((VOUT-XMIN)/DEL+0.5)
      IF (JPOINT.LT.0) JPOINT=0
      IF (JPOINT.GT.103) JPOINT=103
      IF (ILOG.EQ.0) GO TO 240
      VOUT=EXP(XLOG10*VOUT)
  240 JADDR=JPOINT/8+1
      JSPOT=JPOINT-(JADDR-1)*8
      LNEW=ILET1
      IF ((JPOINT/25)*25.EQ.JPOINT) LNEW=ILET2
      ITEMP=IPOINT(JADDR)
      JTEMP=1
      JSTOP=7-JSPOT
      IF (JSTOP.LT.1) GO TO 244
      DO 242 J=1,JSTOP
  242 JTEMP=JTEMP*ISHIFT
  244 IPOINT(JADDR)=ITEMP-LNEW*JTEMP
      WRITE (6,246) FREQ(I),VOUT,(IPOINT(J),J=1,13)
  246 FORMAT (1X,1PE10.3,3X,E10.3,3X,13R8)
      IPOINT(JADDR)=ITEMP
  250 CONTINUE
      WRITE (6,256)
  256 FORMAT (26X,51(1X,1H-)//)
      WRITE (6,261)
  261 FORMAT (1HY)
      RETURN
      END
      SUBROUTINE FOURAN
      COMMON/MISCEL/NOGO,IGOOF,NOPRNT,IACCT,JOBNAM(8),RTIMES(15)
      COMMON/STATUS/MODE,OMEGA,TIME,DELTA,DELOLD,ICALC
      COMMON/KNSTNT/TWOPI,XLOG2,XLOG10,RAD,BOLTZ,CHARGE,VT
      COMMON/OUTDAT/ROUT(101,10),FREQ(101),IONUM,IONAM(10),IOPND(10),
     1   IONND(10),IOFLG(10),NUMOR(3),IOVAR(10,2),IACVAR(5)
      COMMON/TRAN/JTRFLG,TSTEP,TSTOP,TSTART,NOTINT,STEPS(5),ENDPTS(5),
     1   KFROUT,FORFRE,KFPTS
      COMMON/TEMPER/TEMPS(6),NUMTEM,ITEMNO
C
C
      DIMENSION SINCO(9),COSCO(9)
C
C
      XN=KFPTS
      DCCO=0.0
      DO 10 I=1,9
      SINCO(I)=0.0
   10 COSCO(I)=0.0
      ISTART=ICALC-KFPTS
      IKNT=ISTART
   20 IKNT=IKNT+1
      IF (IKNT.GE.ICALC) GO TO 40
      DCCO=DCCO+ROUT(IKNT,KFROUT)
      DO 30 J=1,9
      ARG=TWOPI*FLOAT(IKNT-ISTART)*FLOAT(J)/XN
      SINCO(J)=SINCO(J)+ROUT(IKNT,KFROUT)*SIN(ARG)
      COSCO(J)=COSCO(J)+ROUT(IKNT,KFROUT)*COS(ARG)
   30 CONTINUE
      GO TO 20
   40 DCCO=(DCCO+0.5*(ROUT(ISTART,KFROUT)+ROUT(ICALC,KFROUT)))/XN
      DO 50 J=1,9
      SINCO(J)=2.0*SINCO(J)/XN
      COSCO(J)=(2.0*COSCO(J)+ROUT(ISTART,KFROUT)+ROUT(ICALC,KFROUT))/XN
   50 CONTINUE
      WRITE (6,61) (JOBNAM(I),I=1,8),TEMPS(ITEMNO)
   61 FORMAT (1H7/////1X,125(1H*)///1X,8A10,26X,*-----  SPICE  -----*
     1   ///21X,*FOURIER ANALYSIS*,40X,*TEMPERATURE  *,F10.3,*  DEG C*
     2   ///1X,125(1H*)///)
      IKNT=IOVAR(KFROUT,2)
      WRITE (6,71) IONAM(IKNT),DCCO
   71 FORMAT (5X,*FOURIER COMPONENTS OF TRANSIENT RESPONSE OF  *,R7////
     1   5X,*DC COMPONENT =  *,1PE10.3//5X,*HARMONIC*,9X,*FREQUENCY*,
     2   7X,*FOURIER*,4X,*NORMALIZED*,7X,*PHASE*,6X,*NORMALIZED*/8X,
     3   *NO*,15X,*(HZ)*,8X,*COMPONENT*,4X,*COMPONENT*,7X,*(DEG)*,5X,
     4   *PHASE (DEG)*//)
      IKNT=1
      FREQ1=FORFRE
      XNORM=SQRT(SINCO(1)*SINCO(1)+COSCO(1)*COSCO(1))
      XNHARM=1.0
      IF (XNORM.LT.1.0E-20) GO TO 72
      PNORM=RAD*ATAN2(COSCO(1),SINCO(1))
      GO TO 75
   72 PNORM=0.0
   75 PHASEN=0.0
      WRITE (6,81) IKNT,FREQ1,XNORM,XNHARM,PNORM,PHASEN
   81 FORMAT (9X,I1,11X,1PE10.3,5X,E10.3,1X,0PF12.6,5X,F8.3,5X,F8.3/)
      IF (XNORM.LT.1.0E-20) XNORM=1.0E-20
      THD=0.0
   90 IKNT=IKNT+1
      IF (IKNT.GT.9) GO TO 100
      FREQ1=FLOAT(IKNT)*FORFRE
      HARM=SQRT(SINCO(IKNT)*SINCO(IKNT)+COSCO(IKNT)*COSCO(IKNT))
      XNHARM=HARM/XNORM
      IF (HARM.LT.1.0E-20) GO TO 92
      PHASE=RAD*ATAN2(COSCO(IKNT),SINCO(IKNT))
      GO TO 95
   92 PHASE=0.0
   95 PHASEN=PHASE-PNORM
      THD=THD+XNHARM*XNHARM
      WRITE (6,81) IKNT,FREQ1,HARM,XNHARM,PHASE,PHASEN
      GO TO 90
  100 THD=100.0*SQRT(THD)
      WRITE (6,101) THD
  101 FORMAT (//5X,*TOTAL HARMONIC DISTORTION =  *,F12.6,*  PERCENT*)
      RETURN
      END
    CVq