TITLE	KEY FOR LIBOL 5(223)		
SUBTTL	CREATE A SORT KEY FROM A DISPLAY FIELD		AL BLACKINGTON/CAM

;COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORP, MAYNARD, MA.

	HISEG

ENTRY KEY.
EXTERNAL KILL.

;CALLING SEQUENCE:
;	PUSHJ	17,KEY.
;	<BYTE POINTER TO DISPLAY FIELD>
;	XWD <SIZE OF FIELD>,<FIRST LOCATION FOR KEY>

;IF THE KEY IS DESCENDING, THE SIGN BIT OF THE XWD WILL BE 1.

KEY.:	MOVEI	TA,2		;GET ADDRESS OF
	EXCH	TA,(PP)		;	PARAMETERS, AND
	ADDM	TA,(PP)		;	RESET RETURN ADDRESS

	MOVE	IP,0(TA)	;GET POINTER
	MOVE	OP,1(TA)	;GET KEY ADDRESS
	HLRZ	FS,OP		;GET FIELD SIZE

	TRZN	FS,1B18		;IS KEY DESCENDING?
	TDCA	CS,CS		;NO--SET "CS" TO 0
	MOVNI	CS,1		;YES--SET "CS" TO ALL ONES

	LDB	TA,[POINT 6,IP,11] ;GET BYTE SIZE
	CAIG	TA,^D8
	CAIGE	TA,6
	JRST	BADBYT

	LSH	TA,6
	HRL	OP,TA		;BUILD OUTPUT BYTE POINTER
	AOJA	FS,KEY.3

KEY.1:	SETZM	0(OP)		;CLEAR NEXT WORD

KEY.2:	ILDB	TA,IP		;GET A BYTE
	IDPB	TA,OP		;STASH IT
	TLNE	OP,770000	;ANY ROOM LEFT IN WORD FOR ANOTHER?
	SOJG	FS,KEY.2	;YES--LOOP

	XORM	CS,(OP)		;COMPLEMENT IF DESCENDING
	ADDI	OP,1
	TLZ	OP,77B23

KEY.3:	LDB	TA,[POINT 6,IP,11]
	TLO	OP,@BYTKIK-6(TA)

	SOJG	FS,KEY.1	;LOOP UNTIL DONE

	POPJ	PP,
;BAD BYTE SIZE

BADBYT:	TTCALL	3,[ASCIZ "BAD SORT-KEY BYTE POINTER"]
	JRST	KILL.

;TABLE OF RESIDUES WITH WHICH EACH WORD IS STARTED

BYTKIK:	EXP	^D30B23	;6-BIT
	EXP	^D35B23	;7-BIT
	EXP	^D32B23	;8-BIT


TA=11	;TEMP
FS=13	;FIELD SIZE
OP=14	;OUTPUT POINTER
IP=15	;INPUT POINTER
CS=16	;NON-ZERO IF DESCENDING
PP=17	;PUSH-DOWN POINTER

END
