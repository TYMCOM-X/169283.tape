C
C     ..................................................................
C
C        SUBROUTINE EXPI
C
C        PURPOSE
C           COMPUTES THE EXPONENTIAL INTEGRAL IN THE RANGE -4
C           TO INFINITY.
C
C        USAGE
C           CALL EXPI(RES,X,IER)
C
C        DESCRIPTION OF PARAMETERS
C           RES   - RESULT VALUE
C           X     - ARGUMENT OF EXPONENTIAL INTEGRAL
C           IER   - RESULTANT ERROR CODE WHERE
C                   IER=0  NO ERROR
C                   IER=1  X LESS THAN -4
C
C        REMARKS
C           FOR X = 0 THE RESULT VALUE IS SET TO 1.E75.
C           FOR X LESS THAN -4 CALCULATION IS BYPASSED.
C           THE ARGUMENT REMAINS UNCHANGED.
C
C        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
C           NONE
C
C        METHOD
C           DEFINITION
C           RES=INTEGRAL(EXP(-T)/T, SUMMED OVER T FROM X TO INFINITY).
C           EVALUATION
C           TWO DIFFERENT POLYNOMIAL APPROXIMATIONS ARE USED FOR
C           X GREATER THAN 4 AND FOR ABS(X) EQUAL OR LESS THAN 4.
C           REFERENCE
C           LUKE AND WIMP, 'JACOBI POLYNOMIAL EXPANSIONS OF A GENE-
C           RALIZED HYPERGEOMETRIC FUNCTION OVER A SEMI-INFINITE
C           RANGE', MATHEMATICAL TABLES AND OTHER AIDS TO COMPUTATION,
C           VOL. 17, 1963, ISSUE 84, PP. 395-404.
C
C     ..................................................................
C
      SUBROUTINE EXPI(RES,X,IER)
C
C     TEST OF RANGE
C
      IER=0
      IF(X-4.) 10,10,20
C
   10 IF(X+4.) 55,30,30
C
C     ARGUMENT IS GREATER THAN 4
C
   20 ARG=4./X
      RES=EXP(-X)*((((((((.00094427614*ARG-.0049362007)*ARG+.011723273)
     1 *ARG-.017555779)*ARG+.020412099)*ARG-.022951979)*ARG+.031208561)
     2 *ARG-.062498588)*ARG+.24999999)*ARG
      RETURN
C
C     ARGUMENT IS ABSOLUTELY LESS OR EQUAL 4
C
   30 IF(X) 40,50,40
   400RES=-ALOG(ABS(X))-(((((((((((((.10317602E-11*X-.15798675E-10)*X+
     1.16826592E-9)*X-.21915699E-8)*X+.27635830E-7)*X-.30726221E-6)*X+
     2.30996040E-5)*X-.28337590E-4)*X+.23148392E-3)*X-.0016666906)*X+
     3.010416662)*X-.055555520)*X+.25)*X-1.0)*X-.57721566
      RETURN
C
   50 RES=1.E38
      RETURN
C
C     ARGUMENT IS LESS THAN -4.
C
   55 IER=1
      RETURN
      END
