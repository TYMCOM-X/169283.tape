	TITLE COMPAR FOR LIBOL VERSION 4	
	SUBTTL ALPHANUMERIC STRING COMPARE	PETE WILSON

	HISEG

;CALLED WITH TWO PARAMETERS.
;WORD 1 --
;	BYTE POINTER FOR A-FIELD.
;WORD 2 --
;	BITS 0-5	BYTE POINTER RESIDUE FOR B-FIELD.
;	BIT 6		IGNORED (1 IF A SIGN IS PRESENT).
;	BITS 7-17	SIZE, IN BYTES, OF BOTH FIELDS.
;	BITS 18-35	ADDRESS OF FIRST B-FIELD CHARACTER.
;CALL TO SET.2 PUTS PARAMETERS IN AC'S 13 AND 14.
;RETURNS:
;	CALL+1		IF A LESS THAN B
;	CALL+2		IF A GREATER THAN B
;	CALL+3 (RET.3)	IF A EQUAL TO B
	ENTRY	COMP.		;COMPARE TWO ASCII OR SIXBIT STRINGS.
	ENTRY	CMP.76		;COMPARE ASCII A-FIELD, SIXBIT B-FIELD.
	EXTERNAL	SET.2,RET.3
CHARA=7		;CURRENT CHARACTERS FROM A
CHARB=10	;  AND FROM B FIELDS.
BIT40=11	;0 IF A AND B FIELDS ARE SAME USAGE, 40 OTHERWISE.
AFLDPT=13	;BYTE POINTERS TO A
BFLDPT=14	;  AND TO B FIELDS.
FLDSIZ=15	;BYTES LEFT IN STRING
PP==17		;PUSH-DOWN POINTER

COMP.:	TDZA	BIT40,BIT40	;A AND B FIELDS SAME USAGE, ZERO CONVERSION FACTOR.
CMP.76:	MOVEI	BIT40,40	;B FIELD IS SIXBIT, CONVERT TO ASCII.
	PUSHJ	17,SET.2	;GET TWO PARAMETERS.
	LDB	FLDSIZ,NBYTES	;FIND TOTAL CHARACTERS TO COMPARE.
	JUMPE	FLDSIZ,RET.3	;IF ZERO, FORGET THE WHOLE THING
	TLZ	BFLDPT,007777	;THEN CLEAR I AND X IN B-FIELD POINTER.
	TLNE	AFLDPT,100	;IF A-FIELD IS ASCII,
	TLOA	BFLDPT,700	;  SET B-FIELD ASCII,
	TLOA	BFLDPT,600	;  ELSE SET B-FIELD TO BE SIXBIT
	TLZ	BFLDPT,000040(BIT40)  ;ADJUST B-FIELD BYTE-SIZE IF CMP.76

NXTCMP:	ILDB	CHARA,AFLDPT	;MORE TO DO, GET NEXT CHARACTER FROM A
	ILDB	CHARB,BFLDPT	;  AND FROM B FIELDS.
	ADDI	CHARB,(BIT40)	;ADJUST B-FIELD CHARACTER IF NECESSARY.
	CAIN	CHARA,(CHARB)	;IF EQUAL,
	SOJG	FLDSIZ,NXTCMP	;  LOOP

	JUMPE	FLDSIZ,RET.3	;IF WE HAVE SEEN ALL THE FIELD, QUIT

	CAIL	CHARA,(CHARB)	;IF NOT LESS,
	AOS	(PP)		;  SKIP RETURN
	POPJ	PP,		;RETURN TO CALL+1 OR CALL+2

NBYTES:	POINT	11,BFLDPT,17	;POINTER TO TOTAL BYTES TO CONSIDER.

	END
