MODULE SLKUP(DOLLAR,ENTRIES=($SLKUP),FSAVE,TIMING,TIMER=EXTERNAL(SIX12))=
BEGIN
%VERSION A1     %
%LAST UPDATE, APR. 8,  1975, BY V.E.VAN VLEAR   %

REQUIRE SDEFS.BLI[7,107355];

UNDECLARE $SLKUP;

%LOOK UP ROUTINE - LOOKS UP ENTRY IN SYMBOL TABLE THROUGH HASH TBL.     %
%REQUIRES NAME BLOCK ADDRESS AND NUMBER OF WORDS AS ARGUMENTS   %
%RETURNS 0 IF NOT IN SYMBOL TABLE, RETURNS STP IF FOUND %

GLOBAL ROUTINE $SLKUP (BLK,NWORDS)=
BEGIN

MACHOP ROT = #241;

MAP SH$ SHWD$;

BIND HDIV=SV$HSIZE-1;

REGISTER RG;

SCTR$ := STPN$ := 0;             %CHAIN COUNTER AND LOCATION STP        %
RG := .(@BLK)[0];          %FIRST WORD OF NAME FOR HASHING      %
IF .NWORDS GTR 1 THEN
   BEGIN
   ROT (RG,#11,0,0);    %ROTATE 11 BITS FOR GOOD HASH   %
   RG := .RG+.(@BLK)[1]       %ADD NEXT WORD OF NAME    %
   END;

IF .NWORDS GTR 2 THEN
   BEGIN
   ROT (RG,#11,0,0);    %ROTATE AGAIN   %
   RG := .RG+.(@BLK)[.NWORDS-1] %ADD LAST WORD  %
   END;

RG := .RG MOD HDIV;        %FORM HASH TABLE POINTER     %
IF .RG LSS 0 THEN RG := 0-.RG;     %FORCE POS. INDEX    %
SHWD$ := .SHTB$+.RG;          %FORM ADDRESS OF HASH TBL. ENTRY FOR $SFNME       %

STPT$ := .SHWD$[SH$HCP];     %GET HASH TABLE WORD       %

IF .STPT$ NEQ 0 THEN
   $SCMPR(.BLK,.NWORDS);              %GO COMPARE NAME IF ALREADY HAVE HASH PTR   %

.STPN$
END;

END ELUDOM
