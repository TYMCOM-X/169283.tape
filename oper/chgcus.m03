TITLE CHGCUS - CHANGE CUSTOMER DISTRICT NO.(AND HIS USER'S)
SUBTTL - J. MARCIN 1/23/73

JOBVER=137
LOC JOBVER 	;PUT VERSION NUMBER IN JOB DATA AREA
3
RELOC

;LOAD BLKCHG,TRPINI,CHKRUN,GETACT,NAMUP%F,CHGCUS
;SET REMOTE,WRITE FILES, LISCENSE


INTERN CUST,USER,ICODE,IER,MODE,PPN
EXTERNAL NAMUP,EXIT.,GETACT
EXTERN NIANS,ICUSTN,ISLSMN
;AC'S
F=0
A=1
B=2
C=3
D=4
E=5
N=6
N1=7
N2=10
N3=11
CH=13
Z=14
P=15
BP=16
INDEX=12

;FLAGS IN RIGHT HALF OF F
BACKUP==1	;CHANGES ARE BEING MADE TO A NON-OFFICIAL LUD/DUL
NOACTC==2	;DO NOT CHANGE ACCTG. RECORD...DIST IS GOOD ALLREADY
TTYOUT==10	;I/O IS TO TTY
NEWF==40	;TYPE "NEW" BEFORE ASKING QUESTION
LISTF==100	;OUTPUT CHARS. INSTEAD OF SETUP MESSAGE
SIT31F==200	;WORKING ON SITE 31

;FLAGS IN LEFT HALF OF F 
ASCIF==1	;ASCII INPUT OR OUTPUT
ENDTYD==2	;SET WHEN A 377 OR ALT-MODE INPUT
DECINF==10	;DECIMAL INPUT AND OUTPUT
DIGINF==40	;INPUT IS IN DIGITS

;I/O CHANNELS
DSK==14
TTY==13

;CALLI'S
RESET==0
EXIT==12

CHGCUS:	TROA F,TTYOUT
	JRST CHKUSR
	TRZ F,777767
	MOVE A,[XWD 1,42313]
	MOVEM A,PPN
	CALLI	RESET
	RESET. 00,0
	MOVE	P,PDP
	TRNE F,TTYOUT
	JRST CHGCS1
	PUSHJ P,GETTTY
	MOVEI	CH,1
	PUSHJ P,OUCH
	PUSHJ P,OUCH
	MOVEI	CH,40
	PUSHJ P,OUCH
TRYAGN:	PUSHJ P,INCH
	CAIE	CH,1
	JRST	TRYAGN
	PUSHJ P,INCH
	CAIE	CH,1
	JRST	TRYAGN
	PUSHJ P,INCH
	CAIE	CH,40
	JRST	TRYAGN
CHGCS1:	SETZM ,CODE
	TRZ F,777767
	MOVE P,PDP
	HRLI	F,0	;ZERO LEFT HALF OF F
	SETZM ,ZERST
	MOVE A,[XWD ZERST,ZERST+1]
	BLT A,ZEREND
	PUSHJ P,GETCOD
	CAIN N,1
	JRST FINCUS
	CAIN N,2
	JRST CHGD1
	JRST ERROR

GETCOD:	MOVEI N,2
	MOVE BP,CODEP
	TRNE F,TTYOUT
	TTCALL 3,[ASCIZ/*/]
	MOVEI Z,1
	PUSHJ P,GETNUM
	MOVEM N,CODE
	JUMPE N,ENDIT
	POPJ P,0

USERCM:	MOVE BP,[POINT 6,RUNAME]
	TRNE F,TTYOUT
	TTCALL 3,[ASCIZ/USER NAME: /]
	MOVEI N,^D12
	PUSHJ P,GETWD
	MOVE	N,RUNAME
	MOVE	N1,RUNAME+1
USERC1:	PUSHJ	P,HASHER
	MOVEM N,HUNAME
	MOVEM N1,HLOC
USERC2:	PUSHJ P,GETLUD
	JRST ERROR
	USETI	DSK,@HLOC
	INPUT	DSK,LUDLST
	STATZ DSK,760000
	JRST ERROR
	POPJ P,0

CUSTIN:	TRNE F,TTYOUT
	TTCALL 3,[ASCIZ /CUSTOMER NO.: /]
	TLO F,DECINF
	PUSHJ P,GETNUM
	MOVEM N,ICUSTN
	POPJ P,0

CHKUSR:	HRRI F,0
	CALLI 1,24
	SKIPA
	CALLI EXIT
	CAMN	1,[XWD 1,42313]	;
	JRST CHGCUS+2
	CALLI EXIT

ENDIT:	TRNE F,TTYOUT
	CALLI EXIT
	SETSTS TTY,0
	TTCALL  1,0
	RELEASE TTY,
	CALLI EXIT

GETTTY:	INIT TTY,214	;INIT TTY IN IMAGE MODE
	SIXBIT /TTY/
	0,TTYI
	CALLI EXIT
	INBUF TTY,2
	POPJ P,0

GETSYS:	TRNE F,BACKUP
	JRST GETDSK
	INIT DSK,17
	SIXBIT /SYS/
	0
	JRST ERROR
	POPJ P,0

GETDSK:	INIT DSK,17
	SIXBIT	/DSK/
	0
	JRST ERROR
	POPJ P,0

GETLUD:	CLOSE DSK,
	MOVEI N,^D10
	MOVE A,[SIXBIT /LUD/]
	PUSHJ P,SETEXT
	SETZB C,D
	LOOKUP DSK,A
	POPJ P,0
	MOVE A,[SIXBIT /LUD/]
	PUSHJ P,SETEXT
	SETZB C,D
	ENTER DSK,A
	JRST .+3
	AOS	(P)
	POPJ P,0
	TLNN F,40000
	POPJ P,0
	SOJG N,GETLUD+1
	JRST ERROR

NOGOD2:	MOVEI	CH,2
NOGOOD:	MOVEM CH,ERRCOD
	PUSHJ P,OUCH
	JRST CHGCS1

ERROR:	MOVEI CH,376
	JRST NOGOOD


ENDLUD:	PUSHJ P,GETBLK
	USETO DSK,@HLOC
	OUTPUT DSK,LUDLST
	STATZ DSK,740000
	JRST ERROR
	CLOSE DSK,
	POPJ P,0

GETBLK:	SKIPE A,OVBLK
	MOVEM A,HLOC
	SETZM ,OVBLK
	POPJ P,0

SETEXT:	TRNE F,BACKUP
	SKIPA B,EXT
	HRLZI B,(SIXBIT /SYS/)
	POPJ P,0

CUSUPD:	MOVEM A,NIANS
	TRNN F,TTYOUT
	RELEASE TTY,
	MOVEM F,ACBLK
	MOVE F,[XWD A,ACBLK+1]
	BLT F,ACBLK+16
	JSA 16,NAMUP
	MOVE 16,[XWD ACBLK,0]
	BLT 16,16
	TRNN F,TTYOUT
	PUSHJ P,GETTTY
	MOVE CH,NIANS
	POPJ P,0

;CODE 1 - GET USERS FOR CUSTOMER
FINCUS:	TRO F,BACKUP
	PUSHJ P,SETSIT	;GET ISITE FOR THIS CUSTOMER NO.
	PUSHJ P,CUSTIN	;GET CUSTOMER NO.
	PUSHJ P,GETDIS
	MOVEM N,ODIST
	TRO F,NEWF
	PUSHJ P,GETDIS
	MOVEM N,NDIST
	PUSHJ P,CHKINF
	MOVE A,ICUSTN
	CAIN A,1
	MOVEI A,^D3376
	MOVEM A,ICUSTN
	MOVEI A,2
	PUSHJ P,CUSUPD
	CAIE CH,1
	JRST [CAIN CH,3
	  JRST NOGOD2
	  JRST ERROR]
	MOVE A,ICUSTN
	CAIN A,^D3376
	MOVEI A,1
	MOVEM A,ICUSTN
	TRNN F,TTYOUT
	PUSHJ P,OUCH
	MOVE A,ISLSMN	;GET SALESMAN
	IDIVI A,^D100	;GET OLD DISTRICT
	CAME	A,ODIST	;IF = REQUIRED DISTRICT...
	JRST GIVNAM	;IF NOT, DO NOT CHANGE...BUT GO THRU USERS
	MOVE A,NDIST	;CHANGE TO NEW...
	IMULI A,^D100
	ADD A,B
	MOVEM A,ISLSMN
	MOVEI A,7
	PUSHJ P,CUSUPD
GIVNAM:	PUSHJ P,GETSYS
	MOVEI A,2
	PUSHJ P,USEUPD
	JRST ERROR
	MOVE A,ICUSTN
	MOVEM A,USER+4
GIVNM1:	MOVEI A,10
	PUSHJ P,USEUPD
	JRST SENEND
	MOVE BP,[POINT 1,USER+5,30]
	MOVE A,ISITE
	IBP ,BP
	SOJN A,.-1
	LDB A,BP
	CAIE A,1
	JRST GIVNM1
	MOVE A,USER+3
	IDIVI A,^D100	;GET OLD DISTRICT
	CAME A,NDIST
	JRST .+3
	TRO F,NOACTC
	JRST .+7
	CAME A,ODIST
	JRST GIVNM1
	MOVE A,NDIST
	IMULI A,^D100
	ADD A,B
	MOVEM A,USER+3
	MOVE N,USER+10
	MOVEM N,RUNAME
	MOVE N1,USER+11
	MOVEM N1,RUNAME+1
	PUSHJ P,USERC1
	PUSHJ P,SETDEX
	JRST GIVNM1
	LDB A,DISBP	;GET OLD DISTRICT NO.
	CAME A,ODIST
	JRST GIVNM1	;IF NOT =, GO GET NEXT USER
	MOVE A,NDIST
	DPB A,DISBP	;DEPOSIT NEW DISTRICT NO.
	PUSHJ P,ENDLUD
	PUSHJ P,SAVINF
	PUSHJ P,SENDNAME	;AND SEND NAME
	TRZN F,NOACTC	;SHOUDL ACCTG. CHANGE BE MADE?
	PUSHJ P,[MOVEI A,5
		PUSHJ P,USEUPD
		JRST ERROR
		POPJ P,0]
	TRNE F,TTYOUT
	JRST GIVNM1
	TRNE F,SIT31F
	PUSHJ P,CHGDIS
	PUSHJ P,INCH
	CAIN CH,3
	JRST GIVNM1
	CAIE CH,4
	JRST ERROR
	PUSHJ P,USERC2
	MOVE INDEX,SAVDEX
	MOVE A,SAVDIS
	DPB A,DISBP
	PUSHJ P,ENDLUD
	JRST GIVNM1
SENEND:	MOVEI A,6
	PUSHJ P,USEUPD
	JRST ERROR
	MOVEI CH,377
	PUSHJ P,OUCH
	RELEASE DSK,
	JRST CHGCS1

SENDNA:	MOVE BP,[POINT 6,RUNAME]
	MOVEI N,^D12
	PUSHJ P,WDSOUT
	POPJ P,0

SAVINF:	MOVEM INDEX,SAVDEX
	MOVE A,HLOC
	MOVEM A,SAVLOC
	MOVE A,ODIST
	MOVEM A,SAVDIS
	POPJ P,0

USEUPD:	MOVEM A,ICODE
	MOVEM F,ACBLK
	MOVE F,[XWD A,ACBLK+1]
	BLT F,ACBLK+16
	MOVNI A,1
	MOVEM A,MODE
	JSA 16,GETACT
	MOVE 16,[XWD ACBLK,0]
	BLT 16,16
	MOVE CH,IER
	CAIN CH,2
	POPJ P,0
	JUMPN CH,ERROR
	AOS (P)
	POPJ P,0

;CODE 2 - CHANGE A USER'S DISTRICT NO. IN THE LUD
CHGDIS:	PUSHJ P,GETCOD
CHGD1:	TRZ F,LISTF+BACKUP
	CAIE N,2
	JRST ERROR
	PUSHJ P,GETSYS
	PUSHJ P,USERCM
	PUSHJ P,SETDEX
	JRST NOGOD2
	TRO F,NEWF
	PUSHJ P,GETDIS
	DPB N,DISBP
	PUSHJ P,CHKINF
	MOVEI CH,1
	PUSHJ P,OUCH
	TRO F,LISTF+BACKUP
	PUSHJ P,ENDLUD
	TRNN F,SIT31F
	JRST CHGCS1
	PUSHJ P,GETSYS
	POPJ P,0

CHKINF:	TRNN F,TTYOUT
	POPJ P,0
	TTCALL 3,[ASCIZ /ALL ENTRIES CORRECT? /]
	PUSHJ P,YORN
	POPJ P,0
	JRST CHGCS1
	POPJ P,0

;YES OR NO ROUTINE - Y=RETURN, N=RETURN+1, NEITHER=CALL-1
YORN:	MOVEI N,1
	MOVE BP,[POINT 6,A,29]
	SETZ A,
	PUSHJ P,GETWD
	CAIN A,(SIXBIT /  Y/)
	POPJ P,0
	AOS ,(P)
	CAIN A,(SIXBIT /  N/)
	POPJ P,0
	SOS ,(P)
	SOS ,(P)
	SOS ,(P)
	TTCALL 3,[ASCIZ /TYPE "Y" OR "N" PLEASE.
/]
	POPJ P,0

;GET DISTRICT NO.
GETDIS:	TLO F,DECINF
	TRNN F,TTYOUT
	JRST DIST2
	TRZE F,NEWF
	TTCALL 3,[ASCIZ/NEW /]
	TTCALL 3,[ASCIZ/DISTRICT NO.: /]
DIST2:	PUSHJ P,GETNUM
	POPJ P,0

OUCH:	TRNE F,TTYOUT
	JRST TOUCH
	TTCALL 15,CH
	POPJ P,0
TOUCH:	TRNN F,LISTF
	JRST NOLIS
	ADDI CH,40
	TTCALL 1,CH
	POPJ P,0
NOLIS:	SOJ	CH,
	CAIN CH,375
	MOVEI CH,1
	CAIN CH,376
	SETZ CH,
	TTCALL 3,@MSG1(CH)
	POPJ P,0
INCH:	SOSLE ,TTYI+2
	JRST INCH1
INCH2:	INPUT TTY,
	STATO TTY,20000
	JRST INCH3
	CLOSE TTY,
	JRST INCH2
INCH3:	SKIPG TTYI+2
	JRST INCH2
INCH1:	ILDB CH,TTYI+1
	ANDI CH,377
	POPJ P,0

TINCH:	TTCALL 4,CH
	CAIN CH,"?"
	JRST HELPIT
	JUMPE CH,TINCH
	POPJ P,0

HELPIT:	TTCALL 11,0
	TTCALL 3,HELPMS
	JRST CHGCS1

DECDON:	MOVEI A,^D100000
	JRST .+2
	IDIVI A,^D10
	SOJGE N1,.-1
	SETZ N,
	MOVEI N1,6
NXTDIG:	MOVE CH,ACBLK(N1)
	IMUL CH,A
	ADD N,CH
	SOJLE N1,GETRES
	IDIVI A,^D10
	CAIE A,1
	JRST NXTDIG
	ADD N,ACBLK(N1)
	JRST GETRES

NUMBAD:	TLNE F,DECINF
	JRST DECDON
	LSH N,-3
	SOJG N1,.-1
	JRST GETRES

BADCHR:	TTCALL 3,[ASCIZ / ILLEGAL CHARACTER TYPED.
/]
	TTCALL 11,0
	JRST CHGCS1


GETWD:	TRNN F,TTYOUT
	JRST GET940
GETWD1:	PUSHJ P,TINCH
	CAIN CH,15
	JRST FILLIN	;FILL WITH 0'S OR " "'S
	TLNN F,ASCIF
	SUBI CH,40
	CAIN CH,175
	JRST ALTMD
	IDPB CH,BP
	SOJG N,GETWD1
	JRST GETRES
FILLIN:	SETZ CH,
	TLNE F,ASCIF
	MOVEI CH," "
	IDPB CH,BP
	SOJG N,.-1
	JRST GETRES
ALTMD:	TLZ F,ASCIF+DECINF
	TLO F,ENDTYD
	POPJ P,0
GET940:	PUSHJ P,INCH
	ANDI CH,377
	TLNE F,DIGINF
	JRST .+3
	CAIN CH,377
	JRST ALTMD
	TLNE F,ASCIF
	ADDI CH,40
	IDPB CH,BP
	SOJG N,GET940
	TLZ F,DIGINF
	POPJ P,0

GETNUM:	TRNN F,TTYOUT
	JRST NUM940
	SETZB C,N
	MOVEI N1,6
	MOVE BP,[POINT 3,N,17]
GETNXT:	PUSHJ P,TINCH
	CAIN CH,15
	JRST NUMBAD
	SUBI CH,60
	JUMPGE CH,.+2
	JRST BADCHR
	TLNE F,DECINF
	JRST SAVDIG
	CAILE CH,7
	JRST BADCHR
	IDPB CH,BP
	SOJG N1,GETNXT
	JRST GETRES
SAVDIG:	CAILE CH,11
	JRST BADCHR
	MOVEM CH,ACBLK(N1)
	SOJG N1,GETNXT
	JRST DECDON
NUM940:	TLO F,DIGINF
	SETZ N1,
	MOVEI N,3
	MOVE BP,[POINT 8,N1,11]
	JUMPE Z,.+6
	MOVE A,N
	SUB N,Z
	IBP ,BP
	SOJG N,.-1
	MOVE N,Z
	SETZ Z,
	PUSHJ P,GET940
	MOVE N,N1
	POPJ P,0

SETSIT:	TRZ F,SIT31F
	TRNE F,TTYOUT
	TTCALL 3,[ASCIZ/SITE: /]
	MOVE BP,[POINT 6,EXT]
	MOVEI N,2
	PUSHJ P,GETWD
	MOVE BP,[POINT 6,EXT]
	IBP ,BP
	ILDB N,BP
	SUBI N,20
	CAIL N,1
	CAILE N,3
	JRST ERROR	;NOT SYS31,32,OR 33
	CAIN N,1
	TRO F,SIT31F
	MOVEM N,ISITE
	POPJ P,0

GETRES:	TLZ F,ASCIF+DECINF
	PUSHJ P,TINCH
	CAIN CH,12
	POPJ P,0
	JRST GETRES+1


WDSOUT:	TRO F,LISTF
	ILDB CH,BP
	TLNE F,ASCIF
	SUBI CH,40
	PUSHJ P,OUCH
	SOJG N,WDSOUT+1
	PUSHJ P,CRLF
	TLZ F,ASCIF
	TRZ F,LISTF
	POPJ P,0

CRLF:	TRNE F,TTYOUT
	TTCALL 3,[ASCIZ/
/]
	POPJ P,0

HASHER:	MOVEI	B,0
	MOVE	C,[555555555555]
	MOVE	D,[361275431652]
	MOVE	E,[612754316523]
	JSR	RND
	JSR	RND
	JSR	RND
	JSR	RND
	XOR	E,D
	MOVE	N,E
	TLZ	N,400000
	IDIVI	N,^D887
	MOVE	N,E
	XOR	N,C
	ADDI N1,1
	POPJ	P,0
RND:	0
	ADD	D,N
	ROTC	N,-22
	MOVEI	A,5
RND1:	MOVE	N2,C+1(B)
	MUL	N2,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
	ADDM	N3,D+1(B)
	AOJE	B,RND2
	MOVNI	B,1
	TRNE	D,1
	SKIPL	E
	MOVEI	B,0
	EXCH	C,E
RND2:	SOJG	A,RND1
	JRST	@RND
	POPJ	P,0

SETDEX:		;A ROUTINE TO FIND A USER NAME IN A BLOCK
	MOVE	A,HUNAME	;IF ENTRY IS HERE, FIND HUNAME
	SETZ	INDEX,
	SKIPN	,LUDBLK(INDEX)
	POPJ P,0
	LDB	C,NENTP	;GET SIZ OF ENTRY
	CAME	A,LUDBLK+4(INDEX)	;MATCH?
	JRST	.+4		;NO, GET NEXT
	MOVEM	C,NENTRY	;SAVE SIZE OF ENTRY
	AOS	(P)
	POPJ	P,0	;AND RETURN WITH INDEX ALL SET
	SKIPG	,LUDBLK(INDEX)	;WAS NO. NEGATIVE?
	JRST	.+3		;YES, GO GET  OVERFLOW BLOCK
	ADD	INDEX,C	;NO, GOOD ENTRY, ADD SIZE OF IT TO INDEX
	JRST	SETDEX+2	;AND GO TRY NEXT ONE
	PUSHJ	P,ROVBLK	;READ OVERFLOW BLOCK IN
	JRST	SETDEX+1

ROVBLK:		;READ OVERFLOW BLOCK
	HRRZ	C,LUDBLK(INDEX)
	MOVEM	C,OVBLK	;SAVE BLOCK NO.
	USETI	DSK,@OVBLK
	INPUT	DSK,LUDLST	;READIT!
	STATZ DSK,760000
	JRST ERROR
	SETZ INDEX,
	POPJ	P,0


HELPMS:	ASCIZ/YOU HAVE THE FOLLOWING OPTIONS:
/

MSG1:	[ASCIZ/ENTRY COMPLETED.
/]
	[ASCIZ/ERROR
/]

;STORAGE
;FIRST BYTE POINTERS...
DISBP:	POINT 8,LUDBLK+1(INDEX),35
NENTP:	POINT 7,LUDBLK+2(INDEX),35	;POINTS TO NO. OF WDS IN RECORD
CODEP:	POINT 3,CODE,32
LUDLST:	IOWD 200,LUDBLK
	0
TTYI:	BLOCK 3
CODE:	0
EXT:	0	;EXTENSION FOR NON-OFFICIAL LUD
ZERST:	0	;ZER OUT EVERYTHING FROM HERE TO ZEREND
SAVDEX:	0	;SAVE INDEX
SAVLOC:	0	;SAVE HLOC
SAVDIS:	0	;SAVE NEW DISTRICT
ERRCOD:	0
NENTRY:	0	;NO. OF WORDS IN ENTRY
ODIST:	0	;OLD DISTRICT
NDIST:	0	;NEW DISTRICT
RUNAME:	BLOCK 2
HLOC:	0	;HASH LOCATION
LPPN:	BLOCK 4	;PPN
HUNAME:	BLOCK 6
ACBLK:	BLOCK 17
OVBLK:	0
LUDBLK:	BLOCK	200
USER:	BLOCK 20
CUST:	0
ISITE:	0
ICODE:	0
MODE:	0
IER:	0
PPN:	0
ZEREND:	0
PDP:	XWD -20,.
	BLOCK	21
	END	CHGCUS+1
   