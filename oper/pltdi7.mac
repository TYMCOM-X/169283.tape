TITLE PLTDIS

ENTRY WHERE,PLOTS,PLOT,CROSS,JOB,JTERM,FMAG

PLOTS:	0
	MOVEM 17,SV17
	MOVEI 17,SV0
	BLT 17,SV0+16
	SETZM DISMOD#
	SETZM RPOS#
	SETZM PEN#
	SETZM XAT#
	SETZM YAT#
	SETZM X#
	SETZM Y#
	MOVEI 0,^D105
	MOVEM 0,@(16)
	IONEOU [33]
	IONEOU [14]
        MSTIME 1,
        MSTIME 2,
        SUB 2,1
        CAIG 2,^D1000
        JRST .-3
	MOVSI 17,SV0
	BLT 17,17
	JRA 16,1(16)

CROSS:  0
        MOVEM 17,SV17
        MOVEI   17,SV0
        BLT 17,SV0+16
        HRROI 1,-31
        GETTAB 1,
        0
        TRO 1,200
        CALLI 1,-26
        IONEOU  [33]
        IONEOU  [32]
        INCHRW  3
        CAIN 3,15
        JRST .-2
        INCHRW  4
        ANDI    4,37
        LSH     4,5
        INCHRW  5
        ANDI    5,37
        XORM     5,4
        INCHRW  5
        ANDI    5,37
        LSH     5,5
        INCHRW  6
        ANDI    6,37
        XORM     6,5
        MOVEM   4,@0(16)
        MOVEM   5,@1(16)
        HRROI 1,-31
        GETTAB 1,
        0
        TRZ 1,200
        CALLI 1,-26
        MOVSI   17,SV0
        BLT 17,17
        JRA     16,2(16)

JTERM:  0
        MOVNI 0,1
        GETTMC 0,
        ANDI 0,777777
        JRA 16,1(16)
JOB:    0
        PJOB    0,
        JRA 16,1(16)

WHERE:	0
	MOVEM 17,SV17
	MOVEI 17,SV0
	BLT 17,SV0+16
	MOVE 1,1(16)
	MOVEM 1,TMP1#
	MOVE 1,SV0+1
	HRRZ 1,(16)
	MOVE 2,X
	CAIG 1,17
	ADDI 1,SV0
	MOVEM 2,(1)
	MOVE 1,TMP1
	MOVE 2,Y
	CAIG 1,17
	ADDI 1,SV0
	MOVEM 2,(1)
	MOVSI 17,SV0
	BLT 17,17
	JRA 16,2(16)
PLOT:	0
	MOVEM 17,SV17
	MOVEI 17,SV0
	BLT 17,SV0+16
	MOVEI 17,ARGS-1
	PUSH 17,@(16)
	PUSH 17,@1(16)
	PUSH 17,@2(16)
	MOVM 1,ARGPEN
	CAIG 1,1
	JRST PENOK
	CAIE 1,2
	TDZA 17,17
	MOVEI 17,1
	CAME 17,PEN
	JSA 16,FINPLT
	MOVEM 17,PEN
PENOK:	MOVE 1,XARG
	FSBR 1,X
	MOVE 2,YARG
	FSBR 2,Y
	MOVE 3,X
	FSBR 3,XAT
	MOVE 4,Y
	FSBR 4,YAT
	MOVE 5,1
	FMPR 5,4
	MOVE 6,3
	FMPR 6,2
	FSBR 5,6
	MOVMS 5
	CAMLE 5,[0.0005]
	JRST DOFIN
	FMPR 1,3
	JUMPL 1,DOFIN
	FMPR 2,4
	JUMPGE 2,LVPL1
DOFIN:	JSA 16,FINPLT
LVPL1:	MOVE 1,YARG
	MOVEM 1,Y
	MOVE 1,XARG
	MOVEM 1,X
	SKIPL ARGPEN
	JRST LVPLT
	JSA 16,FINPLT
	SETZM DISMOD
	SETZM X
	SETZM Y
	SETZM PEN
	IONEOU [37]
LVPLT:	MOVSI 17,SV0
	BLT 17,17
	JRA 16,3(16)

EXTERNAL INT
FINPLT:	0
	MOVE 1,X
	CAME 1,XAT
	JRST FINPL1
	MOVE 1,Y
	CAMN 1,YAT
	JRA 16,(16)
FINPL1:	SKIPE RPOS
	JRST BLANK
	MOVE 1,PEN
	MOVEM 1,PENP#
	SKIPL 1,X
	CAMLE 1,XMAX
	JRST BLANK
	MOVEM 1,XP#
	SKIPL 1,Y
	CAMLE 1,YMAX
	JRST BLANK
	MOVEM 1,YP#
	SKIPL 1,XAT
	CAMLE 1,XMAX
	JRST BLANK
	SKIPL 1,YAT
	CAMLE 1,YMAX
	SKIPA
	JRST FINPL4
BLANK:	MOVE 1,X
	FSBR 1,XAT
	MOVM 3,1
	MOVE 2,Y
	FSBR 2,YAT
	MOVM 4,2
	CAMGE 3,4
	MOVE 3,4
	FDVR 1,3
	FDVR 2,3
	SETZB 14,15
	MOVE 12,3
	MOVE 13,3
	SKIPL 4,X
	CAMLE 4,XMAX
	MOVEI 13,0
	SKIPL 4,Y
	CAMLE 4,YMAX
	MOVEI 12,0
	JUMPE 1,NOXM
	MOVN 14,XAT
	FDVR 14,1
	CAIGE 14,0
	MOVEI 14,0
	CAMLE 14,3
	MOVE 14,3
	MOVE 13,XMAX
	FSBR 13,XAT
	FDVR 13,1
	CAMLE 13,3
	MOVE 13,3
	CAIGE 13,0
	MOVEI 13,0
NOXM:	JUMPE 2,NOYM
	MOVN 15,YAT
	FDVR 15,2
	CAIGE 15,0
	MOVEI 15,0
	CAMLE 15,3
	MOVE 15,3
	MOVE 12,YMAX
	FSBR 12,YAT
	FDVR 12,2
	CAMLE 12,3
	MOVE 12,3
	CAIGE 12,0
	MOVEI 12,0
NOYM:	CAMGE 13,14
	EXCH 13,14
	CAMGE 12,15
	EXCH 12,15
	CAMLE 13,12
	MOVE 13,12
	CAMGE 14,15
	MOVE 14,15
	CAMG 13,14
	JRST [SETOM RPOS
		JRST FINPL3]
	MOVE 15,1
	SKIPN RPOS
	JUMPLE 14,NOINV
	SETZM PENP
	MOVE 1,14
	FMPR 1,15
	FADR 1,XAT
	MOVEM 1,XP
	MOVE 1,14
	FMPR 1,2
	FADR 1,YAT
	MOVEM 1,YP
	JSA 16,PLTIT
NOINV:	MOVE 1,PEN
	MOVEM 1,PENP
	SETZM RPOS
	CAME 3,13
	SETOM RPOS
	MOVE 1,13
	FMPR 1,15
	FADR 1,XAT
	MOVEM 1,XP
	MOVE 1,13
	FMPR 1,2
	FADR 1,YAT
	MOVEM 1,YP
FINPL4:	JSA 16,PLTIT
FINPL3:	MOVE 1,X
	MOVEM 1,XAT
	MOVE 1,Y
	MOVEM 1,YAT
	JRA 16,(16)
PLTIT:	0
	SKIPE DISMOD
	SKIPN PENP
	SKIPA
	JRST FINPL2
	SETOM DISMOD
	IONEOU [35]
	SKIPN PENP
	JRST FINPL2
	IONEOU [40]
	IONEOU [140]
	IONEOU [40]
	IONEOU [100]
FINPL2:	MOVE 1,YP
	FMPR 1,STPSIZ
	FADR 1,[0.5]
	MOVEM 1,ZOT#
	JSA 16,INT
	JUMP 2,ZOT
	CAMLE 0,YMAXI
	MOVE 0,YMAXI
	CAIGE 0,0
	MOVEI 0,0
	LSHC 0,-5
	ANDI 0,37
	IORI 40
	IONEOU
	MOVEI
	LSHC 5
	IORI 140
	IONEOU
	MOVE 1,XP
	FMPR 1,STPSIZ
	FADR 1,[0.5]
	MOVEM 1,ZOT
	JSA 16,INT
	JUMP 2,ZOT
	CAMLE 0,XMAXI
	MOVE 0,XMAXI
	CAIGE 0,0
	MOVEI 0,0
	LSHC -5
	ANDI 37
	IORI 40
	IONEOU
	MOVEI
	LSHC 5
	IORI 100
	IONEOU
	JRA 16,(16)

FMAG:   0
        MOVEM 17,SV17
        MOVEI 17,SV0
        BLT 17,SV0+16
        MOVE 1,@(16)
        MOVE 3,@1(16)
        MOVE 2,1
        MOVE 2,1
        FMPR 1,SSIZ
        MOVEM 1,STPSIZ
        MOVE 1,SXMAX
        FDVR 1,2
        MOVEM 1,XMAX
        MOVE 1,SYMAX
        FDVR 1,2
        MOVEM 1,YMAX
        MOVSI 17,SV0
        BLT 17,17
        JRA 16,2(16)


XMAXI:	^D1023
YMAXI:	^D780
XMAX:	10.23
YMAX:	7.80
SV0:	BLOCK 17
SV17:	0
ARGS:
XARG:	0
YARG:	0
ARGPEN:	0

INTERNAL STPSIZ
STPSIZ:	^D100.0
SSIZ:   ^D100.0

SXMAX:  10.23
SYMAX:  7.80
SXMAXI: ^D1023
SYMAXI: ^D780

	END
