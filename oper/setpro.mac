TITLE SETPRO - CHANGE USER'S PROFILE IN LUD.SYS
SUBTTL J. MARCIN 4/11/72

VPFL==1
JOBVER==137
LOC JOBVER
XWD 0,VPFL
RELOC

;AC'S
F=0
A=1
B=2
C=3
D=4
E=5
INDEX=6
N=7
N1=10
N2=11
N3=12
BP=15
CH=16
P=17

;CALLI'S
RESET==0
EXIT==12
GETTAB==41
;I/O CHANNELS
DSK==1

FIXTIM==0

;FLAGS IN  F
CHGMT==1
INITF==2
INIT2==4
DAYLF==100000	;BIT TO SET DAYLIGHT SAVINGS(LEFT HALF)
DETF==100000	;SET DETACH OR LOGOUT MODE8RIGHT HALF)
MESSF==200000	;BIT FOR TYMSHARE MESSAGE
FRENF==200000	;FRENCH


;FLAGS IN RIGHT HALF OF F
ONEMAT==1	;FOUND 1 PARTIAL MATCH
SPDEL==2	;SPACE ***NOT*** DELIMITER ON INPUT
CHANGED==40	;ORIG BLK READ IN WAS CHANGED

SETPRO:	CALLI RESET
	MOVE P,PDP
	SETZB F,ZERST
	MOVE A,[XWD ZERST,ZERST+1]
	BLT A,ZEREND
	MOVE A,[XWD -1,-22]	;GET THIS USER'S NAME
	CALLI A,GETTAB
	JRST ERROR1
	MOVEM A,THSUSR
	MOVE A,[XWD -1,-21]
	CALLI 	A,GETTAB
	JRST ERROR1
	MOVEM A,THSUSR+1
	PUSHJ P,HASHER
	TTCALL 3,[ASCIZ/TYPE ? FOR HELP.
/]
NEWCOM:	TTCALL 3,[ASCIZ/*/]
	SETZ INDEX,
PROF1:	SETZ E,
	MOVE BP,[POINT 6,E]
	PUSHJ P,CHRIN
	JUMPE E,PROF2
	PUSHJ P,SETTAB
PROF2:	TLZE F,INITF
	JRST PROF2B
IFN FIXTIM <
	TLZE F,CHGMT
	JRST PROF2C
>
	CAIN CH,","	;OR COMMA?
	JRST PROF1
	CAIN CH,15
	JRST INPDON
	JRST BADCOM
IFN FIXTIM<
PROF2C:	CAIE CH,"="
	JRST PROF2
	MOVEI A,2
	PUSHJ P,GETNUM
	CAILE N,30
	JRST BADNUM
	MOVEM N,NOHRS
	JRST PROF2
>
PROF2B:	TLO F,INIT2
	CAIE CH,"("
	JRST PROF2
PROF2A:	TRO F,SPDEL
	MOVEI N,^D12
	MOVE BP,[POINT 6,USRNAM]
	PUSHJ P,CHRIN+1
	CAIE CH,")"
	JRST BADCOM
PROF3:	MOVE BP,[POINT 6,FILNAM]
	PUSHJ P,CHRIN
	CAIN CH,15
	JRST INPDON
	JRST PROF2

INPDON:	TTCALL 11,0
	TLZE F,INIT2	;CHK FOR PROMPT NEEDED
	PUSHJ P,HELPIT
	PUSHJ P,GETLUD	;GET LUD ENTRY AND SET UP INDEX
	SETO E,
NXTCMD:	AOJ E,	;GO THRU COMMAND TABLE
	SKIPN ,COMTAB(E)
	JRST WROUT
	MOVE C,DSKBLK+3(INDEX)	;PICK UP PRIV WD TO BE CHANGED
	JRST @COMTAB(E)	;DO GO COMMAND
CHPRIV:	MOVEM C,DSKBLK+3(INDEX)	;SAVE NEW PRIV WD IF CHANGED
CHLUD:	TRO F,CHANGED	;LUD WAS CHANGED
	JRST NXTCMD

SETTAB:	PUSH P,INDEX	;SAVE INDEX TO COMTAB
	MOVEI INDEX,NUMCMD
	SETO B,
	MOVEI N,6
	MOVE BP,[POINT 6,E]
	ILDB C,BP
	JUMPE C,.+3
	SOJG N,.-2
	JRST NXTTRY
	HRRI BP,B	;B HAS MASK
	LDB A,[POINT 6,BP,5]	;DECREMENT BP TO
	ADDI A,6
	DPB A,[POINT 6,BP,5]
	IDPB C,BP	;ZER BITS IN MASK
	SOJG N,.-1
NXTTRY:	SOJL INDEX,TSTMAT
	CAMN E,CMDLST(INDEX)
	JRST MATCH	;ENTIRE WD MATCHES
	MOVE A,CMDLST(INDEX)
	AND A,B
	CAME A,E
	JRST NXTTRY
	MOVEM INDEX,SAVDEX	;PARTIAL MATCH
	TRON F,ONEMAT
	JRST NXTTRY	;FIRST PARTIAL MATCH - KEEP GOING
	JRST BADCOM	;>1 PARTIAL MATCH - AMBIGOUS
TSTMAT:	TRZN F,ONEMAT	;DONE,AT LEAT 1 PARTIAL MATCH?
	JRST BADCOM	;NO - ERROR
	MOVE INDEX,SAVDEX	;ONLY 1 P. MATCH - GET OLD INDEX
MATCH:	MOVE D,DISP(INDEX)	;GET DISP. ADDRESS
	POP P,INDEX	;AND INDEX FOR COMMAND TABLE
	MOVEM D,COMTAB(INDEX)	;SAVE ADDRESS IN TABLE
	ADDI INDEX,1	;INCREMENT INDEX
IFN FIXTIM<
	CAIN D,HOURS
	TLO F,CHGMT
>
	CAIN D,INITL	;CHECK TO SEE IF THIS COMMAND WAS INIT
	TLO F,INITF	;AND SET FLAG
	POPJ P,0

BADCOM:	TTCALL 3,[ASCIZ/ILLEGAL COMMAND.
/]
	TTCALL 11,0
	JRST SETPRO

	DEFINE MAKTAB(A),
	<IRP A
	<	SIXBIT /A/>>

	DEFINE MAKDIS(A),
	<IRP A
	<A>>

IFE FIXTIM<NUMCMD=14
>
IFN FIXTIM<NUMCMD==21
>

CMDLST:	MAKTAB<LIST,INIT,DEINIT,DETACH,LOGOUT,TYMEX,PDP10,SUDS,GE>
	MAKTAB<MESSAG,OMIT,QUIT>
IFN FIXTIM<MAKTAB<ZONE,DAYLIG,STAND,FRENCH,ENGLISH>>

DISP:	MAKDIS<LISTL,INITL,DEINIT,DETACH,LOGT,TYMEX,PDP10,SUDS,GE>
	MAKDIS<MESS,OMIT,QUIT>
IFN FIXTIM<MAKDIS<HOURS,DAYLIG,NODAY,FRENCH,ENGLIS>>


HELPIT:	TTCALL 11,0
	SKIPN ,FILNAM
	SKIPE ,USRNAM
	JRST GETU1
GETUSR:	TRO F,SPDEL	;SPACE IS NOT A DELIMITER ON USERNAME
	TTCALL 3,[ASCIZ/USER NAME: /]
	MOVE BP,[POINT 6,USRNAM]
	MOVEI N,^D12
	PUSHJ P,CHRIN+1
	CAIE CH,15
	JRST BADCOM
GETU1:	SKIPE ,USRNAM
	JRST GETFIL
	MOVE A,THSUSR	;USE THIS USER'S NAME
	MOVEM A,USRNAM
	MOVE A,THSUSR+1
	MOVEM A,USRNAM+1
GETFIL:	TTCALL 11,0
	SKIPE ,FILNAM
	POPJ P,0
	TTCALL 3,[ASCIZ/FILE NAME: /]
	MOVE BP,[POINT 6,FILNAM]
	PUSHJ P,CHRIN
	CAIE CH,15
	JRST BADCOM
	JRST GETFIL

INITL:	PUSHJ P,GETFRE
	MOVE A,FILNAM
	MOVEM A,DSKBLK+5(INDEX)
	MOVE A,USRNAM
	MOVEM A,DSKBLK+6(INDEX)
	MOVE A,USRNAM+1
	MOVEM A,DSKBLK+7(INDEX)
	JRST CHLUD

DEINIT:	LDB A,INITBP
	JUMPE A,NXTCMD
	SETZ A,
	DPB A,INITBP
	MOVE A,NENTRY
	SUBI A,3
	DPB A,NENTP	;DECREMENT SIZE
	MOVEI A,DSKBLK	;SHIFT EVERYTHING
	ADD A,INDEX
	ADDI A,10
	HRLS A,A
	SUBI A,3
	BLT A,DSKBLK+175
	JRST CHLUD

LISTL:	TTCALL 3,[ASCIZ/

   PROFILE DATA
   ------- ----
/]
	LDB C,INITBP
	CAIE C,1
	JRST LIST2
	TTCALL 3,[ASCIZ/
INIT (/]
	MOVE BP,[POINT 6,DSKBLK+6(INDEX)]
	MOVEI N,^D12
	TRO F,SPDEL
	PUSHJ P,SIXOUT+1
	TTCALL 3,[ASCIZ/)/]
	MOVE BP,[POINT 6,DSKBLK+5(INDEX)]
	PUSHJ P,SIXOUT
LIST2:	MOVE A,DSKBLK+3(INDEX)
	TRNE A,DETF
	JRST LIST2A
	TTCALL 3,[ASCIZ/
LOGOUT/]
	SKIPA
LIST2A:	TTCALL 3,[ASCIZ/
DETACH/]
	TTCALL 3,[ASCIZ/ ON DISCONNECT
/]
	LDB A,[POINT 2,A,28]
	TTCALL 3,@MODTAB(A)
	TTCALL 3,[ASCIZ/ MODE
/]
	MOVE A,DSKBLK+2(INDEX)
	TLNE A,MESSF
	TTCALL 3,[ASCIZ/TYMSHARE MESSAGE OMITTED ON LOGIN
/]
IFN FIXTIM<
	LDB N,[POINT 2,DSKBLK+3(INDEX),1]
	CAIN N,1
	TTCALL 3,[ASCIZ/FRENCH
/]
	LDB N,[POINT 5,DSKBLK+3(INDEX),7]
	TTCALL 3,[ASCIZ/HOURS FROM GMT= /]
	PUSHJ P,DECOUT
	LDB A,[POINT 1,DSKBLK+3(INDEX),2]
	JUMPE A,.+2
	TTCALL 3,[ASCIZ/(DAYLIGHT SAVINGS APPLIES)/]
	TTCALL 3,[ASCIZ/
/]
>
	JRST NXTCMD

SIXOUT:	MOVEI N,6
	ILDB CH,BP
	ADDI CH,40
	TTCALL 1,CH
	SOJG N,SIXOUT+1
	POPJ P,0
	
MESS:	MOVE A,DSKBLK+2(INDEX)
	TLZ A,MESSF
	MOVEM A,DSKBLK+2(INDEX)
	JRST CHLUD
OMIT:	MOVE A,DSKBLK+2(INDEX)
	TLO A,MESSF
	JRST MESS+2

IFN FIXTIM<
ENGLIS:	TLZA C,FRENF
FRENCH:	TLO C,FRENF
	JRST CHPRIV
HOURS:	MOVE N,NOHRS
	DPB N,[POINT 5,C,7]
	JRST CHPRIV
DAYLIG:	TLOA C,DAYLF
NODAY:	TLZ C,DAYLF
	JRST CHPRIV
>

DETACH:	TROA C,DETF
LOGT:	TRZ C,DETF
	JRST CHPRIV

PDP10:	SKIPA A,ZERST
TYMEX:	MOVEI A,1
	JRST SETMOD
SUDS:	MOVEI A,3
	JRST SETMOD

GE:MOVEI A,2
SETMOD:	DPB A,[POINT 2,C,28]
	JRST CHPRIV
	POPJ P,0


GETLUD:	INIT DSK,17
	SIXBIT /SYS/
	0
	JRST ERROR2
	HRLZI A,(SIXBIT/LUD/)
	HRLZI B,(SIXBIT/SYS/)
	SETZB C,D
	LOOKUP DSK,A
	JRST ERROR2
	HRLZI A,(SIXBIT/LUD/)
	HRLZI B,(SIXBIT/SYS/)
	SETZB C,D
	ENTER DSK,A
	JRST ERROR2
	USETI DSK,@HLOC	;LUD INITED IN UPDATE MODE
	INPUT DSK,DSKLST	;READ IN BLK AND FIND USER 
	STATZ DSK,760000	;ERRORS+
	JRST ERROR2	;YEP
	HLRE	N,D	;GET LAST BLK NO.
	MOVMS N
	ROT N,-7
	ADDI N,1	;AND SAVE IN CASE WE NEED TO MAKE AN OVERFLOW
	MOVEM N,LASBLK	;BLOCK
	MOVE A,HUNAME
FNDUSR:	SETZ INDEX,	;FIND USER IN LUD
	SKIPN ,DSKBLK(INDEX)	;ZER FOUND?
	JRST ERROR3	;YEP...NO NAME HERE
	LDB C,NENTP	;GET SIZE OF ENTRY
	CAME A,DSKBLK+4(INDEX)	;HASH NAME MATCHES?
	JRST NXTUSR	;NO, LOOK AT NEXT
	MOVEM C,NENTRY	;YES...SAVE SIZE
	LDB C,INITBP	;WAS FILE INITED?
	POPJ P,0
NXTUSR:	SKIPG ,DSKBLK(INDEX)
	JRST .+3
	ADD INDEX,C
	JRST FNDUSR+1
	PUSHJ P,ROVBLK
	JRST FNDUSR
WROUT:	TRZN F,CHANGED
	JRST WR1
	USETO DSK,@HLOC
	OUTPUT DSK,DSKLST
	STATZ DSK,760000
	JRST ERROR2
WR1:	CLOSE DSK,
	RELEASE DSK,
	JRST NEWCOM
QUIT:	CALLI 1,EXIT

;INPUT 6 (OR N) CHARACTERS INTO BP; EXIT ON ANYTHING BUT LETTER
;OR NUMBER; FILL WITH ZEROES; KEEP INPUTTING UNTIL BRK CHR FOUND
CHRIN:	MOVEI N,6
	MOVEI B,0
	TTCALL 4,CH
	JUMPE CH,.-1
	CAIN CH,"?"
	JRST HELP
	CAIL CH,60
	CAILE CH,132
	JRST CHKBRK
	CAILE CH,71
	CAIL CH,101
	JRST OK
CHKBRK:	TRNN F,SPDEL
	JRST CHKSPC
	CAIN CH,40
	JRST OK
	SKIPA
FILLIT:	IDPB B,BP
	SOJG N,.-1
	TRZ F,SPDEL
	POPJ P,0
OK:	SOJL N,CHRIN+2
	SUBI CH,40
	IDPB CH,BP
	JRST CHRIN+2
CHKSPC:	CAIE CH,40
	JRST FILLIT+1
	TTCALL 4,CH
	JRST CHKSPC

;GET A NUMBER IN N (A DIGITS)(DECIMAL)
GETNUM:	SETZB C,N
	TTCALL 4,CH
	CAIN CH,"?"
	JRST HELP
	CAIG CH,71
	CAIGE CH,60
	JRST ALLIN
	SUBI CH,60
	PUSH P,CH
	SOJL A,BADNUM
	AOJA C,GETNUM+1
ALLIN:	MOVEI A,1
ALL1:	POP P,B
	IMUL B,A
	ADDM B,N
	IMULI A,^D10
	SOJG C,ALL1
	POPJ P,0

DECOUT:	JUMPE N,ZEROUT
	MOVEI A,^D100000
	IDIVI A,^D10
	MOVE C,N
	IDIV C,A
	JUMPE C,.-3
	ADDI C,60
	TTCALL 1,C
	IDIVI A,^D10
	MOVE C,D
	IDIV C,A
	CAIE A,1
	JRST .-6
	ADDI C,60
	TTCALL 1,C
	POPJ P,0
ZEROUT:	TTCALL  3,[ASCIZ/0/]
	POPJ P,0


BADNUM:	TTCALL 3,[ASCIZ/NUMBER TOO LARGE.
/]
	TTCALL 11,0
	CALLI EXIT

	POP P,CH
		ADDM CH,N

GETFRE:	LDB A,INITBP
	CAIN A,1
	POPJ P,0	;ALLREADY AN INIT ENTRY
	MOVE N,NENTRY	;GET SIZE OF OLD ENTRY
	ADDI N,3	;INCREASE
	MOVEM N,NENTRY	;SAVE NEW ENTRY SIZE
	MOVEI A,DSKBLK
	SUBI A,3
	ADD A,N
	ADD A,INDEX
	SKIPE ,@A	;IS WD FOLLOWING ENTRY ZERO?
	JRST GET1	;NO MUST MAKE ROOM
	MOVEI B,DSKBLK	;ROOM HERE?
	ADD B,N	;BEFORE END OF BLK
	CAILE B,DSKBLK+177
	JRST GET1
BACK:	MOVE A,DSKBLK+7(INDEX)
	MOVEM A,DSKBLK+12(INDEX)
	MOVE A,DSKBLK+6(INDEX)
	MOVEM A,DSKBLK+11(INDEX)
	MOVE A,DSKBLK+5(INDEX)
	MOVEM A,DSKBLK+10(INDEX)
	MOVEI A,1
	DPB A,INITBP	;SET FLAG
	DPB N,NENTP	;INCREASE ENTRY SIZE
	POPJ P,0
GET1:	TRO F,CHANGED	;MUST MOVE ENTRY - SET CHANGED FLAG
	MOVEI A,DSKBLK
	ADD A,INDEX
	HRLZS A,A
	HRRI A,SAVBLK
	MOVEI B,SAVBLK
	ADD B,NENTRY
	SUBI B,3
	BLT A,@B	;SAVE OLD INFO
	SETZM ,DSKBLK+4(INDEX)	;ZER OLD ENTRY
	LDB C,NENTP
	JRST GETFR4
GET2:	SETZ INDEX,	;SEE IF ROOM IN THIS BLK
	SKIPE A,DSKBLK(INDEX)	;ZER WD FOUND?
	JRST GETFR2	;CHK FOR OVERFLW
	MOVE C,INDEX
	ADD C,N	;ROOM AT END?
	CAIGE C,200
	JRST ROOMFN	;ROOM AT END...RESTORE ENTRY
	AOS A,LASBLK	;MAKE OVERFLOW BLK
	TLO A,400000	;SET FLAG
	MOVEM A,DSKBLK(INDEX)	;SAVE PTR
	USETO DSK,@HLOC	;WRITE OUT OLD BLK
	OUTPUT DSK,DSKLST
	STATZ DSK,740000
	JRST ERROR2
	HRRZM A,HLOC	;SAVE NEW BLK NO.
	SETZB INDEX,DSKBLK	;ZERO BLK IN CORE
	MOVE A,[XWD DSKBLK,DSKBLK+1]
	BLT A,DSKBLK+177
	JRST ROOMFN
GETFR2:	JUMPL A,GETFR3	;GO GET OVERFLOW BLK
	LDB C,NENTP	;GET ENTRY SIZE
	SKIPN ,DSKBLK+4(INDEX)	;ZERO ENTRY?
	JRST .+3	;ENTRY IS BIG ENUF
GETFR4:	ADD INDEX,C	;NO, GET NEXT ENTRY
	JRST GET2+1
	CAMLE N,C
	JRST GETFR4
	CAME N,C
	JRST ROOM1
	JRST ROOMFN
GETFR3:	PUSHJ P,ROVBLK
	JRST GET2
ROOM1:	MOVE B,C
	SUB B,N
	HRRZI A,DSKBLK
	ADD A,INDEX
	ADD A,C
	HRLS A,A
	SUB A,B
	MOVEI C,DSKBLK
	ADDI C,200
	SUB C,B
	BLT A,@B
ROOMFN:	HRLZI A,SAVBLK	;PLACE FOUND,RETURN INFO
	HRRI A,DSKBLK
	ADD A,INDEX
	MOVEI B,DSKBLK
	ADD B,INDEX
	ADD B,NENTRY
	BLT A,@B
	JRST BACK

HASHER:	MOVE N,THSUSR
	MOVE N1,THSUSR+1
	MOVEI B,0
	MOVE C,[555555555555]
	MOVE D,[361275431652]
	MOVE E,[612754316523]
	JSR RND
	JSR RND
	JSR RND
	JSR RND
	XOR E,D
	MOVE N,E
	TLZ N,400000
	IDIVI N,^D887
	MOVE N,E
	XOR N,C
	ADDI N1,1
	MOVEM N,HUNAME
	MOVEM N1,HLOC
	POPJ P,0

RND:	0
	ADD D,N
	ROTC N,-22
	MOVEI A,5
RND1:	MOVE N2,C+1(B)
	MUL N2,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
	ADDM N3,D+1(B)
	AOJE B,RND2
	MOVNI B,1
	TRNE D,1
	SKIPL E
	MOVEI B,0
	EXCH C,E
RND2:	SOJG A,RND1
	JRST @RND

;READ OVERFLOW BLK
ROVBLK:	HRRZ C,DSKBLK(INDEX)
	TLNN F,CHANGE
	JRST ROVBL1
	USETO DSK,@HLOC
	OUTPUT DSK,DSKLST
	STATZ DSK,740000
	JRST ERROR2
ROVBL1:	MOVEM C,HLOC
	USETI DSK,@HLOC
	INPUT DSK,DSKLST
	STATZ DSK,760000
	JRST ERROR2
	SETZ INDEX,
	POPJ P,0

MODTAB:	[ASCIZ/PDP10/]
	[ASCIZ/TYMEX/]
	[ASCIZ/GE/]
	[ASCIZ/SUDS/]

HELP:	TTCALL 11,0
	TTCALL 3,HELMSG
IFN FIXTIM<
	TTCALL 3,HELMS2
>
	JRST NEWCOM

HELMSG:	ASCIZ/TYPE COMMANDS SEPARATED BY COMMAS.
YOU HAVE THE FOLLOWING OPTIONS:

LIST	;LISTS ALL PROFILE INFORMATION
INIT (USERNAME)FILE	;SET UP INIT FILE NAMED
DEINIT	;CANCELS EFFECT OF LAST INIT COMMAND
DETACH	;JOB WILL DETACH ON DISCONNECT
LOGOUT	;JOB WILL LOGOUT ON DISCONNECT
TYMEX	;SETS TYMEX MODE
PDP10 	;SETS PDP10 MODE
SUDS	;SETS SUDS MODE
GE	;SETS GE MODE
MESSAG	;'TYMSHARE' MESSAGE TYPED OUT ON LOGIN
OMIT	;'TYMSHARE' MESSAGE NOT TYPED OUT ON LOGIN
QUIT	;EXIT
/

IFN FIXTIM<
HELMS2:	ASCIZ/ENGLISH	;SETS LANGUAGE BITS TO ENGLISH
FRENCH	;SETS LANGUAGE BITS TO FRENCH
ZONE=N	;SET TIME ZONE TO BE N HOURS FROM GMT
DAYLIG	;SET DAYLIGHT SAVINGS TIME
STAND	;SET STANDARD TIME
/
>

;ERROR MESSAGES
ERROR1:	TTCALL 3,[ASCIZ/
GETTAB ERROR.
/]
	CALLI EXIT

ERROR2:	TTCALL 3,[ASCIZ/
LUD ERROR.
/]
	CALLI EXIT

ERROR3:	TTCALL 3,[ASCIZ/
NAME NOT IN LUD!
/]
	CALLI EXIT


;STORAGE

ZERST:	0
NOHRS:	0
SAVDEX:	0	;SAVE INDEX FOR FIRST COMMAND MATCH
COMTAB:	BLOCK NUMCMD
LASBLK:	0
FILNAM:	0
USRNAM:	BLOCK 2
THSUSR:	BLOCK 2
HUNAME:	0
HLOC:	0
DSKBLK:	BLOCK 200
NENTRY:	0
SAVBLK:	BLOCK 200
ZEREND:	0
DSKLST:	IOWD 200,DSKBLK
	0
NENTP:	POINT 7,DSKBLK+2(INDEX),35
INITBP:	POINT 1,DSKBLK+2(INDEX),27
PDP:	XWD -20,.
	BLOCK 21
	END SETPRO
 