C	COPYRIGHT C 1972 BY CORE & CODE, INC.
	SUBROUTINE MN4ZZZ
C
C THIS ROUTINE IS A 2ND LEVEL DUMMY MAIN PROGRAM FOR THE PROFILE/OUTFIL
C OUTPUT LINK OF PROFILE.  ITS SOLE PURPOSE IS TO SET UP THE ARGUMENT
C LIST PASSED FROM THE LAST LINK (VIA CR3ZZZ) WHICH THE TRUE MAIN
C ROUTINE NEEDS.
C
	COMMON /C17ZZZ/ LASLNK,LNKCOD,NJOBFF,INJFF,IRJFF,
     +  IRUNID,IRNAME(2),IREXT(2),IDNAME(2),IDEXT(2),KRAVE,KRISK,KROI,
     +  NEWJFF,NAMTP2(2),NEXTP2(2),NAMLK2(2),NEXLK2(2),NAMLK4(2),
     +  NEXLK4(2),NAMLK5(2),NEXLK5(2)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	EQUIVALENCE (KRAVE,RAVE),(KRISK,RISK),(KROI,ROI)
	EXTERNAL MNOZZZ
	NUMPRO=MAXPRO+1
	CALL CR3ZZZ(3,9,MNOZZZ,RAVE,RISK,ROI,NPER,NOROW,NTRIAL,NUMPRO,
     +  NPER,MAXPFR)
	END
C
C
C
	SUBROUTINE MNOZZZ(RAVE,RISK,ROI,IRAVE,JRAVE,IRISK,JRISK,
     +  IROI,JROI)
C
C THIS ROUTINE IS THE MAIN PROGRAM FOR THE PROFILE/OUTFIL OUTPUT
C REPORTING LINK OF PROFILE.  THE ROUTINE IS CALLED BY A DUMMY MAIN
C PROFILE/OUTFIL MAIN PROGRAM WHICH SETS UP THE ARGUMENTS USED
C BY THIS ROUTINE.  THIS ROUTINE WILL CONTROL THE CALLING OF ALL
C PROFILE REPORTING SUBROUTINES ACCORDING TO THE OUTPUT REPORT MIX
C SPECIFIED ON THE MOST RECENTLY RETRIEVED REPORT FILE.
C
C RAVE=STANDARD REPORT MATRIX
C RISK=RISK PROFILE MATRIX
C ROI=ROI MATRIX
C IRAVE=CURRENT STANDARD REPORT I INDEX MAXIMUM (NUMBER OF PERIODS)
C JRAVE=CURRENT STANDARD REPORT J INDEX MAXIMUM (NUMBER OF ROWS)
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C IROI=CURRENT ROI I INDEX MAXIMUM (NUMBER OF PERIODS)
C JROI=CURRENT ROI J INDEX MAXIMUM (NUMBER OF LINES IN ROI REPORT)
C
	COMMON /C18ZZZ/ IAGRUS(2),IOCTGR,IOCTUS
	COMMON /C17ZZZ/ LASLNK,LNKCOD,NJOBFF,INJFF,IRJFF,
     +  IRUNID,IRNAME(2),IREXT(2),IDNAME(2),IDEXT(2),KRAVE,KRISK,KROI,
     +  NEWJFF,NAMTP2(2),NEXTP2(2),NAMLK2(2),NEXLK2(2),NAMLK4(2),
     +  NEXLK4(2),NAMLK5(2),NEXLK5(2)
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C02ZZZ/ IERREX,NOTRND,NOMULT,KSEED,NOCOPY,ITMPDT,
     +  NAMTP3(2),NEXTP3(2),ISTRON,IOLDTR,IOLDPR,IOLDRW,IBINON
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C15ZZZ/ ITIMON,ICONN(4),ICOMP(4)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C13ZZZ/ RMEAN(9),RDEV(9)
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	DIMENSION RISK(IRISK,JRISK),RAVE(IRAVE,JRAVE),ROI(IROI,JROI)
	LASLNK=4
	IF(LNKCOD.NE.4)GO TO 1700
	CALL BINZZZ(RAVE,IRAVE,JRAVE,RISK,IRISK,JRISK)
	GO TO 2000
1700	ITTYSK=0
	IF(ITIMON.EQ.1)GO TO 1800
	CALL FFOZZZ(0,0,0)
	CALL SETZZZ(2)
	CALL SEEZZZ(1,2,IPOUT,2)
1800	DO 1810 I=1,NOCOPY
1810	CALL PRNZZZ(RISK,IRISK,JRISK,RAVE,IRAVE,JRAVE)
	IF(ITIMON.EQ.1)GO TO 1900
	CALL SETZZZ(2)
	CALL SEEZZZ(1,2,IPOUT,2)
1900	ITTYSK=ICMDOP
2000	CALL C76ZZZ(ICHNM,NAMLK2(1),NAMLK2(2))
	CALL C76ZZZ(ICHNE,NEXLK2(1),NEXLK2(2))
	CALL CHNZZZ(0,ICHNM,ICHNE,IOCTGR,IOCTUS,INJFF-1)
	END
C
C
C
	SUBROUTINE SORZZZ(NORSK,RISK,IRISK,JRISK)
C
C THIS ROUTINE WILL SORT ITRIAL ENTRIES IN ANY RISK PROFILE BLOCK OF
C THE RISK MATRIX.  AFTER SORTING, THE ROUTINE CALCULATES THE MEAN
C AND STANDARD DEVIATION OF THE ENTRIES IN THE RISK PROFILE, AND LOADS
C THE INFORMATION INTO THE APPROPRIATE ELEMENTS OF RMEAN AND RDEV.
C
C NORSK=NUMBER OF RISK PROFILE TO SORT AND CALCULATE STATISTICS
C RISK=RISK PROFILE MATRIX
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C
	COMMON /C13ZZZ/ RMEAN(9),RDEV(9)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION RISK(IRISK,JRISK)
	TOTRSK=0.0
	TOTSQR=0.0
	JKRISK=JRISK
	DO 5 I=1,ITRIAL
5	RISK(I,JKRISK)=RISK(I,NORSK)
	DO 20 J=1,ITRIAL
	MINLOC=J
	DO 10 K=J,ITRIAL
10	IF(RISK(K,JKRISK).LT.RISK(MINLOC,JKRISK))MINLOC=K
	RTEMP=RISK(J,JKRISK)
	RISK(J,JKRISK)=RISK(MINLOC,JKRISK)
	RISK(MINLOC,JKRISK)=RTEMP
	TOTRSK=TOTRSK+RISK(J,JKRISK)
20	TOTSQR=TOTSQR+RISK(J,JKRISK)**(2)
	IF(RISK(ITRIAL,JKRISK).GT.RISK(1,JKRISK))GO TO 30
	RMEAN(NORSK)=RISK(1,JKRISK)
	RDEV(NORSK)=0.0
	RETURN
30	RMEAN(NORSK)=TOTRSK/FLOAT(ITRIAL)
	RDEV(NORSK)=SQRT(TOTSQR/FLOAT(ITRIAL)-RMEAN(NORSK)**(2))
	RETURN
	END
C
C
C
	SUBROUTINE PRNZZZ(RISK,IRISK,JRISK,RAVE,IRAVE,JRAVE)
C
C THIS ROUTINE CONTROLS THE PRINTING OF ALL OUTPUT REPORTS INCLUDING
C RISK PROFILES AND THE STANDARD REPORT AS SPECIFIED BY THE USER.
C
C RISK=RISK PROFILE MATRIX
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C RAVE=STANDARD REPORT MATRIX
C IRAVE=CURRENT STANDARD REPORT I INDEX MAXIMUM (NUMBER OF PERIODS)
C JRAVE=CURRENT STANDARD REPORT J INDEX MAXIMUM (NUMBER OF ROWS)
C
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C04ZZZ/ INPUT(320),INPOS,INSIZE,INCONT
	DIMENSION RISK(IRISK,JRISK),RAVE(IRAVE,JRAVE)
	CALL FFOZZZ(0,0,0)
	IF(ISTDON.GE.1)CALL PSTZZZ(RAVE,IRAVE,JRAVE)
	IF(IPFRON(1).GE.1)CALL PPVZZZ(1,RISK,IRISK,JRISK)
	IF(IPFRON(2).GE.1)CALL PPVZZZ(2,RISK,IRISK,JRISK)
	IF(IPFRON(3).GE.1)CALL PRIZZZ(RISK,IRISK,JRISK)
	DO 10 I=1,MAXRSK
	IF(IRSKON(I).LT.1)GO TO 10
	IF(IRSKRW(I).GT.JRAVE)GO TO 10
	IF(IRSKCL(I).GT.IRAVE)GO TO 10
	CALL PRSZZZ(I,RISK,IRISK,JRISK)
10	CONTINUE
	IF(ITRIAL.LE.1)RETURN
	CALL SKPZZZ(IPOUT,2)
	CALL OUTZZZ(2,19HSIMULATION TRIALS =,1,19,2,1,IPOUT)
	CALL TRZZZ(FLOAT(ITRIAL),11,NOCHRS)
	CALL OUTZZZ(1,INPUT,1,NOCHRS,3,0,IPOUT)
	CALL WZZZ(IPOUT,0)
	RETURN
	END
C
C
C
	SUBROUTINE PTTZZZ
C
C THIS ROUTINE CONTROLS THE PRINTING OF THE TITLE LINE AND THE RUN I.D.
C WHICH APPEARS AS THE LAST TITLE LINE.  THE ROUTINE CAUSES ANY PRINTING
C TO BE CENTERED WITHIN THE WIDTH AS SPECIFIED BY THE USER.
C
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	CALL SKPZZZ(IPOUT,2)
	IF(NOTIT.LE.0) RETURN
	NCPP=MAXRCH+ISTDWD*ICOLWD
	DO 100 I=1,NOTIT
	ISTART=(I-1)*MAXTWD+1
	ISTOP=LASZZZ(ITITLE,ISTART,I*MAXTWD)
	CALL OUTZZZ(2,ITITLE,ISTART,ISTOP-(ISTART-1)*IWDSIZ,4,NCPP,
     +  IPOUT)
	CALL WZZZ(IPOUT,0)
100	CONTINUE
	RETURN
	END
C
C
C
	SUBROUTINE PCLZZZ(ICNBEG,ICNEND,ITMCOL)
C
C THIS ROUTINE CONTROLS PRINTING THE COLUMN HEADINGS ACROSS THE PAGE.
C HEADINGS ARE RIGHT JUSTIFIED OVER EACH COLUMN, AND EACH HEADING IS
C MAXCCH CHARACTERS WIDE.
C
C ICNBEG=BEGINNING COLUMN NUMBER
C ICNEND=LAST COLUMN NUMBER
C ITMCOL=COLUMN NUMBERS OF COLUMN ALPHAS TO BE PRINTED
C
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	DIMENSION ITMCOL(1)
	CALL SKPZZZ(IPOUT,2)
	IPOS=MAXRCH
	DO 100 I=ICNBEG,ICNEND
	IPOS=IPOS+ICOLWD
	ISTART=(ITMCOL(I)-1)*MAXCWD+1
	ISTOP=LASZZZ(ICOLUM,ISTART,ITMCOL(I)*MAXCWD)
	IF(ISTOP.GT.0)CALL OUTZZZ(2,ICOLUM,ISTART,
     +  ISTOP-(ISTART-1)*IWDSIZ,1,IPOS,IPOUT)
100	CONTINUE
	CALL WZZZ(IPOUT,0)
	CALL SKPZZZ(IPOUT,1)
	RETURN
	END
C
C
C
	SUBROUTINE PSTZZZ(RAVE,IRAVE,JRAVE)
C
C THIS ROUTINE CONTROLS THE PRINTING OF A STANDARD REPORT.
C IT DOES NOT PRINT THE TITLE (PRINTED BY PRITIT).
C
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C04ZZZ/ INPUT(320),INPOS,INSIZE,INCONT
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C13ZZZ/ RMEAN(9),RDEV(9)
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /C06ZZZ/ IOPTN(4),NROPTS,NSOPTS,I10POW(4),NCOPTS
	DIMENSION RAVE(IRAVE,JRAVE),ITMCOL(52),IDASH(4),IPRNAD(9)
	EQUIVALENCE (NULLON,IOPTN(1)),(NODEC,IOPTN(2)),
     +  (NSKIP,IOPTN(3)),(NULINE,IOPTN(4))
	DATA IDASH/2*5H-----,2*5H=====/,IPRNAD/0,0,0,1,0,0,0,0,1/
	IF(ISTDLN(MAXROW+1).EQ.0)GO TO 40
	NUMCOL=0
	DO 30 I=1,MAXCOL
	IF(ISTDLN(MAXROW+I).EQ.0)GO TO 35
	ISTDTP=IABS(ISTDLN(MAXROW+I))
	IF(ISTDTP.LE.MAXCOL)GO TO 5
	ISTDTP=INT(D(ISTDTP/10000)+.5)
	IF(ISTDTP.LE.0)GO TO 30
5	IF(ISTDTP.GT.NPER)GO TO 30
	IF(ISTDLN(MAXROW+I).LT.0)GO TO 10
	IF(NUMCOL.GE.MAXCOL)GO TO 50
	NUMCOL=NUMCOL+1
	ITMCOL(NUMCOL)=ISTDTP
	GO TO 30
10	IF(NUMCOL.GE.MAXCOL)GO TO 50
	IF(ISTDTP-ITMCOL(NUMCOL))15,30,20
15	ITMCOL(NUMCOL+1)=ITMCOL(NUMCOL)-1
	GO TO 25
20	ITMCOL(NUMCOL+1)=ITMCOL(NUMCOL)+1
25	NUMCOL=NUMCOL+1
	GO TO 10
30	CONTINUE
35	IF(NUMCOL.LE.0)RETURN
	GO TO 50
40	DO 45 I=1,NPER
45	ITMCOL(I)=I
	NUMCOL=NPER
	GO TO 35
50	NTPASS=(NUMCOL-1)/ISTDWD+1
	CALL PAGZZZ(0,0,0,0)
	IF(IOPAGE.EQ.0)CALL PTTZZZ
	DO 1000 NPASS=1,NTPASS
	ICNBEG=(NPASS-1)*ISTDWD+1
	ICNEND=MIN0(NUMCOL,ICNBEG+ISTDWD-1)
	IF(IOPAGE.LE.0)GO TO 52
	CALL PAGZZZ(100,ICNBEG,ICNEND,ITMCOL)
	GO TO 54
52	CALL PCLZZZ(ICNBEG,ICNEND,ITMCOL)
54	DO 500 N=1,MAXROW
	IF(ISTDLN(1).EQ.0)GO TO 75
	NEXROW=IABS(ISTDLN(N))
	IF(NEXROW.EQ.0)GO TO 1000
	IF(NEXROW.LE.MAXROW)GO TO 55
	NEXROW=INT(D(NEXROW/10000)+.5)
	IF(NEXROW.LE.0)GO TO 500
55	IF(NEXROW.GT.JRAVE)GO TO 500
	IF(ISTDLN(N).LT.0)GO TO 60
	NROW=NEXROW
	GO TO 80
60	IF(NEXROW-NROW)65,500,70
65	NROW=NROW-1
	GO TO 80
70	NROW=NROW+1
	GO TO 80
75	NROW=N
	IF(NROW.GT.NOROW)GO TO 1000
80	IF(IOPAGE.GT.0)CALL PAGZZZ(1,ICNBEG,ICNEND,ITMCOL)
	CALL OUTZZZ(2,IROW1,(NROW-1)*MAXRWD+1,MAXRCH,2,1,IPOUT)
	NPOS=MAXRCH
	DO 100 NCOL=ICNBEG,ICNEND
	NPOS=NPOS+ICOLWD
	CALL GRXZZZ(ITMCOL(NCOL),NROW)
	IF(NULLON.NE.1)GO TO 85
	IF(RAVE(ITMCOL(NCOL),NROW).EQ.0.0) GO TO 90
85	CALL TRZZZ(RAVE(ITMCOL(NCOL),NROW),NODEC,NCHRS)
	CALL OUTZZZ(1,INPUT,1,NCHRS,1,NPOS+IPRNAD(NODEC/10),IPOUT)
	GO TO 100
90	CALL OUTZZZ(1,1H ,1,1,1,NPOS,IPOUT)
100	CONTINUE
	CALL WZZZ(IPOUT,0)
	IF(NULINE.LE.0)GO TO 300
	IF(IOPAGE.GT.0)CALL PAGZZZ(1,ICNBEG,ICNEND,ITMCOL)
	NPOS=MAXRCH
	DO 200 NCOL=ICNBEG,ICNEND
	NPOS=NPOS+ICOLWD
200	CALL OUTZZZ(2,IDASH,2*NULINE-1,ICOLWD-3,1,NPOS,IPOUT)
	CALL WZZZ(IPOUT,0)
300	IF(NSKIP.LE.0)GO TO 450
	DO 400 MSKIP=1,NSKIP
	IF(IOPAGE.GT.0)CALL PAGZZZ(1,ICNBEG,ICNEND,ITMCOL)
400	CALL SKPZZZ(IPOUT,1)
450	IF(ISTDLN(1).EQ.0)GO TO 500
	IF(ISTDLN(N).LT.0)GO TO 60
500	CONTINUE
1000	IF(IOPAGE.LE.0)CALL SKPZZZ(IPOUT,2)
	IF(IOPAGE.GT.0)CALL PAGZZZ(200,ICNBEG,ICNEND,ITMCOL)
	RETURN
	END
C
C
C
	SUBROUTINE PPTZZZ(NORSK,PROBIN,VALOUT,RISK,IRISK,JRISK)
C
C THIS ROUTINE WILL CALCULATE THE VALUE OF ANY FRACTILE OF A
C SMOOTHED CUMULATIVE PROBABILITY DISTRIBUTION UTILIZING ITRIAL
C VALUES FOUND IN THE JRISK BLOCK OF RISK.
C
C NORSK=NUMBER OF RISK PROFILE IN WHICH FRACTILE IS TO BE FOUND
C PROBIN=PROBABILITY POINT DESIRED
C VALOUT=FRACTILE VALUE RETURNED
C RISK=RISK PROFILE MATRIX
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C
	COMMON /C13ZZZ/ RMEAN(9),RDEV(9)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION RISK(IRISK,JRISK)
	IF(RISK(ITRIAL,JRISK).GT.RISK(1,JRISK))GO TO 100
	VALOUT=RISK(1,JRISK)
	RETURN
100	PRBWNT=PROBIN
	IF(PRBWNT.LT.0.0)PRBWNT=0.0
	IF(PRBWNT.GT.1.0)PRBWNT=1.0
	PRBNOW=0.0
	STEP=1.0/FLOAT(ITRIAL)
	DO 200 I=1,ITRIAL
	VALNOW=RISK(I,JRISK)
	PRBNOW=PRBNOW+STEP
	IF(I.EQ.ITRIAL)GO TO 400
	IF(RISK(I+1,JRISK).LE.VALNOW)GO TO 200
	IF(PRBNOW.GE.PRBWNT)GO TO 400
200	CONTINUE
400	J=I
	PRBLAS=PRBNOW
500	J=J-1
	PRBLAS=PRBLAS-STEP
	IF(J.LE.0)GO TO 700
	IF(RISK(J,JRISK).GE.VALNOW)GO TO 500
	VALLAS=RISK(J,JRISK)
	IF(I.EQ.ITRIAL)GO TO 600
	VALNEX=RISK(I+1,JRISK)
	GO TO 800
600	VALNEX=VALNOW+(VALNOW-VALLAS)
	GO TO 800
700	VALNEX=RISK(I+1,JRISK)
	VALLAS=VALNOW-(VALNEX-VALNOW)
800	TOPPT=VALNEX-.5*(VALNEX-VALNOW)
	BOTPT=VALNOW-.5*(VALNOW-VALLAS)
	VALOUT=BOTPT+(TOPPT-BOTPT)*(PRBWNT-PRBLAS)/(PRBNOW-PRBLAS)
	IF(NORSK.NE.3)RETURN
	IF(VALOUT.LT.0.0)VALOUT=0.0
	IF(VALOUT.GT.1000.0)VALOUT=1000.0
	RETURN
	END
C
C
C
	SUBROUTINE PRSZZZ(NORP,RISK,IRISK,JRISK)
C
C THIS ROUTINE CONTROLS THE PRINTING OF THE RISK PROFILE REPORTS.
C
C NORP=NUMBER OF THE RISK PROFILE TO PRINT
C RISK=RISK PROFILE MATRIX
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C06ZZZ/ IOPTN(4),NROPTS,NSOPTS,I10POW(4),NCOPTS
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C13ZZZ/ RMEAN(9),RDEV(9)
	DIMENSION RISK(IRISK,JRISK)
	CALL SORZZZ(3+NORP,RISK,IRISK,JRISK)
	LOADRT=18
	IF(IRSKIN(NORP).LE.0)LOADRT=1
	IF(ITRIAL.LE.1) LOADRT=1
	NOPRIN=MIN0(IRSKON(NORP),2)
	DO 100 N=1,NOPRIN
	CALL FFOZZZ(N,LOADRT,IRSKIN(NORP))
	CALL SKPZZZ(IPOUT,2)
	CALL LLFZZZ(1,LOADRT)
	CALL OUTZZZ(2,19HFORECASTED VALUE OF,1,19,2,LOADRT,IPOUT)
	CALL WZZZ(IPOUT,0)
	CALL LLFZZZ(2,LOADRT)
	CALL OUTZZZ(2,IROW1,(IRSKRW(NORP)-1)*MAXRWD+1,MAXRCH,2,LOADRT,
     +  IPOUT)
	CALL WZZZ(IPOUT,0)
	IF((N-1)*IRSKON(NORP).LE.2)CALL LLFZZZ(3,LOADRT)
	CALL OUTZZZ(2,ICOLUM,(IRSKCL(NORP)-1)*MAXCWD+1,MAXCCH,2,LOADRT,
     +  IPOUT)
	CALL WZZZ(IPOUT,0)
	CALL GRXZZZ(IRSKCL(NORP),IRSKRW(NORP))
	IF(N.EQ.1)CALL TABZZZ(3+NORP,LOADRT,IRSKIN(NORP),IOPTN(2),
     +  RMEAN(3+NORP),RISK,IRISK,JRISK)
	IF(N.EQ.2)CALL GRAZZZ(3+NORP,IOPTN(2),RISK,IRISK,JRISK,
     +  IRSKON(NORP))
100	LOADRT=18
	RETURN
	END
C
C
C
	SUBROUTINE PPVZZZ(NORP,RISK,IRISK,JRISK)
C
C THIS ROUTINE CONTROLS THE PRINTING OF THE PRESENT VALUE
C REPORT AND THE FUTURE VALUE REPORT (CONTROLED BY NORP) AS
C EITHER A SINGLE VALUE (THE MEAN) OR A RISK PROFILE.
C
C NORP=NUMBER OF THE RISK PROFILE (1=PV, 2=FV)
C RISK=RISK PROFILE MATRIX
C IRISK=CURRENT RISK I INDEX MM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C
	COMMON /C13ZZZ/ RMEAN(9),RDEV(9)
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	DIMENSION RISK(IRISK,JRISK),NAMEPV(6),PFRATE(2)
	DATA NAMEPV/5HPRESE,5HNT VA,5HLUE  ,
     +              5HFUTUR,5HE VAL,5HUE   /
	EQUIVALENCE (PFRATE(1),PVRATE)
	CALL SORZZZ(NORP,RISK,IRISK,JRISK)
	LOADRT=18
	IF(IPFRIN(NORP).LE.0)LOADRT=1
	IF(ITRIAL.LE.1)LOADRT=1
	NOPRIN=MIN0(IPFRON(NORP),2)
	DO 100 N=1,NOPRIN
	CALL FFOZZZ(N,LOADRT,IPFRIN(NORP))
	CALL SKPZZZ(IPOUT,2)
	CALL LLFZZZ(1,LOADRT)
	CALL OUTZZZ(2,NAMEPV,(NORP-1)*3+1,13,2,LOADRT,IPOUT)
	CALL WZZZ(IPOUT,0)
	CALL LLFZZZ(2,LOADRT)
	CALL SRWZZZ(NORP,2,LOADRT)
	IF((N-1)*IPFRON(NORP).LE.2)CALL LLFZZZ(3,LOADRT)
	CALL SRTZZZ(PFRATE(NORP)*100.0,2,LOADRT)
	IF(N.EQ.1)CALL TABZZZ(NORP,LOADRT,IPFRIN(NORP),11,
     +  RMEAN(NORP),RISK,IRISK,JRISK)
	IF(N.EQ.2)CALL GRAZZZ(NORP,11,RISK,IRISK,JRISK,IPFRON(NORP))
100	LOADRT=18
	RETURN
	END
C
C
C
	SUBROUTINE PRIZZZ(RISK,IRISK,JRISK)
C
C THIS ROUTINE CONTROLS THE PRINTING OF THE ROI REPORT AS EITHER A
C SINGLE VALUE (THE ROI OF THE MEAN CASH FLOWS) OR A RISK PROFILE.
C
C RISK=RISK PROFILE MATRIX
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C
	COMMON /C04ZZZ/ INPUT(320),INPOS,INSIZE,INCONT
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C13ZZZ/ RMEAN(9),RDEV(9)
	DIMENSION RISK(IRISK,JRISK)
	CALL SORZZZ(3,RISK,IRISK,JRISK)
	LOADRT=18
	IF(IPFRIN(3).LE.0)LOADRT=1
	IF(ITRIAL.LE.1)LOADRT=1
	NOPRIN=MIN0(IPFRON(3),2)
	DO 100 N=1,NOPRIN
	CALL FFOZZZ(N,LOADRT,IPFRIN(3))
	CALL SKPZZZ(IPOUT,2)
	CALL LLFZZZ(1,LOADRT)
	CALL OUTZZZ(2,15HVALUE OF RETURN,1,15,2,LOADRT,IPOUT)
	CALL WZZZ(IPOUT,0)
	CALL LLFZZZ(2,LOADRT)
	CALL OUTZZZ(2,13HON INVESTMENT,1,13,2,LOADRT,IPOUT)
	CALL WZZZ(IPOUT,0)
	IF((N-1)*IPFRON(3).LE.2)CALL LLFZZZ(3,LOADRT)
	CALL SRWZZZ(3,2,LOADRT)
	IF(N.EQ.2)GO TO 50
	AVE=RMEAN(3)
	IF(LOADRT.LE.1)AVE=RATE(3)
	CALL TABZZZ(3,LOADRT,IPFRIN(3),13,AVE,RISK,IRISK,JRISK)
	IF(LOADRT.LE.1)GO TO 100
	CALL OUTZZZ(2,14HROI OF AVERAGE,1,14,2,1,IPOUT)
	CALL TRZZZ(RATE(3),13,NOCHRS)
	CALL OUTZZZ(1,INPUT,1,NOCHRS,1,LOADRT+11-1,IPOUT)
	CALL OUTZZZ(2,2H %,1,2,3,0,IPOUT)
	CALL WZZZ(IPOUT,0)
	CALL SRWZZZ(3,2,1)
	GO TO 100
50	CALL GRAZZZ(3,33,RISK,IRISK,JRISK,IPFRON(3))
100	LOADRT=18
	RETURN
	END
C
C
C
	SUBROUTINE TABZZZ(NORSK,LOADRT,IBKTS,IDEC,AVE,RISK,IRISK,JRISK)
C
C THIS ROUTINE LOADS THE TABLE PORTION OF ANY RISK PROFILE INTO THE
C OUTPUT BUFFER AND CONTROLS PRINTING THE INFORMATION.
C
C NORSK=RISK PROFILE OR PV/FV/ROI NUMBER
C LOADRT=POSITION WHERE RIGHT SIDE HEADINGS ARE LEFT JUSTIFIED
C IBKTS=NUMBER OF BRACKETS TO SHOW
C IDEC=FLAG FOR NUMBER OF DECIMALS (10=DEFAULT, 11=0 DECIMALS,
C      12=1 DECIMAL,...,19=8 DECIMALS)
C AVE=VALUE TO SHOW AS THE AVERAGE
C RISK=RISK PROFILE MATRIX
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C
	COMMON /C04ZZZ/ INPUT(320),INPOS,INSIZE,INCONT
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C13ZZZ/ RMEAN(9),RDEV(9)
	DIMENSION RISK(IRISK,JRISK)
	CALL SKPZZZ(IPOUT,1)
	IF(LOADRT.LE.1)GO TO 200
	PRBSTP=1.0/FLOAT(IBKTS)
	PRBNOW=0.0
	ISTOP=IBKTS+1
	DO 100 I=1,ISTOP
	CALL PPTZZZ(NORSK,PRBNOW,VALOUT,RISK,IRISK,JRISK)
	CALL TRZZZ(PRBNOW,15,NOCHRS)
	CALL OUTZZZ(1,INPUT,1,NOCHRS,1,7,IPOUT)
	CALL TRZZZ(VALOUT,IDEC,NOCHRS)
	CALL OUTZZZ(1,INPUT,1,NOCHRS,1,LOADRT+11-1,IPOUT)
	IF(NORSK.EQ.3)CALL OUTZZZ(2,2H %,1,2,3,0,IPOUT)
	CALL WZZZ(IPOUT,0)
100	PRBNOW=PRBNOW+PRBSTP
	CALL SKPZZZ(IPOUT,1)
	CALL OUTZZZ(2,7HAVERAGE,1,7,2,1,IPOUT)
200	CALL TRZZZ(AVE,IDEC,NOCHRS)
	CALL OUTZZZ(1,INPUT,1,NOCHRS,1,LOADRT+11-1,IPOUT)
	IF(NORSK.EQ.3)CALL OUTZZZ(2,2H %,1,2,3,0,IPOUT)
	CALL WZZZ(IPOUT,0)
	IF(LOADRT.LE.1)RETURN
	CALL OUTZZZ(2,7HSTD DEV,1,7,2,1,IPOUT)
	CALL TRZZZ(RDEV(NORSK),IDEC,NOCHRS)
	CALL OUTZZZ(1,INPUT,1,NOCHRS,1,LOADRT+11-1,IPOUT)
	IF(NORSK.EQ.3)CALL OUTZZZ(2,2H %,1,2,3,0,IPOUT)
	CALL WZZZ(IPOUT,0)
	RETURN
	END
C
C
C
	SUBROUTINE LLFZZZ(LNO,LOADRT)
C
C THIS ROUTINE WILL LOAD ANY OF THE THREE LEFT HAND SIDE TITLE LINES
C OF A RISK PROFILE INTO THE OUTPUT BUFFER.
C
C LNO=LINE NUMBER TO LOAD
C LOADRT=POSITION WHERE RIGHT SIDE HEADING ARE LEFT JUSTIFIED
C
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	DIMENSION LOUT(9)
	DATA LOUT/
     +  5HPROBA,5HBILIT,5HY    ,
     +  5HOF VA,5HLUE  ,5H     ,
     +  5HOR LE,5HSS   ,5H     /
	IF(LOADRT.LE.1)RETURN
	CALL OUTZZZ(2,LOUT,3*LNO-2,11,2,1,IPOUT)
	RETURN
	END
C
C
C
	SUBROUTINE SRWZZZ(NOPFR,JUST,JFLAG)
C
C THIS ROUTINE WILL LOAD THE ROW NUMBERS OF A PV/FV/ROI REPORT INTO
C THE OUTPUT BUFFER ACCORDING TO THE PASSED IN JUSTIFICATION FLAGS.
C
C NOPFR=FLAG SIGNALING REPORT TYPE (1=PV, 2=FV, 3=ROI)
C JUST=JUSTIFICATION FLAG
C JFLAG=INFORMATION ASSOCIATED WITH JUSTIFICATION FLAG AS FOLLOWS:
C	IF JUST=1        RIGHT JUSTIFY AT JFLAG
C	IF JUST=2        LEFT JUSTIFY AT JFLAG
C	IF JUST=3        CONTINUE WHERE STOPPED (JFLAG=0)
C	IF JUST=4        CENTER (WIDTH=JFLAG)
C
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C04ZZZ/ INPUT(320),INPOS,INSIZE,INCONT
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	CALL OUTZZZ(2,9HFOR ROWS:,1,9,JUST,JFLAG,IPOUT)
	DO 10 I=1,MAXPFR
	IF(IPFRLN(I,NOPFR).EQ.0)GO TO 20
	CALL TRZZZ(FLOAT(IABS(IPFRLN(I,NOPFR))),11,NOCHRS)
10	CALL OUTZZZ(1,INPUT,1,NOCHRS,3,0,IPOUT)
20	CALL WZZZ(IPOUT,0)
	RETURN
	END
C
C
C
	SUBROUTINE SRTZZZ(RATVAL,JUST,JFLAG)
C
C THIS ROUTINE WILL LOAD THE RATE OF A PV/FV REPORT INTO THE
C OUTPUT BUFFER ACCORDING TO THE JUSTIFICATION FLAGS.
C
C RATVAL=RATE VALUE TO BE LOADED
C JUST=JUSTIFICATION FLAG
C JFLAG=INFORMATION ASSOCIATED WITH JUSTIFICATION FLAG AS FOLLOWS:
C	IF JUST=1        RIGHT JUSTIFY AT JFLAG
C	IF JUST=2        LEFT JUSTIFY AT JFLAG
C	IF JUST=3        CONTINUE WHERE STOPPED (JFLAG=0)
C	IF JUST=4        CENTER (WIDTH=JFLAG)
C
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C04ZZZ/ INPUT(320),INPOS,INSIZE,INCONT
	CALL OUTZZZ(2,8HAT RATE ,1,8,JUST,JFLAG,IPOUT)
	CALL TRZZZ(RATVAL,10,NOCHRS)
	CALL OUTZZZ(1,INPUT,1,NOCHRS,3,0,IPOUT)
	CALL OUTZZZ(2,2H %,1,2,3,0,IPOUT)
	CALL WZZZ(IPOUT,0)
	RETURN
	END
C
C
C
	SUBROUTINE GRXZZZ(ICOL,ILOC)
C
C THIS ROUTINE WILL UNPACK THE ROW OPTIONS FROM THE APPROPRIATE
C ELEMENT IN THE ROW2 VECTOR INTO THE IOPTN VECTOR.
C
C ICOL=THE COLUMN WHOSE OPTIONS ARE TO BE UNPACKED
C ILOC=THE ROW WHOSE OPTIONS ARE TO BE UNPACKED
C
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C06ZZZ/ IOPTN(4),NROPTS,NSOPTS,I10POW(4),NCOPTS
	COMMON /C11ZZZ/ IROW2(152)
	IALLOP=IROW2(ILOC)
	DO 100 I=1,NROPTS
	IALLNX=IALLOP/I10POW(I)
	IOPTN(I)=IALLOP-IALLNX*I10POW(I)
100	IALLOP=IALLNX
	IALLOP=IROW2(ICOL+MAXROW)
	DO 200 I=1,NCOPTS
	IALLNX=IALLOP/I10POW(I)
	JOPTN=IALLOP-IALLNX*I10POW(I)
	IF(JOPTN.EQ.0)GO TO 200
	GO TO (110,120,130,140),I
110	IOPTN(1)=1
	GO TO 200
120	IOPTN(2)=JOPTN+IOPTN(2)/10*10
	GO TO 200
130	IOPTN(2)=MOD(IOPTN(2),10)+(JOPTN+1)*10+(IOPTN(2)/50*50)
	GO TO 200
140	IF(IOPTN(2).LT.50)IOPTN(2)=IOPTN(2)+50
200	IALLOP=IALLNX
	RETURN
	END
C
C
C
	SUBROUTINE GRAZZZ(NORSK,IDEC,RISK,IRISK,JRISK,IGTYPE)
C
C THIS ROUTINE WILL CONTROL PRINTING A GRAPH OF THE CUMULATIVE
C PROBABILITY DISTRIBUTION OF ANY PV/FV/ROI OR STANDARD RISK
C PROFILE REPORT.
C
C NORSK=NUMBER OF RISK PROFILE TO BE GRAPHED
C IDEC=FLAG FOR NUMBER OF DECIMAL PLACES ON XAXIS  (VALUES:
C	10=DEFAULT,11=0 DECIMALS,12=1 DECIMAL,...,19=8 DECIMALS)
C RISK=RISK PROFILE MATRIX
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C IGTYPE=GRAPH TYPE (2=CUMULATIVE, 3=MASS)
C
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C01ZZZ/ NASCII(128)
	COMMON /C04ZZZ/ INPUT(320),INPOS,INSIZE,INCONT
	COMMON /C07ZZZ/ LINOUT(136),LINPOS
	DIMENSION IDASH(3),RISK(IRISK,JRISK),XVALUE(45),YAXVL(5)
	DATA IYSKIP/5/,IXSKIP/11/,IAYLOC/7/,IYBKT/4/,IXBKT/4/
	DATA IDASH/2*5H-----,5H+----/,YAXVL/1.00,.80,.40,.20,.08/
	DATA IYAXVL/5/
	CALL SKPZZZ(IPOUT,1)
	CALL PPTZZZ(NORSK,1.0,XMAX,RISK,IRISK,JRISK)
	CALL PPTZZZ(NORSK,0.0,XMIN,RISK,IRISK,JRISK)
	IF(XMAX.NE.XMIN) GO TO 100
	XMAX=XMAX+2.0
	XMIN=XMIN-2.0
100	XINC=(XMAX-XMIN)/FLOAT(IXBKT*IXSKIP)
	IPOINT=IYSKIP*IYBKT+1
	IF(IGTYPE.EQ.3)GO TO 1000
	YINC=1.0/FLOAT(IYBKT*IYSKIP)
	DO 400 I=1,IPOINT
	PROBAT=1.0-FLOAT(I-1)*YINC
	CALL PPTZZZ(NORSK,PROBAT,VALOUT,RISK,IRISK,JRISK)
	IF(MOD(I,IYSKIP).EQ.1) GO TO 200
	CALL OUTZZZ(2,1H',1,1,1,IAYLOC,IPOUT)
	GO TO 300
200	CALL TRZZZ(PROBAT,13,NOCHRS)
	CALL OUTZZZ(1,INPUT,1,NOCHRS,1,IAYLOC-2,IPOUT)
	CALL OUTZZZ(2,1H+,1,1,1,IAYLOC,IPOUT)
300	NOXINC=(VALOUT-XMIN)/XINC+.5
	IF(NOXINC.EQ.0) LINPOS=IAYLOC-1
	CALL OUTZZZ(2,1H*,1,1,1,NOXINC+IAYLOC,IPOUT)
	IF(I.EQ.IPOINT) GO TO 500
400	CALL WZZZ(IPOUT,0)
500	LINPOS=IAYLOC
	DO 600 I=1,IXBKT
600	CALL OUTZZZ(2,IDASH,1,IXSKIP,3,0,IPOUT)
650	CALL WZZZ(IPOUT,0)
	ISTOP=IXBKT+1
	DO 700 I=1,ISTOP
	CALL TRZZZ(XMIN+FLOAT((I-1)*IXSKIP)*XINC,IDEC,NOCHRS)
700	CALL OUTZZZ(1,INPUT,1,NOCHRS,4,2*((I-1)*IXSKIP+IAYLOC)-1,IPOUT)
	CALL WZZZ(IPOUT,0)
	RETURN
1000	JPOINT=IXSKIP*IXBKT+1
	PRBOLD=0.0
	PRBNOW=0.0
	PRBMAX=0.0
	XNOW=XMIN-.5*XINC
	DO 1100 J=1,JPOINT
	XNOW=XNOW+XINC
	CALL VPTZZZ(NORSK,XNOW,PRBNOW,PRBOUT,RISK,IRISK,JRISK)
	XVALUE(J)=PRBOUT-PRBOLD
	PRBOLD=PRBOUT
1100	IF(XVALUE(J).GT.PRBMAX)PRBMAX=XVALUE(J)
	DO 1200 J=1,IYAXVL
1200	IF(PRBMAX.LE.YAXVL(J))YMAX=YAXVL(J)
	YINC=YMAX/FLOAT(IYBKT*IYSKIP)
	DO 1800 I=1,IPOINT
	PROBAT=YMAX-FLOAT(I-1)*YINC
	IF(MOD(I,IYSKIP).EQ.1)GO TO 1300
	CALL OUTZZZ(2,1H',1,1,1,IAYLOC,IPOUT)
	GO TO 1400
1300	IF(I.EQ.IPOINT)PROBAT=0.0
	CALL TRZZZ(PROBAT,13,NOCHRS)
	CALL OUTZZZ(1,INPUT,1,NOCHRS,1,IAYLOC-2,IPOUT)
	CALL OUTZZZ(2,1H+,1,1,1,IAYLOC,IPOUT)
1400	PRBTST=PROBAT-.5*YINC
	DO 1700 J=1,JPOINT
	IF(XVALUE(J).GE.PRBTST)GO TO 1500
	IF(J.EQ.1)GO TO 1700
	ICHR=NASCII(33)
	GO TO 1600
1500	IF(J.EQ.1)LINPOS=IAYLOC-1
	ICHR=NASCII(43)
1600	CALL OUTZZZ(1,ICHR,1,1,1,IAYLOC+J-1,IPOUT)
1700	CONTINUE
	IF(I.EQ.IPOINT)GO TO 1900
1800	CALL WZZZ(IPOUT,0)
1900	DO 2000 J=1,JPOINT
	JNOW=IAYLOC+J-1
	IF(LINOUT(JNOW).EQ.NASCII(33))LINOUT(JNOW)=NASCII(46)
2000	IF(MOD(J,IXSKIP).EQ.1)LINOUT(JNOW)=NASCII(44)
	GO TO 650
	END
C
C
C
	SUBROUTINE FFOZZZ(IRFLG,LOADRT,NOINT)
C
C THIS ROUTINE CHECKS TO SEE IF THERE IS ENOUGH ROOM LEFT ON THE
C CURRENT PAGE OF THE ROUTE FILE FOR THE REPORT WHICH IS ABOUT TO BE
C PRINTED.  IF THERE IS TOO LITTLE ROOM, A FORM FEED IS WRITTEN TO THE
C FILE AND THE PAGE COUNTER (IFFPOS) IS SET BACK TO ZERO.
C
C IRFLG=FLAG TELLING TYPE OF REPORT (0=STD SO ALWAYS ISSUE FORM FEED,
C        1=TABULAR RISK PROFILE, 2=GRAPH RISK PROFILE)
C LOADRT=FLAG INDICATING IF TABULAR RISK PROFILE HAS ANY INTERVALS
C        (1=NO INTERVALS, 18=INTERVALS SPECIFIED BY NOINT)
C NOINT=NUMBER OF RISK PROFILE INTERVALS IF LOADRT=18
C
	COMMON /C01ZZZ/ NASCII(128)
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	IF(IPOUT.NE.IROUTE)RETURN
	IF(IRFLG-1)300,100,200
100	IF(LOADRT.LE.1)GO TO 150
	NONEED=MAX0(NOINT+11,20)
	GO TO 250
150	NONEED=20
	GO TO 250
200	NONEED=28
250	IF(IFFPOS+NONEED.LE.60)RETURN
300	CALL OUTZZZ(1,NASCII,13,1,2,1,IPOUT)
	CALL WZZZ(IPOUT,0)
	IFFPOS=0
	RETURN
	END
C
C
C
	SUBROUTINE VPTZZZ(NORSK,VALIN,PRBNOW,PRBOUT,RISK,IRISK,JRISK)
C
C THIS ROUTINE WILL CALCULATE THE PROBABILITY FOR ANY GIVEN
C VALUE OR LESS FROM A SMOOTHED CUMULATIVE PROBABILITY DISTRIBUTION
C UTILIZING ITRIAL VALUES FOUND IN THE JRISK BLOCK OF RISK. IF
C PRBNOW .EQ. ZERO, THEN THE ROUTINE STARTS THE SEARCH FOR THE
C PROBABILITY AT THE BIGINNING OF THE BLOCK OF SAMPLED VALUES.
C IF PRBNOW .GT. ZERO, THEN THE ROUTINE ASSUMES THAT IT HAS ALREADY
C SEARCHED PART WAY DOWN THE BLOCK OF SAMPLED VALUES, AND
C IT PICKS UP WHERE IT LEFT OFF.
C
C NORSK=NUMBER OF RISK PROFILE IN WHICH PROBABILITY IS TO BE FOUND
C VALIN=VALUE FOR WHICH CUMULATIVE PROBABILITY IS TO BE CALCULATED
C PRBNOW=CURRENT PROBABILITY POINT IN SEARCH
C PRBOUT=CUMULATIVE PROBABILITY RETURNED
C RISK=RISK PROFILE MATRIX
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION RISK(IRISK,JRISK)
	IF(RISK(ITRIAL,JRISK).GT.RISK(1,JRISK))GO TO 100
	PRBOUT=0.0
	IF(VALIN.GE.RISK(1,JRISK))PRBOUT=1.0
	RETURN
100	IF(NORSK.NE.3)GO TO 110
	IF(VALIN.LT.999)GO TO 110
	PRBOUT=1.0
	RETURN
110	IF(PRBNOW.GT.0.0)GO TO 250
	PRBLAS=0.0
	STEP=1.0/FLOAT(ITRIAL)
	I=0
200	I=I+1
	PRBNOW=PRBNOW+STEP
250	VALNOW=RISK(I,JRISK)
	IF(I.EQ.ITRIAL)GO TO 400
	IF(RISK(I+1,JRISK).LE.VALNOW)GO TO 200
	IF(PRBLAS.GT.0.0)GO TO 300
	BOTMID=VALNOW-.5*(RISK(I+1,JRISK)-VALNOW)
300	TOPMID=VALNOW+.5*(RISK(I+1,JRISK)-VALNOW)
	IF(TOPMID.GE.VALIN)GO TO 500
	PRBLAS=PRBNOW
	BOTMID=TOPMID
	GO TO 200
400	TOPMID=VALNOW+(VALNOW-BOTMID)
500	PRBOUT=PRBLAS+(VALIN-BOTMID)/(TOPMID-BOTMID)*(PRBNOW-PRBLAS)
	IF(PRBOUT.GT.1.0)PRBOUT=1.0
	IF(PRBOUT.LT.0.0)PRBOUT=0.0
	RETURN
	END
C
C
C
	SUBROUTINE BINZZZ(RAVE,IRAVE,JRAVE,RISK,IRISK,JRISK)
C
C THIS ROUTINE SAVES THE CURRENT RAVE AND RISK MATRICES IN THE FORM
C OF TWO DUMP MODE BINARY RECORDS.  THE FIRST RECORD CONTAINS
C RAVE(NPER,NOROW) PLUS SUFFICIENT NULLS TO PAD TO AN EVEN 128 WORD
C BLOCK.  THE SECOND RECORD CONTAINS RISK(ITRIAL,MAXPRO) PLUS
C SUFFICIENT NULLS TO PAD TO AN EVEN 128 WORD BLOCK.
C
C RAVE=STANDARD REPORT MATRIX
C IRAVE=CURRENT STANDARD REPORT I INDEX MAXIMUM (NUMBER OF PERIODS)
C JRAVE=CURRENT STANDARD REPORT J INDEX MAXIMUM (NUMBER OF ROWS)
C RISK=RISK PROFILE MATRIX
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C
	DIMENSION NAME(2),NAMEXT(2),RAVE(IRAVE,JRAVE),RISK(IRISK,JRISK)
	CALL ASKZZZ(18HBINARY FILE NAME? ,18,1,1)
	CALL GETZZZ(NAME,NAMEXT,NGROUP,NUSER)
	IF(NAMZZZ(NAME,NAMEXT).LE.0)RETURN
	CALL C76ZZZ(NEW6,NAME(1),NAME(2))
	CALL C76ZZZ(NEXT6,NAMEXT(1),NAMEXT(2))
	CALL OPMZZZ(2,NEW6,NEXT6,NGROUP,NUSER,0,IOK)
	IF(IOK.LT.0)GO TO 10
	CALL MIOZZZ(RAVE,1,IRAVE*JRAVE,2,0,IOK)
	IF(IOK.GT.0)CALL MIOZZZ(RISK,1,IRISK*(JRISK-1),2,0,IOK)
	CALL CLMZZZ(0)
	IF(IOK.GT.0)RETURN
10	CALL SPKZZZ(15HCAN'T OPEN FILE,15,1,0,1)
	RETURN
	END
C
C
C
	SUBROUTINE PAGZZZ(NONEED,ICNBEG,ICNEND,ITMCOL)
C
C THIS ROUTINE CONTROLS THE SEPARATION OF THE STANDARD REPORT INTO
C PAGES. IF THE DATE OPTION IS ON, BOTH THE DATE AND PAGE NUMBER
C ARE SHOWN AT THE TOP OF EACH PAGE. OTHERWISE, NEW PAGES OMIT THE
C DATE. EACH TIME A PAGE IS INITIATED, THE TITLE AND COLUMN HEADINGS
C ARE REPEATED IF THE HEADING OPTION IS ON (OR IF PAGE ON & PAGE =1.)
C IF PAGE ON & PAGE NOT 1 THEN NO COLUMN HEADINGS UNLESS START
C OF NEW LOGICAL PAGE. NEVER A NEW TITLE IF ONLY PAGE ON & PAGE
C IS NOT EQUAL TO ONE. CALLING THIS ROUTINE WITH NONEED=0 WILL
C INITIALIZE THE ROUTE.
C
C NONEED=NUMBER OF CONSECUTIVE LINES NEEDED ON THIS PAGE
C        (0=INITIALIZE, 100=ALWAYS PRINT COLUMN HEADINGS)
C ICNBEG=BEGINNING COLUMN NUMBER
C ICNEND=LAST COLUMN NUMBER
C ITMCOL=COLUMN NUMBERS OF COLUMN ALPHAS TO BE PRINTED
C
	COMMON /C04ZZZ/ INPUT(320),INPOS,INSIZE,INCONT
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C01ZZZ/ NASCII(128)
	DIMENSION NWDATE(2),ITMCOL(1)
	IF(NONEED.EQ.0)GO TO 300
	IF(IFFPOS+NONEED.LE.60)RETURN
	IF(NOPAGE.LE.0)GO TO 20
	IF(IPOUT.EQ.IROUTE)GO TO 10
	CALL SKPZZZ(IPOUT,66-IFFPOS)
	GO TO 20
10	CALL OUTZZZ(1,NASCII,13,1,2,1,IPOUT)
	CALL WZZZ(IPOUT,0)
20	IFFPOS=0
	IF(NONEED.EQ.200)RETURN
	CALL SKPZZZ(IPOUT,1)
	IF(IODATE.GT.0)CALL OUTZZZ(2,NWDATE,1,2*IWDSIZ,2,1,IPOUT)
	NOPAGE=NOPAGE+1
	NCPP=MAXRCH+ISTDWD*ICOLWD
	CALL OUTZZZ(2,4HPAGE,1,4,2,NCPP-7,IPOUT)
	CALL TRZZZ(FLOAT(NOPAGE),11,NOCHRS)
	CALL OUTZZZ(1,INPUT,2,NOCHRS-1,1,NCPP,IPOUT)
	CALL WZZZ(IPOUT,0)
	CALL SKPZZZ(IPOUT,1)
	IF(IOPAGE.GT.1)GO TO 100
	IF(NONEED.NE.100)RETURN
	IF(NOPAGE.NE.1)GO TO 200
100	CALL PTTZZZ
200	CALL PCLZZZ(ICNBEG,ICNEND,ITMCOL)
	RETURN
300	NOPAGE=0
	IFFPOS=0
	CALL DATE(NWDATE)
	IF(IOPAGE.GT.0)RETURN
	IF(IODATE.EQ.0)RETURN
	CALL SKPZZZ(IPOUT,1)
	CALL OUTZZZ(2,NWDATE,1,2*IWDSIZ,2,1,IPOUT)
	CALL WZZZ(IPOUT,0)
	RETURN
	END
    UHó