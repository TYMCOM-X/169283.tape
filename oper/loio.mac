TITLE LOIO   BLISS COMPILER I/O  W. WULF & J. NEWCOMER
;MODIFIED BY M. MANUGIAN                16 MAY 72
SUBTTL  DECLARATIONS
TWOSEG          ;%2.13%
RELOC 400000            ;%2.13%

;       SET CMUSW==1 FOR A CMU VERSION OF BLISS
;               OR ==0 FOR A STANDARD VERSION.  THE
;               CMU VERSION ACCEPTS THE FUNNY CMU PPN'S
;
;       SET TIMSW==1 TO GENERATE TIMING HOOKS
;
IFNDEF TIMSW,<TIMSW==0>
IF1,<
   IFN TIMSW,<
PRINTX TIMING TURNED ON!!!!!
>
>
IFN TIMSW,<
        EXTERNAL TIMER>
IFNDEF CMUSW,<CMUSW==0>
IF1, <
IFN CMUSW,<
PRINTX BLISS LOIO MODULE ASSEMBLED FOR CMU SYSTEM
SUBTTL LOIO---CMU VERSION---DECLARATIONS
PAGE>

IFE CMUSW,<
SUBTTL LOIO---STANDARD VERSION---DECLARATIONS
PAGE>
>
DEFINE SUBTLE(STR) <
IFE CMUSW,<
SUBTTL LOIO---STANDARD VERSION---STR>
IFN CMUSW,<
SUBTTL LOIO---CMU VERSION---STR>
PAGE>
        DEFINE  BLK(A,B)
<               A==     ZZ
                ZZ==    ZZ+B>

        ZZ==    0
        BLK     SEQNUM, 1       ;REGISTER FOR SEQUENCE NUMBER
        BLK     PAGCNT, 1       ;REGISTER FOR PAGE COUNT
        BLK     TTLBUF, 21      ;BUFFER FOR PROGRAM TITLE
        BLK     XDATE, 1        ;REGISTER FOR DATE OF COMPILATION
        BLK     XP, 45          ;EXECUTIVE PUSHDOWN LIST
        BLK     XA, 16          ;EXECUTIVE ACCUMULATOR STORAGE
        BLK     XE, 11          ;EXECUTIVE OPEN ARRAY
        BLK     XB, 22          ;EXECUTIVE BUFFER HEADERS

TMPSIZ==ZZ

;EXTERNALS AND ENTRIES

EXTERNAL        JOBFF,JOBREL,JOBSYM,JOBVER,FFSEEN


;BITS FOR GETCHR STATUS AND IO STATUS CALLS

        DRCTRB==        4               ;1-DEVICE HAS A DIRECTORY
        TTYBIT==        10              ;1-DEVICE IS A TTY
        AVBBIT==        40              ;1-DEVICE IS AVAILABLE FOR INIT
        AMODE==         3               ;BITS FOR ASCII AND ASCII LINE
        BMODE==         10000           ;BIT FOR BINARY MODE
        INITB==         200000          ;1-DEVICE HAS BEEN INITIALIZED
        IODATA==        100000          ;1-IO DATA ERROR
        IODEV==         200000          ;1-IO DEVICE ERROR
        IOBKTL==        40000           ;1-IO BLOCK TOO LARGE
        IOEOF==         20000           ;1-END OF FILE ON IO DEVICE

;ACCUMULATOR ASSIGNMENTS

        A=      2               ;ARGUMENTS
        B=      1               ;SCRATCH
        C=      13              ;SCRATCH
        D=      14              ;DEVICE CHANNEL NUMBER
        E=      0               ;BYTE POINTER AC FOR GETCHR
        F=      12              ;FLAGS
        G=      10              ;SCRATCH
        H=      11              ;SCRATCH
        P=      17              ;PUSHDOWN ACCUMULATOR
        R=      3               ;USED IN NUMOUT ROUTINE
        S=      4               ;SIXBIT SYMBOL
        T=      16              ;DATA AREA POINTER
        V=      15              ;DATA AREA POINTER, TEMPORARY
;ACCUMULATORS USED BY THE COMPILER IN CALLS TO THE EXEC
        J=      14              ;USED IN CHAR ROUTINE CALLS
        K=      2               ;USED IN LSTOUT CALLS
        L=      10              ;USED IN LSTMES CALLS
        N=      11              ;CHAR AND LSTOUT JSP ACCUMULATOR
;COMPILER AND EXECUTIVE FLAGS
;FLAGS SET BY EXEC, USED BY COMPILER (LEFT HALF OF AC F)

        ERROPN==        1               ;1-ERROR MESSAGE FILE OPEN
        CCLFLAG==       2               ;1-"CCL" DECLARATION
        TTFLAG==        4               ;1-/T SWITCH
        GRFLG==         10              ;1-LOAD ALL ROUTINES AS GLOBAL
        EMFLAG==        20              ;1-ENABLE EXPAND MACRO TRACE
        NPTFLG==        40              ;1-NO-OPTIMIZE FLAG
        SFLAG==         100             ;1-PRINT STATS FOR B-L-I-M-P
        IFLAG==         200             ;1-OUTPUT LUNDE DESCRIPTORS
        SRFLAG==        400             ;1-SAVE/RESTORE REGS AT EXCHJ
        BINFLG==        1000            ;1-SUPPRESS BINARY OUTPUT
        LSTFLG==        2000            ;1-SUPPRESS LISTING OUTPUT
        TWOFLG==        4000            ;1-GENERATE GLOBALS IN LOWSEG
        MFLAG==         10000           ;1-OUTPUT MACHINE CODE LISTING
        RFLAG==         20000           ;1-
        HGHFLG==        40000           ;1-HISEG VERSION 
        FFFLAG==        100000          ;V2G-1-FIX UP FREG ON EVERY ROUTINE ENTRY/EXIT
        FINFLG==        200000          ;1-ALL SOURCE FILES READ
        XREF==          400000          ;1-PRINT BLISS XREF

;VARIABLE PARAMETERS
        TTLSIZ==43

;FLAGS SET AND USED ONLY BY THE EXEC

        DEVBIT==        1               ;1-DEVICE SEEN IN COMMAND STRING
        IFIBIT==        2               ;1-OUTPUT DEVICES & FILES INIT-ED AND ENTER-ED
        ARWBIT==        4               ;1-LEFT ARROW SEEN
        EXTBIT==        10              ;1-EXPLICIT EXTERNSION NAME SEEN
        SWTBIT==        20              ;1-ENTER SWITCH MODE
        CHRBIT==        40              ;1-SPECIAL CALL FROM CHAR ROUTINE
        INFOBIT==       100             ;1-VALID INFORMATION SEEN
        ERRBIT==        200             ;1-SUPPRESS ERRORS ON TTY
        FSPCBI==        400             ;V2G- 1-PART OF FILESPEC SEEN(IE NOT PERM PPN)
        NORBIT==        1000            ;1-NORMAL MODE COMMAND
        TTYLST==        2000            ;1-LISTING DEVICE IS A TTY
        TTYONB==        4000            ;1-TTY HAS BEEN INITIALIZED
        ENDBIT==        10000           ;1-END OF ALL INPUT FILES
        SLSHBIT==       20000           ;1-SWITCH MODE ENTERED WITH </>
        HDRBIT==        40000           ;1-HEADER HAS BEEN SET UP
        DIDHED==        100000          ;1-ONE HEADER HAS BEEN PRINTED
        UNBIT==         200000          ;1-COMPLEMENT SWITCH
        CCLBIT==        400000          ;1-COMMAND STRING FROM CCL FILE



F4EXEC: HRLZ    T, JOBREL       ;PUT HIGHEST ADDRESS IN LEFT HALF
        HRR     T, JOBFF        ;BEGINNING OF IMPURE AREA IN RH
        HRLZI   P, XP-XA        ;GET A COUNT FOR PUSHDOWN LIST
        HRRI    P, XP-1(T)      ;GET PUSHDOWN LIST ADDRESS
        MOVEI F, 0              ;INITIALIZE SWITCHES
        TLO     F,TWOFLG+SRFLAG
        SKIPE   CCLCTL          ; CCL SWITCH SET?
        TRO     F,CCLBIT        ; YES...SET CCL CONTROL BIT
        SETZM   FILLINE
        SETZM   FILPAGE
        AOS     FILPAGE
        SETZM   NEWPAGE
        SETZM   NEWFILE
        SETZM   LINCT
        SETZM   SOSPGC
        AOS     SOSPGC
        SETZM   PPNPERM ; CLEAR PPN 
        SETZM   NEDCCM          ;DON'T NEED CCL MESSAGE YET
        MOVE    B,[ASCII/BLISS/]
        MOVEM   B,TTLBUF(T)
        MOVE    B,[ASCII/  V. /]
        MOVEM   B,TTLBUF+1(T)


;THE RESTRT ROUTINE PERFORMS THE FOLLOWING TASKS BEFORE BEGINNING
;A NEW COMPILATION:
;       1. OBTAINS THE DATE AND TIME FROM THE MONITOR
;       2. SETS THE LINE COUNT AND PAGE COUNT FOR THE LISTING
;          FILE TO ZERO
;       3. FILLS THE TITLE BUFFER WITH ASCII BLANKS
;       4. INITIALIZES THE FLAG AC BY CLEARING ALL BITS, THEN
;          SETTING BITS FOR SUPPRESSION OF BIN AND LST OUTPUT
;       5. CLEARS THE DEVICE CHANNEL NUMBER ACCUMULATOR
;       6. INITIALIZES THE SWITCH BUFFER COUNTER TO ZERO
;THE SECTION OF CODING JUST PRIOR TO GETLIN UPDATES JOBFF SO
;THAT IT WILL POINT TO THE BEGINNING OF THE IO BUFFERS - I.E.
;ROOM IS MADE FOR THE JOB DATA AREA, THE EXEC FIXED TABLES,
;AND THE COMPILER VARIABLE DATA AREA.
; REFERENCES ARE MADE TO JOBFF INDEXED BY AC 16.
;JOBFF ITSELF IS LEFT UNALTERED EXCEPT DURING TEMPORARY CALLS
;TO THE MONITOR FOR INBUFS AND OUTBUFS. THE ACTUAL UPDATED
;STATE OF JOBFF IS LEFT IN REGISTER XA+F IN THE IMPURE AREA .
        HRRZ A, JOBFF
        ADDI A,TMPSIZ
        HRRM A, TTOBUF
        ADDI A, 30
        HRRM A,XA+F(T)
        HRLI    V, T            ;SET UP AC V FOR INDEXING
        SETZM   PAGCNT(T)       ;SET THE PAGE NUMBER TO ZERO
        CALLI   A, DATE         ;GET THE DATE FROM THE MONITOR
        MOVEM   A, XDATE(T)     ;SAVE IT
        SETZM   BINFIL          ; INDICATE WE ARE STARTING
        SETZM   DOPAGE          ; NO PAGE REQUEST
        MOVE    A,PACCUM        ; SAVE PACCUM POINTER
        MOVEM   A,SAVP          ; IN CASE DEVICE SPECIFIED
        TRNE    F,CCLBIT                ; WE GOT CCL?
        JRST    CCLIN1          ; YES...SKIP TTY STUFF
        MOVEI A, "*"
        JSP N, TTYOUT
        RELEAS 5,               ;RELEASE THE TTY
        TRZ F, TTYONB
        MOVEI   D, 4            ;INITIALIZE TTY FOR INPUT
        HRLZI   S, (SIXBIT /TTY/)       ;SIXBIT FOR TTY
        PUSHJ   P, DEVINI       ;CALL INITIALIZING ROUTINE
        INPUT   4,              ;GET A LINE OF COMMAND STRING
        RELEAS  4,              ;RELEAS TTY FOR FUTURE USE
CCLIN1: MOVEI D,0
        HRLI    B,DEV   ;CLEAR BUFFER FOR DEVICES,FILENAMES,EXTS
        HRRI    B,DEV+1
        SETZM   -1(B)
        BLT     B,EXT+2
        MOVSI   B,(SIXBIT /REL/)        ;V2G-SET UP DEFAULT BINARY AND
        MOVEM   B,EXT                   ;V2G-
        MOVE    B,['LST',,-1]           ;LISTING EXTENSION,,DEFAULT FLAG
        MOVEM   B,EXT+1                 ;V2G-
        MOVSI   B,(SIXBIT /BLI/)        ;V2G-SOURCE EXTENSION
        MOVEM   B,EXT+2                 ;V2G-
SUBTLE <COMMAND DECODE>

;EXEC COMMAND STRING DISPATCHING
;THIS ROUTINE PICKS UP CHARACTERS FROM THE EXEC TTY BUFFER AND
;DISPATCHES TO THE PROPER ROUTINE DEPENDING ON THE TYPE OF
;CHARACTER. A TABLE OF BYTES AND BYTE POINTERS ALLOWS EACH CHARAC-
;TER IN THE ASCII SET TO BE TREATED INDIVIDUALLY.
;THE ROUTINE MAY BE ENTERED AT GETCHR IF IT IS DESIRED TO 
;ACCUMULATE A 6-LETTER SIXBIT SYMBOL IN AC S. SYMBOLS OF
;DIFFERENT LENGTHS MAY BE ACQUIRED BY SETTING THE CONTENTS OF
;AC B TO THE DESIRED LENGTH AND ENTERING AT GETCH1.

GETCH2: SETZM   PPNVAL(D)       ;V2G- CLEAR LAST TTEMP PPN
        TRZ     F,  DEVBIT+FSPCBI+EXTBIT        ;V2G-
GETCHR: MOVEI   B, 6            ;INITIALIZE SYMBOL LENGTH COUNTER
GETCH1: MOVEI   S, 0            ;INITIALIZE SYMBOL ACCUMULATOR
        MOVE    E, SYMPTR       ;INITIALIZE SYMBOL BYTE AC

GETCMN: PUSHJ   P,CMDCHR        ;GET A CHARACTER FROM COMMAND STRING
        CAIN    A,"!"           ; RUN REQUEST?
        JRST    CCLLINK         ; YES---GO LINK
        CAIN    A,"["           ; PPN?
        JRST    PPN             ; GO PROCESS IT
        CAIN    A,"-"           ; "-"?
        JRST    UNSWITCH        ; MAYBE /-(SOMETHING) SWITCH?
        MOVE    G, A            ;ANOTHER COPY OF IT IN AC G
        IDIVI   G, 11           ;TRANSLATE TO 4-BIT CODE
        LDB     G, TABLE(H)     ;USE PROPER BYTE POINTER
        CAIGE   G, 4            ;MODIFY CODE IF .GE. 4
        TRNN    F, SWTBIT       ;MODIFY IF SWITCH IS ON
        ADDI    G, 4            ;CHANGE DISPATCH BY ADDING 4
        HRRZ    H, DSPTCH(G)    ;LOAD RIGHT HALF DISPATCH
        CAIL    G, 10           ;SKIP IF CORRECT
        HLRZ    H, DSPTCH-10(G) ;GET LEFT HALF DISPATCH
        JRST    @H              ;EXIT

CMDCHR: TRNE    F,CCLBIT
        JRST    CMDCCL
        ILDB    A,XB+15(T)      ;NOT CCL, GET CHAR FROM TTY BUFFER
        POPJ    P,
CMDCCL: SOSGE   CCLBFH+2        ;CCL MODE, INPUT FROM FILE OR TMPCOR
        JRST    CCLRD
        ILDB    A,CCLBFH+1      ;GET CHARACTER FROM BUFFER
        JUMPE   A,CMDCCL        ;IGNORE NULLS
        POPJ    P,
CCLRD:  SKIPN   CCLBFH          ;BUFFER EMPTY, WAS IT DSK OR TMPCOR?
        JRST    XIT             ;TMPCOR, ALL DONE
        IN      7,              ;DSK, INPUT A NEW BLOCK
        JRST    CMDCCL
        STATZ   7,740000        ;COULDN'T INPUT, CHECK FOR ERROR
        OUTSTR  [ASCIZ/CCL INPUT ERROR/]
XIT:    EXIT    1,              ;EOF OR ERROR, EXIT
        JRST    .-1

UNSWITCH: TRNN F,SWTBIT         ; "-" IN SWITCH MODE?
        JRST    ERR2            ; NO---ILLEGAL
        TRO     F,UNBIT         ; INDICATE COMPLEMENT
        JRST    GETCMN          ; AND GO ON

;COMMAND DISPATCH TABLE AND BYTE POINTERS
DSPTCH: XWD     GETCMN,ERR1     ;IGNORED CHAR, BAD CHAR(SWITCH)
        XWD     SWTCH,SWTCHA    ;<(>, LETTER(SWITCH MODE)
        XWD     COLON,ERR1      ;<:>, NUMBER(SWITCH MODE)
        XWD     PERIOD,SWTCHE   ;<.>,<)>ESCAPE SWITCH MODE
        XWD     LFTARW,ERR2     ;<_> OR <=>, BAD CHAR (NORMAL MODE)
        XWD     COMMA,STORE     ;<,>,ALPHABETICH CHAR (NORMAL)
        XWD     CARRTN,STORE    ;<CR>,NUMERIC CHAR (NORMAL)
        XWD     SLASH,ERR2      ;</>, <)> ILLEGAL ESCAPE

TABLE:  POINT   4, BITE(G), 3
        POINT   4, BITE(G), 7
        POINT   4, BITE(G), 11
        POINT   4, BITE(G), 15
        POINT   4, BITE(G), 19
        POINT   4, BITE(G), 23
        POINT   4, BITE(G), 27
        POINT   4, BITE(G), 31
        POINT   4, BITE(G), 35


;BYTE TABLE FOR DISPATCHING
;CLASSIFICATION BYTE CODES

;       BYTE    DISP    CLASSIFICATION

;       00      00      ILLEGAL CHARACTER, SWITCH MODE
;       01      01      ALPHABETIC CHARACTER, SWITCH MODE
;       02      02      NUMERIC CHARACTER, SWTICH MODE
;       03      03      SWITCH MODE ESCAPE, SWITCH MODE

;       00      04      ILLEGAL CHARACTER, NORMAL MODE
;       01      05      ALPHABETIC CHARACTER, NORMAL MODE
;       02      06      NUMERIC CHARACTER, NORMAL MODE
;       03      07      SWITCH MODE ESCAPE, NORMAL MODE

;       04      10      IGNORED CHARACTER
;       05      11      ENTER SWITCH MODE WITH A <(>
;       06      12      DEVICE DELIMITER, <:>
;       07      13      FILE EXTENSION DELIMITER, <.>
;       10      14      OUTPUT SPEC. DELIMITER, <_> OR <=>
;       11      15      FILE DELIMITER, <,>
;       12      16      COMMAND TERMINATOR, <CR>
;       13      17      ENTER SWITCH MODE WITH </>

;BYTE TABLE:

BITE:   BYTE    (4) 4,0,0,0,0,0,0,0,0
        BYTE    (4) 4,4,4,4,12,0,0,0,0
        BYTE    (4) 0,0,0,0,0,0,0,0,0
        BYTE    (4) 0,0,0,0,0,11,0,4,0
        BYTE    (4) 0,0,0,0,5,3,0,0,11
        BYTE    (4) 0,7,13,2,2,2,2,2,2
        BYTE    (4) 2,2,2,2,6,0,0,10,0
        BYTE    (4) 0,0,1,1,1,1,1,1,1
        BYTE    (4) 1,1,1,1,1,1,1,1,1
        BYTE    (4) 1,1,1,1,1,1,1,1,1
        BYTE    (4) 1,0,0,0,0,10,0,0,0
        BYTE    (4) 0,0,0,0,0,0,0,0,0
        BYTE    (4) 0,0,0,0,0,0,0,0,0
        BYTE    (4) 0,0,0,0,0,0,0,0,12
        BYTE    (4) 0,4


;THE FOLLOWING TWO ROUTINES HANDLE ALPHANUMERIC CHARACTERS
;FOUND IN THE COMMAND STRING. IN NORMAL MODE, THE CHARACTER
;IS DEPOSITED TO FORM A SIXBIT SYMBOL. IN SWITCH MODE, THE
;PROPER INSTRUCTION IS EXECUTED WITH THE AID OF A DISPATCH
;TABLE. THEN, IF SWITCH MODE WAS ENTERED WITH A SLASH, THE
;EXEC EXITS FROM SWITCH MODE. ALSO, IF THE SWITCH BEING PRO-
;CESSED IS IN A STANDARD DEFINITION, THE CHARACTER IS STORED.

        EXTERNAL PACCUM         ; ..BLISS PTR TO CHR ACCUMULATOR
STORE:  TRO     F, INFOBIT+FSPCBI       ;V2G- TURN ON BIT FOR CR ROUTINE + PERM PPN TEST
STOREA: SKIPN   BINFIL          ; IF BIN FILE ID WE SAVE
        SOJL    B, GETCMN       ;JUMP IF NO ROOM FOR CHAR
        SUBI    A, 40           ;CONVERT FROM ASCII TO SIXBIT
        IDPB    A, E            ;STORE CHARACTER IN E
        JRST    GETCMN          ;RETURN FOR NEXT CHARACTER


SUBTLE <SWITCH PROCESSOR>
SWTCHA: TRO     F, INFOBIT      ;TURN ON FLAG FOR C.R.
        TRZE    F,UNBIT         ; ARE WE COMPLEMENTING A SWITCH?
        ADDI    A,^D26          ; YES--USE SECOND DISPATCH TABLE
        XCT     SLIST-101(A)    ;EXECUTE PROPER SWITCH INSTRUCTION
        TRZE    F, SLSHBIT      ;CALLED BY A SLASH?
        TRZ     F, SWTBIT       ;YES, EXIT FROM SWITCH MODE
        JRST    GETCMN          ;RETURN FOR MORE CHARACTERS


;THE FOLLOWING THREE ROUTINES HANDLE THE CONTROL CHARACTERS
;IN THE COMMAND STRING WHICH CAUSE THE EXEC TO ENTER INTO AND
;EXIT FROM SWITCH MODE. THERE ARE TWO TYPES OF SWITCH MODE,
;DEPENDING ON WHETHER IT IS ENTERED WITH A </> OR A <(>.

SLASH:  TRO     F, SLSHBIT      ;ENTER SWITCH MODE WITH A </>

SWTCH:  TRO     F, SWTBIT       ;ENTER SWITCH MODE WITH A <(>
        JRST    GETCMN          ;RETURN FOR MORE CHARACTERS


SWTCHE: TRZ     F, SWTBIT       ;EXIT FROM SWITCH MODE WITH A <)>
        JRST    GETCMN          ;RETURN FOR MORE CHARACTERS


;DISPATCH TABLE FOR SWITCHES

SLIST:  JRST    ERR1            ;A - ERROR
        SETOM   BLIMP           ;B - TYPE BLIMP MESSAGE
        TLO     F,XREF          ;C - BLISS XREF PLEASE
        JRST    ERR1            ;D - ERROR
        TLO     F,EMFLAG        ;E - ENABLE EXPAND MACRO FLAG
        TLO     F,FFFLAG        ;V2G-F - FIX UP FREG ON EVERY ROUTINE ENTRY/EXIT
        TLO     F,GRFLG         ;G - LOAD ALL ROUTINES AS GLOBAL
        TLC     F,TWOFLG+HGHFLG ;H - HIGH SEGMENT PROGRAM FILE
        TLO     F,IFLAG         ;I - LUNDE DESCRIPTORS
        JRST    ERR1            ;J - ERROR
        TLO     F,LSTFLG        ;K - KILL THE LISTING
        TLZ     F,LSTFLG        ;L - ENABLE LISTING
        TLO     F,MFLAG         ;M - MACHINE LANG. LIST ENABLE
        TRO     F,ERRBIT        ;N - SUPPRESS ERRORS TO TTY
        TLZ     F,NPTFLG        ;O - OPTIMIZE SWITCH
        JRST    ERR1            ;P - ERROR
        SETOM   QUIET           ;Q - KEEP TTY QUIET
        TLZ     F,SRFLAG        ;R - NO SAV/RESTORE AT EXCHJ
        TLO     F,SFLAG         ;S - B-L-I-M-P STATS
        PUSHJ   P,TENABLE       ;T - ENABLE TRACE/TIMING 
        TLO     F,NPTFLG        ;U - NO OPTIMIZATION
        TLC     F,TWOFLG        ;V - LOW SEGMENT PROGRAM FILE
        JRST    ERR1            ;W - ERROR
        SETZM   CODETOG         ;X - SYNTAX CHECK ONLY SWITCH
        JRST    ERR1            ;Y - ERROR
        JRST    ERR1            ;Z - ERROR
;       COMPLEMENT DISPATCH TABLE
        JRST    ERR1            ; -A - ERROR
        JRST    ERR1            ; -B - ERROR
        TLZ     F,XREF          ; -C - TURN OFF XREF
        JRST    ERR1            ; -D - ERROR
        TLZ     F,EMFLAG        ; -E - DON'T EXPAND MACROS
        TLZ     F,FFFLAG        ;V2G- -F - FIX UP FREG ON EVERY ROUTINE ENTRY/EXIT
        TLZ     F,GRFLG         ; -G - DO NOT LOAD LOCAL ROUTINES AS GLOBAL
        JRST    ERR1            ; -H - CANNOT DISABLE /H
        TLZ   FLAG         ; -I - NOINSPECT
        JRST    ERR1            ; -J - ERROR
        JRST    ERR1            ; -K - ERROR
        TLO     F,LSTFLG        ; -L - TURN OFF LISTING
        TLZ     F,MFLAG         ; -M - TURN OFF MACHINE CODE
        TRZ     F,ERRBIT        ; -N - RE-ENABLE ERRORS TO TTY
        TLO     F,NPTFLG        ; -O - NO OPTIMIZATION
        JRST    ERR1            ; -P - ERROR
        SETZM   QUIET           ; -Q - STOP KEEPING TTY QUIET
        TLO     F,SRFLAG        ; -R - YES SAV/RESTOR AT EXCHJ
        TLZ     F,SFLAG         ; -S - NO B-L-I-M-P STATS
        PUSHJ   P,TDISABLE      ; -T - NO TRACE/TIMING
        JRST    ERR1            ; -U - ERROR
        JRST    ERR1            ; -V - CANNOT DISABLE /V
        JRST    ERR1            ; -W - ERROR
        JRST    ERR1            ; -X - CANNOT RE-ENABLE CODE GEN.
        JRST    ERR1            ; -Y - ERROR
        JRST    ERR1            ; -Z - ERROR


        EXTERNAL MHTIME         ; MASTER TMING SWITCH
TENABLE: TLO    F,TTFLAG        ; TURN ON TTFLAG
        MOVEI   A,1             ; AND MHTIME
        MOVEM   A,MHTIME        ; ...
        POPJ    P,

TDISABLE: TLZ   F,TTFLAG        ; TURN OFF TTFLAG
        SETZM   MHTIME          ; AND MHTIME
        POPJ    P,

EXTERNAL CODETOG                ; - BLISS CODE SUPPRESSION TOGGLE



        

SUBTLE <RUN (!) REQUEST>
;       THIS ROUTINE PROCESSES THE "!" RUN REQUEST
;
CCLLIN: JUMPN D,ERR4    ;MUST BE FIRST NAME
        SETZM EXT(D)    ;NO DEFAULT EXTENSION
        TRNE F,EXTBIT
        JRST .+3
        MOVEM S,NAME(D)
        JRST .+2
        MOVEM S,EXT(D)
        SKIPN 1,DEV(D)  ;GET DEVICE TO RUN FROM
        MOVSI 1,'SYS'   ;DEFAULT IS SYS
        MOVE 2,NAME(D)
        MOVE 3,EXT(D)
        MOVE 5,PPNVAL(D)
        SETZB 4,6
        MOVE [1,,1]     ;RUN IT WITH OFFSET OF 1
        RUN
        HALT    .               ; WE BLEW IT
SUBTLE <PPN PROCESSING>

PPN:    PUSH    P,E             ; SAVE E
        PUSH    P,S             ; SAVE S
        PUSH    P,S+1           ; SAVE S+1
        IFE     CMUSW,<
        SETZ    E,>
;       COLLECT PPN AND ISSUE UUO TO CONVERT IT
        SETZM   PPNBUFF         ; CLEAR PPN BUFFER
        SETZM   PPNBUFF+1       ;
        SETZM   PPNBUFF+2       ;
        MOVE    S,[POINT 7,PPNBUFF]     ; BYTE POINTER
PPNLOOP: PUSHJ  P,CMDCHR        ; GET A CHAR
        CAIN    A,"]"           ; END OF PPN?
        JRST    PPNGOT          ; YES---WE GOT ONE
        IFE     CMUSW,<
        CAIN    A,","           ; IS IT A ","?
        JRST    PPNCOM          ; YES---PROCESS IT>
        IDPB    A,S             ; PLUNK IT DOWN
        IFN     CMUSW,<
        CAIGE   A,"0"
        JRST    PPNERR
        CAIG    A,"9"
        JRST    PPNLOOP
        CAIGE   A,"A"
        JRST    PPNERR
        CAIG    A,"Z"
        JRST    PPNLOOP
        JRST    PPNERR>
        IFE     CMUSW,<
        CAIGE   A,"0"
        JRST    PPNERR
        CAIG    A,"7"
        SKIPA
        JRST    PPNERR
        IMULI   E,10
        SUBI    A,"0"
        ADD     E,A
        HRRM    E,PPNVAL(D)     ;V2G-
        JRST    PPNLOOP
PPNCOM: MOVSS   PPNVAL(D)       ;V2G-
        IDPB    A,S
        SETZ    E,
        JRST    PPNLOOP>
        IFN     CMUSW,<
PPNGOT: HRRI    S,PPNBUFF       ; WHERE TO READ DEC PPN
        HRLI    S,PPNVAL(D)     ;V2G- WHERE TO PUT OCTAL PPN
        CALL    S,[SIXBIT /CMUDEC/]     ; CONVERT
        JRST    PPNERR          ; THAT'S A NO-NO>
        IFE     CMUSW,<
PPNGOT: >
        MOVE    S,PPNVAL(D)     ;V2G- GET THE CURRENT PPN
        TRNN    F,FSPCBI        ;V2G- DID WE HAVE ANYTHING BEFORE THIS?
        MOVEM   S,PPNPERM       ;V2G- NO, MAKE PPN PERM
PPNTMP:
        POP     P,S+1           ; RESTOR S+1
        POP     P,S             ; RESTORE S
        POP     P,E             ; RESTORE E
        JRST    GETCMN
SUBTLE  <PROCESSORS FOR CR/LF,"_","=",".",":">
;SUBROUTINE TO HANDLE THE OUTPUT SPECIFICATION DELIMITER <_>
;OR <=>. THE ROUTINE DOES A LOOKUP ON THE FILE NAME PRECEDING
;IT, IF NECESSARY, SINCE THE <_> ACTS ALSO AS A FILE NAME
;DELIMITER. THE CHANNEL NUMBER IS RESET FOR INPUT.

LFTARW: CAILE   D,1     ;%2.18%MUST DELIMIT FILESPEC #1
        JRST    ERR4    ;OR ELSE COMMAND ERROR

        TRNE    F,EXTBIT        ;IF WE SAW A DOT...
        JRST    LFT1            ;SAVE ID AS EXT
        MOVEM   S,NAME(D)       ;ELSE SAVE ID AS NAME
        SKIPA                   ;V2G- DON'T SAVE ID AS EXT
LFT1:   MOVEM   S,EXT(D)        ;SAVE PROPER EXT
        MOVE    B,PPNPERM       ;V2G- GET PERM PPN
        SKIPN   PPNVAL(D)       ;V2G- IF TEMP SET, LEAVE IT
        MOVEM   B,PPNVAL(D)     ;V2G- OTHERWISE USE PERM
        TRO     F,ARWBIT+NORBIT ; SET FLAGS
        MOVEI   D,2     ;%2.18% FOR SOURCE DEVICE
        JRST    GETCH2  ;%2.18% GO LOOK FOR IT


;SUBROUTINE TO HANDLE THE FILE NAME EXTENSION DELIMITER <.>
;THE EXTENSION BIT IS TURNED ON WHENEVER A PERIOD IS SEEN. IN
;NORMAL MODE (I.E. WHEN NORBIT IS ON), THE FILE NAME IS STORED
;IN THE LOOKUP BLOCK, THE CHARACTER COUNTER IS RESET TO 3, AND
;GETCHR IS ENTERED PROPERLY TO COLLECT A FILE NAME EXTENSION.
;IN THE CASE OF A NON-NORMAL COMMAND, THE FILE NAME IS SAVED
;IN THE FILE REGISTER OF THE STANDARDS BUFFER

PERIOD: TRO     F, EXTBIT       ;TURN ON THE EXTENSION BIT
        MOVEM   S, NAME(D)      ;SAVE FILE NAME
        MOVEI   B, 3            ;SET COUNT TO 3
        JRST    GETCH1          ;GO COLLECT EXTENSION



CCLMES: MOVEI   L,[ASCIZ /BLISS: /]     ; MESSAGE 
        PUSHJ   P,TTYMES        ; GO DO IT
        SETOM   NEDCCM          ;NEED TO FINISH MESSAGE LATER
        POPJ    P,
  
;THE COLON ROUTINE IS CALLED WHENEVER THE EXEC FINDS A <:>
;IN THE COMMAND STRING. ROUTINE DEVINI IS CALLED BY VARIOUS
;PARTS OF THE EXEC TO CALCULATE BUFFER HEADERS AND PERFORM
;THE ACTUAL INITIALIZATION. A DEVICE MAY BE INITIALIZED AS
;FOLLOWS:
;       PUSHJ   P, DEVINI
;       SIXBIT SYMBOL NAME IN AC S
;       DESIRED CHANNEL NUMBER IN AC D

COLON:  TRO     F, DEVBIT+NORBIT
        MOVEM   S,DEV(D)        ;SAVE DEVICE NAME
        JRST    GETCHR

DEVINI: MOVEI   S+1, 3          ;CALCULATE BUF HEADER POS.
        IMUL    S+1,D
        CAIN    D,5             ;GET OUTPUT TTY BUFFERS AS IF IT WERE ON CHN 0
        SETZM   S+1
        ADDI    S+1, XB(T)      ;THIS IS THE TYPE 1 PART
        MOVE    G, S            ;GET DEVICE CHARACTERISTICS
        MOVEM   S, XE+1(T)      ;SAVE DEVICE FOR ERROR ROUTINES
        CALLI   G, DEVCHR       ;GET DEVICE CHARACTERISTICS
        JUMPE   G, ERR11        ;NO SUCH DEVICE?
        CAIN    D,5             ;ASSUME DEVICE TTY FOR OUTPUT IS OK
        JRST    DEVINC
        CAIN    D, 1            ;CHECK TO SEE IF DEVICE CAN
        TLNN    G, 1            ;DO INPUT OR OUTPUT PROPERLY
        TLNE    G, -1(D)        ;...
        SKIPA                   ;GOOD
        JRST    ERR15           ;NOT-SO-GOOD
DEVINC: XCT     DEVLST-1(D)     ;SET FLAG OR JUMP
;       THE XCT GOES TO A JUMP TABLE
DEVINB: MOVSS   S+1             ;SWAP HEADER FOR OUTPUT
DEVIN1: HRLZI   S-1,041000      ;GET INIT OPCODE, ASCII MODE
        DPB     D, DEVPTR       ;SET UP THE CHANNEL NUMBER
        CAIN    D, 1            ;IS THIS THE BINARY DEVICE?
        HRRI S-1, 10            ;YES, CHANGE MODE TO IMAGE
        TLNN    G, TTYBIT       ;IS THE DEVICE A TTY?
        JRST    .+4             ;NO, SKIP AROUND
        CAIE    D,4             ;CHK INPUT TTY
        CAIN    D, 3            ;YES, IS IT AN OUTPUT DEVICE?
        HRRI    S-1, 1          ;NO, INPUT DEVICE -ASCII MODE
        MOVE    S+2, DEV1       ;ERROR RETURN
        MOVE    S+3, DEV2       ;NORMAL RETURN
        JRST    S-1             ;EXECUTE THE INIT
DEVIN2: HRRI    A, 1            ;SINGLE BUFFERED IO
        CAIE    D,4
        TLNE    G, TTYBIT       ;IS DEVICE A TELTYPE?
        HRRI    A, 1            ;YES, ONLY SINGLE BUFFER
        MOVE    B, XA+F(T)      ;GET UPDATED JOBFF FROM BUFFER
        CAIN    D, 5
        MOVE B, TTOBUF
        EXCH    B, JOBFF        ;LET THE MONITOR PLAY WITH IT
        HRLI    A, 065000       ;OPCODE FOR OUTBUF
        CAIE    D, 4            ; CHK INPUT TTY
        CAIN    D, 3            ;IS IT AN INPUT DEVICE?
        HRLI    A, 064000       ;YES, CHANGE CODE TO INBUF
        DPB     D, CHPTR        ;SET UP THE CHANNEL NUMBER
        XCT     A               ;EXECUTE THE OUTBUF OR INBUF
        EXCH    B, JOBFF        ;PUT OLD JOBFF BACK AGAIN
        CAIE    D, 5
        MOVEM   B, XA+F(T)      ;SAVE NEW UPDATED JOBFF
        CAIN    D,5             ;DUMMY OUTPUT FOR EXEC TTY
        OUTPUT  5,              ;...
        POPJ    P,              ;EXIT


;SECTION DEVIN4 INITIALIZES THE SOURCE DEVICE. IT TESTS THE
;SYNTAX OF THE COMMAND STRING FOR POSSIBLE ERRORS, AND PUTS
;THE BUFFER FOR THIS DEVICE ON TOP OF ANY PREVIOUS SOURCE
;DEVICES THAT MAY HAVE BEEN EXECUTED. A POINTER IS SAVED TO
;LOCATE THE POSITION OF THIS BUFFER. IF THE SOURCE DEVICE
;DOES NOT HAVE A DIRECTORY, THE SCAN OF THE COMMAND STRING IS
;STOPPED, AND CONTROL TRANSFERS TO THE CHAR ROUTINE.
;A CHECK IS ALSO MADE TO SEE IF THE DEVICE CAN BE PROPERLY
;INITIALIZED IN ASCII OR ASCII LINE MODE.
DEVIN4: TRNN    F, ARWBIT
        JRST    ERR4            ;NO, ERROR CONDITION
        HRRZ    A, XA+F(T)      ;RESET JOBFF FOR SOURCE DEVICE
        MOVEM   A, XB-1(T)      ;SAVE IT
        TRNN    G, AMODE        ;CAN DEVICE HANDLE ASCII MODE?
        JRST    ERR17           ;NO, ERROR
        MOVE    A,[POINT 7,FILNAME##]
        MOVE    H,NAME+2
        SETZM   FILNAME
        SETZM   FILNAME+1
SETFNM: MOVEI   G,0
        LSHC    G,6
        JUMPE   G,.+2
        ADDI    G,40
        IDPB    G,A             ;SET UP ASCII FILE NAME FOR ERROR MESSAGES
        JUMPN   H,SETFNM
        JRST    DEVIN1

;SECTION DEVIN5 INITIALIZES THE LISTING DEVICE. IT CHECKS TO
;SEE IF THE DEVICE IS A TELTYPE, AND, IF SO, SETS A FLAG FOR
;THE LSTOUT, LSTMES, ERROUT AND ERRMES ROUTINES
;THE ROUTINE ALSO CHECKS TO SEE IF THE DEVICE CAN BE PROPERLY
;INITIALIZED IN ASCII OR ASCII LINE MODE.
DEVIN5: TLNE    G, TTYBIT       ;YES, SET FLAG FOR IO ROUTINES
        TRO     F, TTYLST       ;YES, SET FLAG FOR IO ROUTINES
        TRNN    G, AMODE        ;CAN DEVICE HANDLE ASCII MODE?
        JRST    ERR17           ;NO, ERROR
        TLNN    F,XREF          ;ARE WE DOING AN XREF?
        JRST    DEVINB          ;NOPE
        HRRE    A,EXT+1         ;YUP, WAS EXPLICIT LISTING EXTENSION GIVEN?
        AOJN    A,DEVINB        ;JUMP IF SO (WHEN DEFAULT IS SET, RIGHT
        MOVSI   A,'CRF'         ;HALF IS SET TO -1 AS A FLAG)
        MOVEM   A,EXT+1         ;NO, CHANGE DEFAULT FROM LST TO CRF
        JRST    DEVINB





DEVIN6: TRNN    G, BMODE        ;CAN DEVICE HANDLE BINARY MODE?
        JRST    ERR17           ;NO, ERROR
        JRST DEVINB
DEVLST: JRST    DEVIN6          ;BINARY DISPATCH
        JRST    DEVIN5          ;DISPATCHES
        JRST    DEVIN4          ;...
        JRST    DEVIN1          ;...
        TRO     F,TTYONB        ;TTY OUTPUT INITIALIZED


SUBTLE  <PROCESSOR FOR ",">
CARRTN: TRNN    F, INFOBIT      ;ANY VALID INFORMATION SEEN?
        JRST F4EXEC
        TRO     F, ENDBIT       ;END OF ALL INPUT FILES
        TRNN    F,ARWBIT+NORBIT ;MUST HAVE SEEN  ARROW & BE IN STD MODE
        JRST    ERR4            ;OR ELSE COMMAND ERROR

        JRST    COMRET          ;HANDLE NEXT PART AS IF COMMA
COMMA:  JUMPE   D,COMOK         ;COMMA MUST DELMIT FILESPC 0 OR...
        SETOM   NEWFILE##       ;MULTIPLE SOURCE FILES, ERROR PRINTER SHOULD PRINT NAME
COMRET: CAIE    D,2             ;COMMA OR CAR RET DELIMITS FILESPEC #2 AND UP
        JRST    ERR4            ;OR ELSE COMMAND ERROR

COMOK:  TRNE    F,EXTBIT        ;WAS AN EXTENSION GIVEN?
        JRST    COMOK1          ;YES, SAVE ID AS EXTENSION
        MOVEM   S,NAME(D)       ;SAVE ID AS NAME
        SKIPA                   ;V2G- DON'T SAVE ID AS EXT

COMOK1: MOVEM   S,EXT(D)        ;SAVE PROPER EXTENSION

        MOVE    B,PPNPERM       ;V2G- GET PERM PPN
        SKIPN   PPNVAL(D)       ;V2G- IF TEMP SET, LEAVE IT
        MOVEM   B,PPNVAL(D)     ;V2G- OTHERWISE USE PERM
        CAIN    D,2             ;IF A SOURCE FILE SPEC
        JRST    COMMA1          ;WE REALLY WANT TO INIT DEV & FILENAME

        AOS     BINFIL          ;WE ARE NOW PAST THE BINARY DEVICE
        TRNE    F,ENDBIT                ;HAVE WE SEEN CR?
        JRST    CAR2            ;YES, TERMINATE COMMAND SCAN

        AOJA    D,GETCH2        ;UP FILESPEC # AND GET NEXT ITEM

;WE HAVE A SOURCE FILE SPEC DELIMITED BY COMMA OR CR

COMMA1: SKIPN   B,NAME+2        ;NULL SOURCE FILE NAME IS ILLEGAL
        JRST    ERR4

        TRNE    F,IFIBIT        ;HAVE OUTPUT FILES BEEN ENTER-ED?
        JRST    DOFILE          ;YES, JUST DO THIS ONE

;AT THIS POINT WE NEED TO SET UP FILESPEC DEFAULTS AND ENTER OUTPUT FILES

;NOW CHECK FOR NULL SPECIFIERS AND INCORRECT SYNTAX WITH NULL FILENAMES

        MOVEI   D,0
        TRO     F,IFIBIT        ;TELLS COMMA ROUTINE THAT OUTPUT FILESPECS ENTER-ED

DEFAULTS:       SKIPE   NAME(D)         ;V2G-
        JRST    DEF1            ;V2G- NAME IS NOT NULL, SO CHECK DEIVCE

        SKIPN   DEV(D)          ;V2G-
        JRST    TURNOF          ;V2G- NULL NAME + DEV MEANS NO OUTPUT

        MOVE    B,NAME+2        ;V2G- GET NAME OF SOURCE FILE 1 FOR DEFAULT
        MOVEM   B,NAME(D)       ;V2G- NON-NULL DEVICE MEANS USE DEFAULT NAME


; NON-ZERO NAME FOR THIS FILESPEC SO CHECK FOR DEVICE DEFAULTS

DEF1:   CAIN    D,0             ;IF REL FILE SAVE NAME
        PUSHJ   P,DEF2          ;SET UP ACCUM FOR DEFAULT MODULE NAME

        MOVSI   B,(SIXBIT 'DSK')        ;DSK IS DEFAULT DEVICE
        SKIPN   DEV(D)          ;IF DEVICE IS NULL,
        MOVEM   B,DEV(D)        ;PUT IN DEFAULT

;NOW DO THE INIT & LOOKUP/ENTER

DOFILE: MOVE    S,DEV(D)        ;GET DEVICE NAME
        AOS     D               ;CHANNEL # = FILESPEC POSITION + 1
        PUSHJ   P,DEVINI        ;INIT THE DEVICE

        MOVE    S,NAME-1(D)     ;GET PROPER FILENAME
        MOVEM   S,XE+3(T)       ;SET NAME IN LOOKUP HEADER

        HLLZ    S,EXT-1(D)      ;GET PROPER EXTENSION

        PUSHJ   P,LOOKUP        ;GO DO LOOKUP/ENTER

        JRST    DEFAULTS        ;GO DO NEXT FILESPEC

;PUT .REL FILENAME INTO PACCUM FOR DEFAULT MODULE NAME

DEF2:   MOVE    A,SAVP          ;MAKE SURE WE GET A GOOD PACCUM
        MOVEM   A,PACCUM        ;SET UP PACCUM
        HRREI   B,-2
        MOVEM   B,0(A)          ;INIT ACCUM
        MOVEM   B,1(A)          ;INIT ACCUM+1

        MOVE    B,[000600,,NAME-1]      ;MAKE BP TO SIXBIT NAME

        MOVNI   G,6             ;SET UP MAX CHAR COUNT

DEF3:   ILDB    H,B
        JUMPE   H,CPOPJ         ;NLL CHAR MEANNS DONE
        MOVEI   H,40(H)         ;V2G- SIXBIT TO ASCII
        IDPB    H,A             ;INTO ACCUM
        AOJL    G,DEF3          ;SIX CHARS MAX

CPOPJ:  POPJ    P,              ;DONE!

TURNOF: CAIN    D,0             ;V2G- IF BINARY FILESPEC,
        TLO     F,BINFLG        ;V2G- TURN OFF BINARY OUTPUT

        CAIN    D,1             ;V2G- IF LISTING FILESPEC,
        TLO     F,LSTFLG        ;V2G- TURN OFF LISTING OUTPUT

        AOJA    D,DEFAULTS      ;V2G- DON'T INIT DEVICE, GO TO NEXT FILESPEC


CAR2:   TRNE    F,CCLBIT        ; ARE WE IN CCL MODE?
        PUSHJ   P,CCLMES        ; YES---GO OUTPUT MESSAGE
        RELEAS 5,               ;RELEASE TTY FOR OUTPUT CHANNEL
        TRZ     F, TTYONB       ;SET FLAG FOR IO ROUTINES
        JRST @BIO

LOOKUP: MOVEM   S,XE+4(T)       ;SAVE EXT
        SETZM   XE+5(T)         ; LET MONITOR SUUPPLY DATE & TIME
LOOK3:  SETZM   XE+6(T)         ;CLEAR FOURTH WORD OF BLOCK
        SKIPE   A,PPNVAL-1(D)   ;V2G- DO WE HAVE A PPN?
        MOVEM   A,XE+6(T)       ; YES---USE IT
LOOK3B:
        HRLI    A, 077000       ;ENTER UUO
        CAIN    D, 3            ;IS IT AN INPUT DEVICE?
        HRLI    A, 076000       ;YES, GET LOOKUP UUO
        DPB     D, CHPTR        ;LOAD THE CHANNEL NUMBER
        HRRI    A, XE+3(T)      ;GET ADDRESS OF LOOKUP BLOCK
        XCT     A               ;EXECUTE THE LOOKUP OR ENTER
        JRST    LOOK4           ;FAILURE-LOOKUP OR ENTER
        TRNE    F, NORBIT       ;NORMAL MODE COMMAND?
        CAIE    D, 3            ;IS THIS THE SOURCE DEVICE?
        POPJ    P,              ;NORMAL EXIT
        JRST    LOOK5           ;TERMINATE SCAN OF COMMAND STRING
LOOK4:  CAIN    D, 3            ;IS THIS AN INPUT DEVICE?
        JRST    ERR5            ;YES, LOOKUP FAILURE
        JRST    ERR14           ;NO, ENTER FAILURE

LOOK5:  POP     P,A             ;NO, CLEAR OFF PUSHDOWN LIST
        TRZN    F,CHRBIT        ;CALLED BY CHAR ROUTINE?
        JRST    CAR2            ;GO INTO CARRIAGE RETURN ROUTINE
        POPJ    P,              ;YES, EXIT

DEV1:   JRST    ERR3
DEV2:   JRST    DEVIN2
DEVPTR: POINT   4,S-1,12
SUBTLE  <LST FILE OUTPUT ROUTINES>

;SUBROUTINE FOR OUTPUT ON THE LISTING FILE
;SUBROUTINES LSTOUT AND LSTMES ARE STRAIGHT-LINE CODING
;PROGRAMS THAT DECREMENT THE COUNT OF THE LISTING DEVICE BUFFER
;AND OUTPUT A BUFFER WHEN REQUIRED. A CHECK IS MADE FOR THE
;OCCURRENCE OF DATA ERRORS AND DEVICE ERRORS.
;THE TWO ROUTINES ARE CALLED IN THE FOLLOWING MANNER;
;CALLS TO LSTOUT
;       JSP     N, LSTOUT
;       CHARACTER RIGHT JUSTIFIED IN ACCUMULATOR K

;CALLS TO LSTMES
;       MOVEI   L, ADDRESS OF MESSAGE
;       PUSHJ   P, LSTMES
;THE ROUTINE LSTOUT SEARCHES FOR CARRIAGE RETURNS, AND CALLS
;HEADER TO OUTPUT A CARRIAGE RETURN - LINE FEED AND A FORM FEED
;IF NECESSARY. NO CHECK IS MADE FOR LINE FEEDS OR FORM FEEDS IN
;LSTOUT, SINCE CHAR FILTERS THEM OUT OF THE INPUT FILES.

LSTOUT: CAIN    K, 15           ;IS CHARACTER A CARRIAGE RETURN?
        JRST    HEADER          ;YES, CALL HEADER
        CAIN    K,14            ;FORM FEED?
        JRST    HEDR            ;YES
LIST2:  SOSG    XB+10(T)        ;DECREMENT ITEM COUNT
        PUSHJ   P, LIST1        ;EMPTY ENTIRE BUFFER
        IDPB    K, XB+3*2+1(T)  ;STORE THE CHARACTER
        JRST    (N)             ;EXIT


LISTC:  PUSH    P, K            ;SAVE A WORKING ACCUMULATOR
        PUSH P, N
LISTA:  ILDB    K, L            ;GET A CHARACTER
        JUMPE   K, LISTB        ;IS IT A NULL?
        JSP     N, LSTOUT       ;CALL THE OUTPUT ROUTINE
        JRST    LISTA           ;GET MORE CHARACTERS
LISTB:  POP P, N
        POP P, K
        POPJ    P,              ;EXIT


LIST1:  OUTPUT  2,              ;EMPTY A BUFFER
        STATZ   2, IODATA+IODEV+IOBKTL  ;CHECK FOR ERRORS
        JRST    ERR7            ;YES, GO COMPLAIN
        POPJ    P,              ;NO, EXIT



;ROUTINE HEADER DEPOSITS THE LINE FEED IN THE LISTING FILE
;BUFFER AND THEN CHECKS TO SEE IF A FORM FEED IS NECESSARY.
;IF SO, THE ACCUMULATORS ARE SAVED, THE PAGE COUNTER IS
;INCREMENTED, A FORM FEED IS ISSUED, AND A HEADER IS PRINTED.
;THE HEADER CONSISTS OF THE PROGRAM TITLE, FOUND IN THE
;TTLBUF BUFFER,  THE DATE, TIME, AND PAGE NUMBER

HEADER: PUSH    P, N            ;SAVE RETURN ADDRESS
        JSP     N, LIST2        ;OUTPUT THE CARRIAGE RETURN
        MOVEI   K, 12           ;OUTPUT A LINE FEED
        JSP     N, LIST2        ;...
        POP     P, N            ;RESTORE RETURN ADDRESS
        TRNE    F, TTYLST       ;IS LISTING DEVICE A TTY?
        PUSHJ   P, LIST1        ;YES, EMPTY THE BUFFER
        JRST (N)
HEDR:   SETZM   PAGCN
        PUSHJ P,        ACSAVE  ;SAVE THE ACC'S
        SETZM   LINCT
        SETZM   DOPAGE          ; IF@PAGE REQUESTED, WE DID IT
        SKIPGE  FFSEEN          ;IF WE SAW FF, ZERO SECONDARY PAGECOUNT
        SETZM   PAGCNT(T)
        HRLI    V, T            ;PUT T IN INDEX PORTION OF V
        SKIPN PAGCN
        AOS     PAGCNT(T)       ;INCREMENT THE PAGE COUNTER
        MOVEI K, 14
        TRNE    F, TTYLST       ;IS LISTING DEVICE A TTY?
        MOVEI   K, 13           ;YES, CHANGE TO VERTICAL TAB
        JSP     N, LIST2        ;OUTPUT IT
        MOVEI K, 15
        JSP N, LIST2
        PUSHJ P, HDRSET
        MOVE    S,SOSPGC        ;GET SOS PAGE NO.
        PUSHJ   P,NUMOUT        ; PRINT IT
        MOVEI   S,"-"           ; PRINT A HYPHEN
        IDPB    S,B
        MOVE    S, PAGCNT(T)    ;GET THE PAGE NUMBER
        PUSHJ   P, NUMOUT       ;INSERT IT IN THE BUFFER
        SKIPE S,PAGCN
        JRST HDR2
HDR0:   MOVEI S,        0       ;MAKE IT AN ASCIZ MESSAGE
        IDPB    S, B            ;BY STORING A NULL CHARACTER
        MOVE    L, TTPNT2       ;GET AN INDEXED BYTE POINTER
        PUSHJ   P, LISTC        ;SEND WHOLE LINE TO LSTMES
        MOVEI K, 15
        JSP N, LIST2
        MOVEI   K, 12           ;OUTPUT A LINE FEED
        MOVEI   B,2
HDR1:   JSP     N, LIST2        ;...
        TRNE    F, TTYLST       ;IS LISTING DEVICE A TELETYPE?
        PUSHJ   P, LIST1        ;YES, OUTPUT THE WHOLE LINE
        SOJG    B, HDR1         ;LOOP BACK FOR MORE LINE FEEDS
        MOVE    B,TTPSAV
        MOVEI   K,0             ;TERMINATE BUFFER BEFORE PAGE # FOR CREF
        IDPB    K,B
        PUSHJ   P, ACREST       ;RESTORE ACCUMULATORS
        JRST    (N)             ;EXIT TO COMPILER
HDR2:   MOVEI L,55
        IDPB L,B
        PUSHJ P,NUMOUT
        JRST HDR0



;THE FOLLOWING SECTION PRINTS THE DATE, WHICH IS FOUND IN
;REGISTER XDATE(T) IN THE FORM
;       ((Y-1964)*12 + (M-1))*31 + (D-1)
HDRSET: MOVE    B, TTLPNT       ;SET THE BYTE POINTER IN AC B
        MOVE    R,JOBVER
        PUSHJ   P,OUTVER        ;OUTPUT TYMSHARE VERSION NUMBER
        MOVEI   S,"-"
        IDPB    S,B
        LSH     R,-^D9
        PUSHJ   P,OUTVER        ;OUTPUT DEC VERSION NUMBER
HDRS2:  MOVEI   S,11            ;PUT IN A TAB
        IDPB    S,B
        MOVE    R, XDATE(T)     ;GET THE DATE IN R
        IDIVI   R,^D31
        ADDI    S,1             ;SET UP DAY IN S
        PUSHJ   P,NUMOUT
        MOVEI   K,"-"
        IDPB    K,B
        IDIVI   R,^D12
        MOVE    K,MONTAB(S)     ;GET REVERSED MONTH NAME IN K
        IDPB    K,B
        LSH     K,-7
        JUMPN   K,.-2
        MOVEI   K,"-"
        IDPB    K,B
        MOVEI   S,^D64(R)       ;YEAR IN S
        PUSHJ   P, NUMOUT       ;TYPE IT
        MOVEI   K, 40           ;OUTPUT A SPACE
        IDPB    K, B            ;...
        IDPB    K,B
;THE FOLLOWING SECTION OF CODE PRINTS THE TIME, WHICH IS
;FOUND IN XTIME(T) AS THE NUMBER OF (1/60)'S OF A SECOND
;SINCE MIDNIGHT. THE FORMAT OF  THE TIME PRINTOUT IS
;HHMM.SS
        CALLI R, TIMUUO
        IDIVI   R, ^D1000       ;TIME IN SECONDS NOW IN R
        IDIVI   R, 74           ;EXCESS SECONDS IN S
        PUSH    P, S            ;SAVE THE SECONDS
        IDIVI   R, 74           ;MINUTES IN S
        PUSH    P, S            ;SAVE THE MINUTES
        MOVE    S, R            ;GET HOURS IN S
        PUSHJ   P, NUMOUT       ;OUTPUT THE HOURS
        MOVEI   K,":"           ;OUTPUT A COLON AFTER THE HOURS
        IDPB    K, B            ;...
        POP     P, S            ;RECOVER THE MINUTES
        PUSHJ   P,OUTN2         ;OUTPUT THE MINUTES
        MOVEI   K,":"           ;OUTPUT A COLON
        IDPB    K, B            ;...
        POP     P, S            ;RECOVER THE SECONDS
        PUSHJ   P,OUTN2         ;OUTPUT THE SECONDS
        MOVEI   K, 11           ;OUTPUT A TAB
        IDPB    K, B            ;...
;
;
;       OUTPUT THE MODULE NAME AND FILE NAME
;
        MOVEI   S,^D10
        MOVE    R,[POINT 7,MNAM##]
OUTMNM: ILDB    K,R
        CAIN    K,177
        JRST    .+3
        IDPB    K,B             ;OUTPUT MODULE NAME
        SOJG    S,OUTMNM
        MOVEI   K,11
        IDPB    K,B             ; ANOTHER TAB
        MOVEI   S,6             ; COUNT OF CHARS
        HRRI    R,XE+3(T)       ; POINT TO FILE NAME
        HRLI    R,440600        ; SIXBIT POINTER
        PUSHJ   P,HDRSIX        ; PRINT IT
        MOVEI   K,"."           ; DOT
        IDPB    K,B             ; ...
        MOVEI   S,3             ; COUNT
        HRRI    R,XE+4(T)       ; POINT TO EXTENSION NAME
        HRLI    R,440600        ; SIXIBIT POINTER
        PUSHJ   P,HDRSIX        ; PRINT IT
        MOVEM   B,TTPSAV               ;SAVE POINTER BEFORE PAGE #
        MOVEI   K,11
        IDPB    K,B
        IDPB    K,B             ; ANOTHER TAB

;THE FINAL SECTION OF CODING PICKS UP THE WORD "PAGE " AND
;STORES IT IN THE PROPER PLACE IN THE TITLE BUFFER.

        MOVEI   S, 5            ;COUNTER FOR FIVE CHARACTERS
        MOVE    R, PGEPTR       ;GET BYTE POINTER TO "PAGE "
        ILDB    K, R            ;PICK UP A CHARACTER
        IDPB    K, B            ;STORE IT
        SOJG    S, .-2          ;LOOP FOR MORE CHARACTERS
        POPJ    P,              ;EXIT


OUTN2:  MOVEI   K,"0"           ;OUTPUT DECIMAL NUMBER OF AT LEAST 2 DIGITS
        CAIGE   S,^D10
        IDPB    K,B
        JRST    NUMOUT

OUTVER: LDB     S,[POINT 9,R,17]
        PUSHJ   P,OCTOUT        ;OUTPUT SPECIFICATION PART
        MOVEI   S,"."
        IDPB    S,B
        LDB     S,[POINT 9,R,35]
;FALL THROUGH TO OCTOUT FOR RELEASE PART
ENTRY   NUMOUT
OCTOUT: MOVEI   C,^D8           ;OUTPUT THE OCTAL NUMBER IN S
        SKIPA
NUMOUT: MOVEI   C,^D10          ;OUTPUT THE DECIMAL NUMBER IN S
NUMO2:  IDIVI   S, (C)          ;RECURSIVE SUBROUTINE
        HRLM    S+1, (P)        ;SAVE REMAINDER ON PUSHDOWN LIST
        SKIPE   S               ;ALL DONE?
        PUSHJ   P, NUMO2        ;NO, CALL NUMOUT AGAIN
        HLRZ    K, (P)          ;RETRIEVE NUMBER FROM PD LIST
        ADDI    K, 60           ;MAKE IT ASCII
        IDPB    K, B            ;OUTPUT IT
        POPJ    P,              ;GET NEXT NUMBER OR EXIT

HDRSIX: ILDB    K,R             ; GET A SIXIBIT CHAR
        JUMPE   K,HDR3          ; DO NOT PRINT NULLS
        ADDI    K,40            ; MAKE 7-BIT
        IDPB    K,B             ; WRITE INTO BUFFER
HDR3:   SOJG    S,HDRSIX        ; LNOP
        POPJ    P,              ; RETURN

MONTAB: EXP     "NAJ","BEF","RAM","RPA","YAM","NUJ"
        EXP     "LUJ","GUA","PES","TCO","VON","CED"

SUBTLE  <TELETYPE OUTPUT ROUTINES>
;BASIC TELTYPE OUTPUT ROUTINES
;THESE ROUTINES EXIST FOR THE USE OF THE EXEC ONLY, BUT THEIR
;CALLING SEQUENCES ARE THE SAME AS THOSE FOR LSTOUT, LSTMES,
;CONOUT, CONMES, ERROUT, AND ERRMES

TTYOUT: SKIPE   QUIET           ;IF KEEPING TELETYPE QUIET
        SKIPE   ERRORFOUND##    ;AND NO ERRORS FOUND,
        JRST    .+2
        JRST    (N)             ;THEN DON'T DO ANYTHING
        AOSE    NEDCCM          ;DO WE NEED TO DO CCL MESSAGE?
        JRST    TTYOU1          ;NOPE
        PUSH    P,N             ;YES, SET UP FOR A RECURSIVE CALL
        PUSH    P,K
        MOVEI   L,MNAM
        PUSHJ   P,TTYMES        ;OUTPUT MODULE NAME
        MOVEI   K,15
        JSP     N,TTYOUT        ;AND A CR-LF
        POP     P,K
        POP     P,N
TTYOU1: TRNN    F, TTYONB       ;HAS TELETYPE BEEN INITIALIZED?
        PUSHJ   P, TTINIT       ;NO, INITIALIZE IT
        CAIN    K,12            ;A LINE FEED?
        JRST (N)                ;YES, IGNORE IT
        IDPB    K, XB+1(T)      ;STORE THE CHARACTER
        CAIE    K,15            ;A CARRIAGE RETURN?
        JRST (N)                ;NO RETURN
        MOVEI   K,12            ;YES, INSERT A LINE FEED
        IDPB    K, XB+1(T)      ;INTO THE BUFFER
        OUTPUT 5,               ;OUTPUT THE BUFFER
        JRST    (N)     ;RETURN

ENTRY TTYMES
TTYMES: PUSH    P, K            ;SAVE WORKING ACCUMULATOR
        HRLI    L, 440700       ;MAKE A BYTE POINTER

TTMESA: ILDB    K, L            ;GET A CHARACTER
        JUMPE   K, TTMESB       ;IS IT A NULL?
        CAIN    K,177
        JRST    TTMESB          ;OR 177?
        JSP     N, TTYOUT       ;NO, OUTPUT IT
        JRST    TTMESA          ;RETURN FOR MORE CHARACTERS
TTMESB: POP     P, K            ;RESTORE WORKING AC
        POPJ    P,              ;EXIT


TTINIT: 
        PUSH    P, XE+1(T)      ;SAVE CURRENT DEVICE NAME
        PUSHJ   P, ACSAVE       ;SAVE ACCUMULATORS
        HRLI    V, T            ;PUT T IN INDEX PORTION OF V
        HRLZI   S, (SIXBIT /TTY/)       ;INITIALIZE TTY
        MOVEI   D, 5            ;CHANNEL 5, EXEC OUTPUT
        PUSHJ   P, DEVINI       ;...
        PUSHJ   P, ACREST       ;RESTORE ACCUMULATORS
        POP     P, XE+1(T)      ;RESTORE CURRENT DEVICE NAME
        POPJ    P,              ;EXIT

SUBTLE <READ SOURCE FILES>
;ROUTINE TO INPUT CHARACTERS FROM THE SOURCE DEVICE
;THIS ROUTINE ASSUMES THAT THE COMMAND SCANNER PORTION OF
;THE EXEC HALTS AFTER IT HAS FOUND THE FIRST INPUT FILE.
;ROUTINE CHAR INPUTS CHARACTERS ON CHANNEL 3 UNTIL AN END OF
;FILE CONDITION IS MET. IT THEN CHECKS A FLAG TO SEE IF ANY 
;MORE INPUT FILES NEED TO BE INITIALIZED. IF SO, IT RELEASES
;THE OLD INPUT FILE AND INITIALIZES A NEW ONE, AND CONTINUES 
;TO DO INPUT ON THIS FILE.
;WHEN THE LAST END OF FILE IS REACHED, THE EXEC PLACES AN
;S2 CHARACTER AND A CARRIAGE RETURN IN THE BUFFER
CHARA:  MOVEM   J,SEQNUM(T)     ;YOU CMU DUMMIES FORGOT TO SAVE THE LINE NUMBER!
        MOVNI   J,5
        ADDM    J,XB+13(T)
        AOS     XB+12(T)
INCHR:  JRST    .+1
CHAR:   SOSG    XB+3*3+2(T)
        PUSHJ   P,CHAR1
        IBP     XB+12(T)
        MOVE    J, @XB+12(T)
        TRZE    J,1
        JRST CHARA
        LDB     J,XB+12(T)
        JUMPE   J, CHAR
        JRST    (N)
CHAR1:  INPUT   3,              ;CALL MONITIOR FOR A BUFFER
        STATZ   3, IODATA+IODEV+IOBKTL
        JRST    ERR8            ;INPUT TRANSMISSION ERROR
        STATO   3, IOEOF        ;WAS AN END OF FILE REACHED?
        POPJ    P,              ;EXIT
        PUSHJ   P, ACSAVE       ;YES, SAVE ACS
        HRLI    V, T            ;PUT T IN INDEX PORTION OF V
        CLOSE 3,
        TRNE    F, ENDBIT       ;END OF ALL INPUT FILES?
        JRST    CHAR2           ;YES
        MOVE    A, XB-1(T)      ;PREPARE FOR NEW DEVICE...
        EXCH    A, XA+F(T)      ;...BY RESETTING UPDATED JOBFF
        MOVEI   D, 2            ;SET CHANNEL NUMBER AC
        MOVEI   A,1
        MOVEM   A,FILLINE##     ;ZAP FILE'S LOCAL LINE NUMBER
        MOVEM   A,FILPAGE##     ;AND PAGE NUMBER
        SETZM   NEWPAGE##       ;NOT A NEW PAGE REALLY
        SETOM   NEWFILE##       ;BUT A NEW FILE YES YES
        SETZM   SEQNUM(T)       ;ZAP CURRENT EDIT10 SEQUENCE NUMBER
        TRO     F, CHRBIT       ;SET THE BIT
        MOVEM   0,SAVE0         ; IN CASE WE HAVE TO PUNT
        PUSHJ   P, GETCH2       ;GET SOME MORE OF THE COMMAND
        PUSHJ   P, ACREST       ;RESTORE THE ACCUMULATORS
        POPJ    P,              ;AND RETURN


CHAR2:  MOVEI   A, 3            ;SET UP ARTIFICIAL COUNT
        MOVEM   A, XB+3*3+2(T)  ;IN BUFFER HEADER
        HRR     A, XB-1(T)      ;SET UP A BYTE POINTER
        HRLI    A, 440700       ;...
        MOVEM   A, XB+3*3+1(T)  ;SAVE IT IN THE BUFFER HEADER
        MOVEI   B, 32           ;EOF CHARACTER (32)
        IDPB    B, A            ;...
        MOVEI   B, 15           ;CARRIAGE RETURN
        IDPB    B, A            ;...
        TLO     F,FINFLG        ;GIVE FINAL EOF FLAG TO BLISS
        PUSHJ   P, ACREST       ;RESTORE ACS
        POPJ    P,              ;EXIT

;ROUTINES TO SAVE AND RESTORE THE COMPILER ACCUMULATORS
ACSAVE: EXCH    A,0(P)          ; A GETS RETURN ADDRESS
        PUSH    P,B     ;
        PUSH    P,C
        PUSH    P,D
        PUSH    P,E
        PUSH    P,G
        PUSH    P,H
        PUSH    P,R
        PUSH    P,S
        PUSH    P,V
        PUSH    P,S+1
        JRST    @A      ; SNEAKY
ACREST: POP     P,A     ; A_RETURN ADDRESS
        POP     P,S+1
        POP     P,V
        POP     P,S
        POP     P,R
        POP     P,H
        POP     P,G
        POP     P,E
        POP     P,D
        POP     P,C
        POP     P,B
        EXCH    A,0(P)  ; TOP_RETURN; A_PREVIOUS TOP
        POPJ    P,      ; SNEAKY (SEE COMMENT ABOVE)


OUTBN2: OUTPUT 1,               ;OUTPUT ON CHANNEL 1
        STATZ 1, IODATA+IOBKTL+IODEV    ;CHECK FOR ERRORS
        JRST ERR10              ;TRANSMISSION ERROR
        POPJ P,                 ;RETURN


SUBTLE <ERROR ROUTINES>
;ERROR ROUTINES

;BAD CHARACTER OR NUMBER IN SWITCH MODE
ERR1:   JSP N, TTYOUT
        MOVEI   L, ERR1M        ;TYPE A MESSAGE
ERRCOM: PUSHJ P, TTYMES
        JRST    ERROR           ;NO, EXIT

;BAD CHARACTER IN COMMAND STRING, NORMAL MODE
ERR2:   JSP     N, TTYOUT       ;TYPE THE BAD CHARACTER
        MOVEI   L, ERR2M        ;MESSAGE
        JRST ERRCOM

;XXX IS NOT AVAILABLE
ERR3:   PUSHJ   P, STYPO        ;TYPE THE DEVICE NAME
        MOVEI   L, ERR3M        ;GET A MESSAGE
ERR3A:  PUSHJ   P, TTYMES       ;TYPE IT
        CAIN    D, 3            ;SOURCE DEVICE?
        POP     P, A            ;YES, CLEAR JUNK OFF PD LIST
        JRST ERROR
; BLISS COMMAND ERROR
ERR4:   MOVEI   L, ERR4M        ;TYPE A MESSAGE
        JRST ERRCOM
;LOOKUP FAILURE
ERR5:   HLRZ    S, XE+4(T)      ;GET FILE NAME EXTENSION
        CAIE    S,(SIXBIT 'BLI')        ;IF BLI WAS NOT PREVIOUS TRY
        JRST    ERR5C           ;THEN SEE IF WE TRIED NULL EXT
        MOVSI   S,(SIXBIT 'B10')        ;TRY SECOND DEFAULT
        JRST    ERR5D           ;GO DO THE LOOKUP AGAIN

ERR5C:  JUMPE   S, ERR5B        ;IS IT BLANK?
        MOVEI   S, 0            ;NO, TRY ANOTHER LOOKUP
ERR5D:  TRNN F, EXTBIT
        JRST    LOOKUP          ;ENTER LOOKUP ROUTINE AGAIN
ERR5B:  MOVEI   L, ERR5M        ;"CANNOT FIND FILE"
ERR5A:  PUSHJ   P, TTYMES       ;TYPE IT
        MOVE    S, XE+3(T)      ;GET THE FILE NAME
        PUSHJ   P, STYPO        ;TYPE IT
        MOVEI   A,"."
        JSP     N,TTYOUT        
        HLLZ    S,XE+4(T)       ; GET THE EXTENSION
        PUSHJ   P,STYPO         ; TYPE IT TOO
        SKIPN   A,PPNVAL-1(D)   ;V2G-  PPN?
        JRST    NOPPPN          ; NO---NONE AT ALL
PPPN:   PUSH    P,A             ; SAVE PPN
        MOVEI   A,"["           ; [
        JSP     N,TTYOUT        ; PRINT IT
        POP     P,A             ; RESTOR A
        HRRI    S,PPNBUFF       ; POINT TO BUFFER
        IFN     CMUSW,<
        HRLI    S,A             ; AND A
        CALL    S,[SIXBIT /DECCMU/]     ; AND CONVERT IT
        JRST    PPNX            ; SOMETHING HAPPENED>
        IFE     CMUSW,<
        HRLI    S,440700
        PUSH    P,S+1           ; SAVE S+1
        PUSH    P,S+2
PPNA4:  MOVEI   S+2,6
PPNA3:  LDB     S+1,[POINT 3,A,2]
        JUMPN   S+1,PPNA1       ; IF NOT 0, GO PRINT IT
PPNA2:  LSH     A,3             ; SHIFT OVER PPN
        SOJG    S+2,PPNA3       ; GET NEXT
PPNA7:  JUMPE   A,PPNA6         ;V2G- ARE WE DONE?
        MOVEI   S+1,","         ; PRINT ,
        IDPB    S+1,S
        JRST    PPNA4           ; GET PROGRAMMER NO.
PPNA1:  ADDI    S+1,"0"         ; CONVERT TO ASCII
        IDPB    S+1,S           ; PLUNK IT DOWN
        LSH     A,3             ;V2G- PRINT 0S TOO NOW
        SOJLE   S+2,PPNA7       ;V2G- IF DONE, CHECK FOR PROG #
        LDB     S+1,[POINT 3,A,2]       ;V2G- GET NEXT DIGIT
        JRST    PPNA1           ;V2G- GO TYPE IT
PPNA6:  SETZ    S+1,
        IDPB    S+1,S           ; MAKE IT ASCIZ STRING
        POP     P,S+2
        POP     P,S+1 >
        MOVEI   L,PPNBUFF       ; POINT TO BUFFER
        PUSHJ   P,TTYMES        ; PRINT PPN
PPNX:   MOVEI   A,"]"
        JSP     N,TTYOUT
NOPPPN:
        MOVEI   L, ERR5MA       ;"ON DEVICE "
        PUSHJ   P, TTYMES       ;TYPE IT
        MOVE    S, XE+1(T)      ;GET DEVICE NAME
        PUSHJ   P, STYPO        ;TYPE IT
        MOVEI   A, 15           ;TYPE A CARRIAGE RETURN
        JSP     N, TTYOUT       ;...
        MOVEI   A, 12           ;TYPE A LINE FEED
        JSP     N, TTYOUT       ;...
        JRST    ERROR           ;EXIT
;OUTPUT ERROR ON LISTING DEVICE
ERR7:   MOVEI   D, 2            ;LISTING IS DEVICE NUMBER 2
        JRST    ERR10A          ;ENTER COMMON ROUTINE

;INPUT ERROR ON SOURCE DEVICE
ERR8:   MOVEI   D, 3            ;SOURCE IS DEVICE NUMBER 3
        JRST    ERR10A          ;ENTER COMMON ROUTINE


;OUTPUT ERROR ON BINARY DEVICE
ERR10:  MOVEI   D, 1            ;BINARY IS DEVICE NUMBER 1
ERR10A: MOVEI   L, ERR10C       ;"TRANSMISSION ERROR ON "
        PUSHJ   P, TTYMES       ;TYPE IT
        MOVE    L, ERR10M-1(D)  ;GET DEVICE TYPE
        TRO     F,CHRBIT        ; TRICK IT INTO PUNTING
        MOVEM   0,SAVE0         ; PUNT GLITCH NEEDS THIS TOO
        JRST ERRCOM

;XXX IS NOT A DEVICE
ERR11:  MOVE    S, XE+1(T)      ;GET DEVICE NAME
        PUSHJ   P, STYPO        ;TYPE IT
        MOVEI   L, ERR11M       ;"IS NOT A DEVICE"
        JRST ERRCOM



;PROTECTION ERROR ON FILE XXX
ERR12:  MOVEI   L, ERR12M       ;"PROTECTION ERROR ON FILE "
        JRST    ERR5A           ;EXIT

;ANOTHER USER WRITING ON FILE XXX
ERR13:  MOVEI   L, ERR13M       ;"ANOTHERUSER WRITING..."
        JRST    ERR5A           ;EXIT

;DIRECTORY CAPACITY EXCEEDED ON FILE XXX
ERR14:  MOVEI   L, ERR14M       ;"DIRECTORY CAPACITY EXCEEDED..."
        JRST    ERR5A           ;EXIT

;XXX CANNOT DO IO AS REQUESTED
ERR15:  PUSHJ   P, STYPO        ;TYPE DEVICE NAME
        MOVEI   L, ERR15M       ;GET A MESSAGE
        JRST    ERR3A           ;ENTER "NOT AVAILABLE" ROUTINE

;XXX CANNOT BE USED AS AN XXX DEVICE
ERR17:  PUSHJ   P, STYPO        ;TYPE DEVICE NAME
        MOVEI   L, ERR17M       ;GET A MESSAGE
        PUSHJ   P, TTYMES       ;TYPE THE MESSAGE
        MOVE    L, ERR10M-1(D)  ;GET DEVICE TYPE
        JRST ERRCOM

PPNERR: MOVEI   L,ERRPPN        ; PPN ERR
        PUSHJ   P,TTYMES
        MOVEI   A,"["
        JSP     N,TTYOUT
        MOVEI   L,PPNBUFF
        PUSHJ   P,TTYMESS
                MOVEI   A,"]"
        JSP     N,TTYOUT
        MOVEI   A,15
        JSP     N,TTYOUT
        JRST    ERROR

ERRPPN: ASCIZ   "ILLEGAL PPN: "



;AUXILIARY ERROR SUBROUTINES

ERROR:  TRZE F,CHRBIT           ; DID WE COME FROM CHR ROUTINE?
        JRST PUNTIT             ; YES...PUNT
        TRZE    F, TTYONB       ;WAS TTY INITIALIZED FOR OUTPUT?
        RELEAS  4,              ;YES, GET RID OF IT
        TRZE    F, ARWBIT       ;DID WE GET TO A SOURCE FILE?
        RELEAS  3,              ;YES, RELEASE IT
        TLON    F, LSTFLG       ;LISTING DEVICE?
        RELEAS  2,              ;YES, RELEASE IT
        TLON    F, BINFLG       ;BINARY DEVICE?
        RELEAS  1,              ;YES, RELEASE IT
        SETZM   CCLCTL          ; CLEAR CCL SWITCHES
        JRST F4EXEC

PUNTIT: MOVE    0,SAVE0         ; GET BLISS STACK REG
        SETZM   CCLCTL
        PUSHJ   0,SVBLIS        ; BACK TO BLISS WORLD
        PUSH    0,[500]         ; PUNT (#500);
        PUSHJ   0,PUNT
        EXTERN PUNT

;ROUTINE STYPO TYPES OUT THE CONTENTS OF AC S, WHOSE
;CONTENTS ARE ASSUMED TO BE SIXBIT CHARACTERS

STYPO:  MOVEI   B, 6            ;SIX CHARACTERS
        MOVE    C, SYMPTR       ;POINTER TO AC A
STYPO2: ILDB    A, C            ;GET A CHARACTER
        JUMPE   A, STYPO3       ;DONT TYPE A NULL
        ADDI    A, 40           ;CONVERT TO SEVEN BIT ASCII
        JSP     N, TTYOUT       ;TYPE IT OUT
STYPO3: SOJG    B, STYPO2       ;MORE TO TYPE?
        POPJ    P,              ;NO, EXIT

SUBTLE <ERROR MESSAGES>
;ERROR MESSAGE ADDRESSES
ERR10M: XWD     0, ERR10D
        XWD     0, ERR10E
        XWD     0, ERR10F

;ERROR MESSAGES

ERR1M:  ASCIZ   " IS AN ILLEGAL SWITCH
"
ERR2M:  ASCIZ   " IS AN ILLEGAL CHARACTER
"
ERR3M:  ASCIZ   " IS NOT AVAILABLE
"
ERR4M:  ASCIZ  "BLISS  COMMAND ERROR
"
ERR5M:  ASCIZ   "CANNOT FIND FILE "
ERR5MA: ASCIZ   " ON DEVICE "


ERR10C: ASCIZ   "TRANSMISSION ERROR ON "
ERR10D: ASCIZ   "BINARY DEVICE
"


ERR10E: ASCIZ   "LISTING DEVICE
"
ERR10F: ASCIZ   "SOURCE DEVICE
"
ERR11M: ASCIZ   " IS NOT A DEVICE
"
ERR12M: ASCIZ   "PROTECTION FAILURE FOR FILE "
ERR13M: ASCIZ   "ANOTHER USER CURRENTLY WRITING FILE "
ERR14M: ASCIZ   "DIRECTORY FULL OR FILE WRITE-PROTECTED: "
ERR15M: ASCIZ   " CANNOT DO IO AS REQUESTED
"


ERR17M: ASCIZ   " CANNOT BE USED AS "
SUBTLE <CONSTANTS AND DATA AREAS>
PGEMES: ASCIZ   "PAGE "

;CONSTANTS AND FIXED DATA

DATE==14
TIMUUO==23
DEVCHR==4
CHPTR:  POINT   4, A, 12
SYMPTR: POINT   6, S
TTPNT2: POINT   7, TTLBUF(T)
TTLPNT: POINT   7, TTLBUF+2(T)
PGEPTR: POINT   7, PGEMES


SUBTLE <BLISS INTERFACE ROUTINES>
; BLISS INTERFACE ROUTINES
;---------------------------


EXTERNAL BXA,DEVBPT
EXTERNAL BUFF,PBUFF
INTERNAL FORCE,TTYLIS,READTEXT,INITIO,FINIO,PAGE,BIO, SOSPGC, NAME, TTLBUF

DEFINE RENTRY <
        IFE TIMSW,<>
        IFN TIMSW,<
        HRRZI 12,0
        PUSH  12
        PUSHJ TIMER
        SUB     0,[XWD 1,1]
>>
DEFINE REXIT <
        IFE TIMSW,<>
        IFN TIMSW,<
        HRROI   12,0
        PUSH    12
        PUSHJ   TIMER
        SUB     0,[XWD 1,1]
>>


SVBLIS: EXCH B,BXA+B
        EXCH F,BXA+F
        EXCH T,BXA+T
        EXCH P,BXA+P
        EXCH J,BXA+J
        EXCH K,BXA+K
        EXCH L,BXA+L
        EXCH N,BXA+N
        POPJ 0,0

FORCE:  RENTRY
        PUSHJ 0,SVBLIS
        MOVE  K,0
        MOVE  K,-1(K)
        XCT   DSLIST(K)
        PUSHJ 0,SVBLIS
        REXIT
        POPJ  0,0
DSLIST: JFCL
        PUSHJ P,OUTBN2
        PUSHJ P,LIST1
        JFCL
        JFCL


INITIO: RENTRY
        PUSHJ 0,SVBLIS
        EXCH  0,BXA
        JSR   BIO
        MOVEI N,XB(T)
        MOVEM N,DEVBPT
        EXCH  0,BXA
        PUSHJ 0,SVBLIS
        REXIT
        POPJ  0,0

FINIO:  RENTRY
        PUSHJ 0,SVBLIS
        CLOSE 1,0
        CLOSE 2,0
        CLOSE 4,0
        TLO     F,BINFLG+LSTFLG ;%2.19% DEVICES ARE RELEASED
        TRZ     F,TTYONB        ;%2.19% TTY IS OFF ALSO
        PUSHJ   0,SVBLIS        ;%2.19% SAVE NEW FLAGS WORD
        REXIT
        POPJ  0,0

PAGE:   RENTRY
        PUSHJ 0,SVBLIS
        SKIPE   NAME+1          ;IF THERE IS ANY LISTING DEVICE AT ALL,
        TROE    F,DIDHED        ;DO VERY FIRST HEADER EVEN IF LISTING TURNED OFF
        TLNN    F,LSTFLG
        JSP   N,HEDR
        PUSHJ 0,SVBLIS
        REXIT
        POPJ  0,0

TTYLIS: RENTRY
        PUSHJ 0,SVBLIS
        MOVE  K,0
        MOVE K,-1(K)
        JSP   N,TTYOUT
        PUSHJ 0,SVBLIS
        REXIT
        POPJ  0,0

READTEXT: RENTRY
        PUSHJ 0,SVBLIS
        MOVEI   3,0     ;RETURN ZERO VALUE IF LINE OK
        MOVE  N,[POINT 7,BUFF-1,34]
        MOVEM N,PBUFF
        MOVEI N," "
        IDPB  N,PBUFF
        TLNE  F,FINFLG
        JRST  RTEXIT
RTEXT1: JSP   N,INCHR
        HRRZ    A,PBUFF
        CAIL    A,BUFF+^D27     ;ALLOW MAX OF 135 CHARS IN LINE
        JRST    RTEXT4          ;RETURN 1 TO INDICATE LINE TOO LONG
        CAIE  J,11
        CAIL  J," "
        IDPB  J,PBUFF
        CAIN    J,14    ;FF
        JRST    RTEXT2
        CAIE  J,15
        JRST  RTEXT1
        IDPB J,PBUFF
RTEXIT: MOVEI J,177
        IDPB  J,PBUFF
        MOVE  N,[POINT 7,BUFF-1,34]
        MOVEM N,PBUFF
        PUSHJ 0,SVBLIS
        REXIT
        POPJ  0,0

RTEXT2: MOVEI   B,1
        MOVEM   B,FFSEEN        ;SET TO 1 WHEN SEEN, AND -1 WHEN WE WANT
                                ;TO PRINT PAGE HEADER WITH UPDATED PAGECOUNT
        JRST    RTEXT1

RTEXT4: JSP     N,INCHR         ;SKIP THRU REMAINDER OF LINE
        CAIE    J,15            ;UNTIL WE GET CR
        JRST    .-2
        MOVEI   3,1             ;INDICATES ERROR READING LINE
        JRST    RTEXIT          ;INSERT #177 TO TERMINATE

RELOC           ;%2.13%
;       CONTROL WORDS FOR CCL CAPABILITIES
CCLCTL:: 0                      ; CCL SWITCHES
                                ; 1=> CCL ENTRY, 3=> CCL FILE READ
                                ; 0 => NO CCL ENTRY
CCLBFH:: BLOCK 3                ; BUFFER HEADER FOR CCL COMMANDS
CCLBUF:: BLOCK ^D131            ; BUFFER FOR CCL COMMANDS
SUBTLE <INITIALIZATION>
BIO:    0
        JRST    F4EXEC          ;%2.13%
SOSPG:  XWD     201004,20100
DEV:    BLOCK   3               ;FILESPEC DEVICES
NAME:   BLOCK   3               ;FILESPEC FILENAMES
PPNVAL: BLOCK   3               ;V2G- WHAT OUR PPN IS FOR THIS FILESPEC
EXT:    BLOCK   3               ;FILESPEC EXTENSIONS
SOSPGC: 0
SAVP:   0                       ; PLACE TO SAVE PACCUM
BINFIL: 0                       ; ACCUMULATE MODULE NAME SWITCH
BLIMP:: 0                       ;FLAG SAYS TO PRINT FUNNY BLIMP MESSAGE
QUIET:  0                       ;NONZERO SAYS KEEP TTY QUIET
TTPSAV: 0                       ;SAVED POINTER INTO TTLBUF BEFORE PAGE #
NEDCCM: 0                       ;-1 SAYS NEED TO TYPE CCL MESSAGE
PPNBUFF:        BLOCK   3       ; WHERE WE HOLD PPN'S
PPNPERM: 0              ; A PERMANENT PPN
TTOBUF: Z
LINCT:  Z
PAGCN:  Z
DOPAGE::        0
SAVE0:: 0       ;STORAGE TO SAVE AC 0

        RELOC   ;PUT LITERALS IN HISEG

        END     ;OF LOIO MODULE
 ~ 1