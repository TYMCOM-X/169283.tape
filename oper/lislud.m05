TITLE LISLUD - LIST THE CUPERTINO LUD AND ACCOUNTING
SUBTTL J. MARCIN -1/4/72

;LOAD BLKDT1,COMP1,TRPINI,READIT,LISTER%F,LISLUD

VLISLD==5
JOBVER=137
LOC JOBVER
XWD 0,VLISLD
RELOC

EXTERNAL READIT,FORSE.,LISTER,EXIT.,IBCODE
EXTERNAL IUSE1,IUSE2,PCODE,TCODE,ICUSTN,ISLSMN

MLON
;AC'S
F=0
A=1
B=2
C=3
D=4
E=5
INDX2=6
N=7
N1=10
N2=11
N3=12
WD=12
BP=14
P=15
CH=13
INDEX=16

;FLAGS IN RIGHT HALF OF F
TTY==1	;OUTPUT GOES TO TTY
NOSPC==2	;DON'T TYPE SPACE
LEAD==4	;DON'T TYPE LEADING ZEROES
DULOUT==10	;DUL IS BEING OUTPUT
NOLOK==40	;DON'T CHECK USERS.DAT


;I/O CHANNELS
DSK=17
LPT=16
DSK1==15

;UUO'S
RESET=0
EXIT=12
LICTAB==-20	;GETTAB FOR LICENSE
SETLIC==-10	;SET LICENSE

OPRLIC==100000	;OPERATOR LICENSE

LSTBUF:	RESET. 00,0
	CALLI RESET
	MOVE P,PDP
	SETZ F,
	HRROI A,LICTAB
	GETTAB A,
	MOVEI A,0	;SAY NO LICENSE
	TLNE A,OPRLIC	;DOES HE HAVE OPER LIC??
	JRST LICOK
	HRLS A
	CALLI A,SETLIC
LICOK:	PUSHJ P,SAVACS
	JSA 16,READIT
	0,MAX
	MOVE 16,[XWD ACBLK,0]
	BLT 16,16
	PUSHJ P,GETDSK
	TTCALL 3,[ASCIZ/ TYPE "H" FOR HELP.
/]
LISLUD:	TTCALL 3,[ASCIZ /*/]
	TTCALL 4,CH
	TTCALL 11,0
	CAIN CH,"L"
	JRST LISTIT
	CAIN CH,"D"
	JRST LISDUL
	CAIN CH,"T"
	JRST TYPIT
	CAIN CH,"P"
	JRST TYPPN
	CAIN CH,"C"
	JRST CHKIT
	CAIN CH,"A"
	JRST ALLOUT
	CAIN CH,"N"
	JRST NAMOUT
	CAIN CH,"H"
	JRST HELP
	CAIN CH,"Q"
	CALLI EXIT
	CAIN CH,"E"
	CALLI EXIT
	JRST LISLUD-1

SAVACS:	MOVEM F,ACBLK
	MOVE F,[XWD A,ACBLK+1]
	BLT F,ACBLK+16
	POPJ P,0

HELP:	TTCALL 3,HELMSG
	JRST LISLUD
HELMSG:	ASCIZ / YOU HAVE THE FOLLOWING OPTIONS:
L	LISTS LUD.SYS AND ACCOUNTING FILES ON LPT
	(OR ON LUD.LST IF DSK ASSIGNED LPT)
D	LISTS DUL.SYS ON THE LPT (OR DUL.LST IF DSK ASSIGNED LPT)
T	TYPES THE LUD AND ACCOUNTING ENTRY FOR THE NAME
	(TYPE NAME ON NEXT LINE)
P	TYPES THE LUD AND ACCOUNTING ENTRY FOR THE PPN
	(TYPE PPN ON NEXT LINE)
N	TYPES THE NAME AND ADDRESS ENTRY FOR THE CUSTOMER NUMBER
	(TYPE CUSTOMER NUMBER ON THE NEXT LINE)
A	WRITES ALL NAMES AND ADDRESSES IN A DSK FILE CALLED ADRES.DAT
C	CONSISTENCY CHECK BETWEEN USERS.DAT,LUD.SYS AND DUL.SYS
	PROGRAM EXITS WHEN COMPLETE
H	TYPES THIS TEXT
E(OR Q)	EXITS
/

LISTIT:	PUSHJ P,GETLUD
	PUSHJ P,GETLPT
	PUSHJ P,HEADOUT
	MOVEI A,1
	CAIL A,^D888
	JRST LISEND
	USETI DSK,@A
	INPUT DSK,LUDLST
LIST1:	SETZ INDEX,
	SKIPN B,LUDBLK(INDEX)
	AOJA A,LISTIT+4
	JUMPG B,LIST2
	PUSHJ P,ROVBLK
	JRST LIST1
LIST2:	SKIPN ,LUDBLK+4(INDEX)
	JRST .+4
	PUSH P,A
	PUSHJ P,ENTOUT
	POP P,A
	LDB C,NENTP
	ADD INDEX,C
	JRST LIST1+1
LISEND:	CLOSE LPT,
	RELEASE LPT,
	JRST LISLUD

GETDSK:	INIT DSK,17
	SIXBIT /SYS/
	0
	CALLI EXIT
	POPJ P,0

ENTOUT:	MOVE N,LUDBLK(INDEX)	;OUTPUT NAME IN SIXBIT
	PUSHJ P,SQZIT2
	MOVEM N,NAME
	MOVE BP,[POINT 3,LUDBLK(INDEX)]	;OUTPUT PPN
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	MOVE N,LUDBLK+1(INDEX)
	PUSHJ P,DECPRT
	MOVE BP,[POINT 3,LUDBLK+3(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	MOVE BP,[POINT 3,LUDBLK+4(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT	;HASHED USER NAME
	MOVE WD,LUDBLK+2(INDEX)
	ANDI WD,177
	SUBI WD,5
	MOVEM WD,N1
	PUSH P,INDEX
	JRST NXTST1
NXTSTR:	PUSHJ P,CRLF
	MOVEI CH,11
	MOVEI N,5
	PUSHJ P,OUCH
	SOJG N,.-1
	PUSHJ P,SPACE
	PUSHJ P,SPACE
NXTST1:	MOVE BP,[POINT 6,LUDBLK+5(INDEX)]
	MOVEI N,4
	PUSHJ P,SIXOUT	;STR NAME
	MOVE N,LUDBLK+6(INDEX)
	PUSHJ P,SPACE
	PUSH P,N1
	PUSHJ P,DECPRT	;QUOTA
	MOVE N,LUDBLK+7(INDEX)
	PUSHJ P,SPACE
	PUSHJ P,DECPRT	;QUOTA
	POP P,N1
	SUBI N1,3
	ADDI INDEX,3
	JUMPG N1,NXTSTR
	POP P,INDEX
	PUSHJ P,GETACT
	JRST NOOUT
REST1:	PUSHJ P,CRLF
	MOVE N,PCODE(INDX2)
	MOVEI A,3
	PUSHJ P,SPACE
	PUSHJ P,DECPRT+1
	MOVE N,TCODE(INDX2)
	MOVEI A,3
	PUSHJ P,SPACE
	PUSHJ P,DECPRT+1
	MOVE N,ICUSTN(INDX2)
	MOVEI A,2
	PUSHJ P,SPACE
	PUSHJ P,DECPRT+1
	MOVE N,ISLSMN(INDX2)
	PUSHJ P,SPACE
	MOVEI A,2
	PUSHJ P,DECPRT+1
	PUSHJ P,CRLF
	MOVE A,IUSE1(INDX2)
	MOVE B,IUSE2(INDX2)
	SUBI INDX2,1
	CAME A,IUSE1(INDX2)
	POPJ P,0
	CAME B,IUSE2(INDX2)
	POPJ P,0
	MOVEI C,^D49
	PUSHJ P,SPACE
	SOJG C,.-1
	JRST  REST1

NOOUT:	MOVE BP,[POINT 7,MSG3]
	PUSHJ P,MSGOUT
	POPJ P,0

MSGOUT:	ILDB CH,BP
	JUMPE CH,ENDOUT
	PUSHJ P,OUCH
	JRST MSGOUT
ENDOUT:	POPJ P,0

LISDUL:	TRO F,DULOUT
	PUSHJ P,GETLPT
	PUSHJ P,GETDUL
	MOVEI A,1
LISD5:	CAIL A,^D102
	JRST LISEND
	USETI DSK1,@A
	INPUT DSK1,DULLST
LISD1:	SETZ C,
	SKIPN B,DULBLK(C)
	AOJA A,LISD5
	JUMPG  B,LISD2
	PUSHJ P,RBLK1
	JRST LISD1
LISD2:	PUSHJ P,ENTD
	PUSHJ P,CRLF
	ADDI C,3
	JRST LISD1+1
ENTD:	MOVEI N,^D12
	MOVE BP,[POINT 3,DULBLK(C)]
	PUSHJ P,OCTOUT
	MOVE BP,[POINT 6,DULBLK+1(C)]
	PUSHJ P,SIXOUT
	TRO F,NOSPC
	PUSHJ P,SIXOUT
	POPJ P,0

TYPIT:	PUSHJ P,GETUSR
	MOVEM N,NAME
	PUSHJ P,HASHER
	PUSHJ P,GETLUD
	TRO F,NOLOK
	PUSHJ P,FINNAM
	JRST NOENTRY
	JRST NOENT1
	TRO F,TTY
	TTCALL 3,[ASCIZ/PPN AND AUN: /]
	MOVE BP,[POINT 3,LUDBLK(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	TTCALL 3,[ASCIZ/
DISTRICT NO.: /]
	MOVE N,LUDBLK+1(INDEX)
	PUSHJ P,DECPRT
	TTCALL 3,[ASCIZ/
PRIV. BITS: /]
	MOVE BP,[POINT 3,LUDBLK+3(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT	;PRIV BITS.
	TTCALL 3,[ASCIZ/
HASH OF USER NAME: /]
	MOVE BP,[POINT 3,LUDBLK+4(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	MOVE WD,LUDBLK+2(INDEX)
	ANDI WD,177
	SUBI WD,5
	MOVEM WD,N1
	PUSH P,INDEX
NXTONE:	TTCALL 3,[ASCIZ/
STRUCTURE: /]
	MOVE BP,[POINT 6,LUDBLK+5(INDEX)]
	MOVEI N,4
	PUSHJ P,SIXOUT
	PUSH P,N1
	TTCALL 3,[ASCIZ/
QUOTA IN: /]
	MOVE N,LUDBLK+6(INDEX)
	PUSHJ P,DECPRT
	TTCALL 3,[ASCIZ/.
QUOTA OUT: /]
	MOVE N,LUDBLK+7(INDEX)
	PUSHJ P,DECPRT
	POP P,N1
	MOVEI CH,"."
	PUSHJ P,OUCH
	SUBI N1,3
	ADDI INDEX,3
	JUMPG N1,NXTONE
	POP P,INDEX
	PUSH P,A
	MOVE N,LUDBLK(INDEX)
	PUSHJ P,UNSQZ
	POP P,A
	MOVEM N,NAME
	PUSHJ P,GETACT
	JRST NOACTG
RESOUT:	TTCALL 3,[ASCIZ/
PRICING CODE: /]
	MOVE N,PCODE(INDX2)
	MOVEI A,3
	PUSHJ P,DECPRT+1
OUTTRK:	TTCALL 3,[ASCIZ/
TRACKING CODE: /]
	MOVE N,TCODE(INDX2)
	MOVEI A,3
	PUSHJ P,DECPRT+1
OUTCUS:	TTCALL 3,[ASCIZ/
CUSTOMER NO.: /]
	MOVE N,ICUSTN(INDX2)
	MOVEI A,2
	PUSHJ P,DECPRT+1
OUTSLS:	TTCALL 3,[ASCIZ/
SALESMAN CODE: /]
	MOVE N,ISLSMN(INDX2)
	MOVEI A,2
	PUSHJ P,DECPRT+1
	PUSHJ P,CRLF
	JRST LISLUD

;OUTPUT SALESMAN CODE

NOENTRY:	TTCALL 3,[ASCIZ /NO SUCH USER.
/]
	JRST LISLUD
NOENT1:	TTCALL 3,[ASCIZ/	***INVALID USER.
/]
	JRST LISLUD

MSG3:	ASCIZ/
THERE IS NO ACCOUTNING ENTRY FOR THIS USER.
/
NOACTG:	MOVE BP,[POINT 7,MSG3]
	PUSHJ P,MSGOUT
	JRST LISLUD

DSKERR:	TTCALL 3,[ASCIZ/ERROR IN LUD.
/]
	CALLI EXIT

ALLOUT:	MOVEI A,1
	MOVEM A,SCODE
	PUSHJ P,SAVACS
	RELEASE DSK,
	PUSHJ P,NOTTY
	JSA 16,LISTER
	0,SCODE
	0,0
ALL1:	MOVE 16,[XWD ACBLK,0]
	BLT 16,16
	PUSHJ P,GETDSK
	JRST LISLUD

NAMOUT:	PUSHJ P,GETCUS
	MOVEI A,2
	MOVEM A,SCODE
	RELEASE DSK,
	PUSHJ P,NOTTY
	JSA 16,LISTER
	0,SCODE
	0,CUSTMR
	JRST ALL1

NOTTY:	INIT 14,0
	SIXBIT /TTY/
	0
	CALLI EXIT
	RELEASE 14,
	POPJ P,0

;RETURN IF NOT FOUND
;RETURN+1 IF NOT FOUND BUT USER WAS THERE(DELETED IN LUD)
;RETURN+2 IF FOUND
FINNAM:	USETI DSK,@N1
	INPUT DSK,LUDLST
	STATZ DSK,760000
	JRST DSKERR
TYP1:	SETZ INDEX,
	SKIPN B,LUDBLK(INDEX)
	JRST CHKDEL
	JUMPG B,TYP2
	PUSH P,C
	PUSHJ P,ROVBLK
	POP P,C
	JRST TYP1
TYP2:	CAMN N,LUDBLK+4(INDEX)
	JRST TYPEND
	ADDI INDEX,10
	JRST	TYP1+1
TYPEND:	AOS (P)
	AOS (P)
	POPJ P,0
CHKDEL:	TRZE F,NOLOK
	POPJ P,0
	MOVE N,DULBLK(C)
	PUSH P,C
	PUSHJ P,SQZIT2
	POP P,C
	MOVEM N,NAME
	PUSHJ P,GETACT
	POPJ P,0
	SKIPE ,IBCODE(INDX2)
	POPJ P,0
	SETZM ,IUSE1(INDX2)
	AOS (P)
	POPJ P,0

TYPPN:	TRO F,TTY
	PUSHJ P,GETPPN
	PUSHJ P,GETDUL
	PUSHJ P,FINPPN
	JRST	NOENTRY
	TTCALL 3,[ASCIZ /USER NAME: /]
	MOVE BP,[POINT 6,DULBLK+1(C)]
	PUSHJ P,SIXOUT
	TRO F,NOSPC
	PUSHJ P,SIXOUT
	MOVE N,DULBLK+1(C)
	MOVE N1,DULBLK+2(C)
	PUSHJ P,CRLF
	JRST TYPIT+2

GETUSR:	TTCALL 3,[ASCIZ /USER: /]
	SETZB N,N1
	MOVE BP,[POINT 6,N]
	MOVEI N2,^D12
	TTCALL 4,CH
	CAIN CH,15
	JRST GOTUSR
	SUBI CH,40
	IDPB CH,BP
	SOJG N2,.-5
GOTUSR:	TTCALL 11,0
	POPJ P,0

GETPPN:	TTCALL 3,[ASCIZ /PPN: /]
	SETZ A,
	MOVEI N2,6
	MOVE BP,[POINT 3,A,17]
	PUSHJ P,GETCHR
	JRST GOTSEC
	JRST FIRSTIN
	IDPB CH,BP
	SOJG N2,.-4
	PUSHJ P,GETCHR
	JRST GOTSEC
	SKIPA
	JRST .-3
FIRSTIN: JUMPE N2,SECOND
	LSH A,-3
	SOJG N2,.-1
SECOND:	HRLZM A,N
	SETZ A,
	MOVEI N2,6
	MOVE BP,[POINT 3,A,17]
	PUSHJ P,GETCHR
	JRST GOTSEC
	JRST BADCHR
	IDPB CH,BP
	SOJG N2,.-4
	PUSHJ P,GETCHR
	JRST GOTSEC
	JRST BADCHR
	JRST .-3
GOTSEC:	JUMPE N2,.+3
	LSH A,-3
	SOJG N2,.-1
	HRRM A,N
	TTCALL 11,0
	POPJ P,0

GETCHR:	TTCALL 4,CH
	CAIN CH,15
	POPJ P,0
	CAIE CH,54
	JRST .+3
	AOS (P)
	POPJ P,0
	SUBI CH,60
	CAILE CH,7
	JRST BADCHR
	JUMPL CH,BADCHR
	AOS (P)
	AOS (P)
	POPJ P,0

GETCUS:	TTCALL 3,[ASCIZ/CUSTOMER NUMBER: /]
	SETZB A,CUSTMR
	MOVEI N2,4
	MOVEI N,^D1000
NXTCH1:	TTCALL 4,CH
	CAIN CH,15
	JRST GOTCUS
	SUBI CH,60
	CAILE CH,11
	JRST BADCH1
	JUMPL CH,BADCH1
	IMUL CH,N
	ADD A,CH
	IDIVI N,^D10
	SOJG N2,NXTCH1
GOTCUS:	JUMPE N2,.+3
	IDIVI A,^D10
	SOJG N2,.-1
	MOVEM A,CUSTMR
	TTCALL 11,0
	POPJ P,0

BADCH1:	TTCALL 3,[ASCIZ /ILLEGAL CHARACTER.
/]
	TTCALL 11,0
	JRST GETCUS

BADCHR:	TTCALL 3,[ASCIZ /ILLEGAL CHARACTER.
/]
	TTCALL 11,0
	JRST LSTBUF

FINPPN:	MOVE D,N
	IDIVI N,^D101
	ADDI N1,1
	USETI DSK1,@N1
	INPUT DSK1,DULLST
	SETZ C,
PPN1:	SKIPN ,DULBLK(C)
	POPJ P,0
	SKIPG ,DULBLK(C)
	JRST PPN2
	CAMN D,DULBLK(C)
	JRST	PPNEND
	ADDI C,3
	CAIGE C,200
	JRST PPN1
	POPJ P,0
PPN2:	PUSHJ P,RBLK1
	JRST PPN1-1
PPNEND:	MOVE N1,DULBLK+1(C)
	MOVEM N1,NAME
	MOVE N1,DULBLK+2(C)
	MOVEM N1,NAME+1
	AOS (P)
	POPJ P,0

CHKIT:	TRO F,TTY
	PUSHJ P,GETLUD
	PUSHJ P,GETDUL
CHKLUD:	TTCALL 3,[ASCIZ/CHECKING LUD AND USERS.
/]
	MOVEI A,1
NXTBLK:	CAIL A,^D888
	JRST CHKDUL
	USETI DSK,@A
	INPUT DSK,LUDLST
	STATZ DSK,760000
	JRST DSKERR
LUD1:	SETZ INDEX,
	SKIPN B,LUDBLK(INDEX)
	AOJA A,NXTBLK
	JUMPG B,LUD2
	PUSHJ P,ROVBLK
	JRST LUD1
LUD2:	SKIPN ,LUDBLK+4(INDEX)
	JRST NXTLUD
	MOVE N,LUDBLK(INDEX)
	PUSHJ P,FINPPN
	JRST NODUL
	MOVE N,DULBLK+1(C)
	MOVE N1,DULBLK+2(C)
	PUSH P,C
	PUSH P,A
	PUSHJ P,HASHER
	POP P,A
	POP P,C
	CAME N1,A
	JRST NODUL1
	CAME N,LUDBLK+4(INDEX)
	JRST NODUL2
	MOVE N,LUDBLK(INDEX)
	PUSH P,C
	PUSHJ P,SQZIT2
	POP P,C
	MOVEM N,NAME
	PUSHJ P,GETACT
	JRST NOACT3
	SETZM ,IUSE1(INDX2)
NXTLUD:	LDB C,NENTP
	ADD INDEX,C
	JRST LUD1+1

NODUL:	TTCALL 3,[ASCIZ /NO ENTRY IN DUL FOR /]
	MOVE BP,[POINT 3,LUDBLK(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
	PUSHJ P,CRLF
	JRST NXTLUD

NODUL1:	TTCALL 3,[ASCIZ /HASH LOC OF DUL NAME =/]
	MOVE BP,[POINT 3,N1,17]
	MOVEI N,6
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
	TTCALL 3,[ASCIZ /LUD BLK NO. =/]
	MOVEI N,6
	MOVE BP,[POINT 3,A,17]
	MOVEI N,6
	PUSHJ P,OCTOUT
DONMSG:	PUSHJ P,CRLF
	PUSHJ P,CRLF
	JRST NXTLUD

NODUL2:	TTCALL 3,[ASCIZ /HASH OF DUL NAME =/]
	MOVE N1,N
	MOVE BP,[POINT 3,N1]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
	TTCALL 3,[ASCIZ /ENTRY IN LUD =/]
	MOVE BP,[POINT 3,LUDBLK+4(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	JRST DONMSG

NOACT3:	TTCALL 3,[ASCIZ/ THERE IS NO ACCOUNTING ENTRY FOR/]
	MOVE BP,[POINT 6,DULBLK+1(C)]
	PUSHJ P,SIXOUT
	TRO F,NOSPC
	PUSHJ P,SIXOUT
	TTCALL 3,[ASCIZ/(/]
	MOVE BP,[POINT 3,LUDBLK(INDEX)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT+1
	TTCALL 3,[ASCIZ/)
/]
	JRST NXTLUD

BADDUL:	TTCALL 3,[ASCIZ /PPN /]
	MOVE BP,[POINT 3,DULBLK(C)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	TTCALL 3,[ASCIZ / IS IN BLOCK /]
	MOVE BP,[POINT 3,A,17]
	MOVEI N,6
	PUSHJ P,OCTOUT
	TTCALL 3,[ASCIZ /.
(IT SHOULD BE IN BLOCK /]
	MOVE BP,[POINT 3,N1,17]
	MOVEI N,6
	PUSHJ P,OCTOUT
	TTCALL 3,[ASCIZ /.)
/]
	PUSHJ P,CRLF
	JRST NXTDUL

BAD2:	TTCALL 3,[ASCIZ/NO ENTRY IN LUD FOR /]
	MOVE BP,[POINT 6,DULBLK+1(C)]
	PUSHJ P,SIXOUT
	TRO F,NOSPC
	PUSHJ P,SIXOUT
	PUSHJ P,CRLF
	JRST NXTDUL

BAD3:	TTCALL 3,[ASCIZ /DUL PPN =/]
	MOVE BP,[POINT 3,DULBLK(C)]
	MOVEI N,^D12
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
	TTCALL 3,[ASCIZ /LUD PPN =/]
	MOVEI N,^D12
	MOVE BP,[POINT 3,LUDBLK(INDEX)]
	PUSHJ P,OCTOUT
	PUSHJ P,CRLF
	PUSHJ P,CRLF
	JRST NXTDUL
CHKDUL:	TTCALL 3,[ASCIZ/CHECKING DUL.
/]
	MOVEI A,1
	CAIL A,^D102
	JRST ENDLUD
	USETI DSK1,@A
	INPUT DSK1,DULLST
DUL1:	SETZ C,
	SKIPN B,DULBLK(C)
	AOJA A,CHKDUL+2
	JUMPG B,.+3
	PUSHJ P,RBLK1
	JRST DUL1
	MOVE N,DULBLK(C)
	IDIVI N,^D101
	ADDI N1,1
	CAME N1,A
	JRST BADDUL
DUL2:	MOVE N,DULBLK+1(C)
	MOVE N1,DULBLK+2(C)
	PUSH P,C
	PUSH P,A
	PUSHJ P,HASHER
	POP P,A
	POP P,C
	PUSHJ P,FINNAM
	JRST BAD2	;NOT FOUND
	JRST NXTDUL	;HAS BEEN DELETED

	MOVE N,DULBLK(C)
	CAME N,LUDBLK(INDEX)
	JRST BAD3
NXTDUL:	ADDI C,3
	JRST DUL1+1

ENDLUD:	TLZ F,400000
	MOVE INDEX,MAX
	SUBI INDEX,1
	SKIPE ,IUSE1(INDEX)
	PUSHJ P,USET
	SOJG INDEX,.-2
	TTCALL 3,[ASCIZ/DONE CHECKING.
/]
	CALLI 1,EXIT

USET:	TLON	F,400000
	TTCALL 3,[ASCIZ/
THE FOLLOWING USERS ARE IN USERS.DAT
BUT NOT IN THE LUD:
/]
	PUSHJ P,SPACE
	MOVE BP,[POINT 7,IUSE1(INDEX)]
	ILDB CH,BP
	PUSHJ P,OUCH
	ILDB CH,BP
	PUSHJ P,OUCH
	ILDB CH,BP
	PUSHJ P,OUCH
	MOVE BP,[POINT 7,IUSE2(INDEX)]
	ILDB CH,BP
	PUSHJ P,OUCH
	ILDB	CH,BP
	PUSHJ P,OUCH
	ILDB CH,BP
	PUSHJ P,OUCH
	PUSHJ P,CRLF
	POPJ P,0

GETACT:	PUSH P,A
	PUSHJ P,SIXASC
	POP P,A
	MOVE INDX2,MAX
	MOVE B,UNAME
AGN:	CAMN B,IUSE1(INDX2)
	JRST AGN1
	SOJGE INDX2,AGN
	POPJ P,0
AGN1:	MOVE D,UNAME+1
	CAME D,IUSE2(INDX2)
	JRST .-4
	AOS (P)
	POPJ P,0

SIXASC:	MOVE A,[POINT 6,NAME] ;CHANGE SIXBIT NAME IN NAME TO
	MOVE B,[POINT 7,UNAME]	;ASCII IN UNAME,UNAME+1
	MOVEI N,6
SIX2:	ILDB CH,A
	ADDI CH,40
	IDPB CH,B
	SOJE N,DON
	CAIE N,3
	JRST SIX2
	PUSHJ P,ADD2
	JRST SIX2
DON:	PUSHJ P,ADD2
	POPJ P,0
ADD2:	MOVEI CH," "
	IDPB CH,B
	IDPB CH,B
	POPJ P,0

GETLPT:	INIT LPT,1
	SIXBIT /LPT/
	LPTO,,0
	JRST NOLPT
	OUTBUF LPT,2
	MOVE A,[SIXBIT/LUD   /]
	TRZE F,DULOUT
	MOVE A,[SIXBIT/DUL/]
	HRLZI B,(SIXBIT/LST/)
	SETZB C,D
	ENTER LPT,A
	CALLI EXIT
	POPJ P,0

NOLPT:	TTCALL 3,[ASCIZ /LPT NOT AVAILABLE.
/]
	JRST LISLUD

OUCH:	TRNE F,TTY
	JRST TOUCH
	SOSG ,LPTO+2
	OUTPUT LPT,0
	IDPB CH,LPTO+1
	POPJ P,0

TOUCH:	TTCALL 1,CH
	POPJ P,0

RBLK1:	HRRZ C,DULBLK(C)
	USETI DSK1,@C
	INPUT DSK1,DULLST
	POPJ P,0

ROVBLK:	HRRZ C,LUDBLK(INDEX)
	USETI DSK,@C
	INPUT DSK,LUDLST
	POPJ P,0

GETDUL:	INIT DSK1,17
	SIXBIT /SYS/
	0
	CALLI EXIT
	MOVE A,[SIXBIT/DUL/]
	HRLZI B,(SIXBIT/SYS/)
	SETZB C,D
	LOOKUP DSK1,A
	SKIPA
	POPJ P,0
	TTCALL 3,[ASCIZ/CAN'T ACCESS DUL.
/]
	JRST LISLUD

GETLUD:	MOVE A,[SIXBIT /LUD/]
	HRLZI	B,(SIXBIT/SYS/)
	SETZB C,D
	LOOKUP DSK,A
	JRST DSKERR
	POPJ P,0


HEADOU:	MOVE BP,[POINT 7,HEADING]
	ILDB CH,BP
	JUMPE CH,.+3
	PUSHJ P,OUCH
	JRST HEADOUT+1
	POPJ P,0

SIXOUT:	MOVEI N,6
	TRZN F,NOSPC
	PUSHJ P,SPACE
	ILDB CH,BP
	ADDI CH,40
	PUSHJ P,OUCH
	SOJG N,SIXOUT+3
	POPJ P,0

DECPRT:	SETZ A,
	MOVEI D,12
	CAIG N,0
	JRST ZEROUT
CHECKD:	CAMLE N,TABD(A)
	JRST RDXPRT
	PUSHJ P,SPACE
	ADDI A,1
	JRST CHECKD
RDXPRT:	MOVMS N
	IDIVI N,(D)
	HRLM N1,0(P)
	SKIPN N
	PUSHJ P,SPC
	PUSHJ P,RDXPRT
	HLRZ CH,0(P)
	ADDI CH,"0"
	JRST OUCH
ZEROUT:	MOVEI N,6
	PUSHJ P,SPACE
	SOJG N,.-1
	MOVEI CH,"0"
	PUSHJ P,OUCH
	POPJ P,0

OCTOUT:	PUSHJ P,SPACE
	MOVE B,N
OCT2:	ILDB CH,BP
	JUMPN CH,OCT3
	TRNE F,LEAD
	SOJA N,OCT2
OCT3:	TRZ F,LEAD
	ADDI CH,60
	PUSHJ P,OUCH
	CAIN N,7
	JRST .+3
	SOJG N,OCT2
	POPJ P,0
	CAIG B,6
	SOJA N,OCT2
	MOVEI CH,54
	PUSHJ P,OUCH
	SOJA N,OCT2

CRLF:	MOVEI CH,15
	PUSHJ P,OUCH
	MOVEI CH,12
	PUSHJ P,OUCH
	POPJ P,0

SPC:	PUSHJ P,SPACE
	AOS (P)
	POPJ P,0

SPACE:	TRNE F,TTY
	JRST TSPACE
	MOVEI CH,40
	PUSHJ P,OUCH
	POPJ P,0

TSPACE:	TTCALL 3,[ASCIZ / /]
	POPJ P,0

SQZIT2:	PUSHJ P,.+1
	MOVE B,N
	MOVEI N1,3
	HRRZ C,B
SQZTS0:	IDIVI C,50
	EXCH C,D
	CAIL C,13
	ADDI C,41-13-20
	ADDI C,20
	CAIN C,32
	MOVEI C,0
	ROTC B,-6
	MOVE C,D
	SOJG N1,SQZTS0
	MOVE N,B
	POPJ P,0

HASHER:	MOVEI B,0
	MOVE C,[555555555555]
	MOVE D,[361275431652]
	MOVE E,[612754316523]
	JSR RND
	JSR RND
	JSR RND
	JSR RND
	XOR E,D
	MOVE N,E
	TLZ N,400000
	IDIVI N,^D887
	MOVE N,E
	XOR N,C
	ADDI N1,1
	POPJ P,0
RND:	0
	ADD D,N
	ROTC N,-22
	MOVEI A,5
RND1:	MOVE N2,C+1(B)
	MUL N2,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
	ADDM N3,D+1(B)
	AOJE B,RND2
	MOVNI B,1
	TRNE D,1
	SKIPL E
	MOVEI B,0
	EXCH C,E
RND2:	SOJG A,RND1
	JRST @RND
	POPJ P,0

UNSQZ:	PUSHJ P,.+1
	MOVE A,N
	MOVEI N1,3
	HRRZ B,A
SQZTSO:	IDIVI B,50
	EXCH B,C
	CAIL B,13
	ADDI B,41-13-20
	ADDI B,20
	CAIN B,32
	MOVEI B,0
	ROTC A,-6
	MOVE B,C
	SOJG N1,SQZTSO
	MOVE N,A
	POPJ P,0

LPTO:	BLOCK 3
	0
DULLST:	IOWD 200,DULBLK
	0
DULBLK:	BLOCK 200
LUDLST:	IOWD 200,LUDBLK
	0
LUDBLK:	BLOCK 200
PDP:	XWD	-20,.
	BLOCK 21

HEADING:	ASCIZ/
 PPN(& AUN)      DIST    P-BITS         HASH      STR       Q-IN   Q-OUT
  PCD  TCD  CUST  SLSMN


/

NAME:	BLOCK 2

UNAME:	BLOCK 2
MAX:	0
SCODE:	0
CUSTMR:	0
TABD:	^D99999	;A=0(6 PLACES)
	^D9999	;A=1(5 PLACES)
	^D999	;A=2(4 PLACES)
	^D99	;A=3(3 PLACES)
	^D9	;A=4(2 PLACES)
	0	;A=5(1 PLACE)
ACBLK:	BLOCK 17
NENTP:	POINT 7,LUDBLK+2(INDEX),35
	END LSTBUF
