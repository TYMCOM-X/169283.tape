MODULE TILER(DOLLAR,ENTRIES=($TILER,$TPRIV,$TCNIV),
  FSAVE,TIMING,TIMER=EXTERNAL(SIX12))=BEGIN
%VERSION B1%
%LAST UPDATE, APR. 3, 1975, BY V.E.VAN VLEAR%
REQUIRE DVDEFS.BLI[7,107355];
REQUIRE RDEFS.BLI[7,107355];
REQUIRE CDEFS.BLI[7,107355];
REQUIRE KDEFS.BLI[7,107355];
REQUIRE UDEFS.BLI[7,107355];

UNDECLARE $TILER, $TPRIV, $TCNIV;

%  THIS COLLECTION OF ROUTINES HANDLES THE FUNCTIONS OF PUTTING THINGS
   ON THE INVALIDS FILES, VIZ, THE RECORD, AND DIAGNOSTIC MESSAGES.
%
MAP SC$ RSRC$;


GLOBAL ROUTINE $TCNIV(FI, QP, FP)=
BEGIN

LOCAL KFCB$ INVFCB;

INVFCB := .RSRC$[SC$LFCB];
$TCNVL(.FI, .QP, .FP, .INVFCB[K$ODADR])

END;


GLOBAL ROUTINE $TPRIV(QP, STADR)=
BEGIN

LOCAL KFCB$ INVFCB;
MAP DDW$I QP;

INVFCB := .RSRC$[SC$LFCB];
$TPRSL(.QP[DD$QP], (.STADR)<36,7>, .INVFCB[K$ODADR]);
END;


%WRITE ERROR MESSAGE%
GLOBAL ROUTINE $TILER(ET,DFI,VSW)=
BEGIN

MAP DFI$ DFI;

LABEL IVBLK;

LOCAL
  PTR,                  %Temp. string Pointer%
  NM,                   %Adr. of field name%
  CLINE$ LCB,           %LINE CONTROL BLOCK%
  KFCB$ FCB,            %FILE CONTROL BLOCK%
  KFCB$ IFCB,           %Invalids FCB address%
  NCH;                  %No. of chars. in field name%
MAP SC$ RSRC$;

$UAPSZ(.ET);
IF .DFI NEQ 0 THEN
  BEGIN
  $UAPSC((PLIT ASCIZ ' for "')<36,7>,6);
  NM:=.DFI[DD$NAME];
  PTR:=@(.NM-1);
  NCH:=.(.PTR)<0,18>;              %NO OF CHARS. IN FIELD NAME%
  $UAPSC((.NM)<36,7>,.NCH);
  $UOCHR("""")     %FOR FULL QUOTE%
  END;

FCB:=.RSRC$[SC$FCB];
IF NOT .FCB[K$TERMI] THEN
  BEGIN
  LCB:=.RSRC$[SC$LCB];
  $UAPSC((PLIT ASCIZ ' in record # ')<36,7>,13);
  $UAPN(.LCB[C$SEQNO],10,1)
  END;

IF .VSW THEN
  %PRINT VALUE OF VARIABLE%
  BEGIN
  $UOCHR(":");
  $UOCHR(" ");
  IF .DFI[DD$TYPE] EQL DDV$STR THEN
    $TPRSL(.DFI[DD$QP],(.DFI)<36,7>, .UL$)
  ELSE
    $TCNVL(.DFI, .DFI[DD$QP]<0,36>,.DFI[DD$FP],.UL$)
  END;

$UOUTL();

IF .RSRC$[SC$IFNAME] NEQ SCV$NULL THEN
  IVBLK: BEGIN                   %START OF INVALIDS MSG. TYPE%

  IF .RSRC$[SC$LFCB] EQL SCV$NULL THEN
     $TOPIV(.RSRC$);               %OPEN INVALID FILE%
  IFCB := .RSRC$[SC$LFCB];           %GET INVALID F.C.B.%
  IF .FCB[K$TERMI] THEN
    IF .IFCB[K$TERMI] THEN
      LEAVE IVBLK;               %DONT WRITE INVALIDS IF TERMINAL %
  LCB := .RSRC$[SC$LCB];             %GET LINE CONTROL BLOCK ADR.%
  %COPY CURRENT SOURCE LINE TO INVALIDS%
  $TPRIV(.LCB[C$LCBCNT], .LCB[C$BPTR]);
  $TPRIV(2, $USPTR(': '));

  % NOW THE DIAGNOSTIC MESSAGE %
  $TPRIV($ULENP(.ET), .ET);
  IF .DFI NEQ 0            % PRINT FIELD NAME IF WE HAVE IT %
     THEN BEGIN
        $TPRIV(6, $USPTR(' for "'));
        $TPRIV(.NCH, (.NM)<36,7>);
        $TPRIV(1, $USPTR('"'))
     END;

  IF .VSW THEN
    %PRINT VALUE OF VARIABLE%
    BEGIN
    $TPRIV(2, $USPTR(': '));
    IF .DFI[DD$TYPE] EQL DDV$STR THEN
      $TPRIV(.DFI[DD$QP], .DFI)     %PRINT CHARACTER FIELD%
    ELSE
      $TCNIV(.DFI,.DFI[DD$QP]<0,36>, .DFI[DD$FP])
    END;
  $KWRITE(IFCB)
  END


END;
END ELUDOM
