TITLE	PUTCPY FOR COBOL 5(61)		
SUBTTL	WRITE OUT A CPYFIL CHARACTER	AL BLACKINGTON/CAM

;COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORP, MAYNARD, MA

TWOSEG
RELOC	400000

ENTRY PUTCPY,PUTFEL,PUTCIF
EXTERNAL DEVDED

PUTCIF:	TSWF	FCOPY		;ARE WE COPYING?
	POPJ	PP,		;YES--DON'T WRITE


PUTCPY:	AOS	CP,SAVECP

	CAIE	CH,12		;END OF LINE?
	CAIN	CH,14
	JRST	PUTEOL		;YES

	CAILE	CP,CPMAXN	;ARE WE BEYOND PRINT PAGE?
	POPJ	PP,		;YES--RETURN

	CAIG	CP,6		;ARE WE IN SEQUENCE NUMBER FIELD?
	JRST	PUTSEQ		;YES

	CAIN	CH,11		;IS THIS A TAB?
	PUSHJ	PP,BMPCP	;YES

	CAIN	CP,7
	JRST	PUTCON

PUTCP0:	TSWF	FNOCPY		;ANY NEED FOR THE FILE?
	POPJ	PP,		;NO--RETURN
	SOSG	CPYBHO+2
	JRST	PUTCP2
PUTCP1:	IDPB	CH,CPYBHO+1
	POPJ	PP,

PUTCP2:	OUT	CPY,
	JRST	PUTCP1

	MOVEI	CH,CPYDEV
	JRST	DEVDED
;PUT SOMETHING INTO COLUMN 7

PUTCON:	CAIE	CH," "
	CAIN	CH,11
	JRST	PUTCP0
	CAIE	CH,"-"
	CAIN	CH,"*"
	JRST	PUTCP0

	PUSH	PP,CH
	MOVEI	CH," "
	PUSHJ	PP,PUTCP0
	MOVEI	CH,10
	MOVEM	CH,SAVECP
	POP	PP,CH
	JRST	PUTCP0


;PUT A CHARACTER INTO SEQUENCE NUMBER

PUTSEQ:	CAILE	CH,137
	JRST	PUTSQ2
	CAIGE	CH," "
	JRST	PUTSQ3
	JRST	PUTCP0

PUTSQ2:	CAIG	CH,"Z"+40
	CAIGE	CH,"A"+40
	SKIPA
	JRST	PUTCP0

PUTSQ3:	PUSH	PP,CH
	MOVEI	CH," "
	PUSHJ	PP,PUTCP0
	POP	PP,CH
	POPJ	PP,
;END-OF-LINE CHARACTER TO BE PUT OUT. START A NEW LINE.

PUTEOL:	TSWF	FNOCPY;
	JRST	PUTFL2

	PUSH	PP,CH
	SKIPN	SAVBLN
	MOVEM	CP,SAVBCP
	MOVE	LN,SAVELN
	SKIPN	SAVBLN
	MOVEM	LN,SAVBLN

PUTEL1:	MOVE	CH,CPYBHO+1	;IS WORD FINISHED?
	TLNN	CH,760000
	JRST	PUTEL2		;YES

	MOVEI	CH,0		;NO--PUT OUT A NULL
	PUSHJ	PP,PUTCP0
	JRST	PUTEL1

PUTEL2:	POP	PP,CH

PUTFEL:	PUSHJ	PP,PUTCP0	;PUT OUT E-O-L CHARACTER

	AOS	LN,SAVELN	;GET NEW LINE NUMBER
	LDB	CH,[POINT 7,LN,28]	;PUT OUT FIRST HALF OF LINE NUMBER
	TSWF	FRLIB		;IF THIS IS A LIBRARY FILE,
	IORI	CH,100		;  SET HIGH BIT
	PUSHJ	PP,PUTCP0
	MOVE	CH,LN		;PUT OUT OTHER HALF OF LINE NUMBER
	PUSHJ	PP,PUTCP0
	MOVEI	CH,1		;SET "LINE-NUMBER WORD" FLAG
	IORM	CH,@CPYBHO+1

PUTFL2:	TSWT	FRLIB		;READING LIBRARY?
	JRST	PUTFL3		;NO
	MOVEI	LN,6		;YES
	MOVEI	CH," "
	PUSHJ	PP,PUTCP0
	SOJG	LN,.-1
	MOVEI	CP,6
	JRST	PUTFL4

PUTFL3:	MOVEI	CP,0
PUTFL4:	MOVEM	CP,SAVECP
	MOVE	LN,SAVELN

	SETZM	EOLKAR
	CAIG	LN,17774	;TOO MANY LINES?
	POPJ	PP,		;NO--RETURN

	TTCALL	3,[ASCIZ "?SOURCE PROGRAM TOO LONG
"]
	SWON	FEOF!FFATAL;
	POPJ	PP,
;TAB BEING PUT OUT--RESET SAVECP

BMPCP:	MOVE	CH,SAVECP
	ADDI	CH,4
	ANDCMI	CH,7
	ADDI	CH,3
	MOVEM	CH,SAVECP

	MOVEI	CH,11
	POPJ	PP,


EXTERNAL CPYBHO,CPYDEV,CPMAXN
EXTERNAL SAVECP,SAVELN,SAVBLN,SAVBCP,EOLKAR

	END
