TITLE FLASH - USER PROGRAM TO READ AND STORE NEWSWIRE - V004
SUBTTL R CLEMENTS - 24 AUG 70
VFLASH==4

;AC'S
F=0
A=1
B=2
C=3
D=4
N=5
N1=6
T=7
T1=10
T2=11
CH=12
IC=13
P=17

;FLAGS
L.TYP==1	;TYPE AS YOU READ
L.ASC==2	;WRITE ASCII DISK FILE
L.BIN==4	;WRITE IMAGE DISK FILE
L.NXTA==10	;INDEX ASCII FILE NUMBER
L.NXTB==20	;INDEX BIN FILE NUMBER
L.ASCB==40	;WRITING ASCII
L.BINB==100	;WRITING BIN
L.IDL==200	;IDLING INPUT (LAST IN WAS EOF)
L.CAST==400	;CASE FOR IC FOR TTY
L.CASA==1000	;CASE FOR IC,FOR ASC FILE
L.NOF==2000	;MINUS TYPED
L.36==4000	;INPUT DEVICE IS 36 BIT IMAGE, NOT 8
L.DIRI==10000	;INPUT DEV HAS A DIRECTORY
L.OCT==20000	;OCTAL DUMP OF SPACE CHARACTERS

S=400000	;PARAMETER
BLKSPF==40		;BLOCKS PER FILE

EXTERN JOBSA,JOBREN,JOBFF,JOBREL

;I/O CHANNELS

TT==0
UP==1
DA==2
DB==3

LOC 137
EXP VFLASH
RELOC
FLASH:	MOVSI F,L.TYP		;INITIAL FLAG SETTINGS
	SETZM NCHARS
	SETZM CWP
	SETZM CAP
	SETZM CBP
	MOVE P,PDP
	PUSHJ P,CLRDA
	PUSHJ P,CLRDB		;CLEAR BUFFERS FOR OUTFILES
	SETZM BINFNO
	SETZM ASCFNO
	SETZM INPFNO

REE:	MOVEI T,REE
	MOVEM T,JOBREN
	TLZE F,L.ASCB
	PUSHJ P,CLSDA
	TLZE F,L.BINB
	PUSHJ P,CLSDB
	RELEAS DA,0
	RELEAS DB,0

	CALLI 0
	MOVE P,PDP
	MOVEI A,200
	MOVSI B,(SIXBIT /TTY/)
	MOVE C,[XWD TYOHED,TYIHED]
	OPEN TT,A
	  CALLI 12

	MOVE T,UPIDEV
	CALLI T,4
	TLNE T,40
	TRNN T,400000
	JRST NOTASG
	TLNN T,210
	TLO F,L.36
	TLNE T,4
	TLO F,L.DIRI
	MOVE T,JOBFF
	MOVEM T,SJFF
	PUSHJ P,FREEA
	PUSHJ P,FREEB
REUPI:	MOVEI A,210
	MOVE B,UPIDEV
	MOVEI C,UPIHED
	OPEN UP,A
	  JRST OPNUPX

	TLNN F,L.DIRI
	JRST MAINLP
	MOVE N,INPFNO
	PUSHJ P,NUMEXT
	MOVEM B,INPEXT
	MOVE A,[SIXBIT /UPITXT/]
	SETZB C,D
	LOOKUP UP,A
	  JRST OPNUPY

MAINLP:	TTCALL 2,CH		;ANY COMMAND CHARS?
	  JRST RDUPI		;NO. GO READ NEWS
	CAIG CH,172
	CAIG CH,140
	SKIPA
	TRZ CH,40
	MOVSI A,-DISPL
CMDLP:	HLRZ B,DISP(A)
	HRRZ C,DISP(A)
	CAIN CH,0(B)
	JRST 0(C)
	AOBJN A,CMDLP
COMRET:	TLZ F,L.NOF
	JRST MAINLP

DISP:	XWD "-",MINUS
	XWD "A",ASCON
	XWD "I",BINON
	XWD "T",TYPON
	XWD "N",NEWON
	XWD "O",OCTON
	XWD "X",EXIT
	XWD "L",LIST
	XWD "H",HELP
	XWD "?",HELP
	XWD 32,EXIT
DISPL=.-DISP
;MAIN LOOP IF NO COMMAND TYI

RDUPI:	SOSLE UPIHED+2
	JRST RDUPOK
	TLZ F,L.IDL
	MOVEI T,6
	TLNE F,L.36
	DPB T,[POINT 6,UPIHED+1,11]
	INPUT UP,0
	STATO UP,760000
	JRST RDUPOK
	STATO UP,740000
	JRST RDUPEF
	MOVEI A,[ASCIZ /
?INPUT ERROR
/]
	PUSHJ P,MSG
	JRST REE

RDUPEF:	TLO F,L.IDL
	CLOSE UP,0
	MOVE T,SJFF
	MOVEM T,JOBFF
	AOS T,INPFNO
	CAILE T,^D999
	JRST EXIT
	JRST REUPI

RDUPOK:	AOS NCHARS
	ILDB IC,UPIHED+1
	ANDI IC,77
	SKIPE A,CWP
	CAMN A,[POINT 6,COREND,35]
	MOVE A,[POINT 6,CORE]
	IDPB IC,A
	MOVEM A,CWP
	CAIN IC,33
	TLO F,L.CAST
	CAIE IC,37
	CAIN IC,77
	TLZ F,L.CAST
TYPQ:	TLNN F,L.TYP
	JRST ASCQ
	HLRE CH,XTAB(IC)
	TLNE F,L.CAST
	HRRE CH,XTAB(IC)
	JUMPG CH,TYPA
	JUMPE CH,ASCQ
	ANDI CH,77777
	MOVEI A,LT(CH)
	PUSHJ P,MSG
	JRST ASCQ

TYPA:	PUSHJ P,TYO

	JRST ASCQ
ASCQ:	TLNE F,L.ASC
	JRST ASC1
	TLZE F,L.ASCB
	PUSHJ P,CLSDA
	JRST BINQ

ASC1:	TLZN F,L.NXTA
	JRST ASC1A
	TLNE F,L.ASCB
	PUSHJ P,CLSDA
	PUSHJ P,FREEA
ASC1A:	TLNN F,L.ASCB
	PUSHJ P,INITA
ASC2:	SKIPE A,CAP
	CAMN A,[POINT 6,COREND,35]
	MOVE A,[POINT 6,CORE]
	ILDB IC,A
	MOVEM A,CAP
	CAIN IC,33
	TLO F,L.CASA
	CAIE IC,37
	CAIN IC,77
	TLZ F,L.CASA
	HLRE CH,XTAB(IC)
	TLNE F,L.CASA
	HRRE CH,XTAB(IC)
	CAIN CH," "
	JRST ASCS
	JUMPG CH,ASCA
	JUMPE CH,ASCN
	ANDI CH,77777
	MOVEI A,LT(CH)
	PUSHJ P,ASCMSG
	JRST ASCN
ASCA:	PUSHJ P,ASCTYO
ASCN:	MOVE A,CAP
	CAME A,CWP
	JRST ASC2
	JRST BINQ

ASCS:	TLNN F,L.OCT
	JRST ASCA
	MOVEI CH,"<"
	PUSHJ P,ASCTYO
	MOVEI CH,40(IC)
	PUSHJ P,ASCTYO
	MOVEI CH,">"
	JRST ASCA
INITA:	SETZM ASCBLK
	PUSHJ P,CLRDA
	TLZ F,L.NXTA
	TLO F,L.ASCB
	POPJ P,0

NUMEXT:	MOVEI B,0
	MOVE T,[POINT 6,B]
NUMEX1:	IDIVI N,12
	HRLM N1,0(P)
	SKIPE N
	PUSHJ P,NUMEX1
	HLRZ T1,0(P)
	ADDI T1,20
	IDPB T1,T
	POPJ P,0

CLSDA:	SKIPE CAP2
	PUSHJ P,WRTDA
	CLOSE DA,0
	TLZ F,L.ASCB
	POPJ P,0

ASCMSG:	HRLI A,440700
ASCMS1:	ILDB CH,A
	JUMPE CH,CPOPJ
	PUSHJ P,ASCTYO
	JRST ASCMS1

ASCTYO:	SKIPN T,CAP2
	MOVE T,[POINT 7,ABUF]
	IDPB CH,T
	MOVEM T,CAP2
	CAME T,[POINT 7,ABUFE,34]
	POPJ P,0
	PUSH P,A
	PUSH P,T
	PUSHJ P,WRTDA
	MOVE T,ASCBLK
	CAIL T,BLKSPF-1
	PUSHJ P,SELA
	POP P,T
	POP P,A
CPOPJ:	POPJ P,0
BINQ:	TLNE F,L.BIN
	JRST BIN1
	TLZE F,L.BINB
	PUSHJ P,CLSDB
	JRST MAINLP

BIN1:	TLZN F,L.NXTB
	JRST BIN1A
	TLNE F,L.BINB
	PUSHJ P,CLSDB
	PUSHJ P,FREEB
BIN1A:	TLNN F,L.BINB
	PUSHJ P,INITB
BIN2:	SKIPE A,CBP
	CAMN A,[POINT 6,COREND,35]
	MOVE A,[POINT 6,CORE]
	ILDB IC,A
	MOVEM A,CBP
	PUSHJ P,BINTYO
	MOVE A,CBP
	CAME A,CWP
	JRST BIN2
	JRST MAINLP

INITB:	SETZM BINBLK
	PUSHJ P,CLRDB
	TLO F,L.BINB
	TLZ F,L.NXTB
	POPJ P,0

CLSDB:	SKIPE CBP2
	PUSHJ P,WRTDB
	CLOSE DB,0
	TLZ F,L.BINB
	POPJ P,0

BINTYO:	SKIPN T,CBP2
	MOVE T,[POINT 6,BBUF]
	IDPB IC,T
	MOVEM T,CBP2
	CAME T,[POINT 6,BBUFE,35]
	POPJ P,0
	PUSHJ P,WRTDB
	MOVE T,BINBLK
	CAIL T,BLKSPF-1
	PUSHJ P,SELB
	POPJ P,0
WRTDB:	PUSHJ P,SELB
	ENTER DB,A
	  JRST NXTDBN
	MOVE T,BINBLK
	USETO DB,1(T)
	OUTPUT DB,IOLDB
	CLOSE DB,0
CLRDB:	SETZM BBUF
	MOVE T,[XWD BBUF,BBUF+1]
	BLT T,BBUFE
	SETZM CBP2
	POPJ P,0

SELB:	MOVEI A,17
	MOVSI B,(SIXBIT /DSK/)
	MOVEI C,0
	OPEN DB,A
	  JRST NODSK
	MOVE N,BINFNO
NXTDBL:	CAILE N,^D999
	JRST ALLFUL
	PUSHJ P,NUMEXT
	MOVEM B,BINEXT
	MOVE A,[SIXBIT /UPIBIN/]
	SETZB C,D
	LOOKUP DB,A
	JRST NXTDBQ
NXTDBY:	HLRE T,D
	SKIPGE T
	ASH T,-7
	MOVMS T
	CAIL T,BLKSPF
	JRST NXTDBN
	MOVEM T,BINBLK
	SETZB C,D
	HLLZS B
	POPJ P,0

NXTDBQ:	MOVEI D,0
	TRNN B,-1
	JRST NXTDBY
NXTDBN:	AOS N,BINFNO
	JRST NXTDBL

FREEB:	PUSHJ P,SELB
	SKIPE BINBLK
	JRST FREEB1
	CLOSE DB,0
	POPJ P,0

FREEB1:	AOS BINFNO
	JRST FREEB
WRTDA:	PUSHJ P,SELA
	ENTER DA,A
	  JRST NXTDAN
	USETO DA,1(T)
	OUTPUT DA,IOLDA
	CLOSE DA,0
CLRDA:	SETZM ABUF
	MOVE T,[XWD ABUF,ABUF+1]
	BLT T,ABUFE
	SETZM CAP2
	POPJ P,0

SELA:	MOVEI A,17
	MOVSI B,(SIXBIT /DSK/)
	MOVEI C,0
	OPEN DA,A
	JRST NODSK
	MOVE N,ASCFNO
NXTDAL:	CAILE N,^D999
	JRST ALLFUL
	PUSHJ P,NUMEXT
	MOVEM B,ASCEXT
	MOVE A,[SIXBIT /UPITXT/]
	SETZB C,D
	LOOKUP DA,A
	JRST NXTDAQ
NXTDAY:	HLRE T,D
	SKIPGE T
	ASH T,-7
	MOVMS T
	CAIL T,BLKSPF
	JRST NXTDAN
	MOVEM T,ASCBLK
	SETZB C,D
	HLLZS B
	POPJ P,0

NXTDAQ:	MOVEI D,0
	TRNN B,-1
	JRST NXTDAY
NXTDAN:	AOS N,ASCFNO
	JRST NXTDAL

FREEA:	PUSHJ P,SELA
	SKIPE ASCBLK
	JRST FREEA1
	CLOSE DA,0
	POPJ P,0

FREEA1:	AOS ASCFNO
	JRST FREEA
;COMMANDS

ASCON:	TLNN F,L.NOF
	TLOA F,L.ASC
ASCOFF:	TLZ F,L.ASC
	JRST COMRET
BINON:	TLNN F,L.NOF
	TLOA F,L.BIN
BINOFF:	TLZ F,L.BIN
	JRST COMRET
TYPON:	TLNN F,L.NOF
	TLOA F,L.TYP
TYPOFF:	TLZ F,L.TYP
	JRST COMRET
NEWON:	TLO F,L.NXTA!L.NXTB
	JRST COMRET

EXIT:	TLZE F,L.ASCB
	PUSHJ P,CLSDA
	TLZE F,L.BINB
	PUSHJ P,CLSDB
	CALLI 12

HELP:	MOVEI A,HELPM
	PUSHJ P,MSG
	JRST COMRET

MINUS:	TLC F,L.NOF
	JRST MAINLP

OCTON:	TLNN F,L.NOF
	TLOA F,L.OCT
	TLZ F,L.OCT
	JRST COMRET
LIST:	MOVEI A,[ASCIZ /

;;;
CHARS READ: /]
	PUSHJ P,MSG
	MOVE N,NCHARS
	PUSHJ P,DECPRT
	PUSHJ P,CRLF
	TLNN F,L.ASCB
	JRST LISTA
	MOVEI A,[ASCIZ /WRITING UPITXT./]
	PUSHJ P,MSG
	MOVE A,ASCEXT
	PUSHJ P,SIXOUT
	MOVEI A,[ASCIZ /, BLK /]
	PUSHJ P,MSG
	MOVE N,ASCBLK
	PUSHJ P,DECPRT
	MOVEI A,[ASCIZ /, WD /]
	PUSHJ P,MSG
	HRRZ N,CAP2
	SKIPE N
	SUBI N,ABUF
	PUSHJ P,DECPRT
	PUSHJ P,CRLF
LISTA:	TLNN F,L.BINB
	JRST LISTB
	MOVEI A,[ASCIZ /WRITING UPIBIN./]
	PUSHJ P,MSG
	MOVE A,BINEXT
	PUSHJ P,SIXOUT
	MOVEI A,[ASCIZ /, BLK /]
	PUSHJ P,MSG
	MOVE N,BINBLK
	PUSHJ P,DECPRT
	MOVEI A,[ASCIZ /, WD /]
	PUSHJ P,MSG
	HRRZ N,CBP2
	SKIPE N
	SUBI N,BBUF
	PUSHJ P,DECPRT
	PUSHJ P,CRLF
LISTB:	MOVEI A,[ASCIZ /(IDLING)
/]
	TLNE F,L.IDL
	PUSHJ P,MSG
	MOVEI A,[ASCIZ /;;;

/]
	PUSHJ P,MSG
	JRST MAINLP
HELPM:	ASCIZ /

;;;
- - COMPLEMENTS ACTION
A - ASCII FILE ON
I - IMAGE FILE ON
T - TTY OUTPUT ON
N - FORCE NEW OUTPUT FILES
O - SPACES AS MAGIC CHARS
L - LIST CURRENT STATUS
H - HELP
X - EXIT
;;;
/
;CHARACTER TRANSLATION TABLE
;XWD LCENTRY,UCENTRY
;ENTRIES ARE: 0 - IGNORE, 1-177 - ASCII CHARACTER, SIGN+N - TYPE
; ASCIZ STRING AT LT+N

XTAB:	XWD 0,0		;NUL +0
	XWD 145,105	;E
	XWD L1,L1	;LF
	XWD 141,101	;A
	XWD 40,40	;SPACE BAR
	XWD 163,123	;S
	XWD 151,111	;I
	XWD 165,125	;U
	XWD 0,0		;CR +10
	XWD 144,104	;D
	XWD 162,122	;R
	XWD 152,112	;J
	XWD 156,116	;N
	XWD 146,106	;F
	XWD 143,103	;C
	XWD 153,113	;K
	XWD 164,124	;T +20
	XWD 172,132	;Z
	XWD 154,114	;L
	XWD 167,127	;W
	XWD 150,110	;H
	XWD 171,131	;Y
	XWD 160,120	;P
	XWD 161,121	;Q
	XWD 157,117	;O +30
	XWD 142,102	;B
	XWD 147,107	;G
	XWD 0,0		;SHIFT
	XWD 155,115	;M
	XWD 170,130	;X
	XWD 166,126	;V
	XWD 0,0		;UNSHIFT
;XTAB+40:

	XWD 40,40	;THIN SP +40
	XWD 63,L2	;3 3/8
	XWD L1,L1	;PF
	XWD 44,41	;$ !
	XWD 40,40	;ADD THIN SP
	XWD 40,40	;EM SP
	XWD 70,55	;8 -
	XWD 67,L3	;7 7/8
	XWD 47,140	;LEFT ' RIGHT ' +50
	XWD 53,100	;HYPHEN @
	XWD 64,L4	;4 1/2
	XWD 7,7		;BELL
	XWD 54,54	; , ,
	XWD 40,40	;QUAD LEFT
	XWD 40,40	;EN SPACE
	XWD 40,40	;QUAD RIGHT
	XWD 65,L5	;5 5/8 +60
	XWD 51,50	;) (
	XWD 40,40	;V RULE
	XWD 62,L6	;2 1/4
	XWD 40,40	;EM LEADER
	XWD 66,L7	;6 3/4
	XWD 60,77	;0 ?
	XWD 40,40	;EN LEADER
	XWD 71,46	;9 & +70
	XWD 40,40	;UPPER RAIL
	XWD 73,72	; ; :
	XWD 40,40	;LOWER RAIL
	XWD 56,56	; . .
	XWD 61,L8	;1 1/8
	XWD 40,40	;QUAD CENTER
	XWD 0,0		;RUBOUT	+77
;ASCII FOR MULTI-CHARACTER TRANSLATIONS

LT:

L1=S+.-LT
	ASCIZ /
/
L2=S+.-LT
	ASCIZ ._3/8.
L3=S+.-LT
	ASCIZ ._7/8.
L4=S+.-LT
	ASCIZ ._1/2.
L5=S+.-LT
	ASCIZ ._5/8.
L6=S+.-LT
	ASCIZ ._1/4.
L7=S+.-LT
	ASCIZ ._3/4.
L8=S+.-LT
	ASCIZ ._1/8.
MSG:	HRLI A,440700
MSGL:	ILDB CH,A
	JUMPE CH,CPOPJ
	PUSHJ P,TYO
	JRST MSGL

CRLF:	JSP A,MSG
	ASCIZ /
/

TYO:	TTCALL 1,CH
	POPJ P,0

DECPRT:	IDIVI N,12
	HRLM N1,0(P)
	SKIPE N
	PUSHJ P,DECPRT
	HLRZ CH,0(P)
	ADDI CH,60
	JRST TYO

SIXOUT:	MOVE T,[POINT 6,A]
SIXL:	ILDB CH,T
	JUMPE CH,CPOPJ
	ADDI CH,40
	PUSHJ P,TYO
	TLNE T,770000
	JRST SIXL
	POPJ P,0
ALLFUL:	MOVEI A,[ASCIZ /

?NO FILES LEFT
?/]
BADMSG:	PUSHJ P,MSG
	JRST EXIT

NODSK:	TLZ F,L.ASCB!L.BINB
	JSP A,BADMSG
	ASCIZ /
?NO DISK?
/
NOTASG:	JSP A,BADMSG
	ASCIZ /
?INPUT DEVICE NOT ASSIGNED
/
OPNUPX:	JSP A,BADMSG
	ASCIZ /
?CANT INIT INPUT DEVICE
/

OPNUPY:	JSP A,BADMSG
	ASCIZ /
?INPUT FILE NOT FOUND
/
;CONSTANTS AND DATA STORAGE

PDP:	XWD -20,PDL-1

UPIDEV:	SIXBIT /TTY13/

IOLDA:	IOWD 200,ABUF
	0

IOLDB:	IOWD 200,BBUF
	0

LIT

ASCBLK:	0
BINBLK:	0
ASCEXT:	0
BINEXT:	0
INPEXT:	0
NCHARS:	0
PDL:	BLOCK 20
CWP:	0
CAP:	0
CBP:	0
CAP2:	0
CBP2:	0
ASCFNO:	0
BINFNO:	0
INPFNO:	0
SJFF:	0

UPIHED:	BLOCK 3
TYIHED:	BLOCK 3
TYOHED:	BLOCK 3

ABUF:	BLOCK 200
ABUFE=.-1

BBUF:	BLOCK 200
BBUFE=.-1

CORE:	BLOCK 200
COREND=.-1

FLASHX:	END FLASH
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                     