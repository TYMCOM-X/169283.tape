TITLE LOOKFL - CUSP TO PRINT LOOKUP VALUES FOR A FILE    V004(2)
SUBTTL	D.BLACK/DJB/DAL/PFC		23-JUL-71

VLOOKF==4
VEDIT==2
VMINOR==0
VWHO==0

	INTERN	.JBVER
.JBVER==137
	LOC	.JBVER
	BYTE	(3)VWHO(9)VLOOKF(6)VMINOR(18)VEDIT
	RELOC

IFNDEF PURESW,<PURESW=0>

;PARAMETERS FROM COMMOD

RIBPPN==1	;DIRECTORY NAME
RIBNAM==2	;FILE NAME
RIBEXT==3	;(LH) EXT
RIBACD==3	;(RH) ACCESS DATE
RIBERR==3	;(RH) ERROR CODE IF FAILS
RIBPRV==4	;ACCESS PRIVILEGES (BITS 0-8)
RIBMOD==4	;MOD (BITS 9-12)
RIBCTM==4	;CREATION TIME (BITS 13-23)
RIBCDT==4	;CREATION DATE (BITS 24-35)
RIBSIZ==5	;WORDS WRITTEN
RIBVER==6	;VERSION NUMBER
RIBSPL==7	;ENTERED NAME OF SPOOLED DATA
RIBEST==10	;ESTIMATED LENGTH IN CORE AND NUMBER OF BLOCKS
RIBALC==11	;BLOCKS ALLOCATED INCLUDING BOTH RIBS
RIBPOS==12	;LOGICAL BLOCK WITHIN STR OF LAST ALLOCATION
RIBNCA==14	;NONPRIVILEGED CUSTOMER ARG
RIBMTA==15	;TAPE LABEL
RIBDEV==16	;STR FILE STARTS ON
RIBSTS==17	;STATUS BITS
RIBELB==20	;LOGICAL BLOCK WITHIN ERROR IN WHICH ERROR BEGINS
RIBEUN==21	;(LH) LOGICAL UNIT IN STR ERROR OCCURRED ON
RIBNBB==21	;(RH) CONSECUTIVE BAD BLOCKS
RIBQTF==22	;(UFD ONLY) FIRST COME FIRST SERVED QUOTA
RIBQTO==23	;(UFD ONLY) LOGGED OUT QUOTA
RIBQTR==24	;(UFD ONLY) RESERVED QUOTA
RIBUSD==25	;(UFD ONLY) BLOCKS USED
RIBAUT==26	;AUTHOR (PPN)
RIBNXT==27	;NEXT STR IF FILE IS CONTINUED
RIBPRD==30	;PREVIOUS FILE IF CONTINUED
RIBPCA==31	;PRIVILEGED CUSTOMER ARG
RIBUFD==32	;LOGICAL BLOCK OF UFD DATA BLOCK CONTAINING ENTRY FOR THIS FILE

RIBENT==32	;LAST ENTRY

RIPDIR==400000	;DIRECTORY BIT IN RH STATUS WORD
;ACCUMULATORS

T=1
T1=T+1
T2=T1+1
T3=T2+1
P1=5
SFD=6
P=17

;IO CHANNELS

TTY==1
LPT==2
DSK==3

;STUFF

OUTSTR==3		;TTCALL OUT STRING FUNCTION
PATH.==110
SFDLVL==5		;MAX NUMBER OF NESTED SFD'S

LF==12
FORMFD==14

	DEFINE MSG (A)<
	XLIST
	MOVEI	T,[ASCIZ \A\]
	PUSHJ	P,MSGX
	LIST
>

DEFINE	T	(A,B,C)<
TEST'B:	SKIPN	LOOKBF+RIB'A
	JRST	TEST'C>
;STORAGE MACROS

DEFINE U(A)<UU(A,1)>

.ZZ==140

IFN PURESW,<
	HISEG		;FOR THE LOADER
	DEFINE UU(A,B)<
	A==.ZZ
	.ZZ==.ZZ+B
>	;END MACRO DEFINITION OF UU
>	;END CONDITIONAL ON PURESW

IFE PURESW,<
	DEFINE UU(A,B)<
	A:	BLOCK	B
	.ZZ==.ZZ+B
>	;END MACRO DEFINITION OF UU
>	;END CONDITIONAL ON PURESW
	SUBTTL	GET FILE
LOOKFL:	RESET
	MOVE	P,PDL
	INIT	TTY,
	SIXBIT	.TTY.
	IBUF
	  EXIT
	INIT	LPT,
	SIXBIT	.TTY.
	XWD	OBUF,
	  JRST	NOLPT
	MOVE	T,[SIXBIT .LOOKFL.]
	MOVSI	T1,(SIXBIT .TXT.)
	SETZB	T2,T3
	ENTER	LPT,T
	  JFCL
	MOVE	T,[XWD 3,LOOKBF]
	SETOM	LOOKBF		;READ THE DEFAULT PATH
	CALLI	T,PATH.
	  SKIPA	SFD,LOOKBF	;ERROR - NO SFDS IN THIS MONITOR
	MOVEI	SFD,0		;WON - SFD=0
LOOKF1:	SKPINL			 ;INPUT TO DEFEAT CONTROL O
	  JFCL			;DONT CARE ABOUT RESULT
	TTCALL	OUTSTR,[ASCIZ .
File: .]
	SETZM	LOOKBF
	MOVE	T,[XWD LOOKBF,LOOKBF+1]
	BLT	T,LOOKBF+RIBENT
	SETZM	SFDBLK
	MOVE	T,[XWD SFDBLK,SFDBLK+1]
	BLT	T,SFDNAM+SFDLVL-1
	PUSHJ	P,GETNAM	;GET NAME OF FILE TO LOOK AT
	  JRST	LOOKF1		;NOT QUITE, TRY AGAIN
	HLRZ	T,EXT		;GET EXTENSION
	MOVEI	T2,16
	GETTAB	T2,
	  MOVE	T2,[1,,1]	;GET MFD
	CAIN	T,'UFD'		;IF UFD
	SKIPE	PPN		;AND USER DIDN'T SAY
	JRST	.+2
	MOVEM	T2,PPN		;THEN STORE IN MFD
	SETZB	T,T2
	SKIPN	T1,STRNAM	;SKIP IF STR GIVEN
	MOVSI	T1,(SIXBIT .DSK.) ;ELSE USE DSK
	OPEN	DSK,T
	  JRST	NODSK		;CANT FIND IT
	SETZM	LFAIL		;CLEAR FAIL FLAG
	MOVEI	T,SFDBLK
	SKIPLE	SFD
	EXCH	T,PPN
	MOVEM	T,SFDPPN
	LOOKUP	DSK,LOOKBF	;EXTENDED LOOKUP OF FILE
	  SETOM	LFAIL		;TOO BAD
	SUBTTL	OUTPUT PARAMETERS
	SKIPN	T,LOOKBF+RIBDEV ;DEVICE FILE BEGINS ON
	JRST	LOOKF2
	PUSHJ	P,SIXPRT
	PUSHJ	P,COLON
LOOKF2:	MOVE	T,LOOKBF+RIBSTS
	TRNE	T,RIPDIR	;SKIP UNLESS DIRECTORY
	SKIPG	LOOKBF+RIBNAM	; IF DIRECTORY NAME IS NEGATIVE,
	SKIPA	T,LOOKBF+RIBNAM	; TYPE IT AS SIXBIT
	JRST	NAMDIR
	PUSHJ	P,SIXPRT
LOOKF4:	HLLZ	T,LOOKBF+RIBEXT	;EXT
	SKIPN	T
	JRST	LOOKF3
	PUSHJ	P,DOT
	HLLZ	T,LOOKBF+RIBEXT
	PUSHJ	P,SIXPRT
LOOKF3:	PUSHJ	P,LBRAK		;LEFT BRACKET
	JUMPGE	SFD,LOOKF5	;GO IF SFD'S IN THIS MONITOR
	HLRZ	T,LOOKBF+RIBPPN;NONE, TYPE PPN
	PUSHJ	P,OCTPRT	;PROJECT OF DIRECTORY
	PUSHJ	P,COMMA
	HRRZ	T,LOOKBF+RIBPPN
	PUSHJ	P,OCTPRT	;PROGRAMMER OF DIRECTORY
	JRST	LOOKF7

;HERE IF THE MONITOR SUPPORTS SFD'S
LOOKF5:	MOVEI	T,DSK		;READ THE ACTUAL PATH TO THE FILE
	MOVEM	T,PTHBLK
	MOVE	T,[XWD SFDLVL+3,PTHBLK]
	CALLI	T,PATH.
	  HALT		;BUT PATH. WON EARLIER!
	HLRZ	T,PTHPPN	;PPN
	PUSHJ	P,OCTPRT
	PUSHJ	P,COMMA
	HRRZ	T,PTHPPN
	PUSHJ	P,OCTPRT
	MOVSI	SFD,-SFDLVL	;START AT 1ST SFD NAME
LOOKF6:	SKIPN	PTHSFD(SFD)	;IS THER AN SFD?
	JRST	LOOKF7		;NO
	PUSHJ	P,COMMA		;YES, TYPE ","
	MOVE	T,PTHSFD(SFD)	; AND "NAME"
	PUSHJ	P,SIXPRT
	AOBJN	SFD,LOOKF6	;TRY NEXT SFD IN PATH
LOOKF7:	PUSHJ	P,RBRAK		;DONE - TYPE "]"
	SKIPE	LFAIL		;LOOKUP WIN?
	JRST	LOOKER		;NO
GO:	MSG	<

Access date: >
	HRRZ	T,LOOKBF+RIBACD	;ACCESS DATE
	PUSHJ	P,DATE
	MSG	<
Creation time, date: >
	LDB	T,CRETIM
	PUSHJ	P,TIME
	PUSHJ	P,SPACE
	LDB	T,CREDAT
	PUSHJ	P,DATE
	MSG	<
Access protection: >
	LDB	T,PRV
	MOVEI	T2,3
	PUSHJ	P,OCTFUL
	MSG	<
Mode: >
	LDB	T,MODE
	PUSHJ	P,OCTPRT
	MSG	<
Words written: >
	MOVE	T,LOOKBF+RIBSIZ
	PUSHJ	P,DECPRT
	T	VER,0,1A
	MSG	<
Version number: >
	HLRZ	T,LOOKBF+RIBVER
	PUSHJ	P,OCTPRT
	PUSHJ	P,COMMA
	HRRZ	T,LOOKBF+RIBVER
	PUSHJ	P,OCTPRT
	T	SPL,1A,2
	MSG	<
Spooled file name in ENTER: >
	MOVE	T,LOOKBF+RIBSPL
	PUSHJ	P,SIXPRT
	T	EST,1,2
	MSG	<
Estimated length: >
	MOVE	T,LOOKBF+RIBEST
	PUSHJ	P,DECPRT
	T	ALC,2,3
	MSG	<
Blocks allocated: >
	MOVE	T,LOOKBF+RIBALC
	PUSHJ	P,DECPRT
	T	POS,3,4
	MSG	<
Position of last allocation: >
	MOVE	T,LOOKBF+RIBPOS
	PUSHJ	P,OCTPRT
	T	NCA,4,5
	MSG	<
Nonprivileged customer arg: >
	MOVE	T,LOOKBF+RIBNCA
	MOVEI	T2,^D12
	PUSHJ	P,OCTFUL
	T	MTA,5,6
	MSG	<
Tape label: >
	MOVE	T,LOOKBF+RIBMTA
	PUSHJ	P,SIXPRT
	T	STS,6,7
	MSG	<
Status bits: >
	MOVE	T,LOOKBF+RIBSTS
	MOVEI	T2,^D12
	PUSHJ	P,OCTFUL
	T	ELB,7,8
	MSG	<
Error logical block: >
	MOVE	T,LOOKBF+RIBELB
	PUSHJ	P,OCTPRT
	T	EUN,8,9
	MSG	<
Error logical unit: >
	HLRZ	T,LOOKBF+RIBEUN
	PUSHJ	P,OCTPRT
	T	NBB,9,10
	MSG	<
Number of bad blocks: >
	HRRZ	T,LOOKBF+RIBNBB
	PUSHJ	P,DECPRT
TEST10:	MOVE	T,LOOKBF+RIBSTS
	TRNN	T,RIPDIR	;SKIP IF DIRECTORY
	JRST	NOTUFD
	MSG	<
First come first served quota: >
	MOVE	T,LOOKBF+RIBQTF
	PUSHJ	P,DECPRT
	MSG	<
Logged out quota: >
	MOVE	T,LOOKBF+RIBQTO
	PUSHJ	P,DECPRT
	MSG	<
Reserved quota: >
	MOVE	T,LOOKBF+RIBQTR
	PUSHJ	P,DECPRT
	MSG	<
Blocks used: >
	MOVE	T,LOOKBF+RIBUSD
	PUSHJ	P,DECPRT
NOTUFD:	MOVE	T,LOOKBF+RIBAUT
	CAMN	T,LOOKBF+RIBPPN
	JRST	TEST12
	MSG	<
Author: >
	HLRZ	T,LOOKBF+RIBAUT
	PUSHJ	P,OCTPRT
	PUSHJ	P,COMMA
	HRRZ	T,LOOKBF+RIBAUT
	PUSHJ	P,OCTPRT
	T	NXT,12,13
	T	PRD,13,14
	MSG	<
Next str: >
	MOVE	T,LOOKBF+RIBNXT
	PUSHJ	P,SIXPRT
	MSG	<
Previous str: >
	MOVE	T,LOOKBF+RIBPRD
	PUSHJ	P,SIXPRT
	MSG	<
Privileged customer arg: >
	MOVE	T,LOOKBF+RIBPCA
	MOVEI	T2,^D12
	PUSHJ	P,OCTFUL
TEST14:	MSG	<
Data block in UFD: >
	MOVE	T,LOOKBF+RIBUFD
	PUSHJ	P,OCTPRT
	MSG	<

>
	CLOSE	LPT,
	RELEASE	LPT,
	JRST	LOOKFL
	SUBTTL	USEFUL SUBROUTINES

NAMDIR:	HLRZ	T,LOOKBF+RIBNAM
	PUSHJ	P,OCTPRT
	PUSHJ	P,COMMA
	HRRZ	T,LOOKBF+RIBNAM
	PUSHJ	P,OCTPRT
	JRST	LOOKF4

LOOKER:	MSG	<

LOOKUP failure: >
	HRRZ	T,LOOKBF+RIBERR
	PUSHJ	P,OCTPRT
	CLOSE	LPT,
	RELEASE	LPT,
	JRST	LOOKFL

NOLPT:	TTCALL	OUTSTR,[ASCIZ .
Can't open TTY.]
	JRST	LOOKFL

NODSK:	TTCALL	OUTSTR,[ASCIZ .
Can't open str.]
	JRST	LOOKFL
	SUBTTL	COMMAND SCANNER, SUCH AS IT IS
GETNAM:	SETZM	STRNAM
	SETZM	FILE
	SETZM	EXT
	SETZM	PPN
	MOVEI	T,RIBENT
	MOVEM	T,LOOKBF
	SKIPL	SFD
	SETZ	SFD,
NAM0:	PUSHJ	P,SIXAN
NAM0A:	CAIE	T,":"
	JRST	NAM1
	MOVEM	T1,STRNAM
	JRST	NAM0
NAM1:	CAIE	T,"."
	JRST	NAM2
	MOVEM	T1,FILE
	JRST	NAM0
NAM2:	CAIE	T,"["
	JRST	NAM3
	SKIPE	FILE
	MOVEM	T1,EXT
	SKIPN	FILE
	MOVEM	T1,FILE
	PUSHJ	P,OCTRD
	CAIE	T,","
	JRST	FILERR
	HRLZM	T1,PPN
	PUSHJ	P,OCTRD
	CAIE	T,"]"
	CAIN	T,12
	JRST	.+2
	JRST	TRYSFD
	HRRM	T1,PPN
NAM2A:	CAIN	T,"]"
	PUSHJ	P,TTI
	CAIGE	T,40
	JRST	CPOPJ1
NAM3:	CAIE	T,"/"
	JRST	NAM4
	PUSHJ	P,TTI
	CAIN	T,"X"
	EXIT
	CAIN	T,"H"
	JRST	HELPMS
	JRST	FILERR
NAM4:	CAIE	T,","
	JRST	NAM5
	SKIPE	FILE
	JRST	FILERR
	MOVE	T2,T1		;T2=CHARS READ
	MOVE	T3,[POINT 6,T2]
	SETZ	T,
SIXOC1:	TLNN	T3,770000
	JRST	NAM4A
	ILDB	T1,T3		;T1=NEXT CHAR
	JUMPE	T1,NAM4A	;QUIT IF NO MORE CHARS
	SUBI	T1,20		;CONVERT SIXBIT TO BINARY
	CAILE	T1,7		;SKIP IF OCTAL DIGIT
	JRST	FILERR
	LSH	T,3
	ADD	T,T1
	JRST	SIXOC1
NAM4A:	HRLM	T,(P)		;REMEMBER PROJECT NUMBER
	PUSHJ	P,OCTRD		;READ PROGRAMMER NUMBER
	HLL	T1,(P)		;RESTORE PROJECT
	MOVEI	T2,RIPDIR	;SET DIRECTORY BIT IN CASE LOOKUP FAILSA
	ORM	T2,LOOKBF+RIBSTS;SO WILL PRINT CORRECT NAME
	JRST	NAM0A		;AND PRETEND WE HAVE A FILE NAME
NAM5:	CAIL	T,40
	JRST	FILERR
	SKIPE	FILE
	MOVEM	T1,EXT
	SKIPN	FILE
	MOVEM	T1,FILE
CPOPJ1:	AOS	(P)
	POPJ	P,
TRYSFD:	CAIE	T,","
	JRST	FILERR
	HRRM	T1,PPN
	JUMPL	SFD,NOSFDS
TRYSF1:	PUSHJ	P,SIXAN
	MOVEM	T1,SFDNAM(SFD)
	ADDI	SFD,1
	CAIE	T,12
	CAIN	T,"]"
	JRST	NAM2A
	CAIL	SFD,SFDLVL
	JRST	TOOMNY
	CAIE	T,","
	JRST	NAM2A
	JRST	TRYSF1

NOSFDS:	MOVEI	T1,[ASCIZ .THIS MONITOR DOES NOT SUPPORT SFD'S.]
	JRST	TYPEX

TOOMNY:	MOVEI	T1,[ASCIZ .TOO MANY SFD'S SPECIFIED.]
	JRST	TYPEX

FILERR:	MOVEI	T1,[ASCIZ .
?.]
	JRST	TYPEX

HELPMS:	MOVEI	T1,HELPTX
	JRST	TYPEX

	PUSHJ	P,TTI
TYPEX:	CAIE	T,LF
	JRST	.-2
	TTCALL	OUTSTR,(T1)
	POPJ	P,
	SUBTTL	MORE USEFUL ROUTINES
COLON:	MOVEI	T,":"
	JRST	TYO

COMMA:	MOVEI	T,","
	JRST	TYO

DOT:	MOVEI	T,"."
	JRST	TYO

LBRAK:	MOVEI	T,"["
	JRST	TYO

RBRAK:	MOVEI	T,"]"
	JRST	TYO

SPACE:	MOVEI	T," "
	JRST	TYO

MSGX:	MOVE	T1,T
	HRLI	T1,440700
MSGL:	ILDB	T,T1
	JUMPE	T,CPOPJ
	PUSHJ	P,TYO
	JRST	MSGL
DECPR2:	CAILE	T,11
	JRST	DECPRT
	PUSH	P,T
	MOVEI	T,"0"
	PUSHJ	P,TYO
	POP	P,T
	JRST	DECPRT

OCTFUL:	MOVE	T1,T		;T1=WORD TO PRINT
	MOVEI	T3,^D12
	SUB	T3,T2		;T3=CHARS TO IGNORE
	IMULI	T3,3
	ROTC	T,(T3)		;PRINT (T2) LOWER CHARS
OCTFU1:	SETZ	T,
	ROTC	T,3		;T=NEXT CHAR
	ADDI	T,"0"
	PUSHJ	P,TYO
	SOJG	T2,OCTFU1
	POPJ	P,

OCTPRT:	SKIPA	T2,[^D8]
DECPRT:	MOVEI	T2,^D10
RDXPRT:	JUMPGE	T,RDXPR1	;OK IF POS
	PUSH	P,T
	MOVEI	T,"-"		;PUT OUT MINUS SIGN
	PUSHJ	P,TYO
	POP	P,T		;RESTORE WORD
	MOVMS	T		;POSITIVE VALUE
RDXPR1:	IDIVI	T,(T2)
	HRLM	T1,(P)
	SKIPE	T
	PUSHJ	P,RDXPR1
	HLRZ	T,(P)
	ADDI	T,"0"
TYO:	SOSG	OBUF+2
	OUTPUT	LPT,
	IDPB	T,OBUF+1
	POPJ	P,
SIXPRT:	MOVE	T2,T		;SAVE WORD IN T2
	MOVE	T1,[POINT 6,T2]
SIXPR1:	TLNN	T2,770000
CPOPJ:	POPJ	P,
	ILDB	T,T1
	JUMPE	T,CPOPJ
	ADDI	T,40
	PUSHJ	P,TYO
	JRST	SIXPR1

TIME:	IDIVI	T,^D60000	;TO MINUTES
	MOVE	T,T1
	IDIVI	T,^D60		;NOW BREAK IN MINS AND HRS
	PUSH	P,T1		;SAVE MINUTES
	PUSHJ	P,DECPR2
	POP	P,T		;MINUTES
	PUSHJ	P,DECPR2
	MOVEI	T,"  "		;2 SPACES
	JRST	TYO

DATE:	IDIVI	T,^D31		;THE USUAL DATE PRINTER
	PUSH	P,T
	MOVEI	T,1(T1)		;DAY OF MONTH
	PUSHJ	P,DECPR2
	POP	P,T
	IDIVI	T,^D12		;MONTH AND YEAR
	PUSH	P,T
	MOVE	T1,MONTAB(T1)
	MOVEM	T1,DATEMP
	SETZM	DATEMP+1	;FOR ASCIZ, DDTOUT
	MOVEI	T,DATEMP
	PUSHJ	P,MSGX
	POP	P,T
	MOVEI	T,^D64(T)	;YEAR
	JRST	DECPR2
SIXAN:	SETZ	T1,
	MOVE	T2,[POINT 6,T1]
	PUSHJ	P,SSP
	JRST	.+2
SIXANL:	PUSHJ	P,TTI
	CAIG	T,"Z"
	CAIGE	T,"0"
	POPJ	P,
	CAIGE	T,"A"
	CAIG	T,"9"
	JRST	.+2
	POPJ	P,
	SUBI	T,40
	TLNE	T2,770000
	IDPB	T,T2
	JRST	SIXANL

SSP:	PUSHJ	P,TTI
	CAIE	T,11
	CAIN	T,40
	JRST	SSP
	POPJ	P,

OCTRD:	SETZ	T1,
OCTRD1:	PUSHJ	P,TTI
	CAIGE	T,"0"
	POPJ	P,
	CAILE	T,"7"
	POPJ	P,
	LSH	T1,3
	ADDI	T1,-60(T)
	JRST	OCTRD1
TTI:	SOSLE	IBUF+2

	JRST	TTIOK
	INPUT	TTY,
TTIOK:	ILDB	T,IBUF+1
	JUMPE	T,TTI
	CAIE	T,15		;IGNORE CARRIAGE RETURNS
	CAIN	T,177
	JRST	TTI		;IGNORE RIBOUTS
	CAIL	T,175		;CONVERT TO STANDARD ALTMODE
	MOVEI	T,33
	CAIL	T,140		;LOWER CASE TO UPPER CASE
	TRC	T,40
	CAIE	T,3		;CHECK FOR ^C
	CAIN	T,32		; AND FOR ^Z
	JRST	[RESET
		 EXIT	1,
		 JRST	LOOKFL]
	POPJ	P,
CRETIM:	POINT	11,LOOKBF+RIBCTM,23
CREDAT:	POINT	12,LOOKBF+RIBCDT,35
PRV:	POINT	9,LOOKBF+RIBPRV,8
MODE:	POINT	4,LOOKBF+RIBMOD,12

PDL:	IOWD	20,PDLIST

MONTAB:	ASCII /-Jan-/
	ASCII /-Feb-/
	ASCII /-Mar-/
	ASCII /-Apr-/
	ASCII /-May-/
	ASCII /-Jun-/
	ASCII /-Jul-/
	ASCII /-Aug-/
	ASCII /-Sep-/
	ASCII /-Oct-/
	ASCII /-Nov-/
	ASCII /-Dec-/

HELPTX:	ASCIZ	\To list non-null extended LOOKUP values for a file, type file name in form
DEV:FILE.EXT[PROJ,PROG,SFD,SFD,....]
\
	SUBTTL	LITERALS AND STORAGE
;LITERALS
	XLIST
	LIT
	LIST

;STORAGE

UU(DATEMP,2)

UU(IBUF,3)
UU(OBUF,3)
U(STRNAM)
U(LFAIL)
UU(LOOKBF,RIBENT+1)
FILE==LOOKBF+RIBNAM
EXT==LOOKBF+RIBEXT
PPN==LOOKBF+RIBPPN
UU(SFDBLK,2)
U(SFDPPN)
UU(SFDNAM,SFDLVL)

UU(PTHBLK,2)
U(PTHPPN)
UU(PTHSFD,SFDLVL)
UU(PDLIST,20)
	END	LOOKFL
