 SWITCH 1    201 CARD
 SWITCH 5    PDP10 BASE CODE
:8/7/72      VERSION 7 BASE
 IF 1
: 201 CARD VERSION
 ELSE
: VERSION FOR NO 201 CARD
 ENDIF
 IF 5
: PDP10 HOST
 ENDIF
 IF 8
: DUAL 940 8 LINES 12K
 ENDIF
 JSS1 *; JMPM CRASH
 R 10 POWER FAILURE TRAP LOCATION AND ERROR COUNT FOR LINE 4
 0;0; EXC 77; INR POWFAL; JMPM CRASH
POWFAL 0 POWER FAILURE FLAG
 R 20
 IF 1
ZDET 21;23;25;27;31;33;35;37
 I *; R 6*
 R 36; JMPM RNG
 JMPM OUT0; JMPM IN0; JMPM OUT1; JMPM IN1
 JMPM OUT2; JMPM IN2; JMPM OUT3; JMPM IN3
 ENDIF
 IF 1&8
 JMPM OUT4; JMPM IN4; JMPM OUT5; JMPM IN5
 JMPM OUT6; JMPM IN6; JMPM OUT7; JMPM IN7
 ELSEIF 1&(#8)
 JMPM CRASH; JMPM CRASH; JMPM CRASH
 JMPM CRASH; JMPM CRASH; JMPM CRASH
 ENDIF
 IF #1
 JMPM JTAB2.; JMPM JTAB.; JMPM IN1; JMPM IN0
 ENDIF
 R 100
MACHNO 43
 410 VERSION NUMBER
DLOCK 0 DEBUGGER LOCK
DSWICH 0 DOWNSTREAM SWITCH
CRSHCT 0 CRASH COUNT
 IF 8
N940L 200+NLINES NUMBER OF 940S AND 201 LINES
 ELSE
N940L 100+NLINES  NUMBER OF 940S AND 201 LINES
 ENDIF
33D
UPDOWN -1
 
 IF 8
UPDN1 -1
SWTCHO 0;0;0;0;0;0;0;0
 ELSE
SWTCHO 0;0;0;0;0
 ENDIF
MACH 1000;2;0;0;0
IOTA +IOTAB0;+IOTAB1;+IOTAB2;+IOTAB3;+IOTAB4
 IF 8
 +IOTAB5;+IOTAB6;+IOTAB7
 ENDIF
 +IOTABI
IOTABS -4D;-6D;0;0;0;-2D
GROUPN 2;3;0;0;0
GROUPI 00;02;05;10;13;+NUMGRP-1
BORO 10000
BORI 10000;10000;10000;10000;10000
STOPIN -1;-1;-1;-1;-1;-1
 IF 8
 -1;-1;-1
 ENDIF
CRSAVE R 10* ARRAY TO SAVE CRASH INFORMATION
 IF 5
ZERO 0
PADDR 0
IPADDR 0
IW1 0
IW2 0
 ENDIF
LINER 0;0;0;0
 IF 8
 0;0;0;0
 ENDIF
MASK 1;2;4;10;20;40;100;200;400;1000;2000;4000
 10000;20000;40000;100000
CMSK 177776;177775;177773;177767;177757;177737;177677;177577;177377
 176777;175777;173777;167777;157777;137777;77777
RSF 21;42;104;210;231;63;146;314;335;273;167;356
FLAG E *
: GENBUF MUST BE FIRST   UNASSIGNED BUFFERS USE BIT 0 OF GENBUF
: IIN1 MUST BE SECOND FOR RT8B STOPIN SETTING
GENBUF R 1*
 IF 8
IIN R 10*
 ELSE
IIN R 6*
 ENDIF
: WARNING! RT6 REQUIRES CFOB TO BE THE LAST GROUP OF FLAGS
 IF 8
LS E 10
NUMGRP E 32
 ELSE
LS E 5
NUMGRP E 22
 ENDIF
CFOB R NUMGRP+*    CANDIDATES FOR OUTPUT BUFFER
PCFOB R NUMGRP+*   PROCESS CAND. FOR OBUF.
RSBF R NUMGRP+*    RESTRICTED BUFFER FLAGS
LRESET R LS*        LINE RESET
TIME1 R LS*         TIME SINCE LAST RECORD MADE
OSF R LS*           OUTPUT SECTOR FLAGS
ISF R LS*           INPUT SECTOR FLAGS
CAOS R LS*          CURRENT ASSEMBLING OUTPUT SECTOR
COS R LS*           CURRENT OUTPUT SECTOR
CDIS R LS*          CURRENT DISASSEMBLING INPUT SECTOR
CIS R LS*           CURRENT INPUT SECTOR
LRR R LS*           LAST RECORD RECIEVED
ASDW R LS*          ASSEMBLED WORD
NDASW R LS*         NEXT DISASSEMBLING WORD
 IF #1
ASW R 2*           ASSEMBLING WORD
DASW R 2*          DISASSEMBLING WORD
 ENDIF
TSLR R LS*          TIME SINCE LAST RESET
QOVERT R LS*        OUTPUT VERT CKSM.
QODIAG R LS*        OUTPUT DIAG. CKSM.
QIFRST R LS*        FIRST WORD OF RECORD
QIVERT R LS*        INPUT VERT. CKSM.
QIDIAG R LS*        INPUT DIAG. CKSM.
IRC R LS*           INPUT RECORD COUNT
ORC R LS*           OUTPUT RECORD COUNT
 IF #1
INLIST R 2*
OUTLST R 2*
 ENDIF
SWTCHI R LS*        SWITCH FOR INPUT CLEANUP
LINSAT R LS*        LINE SATURATION COUNTER
INDATA R LS*        INCOMMING DATA FLG FOR DIAGNOSTICS
ORA R LS*           OUTPUT RECORD ADDRESS
IRA R LS*           INPUT RECORD ADDRESS
 IF 8
STOPO R 10*
 ELSE
STOPO R 6*
 ENDIF
ACTPRT 1;0;0;0;0;0     ACTIVE PORTS
 IF 8
 0;0
 ENDIF
W1 0
W2 0
W3 0
W4 0
W5 0
W6 0   WORKING STORAGE
W7 0
W8 0
W9 0
W10 0
W11 0
W12 0
W13 0
: OSEC = ADDRESS OF OUTPUT SECTORS
: ISEC = ADDRESS OF INPUT SECTORS
: CHECK BR BEFORE CHANGING OSEC
 IF 8
OSEC 15732;16132;16332;16532;16732;17132;17332;17532
ISEC 20000;20200;20400;20600;21000;21200;21400;21600
 ELSE
OSEC 12400;12600;13000;13200
ISEC 13400;13600;14000;14200
 ENDIF
: JUMP ADDRESS FOR ZERO DETECT, NORMAL JUMP
: ADDRESS, AND ADDRESS OF JUMP INSTRUCTION.
 IF 1&(#8)
INZD 6IN0;6IN1;6IN2;6IN3
INNORM +IN0.;+IN1.;+IN2.;+IN3.
INAD 5IN0;5IN1;5IN2;5IN3
OUTT +OUT0;+OUT1;+OUT2;+OUT3
INTT +IN0;+IN1;+IN2;+IN3
 ENDIF
 IF 1&8
INZD 6IN0;6IN1;6IN2;6IN3;6IN4;6IN5;6IN6;6IN7
INNORM +IN0.;+IN1.;+IN2.;+IN3.;+IN4.;+IN5.;+IN6.;+IN7.
INAD 5IN0;5IN1;5IN2;5IN3;5IN4;5IN5;5IN6;5IN7
OUTT +OUT0;+OUT1;+OUT2;+OUT3;+OUT4;+OUT5;+OUT6;+OUT7
INTT +IN0;+IN1;+IN2;+IN3;+IN4;+IN5;+IN6;+IN7
 ENDIF
 IF #1
:JUMP ADDRESS FOR ZERO DETECT, NORMAL JUMP 
:ADDRESS, AND ADDRESS OF JUMP INSTRUCTION.
INZD +IN0;+IN1
INNORM +IN0B;+IN1B
INAD +INS;1INS
INS +IN0;+IN1
 ENDIF
 L *; R 200*
CLNLOK R 1*
UPDWN R 1*         UP-DOWN FLG.
INA R 1*           SAVE LOCATIONS FOR INTERRUPTS
 IF #1
INB R 1*; ENDIF
CNA R 1*           SAVE LOCATIONS FOR CLEANUP
CNB R 1*
CNX R 1*
FULL R 1*
NRW 0
NRW1 0
NRM 0
NRA 0
NRSW 0
SUCUNT 0
SCOUNT 0
SPORT 0
TCH 0
TIMSEC 0       TIME SINCE LAST ONE HALF SECOND
TIMMIN 0 TIME SINCE LAST MINUTE
TIMEH 0        HIGH ORDER REAL TIME
TIMEL 0        LOW ORDER REAL TIME
UNHAP R 1*         UNHAPPY FLG.
SACTIV R 1*        SUPERVISOR ACTIVE
USWICH R 1*        UPSTREAM SWITCH
ICOUNT 0
IRING 0            INPUT RING ADDRESS
ORING 0            OUTPUT RING ADDRESS
 IF 8
ICNT1 0
IRING1 0
ORING1 0
UNHAP1 R 1*
MICRJ2 R 1*
MICER2 R 1*
FULL1 R 1*
UPDWN1 R 1*
 ENDIF
 IF #5
SPSTAT R 1*
SIRING R 1*        SUPERVISOR INPUT RING
SORING R 1*        SUPERVISOR OUTPUT RING
PAGE0 R 1*         SUPERVISOR PAGE 0
SFULL R 1*
STIME R 1*         SUPERVISOR TIME
 ENDIF
MICRJC 0       MIC REJECT COUNT
MICERR 0 MIC ERROR COUNT
 IF 5
 ELSE
RD940 1400      HIGH ORDER BITS TO READ MIC
K1 106262
K2 157646
K3 101234
 ENDIF
 IF 1
OUT0 0
 OME 76,NDASW; EXC 377; JMP OUT0.
OUT1 0
 OME 76,1NDASW; EXC 377; JMP OUT1.
OUT2 0
 OME 76,2NDASW; EXC 377; JMP OUT2.
OUT3 0
 OME 76,3NDASW; EXC 377; JMP OUT3.
 ENDIF
 IF 1&8
OUT4 0
 OME 76,4NDASW; EXC 377; JMP OUT4.
OUT5 0
 OME 76,5NDASW; EXC 377; JMP OUT5.
OUT6 0
 OME 76,6NDASW; EXC 377; JMP OUT6.
OUT7 0
 OME 76,7NDASW; EXC 377; JMP OUT7.
 ENDIF
 IF 1
IN0 0
 IME 76,ASDW; EXC 377; JMP IN0.; OME 77,ZDET; JMP IN0.
IN1 0
 IME 76,1ASDW; EXC 377; JMP IN1.; OME 77,1ZDET; JMP IN1.
IN2 0
 IME 76,2ASDW; EXC 377; JMP IN2.; OME 77,2ZDET; JMP IN2.
IN3 0
 IME 76,3ASDW; EXC 377; JMP IN3.; OME 77,3ZDET; JMP IN3.
 ENDIF
 IF 1&8
IN4 0
 IME 76,4ASDW; EXC 377; JMP IN4.; OME 77,4ZDET; JMP IN4.
IN5 0
 IME 76,5ASDW; EXC 377; JMP IN5.; OME 77,5ZDET; JMP IN5.
IN6 0
 IME 76,6ASDW; EXC 377; JMP IN6.; OME 77,6ZDET; JMP IN6.
IN7 0
 IME 76,7ASDW; EXC 377; JMP IN7.; OME 77,7ZDET; JMP IN7.
 ENDIF
 IF 1
RNG 0
 STA INA; LDA CLNLOK; JAZ RNG1; LDA INA; EXC 377; JMP RNG.
 ELSE
IN0 0
 STA INA; CIA 60; JAP IN0A; LDA INA; EXC 177; JMP IN0.
IN0A LDA =2; STA ASW; LDA INNORM; STA 27; LDA INA; EXC 177; JMP IN0.
IN0B 0
 STB INB; CIB 60; STA INA; LDA ASW; JAN 11*
 LLRL 1; STA ASW; LDA INA; LDB INB; EXC 177; JMP IN0B.
 LLRL 1; STA ASDW; LDA INS; STA 27; INC ,A; STA ASW
 INR INLIST; LDA INA; LDB INB; EXC 177; JMP IN0B.
IN1 0
 STA INA; CIA 60; LRLA 1; JAP IN1A; LDA INA; EXC 177; JMP IN1.
IN1A LDA =2; STA 1ASW; LDA 1INNORM; STA 25
 LDA INA; EXC 177; JMP IN1.
IN1B 0
 STB INB; CIB 60; LRLB 1; STA INA; LDA 1ASW; JAN 11*
 LLRL 1; STA 1ASW; LDA INA; LDB INB; EXC 177; JMP IN1B.
 LLRL 1; STA 1ASDW; LDA 1INS; STA 25; INC ,A; STA 1ASW
 INR 1INLIST; LDA INA; LDB INB; EXC 177; JMP IN1B.
JTAB2 +N202;+N202;+N202;+N202;+N202;+N202;+N202;+N202;+N202
 +N202;+N202;+N202;+N202;+N202;+N202;+S202
JTAB +RNG;+N201;+N201;+N201;+RNG;+N201;+N201;+N201
 +RNG;+N201;+N201;+N201;+RNG;+N201;+N201;+S201
N202 0
 STA INA; LDA 1+DASW; OAR 64; LRLA 1; STA 1+DASW
 LDA INA; INR 21; EXC 177; JMP N202.
S202 0
 OME 64,1+DASW; STA INA; LDA =JTAB2.; STA 21; LDA 1NDASW; STA 1+DASW
 INR 1OUTLST; LDA INA; EXC 177; JMP S202.
S201 0
 OME 60,DASW; STA INA
 LDA =JTAB.; STA 23; LDA NDASW; STA DASW
 INR OUTLST; LDA INA; EXC 177; JMP S201.
N201 0
 STA INA; LDA DASW; OAR 60; LRLA 1; STA DASW
 INR 23; LDA INA; EXC 177; JMP N201.
RNG 0
 STA INA; LDA DASW; OAR 60; LRLA 1; STA DASW
 INR 23; LDA CLNLOK; JAZ RNG1; LDA INA; EXC 177; JMP RNG.
 ENDIF
RNG1 COM ,A; STA CLNLOK; LDA RNG; STA CLENRT+1; EXC 377; LDA INA
: CLEANUP FOR 201 INTERRUPTS
 STA CNA; STB CNB; STX CNX
 IF 1
CLNOUT LDA OUT0; JAP BUMPT; LDA OUT1; ANA OUT2; ANA OUT3
 ENDIF
 IF 1&8
 ANA OUT4; ANA OUT5; ANA OUT6; ANA OUT7
 ENDIF
 IF 1
 JAN CLENIN; INC ,X; LDA OUT1; JAP QIO
 IXR; LDA OUT2; JAP QIO; IXR
 ENDIF
 IF 1&8
 LDA OUT3; JAP QIO; IXR; LDA OUT4
 JAP QIO; IXR; LDA OUT5; JAP QIO
 IXR; LDA OUT6; JAP QIO; IXR
 ENDIF
 IF 1
: OUTPUT INTERRUPT CLEANUP ROUTINE  LINE NUMBER IN X
QIO TXA; ADD =OUTT; TAB; COM ,A; LDB 0,6; STA 0,6
 ELSE
CLNOUT LDA OUTLST; JAP BUMPT; LDA 1OUTLST
 JAN CLENIN; INC ,X
: OUTPUT INTERRUPT CLEANUP ROUTINE  LINE NUMBER IN X
QIO COM ,A; STA OUTLST,5
 ENDIF
 LDA SWTCHO,5; JAN QOH; LRLA 1; JAN QOE; LRLA 1; JAN QOC
: OUTPUT ONES, AFTER WHICH CHECK BIT 3 OF SWITCH
: TO SEE WHETHER TO OUTPUT RESET PATTERN OR RETRANSMIT
 LDB ORC,5; JBZ QOB; DBR; STB ORC,5
QOA COM ,A; STA NDASW,5; JMP CLNOUT
QOB LRLA 1; JAN QOK; LSRA 3; ORA =20000; STA SWTCHO,5
 LDA =5253; STA NDASW,5; JMP CLNOUT
: TRANSMIT SECOND HALF OF RESET PATTERN, THEN CHECK BIT 4 OF SWITCH
: TO SEE WHETHER TO OUTPUT ONES OR LOOK FOR NEW RECORD
QOC LDA MACHNO; STA NDASW,5; LDA SWTCHO,5; LRLA 4; JAN QOJ+2
 TZA; STA SWTCHO,5; LDA =41; STA ORC,5; JMP CLNOUT
: LOOK FOR NEW RECORD TO OUTPUT
QOE LDA COS,5; ANA =3; TAB; LDA OSF,5; ANA RSF,6
 SUB MASK,6; JAZ QOF; JAN QOA; LDA =40; STA ORC,5
 LDA =10000; STA SWTCHO,5; JMP QOA
: NEW RECORD FOUND   SEND IT OUT
QOF INR COS,5; LDA OSF,5; ORA MASK+4,6; STA OSF,5; TBA
QOG LRLA 5; ADD OSEC,5; TAB; IAR; STA ORA,5
 LDA 0,6; ORA LRR,5; STA NDASW,5; STA QOVERT,5; STA QODIAG,5
 LSRA 6; ANA =37; STA ORC,5; LDA =.; STA SWTCHO,5; JMP CLNOUT
: OUTPUT NEXT WORD OF RECORD
QOH LDA ORC,5; JAZ QOJ; DAR; STA ORC,5; JAZ QOI
 LDB ORA,5; INR ORA,5; LDA 0,6; STA NDASW,5
 ERA QOVERT,5; STA QOVERT,5; LDA QODIAG,5; LRLA 1
 ERA NDASW,5; STA QODIAG,5; JMP CLNOUT
: SECOND TO LAST WORD OF RECORD
QOI LDA QOVERT,5; STA NDASW,5; JMP QOI-6
: LAST WORD OF RECORD
QOJ LDA QODIAG,5; STA NDASW,5; LDA =40000; STA SWTCHO,5; JMP CLNOUT
: CHECK FOR RETRANSMIT
QOK LDA =40000; STA SWTCHO,5; LDA OSF,5; ERA =377
 DAR; JAP QOE; LDA COS,5; ANA =3; JMP QOG
: USE IF SWITCH 2 IF LINE 0 IS 4800 BPS, SWITCH 3 IF 9600 BPS,
: NO SWITCH IF 2400 BPS
BUMPT LDA TIMEL; IF 2; ADD =2; ELSEIF 3; IAR
 ELSE; ADD =4; ENDIF
 STA TIMEL; XAZ BUMPTH; TZX; JMP QIO
BUMPTH INR TIMEH
: 201 INPUT INTERRUPT CLEANUP
 IF 1
CLENIN TZX; LDA IN0; JAP QII
 LDA IN1; ANA IN2; ANA IN3
 ENDIF
 IF 1&8
 ANA IN4; ANA IN5; ANA IN6; ANA IN7
 ENDIF
 IF 1
 JAN CLENDN; IXR; LDA IN1; JAP QII; IXR
 LDA IN2; JAP QII; IXR
 ENDIF
 IF 1&8
 LDA IN3; JAP QII; IXR; LDA IN4
 JAP QII; IXR; LDA IN5; JAP QII
 IXR; LDA IN6; JAP QII; IXR
 ENDIF
 IF 1
QII TXA; ADD =INTT; TAB; COM ,A; LDB 0,6; STA 0,6
 ENDIF
 IF #1
CLENIN TZX; LDA INLIST; JAP QII
 LDA 1INLIST; JAN CLENDN; IXR
QII COM ,A; STA INLIST,5
 ENDIF
 LDA SWTCHI,5; JAN QID; LRLA 1; JAN QIC; LRLA 1; JAN QIB
: SWITCH A IS SET BY INIT
: LOOK FOR FIRST HALF OF RESET PATTERN
QIA LDA ASDW,5; ERA =5253; JAZ 4*; JMP QIRZ
 LDA TIMEL; SUB TSLR,5; JAN 5*; SUB =2000; JAN QIRZ
: SET B SWITCH
 LDA =20000; STA SWTCHI,5; JMP QIRZ
: LOOK FOR SECOND HALF OF RESET PATTERN
: SET C SWITCH
QIB LDA =40000; STA SWTCHI,5; LDA ASDW,5; ERA MACH,5; JAZ 4*
 JMP QIRZ; LDB =7; STB LRR,5; STA ISF,5; STA CDIS,5; INR LRESET,5
 STA OSF,5; STA COS,5; STA CAOS,5; LDA TIMEL; STA TSLR,5
 LDA =4000; STA SWTCHO,5; LDA =41; STA ORC,5; JMP QIRN
: LOOK FOR FIRST WORD OF RECORD
QIC LDA ASDW,5; STA QW; ANA =74000; ERA BORI,5; DAR; JAP QIA
 LDA QW; STA QIFRST,5; STA QIVERT,5; STA QIDIAG,5
 LSRA 3; TAB; LSRA 3; ANA =37; STA IRC,5; DEC B,A
 SUB LRR,5; ANA =4; JAZ 6*
: SET D SWITCH
QICC LDA =100000; STA SWTCHI,5; JMP QIRN
 TBA; ANA =3; TAB; LDA ISF,5; ANA MASK,6; DAR; JAP QICC
: SET E SWITCH
 LDA =140000; STA SWTCHI,5; STB CIS,5
 TXA; ADD =ISEC; TAB; LDA CIS,5; LRLA 5; ADD 0,6
 STA IRA,5; TAB; LDA QW; STA 0,6; JMP QIRN
QID LRLA 1; JAN QIE
: INPUT RECORD WITHOUT STORING IT IN SECTOR
 LDA IRC,5; JAZ QIDD; DEC A,AB; STA IRC,5; LDA ASDW,5
 ERA QIVERT,5; STA QIVERT,5; LDA QIDIAG,5; LRLA 1
 ERA ASDW,5; STA QIDIAG,5; JBZ QIRZ; JMP QIRN
: LAST WORD OF RECORD   SET C SWITCH
QIDD LDA =40000; STA SWTCHI,5; LDA ASDW,5; ERA QIDIAG,5
 ORA QIVERT,5; JAZ QIF; JMP QIRN
: INPUT RECORD AND STORE IN SECTOR
QIE LDA IRC,5; JAZ QIEE; DAR; STA IRC,5
 INR IRA,5; LDB IRA,5; LDA ASDW,5; STA 0,6
 ERA QIVERT,5; STA QIVERT,5; LDA QIDIAG,5; LRLA 1
 ERA 0,6; STA QIDIAG,5; LDA IRC,5; JAZ QIRZ; JMP QIRN
: LAST WORD OF RECORD   SET C SWITCH
QIEE LDA =40000; STA SWTCHI,5; LDA ASDW,5; ERA QIDIAG,5; ORA QIVERT,5
 JAZ 5*; INR LINER,5; JMP QIRN
 LDB CIS,5; LDA ISF,5; ORA MASK,6; STA ISF,5
: PHYSICAL RECORD HAS GOOD CHECKSUMS
: CHECK ACKNOWLEDGEMENT AND SEE WHICH OUTPUT SECTORS CAN BE CLEARED
QIF LDA CAOS,5; DEC A,AB; SUB QIFRST,5; ANA =7
 JAZ QIG; STA QIVERT,5; SUB =4; JAP QIRN
 TBA; LLSR 2; LDA QIVERT,5; LLRL 2; TAB
 LDA OSF,5; ANA RSF-4,6
QIG STA OSF,5
QIRN TXA; ADD =INZD; TAB; LDA INNORM-INZD,6; JMP 6*
QIRZ TXA; ADD =INZD; TAB; LDA 0,6
 LDB INAD-INZD,6; STA 0,6; JMP CLENIN
QW 0
CLENDN LDA CNA; LDB CNB; LDX CNX
 EXC 77; INR CLNLOK; EXC 377
CLENRT JMP *
 IF 8
X E 44000
 ELSE
X E 31000
 ENDIF
 +X
BUFERS +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X
 IF 8
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
 +X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X;+X
ENDBUF 60000
 ELSE
ENDBUF 37666
 ENDIF
NBUF E ENDBUF-BUFERS
 R 1NBUF*
: 940 PORTS  BUFFER NUMBERS 0 TO 277
20;0;21;0;22;0;23;0;24;0;25;0;26;0;27;0
30;0;31;0;32;0;33;0;34;0;35;0;36;0;37;0
40;0;41;0;42;0;43;0;44;0;45;0;46;0;47;0
50;0;51;0;52;0;53;0;54;0;55;0;56;0;57;0
60;0;61;0;62;0;63;0;64;0;65;0;66;0;67;0
70;0;71;0;72;0;73;0;74;0;75;0;76;0;77;0
100;0;101;0;102;0;103;0;104;0;105;0;106;0;107;0
110;0;111;0;112;0;113;0;114;0;115;0;116;0;117;0
120;0;121;0;122;0;123;0;124;0;125;0;126;0;127;0
130;0;131;0;132;0;133;0;134;0;135;0;136;0;137;0
140;0;141;0;142;0;143;0;144;0;145;0;146;0;147;0
150;0;151;0;152;0;153;0;154;0;155;0;156;0;157;0
 IF 8
160;0;161;0;162;0;163;0;164;0;165;0;166;0;167;0
170;0;171;0;172;0;173;0;174;0;175;0;176;0;177;0
200;0;201;0;202;0;203;0;204;0;205;0;206;0;207;0
210;0;211;0;212;0;213;0;214;0;215;0;216;0;217;0
: SUPERVISORY CHANNELS 400-417
: FLOW CONTROL CHANNELS 420-437
 ELSE
: SUPERVISORY CHANNELS  300 TO 311
: FLOW CONTROL CHANNELS 312 TO 323
 ENDIF
160;1;220;2;0;0;0;0;0;0
161;6;221;7;0;0;0;0;0;0
 IF 8
: SPARES 440-737
 ELSE
: SPARES 324 TO 463
 ENDIF
0;0;0;0
0;0;0;0;0;0;0;0;0;0;0;0;0;0;0;0
0;0;0;0;0;0;0;0;0;0;0;0;0;0;0;0
0;0;0;0;0;0;0;0;0;0;0;0;0;0;0;0
0;0;0;0;0;0;0;0;0;0;0;0;0;0;0;0
0;0;0;0;0;0;0;0
0;0;0;0;0;0;0;0;0;0
0;0;0;0;0;0;0;0;0;0
 IF 8
 0;0;0;0;0;0;0;0;0;0;0;0;0;0;0;0
 0;0;0;0;0;0;0;0;0;0;0;0;0;0;0;0
 0;0;0;0;0;0;0;0;0;0;0;0;0;0;0;0
 0;0;0;0;0;0;0;0;0;0;0;0;0;0;0;0
 0;0;0;0;0;0;0;0;0;0;0;0;0;0;0;0
 0;0;0;0;0;0;0;0;0;0;0;0;0;0;0;0
 ENDIF
BP E 0
BC E 2NBUF
BF E BC+NBUF
: A FATAL ERROR HAS OCCURRED
: SAVE ERROR NUMBER AND (SOMETIMES) MARK LOCATION
: OF ROUTINE IN WHICH ERROR WAS DETECTED
CRASH 0
 EXC 77; LDA CRSHCT; INR CRSHCT; ANA =3; TAX
 LDB CRASH; STB CRSAVE,5; LDA 0,6; STA CRSAVE+4,5
 IF 8
 LDA UPDN1; JAN 4*; JMPM CR941A
 ENDIF
 LDA UPDOWN; JAN INIT; IF 5; JMPM CR10; ELSE; JMPM CR940A
 COP ,AX; DAR; JAN *-1; LDB =10000
CRCOPY LDA 0,5; OBR 36; OAR 37; LDA 11MASK; OAR 35
 INC X,AX; IBR; SUB =20000; JAN CRCOPY
 ENDIF
 IF 5
CRC10 LDA =-1200; STA W7
CRC10A INR W7; LDA W7; JAP INIT
 TZA; STA PADDR; LDB =152; JMPM LOOK
 JBZ CRC10A; COP B,A; LRLA 4; ANA =177760
 COP A,B; LDA PADDR; ANA =17; COP AB,A
 ADD =10000; TAX; LDB =17777; OME 37,ZERO
CRC10B LDA 0,6; COM A,A; OAR 36; DBR
 LDA 0,6; COM A,A; OAR 35; DEC X,AX; ORA 17MASK
 OAR 34; JBZ INIT; DBR
 JMP CRC10B
 ENDIF
INIT TZB; LDX =BUFERS+BC+NBUF; LDA =NBUF
 DAR; DXR; STB 0,5; JAP *-3; STB POWFAL
 STB SWTCHO; STB 1SWTCHO; STB 2SWTCHO; STB 3SWTCHO; STB 4SWTCHO
 IF 8
 STB 5SWTCHO; STB 6SWTCHO; STB 7SWTCHO
 ENDIF
 LDX =MICERR+1; LDA =MICERR-CLNLOK; DXR; DAR; STB 0,5; JAP *-3
 LDX =W13+1; LDA =W13-FLAG; DXR; DAR; STB 0,5; JAP *-3
 INR ACTPRT
 LDX IOTA; LDA =BR-IOTAB0-1; STB BR-IOTAB0,5; IXR; DAR; JAP *-3
 IF 1; LDA =17; OAR 77; DAR; JAP *-2; LDB =NLINES
EN JBZ SEN; DBR; TBA; LRLA 1
 ORA 4MASK; OAR 77; JMP EN
: EXC 76 RESETS OVERRUN FLIPFLOP, EXC 276 MAKES SURE THE CLOCK IS OK
SEN EXC 76; EXC 276; ENDIF; EXC 377
 IF 5
EXEC JMPM BILTEL; JMPM RMAKE; JMPM BILTEL
 ELSEIF #8
EXEC JMPM DAVTEL; JMPM RMAKE; JMPM DAVTEL
 ELSEIF 8
EXEC JMPM DAVTEL; JMPM RMAKE; JMPM DVTEL
 ENDIF
 JMPM RTD; JMPM SEC; JMP EXEC
: RECORD TEARDOWN  PROCESSES PHYSICAL RECORDS FROM 201 LINES
RTD 0
 LDA =NLINES; STA R1
: CHECK EACH LINE FOR SOMETHING TO DO
RT1 LDB BZ RTD.; DBR; STB R1
 LDA CDIS,6; ANA =3; TAX; LDA ISF,6; ANA MASK,5; JAZ RT1
: RECORD FOUND   GET SECTOR ADDRESS
 TBA; ADD =ISEC; TAB; TXA; LRLA 5; ADD 0,6; TAX; LDB R1
 IAR; LRLA 1; STA R3
 LDA 0,5; LSRA 5; ANA =76
 COM A,A; ADD =4; STA R2; LDA 0,5; LSRA 3; ANA =7; STA LRR,6
: PROCESS LOGICAL RECORD
RT2 JMPM RT7; JAZM CRASH; COM A,A; STA R4; JMPM RT7
 TAB; SUB =2; STA R5; TBA; LDB R1; ADD IOTA,6
 STA R6; TAB; LDA 0,6; ADD =BUFERS; ANA 17CMSK; TAX
: COPY OUT CHARACTERS FROM LOGICAL RECORD TO BUFFER
RT3 JMPM RT7; TAB; LSRB 2; JBZ RT4
RT3A JMPM WCI; LDA R4; JAN RT3
: DONE WITH LOGICAL RECORD
 LDA R2; JAN RT2
: DONE WITH SECTOR
 LDB R1; LDA CDIS,6; INR CDIS,6; ANA =3; TAX; LDA CMSK,5
: DISABLE INTERRUPTS TO PREVENT INTERFERENCE FROM QII
 EXC 77; ANA ISF,6; EXC 377; STA ISF,6; INR INDATA,6; JMP RTD.
RT4 TAB; LDA R5; JAN RT5; TBA; SUB =2; JAZ RT6; JAP RT9
: CHARACTER PAIR   MUST NOT BE SPLIT UP
 TBA; STB W1; JMPM WCI; JMPM RT7
: CHECK FOR CIRCUIT GOUGER
 STA W2; LSRA 2; ERA =12; SUB W1; JAP RT5-1; LDB R6
 LDA 0,6; ORA 17MASK; STA 0,6; LDB W2
RT5 TBA; JMP RT3A
: CHARACTER GOBBLER
RT6 TXA; SUB =BUFERS; JAN RT3A; JMPM RT8; LDA =2; JMP RT3A
: RT7 BUMPS R2, R3, AND R4 AND RETURNS CHARACTER FROM SECTOR
: IN A
RT7 0
 INR R2; INR R4; LDA R3; INR R3; TAB; LSRB 1; ANA =1; JAZ 6*
 LDA 0,6; ANA =377; JMP RT7.; LDA 0,6; LSRA 10; JMP RT7.
: EXPIDITE ZAPPERS AND GOBBLERS
RT8 0
 TZA; STA BC,5; STX W4; TXA; ADD =NBUF; TAX; LDA BC,5
RT8T E CFOB-FLAG
RT8T2 E RT8T+RT8T+RT8T+RT8T+RT8T+RT8T+RT8T+RT8T
 SUB =RT8T2+RT8T2; JAN RT8B; TAB
 SUB =BR-IOTAB0; JAP RT8A; ADD =BR; TAX
 LDA =40; STA BR-IOTAB0,5; TBA; JMPM BITADD
 ORA RSBF,6; STA RSBF,6
RT8A LDX W4; JMP RT8.
RT8B LDA BC,5; JMPM BITADD; JBZ RT8A
 ORA STOPIN-1,6; STA STOPIN-1,6; JMP RT8A
EMPTY 0
 LDA BC,5; JAZ EMPTY.; JMPM GCI; JMP EMPTY+1
: CIRCUIT ZAPPER
RT9 LDB R6; LDA 0,6; IAR; JAZ RT3A; DAR; ERA MASK; ADD =BUFERS
 ANA 17CMSK; STX W1; TAX; COM ,A; STA 0,6
 JMPM ZBF; LDX W1; JMPM RT8; LDA =3; JMP RT3A
R1 0 LINE NUMBER
R2 0 APPROXIMATE NUMBER OF CHARACTERS IN PHYSICAL RECORD
R3 0 CHARACTER ADDRESS IN SECTOR
R4 0 COUNT DOWN FOR LOGICAL RECORD
R5 0 VIRTUAL CHANNEL SWITCH (FIRST 2 CHANNELS DO NOT GET CHECKED)
R6 0 IOTAB ADDRESS
: RECORD MAKER   MAKES PHYSICAL RECORDS FOR 201 LINES
: W1 LINE NUMBER
: W2 NUMBER OF CHARACTERS IN PHYSICAL RECORD
: W3 CHARACTER ADDRESS IN SECTOR
: W4 GROUP INDEX
: W5 NUMBER OF GROUPS
: W6 BUFFER NUMBER   ALSO ACCUMULATER FOR RM13
: W7 LOGICAL RECORD SIZE
: W8 ADDRESS OF BEGINNING OF LOGICAL RECORD
: W9 COUNT DOWN FOR LOGICAL RECORD
: W10 BIT POSITION NUMBER
: W11 CHANNEL NUMBER
: W12 ADDRESS OF IOTAB ENTRY
: W13 FIRST HALF OF CHARACTER PAIR
RMAKE 0
 LDA =NLINES; STA W1
: CHECK EACH LINE FOR SOMETHING TO DO
RM1 LDB W1; JBZ RMAKE.; DBR; STB W1
: HOW LONG SINCE LAST RECORD MADE ON THIS LINE
 LDA TIMEL; SUB TIME1,6
: FOR 9600 BPS LINE, SUBTRACT 20 INSTEAD OF 40
: THIS CODING ASSUMES THAT ONLY ONE 9600 BPS LINE IS USED
 IF 3; JAN 10*; SUB =20; JBZ 3*; SUB =20
 ELSE; JAN 5*; SUB =40; ENDIF; JAN RM1
: DO NOT LET CAOS GET TOO FAR AHEAD OF COS
 LDA CAOS,6; SUB COS,6; SUB =2; JAP RM1
: IS THIS SECTOR OCCUPIED
 LDA CAOS,6; ANA =3; TAX; LDA OSF,6; ANA MASK,5; JAZ 4*
 JMP RM1; STA W2; TXA; LRLA 5; ADD OSEC,6
 IAR; LRLA 1; STA W3
: ARE THERE ANY CHARACTERS READY
 JMPM RM13; JAZ RM1
: PROCEED TO BUILD RECORD
RM2 LDA W5; DAR; JAN RM12; STA W5; LDB W4; INR W4
: ANY LIVE ONES IN THIS GROUP?
RM3 LDA CFOB,6; ANA PCFOB,6; ANA RSBF,6; JAZ RM2
: WE MAY HAVE ONE
 JMPM BID; STX W10; LDA PCFOB,6; ANA CMSK,5; STA PCFOB,6
: COMPUTE CHANNEL NUMBER
 TBA; LDB W1; SUB GROUPI,6; LRLA 4; COP AX,A; STA W11
: LOOK UP BUFFER NUMBER
 ADD IOTA,6; STA W12; TAX; LDA 0,5; ERA MASK
 ANA 17CMSK; ADD =BUFERS; STA W6
: CHECK FOR NULL ENTRY
 LDA 0,5; IAR; JAZ RM18
: IS BUFFER RESTRICTION COUNT AT LEAST 6
  LDA BR-IOTAB0,5; TAB; SUB =6; JAN RM7
: FIND SIZE OF LOGICAL RECORD
: HOW MUCH ROOM LEFT IN SECTOR
 SUB =52D; ADD W2; JAN 5*; COM A,A; ADD BR-IOTAB0,5; INC A,B
: HOW MANY CHARACTERS IN BUFFER
 LDX W6; TBA; SUB BC,5; JAN 3*; LDB BC,5
: TEST FOR SUPERVISORY OR FLOW RESTRICTION CHANNEL
 LDX W11; JXZ RM15; DEC X,A; JAZ RM16
RM3A STB W7; COM B,B; INC B,B; STB W9
: BUILD LOGICAL RECORD
 LDA W3; STA W8; IAR; STA W3
: STORE CHANNEL NUMBER
 JMPM RM14
: COPY CHARACTERS INTO SECTOR
RM4 LDX W6; JMPM GCI; TAX
: TEST FOR SPECIAL CHARACTER PAIRS OR CIRCUIT ZAPPER
 LSRA 2; JAZ RM10
RM5 INR W3; LDA W3; JMPM RM14; INR W9; LDA W9; JAN RM4
: DONE COPYING. NOW PLACE SIZE AT BEGINNING OF RECORD
RM6 INR W3; LDX W7; LDA W8; JMPM RM14
: ADJUST BUFFER RESTRICTION COUNT
 LDX W12; LDA BR-IOTAB0,5; SUB W7; STA BR-IOTAB0,5; SUB =6
: RM7 IS ENTERED FROM MIDDLE OF RM3 SECTION
: ALSO ENTERED FROM RM19
RM7 LDB W4; DBR; JAP RM8
: BUFFER RESTRICTION COUNT LESS THAN 6. TURN OFF RSBF
: THE NUMBER 6 IS CHOSEN FOR EFFICIENCY AND TO PREVENT BUG IN RM15
 LDX W10; LDA RSBF,6; ANA CMSK,5; STA RSBF,6
: IS THERE ROOM IN THE SECTOR FOR ANOTHER RECORD
: NOTE: SUBTLE INEFFICIENCY BUG CAUSED BY RM10
: IF 54D IS CHANGED TO 55D
RM8 LDA =54D; SUB W2; JAP RM3
: FINISH PHYSICAL RECORD
: FIND ADDRESS OF FIRST WORD OF SECTOR
RM9 LDB W1; LDA CAOS,6; ANA =3; LRLA 5; ADD OSEC,6; TAX
 LDA CAOS,6; LLSR 3; LDA W2
: NOTE: RSBF MAY HAVE BEEN TURNED ON WHEN INAPPROPRIATE,
: POSSIBLY RESULTING IN NO LOGICAL RECORDS BEING BUILT.
: IF SO, WE ABORT THIS PHYSICAL RECORD
 JAZ RMAKE.; ADD =3; LSRA 1; LLRL 6; LDB W1; ORA BORO; STA 0,5
: RMAKE IS NOW DONE WITH THIS PHYSICAL RECORD
 LDA CAOS,6; INR CAOS,6; ANA =3; TAX; LDA MASK,5
: DISABLE INTERRUPTS TO PREVENT INTERFERENCE FROM QII AND QIO
 EXC 77; ORA OSF,6; EXC 377; STA OSF,6
 LDA TIMEL; STA TIME1,6; JMP RMAKE.
: SPECIAL CHARACTER PAIR, CHARACTER GOBBLER, OR CIRCUIT ZAPPER
: THE FIRST CHARACTER MAY NOT GO IF THERE IS NOT ROOM
: FOR THE SECOND TO GO WITH IT. THIS PAIR OF CHARACTERS
: MUST NOT BE SPLIT UP
: THE FIRST 2 CHANNELS DO NOT HAVE THESE CHARACTERS
RM10 LDA W11; SUB =2; JAN RM5
 TXA; SUB =2; JAZ RM5; JAP RM17
 INR W9; LDA W9; JAP RM11; INR W3
 LDA W3; JMPM RM14
 LDX W6; JMPM GCI; TAX
: CHECK FOR CIRCUIT GOUGER
 LSRA 2; ERA =12; JAZ 4*; JMP RM5
 LDA W12,7; ORA 17MASK; STA W12,7; JMP RM5
: NO ROOM FOR SECOND CHARACTER
: PUT THE FIRST ONE BACK
RM11 LDX W6; JMPM WCD; LDA W7; DAR; STA W7; JMP RM6
: GO AROUND AGAIN IF ANY BUSINESS LEFT
RM12 LDB W1; JMPM RM13; LDB W4; COM ,AX; ADD W5
 STX PCFOB,6; IBR; DAR; JAP *-3; LDA W6; JAZ RM9; JMP RM2
: INITIALIZE W4 AND W5
: ALSO SEE IF THERE ARE ANY POSSIBILITIES FOR BUILDING LOGICAL RECORDS
RM13 0
 TZA; STA W6; LDX GROUPN,6; STX W5; LDB GROUPI,6; STB W4
 LDA CFOB,6; ANA RSBF,6; ORA W6; STA W6
 DXR; JXZ RM13.; IBR; JMP *-10
: STORE THE CHARACTER IN THE X BOX AT THE CHARACTER ADDRESS
: IN THE A BOX   ALSO BUMP W2
RM14 0
 INR W2; TAB; LSRB 1; ANA =1; JAZ 10*
 LDA 0,6; ANA =177400; COP AX,A; STA 0,6; JMP RM14.
 TXA; LRLA 10; TAX; LDA 0,6; ANA =377; COP AX,A; STA 0,6; JMP RM14.
: SUPERVISORY CHANNEL MAY SEND ONLY MULTIPLE OF 6 CHARACTER RECORDS
RM15 STB W7; COM B,A; ADD =6; JAN *-1; SUB =5; ADD W7
 JAZ RM9; TAB; JMP RM3A
: FLOW RESTRICTION CHANNEL MAY SEND ONLY FIXED RECORD SIZE
RM16 TBA; LDX W1; ADD IOTABS,5; JAZ 4*; JMP RM9; INC ,X; JMP RM3A
: CIRCUIT ZAPPER
RM17 LDX W6; JMPM ZBF; LDA W9; IAR; ADD W7; STA W7
 LDA W12,7; ADD =BUFERS; ANA 17CMSK; COM ,B; STB W12,7; TAX; JMPM EMPTY
 INR W3; LDA W3; LDX =3; JMPM RM14; JMP RM6
: A POINTER TO A NULL ENTRY HAS BEEN FOUND
: DEASSIGN ALL BUFFERS WITH SUCH A POINTER
RM18 LDA W4; ADD =CFOB-FLAG-1; LRLA 4
 ORA W10; STA W10; LDX =BUFERS+NBUF
RM19 LDA BC,5; SUB W10; JAZ RM20; INC X,AX; SUB =BUFERS+NBUF+NBUF
 JAN RM19; JMP RM7
RM20 INR RMERR; TXA; SUB =NBUF; TAX; JMPM ZBF; TBX; JMP RM19
RMERR 0
: WRITE CHARACTER AND DECREMENT
: THIS ROUTINE MAY BE CALLED AFTER A CALL TO GCI FOR THE
: SAME BUFFER TO PUT THE CHARACTER BACK, PROVIDED THAT
: THERE HAVE BEEN NO INTERVENING CALLS TO WCI
: THE BUFFER NUMBER IS IN THE X BOX
: IT IS NOT NECCESSARY TO GIVE THE CHARACTER
WCD 0
 INR BC,5; LDA BP,5; DAR; STA BP,5
 TXA; ADD =NBUF; TAX; LDA BC,5; JMPM BITADD
 ORA FLAG,6; STA FLAG,6; JMP WCD.
ZBF 0
 JMPM EMPTY; TXA; ADD =NBUF; TAB; TZA; STA BC,6; JMP ZBF.
HLTG JMPM CRASH  GCI HALT  BAD BUFER NUMBER OR EMPTY BUFER
GCI 0
 TXA; SUB =BUFERS; JAN HLTG; SUB =NBUF; JAP HLTG
 LDA BC,5; JAZ HLTG; DAR; STA BC,5; JAZ GCI3
GCI1 LDA BP,5; INR BP,5; TAB; LSRB 1; ANA =1
 JAZ GCI2; LDA 0,6; ANA =377; JMP GCI.
GCI2 LDA 0,6; LSRA 10; JMP GCI.
GCI3 STX PW1; TXA; ADD =NBUF; TAX; LDA BC,5
 TAB; LSRB 4; ANA =17; TAX; LDA FLAG,6
 ANA CMSK,5; STA FLAG,6; LDX PW1; JMP GCI1
HLTWT IAR; JAZ WCI.
HLTW JMPM CRASH  WCI HALT  BAD BUFER NUMBER
WCI 0
 ANA =377; STA PW1; TXA; SUB =BUFERS; JAN HLTWT; SUB =NBUF; JAP HLTW
 LDA BC,5; TAB; ADD BP,5; STA PW2; SUB BP+1,5
 JAZ WCI4
WCI1 JBZ 4*; JMP WCI2; STX PW3; TXA; ADD =NBUF; TAB
 LDA BC,6; JAZ WCI.; TAB; LSRB 4
 ANA =17; TAX; LDA FLAG,6; ORA MASK,5; STA FLAG,6; LDX PW3
WCI2 INR BC,5; LDA PW2; TAB; LSRB 1; ANA =1; JAZ WCI3; LDA 0,6
 ANA =177400; ORA PW1; STA 0,6; JMP WCI.
WCI3 LDA PW1; LRLA 10; STA PW1; LDA 0,6; ANA =377; ORA PW1
 STA 0,6; JMP WCI.
WCI4 STX PW3; DXR; LDA BP+1,5; SUB BP,5; SUB BC,5; SUB =7
 JAP WCI5; 5345; SUB =BUFERS-1; JAP WCI4+2; JMP WCI8
WCI5 IXR; ANA =-2; ADD =4; STA PW4; LDA PW2; IAR; LSRA 1; STA PW5
 STX PW6; LDA BP,5; TAB; SUB PW4; ANA =-2; LLSR 1; TAX
 TBA; SUB PW5; ADD =100; JAN 5*; INR PW7; JMP 3*; INR PW8
WCI6 LDA 0,6; STA 0,5; IXR; 5123; SUB PW5; JAN WCI6; LDX PW3
WCI7 LDA BP,5; SUB PW4; STA BP,5; 5345; SUB PW6; JAP WCI7; LDA PW2
 SUB PW4; STA PW2; JMP WCI12
WCI8 LDB =BUFERS+NBUF-1; LDA BP+1,6; SUB BP,6; SUB BC,6; SUB =7
 JAP WCI9; 5323; SUB PW3; JAP WCI8+1
: NO MORE BUFFER SPACE
 STA PW11+2; LDX =BUFERS; LDA BC,5; SUB PW11+2; JAN 5*
 LDA BC,5; STA PW11+2; STX PW12; INC X,AX; SUB =BUFERS+NBUF
 JAN *-11; LDX PW12; LDA =10; STA BC,5; LDA PW1
 STA PW12; INR PW11; LDA =PW11+3; STA PW11+12
 JMPM GCI; LDB PW11+12; INR PW11+12; STA 0,6
 LDA BC,5; DAR; JAP *-7; LDA PW12; STA PW1
 STX PW12; LDX PW3; JMP WCI4+1
WCI9 ANA =-2; ADD =4; STA PW4; LDA PW2; LSRA 1; STA PW5
 LDA BP,6; ADD BC,6; IBR; STB PW6; TAB; ADD PW4; ANA =-2; LLSR 1; TAX
 TBA; SUB PW5; SUB =200; JAP 5*; INR PW9; JMP 3*; INR PW10
WCI10 LDA 0,6; STA 0,5; DXR; 5323; SUB PW5; JAP WCI10
 LDX PW3; IXR 
WCI11 LDA BP,5; ADD PW4; STA BP,5; 5145; SUB PW6; JAN WCI11
WCI12 LDX PW3; LDB BC,5; JMP WCI1
PW1 0
PW2 0
PW3 0
PW4 0
PW5 0
PW6 0
PW7 0
PW8 0
PW9 0
PW10 0
PW11 0
PW12 0; R 11*
SEC 0
: RESTRICTED BUFFER DESTINATION
: FOR EACH ONE BIT, RESET RESTRICTED BUFFER COUNT
: ALSO SET ALL RESTRICTED BUFFER FLAGS
SECD LDA =NLINES; STA W1
RBD1 LDB W1; JBZ LEP; DBR; STB W1
 LDX IOTA,6; LDA 1,5; ANA 17CMSK; ADD =BUFERS; TAX
 LDA BC,5; JAZ RBD1
 STX W2; LDA IOTA,6; STA W3
 LDA IOTABS,6; STA W4; LDX GROUPI,6; LDA GROUPN,6; DAR
 COM ,B; STB RSBF,5; IXR; DAR; JAP *-3
RBD3 LDX W2; JMPM GCI; LLSR 10; LDA W3; TAX; ADD =10; STA W5
RBD4 TBA; JAP RBD5; LDA 0,5; JAP 5*
 LDA =200; JMP 3*; LDA =40; STA BR-IOTAB0,5
RBD5 LRLB 1; INC X,AX; SUB W5; JAN RBD4; STX W3
 INR W4; LDA W4; JAN RBD3; JMP RBD1
: LEPRECHAUN INPUT PROCESSOR
: CHECK STATUS OF SUPERVISOR
 IF 8
LEP LDA 400BC+BUFERS; ADD 402BC+BUFERS; ADD 404BC+BUFERS
 ADD 406BC+BUFERS; ADD 410BC+BUFERS; ADD 412BC+BUFERS
 ADD 414BC+BUFERS; ADD 416BC+BUFERS; SUB =2000
 COM ,B; JAN 15*; LDA USWICH; DAR; STB W10
 JAZ LEPD; SUB =377BUFERS
 ELSE
LEP LDA 300BC+BUFERS; ADD 302BC+BUFERS; ADD 304BC+BUFERS
 ADD 306BC+BUFERS; ADD 310BC+BUFERS; SUB =1000; COM ,B; JAN 15*
 LDA USWICH; DAR; STB W10; JAZ LEPD; SUB =277BUFERS
 ENDIF
 JAN 5*; LSRA 1; TAX; LDB CMSK+1,5; STB W10
 IF 5
 ELSE
 LDA UPDOWN; JAN LEPD; LDA SPSTAT; ADD =LEPSTB.
 STA 2*; JMP *
LEPSTB +LEPD1 INACTIVE
+LEPA ACTIVE
+LEPB INPUT RING FULL
+LEPC GOING TO SLEEP
 +LEPB INPUT RING ALMOST FULL
LEPA LDA SORING; ANA =77; ADD =1644; ADD PAGE0; STA W5
 JMPM LOOK; ERA =20; 1030; +LEPD; ERA =20; STA W1; STB W2
 INR SORING; LDA W5; ADD =100; JMPM LOOK; STA W3; STB W4
 LDA W5; ADD =200; JMPM LOOK
 ERA W1; ERA W3; ADD =177401; TAX; TBA; ERA W2; ERA W4
 IAR; 1050; 5*; JMPM CR940; JMP SECP
 LDB W1; LDA W2; LLRL 10; STB W2; ORA W3; STA W3
 LDA =20; TZB; LDX W5; JMPM IN; JANM CR940; JMP LEPE
LEPB LDA TIMEH; SUB STIME; LSRA 1; JAZ 6*; JMPM CR940; JMP SECP
 LDA SIRING; ADD =60; ANA =377; ADD =244; ADD PAGE0; JMPM LOOK
 ERA =20; 1030; 3*; JMP SECP; INC ,A; STA SPSTAT
 LDA =-40; STA SUCUNT; JMP LEPA
LEPC LDA =211; JMPM LOOK; 1030; 3*; JMP LEPD
 STA SPSTAT; LDA USWICH; DAR; JAZ 4*; JMP LEPD; STA USWICH
LEPD1 LDA MACHNO; SUB =5; JAP LEPD
 LDA =211; JMPM LOOK; JBZ LEPD; DAR; JAPM CR940; TBA
 SUB =10; JANM CR940; SUB =30; JAPM CR940
 LRLB 13; STB PAGE0; TZA; STA SORING; STA SIRING
 LDA USWICH; SUB =2; JAN LEPD2; LDA MACHNO; ADD =55500
 STA W3; TZA; JMPM LEPSEN
LEPD2 INC ,A; STA SPSTAT; STA USWICH; LDA N940L; STA W3
 LDA 101; STA W4; TZA; JMPM LEPSEN
 ENDIF
LEPD LDA GENBUF; IF 8; ANA =776; ELSE; ANA =76; ENDIF; ANA W10
 JAZ SECP; JMPM BID
 TXA; LRLA 1; IF 8; ADD =377BUFERS; ELSE; ADD =277BUFERS; ENDIF
 STA W1; TAX
 LDA =W2; STA W10; JMPM GCI; LRLA 10; STA W5; JMPM GCI
 ORA W5; STA W10,7; LDA W10; INR W10; SUB =W4; JAN *-13
LEPE LDA W2; JAP 11*
: UPSTREAM RECORD
 LDX USWICH; JXZ LEP; JMPM LEPSN; JMP LEP
: DOWNSTREAM RECORD
 ANA =7777; ERA MACHNO; JAZ LEPDIS; LDA DSWICH; JAZ LEPRET
 SUB =NLINES+1; JAP LEPRET; ASLA 1
 IF 8; ADD =400BUFERS+NLINES+NLINES; ELSE; ADD =300BUFERS+NLINES+NLINES
 ENDIF
 TAX; JMPM LEPSN; JMP LEP
: RETURN ILLEGAL DOWNSTREAM RECORD
LEPRET LDA W2; STA W4; LDA =20000; JMPM LEPSEN; JMP LEP
: LEPRECHAUN DISPATCHER TO PROCESS SUPERVISOR COMMANDS
LEPDIS LDA W2; LSRA 14; ADD =LEPTAB.; STA 2*; JMP *
LEPTAB +LEP0;+LEP1;+LEP2;+LEP3;+LEP4;+LEP5;+LEP6;+LEPRET
: TAKEOVER COMMAND
LEP0 TZA; JMPM LEPSEN; LDX W1; DXR; STX USWICH; TZA; STA BC,5
 LDA N940L; STA W3; LDA 101; STA W4; TZA; JMPM LEPSEN; JMP LEP
: PLACE 2 CHARACTERS INTO A BUFFER
LEP1 LDA W3; ADD =BUFERS; TAX; LDA W4; LSRA 10
 JMPM WCI; LDA W4; JMPM WCI; JMP LEP
: CHANGE TABLE ENTRY
: CHECK FOR VALID POINTER
LEP2 LDA W3; LSRA 12; TAB; SUB =NLINES; JAP LEP2C
 LDA W3; ANA =1776; JAZ LEPRET
LEP2B LSRA 3; ADD IOTABS,6; JAP LEPRET
: COMPUTE ENTRY ADDRESS
 LDA W3; ANA =1777; STA W3; ADD IOTA,6; TAX; STX W7; LDA W4
 ANA 17CMSK; SUB =NBUF; JAP LEPRET
 LDA 0,5; IAR; JAZ LEP2D; DAR; ANA 17CMSK
 ERA =1; ADD =BUFERS; TAX; STB W8; JMPM ZBF; LDB W8
LEP2D LDX W7; LDA W4; STA 0,5
: GET BUFFER NUMBER
 ERA MASK; ADD =BUFERS; STA W6
: COMPUTE NEW BF
 LDA GROUPI,6; ADD =CFOB-FLAG; LRLA 4; ADD W3; STA W7
 LDB W6; LDX BC,6; TBA; ADD =NBUF; TAB; JXZ LEP2A
: TURN OFF OLD BIT, TURN ON NEW BIT
 LDA BC,6; JMPM BITADD; COM A,A; ANA FLAG,6; STA FLAG,6
 LDA W7; JMPM BITADD; ORA FLAG,6; STA FLAG,6
 LDA W6; ADD =NBUF; TAB
LEP2A LDA W7; STA BC,6; LDA =100001; STA W3
 LDA =30000; JMPM LEPSEN; JMP LEP
 IF 8
LEP2C SUB =10-NLINES
 ELSE
LEP2C SUB =5-NLINES
 ENDIF
 TAX; LDA W3; ANA =1777; JXZ LEP2B; JMP LEPRET
: CHANGE CELL OR SPECIAL MESSAGE
LEP3 LDA W3
 IF 5
 JAN LEPRET
 ELSE
 JAN LEPS
 ENDIF
 SUB =DLOCK; LSRA 6; JAZ 5*; LDA DLOCK; JAP LEP
 LDA W4; STA W3,7; JMP LEP
 IF 5; ELSE
LEPS ANA =77; SUB =2; JAZ 4*; JMP LEPRET
 LDA =3; STA SPSTAT; LDX =1BUFERS; JMPM WCI; JMP LEP
 ENDIF
: REQUEST 2 CELLS
LEP4 LDA W4; STA W5; LDA W3,7; STA W4; LDA =30000; JMPM LEPSEN
 LDA W5; STA W3; LDA W3,7; STA W4; LDA =30000; JMPM LEPSEN; JMP LEP
: INPUT A MESSAGE TO THE HOST
 IF 8
LEP5 LDA W3; LSRA 10; ANA =3; TAX; STX W7
 ADD =LEP5T.; STA 2*; JMP *
LEP5T +LEP51;+LEP52;+LEP7;+LEP7
LEP51 LDA UPDOWN; JAN LEPRET; LDA ICOUNT
 JAN 7*; JMPM UPDATE; LDA UPDOWN
LEP5D1 JAN LEP; LDA ICOUNT; JAP LEP; LDA W3; ANA =377; SUB =200
 JAP LEPRET; ADD =200; JMPM BITADD
 LDA W4; LSRA 10; STA W6; SUB =11; JAZ 4*; JMP LEP5B1
LEP5A1 LDA MASK,5; ANA ACTPRT,6; JAZ LEP5C1; LDA W3
 ANA=377; LRLA 1; ADD =1BUFERS
 TAX; LDA =3; JMPM WCI; JMP LEP
LEP5C1 LDA MASK,5; ORA ACTPRT,6; STA ACTPRT,6
 LDA W4; LLSR 6; LSRB 2; LDA W3; LLSR 10
 LDA W7; ADD =LEP5CT.; STA 2*; JMP *
LEP5CT +LEP5C2;+LEP5C3
LEP5C2 LDA IRING; INR IRING; INR ICOUNT; ANA =77
 LRLA 1; ADD =50000; TAX; LDA W6; JMPM IN940
 EXC 30; JMP LEP
LEP5C3 LDA IRING1; INR IRING1; INR ICNT1
 ANA =77; LRLA 1; ADD =50000; TAX; LDA W6
 JMPM INN941; EXC 230; JMP LEP
LEP5B1 DAR; JAZ LEP5A1; ADD =5; JAZ 4*
 JMP LEPRET; LDA W3; JAN 3*; ANA =377
 STA SPORT; LDA =-40; STA SCOUNT
 JMP LEP5C1+3
LEP52 LDA UPDN1; JAN LEPRET; LDA ICNT1; JAN LEP5D1+5; JMPM UUPDT1
 LDA UPDN1; JAN LEP; LDA ICNT1; JAP LEP; JMP LEP5D1+5
 ELSE
LEP5 LDA UPDOWN; JAN LEPRET; LDA ICOUNT; JAN 12*
 ENDIF
 IF 5
 JMPM BUPDAT
 ELSEIF #8
 JMPM UPDATE
 ENDIF
 IF #8
 LDA UPDOWN; JAN LEP; LDA ICOUNT; JAP LEP
 LDA W3; ANA =377; SUB =140
 JAP LEPRET; ADD =140; JMPM BITADD; LDA W4; LSRA 10
 STA W6; SUB =11; JAZ 4*; JMP LEP5B
LEP5A LDA MASK,5
 ANA ACTPRT,6; JAZ LEP5C; LDA W3
 ANA =377; LRLA 1; ADD =1BUFERS; TAX; LDA =3
 JMPM WCI; JMP LEP
LEP5C LDA MASK,5; ORA ACTPRT,6; STA ACTPRT,6
 ENDIF
 IF 5
 LDA IRING; INR IRING; INR ICOUNT; ANA =77; LRLA 1
 ADD =160; TAX; LDA W4; ANA =177400; TAB; LDA W3
 ANA =377; COP AB,B; LDA W4; ANA =377; LLRL 20
 JMPM IN10; JMP LEP
 ELSEIF #8
 LDB W4; LRLB 10; LDA W3; LLSR 10
 LDA IRING; INR IRING; INR ICOUNT; ANA =77; LRLA 1; ADD =50000
 TAX; LDA W6; JMPM IN940; EXC 30; JMP LEP
 ENDIF
 IF #8
LEP5B DAR; JAZ LEP5A; ADD =5; JAZ 4*; JMP LEPRET
 LDA W3; JAN 3*; ANA =377; STA SPORT; LDA =-40; STA SCOUNT; JMP LEP5C+3
 ENDIF
: READ AND WRITE FROM HOST
 IF 8
LEP6 LDA W3; ANA 15MASK; JAZ LEP6A
 LDA 2LEP6T1; STA 4LEP63; LDA 3LEP6T1; STA 2LEP64
 JMP LEP6B
LEP6A LDA LEP6T1; STA 4LEP63
 LDA 1LEP6T1; JMP LEP6A-3
LEP6T1 +IN;+LOOK;+INN;+LOOOK
LEP6B LDA W3; LSRA 10; ANA =7; ADD =LEP6TB.
 ELSE
LEP6 LDA W3; LSRA 10; ANA =7; ADD =LEP6TB.
 ENDIF
 STA 2*; JMP *
LEP6TB +LEPRET;+LEP61;+LEP62;+LEP63;+LEP64
 +LEPRET;+LEPRET;+LEPRET
LEP61 LDA W4; STA LEP6LD; LDA W3
 STA LEP6MD; JMP LEP
LEP62 LDA W3; LDB W4; LLRL 10; STA LEP6HD
 COP B,A; ERA LEP6MD; ANA =177400; ERA LEP6MD
 STA LEP6MD; JMP LEP
 IF 5
LEP63 LDA LEP6LD; ANA =17; COM A,A; STA PADDR; LDA W3
 LDB W4; LLSR 13; COP B,A; ANA =160; ORA PADDR
 STA PADDR; LDA W4; ORA 17MASK; STA IPADDR
 LDA LEP6MD; LDB LEP6LD; LLSR 4; TAX
 LDA LEP6HD; LRLA 4; ANA =170000; COP AX,A
 JMPM IN; JMP LEP
 ELSE
LEP63 LDB LEP6LD; LDA LEP6MD; LDX W4
 JMPM IN; JMP LEP
 ENDIF
 IF 5
LEP64 LDA W3; LDB W4; LLSR 13; COP B,A
 ANA =160; STA PADDR; LDA W4; ANA 17CMSK
 COP A,B; JMPM LOOK; LLRL 4; STA LEP6MD
 STB LEP6LD; TBA; ANA =17; STA LEP6HD; LDA PADDR
 ANA =17; TAB; LDA LEP6LD
 ANA =177760; COP AB,A; STA LEP6LD
 JMPM LEP64A; JMPM LEP64B; JMP LEP
 ELSE
LEP64 LDA W4; JMPM LOOK; STA LEP6MD
 STB LEP6LD; JMPM LEP64A; JMP LEP
 ENDIF
LEP64A 0; LDA LEP6MD; ANA =377; ORA 10MASK
 STA W3; LDA LEP6LD; STA W4; LDA =60000; JMPM LEPSEN
 JMP LEP64A.
LEP64B 0; LDA LEP6HD; LDB LEP6MD; LLSR 10
 STB W4; ORA 11MASK; STA W3; LDA =60000; JMPM LEPSEN
 JMP LEP64B.
LEP6LD 0
LEP6MD 0
LEP6HD 0
: CRASH SUPERVISOR TYPE 7
LEP7 LDA =70000; JMPM LEPSEN; JMP LEP
: SEND RECORD UPSTREAM
LEPSEN 0
 LDX USWICH; JXZ LEPSEN.; ORA MACHNO; ORA =.
 STA W2; JMPM LEPSN; JMP LEPSEN.
: SEND A RECORD
LEPSN 0
 IF 5
 ELSE
 DEC X,B; JBZ LEPSNA
 ENDIF
 LDA =W2; STA W10; LDA W10,7; LSRA 10; JMPM WCI; LDA W10,7; JMPM WCI
 LDA W10; INR W10; SUB =W4; JAN *-12; JMP LEPSN.
 IF 5
 ELSE
LEPSNA LDA SPSTAT; SUB =2; LSRA 1; JAZ LEPSN.
 LDA =-4; STA SACTIV
 LDA SIRING; ANA =377
 ADD =644; ADD PAGE0; TAX; LDB W4; LDA W3; JMPM IN; JAN LEPSD
 TXA; ADD =400; TAX; LDA W2; LDB W3; LLRL 10; ERA W4; COM A,B
 LDA W2; LSRA 10; ERA W3; COM A,A; JMPM IN; JAN LEPSD; TXA; SUB =1000
 TAX; LDB W3; LDA W2; LLSR 10; JMPM IN; JAN LEPSD
 INR SIRING; INR SUCUNT; LDA SUCUNT; JAN LEPSN.
 LDA SIRING; ANA =377; ADD =244; ADD PAGE0; JMPM LOOK
 ERA =20; 1030; 3*; INR SFULL; LDA =-2; ADD =4; STA SPSTAT
 LDA TIMEH; STA STIME; JMP LEPSN.
LEPSD JMPM CR940; JMP LEP
 ENDIF
SECP LDA TIMEL; SUB TIMSEC; SUB =300D; JAN SEC.
 LDA TIMEL; STA TIMSEC
: HALF SECOND LOGIC
: MAYBE SEND IN TYPE 10 FOR PORT BLOCKED ON OUTPUT
 IF 8
 LDA UPDOWN; JAN SUP; TZB
TYPE10 STB W1; COM ,A; STA W2; LDA STOPO,6; ANA W2
 JAZ TYP10A; JMPM BID; LDA CMSK,5; ANA W2
 STA W2; TBA; LRLA 4; COP AX,A; LRLA 1
 STA W3; ADD =1BUFERS; TAB; LDA BC,6; SUB =40
 LDB W1; JAP TYPE10+3
 LDA ICOUNT
 JAN 7*; JMPM UPDATE; LDA ICOUNT
 JAP HSEC2-4; LDB W1; LDA CMSK,5; ANA STOPO,6; STA STOPO,6
 LDA IRING; INR IRING
 INR ICOUNT; ANA =77; LRLA 1
 ADD =50000; TAX; LDB W3; LRLB 7; LDA =10
 JMPM IN940; EXC 30; LDB W1; JMP TYPE10+3
TYP10A INC B,AB; SUB =4; JAN TYPE10
SUP LDA SACTIV; JAP HSEC2-4; INR SACTIV
 LDX =BUFERS; LDA =215; JMPM WCI
 LDA UPDN1; JAN RBS1-2; LDB =4
HSEC2 STB W1; COM ,A; STA W2; LDA STOPO,6
 ANA W2; JAZ HSEC2A; JMPM BID
 LDA CMSK,5; ANA W2; STA W2; TBA; LRLA 4
 COP AX,A; LRLA 1; STA W3; ADD =1BUFERS
 TAB; LDA BC,6; SUB =40; LDB W1
 JAP HSEC2+3
 LDA ICNT1; JAN 7*
 JMPM UUPDT1; LDA ICNT1; JAP RBS1-2
 LDB W1; LDA CMSK,5; ANA STOPO,6; STA STOPO,6
 LDA IRING1; INR IRING1; INR ICNT1
 ANA =77; LRLA 1; ADD =50000; TAX
 LDB W3; LRLB 7; LDA =10; JMPM INN941
 EXC 230; LDB W1; JMP HSEC2+3
HSEC2A INC B,AB; SUB =10; JAN HSEC2
 ELSE
 LDA UPDOWN
 ENDIF
 IF 5
 JAN RBS1-2
 ELSEIF #8
 JAN SUP
 ENDIF
 IF #8
 TZB
TYPE10 STB W1; COM ,A; STA W2; LDA STOPO,6; ANA W2; JAZ TYP10A
 JMPM BID; LDA CMSK,5; ANA W2; STA W2
 TBA; LRLA 4; COP AX,A; LRLA 1; STA W3
 ADD =1BUFERS; TAB; LDA BC,6; SUB =40; LDB W1; JAP TYPE10+3
 LDA ICOUNT; JAN 7*
 ENDIF
 IF 5
 JMPM BUPDAT
 ELSEIF #8
 JMPM UPDATE
 ENDIF
 IF #8
 LDA ICOUNT; JAP RBS1-2
 LDB W1; LDA CMSK,5; ANA STOPO,6; STA STOPO,6
 LDA IRING; INR IRING; INR ICOUNT; ANA =77; LRLA 1
 ENDIF
 IF 5
 ADD =160; TAX; LDB W3; LRLB 7; LDA =10; LLRL 10
 TZB; JMPM IN10; LDB W1; JMP TYPE10+3
 ELSEIF #8
 ADD =50000
 TAX; LDB W3; LRLB 7; LDA =10; JMPM IN940
 EXC 30; LDB W1; JMP TYPE10+3
 ENDIF
 IF #8
TYP10A INC B,AB; SUB =6; JAN TYPE10
 ENDIF
 IF 5
 ELSEIF #8
: MAYBE ACTIVATE SUPERVISOR
SUP LDA SACTIV; JAP RBS1-2; INR SACTIV
 LDX =BUFERS; LDA =215; JMPM WCI
 ENDIF
: RESTRICTED BUFFER SOURCE
: SEND A ONE BIT FOR EACH CHANNEL WHICH MAY RECEIVE MORE CHARACTERS
 LDA =NLINES; STA W1
RBS1 LDB W1; JBZ SECP4A; DBR; STB W1
 LDX IOTA,6; LDA 1,5; ANA 17CMSK; ADD =BUFERS-1; TAX
: SEE IF OUTPUT BUFFER IS EMPTY
 LDA BC,5; DAR; JAP RBS5; STX W2; LDA IOTA,6; STA W3
 LDA IOTABS,6; STA W4
RBS2 LDB 7MASK; LDA W3,7; INR W3; ADD =BUFERS; TAX
 JAN RBS4; LDA BC,5; SUB =40
: NOTE THAT BUFFER -1 IS ALWAYS "EMPTY"
RBS3 LLRL 1; TBA; JAP RBS2+1; LDX W2; JMPM WCI
 INR W4; LDA W4; JAN RBS2; JMP RBS1
RBS4 LDA BC,5; SUB =200; JMP RBS3
RBS5 INR LINSAT,6; JMP RBS1
SECP4A LDX =PW12; LDA 0,5; JAZ INT; SUB =BUFERS; STA W4; TZA; STA 0,5
 LDA =100003; STA W3; LDA =30000; JMPM LEPSEN
INT LDX =IOTABI+17; LDA 0,5; ADD =BUFERS+BC; ANA 17CMSK; STA W1
 LDA W1,7; SUB =200; LLRL 1; DEC X,AX; SUB =IOTABI
 JAP INT+1; LDA RSBF+NUMGRP-1; COP AB,A; STA RSBF+NUMGRP-1
INT1 LDA CFOB+NUMGRP-1; ANA RSBF+NUMGRP-1; JAZ SECP4
 JMPM BID; STX W1; TXA; ADD =IOTABI; STA W2; TXA; ERA MASK; ADD =IOTABI
 STA W3; LDA W3,7; ADD =BUFERS; ANA 17CMSK; STA W4; LDA W2,7
 ADD =BUFERS+1; ANA 17CMSK; TAX; JMPM GCI; STA W5
 LSRA 2; JAZ INT3; LDX W4; LDA W5
INT2 JMPM WCI; LDA BC,5; SUB =200; JAN INT1
 LDA RSBF+NUMGRP-1; LDX W1; ANA CMSK,5; STA RSBF+NUMGRP-1; JMP INT1
INT3 LDA W5; SUB =2; JAZ INT5; JAP INT4; JMPM GCI; STA W6; LDA W5
 LDX W4; JMPM WCI; LDA W6; JMP INT2
INT4 COM ,A; STA W2,7; JMPM ZBF
INT5 LDA W4; TAX; SUB =BUFERS; JAN INT1; JMPM RT8; JMP INT2-1
 IF 8
SECP4 LDA UPDOWN; JAP SECP42
: CHECK TO SEE IF HOST 1 HAS COME UP
 EXC 130  RESET MIC
 JMPM KEY; JAP SECP3; LDA UPDWN; INR UPDWN
 JAN SECP3; TZB; LDX =3; COM ,A; STA STOPIN,5
 STB STOPO,5; STB ACTPRT,5; DEC X,AX
 JAP *-5; STB UPDOWN; STB ICOUNT; STB SPSTAT
 STB IRING; STB ORING; INR ACTPRT; LDA 106
 ANA 17CMSK; STA 106; STA W4; LDA =100002; STA W3; LDA =30000
 JMPM LEPSEN
SECP3 TZB; LDX =3; STB IIN,5; DEC X,AX; JAP *-2
 LDX =176BUFERS; LDA =77; STB BC,5; DXR; DXR
 DAR; JAP *-4
SECP42 LDA UPDN1; JAP SECT
: CHECK TO SEE IF HOST 2 HAS COME UP
 EXC 330  RESET MIC
 JMPM KEEY; JAP SECP32; LDA UPDWN1
 INR UPDWN1; JAN SECP32; TZB; LDX =7
 COM ,A; STA STOPIN,5; STB STOPO,5
 STB ACTPRT,5; DEC X,AX; SUB =4; JAP *-6
 STB UPDN1; STB ICNT1; STB IRING1
 STB ORING1; LDA 110
 ANA 17CMSK; STA 110; STA W4
 LDA =100002; STA W3; LDA =30000; JMPM LEPSEN
SECP32 TZB; LDX =7; STB IIN,5; DEC X,AX; SUB =4
 JAP *-3; LDX =376BUFERS; LDA =77; STB BC,5
 DXR; DXR; DAR; JAP *-4
 ENDIF
 IF #8
SECP4 LDA UPDOWN; JAP SECT
 ENDIF
 IF 5
: CHECK TO SEE IF PDP10 HAS COME UP
 EXC 34        RESET INTERFACE
 ELSEIF #8
: CHECK TO SEE IF THE 940 HAS COME UP
 EXC 130   RESET MIC
 ENDIF; IF #8
 JMPM KEY; JAP SECP3; LDA UPDWN; INR UPDWN; JAN SECP3
 LDA TIMMIN; ADD =1; ANA =7; JAZ SECP3
 TZB; LDX =5; COM ,A; STA STOPIN,5; STB STOPO,5; STB ACTPRT,5
 DEC X,AX; JAP *-5; STB UPDOWN; STB ICOUNT
 ENDIF
 IF 5; ELSEIF #8; STB SPSTAT; ENDIF
 IF #8
 STB IRING; STB ORING; ENDIF
 IF 5; ELSEIF #8; INR ACTPRT; ENDIF; IF #8
 LDA 106; ANA 17CMSK; STA 106
 STA W4; LDA =100002; STA W3; LDA =30000; JMPM LEPSEN
SECP3 TZB; LDX =5; STB IIN,5; DEC X,AX; JAP *-2
 LDX =276BUFERS; LDA =137
 STB BC,5; DXR; DXR; DAR; JAP *-4
 ENDIF
: 4 SECOND LOGIC
SECT INR TIMMIN; LDA TIMMIN; ANA =7; DAR; JAP SEC.
 LDA UPDOWN; IF 8; JAN SECT2; ELSE; JAN SECT1; ENDIF
 IF 5
 ELSE
: MAYBE ACTIVATE SUPERVISOR
 LDA MACHNO; SUB =5; JAP CHECK9-2
 LDA USWICH; DAR; JAZ 10*; LDA TIMMIN; ANA =37
 JAZ 4*; JMP CHECK9-2; LDX =BUFERS
 LDA =376; 1300; 4*; SUB =161; JMP 5*; JSS3 3*; SUB =171; JMPM WCI
 ENDIF
: CHECK TO SEE IF THE HOST IS STILL UP
 LDA =-10; STA W1
CHECK9 JMPM KEY; JAZ CK9B
 IF 5
 JAN SECT1
 JMPM CR10
 ELSEIF #8
 JAN SECT1-1
 JMPM DOWN9
 ENDIF
 IF 8
 JAN SECT2-1; JMPM DOWN9; JMP SECT2; ELSE
 JMP SECT1
 ENDIF
CK9B INR W1; LDA W1; JAN CHECK9; JMP CHECK9+6
 IF 5
 ELSE
 EXC 30
 ENDIF
 IF 8
SECT2 LDA UPDN1; JAN SECT1; LDA =-10; STA W1
CK92 JMPM KEEY; JAZ CK9B2; JAN SECT1-1
 JMPM DOWN91; JMP SECT1
CK9B2 INR W1; LDA W1; JAN CK92; JMP CK92+6
 EXC 230
 ENDIF
: 16 SECOND LOGIC
SECT1 LDA TIMMIN; ANA =37; DAR; JAP SEC.
 LDA UPDOWN; IF 8; JAN SECT4; ELSE; JAN SECW-1; ENDIF
 IF 5
 TZA; STA PADDR; LDB =151
 ELSE
 LDA =210
 ENDIF
 JMPM LOOK; TBA; ERA UNHAP; STB UNHAP
 IF 8; JAZ +SECT4; JBZ +SECT4; ELSE; JAZ +SECW-1; JBZ +SECW-1; ENDIF
 STB W4; LDA 106; ANA =77
 LRLA 6; ORA =100010; STA W3; LDA =30000; JMPM LEPSEN
 IF 8
SECT4 LDA UPDN1; JAN SECW-1; LDA =210; JMPM LOOOK
 TBA; ERA UNHAP1; STB UNHAP1; 1030; +SECW-1; STB W4
 LDA 110; ANA =77; LRLA 6; ORA =100010
 STA W3; LDA =30000; JMPM LEPSEN
 ENDIF
 LDX =NLINES
SECW DXR; LDB IOTA,5; LDA =200; STA BR-IOTAB0+1,6
 LDB GROUPI,5; LDA RSBF,6; ORA 1MASK; STA RSBF,6
 LDA LRESET,5; JAZ SECW2; TZA; STA LRESET,5; LDA =100004
SECW1 STA W3; STX W4; STX W9
 LDA IOTA,5; ADD =2; STA W10; LDA IOTABS,5
 LRLA 3; ANA =-10; ADD =2; STA W11
 LDX IOTA,5; LDA 0,5; ANA 17CMSK; ADD =BUFERS-1; TAX; JMPM EMPTY
SECW1A LDA W10,7; INC A,B; JBZ SECW1B; ERA MASK; ADD =BUFERS
 ANA 17CMSK; TAX; JMPM ZBF; LDA W10,7
 COM ,B; STB W10,7; ADD =BUFERS; ANA 17CMSK; TAX; TZA
 STA BC,5; LDA =3; JMPM WCI
SECW1B INR W10; INR W11; LDA W11; JAN SECW1A
 LDA =30000; JMPM LEPSEN; LDX W9; JMP SECW2A
SECW2 LDA INDATA,5; JAZ SECW3; TZA; STA INDATA,5
SECW2A JXZ SECW4; JMP SECW
SECW3 STA LINSAT,5; STA SWTCHO,5; INR INDATA,5; LDA =100005; JMP SECW1
: 256 SECOND LOGIC
SECW4 LDA TIMMIN; ANA =777; DAR; JAP SEC.
 LDX =NLINES; TZB; STB UNHAP; IF 8; STB UNHAP1; ENDIF
SECW5 DXR; LDA LINER,5; STB LINER,5; SUB =40D; JAP SECW6; LDA LINSAT,5
 STB LINSAT,5; SUB =100D; JAP 6*; JXZ SECW7; JMP SECW5
 LDB =100007; JMP 3*
SECW6 LDB =100006; STB W3; ADD =40D; ANA =1777; LRLA 6; COP AX,A
 STA W4; LDA =30000; JMPM LEPSEN
 IF 8
 JMP SECW5-4
 ELSE
 JMP SECW5-3
 ENDIF
:17 MINUTE 4 SEC LOGIC
SECW7 LDA TIMMIN; ANA =3777; DAR; JAP SEC.
 LDA MACHNO; SUB =5; JAP SEC.; 1300; 3*
 JMP SEC.; LDA =100011; STA W3
 LDA =30000; JMPM LEPSEN; JMP SEC.
 IF 5
 ELSE
: CHECK TO SEE IF THE 940 KEY IS GOOD
: THE KEY IS 40516337B, 51506262B
: IF REJECTED BY THE MIC OR IF ONE OF THE WORDS
: IS ALL ZERO, THE RETURN IN THE A REGISTER IS ZERO.
: IF THE WORDS ARE GARBAGE, THE RETURN IS GREATER
: THAN ZERO. IF THE KEY IS GOOD, THE RETURN
: IS NEGATIVE.
KEY 0
 LDA =213; JMPM LOOK; 1030; +KEY1; STB W2; STA W3; LDA =212
 JMPM LOOK; 1030; +KEY1; LLRL 10; ERA K3; TAX; LDA W3
 COP AB,A; ERA K2; COP AX,X; LDA W2; ERA K1; 1050; +KEY2; INC ,A
KEY1 COM ,B; STB UPDWN; JMP KEY.
KEY2 LDX =212; JMPM IN; DEC ,A; JMP KEY.
 ENDIF
 IF 8
KEEY 0; LDA =213; JMPM LOOOK; 1030; +KEEY1
 STB W2; STA W3; LDA =212; JMPM LOOOK
 1030; +KEEY1; LLRL 10; ERA K3; TAX
 LDA W3; COP AB,A; ERA K2; COP AX,A
 LDA W2; ERA K1; 1050; +KEEY2; INC ,A
KEEY1 COM ,B; STB UPDWN1; JMP KEEY.
KEEY2 LDX =212; JMPM INN; DEC ,A; JMP KEEY.
: ZAP BUFERS AND CLEAN UP CIRCUITS.
: TELL SUPER HOST2 IS DOWN
DOWN91 0
 LDA =200; STA W12; LDA W12; DAR; STA W12; STA W3
 SUB =100; JAN DWN9B1; JMPM ZAP; JMP DOWN91+3
DWN9B1 LDA =-3; STA UPDWN1; COM ,A; STA UPDN1
 LDA 110; ORA 17MASK; STA W4
 LDA =100002; STA W3; LDA =30000
 JMPM LEPSEN; JMP DOWN91.
 ENDIF
:ZAP BUFERS AND CLEAN
:UP CIRCUITS. TELL SUPER
:HOST IS DOWN
DOWN9 0
 IF 8; LDA =100; ELSE; LDA =140; ENDIF
 STA W12; LDA W12; DAR; JAN DOWN9B; STA W12; STA W3
 JMPM ZAP; JMP DOWN9+3
DOWN9B LDA =-3; STA UPDWN; COM ,A; STA UPDOWN
 IF 5; ELSE; LDA USWICH; LSRA 1; JAZ DOWN9A; ENDIF
 LDA UPDOWN-1; ORA 17MASK; STA W4
 LDA =100002; STA W3; LDA =30000; JMPM LEPSEN; JMP DOWN9.
 IF 5; ELSE
DOWN9A STA USWICH; JMP DOWN9.; ENDIF
BID 0          BIT IDENTIFIER
 LDX =17; JAN BID.; LRLA 1; DXR; JAP *-2; JMP BID.
 IF 5
: INTERFACE TO WILLIAM F WEIHER III TELETYPE LOGIC
: W1 GROUP NUMBER
: W2 BIT POSITION NUMBER
: W3 PORT NUMBER
: W4 DATA CHARACTER
BILTEL 0
: IS THE 10 UP
BIL1 LDA UPDOWN; JAN BILTEL.
: IS THERE ROOM IN THE INPUT RING
 LDA ICOUNT; JAN BIL3-1; JMPM BUPDAT
 LDA UPDOWN; JAN BILTEL.; LDA ICOUNT; JAP BILTEL.
: PROCESS 3 GROUPS OF INPUT PORTS
 TZX
BIL3 STX W1; LDA IIN,5; ANA ACTPRT,5; ANA STOPIN,5
 JAZ BIL6; JMPM BID; STX W2; LDA W1; LRLA 4; COP AX,A
 STA W3; LRLA 1; ADD =BUFERS; TAX; JMPM GCI; STA W4
: CHECK FOR SPECIAL HANDLING
 SUB =4; JAN BIL5
: INPUT TYPE 1 DATA CHAR
 LDA IRING
BIL4 INR IRING; INR ICOUNT; ANA =77; LRLA 1
 ADD =160; TAX; LDA 10MASK; ORA W3; LDB W4
BIL4A JMPM IN10; JMP BIL1
BIL5 ADD =2; JAP BIL4-1
: GOBBLER AND ZAPPER GOES STRAIGHT IN
: CHAR PAIR SECOND HALF SET UP
 JMPM GCI; LRLA 10; ORA W4; STA W4; JMP BIL4-1
: PROCESS NEXT GROUP
BIL6 INC X,AX; SUB =6; JAN BIL3
: PROCESS OUTPUT
BIL7 LDA ORING; INR ORING; ANA =177; LRLA 1
 ADD =361; JMPM OUT10; JAZ BILTEL.
: CHECK RANGE OF TYPE NUMBER
 STA W3; LSRA 10; SUB =11; JAN 6*; JMPM CR10
 JMP BILTEL.
: DISPATCH TO TYPE ROUTINE
 ADD =BILT.+11; STA 6*
: FIRST CHECK PORT NUMBER RANGE
 LDA W3; ANA =377; STA W3; SUB =140
 JAN *; JMPM CR10; JMP BILTEL.
BILT +BILTNG;+BILT1;+BILT2;+BILTNG;+BILTNG;+BILT5
 +BILT6;+BILT7;+BILT10
: TYPE 1 DATA CHAR
: CHECK FOR CHAR PAIR
: GOBBLER OR ZAPPER
BILT1 LDA W12; ANA =377; SUB =4; JAN BILT1B
 LDA W3; LRLA 1; ADD =1BUFERS; TAX
BILT1A LDA W3; SUB SPORT; JAZ BILT1S
 LDA W12; JMPM WCI
: CHECK FOR FULL BUFFER
BILT1E LDA BC,5; SUB =200; JAN BIL7
: BUFFER FULL HAVE WE SAID STOP
 LDA W3; JMPM BITADD; ANA STOPO,6
 JAZ 4*; JMP BIL7
: TELL THE 10 TO STOP OUTPUTTING
: ON THIS PORT
 LDA STOPO,6; ORA MASK,5; STA STOPO,6
 LDA IRING; INR IRING; INR ICOUNT
 ANA =77; LRLA 1; ADD =160; TAX
 LDA W3; ORA =3400; TZB; JMP BIL4A
BILT1B ADD =2; JAZ BILT1C; JAP BILT1D
: SPECIAL CHAR PAIR MUST BE SENT TOGETHER
 LDA W3; LRLA 1; ADD =1BUFERS; TAX
 LDA W12; JMPM WCI; LDA W12; LSRA 10
 JMPM WCI; JMP BILT1E
: CHAR GOBBLER CLEARS BUFFER
: AND SETS RSBF AND BR
BILT1C LDA W3; LRLA 1; ADD =1BUFERS; TAX
 JMPM RT8; JMP BILT1A+4
: CIRCUIT ZAPPER
BILT1D JMPM ZAP; JMP BIL7
ZAP 0
 LDA W3; JMPM BITADD; ORA STOPIN,6; STA STOPIN,6
 LDA ACTPRT,6; ANA CMSK,5; STA ACTPRT,6
 LDA W3; LRLA 1; ADD =1BUFERS; TAX
 JMPM RT8; LDA =3; JMPM WCI; LDA =100000
 STA W3; TXA; SUB =BUFERS; STA W4; LDA =30000
 JMPM LEPSEN; JMP ZAP.
: SEND TO SUPERVISOR
BILT1S LDA SCOUNT; JAP BILT1A+4
 LDA W12; ANA =370
 JAZ BILT1A+4; INR SCOUNT
 LDA W12; ANA =377; STA W4
 ANA =177; SUB =55; JAZ BILTS2
BILTS1 TXA; SUB =BUFERS; STA W3; LDA =10000
 JMPM LEPSEN; JMP BIL7
BILTS2 DEC ,A; STA SPORT; JMP BILTS1
: TYPE 2 ALL DONE
BILT2 LDA ORING; DAR; STA ORING; JMP BILTEL.
: TYPE 5 SUPERVISOR REQUEST
BILT5 LDB =2400; JMP BILT6A
: TYPE 6 HOST STATUS
BILT6 LDA W12; ANA =377; JAZ 6*; LDA 106
 ORA 17MASK; JMP 4*; LDA 106; ANA 17CMSK
 STA 106; LDB =3000; STA W3
BILT6A LDA W12; ANA =377; COP AB,A; STA W4
 LDA =50000; JMPM LEPSEN; JMP BIL7
: TYPE 7 STOP INPUTTING
BILT7 LDA W3; JMPM BITADD; COM A,A; ANA STOPIN,6; STA STOPIN,6
 JMP BIL7
: TYPE 10 CANCEL TYPE 7
BILT10 LDA W3; JMPM BITADD; ORA STOPIN,6; STA STOPIN,6; JMP BIL7
: UNIMPLEMENTED TYPE
BILTNG JMPM CR10; JMP BILTEL.
: CRASH 10
CR10A 0; EXC 034; LDA =100030; STA IPADDR; TZA
 STA PADDR; LDB CR10; JMPM IN; LDA =100150; STA IPADDR
 TZA; STA PADDR
 LDB CR10; JMPM IN; JMP CR10A.
CR10 0
 LDA UPDOWN; JAN CR10.; JMPM CR10A; JMPM DOWN9; JMP CR10.
: INPUT THE 32 BITS IN AB
: PLUS 4 EXTRA BITS IN LOW
: ORDER PADDR TO PDP10 ADDRESS
: IN IPADDR (SIGN BIT ON)
: HIGH ORDER 3 ADDRESS
: BITS IN POS 4 THROUGH 6
: OF PADDR COUNTING FROM
: RIGHT. IF INTERFACE IS
: BUSY PADDR RETURNED NEGATIVE
IN 0; JSS2 INB2; SEN 137,INB2; OME 37,PADDR
 COM A,A; OAR 35; COM B,B; OBR 36
 OME 34,IPADDR; JMP IN.
INB2 COM ,X; STX PADDR; JMP IN.
: CHECK TO SEE IF PDP10
: KEY IS GOOD. THE KEY
: IS 633751506262B.
: IF REJECTED BY INTERFACE
: OR WORD IS ZERO A IS RETURNED
: ZERO. IF THE WORD IS GARBAGE
: A IS RETURNED GREATER THAN
: ZERO. IF THE KEY IS GOOD
: IT IS ZEROED AND A IS RETURNED
: NEGATIVE
KEY 0; STX KX; JSS3 KEY2-1; TZA; STA PADDR; LDB =150
 JMPM LOOK; JAZ KEY.; ERA KEY10A
 LLRL 20; ERA KEY10B; TAX; LDA PADDR
 ANA =17; ERA KEY10C; COP ABX,A
 JAZ KEY3; LDA =1; JMP KEY2
KEY3 STA PADDR; LDA =100150
 STA IPADDR; JMPM IN; COM ,A
KEY2 LDX KX; JMP KEY.
KX 0
KEY10A 146772
KEY10B 64313
KEY10C 2
: INPUT 32 BIT WORD IN AB
: INTO ADDRESS X+1 AND ITS
: COMPLEMENT INTO X
: IF REJECTED BY INTERFACE
: AFTER 4 TRIES WILL CRASH
: 10 AND A WILL RETURN 0
: INTERFACE WIRING CAUSES DATA
: BITS TO BE INVERTED
IN10 0; JSS2 IN10A+2; SEN 137,IN10A; OAR 35; OBR 36
 COM A,A; STA IW1; COM B,B; STB IW2
 TXA; ORA 17MASK; OME 37,ZERO; OAR 34
 INC A,B; LDA IW1; SEN 137,IN10B
IN10F OAR 35; OME 36,IW2; OBR 34; JMP IN10.
IN10A JMPM CR10; TZA; JMP IN10.
IN10B LDA =-4; STA W10
IN10E SEN 337,IN10C; INR W10; LDA W10; JAZ IN10D
 SEN 137,IN10E; LDA IW1; JMP IN10F
IN10C JMPM CR10; TZA; JMP IN10.
IN10D JMPM CR10; TZA; JMP IN10.
: READS WORD FROM PDP10
: 15 LOW ORDER ADDRESS BITS
: IN B, 3 HIGH ORDER ADDRESS
: BITS IN POS 4-6 OF PADDR
: RETURNS LEFTMOST BITS IN AB
: RIGHTMOST 4 BITS IN PADDR
: IF REJECTED AFTER 10 TRIES
: AB SET TO ZERO AND PADDR
: SET NEGATIVE
: CRASH ON MEMORY OUT OF RANGE
LOOK 0; JSS2 IBUSY; SEN 137,IBUSY; OME 37,PADDR
 OBR 34; LDA =-10; STA W12
LOOK1 SEN 137,OBUSY; CIA 37
 ANA =17; STA PADDR; CIA 35; CIB 36; JMP LOOK.
IBUSY COM ,A; STA PADDR; COP ,AB; JMP LOOK.
OBUSY SEN 337,LMOR; INR W12; LDA W12
 JAZ TLONG; JMP LOOK1
LMOR JMPM CR10; JMP LOOK.
TLONG JMPM CR10; JMP LOOK.
: GETS TWO WORDS OUT OF PDP10
: AND STORES THH SECOND ONE IN
: A AND W12. A THE LEFTMOST 16
: W12 THE NEXT 16 BITS. THE EXTRA
: FOUR BITS ARE NOT LOOKED AT
: IF THE FIRST WORD IS NOT
: THE LOGICAL COMPLEMENT
: OF THE SECOND CRASH 10.
: IF REJECTED FOUR TIMES
: CRASH 10. IN BOTH CASES
: A WILL BE ZERO ON RETURN
: SETS CELL TO TYPE 2
OUT10A LDX W10; LDA 11MASK; TZB; JMPM IN10
 LDA W11; 1000
OUT10 0; JSS2 OUT10B+2; SEN 137,OUT10B; OME 37,ZERO
 OAR 34; DEC A,B; STB W10; SEN 137,OUT10C
OUT10E CIA 35; CIB 36; OME 37,ZERO
 OME 34,W10; STB W12; STA W11; ANA =177400
 ERA 11MASK; JAZ OUT10-2; SEN 137,OUT10F
OUT10H CIA 35; ERA W11; INC A,B; CIA 36; ERA W12
 IAR; COP AB,A; JAZ OUT10A; JMP IDER
OUT10B JMPM CR10; TZA; JMP OUT10.
OUT10C LDA =-4; STA W11
OUT10D SEN 337,MOR; INR W11; LDA W11
 JAZ OUT10I; SEN 137,OUT10D; JMP OUT10E
OUT10F LDA =-4; STA W10
OUT10G SEN 337,MOR; INR W10; LDA W10
 JAZ OUT10I; SEN 137,OUT10G; JMP OUT10H
OUT10I JMPM CR10; TZA; JMP OUT10.
IDER JMPM CR10; TZA; JMP OUT10.
MOR JMPM CR10; TZA; JMP OUT10.
: CHECKS NEXT QUARTER OF PDP10
: INPUT RING TO SEE IF ICOUNT
: CAN BE UPDATED
: IF UNSUCCESSFUL AFTER
: 5 SECONDS
: WILL CRASH 10
BUPDAT 0
 LDA IRING; ADD =20; ANA =60; LRLA 1
 ADD =161; STA W10; TAB; TZA; STA PADDR
 JMPM LOOK; LSRA 10; SUB =2; JAZ UP2
 LDA HUNGT; JAZ UP1; ADD =3000D; SUB TIMEL
 JAP BUPDAT.; JSS3 BUPDAT.; JMPM CR10; JMP BUPDAT.
UP1 LDA TIMEL; STA HUNGT; INR FULL; JMP BUPDAT.
UP2 STA HUNGT; LDA ICOUNT; SUB =20; STA ICOUNT; JMP BUPDAT.
HUNGT 0
BITADD 0; TAB; LSRB 4; ANA =17
 TAX; LDA MASK,5; JMP BITADD.
 ELSE
: INTERFACE TO THE DAVID ALEXANDER GARDNER TELETYPE LOGIC
: W1 GROUP NUMBER
: W2 BIT POSITION NUMBER
: W3 PORT NUMBER
: W4 DATA CHARACTER
DAVTEL 0
: IS THE 940 UP
DAV1 LDA UPDOWN; JAN DAVTEL.
: IS THERE ROOM IN THE INPUT RING
 LDA ICOUNT; JAN DAV3-1; JMPM UPDATE
 LDA UPDOWN; JAN DAVTEL.; LDA ICOUNT; JAP DAVTEL.
: PROCESS 3 GROUPS OF INPUT PORTS
 TZX
DAV3 STX W1; LDA IIN,5; ANA ACTPRT,5; ANA STOPIN,5; JAZ DAV6; JMPM BID
 STX W2; LDA W1; LRLA 4; COP AX,A; STA W3; LRLA 1; ADD =BUFERS; TAX
 JMPM GCI; STA W4
: CHECK FOR SPECIAL HANDLING
 SUB =4; JAN DAV5
: INPUT TYPE 1 CHARACTER
 LDA IRING
DAV4 INR IRING; INR ICOUNT; ANA =77; LRLA 1; ADD =50000; TAX
 LDA W3; LRLA 10; LDB W4; COP AB,B; INC ,A
DAV4A JMPM IN940; EXC 30; JMP DAV1
DAV5 ADD =2; JAZ DAV4-1; JAN DAV5A
: PROTECT SUPERVISOR FROM ZAPPERS
 LDA W3; DAR; JAP DAV4-1; LDA MACHNO; SUB =5; JAN DAV1; JMP DAV4-1
: CHARACTER PAIR   INPUT SECOND CHARACTER FIRST
DAV5A JMPM GCI; LDB W3; LRLB 10; COP AB,B; LDA IRING; IAR
 ANA =77; LRLA 1; ADD =50000; TAX; INC ,A; JMPM IN940; JAN DAVTEL.
 LDA IRING; INR IRING; INR ICOUNT; JMP DAV4
: PROCESS NEXT GROUP
 ENDIF
 IF 8
DAV6 INC X,AX; SUB =4; JAN DAV3
 ELSEIF #5
DAV6 INC X,AX; SUB =6; JAN DAV3
 ENDIF
 IF #5
: PROCESS OUTPUT
DAV7 LDA ORING; INR ORING
 ANA =177
 LRLA 1; ADD =50200; JMPM OUT
 JAN DAVTEL.
: CHECK RANGE OF TYPE NUMBER
 SUB =11; JAN 6*; JMPM CR940; JMP DAVTEL.
: DISPATCH TO TYPE ROUTINE
 ADD =DAVT.+11; STA DAVT1-5
: FIRST CHECK PORT NUMBER RANGE
 LDA W12; LSRA 10; STA W3; JAZM KLUDGE
 SUB =100; JAN *; JMPM CR940; JMP DAVTEL.
: TYPE 1 DATA CHARACTER
: CHECK FOR CHARACTER PAIR,
: CHARACTER GOBBLER, OR CIRCUIT ZAPPER
DAVT1 LDA W12; ANA =377; SUB =4; JAN DAVT1B
 LDA W3; LRLA 1; ADD =1BUFERS; TAX
DAVT1A LDA W3; SUB SPORT; JAZ DAVT1S; LDA W12; JMPM WCI
: CHECK FOR FULL BUFFER
 LDA BC,5; SUB =200; JAN DAV7
: BUFFER FULL   HAVE WE SAID STOP
 LDA W3; JMPM BITADD; ANA STOPO,6; JAZ 4*; JMP DAV7
: TELL THE 940 TO STOP OUTPUTING ON THIS PORT
 LDA STOPO,6; ORA MASK,5; STA STOPO,6; LDA IRING; INR IRING; INR ICOUNT
 ANA =77; LRLA 1; ADD =50000; TAX; LDB W3; LRLB 10; LDA =7
 JMP DAV4A
DAVT1B ADD =2; JAZ DAVT1C; JAP DAVT1D
: SPECIAL CHARACTER PAIR WHICH MUST BE SENT TOGETHER
 LDA W12; STA W4; LDA ORING; INR ORING
 ANA =177
 LRLA 1
 ADD =50200; JMPM OUT; JAN DAVTEL.
: CHECK FOR TYPE 1
 DAR; JAZ 6*; JMPM CR940; JMP DAVTEL.
: CHECK FOR SAME PORT NUMBER
 LDA W12; LSRA 10; ERA W3; JAZ 6*; JMPM CR940; JMP DAVTEL.
 LDA W3; LRLA 1; ADD =1BUFERS
 TAX; LDA W4; JMPM WCI; JMP DAVT1A+4
: KLUDGE FOR RESTARTING SUPERVISORS
KLUDGE 0
 LDA MACHNO; SUB =5; JAP KL1; LDA W12; ANA =377
 SUB =210; JAZ 5*
KL1 TZA; JMP KLUDGE.
: CONTROLLING FORK OF SUPERVISOR IS ABOUT TO RELEASE PAGE ZERO
 LDA =3; STA SPSTAT; LDX =1BUFERS; JMPM WCI; JMP KL1
: CHARACTER GOBBLER   CLEAR BUFFER AND SET RSBF AND BR
DAVT1C LDA W3; LRLA 1; ADD =1BUFERS; TAX; JMPM RT8; JMP DAVT1A+4
: CIRCUIT ZAPPER
DAVT1D JMPM ZAP; JMP DAV7
: INITIATE CIRCUIT ZAPPER
ZAP 0
 LDA W3; JMPM BITADD; ORA STOPIN,6; STA STOPIN,6; LDA ACTPRT,6
 ANA CMSK,5; STA ACTPRT,6; LDA W3; LRLA 1; ADD =1BUFERS
 TAX; JMPM RT8; LDA =3; JMPM WCI
 LDA =100000; STA W3; TXA; SUB =BUFERS; STA W4
 LDA =30000; JMPM LEPSEN; JMP ZAP.
: SEND TO SUPERVISOR
DAVT1S LDA SCOUNT; JAP DAVT1A+4; LDA W12; ANA =370
 JAZ DAVT1A+4; INR SCOUNT; LDA W12
 STA W4; ANA =177; SUB =55; JAZ DAVTS2
DAVTS1 TXA; SUB =BUFERS; STA W3; LDA =10000; JMPM LEPSEN; JMP DAV7
DAVTS2 DEC ,A; STA SPORT; JMP DAVTS1
: TYPE 2   ALL DONE
DAVT2 LDA ORING; DAR; STA ORING; JMP DAVTEL.
: TYPE 5 SUPERVISOR REQUEST
DAVT5 LDA =5; JMP DAVT6A
: TYPE 6 HOST STATUS
DAVT6 LDA W12; ANA =377; JAZ 6*; LDA 106; ORA 17MASK
 JMP 4*; LDA 106; ANA 17CMSK; STA 106; STA W3; LDA =6
DAVT6A LDB W12; LRLB 10; LLRL 10; STA W4
 LDA =50000; JMPM LEPSEN; JMP DAV7
: TYPE 7   STOP INPUTING
DAVT7 LDA W3; JMPM BITADD; COM A,A; ANA STOPIN,6; STA STOPIN,6; JMP DAV7
: TYPE 10   CANCEL TYPE 7
DAVT10 LDA W3; JMPM BITADD; ORA STOPIN,6; STA STOPIN,6; JMP DAV7
: UNIMPLEMENTED TYPE
DAVTNG JMPM CR940; JMP DAVTEL.
: DISPATCH TABLE
DAVT +DAVTNG;+DAVT1;+DAVT2;+DAVTNG;+DAVTNG;+DAVT5;+DAVT6;+DAVT7;+DAVT10
: CLEAR THE MIC AND ATTEMPT TO CRASH THE 940
CR940A 0; EXC 130; LDX =74; LDA =21; LDB =100214; JMPM IN
 LDX =216; LDB CR940; JMPM IN; LDX =212; JMPM IN
 JMP CR940A.
CR940 0
 LDA UPDOWN; JAN CR940.; JMPM CR940A; JMPM DOWN9; JMP CR940.
BITADD 0
 TAB; LSRB 4; ANA =17; TAX; LDA MASK,5; JMP BITADD.
: READS WORD ADDRESSED BY A
: RETURNS WORD RIGHT JUSTIFIED IN AB
: IF REJECTED BY MIC, AB=0
LOOK1 OAR 36; OME 35,RD940; COP ,AB; SEN 137,LOOK2; SEN 137,LOOK2
 SEN 137,LOOK2; JMP LOOK.
LOOK2 CIA 35; CIB 37; 1000
LOOK 0
 SEN 137,LOOK1; COP ,AB; JMP LOOK.
: GETS 2 WORDS OUT OF THE 940 AND STORES THE SECOND ONE INTO
: A AND W12   LOW ORDER 16 BITS IN W12
: REQUIRES ADDRESS IN A
: WILL TRY 4 TIMES IF REJECTED BY MIC AFTER WHICH MICRJC
: WILL BE BUMPED
: IF FIRST WORD IS NOT LOGICAL COMPLEMENT OF SECOND, MICERR
: WILL BE BUMPED
: ON BOTH ERRORS, 940 WILL BE CRASHED, UPDOWN AND STAYDN SET
: TO -1, AND A WILL BE NEGATIVE ON RETURN
OUT91 INC A,B; OBR 36; OME 35,RD940; STA W10; COM ,B
 SEN 137,OUT92; LRLA 20; SEN 137,OUT92; JMP OUT93
OUT92 OAR 36; CIA 35; IME 37,W12; OME 35,RD940; STA W11
 SUB =2; JAZ OUT-2; SEN 137,OT5; LRLA 4; SEN 137,OT5
OUT93 LDA W10; INR W9; LDB W9; LSRB 2; JBZ OUT+3; INR MICRJC
OUT94 JMPM CR940; JMP OUT.
OT5 CIA 35; ERA W11; ERA =377; TAX; CIA 37; OBR 37
 LDB W10; OBR 36; OME 35,OUTN; IBR; ERA W12; IAR; 1050; +OT6
 INR MICERR; JMP OUT94
OT6 SEN 137,OT7; LRLA 20; SEN 137,OT7; JMP OUT94-1
OT7 OBR 36; OAR 37; OME 35,OUTP; LDA W11; 1000
OUT 0
 TZB; STB W9; SEN 137,OUT91; JMP OUT93+1
OUTN 1375
OUTP 1002
: INPUT THE 24 BIT WORD RIGHT JUSTIFIED IN AB INTO THE
: 940 AT THE ADDRESS CONTAINED IN X
: IF MIC IS BUSY, A IS NEGATIVE ON RETURN
IN91 OBR 37; TXB; OBR 36; ANA =377; ORA 11MASK; OAR 35; 1000
IN 0
 SEN 137,IN91; COM ,A; JMP IN.
: INPUTS THE 24 BIT WORD RIGHT JUSTIFIED IN AB INTO THE 940
: AT (X)+1 AND ITS COMPLEMENT AT (X)
: IF REJECTED BY THE MIC, WILL TRY 4 TIMES AFTER WHICH 940 WILL
: BE CRASHED, MICRJC WILL BE BUMPED, AND A WILL BE NEGATIVE ON RETURN
IN940 0
 STA W11; TZA; STA W10; STB W12; LDA W11; COM A,A; COM B,B
 JMPM IN; IXR; JAN IN940A; LDA W11; LDB W12; JMPM IN; JAP IN940.
IN940A INR W10; LDB W10; LSRB 2; JBZ 7*; INR MICRJC; JMPM CR940
 JMP IN940.; DXR; LDB W12; JMP IN940+5
: CHECKS NEXT QUARTER OF 940 INPUT RING TO SEE IF
: IT CAN UPDATE ICOUNT. IF UNSUCCESSFUL AFTER ONE QUARTER SECOND
: IT WILL CRASH 940
UPDATE 0
 LDA IRING; ADD =20; ANA =60; LRLA 1; ADD =50001; STA W10; JMPM LOOK
 SUB =2; JAZ UP2; LDA TIMEL; STA W9; INR FULL
UP1 EXC 30; LDA W10; JMPM LOOK; SUB =2; JAZ UP2
 LDA TIMEL; SUB W9; SUB =150D; JAN UP1
 JMPM CR940; JMP UPDATE.
UP2 LDA ICOUNT; SUB =20; STA ICOUNT; JMP UPDATE.
 ENDIF
 IF 8
: INTERFACE TO THE DVID ALEXANDER GARDNER TELETYPE LOGIC
: W1 GROUP NUMBER
: W2 BIT POSITION NUMBER
: W3 PORT NUMBER
: W4 DATA CHARACTER
DVTEL 0
: IS THE 941 UP
DV1 LDA UPDN1; JAN DVTEL.
: IS THERE ROOM IN THE INPUT RING
 LDA ICNT1; JAN DV3-1; JMPM UUPDT1
 LDA UPDN1; JAN DVTEL.; LDA ICNT1; JAP DVTEL.
: PROCESS 3 GROUPS OF INPUT PORTS
 LDX =4
DV3 STX W1; LDA IIN,5; ANA ACTPRT,5; ANA STOPIN,5; JAZ DV6; JMPM BID
 STX W2; LDA W1; LRLA 4; COP AX,A; STA W3; LRLA 1; ADD =BUFERS; TAX
 JMPM GCI; STA W4
: CHECK FOR SPECIAL HANDLING
 SUB =4; JAN DV5
: INPUT TYPE 1 CHARACTER
 LDA IRING1
DV4 INR IRING1; INR ICNT1; ANA =77; LRLA 1; ADD =50000; TAX
 LDA W3; LRLA 10; LDB W4; COP AB,B; INC ,A
DV4A JMPM INN941; EXC 230; JMP DV1
DV5 ADD =2; JAZ DV4-1; JAN DV5A
: PROTECT SUPERVISOR FROM ZAPPERS
 LDA W3; DAR; JAP DV4-1; LDA MACHNO; SUB =5; JAN DV1; JMP DV4-1
: CHARACTER PAIR   INPUT SECOND CHARACTER FIRST
DV5A JMPM GCI; LDB W3; LRLB 10; COP AB,B; LDA IRING1; IAR
 ANA =77; LRLA 1; ADD =50000; TAX; INC ,A; JMPM INN941; JAN DVTEL.
 LDA IRING1; INR IRING1; INR ICNT1; JMP DV4
: PROCESS NEXT GROUP
DV6 INC X,AX; SUB =10; JAN DV3
: PROCESS OUUTPUT
DV7 LDA ORING1; INR ORING1
 ANA =177
 LRLA 1; ADD =50200; JMPM OUUT
 JAN DVTEL.
: CHECK RANGE OF TYPE NUMBER
 SUB =11; JAN 6*; JMPM CR941; JMP DVTEL.
: DISPATCH TO TYPE ROUTINE
 ADD =DVT.+11; STA DVT1-5
: FIRST CHECK PORT NUMBER RANGE
 LDA W12; LSRA 10; STA W3
 SUB =200; JAN *; JMPM CR941; JMP DVTEL.
: TYPE 1 DATA CHARACTER
: CHECK FOR CHARACTER PAIR,
: CHARACTER GOBBLER, OR CIRCUIT ZAPPER
DVT1 LDA W12; ANA =377; SUB =4; JAN DVT1B
 LDA W3; LRLA 1; ADD =1BUFERS; TAX
DVT1A LDA W3; SUB SPORT; JAZ DVT1S; LDA W12; JMPM WCI
: CHECK FOR FULL BUFFER
 LDA BC,5; SUB =200; JAN DV7
: BUFFER FULL   HAVE WE SAID STOP
 LDA W3; JMPM BITADD; ANA STOPO,6; JAZ 4*; JMP DV7
: TELL THE 941 TO STOP OUUTPUTING ON THIS PORT
 LDA STOPO,6; ORA MASK,5; STA STOPO,6; LDA IRING1; INR IRING1; INR ICNT1
 ANA =77; LRLA 1; ADD =50000; TAX; LDB W3; LRLB 10; LDA =7
 JMP DV4A
DVT1B ADD =2; JAZ DVT1C; JAP DVT1D
: SPECIAL CHARACTER PAIR WHICH MUST BE SENT TOGETHER
 LDA W12; STA W4; LDA ORING1; INR ORING1
 ANA =177
 LRLA 1
 ADD =50200; JMPM OUUT; JAN DVTEL.
: CHECK FOR TYPE 1
 DAR; JAZ 6*; JMPM CR941; JMP DVTEL.
: CHECK FOR SAME PORT NUMBER
 LDA W12; LSRA 10; ERA W3; JAZ 6*; JMPM CR941; JMP DVTEL.
 LDA W3; LRLA 1; ADD =1BUFERS
 TAX; LDA W4; JMPM WCI; JMP DVT1A+4
: CHARACTER GOBBLER   CLEAR BUFFER AND SET RSBF AND BR
DVT1C LDA W3; LRLA 1; ADD =1BUFERS; TAX; JMPM RT8; JMP DVT1A+4
: CIRCUIT ZAPPER
DVT1D JMPM ZAP; JMP DV7
: SEND TO SUPERVISOR
DVT1S LDA SCOUNT; JAP DVT1A+4; LDA W12; ANA =370
 JAZ DVT1A+4; INR SCOUNT; LDA W12
 STA W4; ANA =177; SUB =55; JAZ DVTS2
DVTS1 TXA; SUB =BUFERS; STA W3; LDA =10000; JMPM LEPSEN; JMP DV7
DVTS2 DEC ,A; STA SPORT; JMP DVTS1
: TYPE 2   ALL DONE
DVT2 LDA ORING1; DAR; STA ORING1; JMP DVTEL.
: TYPE 5 SUPERVISOR REQUEST
DVT5 LDA =5; JMP DVT6A
: TYPE 6 HOST STATUS
DVT6 LDA W12; ANA =377; JAZ 6*; LDA 110; ORA 17MASK
 JMP 4*; LDA 110; ANA 17CMSK; STA 110; STA W3; LDA =6
DVT6A LDB W12; LRLB 10; LLRL 10; STA W4
 LDA =50000; JMPM LEPSEN; JMP DV7
: TYPE 7   STOP INPUTING
DVT7 LDA W3; JMPM BITADD; COM A,A; ANA STOPIN,6; STA STOPIN,6; JMP DV7
: TYPE 10   CANCEL TYPE 7
DVT10 LDA W3; JMPM BITADD; ORA STOPIN,6; STA STOPIN,6; JMP DV7
: UNIMPLEMENTED TYPE
DVTNG JMPM CR941; JMP DVTEL.
: DISPATCH TABLE
DVT +DVTNG;+DVT1;+DVT2;+DVTNG;+DVTNG;+DVT5;+DVT6;+DVT7;+DVT10
: CLEAR THE MIC AND ATTEMPT TO CRASH THE 941
CR941A 0; EXC 330; LDX =74; LDA =21; LDB =100214; JMPM INN
 LDX =216; LDB CR941; JMPM INN; LDX =212; JMPM INN
 JMP CR941A.
CR941 0
 LDA UPDN1; JAN CR941.; JMPM CR941A; JMPM DOWN91; JMP CR941.
: READS WORD ADDRESSED BY A
: RETURNS WORD RIGHT JUSTIFIED IN AB
: IF REJECTED BY MIC, AB=0
LOOOK1 OAR 32; OME 31,RD940; COP ,AB; SEN 133,LOOOK2; SEN 133,LOOOK2
 SEN 133,LOOOK2; JMP LOOOK.
LOOOK2 CIA 31; CIB 33; 1000
LOOOK 0
 SEN 133,LOOOK1; COP ,AB; JMP LOOOK.
: GETS 2 WORDS OUUT OF THE 941 AND STORES THE SECOND ONE INTO
: A AND W12   LOW ORDER 16 BITS IN W12
: REQUIRES ADDRESS IN A
: WILL TRY 4 TIMES IF REJECTED BY MIC AFTER WHICH MICRJ2
: WILL BE BUMPED
: IF FIRST WORD IS NOT LOGICAL COMPLEMENT OF SECOND, MICER2
: WILL BE BUMPED
: ON BOTH ERRORS, 941 WILL BE CRASHED, UPDN1 AND STAYDN SET
: TO -1, AND A WILL BE NEGATIVE ON RETURN
OUUT91 INC A,B; OBR 32; OME 31,RD940; STA W10; COM ,B
 SEN 133,OUUT92; LRLA 20; SEN 133,OUUT92; JMP OUUT93
OUUT92 OAR 32; CIA 31; IME 33,W12; OME 31,RD940; STA W11
 SUB =2; JAZ OUUT-2; SEN 133,OUUT5; LRLA 4; SEN 133,OUUT5
OUUT93 LDA W10; INR W9; LDB W9; LSRB 2; JBZ OUUT+3; INR MICRJ2
OUUT94 JMPM CR941; JMP OUUT.
OUUT5 CIA 31; ERA W11; ERA =377; TAX; CIA 33; OBR 33
 LDB W10; OBR 32; OME 31,OUUTN; IBR; ERA W12; IAR; 1050; +OUUT6
 INR MICER2; JMP OUUT94
OUUT6 SEN 133,OUUT7; LRLA 20; SEN 133,OUUT7; JMP OUUT94-1
OUUT7 OBR 32; OAR 33; OME 31,OUUTP
 LDA W12; ORA =40000; STA W12; LDA W11; 1000
OUUT 0
 TZB; STB W9; SEN 133,OUUT91; JMP OUUT93+1
OUUTN 1375
OUUTP 1002
: INPUT THE 24 BIT WORD RIGHT JUSTIFIED IN AB INTO THE
: 941 AT THE ADDRESS CONTAINED IN X
: IF MIC IS BUSY, A IS NEGATIVE ON RETURN
INN91 OBR 33; TXB; OBR 32; ANA =377; ORA 11MASK; OAR 31; 1000
INN 0
 SEN 133,INN91; COM ,A; JMP INN.
: INNPUTS THE 24 BIT WORD RIGHT JUSTIFIED INN AB INNTO THE 941
: AT (X)+1 AND ITS COMPLEMENT AT (X)
: IF REJECTED BY THE MIC, WILL TRY 4 TIMES AFTER WHICH 941 WILL
: BE CRASHED, MICRJ2 WILL BE BUMPED, AND A WILL BE NEGATIVE ON RETURN
INN941 0
 STA W11; TZA; STA W10; TBA; SUB =40000; TAB
 STB W12; LDA W11; COM A,A; COM B,B
 JMPM INN; IXR; JAN IN941A; LDA W11; LDB W12; JMPM INN; JAP INN941.
IN941A INR W10; LDB W10; LSRB 2; JBZ 7*; INR MICRJ2; JMPM CR941
 JMP INN941.; DXR; LDB W12; JMP INN941+5
: CHECKS NEXT QUARTER OF 941 INNPUT RINNG TO SEE IF
: IT CAN UPDT1 ICNT1. IF UNSUCCESSFUL AFTER ONE QUARTER SECOND
: IT WILL CRASH 941
UUPDT1 0
 LDA IRING1; ADD =20; ANA =60; LRLA 1; ADD =50001; STA W10; JMPM LOOOK
 SUB =2; JAZ UUP2; LDA TIMEL; STA W9; INR FULL1
UUP1 EXC 230; LDA W10; JMPM LOOOK; SUB =2; JAZ UUP2
 LDA TIMEL; SUB W9; SUB =150D; JAN UUP1
 JMPM CR941; JMP UUPDT1.
UUP2 LDA ICNT1; SUB =20; STA ICNT1; JMP UUPDT1.
 ENDIF
: WARNING! IOTABLES MUST BE MULTIPLE OF 16 IN LENGTH
: FIRST CHANNEL SHOULD BE HIGH SPEED FOR EFFICIENCY
: SECOND CHANNEL MUST BE HIGH SPEED TO AVOID
: LOOPING BUG IN RMAKE CAUSED BY COUNTDOWN OF BR
NLINES E 2
IOTAB0 100301;100313
-1;-1;-1;-1;-1;-1;-1
-1;-1;-1;-1;-1;-1;-1
-1;-1;-1;-1;-1;-1;-1;-1
-1;-1;-1;-1;-1;-1;-1;-1
IOTAB1 100303;100315
-1;-1;-1;-1;-1;-1;-1
-1;-1;-1;-1;-1;-1;-1
-1;-1;-1;-1;-1;-1;-1;-1
-1;-1;-1;-1;-1;-1;-1;-1
-1;-1;-1;-1;-1;-1;-1;-1
-1;-1;-1;-1;-1;-1;-1;-1
IOTAB2 E *
IOTAB3 E *
IOTAB4 E *
: INTERNAL TABLE FOR INTERNAL CIRCUITS
IOTABI -1;-1;-1;-1;-1;-1;-1;-1;-1;-1;-1;-1;-1;-1;-1;-1
BR E *
: THE FOLLOWING IS A DUMMY DATA STATEMENT TO CAUSE
: THE ASSEMBLER TO ISSUE A MULTIPLY DEFINED CELL DIAGNOSTIC
: IF NOT ENOUGH ROOM HAS BEEN RESERVED FOR
: THE BUFFER RESTRICTION COUNTS
: BETWEEN BR AND OSEC
 IF 8
 R *+15732-BR+IOTAB0-BR+20
 ELSE
 R *+12400-BR+IOTAB0-BR+20
 ENDIF
 0 READ PRECEEDING COMMENT

 a 	X