MODULE TATTLE(DOLLAR,ENTRIES=($TATTL),
        FSAVE,TIMING,TIMER=EXTERNAL(SIX12))=
BEGIN

REQUIRE RDEFS.BLI[7,107355];
REQUIRE RSDEFS.BLI[7,107355];
REQUIRE CDEFS.BLI[7,107355];
REQUIRE UDEFS.BLI[7,107355];




UNDECLARE $TATTL;

GLOBAL ROUTINE $TATTL(ERRNUM,RNAME,CT)=
   BEGIN
   %$TATTL PRINTS A MESSAGE INDICATING WHICH
   ERROR IN A REPORT ITEM,SOURCE LINE LENGTH,ETC
   HAS CAUSED $TATTL TO BE CALLED
   
   THE SECOND PARAMETER IS THE NAME OF THE SOURCE OR REPORT
   WHICH HAS BEEN INJURED

   THE THIRD PARAMETER IS THE LENTH OF THE NAME

   NOTICE THAT WE DO THE RIGHT THING ON THE CO-ROUTINE RETURN
   (AND ARE AMONG THE FIRST TO DO SO)

   /JS..................3/29/75
   %

   BIND MESS = PLIT (
PLIT ASCIZ 'PROCEDURE EXECUTION IS BEING GRACEFULLY TERMINATED.',
PLIT ASCIZ 'AN ATTEMPT HAS BEEN MADE TO USE AN OUT-OF-RANGE ',
PLIT ASCIZ 'PAGE.NUM IN REPORT ',
PLIT ASCIZ 'PAGE.SIZE IN REPORT ',
PLIT ASCIZ 'LINE.NUM IN REPORT ',
PLIT ASCIZ 'TOP.MARGIN IN REPORT ',
PLIT ASCIZ 'BOTTOM.MARGIN IN REPORT ',
PLIT ASCIZ 'FOOTING.SIZE IN REPORT ',
PLIT ASCIZ 'HEADING.SIZE IN REPORT ',
PLIT ASCIZ 'LINE.LENGTH IN REPORT ',
PLIT ASCIZ 'LINE.LENGTH IN SOURCE ');

   MAP PDB$ RPDB$;
   MACHOP XCT = #256;
   BIND CALLI = #47;
   MACRO MAKEOP(OP,REG,ADDR)=(OP^27+REG^23+ADDR)&;
   LOCAL INST;
   LOCAL SC$ SB,RB$ RB,RSB$ RS;
   $UAPSZ((.MESS[1])<36,7>);  %PRINT 'ATTEMPT'%

   $UOUTL();

   $UAPSZ((.MESS[.ERRNUM])<36,7>);


   $UAPSC((.RNAME)<36,7>,.CT); %PRINT NAME%

   $UOUTL(); %RETURN%

   $UAPSZ((.MESS[0])<36,7>); %NICE MESSAGE SAYING BYE-BYE%
   $UOUTL();

   %NOW CLOSE IT ALL%


SB _ .RPDB$[PD$SCBF];   %FIRST SCB%
RB _ .RPDB$[PD$RB];    %FIRST REPORT BLOCK%
RS _ .RPDB$[PD$RSBF];  %FIRST RELATION SPECIFICATION BLOCK%


% Close chain of source files (sequential input) %

WHILE .SB  NEQ  SCV$NULL
DO
    BEGIN
    IF .SB[SC$FCB] NEQ SCV$NULL THEN $TCLSI(.SB); %CLOSE IT ONLY IF OPENED%
    SB _ .SB[SC$NEXT];
    END;


% Close chain of report files (sequential output) %

WHILE .RB  NEQ  RBV$NULL
DO
    BEGIN
    %IF THE FILE HAS NEVER BEEN OPENED, DON'T CLOSE IT%
    IF .RB[RB$FCB] NEQ RBV$NULL THEN $TCLSO(.RB);
    RB _ .RB[RB$NEXT];
    END;


% Close chain of relations via each relation specification block %

WHILE .RS  NEQ  RSV$NULL
DO
    BEGIN
    $RABRT (.RS);
    IF NOT (.RS[RS$LOCAL]) THEN $RPURG(.RS); %PURGE LOCAL RELATIONS%
    RS _ .RS[RS$NXT];
    END;


   INST_MAKEOP(CALLI,0,#12);
   XCT(0,INST);
END;


END ELUDOM
