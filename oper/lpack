	COMMON A(0/80,0/150)
	DIMENSION NAME(3)
	TYPE 1
	ACCEPT 2,IDEV
	IF(IDEV.NE.5) GO TO 5
	TYPE 3
	ACCEPT 100,(NAME(J),J=1,3)
	TYPE 4
	ACCEPT 200,IND
	TYPE 6
	ACCEPT 300,L,IE,IG,N
	GO TO 10
5	TYPE 7
	ACCEPT 8,INPUT
	CALL IFILE(IDEV,INPUT)
	READ(IDEV,100)(NAME(J),J=1,3)
	READ(IDEV,200) IND
	READ(IDEV,300) L,IE,IG,N
10	M=L+IE+IG
	IB=M+N+IG+1
	IW=M
	Q=.01
	TYPE 1093
	ACCEPT 1094,PREC
	IF(IND.EQ.3HMAX) GO TO 400
	IZ=1
	GO TO 500
400	IZ=-1
500	M=M-1
	IH=1
	IDECRE=0
	JOUT=3
	IC=0
	IERR=0
	ITEMP=IW+1
	IF(ITEMP.LE.80) GO TO 520
	IERR=1
	GO TO 1070
520	IF(IB.LE.150) GO TO 550
	IERR=2
	GO TO 1070
550	DO 600 I=0,ITEMP
	DO 600 J=1,IB
600	A(I,J)=0.
	DO 700 I=0,M
	IF(IDEV.NE.5) GO TO 700
	ITEMP=I+1
	TYPE 9,ITEMP
700	READ(IDEV,800) (A(I,J),J=1,N),A(I,IB)
	IF(IDEV.NE.5) GO TO 710
	TYPE 11,N
710	READ(IDEV,800) (A(IW,J),J=1,N)
	DO 900 J=1,N
900	A(IW,J)=FLOAT(IZ)*A(IW,J)
    	CALL SETUP(N,IG,IW,M,L,IE,Q,IC,IB)
	ITEMP=IG+IE
	IF(ITEMP.EQ.0) IDECRE=1
	TYPE 1000,IH,N
	IF(IG.EQ.0) GO TO  1040
	ITEMP1=N+1
	ITEMP2=N+IG
	TYPE 1010,ITEMP1,ITEMP2
1040	IF(L.EQ.0) GO TO  1050
	ITEMP1=N+IG+1
	ITEMP2=N+IG+L
	TYPE 1020,ITEMP1,ITEMP2
1050	ITEMP=IG+IE
	IF(ITEMP.EQ.0) GO TO 1055
	ITEMP1=N+IG+L+1
	ITEMP2=IB-1
	TYPE 1030,ITEMP1,ITEMP2
1055	TYPE 1090
	ACCEPT 1092,IOPT
	CALL OUTPUT(IOPT,IW,IB,IG,M,N,IZ,JOUT,IH,IDECRE)
1060   	CALL ITER(Q,IH,M,IC,IB,IW,N,IG,L,IERR,JOUT,IE,IDECRE,PREC,IZ)
1070	CALL ERROR(IERR)
	IF(IERR.GT.0) GO TO 2000
	TYPE 1080
	TYPE 1082
	ACCEPT 1092,IOPT
	CALL FINAL(IOPT,IW,IB,IG,M,N,IZ,JOUT,IH,NAME)
	TYPE 1099
1	FORMAT(/1X,'WELCOME TO THE GRAPHIC CONTROLS LP ALGORITHM.',///1X,
     +'WHERE IS YOUR DATA FILE?.',/1X,
     +'ENTER 1 FOR DISK',/1X,
     +'      9 FOR DECTAPE',/1X,
     +'      5 FOR ON-LINE INPUT.',/)
3     FORMAT(/1X,'ENTER THE NAME OF YOUR PROBLEM NOT EXCEEDING 15 CHRS.'
     +,/)
4	FORMAT(/1X,'TYPE MAX FOR MAXIMISATION MIN FOR MINIMISATION.',/)
6	FORMAT(/1X,'TYPE THE VALUES OF L,IE,IG,N ',
     +/1X,'L  = LESS THAN CONSTRAINTS ',
     +/1X,'IE = EQUALITY CONSTRAINTS',
     +/1X,'IG = GREATER THAN CONSTRAINTS',
     +/1X,'N  = NO OF DECISION VARIABLES.',/)
7	FORMAT(/1X,'ENTER THE NAME OF THE FILE.',/)
2	FORMAT(I)
8	FORMAT(A5)
9	FORMAT(/1X,'ENTER THE COEFFICIENTS IN CONSTRAINT NO ',I3,/)
11	FORMAT(/1X,'ENTER THE ',I3,' COEFFICIENTS IN THE OBJECTIVE .',/)
100	FORMAT(3A5)
200	FORMAT(A3)
300	FORMAT(4I)
800	FORMAT(6F)
1000	FORMAT(1X,'YOUR VARIABLES',8X,I3,' THRO ',I3)
1010	FORMAT(1X,'SURPLUS VARIABLES',5X,I3,' THRO ',I3)
1020	FORMAT(1X,'SLACK VARIABLES',7X,I3,' THRO ',I3)
1030	FORMAT(1X,'ARTIFICIAL VARIABLES',2X,I3,' THRO ',I3)
1080	FORMAT(1X,'YOUR PROBLEM HAS BEEN SUCCESSFULLY SOLVED.',/)
1082	FORMAT(1X,'DO YOU WANT THE TABLEAU AS WELL AS THE SOLUTION?.',/,
     +1X,'TYPE SOLN , BOTH OR NONE.',/)
1090	FORMAT(/1X,'DO YOU WANT THE INITIAL TABLEAU OR BASIS?',/,
     +1X,'TYPE BASIS ,TABLE ,BOTH OR NONE.',/)
1092	FORMAT(A5)
1093	FORMAT(/1X,'ENTER THE VALUE OF PRECISION YOU WANT TO USE.',
     +/1X,'A RECOMMENDED VALUE IS BETWEEN 1.E-5 AND 1.E-8.NOTE THAT THE',
     +,/1X,'VALUE SHOULD BE ENTERED IN E FORMAT.',/)
1094	FORMAT(E)
2000	TYPE 1099
1099	FORMAT(//)
	END
	SUBROUTINE SETUP(N,IG,IW,M,L,IE,Q,IC,IB)
	COMMON A(0/80,0/150)
	ITEMP3=M+1
	DO 100 K=1,ITEMP3
	A(K-1,N+IG+K)=1.
100	A(K-1,0)=K+N+IG
	IF(IG.NE.0) GO TO 200
	IF(IE.EQ.0) GO TO 780
	GO TO 650
200	ITEMP1=L+IE+1
	ITEMP2=M+1
	DO 300 K=ITEMP1,ITEMP2
300	A(K-1,K+N-L-IE)=-1.
650	IW=IW+1
	Q=0.
	ITEMP=N+IG
	DO 760 J=1,ITEMP
	S=0.
	ITEMP2=M-IG-IE+1
	DO 720 I=ITEMP2,M
720	S=S+A(I,J)
	A(IW,J)=-S
	IF(A(IW,J).GE.Q) GO TO 760
	Q=A(IW,J)
	IC=J
760	CONTINUE
	S=0.
	ITEMP=M-IG-IE+1
	DO 765 J=ITEMP,M
765	S=S+A(J,IB)
	A(IW,IB)=-S
780	RETURN
	END
      SUBROUTINE ITER(Q,IH,M,IC,IB,IW,N,IG,L,IERR,JOUT,IE,IDECRE,PREC,
     +IZ)
	COMMON A(0/80,0/150)
	IOPT='     '
	IPIVOT=0
	IF(Q.EQ..01) GO TO 1230
500	IF(Q.EQ.0.) GO TO 1330
510	IH=IH+1
	Q=1.E38
	R=-1.
	DO 1000 I=0,M
	IF(A(I,IC).LE.0.) GO TO 1000
	TEMP=A(I,IB)/A(I,IC)
	IF(TEMP.GT.Q) GO TO 1000
	Q=TEMP
	IF(Q.GE.0.) GO TO 520
	IERR=4
	GO TO 1360
520	R=FLOAT(I)
	IR=I
1000	CONTINUE
	IF(R.GE.-.5) GO TO 1050
	IERR=3
	GO TO 1360
1050	P=A(IR,IC)
	A(IR,0)=FLOAT(IC)
	DO 1100 J=1,IB
1100	A(IR,J)=A(IR,J)/P
	DO 1180 I=0,IW
	IF(I.EQ.IR) GO TO 1180
	DO 1170 J=1,IB
	IF(J.EQ.IC) GO TO 1170
	A(I,J)=A(I,J)-A(IR,J)*A(I,IC)
	IF((ABS(A(I,J))).GT.PREC) GO TO 1170
	A(I,J)=0.
1170	CONTINUE
1180	CONTINUE
	DO 1210 I=0,IW
1210	A(I,IC)=0.
	A(IR,IC)=1.
	IF(IOPT.EQ.5HFINAL) GO TO 1230
	ITEMP=IH-1
	TYPE 1150,ITEMP
	ACCEPT 1160,IOPT
	CALL OUTPUT(IOPT,IW,IB,IG,M,N,IZ,JOUT,IH,IDECRE)
	IF(IPIVOT.EQ.1) GO TO 1353
1230	Q=0.
	ITEMP=N+IG+L
	DO 1280 J=1,ITEMP
	IF(A(IW,J).GT.Q) GO TO 1280
	Q=A(IW,J)
	IC=J
1280	CONTINUE
	GO TO 500
1330	ITEMP=M+1
	IF(IW.NE.ITEMP) GO TO 1335
	GO TO 1350
1335	IW=IW-1
	IDECRE=1
	IF(ABS(A(IW+1,IB)).LT.PREC) GO TO 1353
	IERR=4
	GO TO 1360
1353	DO 1358 I=0,M
	TEMP=FLOAT(N+IG+L)
	IF(A(I,0).LE.TEMP) GO TO 1358
	ITEMP=N+IG+L
	DO 1359 J=1,ITEMP
	IF(A(I,J).GE.0.) GO TO 1359
	IPIVOT=1
	IR=I
	IC=J
	IH=IH+1
	GO TO 1050
1359	CONTINUE
	GO TO 1230
1358	CONTINUE
	IPIVOT=2
	GO TO 1230
1150	FORMAT(/1X,'ITERATION ',I3,' COMPLETED.',/,1X,
     +'TYPE YOUR OPTION. BASIS,TABLE,BOTH,NONE OR FINAL?.',/)
1160	FORMAT(A5)
1350	DO 1355 I=0,M
	IF(A(I,IB).GE.0.) GO TO 1355
	IERR=4
	GO TO 1360
1355	CONTINUE
	TEMP=FLOAT(N+IG+L)
	DO 1365 I=0,M
	IF(A(I,0).LE.TEMP) GO TO 1365
	IERR=5
	GO TO 1360
1365	CONTINUE
1360	RETURN
	END
	SUBROUTINE ERROR(IERR)
	IF(IERR.EQ.0) GO TO 500
	GO TO (100,200,300,400,550),IERR
100	TYPE 600
	GO TO 500
200	TYPE 700
	GO TO 500
300	TYPE 800
	GO TO 500
400	TYPE 900
	GO TO 500
550	TYPE 560
560	FORMAT(1X,'SOLUTION TO YOUR PROBLEM IS DEGENERATE.',/)
600	FORMAT(1X,'YOU HAVE TOO MANY CONSTRAINTS.',/)
700	FORMAT(1X,'YOU HAVE TOO MANY VARIABLES.',/)
800	FORMAT(1X,'SOLUTION TO YOUR PROBLEM IS UNBOUNDED.',/)
900	FORMAT(1X,'THERE IS NO FEASIBLE SOLUTION TO YOUR PROBLEM.',/)
500	RETURN
	END
	SUBROUTINE OUTPUT(IOPT,IW,IB,IG,M,N,IZ,JOUT,IH,IDECRE)
	COMMON A(0/80,0/150)
	IF(IOPT.EQ.5HNONE ) GO TO 900
	IF(IOPT.EQ.5HFINAL) GO TO 900
	IF(IOPT.EQ.5HTABLE) GO TO 200
	ITEMP=IH-1
	WRITE(JOUT,100) ITEMP
	WRITE(JOUT,120)
	DO 110 I=0,M
	IF(A(I,0).EQ.0.) GO TO 110
	ITEMP=IFIX(A(I,0))
	WRITE(JOUT,125) ITEMP,A(I,IB)
110	CONTINUE
	IF(IOPT.EQ.5HBASIS) GO TO 800
200	ITEMP=IH-1
   	WRITE(JOUT,130) ITEMP
	DO 240 I=0,IW
	ITEMP2=I+1
	ITEMP=I+1
	IF(IDECRE.EQ.1) ITEMP=I
	ITEMP1=IB-1
	IF(ITEMP.GE.IW) GO TO 219
	WRITE(JOUT,141) ITEMP2
	GO TO 220
219	WRITE(JOUT,142)
220	WRITE(JOUT,140) (A(I,J),J=1,ITEMP1)
	IF(ITEMP.GE.IW) GO TO 222
	WRITE(JOUT,143) A(I,IB)
	GO TO 240
222	WRITE(JOUT,144) A(I,IB)
240	CONTINUE
	GO TO 900
800	TEMP=-FLOAT(IZ)*A(M+1,IB)
	WRITE(JOUT,144) TEMP
141	FORMAT(/,1X,'CONSTRAINT ',I3,/,1X,14('-'),/)
142	FORMAT(/,1X,'OBJECTIVE ROW ',/,1X,13('-'),/)
143	FORMAT(/,1X,'R.H.S.VALUE ',F,/)
144	FORMAT(/,1X,'OBJECTIVE FUNCTION VALUE ',F,/)
100	FORMAT(1X,'BASIS AFTER ITERATION',I3,/,1X,25('-'))
120	FORMAT(1X,'VARIABLE',6X,'VALUE',/,1X,19('-'))
130	FORMAT(//1X,'TABLEAU AFTER',I3,' ITERATIONS',/,1X,25('-'))
140	FORMAT(5(F13.5,1X))
125	FORMAT(4X,I3,1X,F13.5)
900	RETURN
	END
	SUBROUTINE FINAL(IOPT,IW,IB,IG,M,N,IZ,JOUT,IH,NAME)
	COMMON A(0/80,0/150)
	DIMENSION NAME(1)
	IF(IOPT.EQ.5HNONE ) GO TO 900
	ITEMP=IH-1
	WRITE(JOUT,90) (NAME(I),I=1,3)
	WRITE(JOUT,100) ITEMP
	WRITE(JOUT,120)
	DO 110 I=0,M
	IF(A(I,0).EQ.0.) GO TO 110
	ITEMP3=IFIX(A(I,0))
	WRITE(JOUT,125) ITEMP3,A(I,IB)
110	CONTINUE
	OBJ=-A(IW,IB)*FLOAT(IZ)
	WRITE(JOUT,130) OBJ
	WRITE(JOUT,140)
	ITEMP1=N+1
	ITEMP2=IB-IG-1
	DO 150 J=ITEMP1,ITEMP2
150	WRITE(JOUT,125) J,A(IW,J)
	IF(IOPT.EQ.5HSOLN ) GO TO 900
	WRITE(JOUT,170) ITEMP
	DO 240 I=0,IW
	ITEMP=I+1
	ITEMP1=IB-1
	IF(ITEMP.GT.IW) GO TO  219
	WRITE(JOUT,141) ITEMP
	GO TO 220
219	WRITE(JOUT,142)
220	WRITE(JOUT,180) (A(I,J),J=1,ITEMP1)
	IF(ITEMP.GT.IW) GO TO 222
	WRITE(JOUT,143) A(I,IB)
	GO TO 240
222	WRITE(JOUT,144) A(I,IB)
240	CONTINUE
90	FORMAT(/////,10X,3A5,/,10X,15('-'))
141	FORMAT(/,1X,'CONSTRAINT ',I3,/,1X,14('-'),/)
142	FORMAT(/,1X,'OBJECTIVE ROW ',/,1X,13('-'),/)
143	FORMAT(/,1X,'R.H.S.VALUE ',F,/)
144	FORMAT(/,1X,'OBJECTIVE FUNCTION VALUE ',F,/)
100	FORMAT(1X,'SOLUTION AFTER ITERATION ',I3,/,1X,24('-'))
120	FORMAT(1X,'VARIABLE',6X,'VALUE',/,1X,19('-'))
125	FORMAT(4X,I3,1X,F13.5)
130	FORMAT(///,1X,25('*'),/1X,'OPTIMAL OBJECTIVE VALUE = ',F14.5,
     +/1X,25('*'),///////)
140	FORMAT(/1X,'DUAL VARIABLES ',/,1X,15('-'),/,1X,
     +'COLUMN  ',6X,'VALUE',/,1X,19('-'),/)
170	FORMAT(//1X,'TABLEAU AFTER ',I3,' ITERATIONS',/,1X,25('-'))
180	FORMAT(5(F13.5,1X))
900	RETURN
	END
                                                                                                                                                