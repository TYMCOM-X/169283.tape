; SUBROUTINE MACBV (NNR,IWTPTR,IBPTR(1),KVEH,ICELL(IPPOP),IBRC(1),
;    IBPT(1),ICELL(IPPAIR),ICELL(IPSING),ICELL(IPRESC),NISS,
;    IVRESC,IPRSW,IVMEAS(1),IWMEAS(1),NOTV)
; COMPUTE BASE AND VEHICLE TOTALS FOR NNR RESPONDENTS
;
; SUBROUTINE MACUNI (NNR,IWTPTR,IBPTR(1),IBRC(1),IBPT(1))
; TAB UNIVERSE FOR UNITAB RUNS WITH VEHICLES
;
; 0. NNR=# RESPONDENTS TO PROCESS
; 1. IWTPTR=WEIGHTS POP POINTER
; 2. IBPTR(1)=BASE POP POINTER
; 3. KVEH=# VEHICLES
; 4. ICELL(IPPOP)=LOC OF FIRST VEHICLE POP POINTER-1
; 5. IBRC(1)=BASE RESPONDENT COUNT
; 6. IBPT(1)=BASE PROJECTED TOTAL
; 7. ICELL(IPPAIR)=LOC OF FIRST PAIR-1
; 10. ICELL(IPSING)=LOC OF FIRST SINGLE-1
; 11. ICELL(IPRESC)=LOC OF FIRST RESPONSE COUNT-1
; 12. NISS=TOTAL #ISSUES OVER ALL VEHICLES
; 13. IVRESC=1 IF RESPONSE COUNTS REQUESTED, 0 IF NOT
; 14. IPRSW=1 IF PAIRS REQUESTED, 0 IF NOT
; 15. IVMEAS(J)=TOTAL #ISSUES SURVEYED FOR VEHICLE J
; 16. IWMEAS(J)=#ISSUES SURVEYED PER WEEK FOR VEHICLE J
; 17. NOTV=1 IF NO TV SHOWS IN RUN (I.E. ALL MAGS), 0 IF TV PRESENT
;
        TITLE   MACBV
        ENTRY   MACBV
        ENTRY   MACUNI
        W1=1
        W2=2
        W3=3
        W4=0
        W5=11
        NNR=4
        IWM=5
        IVRESC=6
        WT=7
        KVEH=10
        IPRSW=12
        WEEK1=13
        WEEK2=14
;
MACBV:  0
        MOVE    NNR,@0(16)
        MOVE    KVEH,@3(16)
        MOVE    W1,@12(16)
        MOVEM   W1,NISS
        MOVE    IVRESC,@13(16)
        MOVE    IPRSW,@14(16)
        HRRZ    W1,4(16)          ;LOC OF ICELL(IPPOP)
        HRRM    W1,MBV10 
        HRRM    W1,MBV40
        HRRM    W1,MBV110
        HRRM    W1,MBV270
        SUBI    W1,1
        HRRM    W1,MBV30          ;LOC OF ICELL(IPPOP-1)
        HRRM    W1,MBV100
        HRRM    W1,MBV320
        HRRM    W1,MBV350
        MOVE    W1,10(16)         ;LOC OF ICELL(IPSING)
        HRRM    W1,MBV50
        HRRM    W1,MBV60
        HRRM    W1,MBV120
        HRRM    W1,MBV130
        HRRM    W1,MBV280
        HRRM    W1,MBV330
        HRRM    W1,MBV360
        MOVE    W1,11(16)         ;LOC OF ICELL(IPRESC)
        HRRM    W1,MBV70
        HRRM    W1,MBV140
        HRRM    W1,MBV150
        HRRM    W1,MBV290
        HRRM    W1,MBV380
        HRRZ    W1,15(16)        ;LOC OF IVMEAS(1)
        SUBI    W1,1
        HRRM    W1,MBV260
        HRRZ    W1,16(16)        ;LOC OF IWMEAS(1)
        SUBI    W1,1
        HRRM    W1,MBV310
        HRRM    W1,MBV421
        HRRM    W1,MBV422
        MOVE    W1,@17(16)
        MOVEM   W1,NOTV
;
;
; MAIN LOOP - PERFORMED FOR NNR RESPONDENTS
LOOPR:  ILDB    WT,@1(16)
        ILDB    W1,@2(16)
        JUMPG   W1,MBV20         ;JUMP IF BASE TRUE
; RESPONDENT NOT IN BASE - POP & IGNORE VEHICLE BITS
        MOVE    W1,NISS
MBV10:  ILDB    W2,.-.(W1)
        SOJG    W1,MBV10 
        JUMPA   0,NEXTR
; RESPONDENT IN BASE
MBV20:  AOS     0,@5(16)      ;ADD TO RESPONDENT COUNT
        ADDM    WT,@6(16)     ;ADD TO PROJECTED TOTAL
        SKIPN   0,NOTV
        JUMPA   0,MBV250
; PROCESS RUNS WITH MAGAZINES ONLY, NO TV
        JUMPN   IPRSW,MBV90   ;JUMP IF PAIRS REQUESTED
; SINGLES ONLY
        MOVE    W1,NISS      ;W1 POINTS TO ISSUES POP POINTERS
        MOVE    W2,KVEH      ;W2 POINTS TO VEHICLES
MBV30:  ILDB    WEEK1,.-.(W1)
MBV40:  ILDB    WEEK2,.-.(W1)
        CAIE    WEEK1,0
MBV50:  ADDM    WT,.-.(W2)   ;ADD TO SINGLE
        CAIE    WEEK2,0
MBV60:  ADDM    WT,.-.(W2)   ;ADD TO SINGLE
        JUMPE   IVRESC,MBV80   ;JUMP IF NO RESPONSE COUNTS WANTED
; RESPONSE COUNTS
        ADD     WEEK1,WEEK2
MBV70:  ADDM    WEEK1,.-.(W2)
MBV80:  SUBI    W1,2         ;POINT TO NEXT VEHICLE'S ISSUES
        SOJG    W2,MBV30     ;JUMP IF MORE VEHICLES LEFT
NEXTR:  SOJG    NNR,LOOPR    ;JUMP IF MORE RESPONDENTS LEFT
RETURN: JRA     16,20(16)
; SINGLES AND PAIRS
MBV90:  MOVEI   W1,2          ;W1 POINTS TO VEHICLE POP POINTERS
        MOVEI   W2,1          ;W2 POINTS TO VEHICLES I.E.=J FOR V. J
        MOVE    W4,7(16)      ;LOC OF ICELL(IPPAIR)
        HRRM    W4,MBV180
MBV100: ILDB    WEEK1,.-.(W1)
MBV110: ILDB    WEEK2,.-.(W1)
        MOVEM   WEEK1,IWK1-1(W2)
        MOVEM   WEEK2,IWK2-1(W2)
; ADD TO SINGLES
        CAIE    WEEK1,0
MBV120: ADDM    WT,.-.(W2)
        CAIE    WEEK2,0
MBV130: ADDM    WT,.-.(W2)
        JUMPE   IVRESC,MBV160
; RESPONSE COUNTS
MBV140: ADDM    WEEK1,.-.(W2)
MBV150: ADDM    WEEK2,.-.(W2)
; PAIRS
MBV160: MOVE    W3,W2        ;W3 POINTS TO PRECEDING VEHICLES
        HRRZ    W4,MBV180
        ADD     W4,W3
        SUBI    W4,1
        HRRM    W4,MBV180    ;SET UP ADDRESSES FOR PAIR STORAGE
        HRRM    W4,MBV200
        HRRM    W4,MBV210
; SELF-PAIR
        MOVE    W4,WEEK1
        IOR     W4,WEEK2
        CAIE    W4,0
MBV180: ADDM    WT,.-.(W3)
        SOJE    W3,MBV220          ;JUMP IF DOING FIRST VEHICLE
; PAIRS
MBV190: MOVE    W4,WEEK1
        IOR     W4,IWK1-1(W3)
        CAIE    W4,0
MBV200: ADDM    WT,.-.(W3)
        MOVE    W4,WEEK2
        IOR     W4,IWK2-1(W3)
        CAIE    W4,0
MBV210: ADDM    WT,.-.(W3)
        SOJG    W3,MBV190    ;JUMP IF MORE OTHER VEHICLES LEFT
MBV220: ADDI    W1,2             ;MOVE TO NEXT VEHICLE
        ADDI    W2,1
        CAMG    W2,KVEH
        JUMPA   0,MBV100         ;JUMP IF MORE VEHICLES LEFT
        JUMPA   0,NEXTR
;
; PROCESS RUNS INVOLVING TV
;
MBV250: JUMPN   IPRSW,MBV300    ;JUMP IF PAIRS REQUESTED
; SINGLES ONLY
        MOVE    W1,NISS
        MOVE    W2,KVEH
MBV260: MOVE    W3,.-.(W2)      ;IVMEAS(J) TO W3
MBV270: ILDB    WEEK1,.-.(W1)      ;POP ISSUES FOR THIS VEHICLE
        JUMPE   WEEK1,MBV295
MBV280: ADDM    WT,.-.(W2)      ;ADD TO SINGLE
        CAIE    IVRESC,0
MBV290: AOS     0,.-.(W2)          ;ADD TO RESPONSE COUNT
MBV295: SUBI    W1,1            ;MOVE TO NEXT ISSUE
        SOJG    W3,MBV270       ;JUMP IF MORE ISSUES LEFT
        SOJG    W2,MBV260       ;JUMP IF MORE VEHICLES LEFT
        JUMPA   0,NEXTR         ;GO TO NEXT RESPONDENT
; SINGLES AND PAIRS
MBV300: MOVEI   W1,2            ;W1 POINTS TO ISSUE POP POINTERS
        MOVEI   W2,1            ;W2 POINTS TO VEHICLE BEING PROCESSED
        MOVE    W4,7(16)        ;LOC OF ICELL(IPPAIR)
        HRRM    W4,MBV400
MBV310: MOVE    W3,.-.(W2)      ;PICK UP IWMEAS(J)
        MOVEM   W3,IWM
        SETZM   0,IWK1-1(W2)
        SETZM   0,IWK2-1(W2)
        SETZ    W5,
MBV320: ILDB    WEEK1,.-.(W1)      ;POP WEEK 1 ISSUES
        JUMPE   WEEK1,MBV340
        AOS     0,IWK1-1(W2)      ;ADD TO WEEK 1 EXPOSURE SUM
MBV330: ADDM    WT,.-.(W2)      ;ADD TO SINGLE
MBV340: ADD     W1,IWM          ;POINT TO WEEK 2
MBV350: ILDB    WEEK2,.-.(W1)
        SUB     W1,IWM
        IOR     WEEK1,WEEK2     ;SELF-PAIR INCREMENT TO WEEK1
        ADD     W5,WEEK1        ;W5 CONTAINS SELF-PAIR SUM FOR VEHICLE
        JUMPE   WEEK2,MBV370
        AOS     0,IWK2-1(W2)
MBV360: ADDM    WT,.-.(W2)      ;ADD TO SINGLE
MBV370: ADDI    W1,1            ;POINT TO NEXT ISSUE
        SOJG    W3,MBV320       ;JUMP IF MORE ISSUES LEFT
        ADD     W1,IWM
        JUMPE   IVRESC,MBV390
; RESPONSE COUNTS
        MOVE    W3,IWK1-1(W2)
        ADD     W3,IWK2-1(W2)
MBV380: ADDM    W3,.-.(W2)
; PAIRS
MBV390: MOVE    W3,W2
        HRRZ    W4,MBV400
        ADD     W4,W3
        SUBI    W4,1
        HRRM    W4,MBV400      ;SET UP PAIR STORAGE ADDRESSES
        HRRM    W4,MBV430
; SELF-PAIR
        MOVE    W4,W5
        JUMPE   W5,MBV410      ;JUMP IF SELF-PAIR=0 FOR RESPONDENT
        IMUL    W5,WT
MBV400: ADDM    W5,.-.(W3)
MBV410: SOJE    W3,MBV440      ;JUMP IF FIRST VEHICLE
; PAIR=WK1(J)+WK2(J)+WK1(K)+WK2(K)-WK1(J)*WK1(K)-WK2(J)*WK2(K)
MBV420: MOVE    W5,W4
        ADD     W5,IWK1-1(W3)
        ADD     W5,IWK2-1(W3)
        JUMPE   W5,MBV435        ;JUMP IF PAIR IS ZERO
        MOVE    W5,IWK1-1(W2)    ;W5_WK1(J)
        ADD     W5,IWK2-1(W2)    ;WK2(J)
MBV421: IMUL    W5,.-.(W3)       ;*IWMEAS(K)
        MOVE    WEEK1,IWK1-1(W3)   ;WK1(K)
        ADD     WEEK1,IWK2-1(W3)   ;WK2(K)
MBV422: IMUL    WEEK1,.-.(W2)    ;*IWMEAS(J)
        ADD     W5,WEEK1
        MOVE    WEEK1,IWK1-1(W2)    ;WK1(J)
        IMUL    WEEK1,IWK1-1(W3)    ;WK1(K)
        SUB     W5,WEEK1
        MOVE    WEEK2,IWK2-1(W2)    ;WK2(J)
        IMUL    WEEK2,IWK2-1(W3)    ;WK2(K)
        SUB     W5,WEEK2
        IMUL    W5,WT
MBV430: ADDM    W5,.-.(W3)
MBV435: SOJG    W3,MBV420      ;JUMP IF MORE OTHER VEHICLES LEFT
MBV440: ADDI    W2,1           ;MOVE TO NEXT VEHICLE
        CAMG    W2,KVEH
        JUMPA   0,MBV310      ;JUMP IF MORE VEHICLES LEFT
        JUMPA   0,NEXTR       ;MOVE TO NEXT RESPONDENT
;
; UNIVERSE
;
MACUNI: 0
        MOVE    NNR,@0(16)
MUN10:  ILDB    WT,@1(16)
        ILDB    W1,@2(16)
        JUMPE   W1,MUN20
        AOS     0,@3(16)
        ADDM    WT,@4(16)
MUN20:  SOJG    NNR,MUN10
        JRA     16,5(16)
;
;
NISS:   0
NOTV:   0
IWK1:   BLOCK   ^D100
IWK2:   BLOCK   ^D100
;
        END
