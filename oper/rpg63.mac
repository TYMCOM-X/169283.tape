VNO==63         ;VERSION NUMBER
;DEBFLAG==1      ;COMMENT OUT WHEN YOU WANT TO PUT IT IN THE SYSTEM
DEFINE TITL(V),<
TITLE COMPILE -- CCL CONTROL CUSP-- VERSION V
VCOMPIL=XWD V,013>

        TITL    \VNO
IFDEF DEBFLAG,<DEBMAIL==1>;TO DEBUG MAIL STUFF
;PROGRAM TO COMPILE, LOAD, EXECUTE AND DEBUG USER PROGRAMS
SUBTTL WEIHER/CLEMENTS/RCC/PMH/NGP - 17 JUN 69 - V013-4 MODIFIED 16-OCT-69 BY RPG
;TYMEX COMMANDS ADDED THROUGH VERSION 46 BY V.E.VAN VLEAR - DEC 15, 1971
;VERSION 47 FIXES /CREF AND ,/SWITCH, BUGS -RPG
;VERSION 50 ALLOWS QUESTION MARK AS WILD CHARACTER - RPG
;VERSION 51 ADDS THE FDEBUG COMMAND - RPG

;THE ORIGINAL VERSION OF
;THIS PROGRAM WAS WRITTEN AT THE STANFORD UNIVERSITY
;ARTIFICIAL INTELLIGENCE LABORATORY BY WILLIAM F. WEIHER.
;MR. WEIHER'S COOPERATION, AND THAT OF THE A-I LABORATORY,
;ARE GRATEFULLY ACKNOWLEDGED.
;
;CONVERTED TO MACRO SOURCE LANGUAGE FROM FAIL ON
;1 NOVEMBER 68 BY R CLEMENTS
;

INTERNAL VCOMPILE       ;FOR LOADER MAP AND LIBRARY

LOC 137
EXP VCOMPILE
RELOC
IFNDEF TEMP,<TEMP==1>   ;TEMP=1 ALLOWS THE TMPCOR UUO TO BE USED

IFNDEF RUNSW,<RUNSW==1>          ;NON-ZERO TO USE THE RUN UUO
IFNDEF SYSSW,<SYSSW==0>          ;NON ZERO USES DSK [1,4] FOR RUNS

IFNDEF STANSW,<STANSW==0>        ;NON-ZERO TO INCLUDE STANFORD FEATURES
LSTRSW==1
IFNDEF LSTRSW,<LSTRSW==0>        ;NON-ZERO TO USE "LISTER" INSTEAD OF PIP
                                ;FOR TYPE AND LIST COMMANDS
IFNDEF SAVEXT,<SAVEXT=<SIXBIT /   SAV/>>        ;USE DMP FOR PDP6'S
IFE SYSSW,<SYSDEV=<SIXBIT /SYS/>>
IFN SYSSW,<SYSDEV=<SIXBIT /DSK/>>       ;USE DSK [1,1]
IFNDEF WRD4,<WRD4=1>    ;MULTIPLE WORD UFDS
TMSZ=^D11
IFN WRD4,<TMSZ=^D17
HASH=^D37>
LDAT.DATE=[POINT 12,LDAT,35]
LDAT.TIME=[POINT TMSZ,LDAT,23]

MLON
;ACS
P=17            ;PUSHDOWN POINTER
C=16            ;CHARACTERS RETURNED HERE
CS=15           ;CHARACTER STATUS BITS HERE
SVPT=14         ;POINTER TO CURRENT FILE IN LIST OF FILES (AOBJN)
SWPT=13         ;BYTE POINTER INTO SWITCH STORAGE AREA
SWCNT=12        ;NUMBER OF BYTES LEFT FOR SWITCH STORAGE
FL3=11          ;RH RANDOM FLAGS, LEFT HALF LOCAL FLAGS
FL2=10          ; RH ONE PIT PER PROCESSOR TO INDICATE
                ;CHANEL OPEN. LH PROCESSOR DECIDED UPON
IOP==7          ;PDL FOR INPUT NESTING
IOPNT==6        ;POINTER TO CURRENT INPUT FILE

T7=SVPT         ;TEMP USED BY "COUNT"
T6=SWPT         ;TEMP USE BY "MAIL",ETC
T5=5    ;ANOTHER TEMP
T4=4            ;TEMPORARY ACCUMS
T3=3
T2=2
T1=1
FL=0            ;FLAG REGISTER (LEFT HALF PERM SWITCHES)
                ;(RIGHT HALF MISC BITS)

;DEFINITIONS OF CALLI'S

DEVCHR==4
        ;BITS IN DEVCHR
        DSKBT==200000
        DTABT==4
UTPCLR==13
PVTYMX==200     ;MONITOR TYMEX BIT
TSPROP==40000  ;TYMSHARE PROPRIATORY ACCOUNT
ACCSUP==20000   ;ACCOUNT SUPEVISOR
SETPRV==-11
GETPRV==6
;FLAGS (RH OF FL)

PROCS==1        ;PROCESSOR SWITCHES SEEN
DOLOD==2        ;WE WANT TO DO LOADING
PCM1==4         ;LOCAL BIT AVAILABLE FOR ANYBODY
PCM2==10        ;LIKE PCM1
IDF==20         ;SCAN SAW AN IDENTIFIER
LODOUT==40      ;SOME OUTPUT HAS BEEN DONE TO LOADER
NUMFS==100      ;SCAN SAW A NUMBER
PRMPTF==100     ;PROMPTING MSG. TYPED
PERF==200       ;PERMANENT TYPE FLAGS
MAPST==400      ;A MAP DEVICE HAS BEN GENERATED FOR LOADER
CMDSN==1000     ;THE COMMAND SHOULD BE WRITTEN AS SVC OR EDS
INCRF==2000     ;WE ARE FINISHING CREF OUTPUT
INPRNT==4000    ;WE ARE PRINTING A CHARACTER STRING IN ERROR MSG
PIPF==10000     ;DOING SOMETHING FOR PIP
EDITF==20000    ;IN EDIT OR CREATE
CREATF==40000   ;CREATE
LPTFG==PCM2     ;/L FLAG IN DIRECTORY COMMAND
NODAT==PCM1     ;FILE FROM OTHER THAN DSK
NOLOOK==PCM2    ;LOOKUP FAILED
RECALF==200000  ;WE ARE READING A COMMAND SAVE FILE
RPGSRC==PCM1    ;DOING A COMPILE REQUEST FRO SVCM
ALTSRC==PCM2    ;NO LOOKIN ON SVED FOR FILE NAME
NDREL==400000   ;NEED TO ASSEMBLE BIN


;FLAGS (SWITCH TYPE)
LISTSW==1       ;DO LISTING
CRSW==2         ;DO A CREF
LIBSW==4        ;DO A LIBRARY SEARCH OF THIS FILE
RELSW==10       ;DO A LOAD ONLY ON THIS FILE
COMPLS==20      ;COMPILE REGARDLESS OF DATES
IMEDSW==40      ;DO THIS COMMAND INSTEAD OF JUST SETTING SWITCHES
FORWDS==100     ;THIS FILE MAY NOT EXIST YET, BUT IT WILL
NOLODS==200     ;DO NOT LOAD THIS FILE

PDL==100        ;LENGTH OF PDL
SWBK==5 ;NUMBER OF WORDS FOR SWITCHES TO PROCESSOR
LODSCT==10      ;NUMBER OF LOADER SWITCHES PER FILE ALLOWED
RFL==5          ;FILE NO. FOR RANDOM I/O
IFL==6          ;FILE NO. MAIL INPUT
HASHSZ==^D257   ;HASHING PARAMETER
MBXHSH==177     ;HASHING PARAMETER FOR MAILBOX
ENTSIZ==12      ;USER DIR. ENTRY SIZE


;FLAGS IN RH OF FL3

MAP2==1         ;DO A MAP
INLIB==2        ;CURRENTLY IN /L MODE FOR LOADER
PARFLG==4       ;ARGUMENT TO A SWITCH SURROUNDED BY PARENS
LINKB==10       ;SHOULD PUT OUT A /> OR /= TO LOADER
SLINKB==20      ;IT SHOULD BE A /=
CHNBT==40       ;SHOULD PUT OUT A /< TO LOADER
CHN2BT==100     ;WE HAVE PUT OUT CHAIN NAME
REWBT==200      ;LOAD DEVICE SHOULD REWIND
CNSBT==400      ;NO SYMBOLS TO CREF
CNMBT==1000     ;NO MACROS TO CREF
COPBT==2000     ;OPCODES TO CREF
CNLBT==4000     ;NO LISTING TO CREF
CCMBT==10000    ;COMBINE CREFS
CMONBT==20000   ;CREF FOR MONITOR (USE /Q)
TYMX==40000     ;TYMEX FLAG
SPCOM==100000   ;SPECIAL COMMANDS (ONLY BY CTEST)
FDBGSW==200000  ;FDEBUG COMMAND IN PROGRESS
LNKSW==400000   ;LINK IN PROGRESS
ALLCRF==CNSBT!CNMBT!COPBT!CNLBT!CCMBT!CMONBT

;DEFINITIONS OF WORDS IN THE LUD

LDPPN==0                ;PPN FOR USER
LAUD==1         ;AUN FOR USER
LBITS==2        ;BITS (RUN BIT, WATCH BITS, ETC)
LLINK==2        ;HAS SIZE OF ENTRY IN RIGHT 7 BITS
LPRV==3         ;PRV BITS
LHUN==4         ;HASHED USER NAME
REPEAT 0,< THE MACRO PROCESS DEFINES DETAILS ABOUT THE VARIOUS
        PROCESSORS WHICH COMPILE IS EXPECTED TO HANDLE BY CALLING
        THE MACRO X WHICH IS REDEFINED TO PRODUCE THE INFORMATION REQUIRED.
        THE ARGUMENTS ARE SWITCH NAME,EXTENSION,PROCESSOR NAME
        <OPTIONAL>EXTENSION OF NEXT PROCESSOR IF MUST BE PROCESSED MORE
        THAN ONCE (AS COBOL),EXTENSION PRODUCED

        USE F4 OR F40 AS APPROPRIATE FOR THE SYSTEM
>

DEFINE  MESS(T),<JRST [OUTSTR [ASCIZ "T"]
                JRST    EX]>
DEFINE  ERR(T),<JRST [OUTSTR [ASCIZ "T"]
                JRST    ERRCM2]>
DEFINE PROCESS<
X FORTRAN,F4,F40
X MACRO,MAC,MACRO
X FTFORTRAN,FTF,FTF40
X SIMPL,SIM,SIMPL
X FAIL,FAL,FAIL
X SAIL,SAI,SAIL
X COBOL,CBL,COBOL
>

;DEFINE VARIOUS BITS

ALPROC==RELSW   ;OR OF BITS FOR ALL THE PROCESSORS
NPROCS==0       ;NUMBER OF PROCESSORS
FCHLOD==400000  ;LOADER CHANNEL HAS BEEN INITED
CHN==17         ;USED TO DEFINE CHANNEL NUMBER
SPRC==0         ;BITS FOR THOSE PROCESSORS WHICH OUTPUT TO ANOTHER

DEFINE X (A,B,C,D,E)<
B'SW==FCHLOD    ;PROCESSOR BIT
IFDIF <D>,<>,<SPRC==SPRC!FCHLOD>
ALPROC==ALPROC!FCHLOD
NPROCS==NPROCS+1
CAT (B,NUM)==NPROCS     ;NUMBER OF PROCESS (FOR FAIL, PAL DECISION)
FCH'B==FCHLOD   ;ON INDICATES CHANEL OPEN
CHN'B==CHN      ;CHANNEL NUMBER
CHN==CHN-1
FCHLOD==FCHLOD_<-1>>
DEFINE CAT(A,B)
<A'B>

LVAR
ARRAY LOWST[0]  ;SET UP FOR THE ZEROING BLT
LVAR

DEFINE SBTS(A,B)        ;THIS MACRO PRODUCES TABLES FOR OUTPUTTING SWITCHES
<<SIXBIT !/A!>+B>
DEFINE TBGEN (A,B,C)    ;GENERATE /TYPE SWITCH TABLES
<DEFINE SWITCH (P,Q)
<<SIXBIT /P/>>
A:      TABLE
B==.-A
DEFINE SWITCH (P,Q)
<Q>
C:      TABLE
>
PROCESS
CHNLOD==CHN
CHNLST==CHN
FCHLST==FCHLOD
CHNEDT==CHN
FCHEDT==FCHLOD
CHNPIP==CHN
FCHPIP==FCHLOD
CHNDIR==CHN
FCHDIR==FCHLOD
CHNCRF==CHN-1           ;CREF OUTPUT CHANNEL
NSDNM==3        ;MAX NUMBER OF NON-STANDARD PROCESSORS
NSDCHN==CHN-2   ;HIGHEST CHANEL FOR NON-STANDARDS
NONSBS==NSDCHN-NSDNM+1  ;LOWEST CHANEL

DEFINE FOR (START,END,TEXT,INC)
<XXX==START
REPEAT END,<TEXT
IFB <INC>,<XXX==XXX+1>
IFNB <INC>,<XXX==XXX+INC>>>
FOR (1,NSDNM,<CAT (NSDS,\XXX)==FCHLOD_<1-XXX>>)
FCHCRF==FCHLOD_<-1>
LOOK==0         ;CHANNEL FOR DOING LOOKUPS FOR INFORMATION
NFILE==^D65     ;NUMBER OF FILES PERMITTED IN A + CONSTRUCTION
MAXNST==17      ;MAXIMUM NESTING DEPTH TO PERMIT
NESTDP==CHN-3   ;MAXIMUM NESTING DEPTH POSSIBLE WITH THESE PROCESSORS
IFG NESTDP-MAXNST,<NESTDP==MAXNST>      ;PICK SMALLER

EXTERNAL JOBFF,JOBREL,JOBSA,JOBSYM

;DEFINE FOLLOWING BECAUSE MACRO WON'T DO ADR ARITH PROPERLY

JOBERR=42
JOBDDT=74
JOBS41=122
JOBCOR=133

JOBAPR=125
JOBTPC=127
INTERN  JOBDDT,JOBS41,JOBCOR,JOBERR     ;IN CASE JOBDAT CHANGES

        HISEG           ;SIGNAL LOADER FOR HIGH SEGMENT

DEFINE  STRING,<DDTOUT T1,>

IFN NSDNM,<IFE RUNSW,<PRINTX SWITCH CONFLICT>>

NUNPNT==6
NUNTOP==7

IFE RUNSW,<
OFFSET==INHERE-74

NUNCOM: IOWD 0,INHERE
        0
NUNGO2: CALLI 15,11     ;GET PROPER CORE SIZE
        JRST NOCOR      ;LOSE
        IN 1,NUNCOM     ;GET IT
        JRST NUNGO3     ;OK
        PUSH    P,T3            ;SAVE MAIL BLK CURSER
NUNERR: CALLI NUNPNT,3  ;WE LOSE, PRINT ERROR
        JRST EX
NUNERM: ASCIZ #?LINKAGE ERROR - I/O#
NUNGO3: SKIPE 12,OFFSET+JOBCOR  ;GET JOBCOR
        CAMG 12,JOBREL  ;AND SEE IF WE SHOULD EXPAND
        JRST NUNGO4     ;NO, START THE BLT
        POP     P,T3            ;RESTORE MAIL BLK CURSER
        MOVEI NUNPNT,NUNCER
        JRST EX,11      ;YES, DO IT
        JRST NUNERR     ;LOSE
        MOVE 12,OFFSET+JOBS41   ;RESET 41
        MOVEM 12,41
        JRST NUNGO4     ;WIN
NUNCER: ASCIZ /?CORE NEEDED/
INHERE:

NUNAC:  PHASE 0 ;THE CODE TO GO IN THE ACS
NUNGO4: MOVE 12,OFFSET+JOBDDT   ;SET JOBDDT
        JRST EX,2
NUNBLT: BLT NUNTOP,0
        RESET   ;RESET THE WORLD
        AOS 1,JOBSA     ;GET STARTING ADDRESS
        JRST (1)
        NUNERM
        XWD INHERE+1,75 ;THE BLT WORD
DEPHASE
>
CREFIT:        MOVE T1,[SIXBIT 'CROSS']
NUNDO:  MOVEM T1,NAME   ;SET NAME
IFE SYSSW,<     SETZM   NAME+3>
IFN SYSSW,<     MOVE T1,[XWD 1,4]
        MOVEM   T1,NAME+3               ;CUSPS FROM DSK [1,1]
>
IFN RUNSW,<
        SETZM   NAME+1  ;LET MONITOR SUPPLY SAV OR SHR
        SKIPN T1,PCDEV  ;SPECIAL DEVICE??
        JRST NOPCDV
        MOVEM T1,RUNBLK
        SETZM NAME+3    ;ZERO PPN
NOPCDV: SKIPE T1,PCEXT
        MOVEM T1,NAME+1
        SKIPE T1,PCPPN
        MOVEM T1,NAME+3 ;SETUP OTHER THINGS (FROM NON-STANDARD)
IFDEF DEBFLAG,<MOVEI T1,RUNBLK+1
        PUSHJ   P,TPSIX>
        MOVSI   T1,1    ;START ADDR PLUS ONE
        SKIPE   EDCMND#
        SETZ    T1,
        HRRI    T1,RUNBLK       ;ARGUMENTS
        RUN     T1,     ;RUN UUO
        MOVEI   FL,0            ;ACS ARE GONE
        MOVE    P,PDP
        MOVEI   T1,[ASCIZ /?LINKAGE ERROR - RUN UUO
/]
        STRING
        JRST    NOFIL           ;TRY TO GIVE FILE-NAME
>
IFE RUNSW,<
NORUN:  INIT 1,16       ;GET A DSK IN DUMP MODE
        EXP SYSDEV      ;SIXBIT SYS OR DSK
        0
        JRST DSKNA
        MOVSI T1,SAVEXT         ;SIXBIT FOR SAVE OR DMP.
        MOVEM T1,NAME+1
        LOOKUP 1,NAME
        JRST NOFIL
        MOVE T1,NAME    ;SET NAME OF NEW PROCESSOR
        CALL T1,[SIXBIT /SETNAM/]
        HLRO 15,NAME+3  ;GET COUNT
        HRLM 15,NUNCOM
        MOVNS 15        ;MAKE POSITIVE
        MOVEI 16,73(15) ;GET END
        ADDI 15,INHERE  ;CHECK CORE SIZE
        IORI 15,1777
        MOVSI NUNTOP,NUNAC
        BLT NUNTOP,NUNTOP       ;GET ACS LOADER
        HRR NUNBLT,16   ;AND SET END OF BLT
        JRST NUNGO2
>
TERMF==200000
NUMF==100000
SPCF==400000
SPACT==40000    ;SPECIAL ACTION TO BE TAKEN ON CHAR
SPACT2==20000   ;SPECIAL ACTION AT GETCH LEVEL (@ AND ;)



GETNAM: SETZM SVNAM(SVPT)       ;ZERO OUT CELLS IN CASE NOTHING

        SETZM SVEXT(SVPT)       ;GETS PUT THERE
        SETZM SVPPN(SVPT)
        SETZM SWBKS(SVPT)
        SETZM SVDEV(SVPT)
GETN1:  TRNN FL,IDF             ;WAS THE THING SCANNED AN IDENT
        JRST TRYPP1     ;TRY FOR PPN
        PUSH P,ACCUM
        PUSHJ P,SCANS   ;CHECK FOR EXT OR PPN
        SKIPN   SENDFL ;THIS IS WHEN : IS NOT :
        CAIE C,":"      ;IS IT A DEVICE NAME
        JRST NODEV      ;NO
        POP P,SVDEV(SVPT)       ;WE WERE HIDING IT IN THE STACK
        PUSHJ P,SCAN    ;BYPASS
        PUSHJ P,SCAN    ;AND GET NEXT
        TRNN FL,IDF     ;MUST BE AN IDENT
        JRST TRYPP1
        PUSH P,ACCUM
        PUSHJ P,SCANS
NODEV:  POP P,SVNAM(SVPT)
        CAIN C,"["      ;IS IT PPN
        JRST GPPN
        CAIE C,"."      ;NO, EXT?
        POPJ P,         ;NEITHER, RETURN
        PUSHJ P,SCAN    ;NO. GO OVER DOT
        PUSHJ P,SCANS   ;PEEK AT NEXT CHAR
        JUMPN CS,[HLLOS SVEXT(SVPT)     ;BLANK EXTENSION
                JRST TRYPPN]    ;LOOK FOR PPN NEXT
GETN2:  PUSHJ P,SCAN    ;GET EXT
        TRNN FL,IDF
        JRST    [OUTSTR [ASCIZ "EXTENSION EXPECTED"]
                JRST    ERRCM2]
        MOVE T1,ACCUM
        HLLZM T1,SVEXT(SVPT)
        PUSHJ P,SCANS
TRYPPN: CAIE C,"["      ;CHECK FOR PPN AGAIN
        POPJ P,
GPPN:        PUSHJ P,SCAN
GETPP2: PUSHJ P,SCAN
        TRNN FL,IDF
        JRST SYNERR
        MOVE T1,ACCUM
        PUSHJ P,RJUST   ;THIS NEED TO BE RIGHT JUSTIFIED
        HRLM T1,SVPPN(SVPT)     ;STORE LEFT HALF
        PUSHJ P,SCAN
        CAIE C,","
        JRST SYNERR
        PUSHJ P,SCAN
        TRNN FL,IDF
        JRST SYNERR
        MOVE T1,ACCUM
        PUSHJ P,RJUST
        HRRM T1,SVPPN(SVPT)
        PUSHJ P,SCAN
        CAIE C,"]"
        JRST SYNERR
        POPJ P,         ;ALL DONE

        IFN STANSW,<
RJUST:  TRNE T1,77
        POPJ P, ;GET IT OVER THERE
        LSH T1,-6
        JRST RJUST>

IFE STANSW,<
RJUST:  PUSH    P,T3
        MOVE T3,T1      ;CONVERT SIXBIT TO OCTAL
        MOVEI T1,0
CONVOC: MOVEI T2,0
        LSHC T2,6
        CAIL T2,20
        CAILE T2,27
        JRST SYNERR
        LSH T1,3
        IORI T1,-20(T2)
        JUMPN T3,CONVOC
        POP     P,T3
        POPJ P,0
>

TRYPP1: CAIN    C,"["
        JRST    GETPP2
        CAIE    C,"("
        POPJ    P,
        PUSHJ   P,GTUNO
        MOVEM   T3,SVPPN(SVPT)
        CAIE    C,")"
        JRST    SYNERR
        PUSHJ   P,SCANS
        SKIPE   CS              ;MUST BE IDENT IF 0
        POPJ    P,0             ;ALL DONE ON TERMINATOR
        PUSHJ   P,SCAN
        JRST    GETN1   ;CONT. WITH FILE NAME
SCANS: MOVNI T1,1      ;FLAG AS NOTHING SEEN YET
        SKIPN CS,SAVCHR ;CHARACTER WAITING?
SCNS2:  PUSHJ P,GETCH
        JUMPN CS,SCNS1  ;FOUND SOMETHING
        MOVEI T1,0
        JRST SCNS2
SCNS1:  JUMPL CS,SCNS4  ;SPECIAL CHARACTER
        MOVEM CS,SAVCHR ;SAVE THAT CHARACTER
        JUMPL T1,SCNS3  ;DO NOTHING ELSE IF NO BLANKS SEEN
        MOVEM T1,SAVCHR ;IF BLANKS SEEN, SAVE ONE
        MOVSI T1,70000
        ADDM T1,@GETB3(IOPNT)   ;AND BACK UP POINTER
SCNS3:  TDZA CS,CS      ;IN EITHER CASE, RETURN 0
SCNS4:  MOVEM CS,SAVCHR ;SAVE SPECIAL CHARACTER
        HRRZ C,CS               ;GET A CHARACTER TO RETURN
        POPJ P,
SCAN:  TRZ FL,IDF!NUMFS        ;RESET IN CASE NOT
        SKIPN CS,SAVCHR ;WAS THERE SOMETHING LEFT OVER
CONSN:  PUSHJ P,GETCH   ;NO, GET ANOTHER
        JUMPE CS,.-1    ;IGNORE BLANKS
        JUMPL CS,SPCHR  ;IS IT A SPECIAL CHARACTER
        SETZM ACCUM     ;PREPARE TO STORE IT
        MOVE T1,[POINT 6,ACCUM]
        TRO FL,IDF!NUMFS        ;SAY ID AND FOR NOW A NUMBER
        TLNE CS,NUMF    ;IS IT A NUMBER??
        JRST SCNUM      ;YES, GO SCAN IT
SCAN1:  TLNE T1,770000  ;ALL SIX STORED?
        IDPB CS,T1      ;NO, STORE ANOTHER
        PUSHJ P,GETCH   ;GET NEXT
        JUMPG CS,SCAN1  ;ANOTHER ALPHA
        TRZ FL,NUMFS    ;THIS WAS NOT A NUMBER
SCAN2:  MOVEM CS,SAVCHR
        MOVEI C,0       ;TO AVOID CONFUSION
        MOVEI CS,0
        POPJ P,
SPCHR:  HRRZ C,CS       ;RETURN HIM THE HALF OF IT
        SETZM SAVCHR    ;NOTHING SAVED BY NOW
        TLNN CS,SPACT   ;DO WE WANT SPECIAL ACTION?
        POPJ P,         ;NO
        JRST (CS)       ;YES, RH IS DISPATCH
SCNUM:  SETZM NUMAC#    ;THE NUMBER ACCUMULATOR
SCNUM1: TLNE T1,770000  ;SAVE DIGITS IN SIXBIT IN CASE FILE NAME
        IDPB CS,T1
        MOVEI C,^D10
        IMUL C,NUMAC
        ADDI C,-20(CS)  ;ADD IN THE NUMBER
        MOVEM C,NUMAC
        PUSHJ P,GETCH
        JUMPLE CS,SCAN2 ;END OF NUMBER (SPECIAL CHR) GET OUT
        TLNE CS,NUMF    ;IS IT PART OF A NUMBER?
        JRST SCNUM1     ;YES, GO ON
        JRST SCAN1      ;SCAN REST OF IDENT (NUMFS TURNED OFF AT SCAN2-1)
;GETCH RETURNS 7-BIT ASCII CHAR IN C, TABLE ENTRY IN CS

GETCH:  SOSLE @GETB1(IOPNT)     ;USE CORRECT BUFFER HEADER
        JRST OKPICK
IFN TEMP,<      MOVE C,TMPFLG(IOPNT)    ;IS TMPCOR BEING USED
        CAMN C,[-1]             ;SET TO -1 IF YES
        JRST [SETZM TMPFLG(IOPNT)       ;CLEAR TMPFLAG
                JRST POPFIL+1]          ;YES FINISHED WITH THIS READ>
        XCT GETB2(IOPNT)        ;AN IN UUO
        SKIPA
        JRST ERREOF     ;WAS AN ERROR OR EOF, GO CHECK
OKPICK: IBP @GETB3(IOPNT)
        MOVE C,@GETB3(IOPNT)    ;PICK UP THE NEW BYTE POINTER
        MOVE CS,(C)     ;GET THE WORD IT CAME FROM
        TRNE CS,1       ;AND CHECK FOR SEQ NUM
        JRST    [AOS @GETB3(IOPNT)      ;ADVANCE POINTER
                MOVNI CS,5      ;AND ADJUST COUNT
                ADDB CS,@GETB1(IOPNT)
                SKIPG CS        ;CHECK FOR BUFFER OVERRUN
                PUSHJ P,GETCH   ;GET RID OF TAB
                JRST GETCH]
        LDB C,@GETB3(IOPNT)
        JUMPE C,GETCH   ;IGNORE NULLS
EOFRT:  MOVE CS,CTBL(C) ;GET STATUS BITS
EOFRT1: TRNN FL,INPRNT  ;IF PRINTING ERROR, DO NOT NEST
        TLNN CS,SPACT2  ;SPECIAL?
        POPJ P,
        JRST (CS)       ;DO IT

ERREOF: XCT GETB4(IOPNT)        ;TO A STATZ
        JRST READER     ;AN INPUT ERROR
        JRST POPFIL     ;GO GET PREVIOUS FILE
SEMIC:  TRO FL,INPRNT   ;GO GET RID OF GETCH SPECIAL ACTION
SEMIC1: PUSHJ P,GETCH   ;READ CHRS
        CAIE C,12       ;UNTIL A LINE FEED
        JRST SEMIC1
        TRZ FL,INPRNT   ;GET RID OF FLAG
        JRST GETCH      ;AND READ A CHR
CTBL:  0
REPEAT 10,<XWD SPCF,.-CTBL>
        0               ;TAB
        XWD SPCF!SPACT!TERMF+12,CHKTRM
        XWD SPCF,13
        0       ;IGNORE FORM FEEDS
        0               ;CARRET
REPEAT 15,<XWD SPCF,.-CTBL>
        XWD SPCF!TERMF!SPACT+44,CHKTRM
REPEAT 4,<XWD SPCF,.-CTB       0               ;SPACE
        REPEAT 2,<XWD SPCF,.-CTBL>
        3       ;# FOR SPECIAL IN DIR COMMAND
REPEAT 6,<XWD SPCF,.-CTBL>
12
REPEAT 2,<XWD SPCF,.-CTBL>
XWD SPCF!NUMF,.-CTBL    ;ALLOW %-B IN LOADER SWITCHES
REPEAT 2,<XWD SPCF,.-CTBL>
REPEAT 12,<XWD NUMF,.-CTBL-40           ;DIGIT>
        XWD SPCF,.-CTBL ;:
        XWD SPCF!SPACT2+";",SEMIC       ;;
REPEAT 3,<XWD SPCF,.-CTBL>
37                      ;ALLOW QUESTION MARK AS WILD CHARACTER
        XWD SPCF!SPACT2+"@",NEST
REPEAT 32,<EXP .-CTBL-40                ;UPPER CASE LETTERS>
REPEAT 6,<XWD SPCF,.-CTBL>
REPEAT 32,<EXP .-CTBL-100               ;LOWER CASE LETTERS>
        XWD SPCF,.-CTBL
        XWD SPCF,.-CTBL
        XWD SPCF!TERMF!SPACT+44,CHKTRM
        XWD SPCF!TERMF!SPACT+44,CHKTRM
        XWD SPCF!SPACT,POPFIL
COMMA==CTBL+","

CHKTRM: PUSH P,CS       ;SAVE MAGIC BITS
TERMC1: PUSHJ P,GETCH
        JUMPE CS,TERMC1 ;ALSO IGNORE TABS AND SPACES
        TLNE CS,TERMF
        JRST TERMC1     ;BYPASS TERMINATORS
        CAMN CS,COMMA   ;CHECK FOR , AFTER CRET
        JRST    [POP P,(P)      ;GET STACK IN SYNC
                POPJ P,]        ;RETURN THE COMMA
        MOVEM CS,SAVCHR ;SAVE FOR LATER
        POP P,CS
        MOVEI C,0       ;AS GOOD AS ANYTHING ELSE
        POPJ P,

DEFINE QQ<
N=1
REPEAT NESTDP+1,<MAC(\N)
        N=N+1>>

GETB1:  DINCT
DEFINE MAC(X)<IBUF'X+2>
QQ
GETB2:  HALT
DEFINE MAC(X)<IN X,0>
QQ
GETB3:  DINPT
DEFINE MAC(X)<IBUF'X+1>
QQ
GETB4:  HALT
DEFINE MAC(X)<STATZ X,740000>
QQ

NEST:   PUSH P,ACCUM    ;SAVE STATE OF SCANNER
        PUSH P,FL       ;SAVE THE FLAGS (AS IDF?)
        PUSH P,T1
        SETZM SAVCHR
        PUSH P,NAME     ;AND THIS OTHER STUFF
        PUSH P,NAME+1
        PUSH P,NAME+2
        PUSH P,NAME+3
        AOBJP SVPT,TMNER        ;GET A CLEAR SPACE FOR NAME
        PUSHJ P,SCAN
        PUSHJ P,GETNAM  ;GET ONE TO USE
        AOBJP IOPNT,NESTTD      ;TOO DEEP?
        PUSH IOP,SAVCHR
        SETZM   SAVCHR
        PUSHJ P,CHKRM           ;GET BUFFER SPACE
RENEST:
IFN TEMP,<      MOVE C,JOBFF            ;GET START OF BUFFER
        MOVEM C,BUFTAB(IOPNT)   ;SAVE IT FOR RELEASING INFO
        MOVEM C,TMPFIL+1        ;SAVE IOWD FOR TMPCOR UUO
        MOVEM C,@GETB3(IOPNT)   ;DUMMY UP BYTE POINTER
        SOS TMPFIL+1            ;MAKE TMPFIL INTO CORRECT IOWD FORMAT
        MOVNI C,200             ;GET BUFFER LENGTH
        HRLM C,TMPFIL+1         ;STORE NEGATIVE WORD COUNT
        MOVE C,SVNAM(SVPT)      ;PICK UP FILNAM
        HRLZM C,TMPFIL  ;STORE RIGHT THREE LETTERS
        MOVE C,[XWD 1,TMPFIL]   ;SET UP FOR TMPCOR READ
        TMPCOR  C,              ;READ FILE AND DON'T DELETE
        JRST NOTMP              ;NO SUCH FILE, TRY THE DISK
        SETOM TMPFLG(IOPNT)     ;FLAG THAT TMPCOR READ WAS DONE
        IMULI C,5               ;CALCULATE CHARACTER COUNT
        ADDI    C,1             ;ADJUST FOR BOUNDARY CONDITION
        MOVEM C,@GETB1(IOPNT)   ;STORE IN BUFFER HEADER
        MOVEI C,440700          ;SET UP BYTE POINTER
        HRLM C,@GETB3(IOPNT)    ;BUFFER HEADER FINALLY SET UP
        JRST NEXT2              ;CONTINUE INTO MAIN STREAM
NOTMP:                  >
        SETZM   OPENB
        MOVSI   C,(SIXBIT /DSK/)
        MOVEM   C,OPENB+1
        MOVE C,NESTB(IOPNT)     ;GET BUFFER POINTER
        MOVEM C,OPENB+2
        MOVE    C,[OPEN .-.,OPENB]
        DPB IOPNT,[POINT 4,C,12]
        XCT     C
        JRST DSKNA      ;LOSE BIG
       MOVE C,JOBFF
        MOVEM C,BUFTAB(IOPNT)   ;SAVE THE PLACE PUT
        XCT INTAB(IOPNT)        ;DO AN INBUFF
        MOVE C,SVNAM(SVPT)
        MOVEM C,LNAM    ;SET UP FOR LOOKUP
        MOVE C,SVEXT(SVPT)
NEST1:  HLLZM C,LEXT
        MOVE C,SVPPN(SVPT)
        MOVEM C,LPPN
        XCT LKTAB(IOPNT)
        JRST    [TRNE FL,INCRF  ;SPECIAL IF TRYING TO READ QQCREF
                JRST DNCRF
                SKIPN C,SVEXT(SVPT)     ;WAS EXTENSION SPECIFIED
                HLLZ C,LEXT     ;SEE IF BLANK USED
                JUMPN C,NOFIL   ;NO, NOT THERE
                MOVSI C,SIXBIT '   CMD' ;TRY .CMD
                JRST NEST1]
NEXT2:  SUB SVPT,[XWD 1,1]      ;GET HIM POINTED BACK RIGHT
        POP P,NAME+3    ;RESTORE THINGS
        POP P,NAME+2
        POP P,NAME+1
        POP P,NAME
        POP P,T1
        POP P,FL
        POP P,ACCUM
        TRZ FL,RPGSRC!RECALF    ;WE HAVE DONE THE FIND
        JRST GETCH      ;AND CONTINUE TO GET THAT CHR

POPFIL:
        XCT RELTAB(IOPNT)       ;RELEASE HIM
IFN TEMP,<      SETZM TMPFLG(IOPNT)     ;CLEAR TMPCOR FLAG>
        MOVE C,BUFTAB(IOPNT)
        MOVEM C,FREBUF(IOPNT)   ;MARK BUFFER FREE
        POP IOP,CS
        HRRZ C,CS
        SUB IOPNT,[XWD 1,1]     ;POINT IT BACK
        JRST EOFRT1     ;AND GIVE BACK THE CHARACTER

NESTB:  0
DEFINE MAC(X)<IBUF'X>
QQ
DEFINE MAC(X)<ARRAY IBUF'X[3]>
QQ
IFN TEMP,<ARRAY TMPFLG[NESTDP+2]
LVAR
DEFINE MAC(X)<TMPF'X==TMPFLG+X>
QQ>
INTAB:  HALT    ;INBUFS
DEFINE MAC(X)<INBUF X,2>
QQ

LKTAB:  HALT
DEFINE MAC(X)<LOOKUP X,NAME>
QQ
RELTAB: JRST ALLDON
DEFINE MAC(X)<RELEAS X,0>
QQ
;ERROR ROUTINES

ILLCHR: OUTCHR  C
        OUTSTR  [ASCIZ " IS AN ILLEGAL CHARACTER: "]
        JRST    ERRCM2
ETMS:   MOVEI T1,[ASCIZ /TOO MANY SWITCHES: /]
ERRCOM: STRING          ;DDTOUT OR FACSIMILE
ERRCM2: MOVEI T1,20     ;SET TO TYPE SOME CHRS TO TELL WHERE ERROR
        MOVE T2,[POINT 7,ERRBUF]        ;IS FROM
        TRO FL,INPRNT   ;IN CASE EOF WHILE READING CHRS TO TYPE
        SKIPN C,SAVCHR  ;FIND THE ONE LEFT
        JRST PUTER
        TLNE C,SPACT    ;IS IT SPECIAL
        MOVSS C ;YES. GET IT
        SKIPA
PUTER:  PUSHJ P,GETCH
        CAIN C,177      ;THIS IS EOF
        JRST FINER
        IDPB C,T2
        SOJGE T1,PUTER
NOFIL0: MOVE    T1,T2
        JRST QOFIL1     ;PRINT WITH CR/LF
TMNER:  MOVEI T1,[ASCIZ /TOO MANY NAMES: /]
        JRST ERRCOM
DSKNA:  MOVEI T1,[ASCIZ /DISK NOT AVAILABLE: /]
        JRST ERRCOM
OUTER:  MOVEI T1,[ASCIZ /OUTPUT ERROR: /]
        JRST ERRCOM
PROCON: MOVEI T1,[ASCIZ /PROCESSOR CONFLICT: /]
        JRST ERRCOM
NOCOR:  MOVEI T1,[ASCIZ /NOT ENOUGH CORE: /]
        JRST ERRCOM
READER: MOVEI T1,[ASCIZ /INPUT ERROR: /]
        JRST ERRCOM
SYNRR1: SUB IOPNT,[XWD 1,1]     ;GET HIM BACK TO RIGHT PLACE
SYNERR: MOVEI T1,[ASCIZ /COMMAND ERROR: /]
        JRST ERRCOM
NESTTD: MOVEI T1,[ASCIZ /NESTING TOO DEEP: /]
        JRST ERRCOM
UNRECS: MOVEI T1,[ASCIZ /UNRECOGNIZABLE SWITCH: /]
        JRST ERRCOM
NOFIL:  TRZE FL,RPGSRC
        JRST TRYSVD     ;TRY SVED
        TRNE FL,RECALF  ;WE WERE LOOKING UP A SVC FILE
        JRST SYNRR1     ;SO GIVE SPECIAL MESSAGE
        MOVEI T1,[ASCIZ /NO SUCH FILE - /]
NAMCOM: STRING                  ;DDTOUT OR FACSIMILE
        MOVE T1,[POINT 7,ERRBUF]
        MOVE T3,NAME
        PUSHJ P,SIXOUT
        HLLZ T3,NAME+1
        JUMPE T3,QOFIL1
        MOVEI T2,"."
        IDPB T2,T1
QOFIL2: PUSHJ P,SIXOUT
QOFIL1: MOVEI T2,15
       IDPB T2,T1
        MOVEI T2,12
        IDPB T2,T1
        MOVEI T2,0
        IDPB T2,T1
        MOVEI T1,ERRBUF
        STRING
        RESET
        JRST EX ;GO AWAY
FIU:    MOVEI T1,[ASCIZ /FILE IN USE OR PROTECTED - /]
        JRST NAMCOM

SIXOUT: MOVEI T2,0
        LSHC T2,6
        ADDI T2,40
        IDPB T2,T1
        JUMPN T3,SIXOUT
        POPJ P,
DEVNA:  MOVEI T1,[ASCIZ /DEVICE NOT AVAILABLE - /]
        CALLI T1,3
        MOVE T1,[POINT 7,ERRBUF]
        MOVE T3,LOKNAM
        JRST QOFIL2
FINER:  CAIGE T1,10     ;HAVE WE PRINTED ENOUGH??
        JRST NOFIL0     ;YES
        MOVEI C,11      ;NO, PRINT A TAB
        IDPB C,T2
        JRST NOFIL0     ;AND GO AWAY
TMSTD:  MOVEI T1,[ASCIZ /TOO MANY NON-STANDARD PROCESSORS: /]
        JRST ERRCOM
ALLDON:        TRNE FL,INCRF   ;JUST FOUND END OF QQCREF FILE
        JRST DNCRF
        TRNE FL,INPRNT
        JRST FINER      ;IF PRINTENG AND EOF THEN FIINSH UP
        HRRZ T1,(P)     ;GET THE ADDRESS WE WANT TO RETURN TO
        CAIE T1,NXFIL+1 ;THIS SHOULD BE HERE
        JRST    [TRNE   FL3,LNKSW
                CAIE    T1,ILP2
                JRST    [OUTSTR [ASCIZ "LINE TERMINATED IMPROPERLY: "]
                        JRST    ERRCM2]
                JRST    .+1]
        SETZM PCNAM     ;NO LINK NAME TO START WITH
        MOVEI T3,OUTLOD
        MOVSI T2,SIXBIT '   /G '                ;SET UP FOR TERMINATE LOADING
        TRNE FL,DOLOD           ;ARE WE LOADING?
        PUSHJ P,OUTSIX          ;YES, PUT IT OUT
        SETZM PCDEV#    ;ON DEVICE
        SETZM PCPPN#    ;NO PROGJ PROG
        SETZM PCEXT#    ;NO EXTENSION
        MOVEI T4,FCHLOD ;START WITH LOADER
        MOVEI T3,NPROCS ;WHICH IS HERE
        SKIPA
ALDN1:  LSH T4,1        ;LOOK FOR NEXT PROCESSOR
        TRNN FL2,(T4)   ;HAS THAT PROCESSOR BEEN SET UP FOR OUTPUT?
        SOJGE T3,ALDN1  ;NO, TRY NEXT (BUT NOT TOO MANY)
        JUMPL T3,DONEP  ;IF OUT OF PROCESSORS THEN DONE
        PUSH P,T3       ;SAVE IT FOR LATER
        MOVE T3,PRCTAB(T3)      ;GET PROCESSOR OUTPUT ROUTINE
        SKIPN T2,PCNAM  ;IS THERE A PROCESSOR FOR IT TO CALL?
        JRST NONAM      ;NO
        PUSHJ P,OUTSIX  ;YES, PUT OUT ITS NAME
        MOVEI T1,"!"    ;AND THE LOAD SYMBOL
        PUSHJ P,(T3)
        PUSHJ   P,OUCRLF
NONAM:  POP P,T3        ;GET BACK PROCESSOR NUMBER
        XCT RLSTAB(T3)  ;CLOSE UP ITS OUTPUT
        MOVE T1,PRCNAM(T3)      ;GET THE NAME OF THAT PROCESSOR
        MOVEM T1,PCNAM  ;AND SET AS THE ONE TO LINK TO
        SOJGE T3,ALDN1  ;GO BACK IF MORE TO LOOK AT
DONEP:  MOVEI T4,0      ;NON STANDARD PROCESSORS START WITH 0
NSTOUT: CAMLE T4,NOSTNM ;DO WE HAVE THAT MANY?
        JRST DONE       ;NO
        SKIPN NONUSD(T4)        ;HAS THAT FILE BEEN USED
        AOJA T4,NSTOUT          ;NO
        MOVE T3,PRCTAB+NPROCS+1(T4)     ;THE OUTPUT ROUTINE
        SKIPN PCNAM     ;A NAME TO PUT OUT?
        JRST NONAM1
        SKIPE T2,PCDEV  ;A DEVICE?
        JRST    [PUSHJ P,OUTSIX
                MOVEI T1,":"
                PUSHJ P,(T3)
                JRST .+1]
        MOVE T2,PCNAM
        PUSHJ P,OUTSIX
        SKIPE T2,PCEXT  ;CHECK FOR AN EXTENSION
        JRST    [MOVEI T1,"."
                PUSHJ P,(T3)
                PUSHJ P,OUTSIX
                JRST .+1]
        SKIPE T2,PCPPN
        PUSHJ P,OUTPPN
        MOVEI T1,"!"
        PUSHJ P,(T3)
        PUSHJ P,OUCRLF
NONAM1: XCT NSDREL(T4)
        MOVE T1,NONSNM(T4)
        MOVEM T1,PCNAM
        MOVE T1,NONSDV(T4)
        MOVEM T1,PCDEV
        MOVE T1,NONSXT(T4)
        MOVEM T1,PCEXT
        MOVE T1,NONSPN(T4)
        MOVEM T1,PCPPN
        AOJA T4,NSTOUT
NSDREL: FOR (NSDCHN,NSDNM,<RELEASE XXX,0>,-1)

DONE:  TRNE FL,CMDSN   ;DID WE SEE COMMAND FROM TTY?
        JRST DONE1      ;NO, DO NOT WRITE FILE
        MOVE T1,JOBNAM
        HRRI T1,(SIXBIT /SVC/)
        TRNE FL,EDITF
        HRRI T1,(SIXBIT /EDS/)
        MOVEM T1,LNAM   ;SET UP OUTPUT FILE
IFN TEMP,<      HRLZM T1,TMPFIL         ;SAVE NAME IN TMPFIL
        MOVE T1,[POINT 7,DDTBUF]        ;SET UP BYTE POINTER
        MOVNI T2,4                      ;SET UP FOR CHARACTER COUNT
        ILDB T3,T1                      ;GET NEXT CHARACTER
        CAIE T3,177                     ;IS IT A EOF CHARACTER
        SOJA T2,.-2                     ;NO, TRY AGAIN
        IDIVI T2,5                      ;CALCULATE CHARACTER COUNT
        HRLM T2,TMPFIL+1                ;STORE IN TMPCOR OUTPUT BLOCK
        LDB T3,[POINT 6,T1,5]           ;PICK UP BIT POS OF LAST CHAR
        SETO T2,                        ;PREPARE TO BUILD MASK
        LSH T2,7(T3)                    ;MASK OFF REST OF LAST WORD
        ANDM T2,(T1)                    ;  IN DDT BUFFER
        MOVEI T2,DDTBUF-1               ;GET START OF BUFFER
        HRRM T2,TMPFIL+1                ;STORE IN WRITE BLOCK FOR TMPCOR UUO
        MOVE T2,[XWD 3,TMPFIL]          ;SET UP FOR WRITE
        TMPCOR  T2,                     ;WRITE OUT FILE INTO CORE
        SKIPA                           ;IT DID NOT FIT, TRY DISK
        JRST DONE1                      ;GO CLEAN UP AND LEAVE
        MOVEI T2,177                    ;RESTORE EOF CHARACTER IN DDTBUF
        IDPB T2,T1                      ;ALL BACK TO NORMAL
NOFIT:                  >
        MOVSI T1,SIXBIT '   TMP'
        MOVEM T1,LEXT
        SETZM LDAT
        SETZM LPPN
        CLOSE LOOK,0    ;MAKE SURE NOTHING USING THIS CHANEL
        PUSHJ P,CHKRM   ;GET ROOM FOR BUFFERS
        ENTER LOOK,LNAM ;GET SET TO WRITE
        JRST FIU        ;TREAT THIS AS A FATAL ERROR
        OUTBUF LOOK,2
        MOVE T1,[POINT 7,DDTBUF]        ;WRITE ALL OF COMMAND
DONE3:  ILDB T2,T1
        CAIN T2,177     ;DONE?
        JRST DONE2
        SOSG LOOKBF+2
        OUTPUT LOOK,0
        IDPB T2,LOOKBF+1
        JRST DONE3
DONE2:  RELEASE LOOK,0  ;LET IT GO
DONE1:  TRNE FL2,FCHCRF ;DID WE DO ANY CREF?
        PUSHJ P,FINCRF  ;YES, FINISH OFF CREF
        RESET   ;RESET SO EXTRA FILES NOT PUT OUT
        SKIPN T1,PCNAM  ;IS THERE ONE TO LOAD?
        JRST    EX
        JRST NUNDO      ;GO LOAD IT
EX:     MOVEI   T1,17           ;PREPARE TO RELEASE ALL FILES
        MOVE    T2,[RELEAS 0,0]
        DPB     T1,[POINT 4,T2,12]
        XCT     T2
        SOJGE   T1,.-2
        CALLI   1,12
        JRST    .-1
        DEFINE X (A,B,C,D,E)
<RELEASE CHN'B,0>

IFN TEMP,<DEFINE X (A,B,C,D,E)
<PUSHJ P,CHK'B>>

RLSTAB: PROCESS
IFE TEMP,<      RELEASE CHNLOD,0        ;DON'T FORGET LOADER>
IFN TEMP,<      PUSHJ P,CHKLOD>

        DEFINE X (A,B,C,D,E)
<<SIXBIT /C/>>

PRCNAM: PROCESS
        SIXBIT /LOADER/
CHKRM: PUSH P,T1       ;SAVE THE REGISTERS WE ARE USING
        PUSH P,T2
        MOVSI T1,-<NESTDP+2>    ;LOOK TO SEE IF ANY FREED BUFFERS
        SKIPN T2,FREBUF(T1)
        AOBJN T1,.-1    ;TRY AGAIN
        JUMPGE T1,USTOP ;NO, GET IT FROMTOP OF STORAGE
        MOVEM T2,JOBFF  ;YES, SET JOBFF THERE
        SETZM FREBUF(T1)        ;AND MARK IT USED
        JRST TPOPJ      ;THATS ALL FOR NOW
USTOP:  MOVE T1,SVJFF   ;GET THE CURRENT TOP OF BUFFER AREA
        MOVEM T1,JOBFF
        ADDI T1,<203*2>+1       ;LEAVE THIS MUCH ROOM
        MOVEM T1,SVJFF  ;THATS THE NEW TOP
        CAMGE T1,CORTOP ;WILL THAT RUN US OUT OF CORE?
        JRST TPOPJ      ;NO, LEAVE
        PUSH P,CTPOPJ
XPAND:  MOVEI T1,2000   ;GET SET TO EXPAND
        SKIPE DDSYM     ;ONLY IF SYMBOLS NOT MOVED
        ADDM T1,JOBSYM  ;UPDATE IMPORTANT LOCATIONS
        ADDM T1,CORTOP
        ADDM T1,CORT1
        ADD T1,JOBREL   ;NEW TOP DESIRED
        CALLI T1,11     ;ASK FOR IT
        JRST NOCOR      ;LOSE BIG
        MOVE T1,JOBREL
MVCR:   MOVE T2,-2000(T1)       ;MVOVE CORE UP
        MOVEM T2,(T1)
        CAMLE T1,CORTOP ;ARE WE DONE?
        SOJA T1,MVCR
CTPOPJ: POPJ    P,TPOPJ
TPOPJ:  POP P,T2        ;ALL FINISHED
        POP P,T1
        POPJ P,
DEFINE TABLE
<SWITCH COMPILE,<TRZ FL,DOLOD>
SWITCH LOAD,<PUSHJ P,LODVBK>
SWITCH CDEBUG,<PUSHJ P,[SETOM   CDEBFL#
                        MOVSI   T2,(SIXBIT '/S')
                        MOVEI   T3,OUTLOD
                        PUSHJ   P,OUTSIX
                        JRST    OUTSIX]>
SWITCH DEBUG,<PUSHJ P,DEBUG>
SWITCH EXECUTE,<PUSHJ P,XCTR>
SWITCH TRY,<PUSHJ P,TRYIT>
SWITCH FDEBUG,<PUSHJ P,FDEBUG>
SWITCH PRINT,<JRST PRINT>
SWITCH CRE,<PUSHJ P,[TRO FL,EDITF!CREATF
        JRST    TEDIT]>
SWITCH MODIFY,<PUSHJ P,[TRO FL,EDITF
        JRST    NANED]>
SWITCH EDITOR,<PUSHJ P,[TRO FL,EDITF
        TRNN    FL3,TYMX
        JRST    TED10
        SETOM   EDCMND
        MOVE    T1,[SIXBIT "EDITOR"]
        MOVEM   T1,PCNAM
        JRST    DONE]>
SWITCH CREATE,<PUSHJ P,[TRO FL,EDITF!CREATF
        JRST    TEDIT]>
SWITCH LIST,<JRST LISTR>
SWITCH DIRECTORY,<JRST DODIR>
SWITCH CREF,<JRST PLCROS>
SWITCH CROSS,<JRST CREFIT>
SWITCH DELETE,<JRST DODEL>
SWITCH TECO,<PUSHJ P,[TRO FL,EDITF
        JRST    TEDTEC]>
SWITCH MAKE,<PUSHJ P,[TRO FL,EDITF!CREATF
        JRST    TEDTEC]>
SWITCH RENAME,<JRST DOREN>
SWITCH TYPE,<JRST TYPR>
SWITCH COPY,<JRST COPY>
SWITCH DECLARE,<JRST DECLARE>
SWITCH WHO,<JRST WHO>
SWITCH PPN,<JRST PPN>
SWITCH FILES,<JRST DOFL>
SWITCH HELP,<JRST HELP>
SWITCH SYSNO,<JRST SYSNO>
SWITCH DATE,<JRST DODATE>
SWITCH TYMEX,<JRST TYMEX>
SWITCH PDP10,<JRST PDP10>
SWITCH 10EDIT,<[TRO FL,EDITF
        JRST    TED10]>
SWITCH FDC,<JRST FDC>
SWITCH PFDC,<JRST PFDC>
SWITCH CTEST,<JRST RPGRT>
SWITCH MAIL,<JRST MAIL>
SWITCH SEND,<JRST SEND>
SWITCH POSTMAN,<JRST POST>
SWITCH XBASIC,<JRST [MOVEI      T1,[EXP SIXBIT 'SYS',SIXBIT 'XBASIC',0,0,0,0]
                RUN     T1,
                MESS    SYS:XBASIC NOT AVAILABLE
]>>

TBGEN (COMTAB,COMTLG,COMT2)

PLCROS: MOVEI   T1,[ASCIZ /PLEASE USE CROSS COMMAND/]
        STRING
        RESET           ;AND EXIT
        JRST    EX      ;...

TRYIT:  MOVE    T2,[SIXBIT /(DE)/]
        JRST    XCTR1

FDEBUG: MOVSI   T2,(SIXBIT '/S');GIVE LOAD WITH SYMBOLS SWITCH
        PUSHJ   P,LODSIX        ;TO THE LOADER
        TROA    FL3,FDBGSW      ;INDICATE FDEBUG IN PROGRESS
DEBUG:  SKIPA   T2,[SIXBIT '/T']
XCTR:   MOVSI T2,SIXBIT '   /E '
XCTR1:  PUSHJ   P,LODSIX        ;OUTPUT TRY/DEBUG/EXECUTE SWITCHES
LODVBK: MOVE    T2,[SIXBIT /(BK)/]      ;ALL GET BK SWITCHES
        TRNE    FL3,LNKSW
        POPJ    P,
LODSIX: MOVEI   T3,OUTLOD       ;LOADER COMMAND STRING
        JRST    OUTSIX          ;GO PUBLISH SAME

RPGSET: MOVE T1,[POINT 7,FCOMD]
        MOVEM T1,DINPT
        MOVEI FL,RPGSRC!RECALF!CMDSN
        JRST RPGRET

TRYSVD: TRZE FL,ALTSRC  ;FROM COMMAND?
        JRST TRYSV2
        MOVE T1,[POINT 7,FCOMD3]
        MOVEM T1,DINPT
        MOVEI FL,RECALF!CMDSN!ALTSRC
        JRST RPGRET     ;TRY AGAIN
TRYSV2: HLLZ C,JOBNAM
        HRRI C,(SIXBIT /EDS/)
        MOVEM C,SVNAM(SVPT)
        MOVSI C,(SIXBIT /TMP/)
        MOVEM C,SVEXT(SVPT)
        HRRZ C,BUFTAB(IOPNT)
        MOVEM C,JOBFF   ;RESET SINCE ONE INBUF ALREADY DONE
        JRST RENEST

SKPLF:  INCHWL  T2      ;READ CHRS UNTIL WE SEE A LINE FEED
        INCHWL  T2
        CAIE T2,12
        JRST    [OUTSTR [ASCIZ /CONTINUATION ERROR
END
/]
                JRST EX]
        INCHWL  T2      ;NOW READ ONE MORE TO START NEXT LINE
        JRST START2     ;AND WAIT FOR HIM TO TYPE IT. GO

RPGS1:  MOVE T1,[POINT 7,[BYTE (7) "E","D",15,12,177,177]]
        MOVEM T1,DINPT  ;SET UP A FAKE EDIT COMMAND
        JRST RPGRET

GSTRN:  MOVE    T1,[POINT 7,DDTBUF]     ;SET POINTER
        MOVEM   T1,DINPT
        INCHWL  T2      ;WAIT FOR FIRST CHAR.
        JRST    GSTRN2  
GSTRN1: INCHSL  T2      ;READ A COMMAND CHAR INTO T2
        MOVEI   T2,33
GSTRN2: IDPB    T2,T1   ;STORE IN DDTINBUF
        MOVE    T3,CTBL(T2)     ;GET CHAR DESCRIPTOR
        TLNN    T3,TERMF        ;IS IT A BREAK CHAR?
        JRST    GSTRN1  ;NO, GET MORE
        MOVEI   T2,177  ;MARK EN WITH AN EOF FLAG
        IDPB    T2,T1
        IDPB    T2,177
        SETZM   SAVCHR
        POPJ    P,
STTYMX: HRROI   T1,GETPRV       ;SET TYMEX FLAG IF REQ.
        GETTAB  T1,
        JRST    EX
        TRNE    T1,PVTYMX
        TRO     FL3,TYMX        ;SET FLAG
        POPJ    P,
;HERE WE GO TO START THE WORLD TURNING

        JRST    [MOVEI T4,1     ;-1 ENTRY FOR EDIT
                JRST STP2]
STPT:   TDZA    T4,T4           ;NORMAL ENTRY
        MOVNI   T4,1            ;REENTRY FROM AN EDITOR
STP2:   SETZM   LOWST           ;MUST CLEAR LOW CORE
        MOVE    T1,[XWD LOWST,LOWST+1]
        BLT     T1,LOWTOP
        RESET
        SETZM   SENDFL# ;SEND COMMAND NOT IN PROGRESS
        JUMPG T4,RPGS1
        JUMPL   T4,RPGSET
IFNDEF DEBFLAG,<RESCAN>         ;RESET POINTER TO START OF COMMAND
IFDEF  DEBFLAG,<OUTSTR [ASCIZ "DEBUGING MODE:-"]>
        MOVEI FL,0
        MOVE T1,[POINT 7,DDTBUF]        ;SET POINTER
        MOVEM T1,DINPT
START1:IFNDEF DEBFLAG,<INCHSL   T2      ;READ A COMMAND CHAR INTO T2
        MOVEI   T2,33>          ;NON-SKIP MEANS END

IFDEF DEBFLAG,<INCHWL T2>
        CAIN T2,";"     ;ON THIS CHR SKIP TO NEXT LFD
        JRST SKPLF
START2: IDPB    T2,T1           ;STORE IN DDTINBUF
        MOVE    T3,CTBL(T2)     ;GET CHARACTER DESCRIPTOR
        TLNN    T3,TERMF        ;IS IT A BREAK CHAR?
        JRST    START1          ;NO. GO GET MORE.
        MOVEI   T2,177          ;MARK END WITH AN EOF FLAG
        IDPB T2,T1
        IDPB T2,T1      ;MAKE SURE
RPGRET: SETZ    FL3,
RPGRT1:
        GETPPN  T1,             ;GET THIS USER'S PROJ-PROGRAMMER NUMBER
        MOVEM   T1,USRPPN       ;...
        MOVE T1,[XWD INIDAT,INILOW]
        BLT T1,INILOW+INILEN
        MOVEI T1,3
        PJOB    T2,
        IDIVI T2,12
        ADDI T3,20      ;TO SIXBIT
        LSHC T3,-6
        SOJG T1,.-3     ;THREE DIGITS
        HLLZM T4,JOBNAM ;SAVE TO MAKE UNIQUE NAMES
        TLO T4,404040   ;NOW TO ASCII FOR ASCIZ'S
        MOVEI T1,3      ;THREE CHARS
        LSH T3,1
        LSHC T3,6       ;BRING IN A CHAR
        SOJG T1,.-2
        DPB T3,[POINT 21,CRFRDR,27]     ;SAVE IN ASCIZ
        DPB T3,[POINT 21,FCOMD,27]
        DPB T3,[POINT 21,FCOMD2,27]
        MOVSI T1,377777 ;SET COUNT TO A LARGE NUMBER
        MOVEM T1,DINCT
        SETOM NOSTNM#   ;NO NON-STARDARD PROCESSORS SEEN YET
        MOVE P,PDP              ;SET UP PDL
        MOVE IOP,[IOWD <NESTDP+2>*3,IOPD]       ;AND IO PDL
        MOVSI IOPNT,-<NESTDP+1> ;SET NEXT LIMIT
        SETZ    FL2,
        PUSHJ   P,STTYMX        ;SET TYMEX
        TRO FL,DOLOD            ;WE WANT TO LOAD
        HRLI FL,F4SW    ;ASSUME F4
        SETZM LOKNAM    ;NO ALTERNATE DEVICE YET
        OPEN LOOK,DSKLK ;GET THE DSK
        JRST DSKNA
        INBUF LOOK,2
        MOVE T1,JOBFF
        MOVEM T1,SVJFF
        SETZM BINDV#
        SETZM LSTDV#    ;NO AUX DEVICES TO START WITH
        MOVE T1,JOBREL
        SKIPE JOBDDT    ;SEE IF WE SHOULD LEAVE SYMBOL SPACE
        JRST    [HRRZ T1,JOBSYM
                CAMG T1,JOBFF   ;CHECK TO SEE IF SYMBOLS MOVED
                SKIPA T1,JOBREL ;USE JOBREL
                SETOM DDSYM#    ;SET FLAG TO ADJUST JOBSYM ON EXPAND
                JRST .+1]
        MOVEM T1,CORTOP
        MOVEM T1,CORT1
        SETZM SAVCHR    ;TO START THINGS
        HLLZS JOBERR    ;RESET ERROR COUNT
        MOVSI SVPT,-NFILE
        PUSHJ P,SCAN

        MOVE T1,[XWD -COMTLG,COMTAB]    ;CALL SCANNER FOR WORDS
        PUSHJ P,SWSCAN
        JRST    [OUTSTR [ASCIZ "ILLEGAL COMMAND NAME: "]
                JRST    ERRCM2]
        JFCL    ;EXACT MATCH RETURN. ANY MATCH WILL DO
        XCT COMT2-COMTAB(T1)    ;DO IT
COMAT:
IFN WRD4,<      PUSHJ P,RDIUFD>
        TRZE FL,ALTSRC
        PUSHJ P,SCAN    ;GET BY EDIT
        PUSHJ P,SCAN    ;GET NEXT THING
        TRNN IOPNT,-1   ;IF DOWN A LEVEL ITS OK
        TLNN CS,TERMF   ;OR IF NOTHING SEEN
        SKIPA
        JRST COMAT1
        TRNE FL,EDITF
        JRST DOEDT1
        JRST NXFIL1
COMAT1: MOVE T1,[POINT 7,FCOMD] ;GENERATE FAKE COMMAND TO READ
        TRNN FL,EDITF
        TROA FL,RPGSRC!ALTSRC   ;TRY FOR SVCM FILE
        MOVE T1,[POINT 7,FCOMD2]
        MOVEM T1,DINPT  ;SAVE FILE
        TRO FL,RECALF+CMDSN     ;MARK RECALLING FILE, DONT WRITE
        SETZM SAVCHR    ;CLEAR OUT SCANNER
        MOVSI IOPNT,-<NESTDP+2> ;ALLOW EXTRA NESTING
        PUSHJ P,SCAN
        TRNE FL,EDITF
        JRST DOEDIT
NXFIL: PUSHJ P,SCAN
NXFIL1: MOVSI SVPT,-NFILE       ;SET UP FOR NUMBER OF FILES
        MOVEI SWCNT,SWBK*5      ;SET UP FOR SWITCHES
        MOVE SWPT,[POINT 7,SWBLK]       ;AND POINTER
        SETZM SWBKL
        SETZM SWBKB
        SETZM ONAM
        SETZM OEXT
        SETZM OPPN      ;NO OUTPUT EXTENSION OR PPN
        HLL FL3,FL      ;SET TEMP FLAGS FROM PERM FLAGS
        MOVE T1,[POINT 7,LODSBK]        ;SET POINTER TO LOADER
        MOVEM T1,LODSP          ;SWITCH BLOCK
        MOVEI T1,LODSCT
        MOVEM T1,LODCTR
        MOVEM T1,LODCT2 ;AND SET COUNT FOR AFTER FILE NAME SWITCHES
        MOVE T1,[POINT 7,LODSB2]
        MOVEM T1,LODSP2
        SETZM BROCNT    ;CLEAR OUT THE <> COUNT
        JRST    ILP0A
RPGRT:  MOVEI   FL3,SPCOM       ;SET SPEC. COMMAND
        JRST    RPGRT1
;MAIN LOOP FOR READING INPUT

ILP0:   PUSHJ P,SCAN    ;GET FIRST "THING"
ILP0A:  CAIN C,"/"      ;CHECK FOR PERM COMPILE SWITCHES
        JRST COMPS1
        CAIN C,"%"      ;CHECK FOR PERM LOADER FLAGS
        JRST LOADS1
        CAIN    C,","           ;ALLOW SWITCHES W/O FILENAMES
        JRST    NXFIL           ;BETWEEN COMMAS
ILP1A:  TRZ FL,PROCS    ;NO PROCESSOR SWITCHES SEEN YET
ILP1:   CAIN    C,"["
        JRST    [TRON   FL3,LNKSW
                JRST    [RESET
                        MOVE    T1,[POINT 7,DDTBUF]
                        MOVEM   T1,DINPT
                        JRST    RPGRT1]
                MOVE    T1,C
                PUSHJ   P,OUTLOD
                JRST    ILP0]
        PUSHJ P,GETNAM  ;GO GET A FILE NAME
        PUSHJ P,XSWS    ;EXCHANGE LOADER SWITCH LISTS
ILP2A:  PUSHJ P,SCAN    ;GET THE SPECIAL CHR OR WHATEVER
ILP2:   CAIE C,","      ;DONE WITH THIS SET OF NAMES?
        TLNE CS,TERMF   ;WILL ACCEPT A TERMINATOR
        JRST SETUP      ;GO SET UP THE FILES FOR PROCESSORS
        CAIE    C,"]"
        CAIN    C,"!"
        JRST    [TRNN   FL3,LNKSW
                JRST    ILLCHR
                JRST    SETUP]
        CAIN C,"("      ;MAYBE SWITCHES TO BE PASSED TO PROCESSORS
        JRST PROCSW
        CAIN C,"/"      ;OR FOR US
        JRST COMPSW
        CAIN C,"%"
        JRST LOADS2
        CAIN C,">"
        JRST ENDBRO     ;THIS IS THE END OF A BROKET STRING
        CAIN C,"="      ;MAYBE HE IS SETTING THE OUTPUT NAME
        JRST SETONM
        CAIE C,"+"      ;IS THIS A SECOND FILE
        JRST    ILLCHR  ;IT SHOULD HAVE BEEN ONE OF THOSE
        AOBJP SVPT,TMNER        ;MAYBE TOO MANY FILES
        PUSHJ P,XSWS
        PUSHJ P,SCAN    ;GET NEXT
        CAIE C,"<"      ;IS THIS THE <> CONSTRUCTION
        JRST ILP0A      ;NO
       AOS BROCNT      ;WE ARE ONE DEEPER IN BROKETS
        PUSH P,SVPT     ;SAVE AWAY ALL THE IMPORTANT INFORMATION
        PUSH P,SWPT
        PUSH P,SWCNT
        PUSH P,LODSP
        PUSH P,LODSP2
        PUSH P,LODCTR
        PUSH P,LODCT2
        PUSH P,SWBKL
        PUSH P,SWBKB
        PUSH P,ONAM
        JRST ILP0       ;GO FINISH THINGS UP

ENDBRO:
        SOSGE BROCNT    ;ERROR IF NO < WAS SEEN
        JRST SYNERR
        SUB P,[XWD 12,12]       ;RESET PDL
        JRST ILP2A      ;GO PRETEND > NOT SEEN (COMMA SHOULD BE NEXT)

NXFILP: SKIPG BROCNT    ;ARE WE DONING BROKETS?
        JRST NXFIL      ;NO, JUST CONTINUE
        MOVE T1,(P)
        MOVEM T1,ONAM
        MOVE T1,-1(P)
        MOVEM T1,SWBKB
        MOVE T1,-2(P)
        MOVEM T1,SWBKL
        MOVE T1,-3(P)
        MOVEM T1,LODCT2
        MOVE T1,-4(P)
        MOVEM T1,LODCTR
        MOVE T1,-5(P)
        MOVEM T1,LODSP2
        MOVE T1,-6(P)
        MOVEM T1,LODSP
        MOVE SWCNT,-7(P)
        MOVE SWPT,-10(P)
        MOVE SVPT,-11(P)
        JRST ILP0
COMPS: PUSHJ P,SCAN    ;GET THE NAME OF THE SWITCH
        TRNN FL,IDF     ;WAS THERE REALLY AN IDENTIFIER THERE?
        JRST SYNERR     ;LOSE
        MOVE T1,[XWD -TBLG,SWTAB]
        PUSHJ P,SWSCAN
        JRST    [MOVE T1,[XWD -NSDNM,NSDSWS]    ;LOOK FOR NON-STANDARD PROCESSORS
                PUSHJ P,SWSCAN  ;IS IT THERE??
                JRST UNRECS     ;NO MATCH, LOSE
                JRST GSW2       ;EXACT MATCH OK
                JRST GSW2]      ;SO IS PARTIAL MATCH
        JRST GETSW      ;EXACT MATCH, GO
        JRST    [MOVE T1,[XWD -NSDNM,NSDSWS]    ;PARTIAL MATCH
                PUSHJ P,SWSCAN  ;CHECK FURTHER
                JRST GSW1       ;NONE THERE USE OLD MATCH
                JRST GSW2       ;THIS ONE EXACT, USE IT
                JRST UNRECS]    ;BOTH PARTIAL SO LOSE
GSW2:   SUBI T1,NSDSWS
        MOVE T1,SWTAB3(T1)
        JRST INTSW
GSW1:   MOVE T1,SVIND   ;GET BACK ONLY MATCH NUMBER
GETSW:  MOVE T1,SWTAB2-SWTAB(T1)        ;GET ACTION
INTSW:  TLNE T1,IMEDSW
        JRST (T1)       ;DO IT NOW
        TLNE T1,@CALPRO ;A PROCESSOR SW?
        JRST [TLZ FL3,@CALPRO
                TRNE FL,PERF    ;TURN OFF ALL OTHERS
                TLZ FL,@CALPRO
                JRST .+1]
        TLZ FL3,(T1)    ;TURN OFF SWITCHES AS NEEDED
        TRNE FL,PERF    ;PERMANENT?
        TLZ FL,(T1)     ;SET THAT TOO
        MOVSS T1
        TLO FL3,(T1)    ;AND TURN ON OTHERS
        TRNE FL,PERF
        TLO FL,(T1)
        JRST SCAN       ;GET SOMETHING ELSE

DEFINE X (A,B,C,D,E)
<SWITCH A,<XWD B'SW,0>>
        DEFINE TABLE
<SWITCH LIST,<XWD LISTSW,CRSW>
SWITCH CREF,<XWD IMEDSW,SETCRF>
SWITCH M,<XWD MACSW,0>
SWITCH C,<XWD IMEDSW,SETCRF>
SWITCH CROSS,<XWD IMEDSW,SETCRO>
SWITCH LIBRARY,<XWD LIBSW,0>
SWITCH REL,<XWD RELSW,0>
SWITCH NOLIST,LISTSW
SWITCH NOSEARCH,LIBSW
SWITCH L,<XWD LISTSW,CRSW>
SWITCH F,<XWD F4SW,0>
SWITCH N,LISTSW
SWITCH COMPILE,<XWD COMPLS,0>
SWITCH NOCOMPILE,COMPLS
SWITCH NOLOAD,<XWD NOLODS,0>
SWITCH LOAD,NOLODS
SWITCH FORWARD,<XWD FORWDS>
SWITCH NOTFORWARD,FORWDS
SWITCH MAP,<XWD IMEDSW,SETMAP>
SWITCH CHAIN,<XWD IMEDSW,CHAIN>
SWITCH LINK,<XWD IMEDSW,LINK>
SWITCH SLINK,<XWD IMEDSW,SLINK>
SWITCH NONSTANDARD,<XWD IMEDSW,NONSTD>
SWITCH BINDEV,<XWD IMEDSW,SETBDV>
SWITCH LISTDEV,<XWD IMEDSW,SETLDV>
SWITCH READ,0
SWITCH SEQUENCE,0
SWITCH NAME,<XWD IMEDSW,[PUSH P,[SIXBIT '/Y']
                JRST    LNKSP]>
SWITCH CLOSE,<XWD IMEDSW,[MOVSI T2,'/-Y'
                PUSHJ   P,OUTLOD
                JRST    SCAN]>
SWITCH INITIAL,<XWD IMEDSW,[MOVSI T2,'/1Y'
                PUSHJ   P,OUTLOD
                JRST    SCAN]>
SWITCH SYMBOL,<XWD IMEDSW,[PUSH P,[SIXBIT '/2B']
                JRST    LNKSP]>
PROCESS
>

TBGEN (SWTAB,TBLG,SWTAB2)
SWTAB3: FOR (1,NSDNM,<XWD CAT(NSDS,\XXX),0>)    ;NON-STD SWITCHES
SWSCAN: MOVE T3,ACCUM   ;FIND OUT HOW MANY LETTERS
        MOVNI T2,1
SWSC1:  CAMN    T3,[SIXBIT '?']
        JRST    SWSC0C
        LSH T3,6
        LSH T2,-6       ;GET ZEROS IN T2 WHERE THERE ARE LETTERS
        JUMPN T3,SWSC1
        SETOM NUMAT     ;NONE FOUND YET
SWSC3:  MOVE T3,(T1)
        CAMN T3,ACCUM   ;EXACT MATCH?
        JRST CPOPJ1     ;YES, SKIP RETURN
        ANDCM T3,T2     ;LOOK AT ONLY THOSE LETTERS TYPED
        CAME T3,ACCUM   ;MATCH NOW??
        JRST SWSC2      ;NO
        AOS NUMAT       ;RECORD ANOTHER PARTIAL MATCH
        MOVEM T1,SVIND  ;SAVE IT
SWSC2:  AOBJN T1,SWSC3  ;TRY FOR MORE
        SKIPE NUMAT     ;MUST BE ONE AND ONLY 1 PARTIAL MATCH
        POPJ P,         ;ELSE ERROR RETURN
        MOVE T1,SVIND   ;GET IT
        AOS(P)  ;PARTIAL IS DOUBLE SKIP RETURN
CPOPJ1: AOS(P)
        POPJ P,         ;SKIP RETURN
SWSC0C: MOVEM   T1,SVIND
        OUTSTR  [ASCIZ "OPTIONS ARE"]
        MOVEI   T2,":"
SWSC0A: HRRZ    T3,SVIND
        CAIN    T3,COMTAB
        JRST    [MOVE   T3,(T1)
                CAMN    T3,[SIXBIT 'CTEST']
                JRST    SWSC0D
                CAMN    T3,[SIXBIT 'CRE']
                JRST    SWSC0D
                CAMN    T3,[SIXBIT 'CREF']
                JRST    SWSC0D
                JRST    .+1]
        MOVE    T3,(T1)
        OUTCHR  T2
SWSC0B: SETZ    T2,
        LSHC    T2,6
        ADDI    T2," "
        OUTCHR  T2
        JUMPN   T3,SWSC0B
        MOVEI   T2,","
SWSC0D: AOBJN   T1,SWSC0A
        MOVE    T1,SVIND
        OUTSTR  [ASCIZ ".
"]
        JRST    EX

XSWS:   MOVE C,LODSP
        EXCH C,LODSP2
        MOVEM C,LODSP
        MOVE C,LODCTR
        EXCH C,LODCT2
        MOVEM C,LODCTR
        POPJ P,

CHAIN:  PUSHJ P,STARGS  ;THIS TAKES AN ARGUMENT
        TRON FL3,CHN2BT!CHNBT   ;SET FOR HAVE EMITTED AND EMIT /<
        TRNN FL,IDF
        JRST SYNERR     ;IF ARG NOT IDENT OR HAVE SEEN /CHAIN BEFORE
        TRNN FL,DOLOD   ;IGNORE IF NOT LOADING
        JRST FINPRS
        MOVEI T3,OUTLOD
        PUSHJ P,OUCRLF  ;GET ON A NEW LINE
        MOVE T2,ACCUM
        PUSHJ P,OUTSIX  ;PUT OUT THE NAME
        MOVEI T1,"_"
        PUSHJ P,OUTLOD
        TRZ FL,LODOUT   ;DO NOT NEED CRLF
        JRST FINPRS

LNKSP:  PUSHJ   P,GETNAM        ;SWITCHES IN LINK
        MOVE    T3,OUTLOD
        MOVE    T4,SVPT
        PUSHJ   P,CMNO
        POP     P,T2
        PUSHJ   P,OUTSIX
        JRST    SCAN

LINK:   TROA FL3,LINKB  ;SET LINK
SLINK:  TRO FL3,SLINKB  ;OR SLINK BITS
        TRNN FL3,CHN2BT ;MUST HAVE SEEN /CHAIN
        JRST SYNERR
        JRST SCAN
;HERE ON "/" AFTER A FILE NAME

COMPSW: TRZ FL,PERF     ;DOING TEMP
        PUSHJ P,COMPS
        JRST ILP2

;HERE ON "/" AS FIRST CHAR OF IDENT, I.E. PERM SW

COMPS1: TRO FL,PERF
        PUSHJ P,COMPS
        JRST ILP0A

SETMAP: TRO FL3,MAP2
        TRON FL,MAPST   ;IS IT SET?
        TRNN FL,DOLOD   ;OR NOT LOADING?
        JRST SCAN
        MOVEI T3,OUTLOD
        PUSHJ   P,OUCRLF
        MOVE T2,[SIXBIT /MAP_/]
        PUSHJ P,OUTSIX
        TRZ FL,LODOUT   ;DO NOT NEED A COMMA FOR NEXT FILE
        JRST SCAN

SETBDV: MOVEI T4,BINDV
        TRZA FL3,REWBT  ;NO REWIND WHILE CHANGING
SETLDV: MOVEI T4,LSTDV
        PUSHJ P,STARGS
        TRNN FL,IDF     ;DEVICE NAME MUST BE IDENT
        JRST SYNERR
        MOVE T2,ACCUM
        CAMN T2,[SIXBIT /DSK/]  ;DSK IS A SPECIAL CASE
        MOVEI T2,0      ;SAY NO SPECIAL DEVICE
        MOVEM T2,(T4)
        PUSHJ P,SCAN    ;LOOK FOR :
        CAIN C,":"
        PUSHJ P,SCAN
        TRNN FL3,PARFLG ;IS IT IN ()
        POPJ P,         ;NO, JUST EXIT
        EXCH T2,LOKNAM
        OPEN LOOK,LOKINT
        JRST DEVNA      ;NOT THERE
        MOVEM T2,LOKNAM ;RESTORE NAME
NXACT:  CAIN C,")"
        JRST OLDV       ;GET OLD ONE BACK
        TRNN FL,IDF     ;IF IDENT
        JRST SYNERR     ;OK, ELSE ERROR
        MOVE T1,[XWD -ACTLG,ACTAB]
        PUSHJ P,SWSCAN  ;LOOK IT UP
        JRST UNRECS
        JFCL
        XCT ACTAB2-ACTAB(T1)
        PUSHJ P,SCAN
        JRST NXACT
OLDV:   OPEN LOOK,DSKLK
        JRST DSKNA
        JRST SCAN

DEFINE TABLE
<SWITCH ZERO,<CALLI LOOK,UTPCLR>
SWITCH ZER%,<HALT>      ;SHOULD NEVER GET HERE
SWITCH REWIND,<PUSHJ P,DVREW>
SWITCH ADVANCE,<MTAPE LOOK,16>
SWITCH BACKSPACE,<MTAPE LOOK,17>
SWITCH EOT,<MTAPE LOOK,10>
>
TBGEN (ACTAB,ACTLG,ACTAB2)

DVREW:  MTAPE LOOK,1    ;REWIND
        CAIN T4,BINDV   ;IF BINARY
        TRO FL3,REWBT
        POPJ P,         ;REWIND TO LOADER IF NEEDED
PROCSW:        TROE FL,PROCS   ;HAVE WE ALREADY SEEN SOME?
        JRST SYNERR     ;YES, I DEFINE THIS AS ILLEGAL
        SETZB   T3,SWBKS(SVPT)
        MOVE    C,[0 @PROCS0]
        MOVEM   C,PCM#
        MOVEM   SWPT,SWPT0#
PROCS1: PUSHJ   P,GETCH
        SOJLE   SWCNT,ETMS
        CAIE    C,")"
        CAIN    C,","
        JRST    PROCS2
        JUMPLE  CS,SYNERR
        IDPB    C,SWPT0
        CAIE    C,")"
        JRST    PROCS1
        JRST    ILP2A

PROCS0: 0       SWBKS(SVPT)
        EXP     SWBKB,SWBKL    ;DON'T YOU DARE TO MOVE IT
PROCS2: CAMN    SWPT,SWPT0
        JRST    PROCS3
        SKIPE   @PCM
        JRST    SYNERR
        MOVEM   SWPT,@PCM
        IDPB    T3,SWPT0
        MOVE    SWPT,SWPT0
PROCS3: CAIN    C,")"
        JRST    ILP2A
        AOS     C,PCM
        SKIPE   @PCM
        JRST    SYNERR
        JRST    PROCS1

SETONM:        TRNE FL,PROCS   ;PROCESSOR SWITCHES NOT PERMITTED HERE
        JRST SYNERR
        MOVE T1,SVNAM(SVPT)     ;GET THE NAME
        MOVEM T1,ONAM   ;AND SAVE IT AWAY
        MOVE T1,SVEXT(SVPT)
        MOVEM T1,OEXT
        MOVE T1,SVPPN(SVPT)
        MOVEM T1,OPPN   ;SET UP EXT AND PPN FOR OUTPUT NAME
        PUSHJ P,XSWS
        JRST ILP0       ;AND GO

LOADS1: PUSHJ P,LODS1
        JRST ILP0
LOADS2: PUSH P,[ILP2A]  ;SET RETURN POINT
LODS1:  PUSHJ P,GETCH   ;NEXT CHR
        CAIE    C,"-"           ;ALLOW MINUS IN LOADER SWITCHES
        JUMPLE CS,SYNERR        ;SPECIAL CHARACTERS NOT PERMITTED
        IDPB C,LODSP    ;SAVE IT
        SOSG LODCTR     ;CHECK SIZE
        JRST ETMS
        TLNN CS,NUMF    ;A NUMBER
        POPJ P,         ;NO, DONE
        JRST LODS1      ;YES, THEY GET PASSED ON

SWBITS: HRRZ T2,(T4)    ;LIST POINTER IN T4 LIST GEN WITH SBTS
        XCT @(P)        ;THE MAJIC TEST INSTR
        JRST NOSW       ;NOT THIS ONE
        HLLZ T2,(T4)    ;GET SIXBIT
        PUSHJ P,OUTSIX
NOSW:   AOBJN T4,SWBITS
        AOS (P)         ;SKIP THE INSTRUCTION WE WERE EXECUTING
        POPJ P,

SETCRF: TRZ FL3,ALLCRF  ;GET RID OF FLAGS
SETCRC: MOVSI T1,CRSW!LISTSW
        JRST INTSW      ;FAKE A SWITCH AND RETURN

SETCRO: TRZ FL3,ALLCRF
        PUSHJ P,STARGS
SETCR1: TRNN FL,IDF
        JRST SYNERR
        MOVE T1,[XWD -CLG,CSWS]
        PUSHJ P,SWSCAN
        JRST UNRECS
        JFCL
        TRO FL3,@CSWS2-CSWS(T1) ;SET SWITCH
        TRNN FL3,PARFLG ;IN THE () MODE?
        JRST SETCRC     ;SCAN AND FAKE SWITCH
        PUSHJ P,SCAN
        CAIE C,")"
        JRST SETCR1
        JRST SETCRC

DEFINE TABLE
<SWITCH NOSYMBOLS,CNSBT
SWITCH NOMACROS,CNMBT
SWITCH OPCODES,COPBT
SWITCH NOLISTING,CNLBT
SWITCH MONITOR,CMONBT
SWITCH COMBINE,CCMBT>
TBGEN (CSWS,CLG,CSWS2)
SETUP:  MOVEM   C,SAVC#
        SKIPE ONAM      ;SET ONAM IF NOT ALREADY
        JRST ONSET
        MOVE T1,SVNAM(SVPT)     ;AS LAST FILE NAME
        MOVEM T1,ONAM
        MOVE T1,SVPPN(SVPT)
        MOVEM T1,OPPN   ;SET UP PPN SAME AS INPUT
        SETZM OEXT      ;AUTO REL EXT
ONSET:  MOVSI T1,-NFILE ;NUMBER OF FILES
        TRZ FL,NODAT    ;WE HAVE NOT SEEN A DIFFERENT DEVICE
        SETZM SDAT      ;LATEST DATE
        SETZM STIM      ;AND LATEST TIME
        SKIPN T2,SVDEV  ;IF A DEVICE FOR FIRST FILE, USE IT
        MOVE T2,LOKNAM  ;ELSE SAVED NAME (ALL MUST BE SAME
        MOVEM T2,ODEV#  ;OR THIS WILL BE ZEROED LATER
        TLZ FL2,-1      ;NO PROCESSOR YET
        PUSHJ P,GETPRO  ;GO FIND DATE AND PROCESSOR
        TLNE FL2,RELSW  ;IF A REL FILE
        JRST NOCOM1      ;GO LOAD IT NOW
        TRNN FL,NODAT   ;NO DATES ON OTHER DEVICES
        TLNE FL3,COMPLS ;DO WE ALWAYS WANT TO COMPILE?
        JRST DOCOMP     ;YES, IGNORE DATES
        MOVE T1,OPPN    ;LOOK ON THIS AREA FOR REL
REREL:  MOVEM T1,LPPN   ;BUT ONLY FIRST TIME
        MOVE T1,ONAM    ;SEE IF REL IS THERE
        MOVEM T1,LNAM
        SKIPN T1,OEXT   ;CHECK FOR OUTPUT EXTENSION
        MOVSI T1,SIXBIT '   REL'
        MOVEM T1,LEXT
        SKIPN T2,BINDV
        JRST NOBDVR
        CALLI T2,DEVCHR
        TLNN T2,DSKBT!DTABT
        JRST DOCOMP     ;NOT A DIRECTORY DEVICE, RECOMPILE
        MOVE T2,BINDV
        EXCH T2,LOKNAM
        OPEN LOOK,LOKINT
        JRST DEVNA
        LOOKUP LOOK,LNAM
        TRZA FL3,PARFLG ;A GOOD TEMP FLAG
        TRO FL3,PARFLG
        OPEN LOOK,DSKLK
        JRST DSKNA
        TRZN FL3,PARFLG
        JRST DOCOMP     ;COMPILE
IFN WRD4,<LDB T2,[POINT 11,LDAT,23]
        IMULI T2,^D60   ;CONVERT TO 17 BIT TIME
        DPB T2,LDAT.TIME>
        JRST RELDT
NOBDVR: IFE WRD4,<LOOKUP LOOK,LNAM      ;IS IT THERE>
IFN WRD4,<PUSHJ P,UFDLK         ;CHECK FOR IT IN UFD IN CORE>
        JRST DOCOM1     ;NO, WE MUST RECOMPILE
RELDT:  LDB T2,LDAT.DATE       ;GET DATE
        CAMGE T2,SDAT   ;EARLIER
        JRST DOCOMP     ;YES, COMPILE
        CAME T2,SDAT    ;SAME?
        JRST NOCOM1     ;NO, ALL OK< UNLESS NEED LST FILE>
        LDB T2,LDAT.TIME     ;YES, GET TIME
        CAMG T2,STIM    ;LATER?
        JRST DOCOMP     ;NO
        JRST    NOCOM1          ;DON'T NEED TO RECOMPILE
IFE 1,<
NOCOMW: TLNE FL3,LISTSW ;IF WANTS LISTING
        SKIPE LSTDV     ;AND NO LISTING DEVICE
        JRST NOCOM1
        SETZM LPPN      ;THEN LOOK TO SEE IF THERE
        MOVE T1,ONAM
        MOVEM T1,LNAM
        MOVSI T1,(SIXBIT /LST/)
        MOVEM T1,LEXT
IFE WRD4,<LOOKUP LOOK,LNAM>
IFN WRD4,<PUSHJ P,UFDLK>
        JRST DOCOML     ;NOT THERE, GENERATE LISTING
        LDB T2,LDAT.DATE       ;CHECK DATE
        CAMGE T2,SDAT
        JRST DOCOML     ;OLD ONE
        CAME T2,SDAT
        JRST NOCOM1     ;ALL OK
        LDB T2,LDAT.TIME
        CAMLE T2,STIM
        JRST NOCOM1     ;GOOD ONE
        JRST DOCOML     ;OLD ONE
>
DOCOM1:        SKIPE OPPN
        JRST    [SETZB T1,OPPN
                JRST REREL]     ;TRY REL
DOCOMP: TROA FL,NDREL   ;WE NEED TO GENERATE A REL FILE
DOCOML: TRZ FL,NDREL    ;WE JUST WANT LISTING FILE
        SETZM OPPN
        MOVEI T3,0      ;GET THE PROCESSOR
        SKIPA T1,FL2
DC1:    LSH T1,1        ;TRY NEXT BIT
        TLNN T1,400000  ;IS HIGH BIT ON YET?
        AOJA T3,DC1     ;TURNS IT INTO A NUMBER
DC2:
        MOVEM T3,PCNUM  ;SAVE IT FOR LATER
        CAIL T3,NPROCS
        ADDI T3,1       ;ONE MORE (BECAUSE OF REL-LOADER IN TABLE)
        MOVE T3,PRCTAB(T3)      ;GET THE NAME OF THE OUTPUT ROUTINE
        TRNN FL,NDREL   ;REL NEEDED?
        JRST TRYLST     ;NO, LISTING
        SKIPE T2,BINDV
        PUSHJ P,OUTDV
        MOVE T2,ONAM    ;START PUTTING OUT
        PUSHJ P,OUTSIX
        SKIPN T2,OEXT
        JRST NREXT
        HLLZS T2        ;PUT OUT EXTENSION
        MOVEI T1,"."
        PUSHJ P,(T3)
        PUSHJ P,OUTSIX
NREXT:  SKIPE T2,SWBKB  ;ARE THERE SWITCHES
        PUSHJ P,OUTSW   ;YES, OUTPUT THEM
TRYLST: TLNN FL3,LISTSW ;LISTING REQUESTED?
        JRST NOLST
        MOVEI T1,","    ;YES, NEED A COMMA
        PUSHJ P,(T3)
        SKIPE T2,LSTDV
        PUSHJ P,OUTDV
        MOVE T2,ONAM    ;SET IT UP
        PUSHJ P,OUTSIX
        TLNN FL3,CRSW   ;CREF MAYBE
        JRST NOLST1
       MOVSI T2,SIXBIT '   /C '
        PUSHJ P,OUTSIX
NOLST1: PUSH P,T3       ;PUT OUT TO CREF FOR /L ALSO
        PUSHJ P,ENTCRF  ;PUT IT IN THE ###CREF FILE
        POP P,T3
        SKIPE T2,SWBKL  ;SWITCHES?
        PUSHJ P,OUTSW
NOLST:  MOVEI T1,"_"
        PUSHJ P,(T3)
        MOVSI T4,-NFILE
        TRNE    FL3,FDBGSW      ;IF THIS IS AN FDEBUG COMMAND
        TLNN    FL2,F4SW!FTFSW ;AND THIS IS A FORTRAN COMPILATION
        JRST    PRCLP           ;THEN OUTPUT (X) TO COMPILER
        MOVSI   T2,(SIXBIT /(X)/) ;...
        PUSHJ   P,OUTSIX        ;...
PRCLP:  PUSHJ P,CMNO
        SKIPE T2,SWBKS(T4)      ;AND SWITCHES
        PUSHJ P,OUTSW
        CAMN T4,SVPT    ;ALL DONE?
        JRST ENDPRC     ;YES, GO FINISH UP AND CONSIDER LOADING
        MOVEI T1,","
        PUSHJ P,(T3)    ;NEXT FILE
        AOBJN T4,PRCLP
ENDPRC: PUSHJ   P,OUCRLF
IFN SPRC,<
        TLNN FL2,SPRC>
        JRST NOCOM1     ;GO LOAD
IFN SPRC,<
        MOVSI SVPT,-NFILE       ;RESET POINTER
        MOVE T1,ONAM    ;AND FAKE WORLD
        MOVEM T1,SVNAM
        MOVE T3,PCNUM   ;GET BACK PROCESSOR NUMBER
        MOVE T1,INTEXT(T3)      ;GET EXTENSION
        MOVEM T1,SVEXT
        SETZM SVPPN
        SETZM SWBKS
        SETZM SWBKB
        SETZM SWBKL
        HRL FL2,NXPC(T3)        ;SET FOR NEXT PROCESSOR
        JRST DOCOMP     ;AND GO EMIT CALLS
>
CMNO:   SKIPN T2,SVDEV(T4)      ;IS THERE A DEVICE THERE
        JRST PRCLP2     ;NO
        PUSHJ P,OUTSIX  ;YES, PRINT IT
        MOVEI T1,":"
        PUSHJ P,(T3)
PRCLP2: SKIPE T2,SVNAM(T4)      ;PUT OUT NAME
        PUSHJ P,OUTSIX
        HLLZ T2,SVEXT(T4)       ;GET EXTENSION
        JUMPE T2,NOEXT  ;NONE THERE
        MOVEI T1,"."
        PUSHJ P,(T3)
        PUSHJ P,OUTSIX
NOEXT:  SKIPE T2,SVPPN(T4)      ;NEED PPN?
        PUSHJ P,OUTPPN  ;PUT THEM OUT
        POPJ P,
NOCOM1: TLNN FL3,NOLODS ;IF NOT TO LOAD THIS ONE
        TRNN FL,DOLOD   ;OR NOT LOADING
        JRST NXFILP     ;NO, GO TO NEXT
        MOVEI C,0
        IDPB C,LODSP    ;END SECOND SET OF SWITCHES
        IDPB C,LODSP2
        MOVEI T3,OUTLOD ;SET FOR LOADER
        TRNN    FL3,LNKSW
        TRZN FL,LODOUT  ;IS THERE ALREADY OUTPUT THERE?
        SKIPA
        PUSHJ P,OUCRLF
        MOVE T2,[POINT 7,LODSBK]        ;OUTPUT FIRST SWITCHES
        PUSHJ P,OUTSW
        TLNE FL2,RELSW
        SKIPA T2,ODEV   ;IF REL SW, USE GIVEN DEVICE
        MOVE T2,BINDV
        JUMPN T2,LODR0  ;GO USE IT
        EXCH T2,BINNAM  ;SAVE IT, CHECK OLD ONE
        JUMPE T2,LODR1  ;BOTH 0 NO NAME
        SKIPA T2,[SIXBIT /DSK/] ;SET FOR DSK
LODR0:  MOVEM T2,BINNAM ;SAVE NAME
        PUSHJ P,OUTDV
LODR1:  MOVE T2,ONAM    ;NOW FILE NAME
        PUSHJ P,OUTSIX
        SKIPN T2,OEXT
        JRST LODR2
        HLLZS T2
        MOVEI T1,"."
        PUSHJ P,OUTLOD
        PUSHJ P,OUTSIX
LODR2:  SKIPE T2,OPPN   ;THEN THINK ABOUT PPN
        PUSHJ P,OUTPPN
        TLNN FL3,LIBSW  ;LIBRARY?
        JRST    [TRZN FL3,INLIB ;IN LIB MODE CURRENTLY?
                JRST ELOD       ;NO, IGNORE
                MOVSI T2,(SIXBIT :/N:)
                JRST ELODP]
        MOVSI T2,SIXBIT '   /L '        ;TELL LOADER
        TRO FL3,INLIB
ELODP:  PUSHJ P,OUTSIX
ELOD:   SKIPN   FTFFLG
        JRST    ELOD1
        SETZM   FTFFLG
        SETZ    T2,
        PUSHJ   P,OUTSIX
        MOVE    T2,[SIXBIT "SYS:FT"]
        PUSHJ   P,OUTSIX
        MOVE    T2,[SIXBIT "LIB4.R"]
        PUSHJ   P,OUTSIX
        MOVE    T2,[SIXBIT 'EL/L']
        PUSHJ   P,OUTSIX
        PUSHJ   P,OUTSIX
ELOD1:  MOVE T2,[POINT 7,LODSB2]        ;OUTPUT SECOND SET OF SWITCHES
        PUSHJ P,OUTSW
        MOVE T4,[XWD -LDSBLG,LDSBTB]
        PUSHJ P,SWBITS
        TRZN FL3,(T2)   ;THE INSTR TO EXECUTE FOR EACH ENTRY
        SKIPN   CDEBFL          ;COBOL DEBUG
        JRST    ELOD2           ;NO
        MOVE    T2,[SIXBIT ',SYS:C']
        PUSHJ   P,OUTSIX
        MOVE    T2,[SIXBIT 'OBDDT']
        PUSHJ   P,OUTSIX
ELOD2:  TRO FL,LODOUT   ;MARK AS HAVING OUTPUT THERE
        TRNN    FL3,LNKSW
        JRST NXFILP
        MOVE    T1,SAVC
        PUSHJ   P,OUTLOD
        JRST    NXFILP

LDSBTB: SBTS Y,REWBT
        SBTS M,MAP2
        SBTS =,SLINKB
        SBTS >,LINKB
        <SIXBIT !/<!>+CHNBT     ;CAN NOT GET IT IN WITH A MACRO
LDSBLG==.-LDSBTB
GETPRO:        MOVE T3,NPROC1
NFIL:   MOVE T2,SVNAM(T1)       ;SET UP NAME AND PPN
        MOVEM T2,LNAM
        MOVE    T2,SVPPN(T1)
        MOVEM   T2,LPPN
        MOVSI   T2,'FTF'
        MOVEM   T2,LEXT
        PUSHJ   P,ALTDEV
        PUSHJ   P,UFDLK
        SKIPA
        SETOM   FTFFLG#
        HLLZ T2,SVEXT(T1)
NXEXT:  MOVEM T2,LEXT   ;START WITH ORIGINAL EXT
        MOVE T2,SVPPN(T1)
        MOVEM T2,LPPN
        CAME T2,OPPN
        SETZM OPPN      ;IF DIFFERENT PPNS, FILE MUST BE ON HIS
        PUSHJ P,ALTDEV  ;WORRY ABOUT SOME OTHER DEVICE
OKLOOK: IFE WRD4,<LOOKUP LOOK,LNAM>
IFN WRD4,<PUSHJ P,UFDLK         ;CHECK IN CORE>
        JRST NOTYET     ;HAVE NOT FOUND IT YET
DNLOK:  HLLZ T2,LEXT    ;GET THE EXTENSION
FNDAT:  CAMN T2,[SIXBIT /REL/]
        JRST ISREL      ;SPECIAL FOR REL
        MOVEM T2,SVEXT(T1)      ;SAVE EXTENSION
        LDB T2,LDAT.DATE       ;GET DATE
        CAMLE T2,SDAT   ;AND CHECK TO SEE IF LATEST
        JRST SETDT
        CAME T2,SDAT
        JRST OLDAT
        LDB T2,LDAT.TIME
        CAMLE T2,STIM
SETTM:  MOVEM T2,STIM   ;MARK WITH LATER ONE
OLDAT:  HLLZ T2,LEXT    ;GET THE EXTENSION WE FOUND
OLDAT1: JUMPE T2,SETCP  ;SET TO CURRENT PROCESSOR
        MOVN T3,NPROC1
        HRLZS T3        ;MINUS AND IN LEFT HALF
        CAME T2,EXTAB(T3)
        AOBJN T3,.-1
        JUMPGE T3,SETCP ;NOT THERE
        TLNE FL2,@ISPTAB(T3)    ;IS THAT ONE ALREADY SET?
        JRST NFIL2
        TLNE FL2,@CALPRO        ;IS ANY SET?
        JRST PROCON     ;YES, WE HAVE A CONFLICT
        TLO FL2,@ISPTAB(T3)     ;SET UP FOR THIS ONE
NFIL2:  CAME T1,SVPT    ;ARE WE DONE?
NFIL1:  AOBJN T1,GETPRO ;NO, GO ON
        POPJ P,
SETDT: MOVEM T2,SDAT
        LDB T2,LDAT.TIME     ;AND TIME
        JRST SETTM

SETCP:  CAME T1,SVPT    ;AT END?
        JRST NFIL1      ;NO, DO NOT SET
        TLNN FL2,@CALPRO        ;SOMETHING ALREDY SET?
        HLL FL2,FL3     ;NO, SET TO CURRENT PROCESSOR
        POPJ P,         ;AND DONE

NOTYET: SKIPE T2,SVEXT(T1)      ;GET THE CURRENT EXT
        JRST NOFIL1     ;IF HE SPECIFIED AN EXT WE LOSE
PALFAL: MOVE T2,EXTAB-1(T3)     ;ELSE PICK UP ONE
        SOJGE T3,NXEXT
NOFIL1: TLNN FL3,FORWDS ;IF NOT FORWARD
        JRST TRYREL     ;THEN NONE THERE
FORFIL: DATE    T2,     ;GET DATE
        ADDI T2,1       ;SAY IT WAS TOMORROW
        MOVEM T2,LDAT   ;TO FORCE COMPILATION
        HLLZ T2,SVEXT   ;GET EXTENSION
        JRST FNDAT      ;AND GO


TRYREL: MOVE T2,ONAM
        MOVEM T2,LNAM
        SKIPN T2,OEXT
        MOVSI T2,(SIXBIT /REL/)
        MOVEM T2,LEXT
RELT2:  MOVE T2,OPPN
        MOVEM T2,LPPN
        SKIPN T2,ODEV   ;CHECK FOR SAVED OUTPUT DEVICE
        MOVE T2,BINDV   ;ELSE USE BINARY DEV
        EXCH T2,LOKNAM
        MOVEM T2,SVLNM# ;SAVE IT
        PUSHJ P,ALTDVP  ;DOO THE LOOKUP OR RETURN
IFE WRD4,<LOOKUP LOOK,LNAM      ;FOR THIS ONE>
IFN WRD4,<PUSHJ P,UFDLK>
        JRST    [SKIPN ODEV     ;WAS IT FROM HERE??
                JRST NOFILP     ;NO, ALL OUT OF PLACES TO LOOK
                SETZM ODEV      ;FORGET ABOUT IT
                MOVE T2,SVLNM
                MOVEM T2,LOKNAM
                JRST RELT2]     ;TRY AGAIN
        MOVE T2,SVLNM
        EXCH T2,LOKNAM
        MOVEM T2,ODEV   ;WHERE WE FOUND IT
        JRST SETREL     ;AND AWAY WE GO
ISREL:  TRNE SVPT,-1    ;MORE THAN 1?
        JRST SYNERR     ;NOT PERMITTED
        MOVE T2,ONAM    ;CHECK FOR DIFFERENT NAMES
        CAME T2,SVNAM
        JRST SYNERR     ;FLAG AS ERROR
        SKIPE T2,OEXT   ;IF EXTENSION
        CAMN T2,SVEXT   ;AND DIFFERENT
        SKIPA
        JRST SYNERR     ;ALSO ERROR
SETREL: TLO FL2,RELSW   ;IT IS A REL FILE
        POPJ P,
ALTDEV:        SKIPN T2,SVDEV(T1)      ;SPECIAL DEVICE
ALTDVP: SKIPE T2,LOKNAM         ;OR REMEMBERING??
        SKIPA           ;YES
        POPJ P,         ;NO, JUST DO LOOKUP
        CAME T2,ODEV
        SETZM ODEV      ;IF NOT THE SAME, GIVE UP
        MOVEM T2,LOKNAM ;SAVE FOR LATER
        MOVEM T2,SVDEV(T1)      ;AND IN DEVICE FOR OUTPUT
        CAMN    T2,[SIXBIT /SYS/]
        JRST    [CALLI  T2,DEVCHR               ;DEVCHR
                TLNE    T2,DSKBT        ;IS IT DSK:?
                JRST    ALTDV1          ;YES
                JRST    ALTDV2]
        CALLI T2,DEVCHR ;GET CHARACTERISTICS
        TLNE T2,DSKBT   ;A DSK?
        JRST    [SETZM LOKNAM   ;NO MORE NEED
                POPJ P,]        ;USE THIS DEVICE
ALTDV2: TRO FL,NODAT    ;NO DATES ON OTHER DEVICES
        TLNN T2,DTABT   ;A DECTAPE?
        JRST CPOPJ2     ;SAY WE FOUND IT
ALTDV1: OPEN LOOK,LOKINT        ;OPEN FOR INPUT
        JRST DEVNA      ;NOT THERE
        LOOKUP LOOK,LNAM        ;SEE IF FILE IS
        SOS(P)          ;RETURN TO NOT FOUND
        OPEN LOOK,DSKLK ;GET THE DSK BACK
        JRST DSKNA      ;I HOPE THIS NEVER HAPPENES
CPOPJ2: MOVEI T2,2      ;ADVANCE RETURN
        ADDM T2,(P)
IFN WRD4,<DPOPJ:LDB T2,[POINT 11,LDAT,23]
        IMULI T2,^D60   ;CONVERT TIME
        DPB T2,LDAT.TIME>
        POPJ P, ;BACK WE GO

IFE STANSW,<
OUTPPN: HRRZM T2,SAVPPN ;CONVERT TO SIXBIT FOR OUTPUT
        MOVEI T1,"["    ;START OUT
        PUSHJ P,(T3)
        HLRZ T1,T2      ;GET NUMBER
        PUSHJ P,OUTOCT
        MOVEI T1,","
        PUSHJ P,(T3)
        HRRZ T1,SAVPPN
        PUSHJ P,OUTOCT
        MOVEI T1,"]"
        JRST (T3)

OUTOCT: IDIVI T1,10     ;OCTAL OUTPUT
        HRLM T2,(P)
        SKIPE T1
        PUSHJ P,OUTOCT
        HLRZ T1,(P)
        ADDI T1,"0"
        JRST (T3)
>

NOFILP: MOVE T2,SVNAM(T1)       ;RESET THINGS FOR ERROR MSG
        MOVEM T2,NAME
        MOVE T2,SVEXT(T1)
        HLLZM T2,NAME+1
        JRST NOFIL
IFN WRD4,<
RDIUFD: MOVE T1,USRPPN  ;GET THE UFD TO LOOK AT
        MOVEM T1,CKPPN# ;SAVE IT
        MOVEM T1,LNAM
        MOVSI T1,(SIXBIT /UFD/) ;GET SET FOR LOOKUP
        MOVEM T1,LEXT
        MOVE T1,[XWD 1,1]
        MOVEM T1,LPPN
        MOVEI T1,14     ;NEED DSK IN MODE 14
        EXCH T1,DSKLK
        OPEN LOOK,DSKLK
        JRST DSKNA
        MOVEM T1,DSKLK  ;KEEP OLD MODE FOR LATER
        LOOKUP LOOK,LNAM
        JRST ENDUFF     ;RESET CKPPN AND GET OUT
        PUSHJ P,CHKRM
        MOVE T1,JOBFF
        MOVEM T1,SUJFF# ;SAVE FOR LATER
        INBUF LOOK,2    ;GET BUFFERS
        SETZM UFDARY
        MOVE T1,[XWD UFDARY,UFDARY+1]
        BLT T1,UFDARY+HASH-1
MRUFDC: PUSHJ P,CHKRM   ;GET ROOM TO PUT UFD
        MOVE T1,JOBFF   ;HERE IS ROOM
        HRLI T1,-127    ;THIS IS THE NUMBER OF ENTRIES TO FIT
NXTUFD: PUSHJ P,UFDENT  ;GET AN ENTRY T2=NAME,T3=DATE
        JRST ENDUFD     ;T4=EXT, SKIP RETURN IF NOT EOF
        JUMPE T2,NXTUFD ;ZERO NAME IS NULL ENTRY
        MOVEM T2,(T1)   ;SAVE NAME
        MOVEM T3,2(T1)  ;AND DATE
        IDIVI T2,HASH   ;HASH IT
        MOVM T2,T3      ;(TO MAKE IT POSITIVE)
        HRR T4,UFDARY(T2)       ;LINK IT IN
        MOVEM T4,1(T1)  ;AND SAVE EXT AND LINK
        HRRZM T1,UFDARY(T2)     ;SAVE NEW POINTER
        ADDI T1,2       ;USED 3 WORDS
        AOBJN T1,NXTUFD ;AND ONE ENTRY, CHECK FOR OVERFLOW
        JRST MRUFDC     ;WE LOSE BY AN ENTRY BUT SO WHAT
ENDUFF: SETZM CKPPN     ;SO COMPARE WILL LOSE
ENDUFD: RELEASE LOOK,0
        OPEN LOOK,DSKLK
        JRST DSKNA      ;I HOPE NOT
        MOVE T1,SUJFF
        MOVEM T1,FREBUF ;SET AS NO LONGER IN USE
        POPJ P,

UFDENT: JSP T5,UFDWD    ;SO IT CAN POPJ
        ILDB T2,LOOKBF+1        ;GET THE WORD
        JUMPE T2,UFDENT         ;IGNORE 0 WORDS
        JSP T5,UFDWD
        ILDB T4,LOOKBF+1        ;EXT
        JSP T5,UFDWD
        ILDB T3,LOOKBF+1        ;DATE
        JSP T5,UFDWD    ;THIS IS SIZE WHICH WE DON'T WANT
        IBP LOOKBF+1    ;SO JUST SKIP IT
        JSP T5,UFDWD    ;SKIP ANOTHER WORD
        IBP LOOKBF+1
        AOS (P)         ;SKIP RETURN IF ALL IS WELL
        POPJ P,

UFDWD:  SOSG LOOKBF+2
        IN LOOK,0       ;SKIPS ON ERROR
        JRST (T5)       ;SO ALL OK
        STATO LOOK,20000        ;CHECK FOR EOF
        JRST READER     ;LOSE BIG
        POPJ P,         ;REURN TO CALLER
UFDLK: SKIPN T2,LPPN   ;GET SPECIFIED PPN
        MOVE T2,USRPPN  ;OR THIS USERS
        CAME T2,CKPPN   ;AND CHECK WITH ONE IN CORE
        JRST    [LOOKUP LOOK,LNAM
                POPJ P, ;LOSE
                AOS (P) ;SKIP RETURN
                JRST DPOPJ]     ;AND SET DATE
        PUSH P,T3       ;NEED EXTRA AC
        HLLZS LEXT      ;JUST IN CASE IT NEEDS IT
        MOVE T2,LNAM    ;GET NAME
        IDIVI T2,HASH   ;AND HASH
        MOVM T2,T3
        HRRZ T2,UFDARY(T2)
        JUMPE T2,T3POPJ ;NOT IN DIRECTORY
        MOVE T3,LNAM
NMCM:   CAME T3,(T2)    ;THIS ONE?
        JRST NXTNM
        HLLZ T3,1(T2)   ;GET EXT
        CAME T3,LEXT    ;AND CHECK
        JRST NXTRNM     ;GET NEXT AND RESTORE NAME
        MOVE T3,2(T2)   ;NOW THE DATE AND TIME
        MOVEM T3,LDAT
        AOS -1(P)       ;SKIP RETURN
T3POPJ: POP P,T3
        POPJ P,

NXTRNM: MOVE T3,LNAM    ;THIS GOT WIPED OUT
NXTNM:  HRRZ T2,1(T2)   ;LINK
        JUMPE T2,T3POPJ ;GIVE UP AND GO HOME
        JRST NMCM       ;GO LOOK SOME MORE
ARRAY UFDARY[HASH]
>
DEFINE X (A,B,C,D,E)
<B'SW>

ISPTAB: PROCESS
        FOR (1,NSDNM,<CAT(NSDS,\XXX)>)  ;SWITCH TO TURN ON

OUTSIX: MOVEI T1,0
        LSHC T1,6
        ADDI T1,40
        PUSHJ P,(T3)
        JUMPN T2,OUTSIX
CPOPJ:  POPJ P,

IFN STANSW,<
OUTPPN: MOVEM T1,SAVPPN ;SAVE IT AWAY
        ANDCMI T2,-1
        MOVEI T1,"["
        PUSHJ P,(T3)
        PUSHJ P,OUTSIX  ;PRINT IT
        MOVEI T1,","    ;AND A COMMA
        PUSHJ P,(T3)
        HRLZ T2,SAVPPN
        PUSHJ P,OUTSIX
        MOVEI T1,"]"
        JRST (T3)
>

OUTSW:  MOVEM T2,SVSWP  ;SAVE THE POINTER
        ILDB T1,T2      ;PICK UP THE FIRST CHR
        JUMPE T1,CPOPJ  ;AND CHECK FOR NULL AS A PRECAUTION
        MOVEI T1,"("
        PUSHJ P,(T3)    ;SWITCHES ARE IN () TO PROCESSOR
OUTSW1: ILDB T1,SVSWP
        JUMPE T1,[MOVEI T1,")"
                JRST    (T3)]
        PUSHJ P,(T3)
        JRST OUTSW1     ;A NULL WILL MARK THE END

OUTDV:  PUSHJ P,OUTSIX  ;DEVICE IS IN T2
        MOVEI T1,":"
        JRST (T3)
ENTCNM:        MOVE T1,CORTOP  ;CHECK TO SEE IF NAME ALREADY THERE
ENTC1:  CAMN T1,CORT1
        JRST ENTC2
        CAMN T2,1(T1)
        POPJ P, ;NAME THERE, EXIT
        AOJA T1,ENTC1   ;CHECK ANOTHERE
ENTC2:  MOVEM T2,@CORTOP        ;SAVE IT
        AOS (P)         ;SKIP RETURN IF ENTERED
        SOS T1,CORTOP
        CAMG T1,SVJFF   ;CHECK TO SEE IF CORE EXCEEDED
        JRST XPAND
        POPJ P,

ENTCRF: MOVE T2,ONAM
        PUSHJ P,ENTCNM  ;SEE IF NAME ALREADY THERE
        POPJ P, ;YES, DONE
        MOVEI T1,"_"
        PUSHJ P,OUTCRF
        MOVEI T3,OUTCRF
        SKIPE T2,LSTDV
        PUSHJ P,OUTDV   ;PUT OUT DEVICE NAME
        MOVE T2,ONAM
        PUSHJ P,OUTSIX
        MOVSI T2,(SIXBIT !/L!)  ;NO PUT OUT /L FOR A LIST FILE
        TLNN FL3,CRSW   ;IS THIS REALLY CREF??
        PUSHJ P,OUTSIX
        MOVE T4,[XWD -CSBL,CSBT]        ;PUT OUT EXTRA SWS
        PUSHJ P,SWBITS
        TRNN FL3,(T2)
        JRST OUCRLF

FINCRF: MOVSI IOPNT,-2  ;PERMIT ONLY THIS ONE LEVEL
        TRO FL,INCRF    ;SAY WE ARE FINISHING
        MOVEM P,SVPDL   ;SAVE THE PDL FOR LATER
        MOVE T1,[POINT 7,CRFRDR]
        MOVEM T1,DINPT
        MOVEI T3,OUTCRF
FINC1:  PUSHJ P,SCAN    ;GET SOMETHING
        TRNN FL,IDF     ;IGNORE ALL BUT IDENTIFIERS
        JRST FINC1
        MOVEI SVPT,0    ;SET UP IN CASE IT IS SOMETHING ELSE
        PUSHJ P,GETNAM  ;GET A NAME
        MOVE T2,SVNAM   ;GET IT
        PUSHJ P,ENTCNM  ;AND SEE IF IT DUPLICATES
        JRST FINC2A     ;WAIT FOR TERM
        MOVEI T1,"_"
        PUSHJ P,OUTCRF
        SKIPN T2,SVDEV  ;IF A DEVICE IS THERE (WHEN LSTDEV IS IN)
        JRST NOCRDV
        PUSHJ P,OUTSIX
        MOVEI T1,":"
        PUSHJ P,OUTCRF
NOCRDV: MOVE T2,SVNAM
        PUSHJ P,OUTSIX
        SKIPA   ;C IS ALREADY LOADED FROM SCAN
FINC4:  PUSHJ P,GETCH
        TLNE CS,TERMF   ;IS IT ERMINATORE?
        JRST FINC5      ;TRY NEXT
        MOVE T1,C
        PUSHJ P,OUTCRF
        JRST FINC4

FINC2:  PUSHJ P,SCAN
FINC2A: TLNN CS,TERMF
        JRST FINC2
        JRST FINC1
DNCRF:
IFE TEMP,<      RELEASE CHNCRF,0        ;LET GO OF THE CHANNEL>
IFN TEMP,<      PUSHJ P,CHKCRF  ;CHECK FOR TMPCOR UUO BEING DONE>
        MOVE P,SVPDL    ;GET THE ENTERING PDL BACK
        TRZ FL,INCRF    ;NO LONGER THERE
        POPJ P,

FINC5:  PUSHJ P,OUCRLF
        JRST FINC1      ;FINISH UP LINE

CSBT:   SBTS K,CNSBT
        SBTS M,CNMBT
        SBTS O,COPBT
        SBTS S,CNLBT
        SBTS C,CCMBT
        SBTS Q,CMONBT
CSBL==.-CSBT
       DEFINE X (A,B,C,D,E)
<OUT'B>
PRCTAB: PROCESS
        OUTLOD
        FOR (1,NSDNM,<CAT (NSDO,\XXX)>)

IFN SPRC,<
        DEFINE X (A,B,C,D,E)<
IFNB <E>,<SIXBIT /E/>>
INTEXT: PROCESS

        DEFINE X (A,B,C,D,E)
        <D'SW>
NXPC:   PROCESS>

SW==0
DODIR: TLO     FL2,SFLGD!CFLGD ;SET NORMAL DIR. BITS

        TRNE    FL3,TYMX
        TLO     FL2,DFLGD!PFLGD!AFLGD
        JRST    DOFL+1
DODEL:  TRO FL3,DELTSW  ;JUST SET DELETE SWITCH
DOFL:   TLZ FL2,-1      ;NO FLAGS
        TRO FL,PIPF
DODIR1: PUSHJ P,SCAN    ;FIND OUT IF HE WANTS /L OR /F SWITCH
DODIR2: TRNN FL,IDF     ;WAS IT AN IDENT?
        CAIN C,"["
        JRST DIRNAM
        CAIN    C,"("
        JRST    DIRNAM
        CAIN C,"-"
        JRST MINAM
        CAIN C,","
        JRST DODIR1
DIRSLS: CAIE C,"/"
        JRST NOSLSH
        PUSHJ P,SCAN    ;WHICH ONE
        TRNN FL,IDF     ;MUST SEEN AN IDENTIFIER
        JRST SYNERR
        MOVE T1,[XWD -DIRLG,DIRTAB]
        PUSHJ P,SWSCAN
        JRST    SWHELP
        JFCL
        XCT DIRT2-DIRTAB(T1)
        JRST DODIR1
DIRNAM: PUSHJ P,GETNAM
        TRNE    FL3,TYMX
        JRST    NAMDR           ;CHECK FOR SPECIAL NAMES
CMNM:   AOBJP SVPT,TMNER
        SKIPN SVNAM-1(SVPT)     ;IN CASE ONLY DEVICE NAME
        SKIPE SVPPN-1(SVPT)
        JRST DODIR1
        JRST DODIR2
NAMDR:  MOVSI   T1,'ALL'
        CAME    T1,SVNAM(SVPT)
        JRST    DIRNM
        MOVSI   T1,'*.*'                ;SET UP "ALL"
        MOVEM   T1,SVNAM(SVPT)
        JRST    CMNM
DIRNM:  MOVSI   T1,'NOT'
        CAME    T1,SVNAM(SVPT)
        JRST    CMNM
MINAM:  PUSHJ P,SCAN    ;SKIP THE -
        PUSHJ P,GETNAM
        SETOM SWBKS(SVPT)       ;FLAG AS MINUS
        JRST CMNM
NOSLSH: TLNN CS,TERMF   ;THIS MUST BE A TERMINATOR
        JRST SYNERR     ;NOPE, HE LOSES
        MOVE T2,[SIXBIT !TTY:_!]
        TRNE FL,LPTFG   ;ON LINE PRINTER INSTEAD?
        HRLI T2,SIXBIT '   LPT' ;YES
        MOVEI T3,OUTDIR
        TRNN FL3,RENTSW ;NOT IN A RENAME
        PUSHJ P,OUTSIX
        MOVSI T4,-NFILE ;SET UP TO OUTPUT NAMES
NXTDR:  CAMN T4,SVPT
        JRST OSWD
NXTDR1: MOVEI T1,"-"
        SKIPGE SWBKS(T4)
        PUSHJ P,OUTDIR
        PUSHJ P,CMNO
        AOBJP T4,OSWD
        CAMN T4,SVPT
        JRST OSWD
        MOVEI T1,","
        PUSHJ P,OUTDIR
        JRST NXTDR1
OSWD:   MOVE T4,[XWD-DSBTL,DSBT]
        PUSHJ P,SWBITS
        TLNN FL2,(T2)
        MOVE T4,[XWD -D2SBTL,D2SBT]     ;RAN OUT OF BITS IN 1 WORD
        PUSHJ P,SWBITS
        TRNN FL3,(T2)   ;SO USE ANOTHER
        TLNN FL2,GFLGD!LFLGD    ;CECK FOR DATE
        JRST OPIPW
        MOVEI T1,"/"
        PUSHJ P,OUTDIR
        MOVE T1,DIRDAT#
        PUSHJ P,OUTNUM
        MOVEI T1,">"
        TLNN FL2,GFLGD
        MOVEI T1,"<"
        PUSHJ P,OUTDIR
OPIPW:  PUSHJ P,OUCRLF
IFE TEMP,<RELEASE CHNDIR,0>
IFN TEMP,<PUSHJ P,CHKDIR>
        MOVE T1,[SIXBIT /DIRIT/]
        MOVEM T1,PCNAM
        JRST DONE1
OPIP1:  PUSHJ   P,OUCRLF
OPIP2:
IFE TEMP,<      RELEASE CHNPIP,0>
IFN TEMP,<      PUSHJ P,CHKPIP  ;CHECK FOR TMPCOR UUO>
        MOVSI T1,(SIXBIT /PIP/)
        MOVEM T1,PCNAM  ;LOAD THIS ONE
        JRST DONE1

SWHELP: MOVEI   T1,SWHLPM
        STRING
        JRST    EX
SWHLPM: ASCIZ $
SORT SWITCHES ARE:
 /EXTENSION
 /ALPHABETICAL
 /STORAGE
 /REVERSE
ITEM SWITCHES ARE:
 /SIZE
 /CREATION
 /TIME (OF CREATION)
 /SECONDS (OF CREATION)
 /PROTECTION
 /ACCESS
OTHER SWITCHES:
 /TODAY
 /AFTER
 /BEFORE
 /TOTAL
 /TEMPS (OR /K)
 /FAST
 /EVERYTHING
 /UNSORTED
$
;SWITCH VALUES

EFLGD==1
RFLGD==2
TFLGD==4
SFLGD==10
CFLGD==20
DFLGD==40
JFLGD==100
NFLGD==200
BFLGD==400
PFLGD==1000
AFLGD==2000
MFLGD==4000
KFLGD==10000
VFLGD==20000
UFLGD==40000
HFLGD==100000
GFLGD==200000
LFLGD==400000

;NOW BITS IN RH OF FL3


PRMSW==1        ;WANTS PROMPT ON DELETE
DELTSW==2       ;THIS IS A DELETE
RENTSW==4       ;THIS IS A RENAME

DEFINE TABLE
<SWITCH LPT,<TRO FL,LPTFG>
SWITCH EXTENSION,<TLO FL2,EFLGD>
SWITCH REVERSE,<TLO FL2,RFLGD>
SWITCH ALPHABETIC,<TLO FL2,TFLGD>
SWITCH SIZE,<TLO FL2,SFLGD>
SWITCH CREATION,<TLO FL2,CFLGD>
SWITCH TIME,<TLO FL2,DFLGD>
SWITCH SECONDS,<TLO FL2,JFLGD>
SWITCH TOTAL,<TLO FL2,NFLGD>
SWITCH TAPES,<TLO FL2,BFLGD>
SWITCH PROTECTION,<TLO FL2,PFLGD>
SWITCH ACCESS,<TLO FL2,AFLGD>
SWITCH MODE,<TLO FL2,MFLGD>
SWITCH TEMPS,<TLO FL2,KFLGD>
SWITCH K,<TLO FL2,KFLGD>
SWITCH FAST,<TLZ FL2,SFLGD!CFLGD!PFLGD!AFLGD>
SWITCH EVERYTHING,<TLO FL2,SFLGD!JFLGD!PFLGD!AFLGD!MFLGD>
SWITCH UNSORTED,<TLO FL2,UFLGD>
SWITCH STORAGE,<TLO FL2,HFLGD>
SWITCH BEFORE,<JRST SOONER>
SWITCH AFTER,<JRST LATER>
SWITCH TODAY,<JRST TODAY>
SWITCH WAIT,<TRO FL3,PRMSW>
>
TBGEN (DIRTAB,DIRLG,DIRT2)
DSBT:  SBTS E,EFLGD
        SBTS R,RFLGD
        SBTS T,TFLGD
        SBTS S,SFLGD
        SBTS C,CFLGD
        SBTS D,DFLGD
        SBTS J,JFLGD
        SBTS N,NFLGD
        SBTS B,BFLGD
        SBTS P,PFLGD
        SBTS A,AFLGD
        SBTS M,MFLGD
        SBTS K,KFLGD
        SBTS V,VFLGD
        SBTS U,UFLGD
        SBTS H,HFLGD
DSBTL==.-DSBT

D2SBT:  SBTS X,DELTSW
        SBTS Q,PRMSW
        SBTS L,RENTSW
D2SBTL==.-D2SBT

SOONER: TLOA FL2,LFLGD
LATER:  TLOA FL2,GFLGD
        TLZA FL2,GFLGD
        TLZ FL2,LFLGD
        SETZM DIRDAT#
        DATE    T1,
        IDIVI T1,^D31
        MOVEM T2,DRDAY#
        IDIVI T1,^D12
        MOVEM T2,DRMON#
        MOVEM T1,DRYR#
        PUSHJ P,SCAN
        CAIN C,"."
        JRST CRDAGT
        TRNN FL,NUMFS
        JRST SYNERR
        SOS T1,NUMAC
        MOVEM T1,DRDAY
        PUSHJ P,SCAN
        CAIE C,"-"
        JRST CRDAGT
        PUSHJ P,SCAN
        TRNN FL,IDF     ;MUST BE IDENT OR NUMBER
        JRST SYNERR
        TRNN FL,NUMFS
        JRST RMON       ;FIND OUT WHICH MONTH (TYPE IN NAME)
        SOS T1,NUMAC
        MOVEM T1,DRMON
RDYRD:  PUSHJ P,SCAN
        CAIE C,"-"
        JRST CRDAGT
        PUSHJ P,SCAN
        TRNN FL,NUMFS
        JRST SYNERR
        MOVE T1,NUMAC
        SUBI T1,^D64
        MOVEM T1,DRYR
        PUSHJ P,SCAN
CRDAGT: MOVE T1,DRYR
        IMULI T1,^D12
        ADD T1,DRMON
        IMULI T1,^D31
        ADD T1,DRDAY
        MOVEM T1,DIRDAT
       CAIE C,"."
        JRST DODIR2
        PUSHJ P,SCAN
        TRNN FL,NUMFS
        JRST SYNERR
        MOVE T1,NUMAC
        IDIVI T1,^D100
        IMULI T1,^D60
        ADD T1,T2
        IMULI T1,^D60
        DPB T1,[POINT 17,DIRDAT,23]
        JRST DODIR1

TODAY:  TLZ FL2,LFLGD
        TLO FL2,GFLGD
        DATE    T1,
        MOVEM T1,DIRDAT
        JRST DODIR1


RMON:   MOVE T1,[XWD -MONLG,MONTAB]
        PUSHJ P,SWSCAN
        JRST SYNERR
        JFCL
        SUBI T1,MONTAB  ;CONVERT TO NUMBER
        HRRZM T1,DRMON  ;PUT IT IN
        JRST RDYRD      ;DONE

DEFINE IR (A)
<IRP A <<SIXBIT /A/>>>
MONTAB: IR <JANUARY,FEBRUARY,MARCH,APRIL,MAY,JUNE,JULY,AUGUST>
        IR <SEPTEMBER,OCTOBER,NOVEMBER,DECEMBER>
MONLG==.-MONTAB
DOREN: TRO FL,PIPF     ;PERMIT * IN FILES
        TRNE    FL3,TYMX        ;CK IF TYMEX
        JRST    RENAME          ;YES, DO TYMEX RENAME
        PUSHJ P,SCAN
        PUSHJ P,GETNAM  ;READ THE OUTPUT NAME
        SKIPE SVNAM     ;NO NAME, NO SCAN
        PUSHJ P,SCAN
        SETZM SVDEV
        SETZM SVPPN     ;MUST BE ON OTHER SIDE
        MOVEI T3,OUTDIR
        SKIPE SVNAM     ;A NAME IF THERE IS ONE
        PUSHJ P,OUTNAM
        CAIE C,"<"      ;IS THERE PROTECTION?
        JRST NORPRT
        PUSHJ P,SCAN
        TRNN FL,NUMFS
        JRST TRYIDP     ;TRY FOR NAMES ON PROTECTION
COMPRT: MOVEI T1,"<"
        PUSHJ P,OUTDIR
        MOVE T2,NUMAC   ;GET NUMBER
        PUSHJ P,DECOCT  ;CONVET IT
        MOVEI T3,OUTDIR ;DESTROYED BY DECOCT
        PUSHJ P,OUTNUM
        MOVEI T1,">"
        PUSHJ P,OUTDIR
        PUSHJ P,SCAN
        CAIE C,">"
        JRST SYNERR
        PUSHJ P,SCAN
NORPRT: MOVEI T1,"_"
        PUSHJ P,OUTDIR  ;THIS IS LEFT OFF BY DIR COMD
        TRO FL3,RENTSW  ;SET THE BIT
        CAIE C,"="
        JRST SYNERR
        JRST DOFL       ;AND GO

DECOCT: MOVEI T1,0
        JUMPGE T2,DECO1
        HRRZ T1,T2
        POPJ P,         ;NEG FLAG IS SET BY TRYIDP
DECO1:  IDIVI T2,^D10
        HRLM T3,(P)
        SKIPE T2
        PUSHJ P,DECO1
        HLRZ T3,(P)
        LSH T1,3
        ADDI T1,(T3)
        POPJ P,

TRYIDP: MOVEI T5,3      ;NUMBER OF PROTECTION FIELDS
        TDZA T4,T4      ;ACCUMULATE PROTECTION
IDP1:   PUSHJ P,SCAN
        TRNN FL,IDF
        JRST SYNERR     ;MUST BE A NAME
        MOVE T1,[XWD -10,PROTAB]        ;ONLY A FEW POSSIBLE
        PUSHJ P,SWSCAN
        JRST SYNERR     ;NOT A GOOD ONE
        JFCL
        SUBI T1,PROTAB  ;CONVER TO  PROTECTION
        LSH T4,3
        IORI T4,(T1)    ;PUT IT IN
        SOJG T5,IDP1    ;IF MORE, TRY AGAIN
        HRROM T4,NUMAC  ;SAVE FOR COMPRT
        MOVEI T3,OUTDIR ;THIS GOT LOST
        JRST COMPRT

PROTAB: SIXBIT /ALL/
        SIXBIT /CP/
        SIXBIT /UPD/
        SIXBIT /AP/
        SIXBIT /RD/
        SIXBIT /RUN/
        SIXBIT /LK/
        SIXBIT /NO/
COPY:  TRO     FL,PIPF         ;SET PIP FLAG
        MOVEI   T4,[ASCIZ /FROM FILE: /]
        PUSHJ   P,PROMPT        ;TYPE PROMPTING MSG IF REQ.
        PUSHJ   P,GNM   ;GET OLD FILE NAME
        MOVEM   P,SAVPC#
        PUSH    P,SVDEV         ;SAVE DEVICE
        PUSH    P,SVPPN ;SAVE PROJ.-PROG. NO.
        PUSH    P,SVNAM ;SAVE NAME
        PUSH    P,SVEXT
        MOVEI   T4,[ASCIZ /TO: /]
        PUSHJ   P,PROMPT        ;TYPE PROMPTING MSG IF REQ.
        TRZN    FL,PRMPTF       ;AVOID SSCAN IF MSG. TYPED
DOC2:   PUSHJ   P,SCAN  ;IGNORE SEPARATOR
        CAIN    C,"+"
        JRST    [PUSHJ  P,GNM
                PUSH    P,SVDEV
                PUSH    P,SVPPN
                PUSH    P,SVNAM
                PUSH    P,SVEXT
                JRST    DOC2]
        CAIN    C,"_"
        JRST    DOC4
        PUSHJ   P,GNM   ;GET NEW NAME
DOC5:   MOVEI   T3,OUTPIP
        PUSHJ   P,CKTTY ;CK IF TELETYPE WAS REQ.
        JRST    DOC1    ;YES, DEVICE ALREADY OUT
        MOVSI   T2,SIXBIT '   DSK'
        PUSHJ   P,OUTSIX        ;NO,OUTPUT DEVICE
        MOVEI   T1,":"
        PUSHJ   P,OUTPIP
        MOVE    T1,[SIXBIT /SAME/]      ;CK IF COPYING TO SAME NME
        CAME    T1,SVNAM
        JRST    DOC1            ;NOT "SAME"
        MOVE    T1,[SIXBIT "*.*/X"]
        MOVEM   T1,SVNAM
DOC1:   PUSHJ   P,OUTNAM        ;OUTPUT OLD NAME
        MOVEI   T1,"_"
        PUSHJ   P,OUTPIP
        EXCH    P,SAVPC
DOC3:   ADD     P,[XWD 4,4]
        POP     P,SVEXT         ;GET BACK SAVE NEW NAME
        POP     P,SVNAM
        POP     P,SVPPN
        POP     P,SVDEV
        PUSHJ   P,CKTTY         ;CK IF TTY FOR OUTPUT
        JRST    .+1
        PUSHJ   P,OUTNAM        ;OUTPUT OLD NAME
        ADD     P,[XWD 4,4]
        CAMN    P,SAVPC
        JRST    OPIP1
                MOVEI   T1,","
        PUSHJ   P,OUTPIP
        JRST    DOC3

DOC4:   PUSHJ   P,GNM
        PUSH    P,SVDEV
        PUSH    P,SVPPN
        PUSH    P,SVNAM
        PUSH    P,SVEXT
        PUSHJ   P,SCAN
        CAIN    C,","
        JRST    DOC4
        EXCH    P,SAVPC
        ADD     P,[XWD 4,4]
        POP     P,SVEXT
        POP     P,SVNAM
        POP     P,SVPPN
        POP     P,SVDEV
        ADD     P,[XWD 4,4]
        EXCH    P,SAVPC
        JRST    DOC5

GNM:    PUSHJ   P,SCAN
        PUSHJ   P,GETNAM
        MOVSI   T2,SIXBIT "   DSK"      ;STANDARD DEVICE
        SKIPN   T2,SVDEV                ;SEE IF DEVICE SPECIFIED
        MOVEM   T2,SVDEV        ;NO, USE STANDARD
        POPJ    P,

CKTTY:  MOVE    T1,['TELETY']
        MOVE    T2,['TERMIN']
        MOVE    T4,SVNAM
        SETZ    T5,
CKT1:   ADDI    T5,6
        LSH     T4,6
        JUMPN   T4,CKT1
        SUBI    T5,6*6
        MOVE    T4,SVNAM
        LSH     T1,(T5)
        LSH     T2,(T5)
        LSH     T4,(T5)
        CAME    T2,T4
        CAMN    T1,T4           ;LOOK FOR TELETYPE OR ANY PART
        JRST    .+3             ;LOOKS LIKE TELETYPE
        AOS     0(P)    ;SET SKIP RETURN FOR NO TTY
        POPJ    P,
        MOVE    T2,[SIXBIT /TTY:  /]
        MOVEM   T2,SVNAM
        SETZM   SVEXT
        SETZM   SVDEV
        POPJ    P,
HELP:  MOVEI   T1,HELPM
        STRING
        JRST    EX

HELPM:  ASCIZ $
TYMEX VERS. 46, DEC. 15, 1971
NEW COMMANDS "MAIL, SEND, AND POSTMAN":
MAIL: TYPES ALL YOUR MAIL, NO OPTIONS
SEND: ALLOWS YOU TO SEND MAIL TO ONE OR MORE USERS
   SEPARATE USER NAMES WITH COLON
  EXAMPLE:
   SEND JONES:SMITH:FRANK C.R.
   TYPE LETTER
 TERMINATE LETTER WITH CONTROL Z
 SPECIAL OPTION:  LETTER MAY COME FROM A SYMBOLIC FILE
  WHEN THE COMPUTER TYPES "TYPE LETTER", TYPE AND "@"
  FOLLOWED BY THE FILE NAME TERMINATED BY A C.R. 
POSTMAN:  ALLOWS YOU TO READ OR CANCEL MAIL YOU SENT
  ANSWER QUESTIONS WITH "Y C.R. " FOR YES OR "N C.R." FOR NO.
LIST OF VALID COMMANDS:
 COMPILE
 CREATE
 DEBUG
 COPY
 TRY
 CROSS
 DELETE
 DIRECTORY
 EDITOR
 EXECUTE
 LOAD
 MAKE
 RENAME
 TECO
 TYPE
 PRINT
 FILES
 DECLARE
 HELP
 SYSNO
 DATE
 TIME
 PDP10
 TYMEX
 WHO
SEND
MAIL
POSTMAN

$

TYMEX:  TROE    FL3,TYMX        ;CK IF ALREADY IN TYMEX MODE
        JRST    EX              ;YES
        HRROI   T1,GETPRV
        GETTAB  T1,
        JRST    EX
        TRO     T1,PVTYMX
        CALLI   T1,SETPRV
        JRST    EX
        JRST    EX

PDP10:  HRROI   T1,GETPRV
        GETTAB  T1,
        JRST    EX
        TRZ     T1,PVTYMX
        CALLI   T1,SETPRV
        JRST    EX
        JRST    EX
;CHECKS IF END OF STORED STRING - IF SO TYPES STRING POINTED TO IN T4
;AND SETS PROMPT FLAG
PROMPT: PUSHJ   P,SCANS         ;GET TERM. CHAR.
        TRZ     FL,PRMPTF       ;CLEAR FOR POS. PROMPT
        MOVE    T2,SAVCHR       ;GET SAVED CHAR. TO CHECK FLAGS
        TLNN    T2,TERMF        ;TERMINATION FLAG
        POPJ    P,              ;NOT SET
        MOVE    T1,T4           ;GET MESSAGE ADR.
        STRING                  ;TYPE PROMPT MESSAGE
        PUSHJ   P,GSTRN         ;GET NEW STRING
        MOVEI   CS,0
        SETZM   SAVCHR
        TRO     FL,PRMPTF       ;SET FLAG
        POPJ    P,
OCTTYP: MOVEI   T3,10   ;SET BASE TO EIGHT
        JRST    RDXTYP
RADX10: MOVEI   T3,12   ;BASE 10
RDXTYP: PUSH    P,T2    ;SAVE FOR TIME
        PUSHJ   P,PRTDIG
        POP     P,T2
        POPJ    P,0
PRTDIG: IDIV    T1,T3
        HRLM    T2,0(P)         ;SAVE DIGIT IN STACK
        SKIPE   T1              ;CK IF DONE
        PUSHJ   P,PRTDIG        ;NO, GET NEXT DIGIT
        HLRZ    T1,0(P)         ;TAKE DIGITS OFF STACK
        ADDI    T1,"0"          ;CHANGE TO ASCCI
        OUTCHR  T1
        POPJ    P,0
TPSIX:  PUSH    P,T3            ;SAVE A COUPLE REG.
        PUSH    P,T2
        SKIPE   T3,1(T1)        ;IF 0,TREAT 1ST AS LAST
        JRST    TPS1
        MOVE    T3,(T1)
TPS:    JUMPN   T3,TPS2
        POP     P,T2
        POP     P,T3
        POPJ    P,
TPS2:   MOVEI   T2,0
        LSHC    T2,6
        ADDI    T2,40
        OUTCHR  T2
        JRST    TPS
TPS1:   MOVEI   CS,6
        MOVE    T2,(T1)
TPS3:   MOVEI   T1,0
        LSHC    T1,6
        ADDI    T1,40
        OUTCHR  T1
        SOJG    CS,TPS3
        JRST    TPS2

CLUD:   CLOSE   RFL,            ;CLOSE USER DIR. FILE
        STATZ   RFL,740000
        JRST    READER
        POPJ    P,0
DECLARE: TRO   FL,PIPF         ;SET PIP OUTPUT FLAG
        MOVEI   FL3,0           ;USED FOR PROTECT BITS
        PUSHJ   P,SCANS         ;CK IF PROMPTING DESIRED
        MOVE    CS,SAVCHR
        TLNN    CS,TERMF        ;IF TERMF, THEN END AND NEED PROMPT
        JRST    DECL2           ;NO PROMPT
        MOVEI   T1,[ASCIZ /ALLOW PRIVATE:
/]
        PUSHJ   P,SDECL         ;GET PRIVATE PROTECT BITS
        MOVEI   T6,0            ;STND. BITS FOR PRIVATE
        LSH     T6,6            ;POS. FOR MERGE
        IOR     FL3,T6
        MOVEI   T1,[ASCIZ /ACCOUNT:
/]
        PUSHJ   P,SDECL
        MOVEI   T6,4            ;STND BITS FOR ACCOUNT
        LSH     T6,3
        IOR     FL3,T6
        MOVEI   T1,[ASCIZ /PUBLIC:
/]
        PUSHJ   P,SDECL
        MOVEI   T6,7            ;STND BITS FOR PUBLIC
        IOR     FL3,T6
DECL1:  MOVEI   T4,[ASCIZ /FILE(S): /]
        PUSHJ   P,PROMPT
        PUSHJ   P,SPREN
        MOVEI   FL3,RENTSW      ;SET RENAME SW
        PUSHJ   P,STTYMX        ;SET TYMEX
        JRST    DOFL

SPREN:  MOVEI   T3,OUTDIR       ;PREPARE TO OUTPUT IN RENAME FORMAT
        MOVEI   T1,"<"
        PUSHJ   P,OUTDIR
        MOVE    T1,FL3          ;ACCUMULATED PROTECT BITS
        PUSHJ   P,OUTNUM        ;OUTPUT IN DECIMAL FOR DIRIT
        MOVEI   T1,">"
        PUSHJ   P,OUTDIR
        MOVEI   T1,"_"
        PUSHJ   P,OUTDIR        ;LEFT OFF BY FILES COMMAND
        POPJ    P,0
        DECL2:  MOVEI   T4,0            ;ACCUM. BITS HERE
        MOVEI   T5,3            ;3 FIELDS
        PUSHJ   P,IDP1          ;GET PROT. BITS
        MOVE    FL3,T4
        JRST    DECL1

RENAME: MOVEI   T4,[ASCIZ /OLD NAME: /] ;PROMPTING MESSAGE
        PUSHJ   P,PROMPT
        PUSHJ   P,GNM           ;GET OLD NAME
        SKIPE   SVPPN           ;PROJ.-PROG. NO. NOT ALLOWED
        JRST    SYNERR
        PUSH    P,SVNAM         ;SAVE OLD NAME
        PUSH    P,SVEXT
        MOVEI   T4,[ASCIZ /NEW NAME: /]
        PUSHJ   P,PROMPT        ;CK IF END OF STRING AND PROMPT REQ.
        TRZN    FL,PRMPTF       ;TEST FLAG AND SKIP OVER SEP. IF SET
        PUSHJ   P,SCAN          ;SKIP OVER SEPARATOR
        PUSHJ   P,GNM           ;GET NEW NAME
        SKIPE   SVPPN
        JRST    SYNERR
        MOVE    T2,[SIXBIT 'DSK:']
        MOVEI   T3,OUTDIR
        PUSHJ   P,OUTSIX
        PUSHJ   P,OUTNAM
        MOVEI   T1,"_"
        PUSHJ   P,OUTDIR
        POP     P,SVEXT         ;RECOVER OLD NAME
        POP     P,SVNAM
        PUSHJ   P,OUTNAM
        TRO     FL3,RENTSW      ;SET RENAME SW
RENM:   PUSHJ   P,SCAN          ;LOOK FOR POSSIBLE SWITCHES
        TRNN    FL,IDF
        CAIE    C,"/"
        JRST    RENM1
        PUSHJ   P,SCAN
        TRNN    FL,IDF
        JRST    SYNERR
        MOVE    T1,[XWD -DIRLG,DIRTAB]
        PUSHJ   P,SWSCAN
        JRST    SWHELP
        JFCL
        XCT     DIRT2-DIRTAB(T1)
        JRST    RENM
RENM1:  MOVEI   T3,OUTDIR
        TLNN    CS,TERMF
        JRST    SYNERR
        JRST    OSWD
SDECL: MOVEI   T6,7            ;START WITH NOTHING
        STRING
        MOVEI   T1,[ASCIZ /  MODIFY? /]
        PUSHJ   P,YN
        JRST    SDM
        MOVEI   T1,[ASCIZ /  READ(RD)? /]
        PUSHJ   P,YN
        JRST    SD4
        MOVEI   T1,[ASCIZ /  RUN(RUN)? /]
        PUSHJ   P,YN
        JRST    SD5
        MOVEI   T1,[ASCIZ /  LOOKUP(LK)? /]
        PUSHJ   P,YN
        JRST    SD6
        MOVEI   T1,[ASCIZ /  NOTHING(NO)
/]
        STRING
        JRST    CPOPJ1
SDM:    MOVEI   T1,[ASCIZ /  RENAME & DELETE(ALL)? /]
        PUSHJ   P,YN
        JRST    SD0
        MOVEI   T1,[ASCIZ /  CHANGE PROT.(CP)? /]
        PUSHJ   P,YN
        JRST    SD1
        MOVEI   T1,[ASCIZ /  UPDATE(UPD)? /]
        PUSHJ   P,YN
        JRST    SD2
        MOVEI   T1,[ASCIZ /  APPEND(AP)
/]
        STRING
        JRST    SD3
SD0:    SUBI    T6,1
SD1:    SUBI    T6,1
SD2:    SUBI    T6,1
SD3:    SUBI    T6,1
SD4:    SUBI    T6,1
SD5:    SUBI    T6,1
SD6:    SUBI    T6,1
        JRST    CPOPJ1

YN:     STRING
        MOVE    CS,SAVCHR
        JUMPN   CS,YN2
YN1:    PUSHJ   P,GETCH
        JUMPE   CS,YN1
        CAIN    C,15
        JRST    YN1
YN2:    TLNE    CS,TERMF
        JRST    YN4             ;END OF LINE, GET ANOTHER
        MOVE    C,CS
        MOVEI   CS,0
        SETZM   SAVCHR
        CAIN    C,56            ;TEST FOR N
        JRST    CPOPJ1
        CAIN    C,71            ;TEST FOR Y
        POPJ    P,0
        HRRZS   C,C
        CAIE    C,"-"
        JRST    SYNERR
        SUB     P,[XWD 1,1]     ;GO BACK FOR STANDARD BITS
        POPJ    P,0
YN4:    PUSHJ   P,GSTRN         ;GET ANOTHER STRING
        MOVEI   CS,0
        SETZM   SAVCHR
        JRST    YN1


WHO:    GETPPN  T5,
        HRROI   T1,GETPRV
        GETTAB  T1,
        JRST    EX
        TRNE    T1,TSPROP
        MOVEI   T5,0    ; THEN SET 0 AS SW.
        TRNE    T1,ACCSUP
        HRRI    T5,0
        MOVEI   C,1     ;JOB NO.
WHO1:   HRL     T2,C
        HRRI    T2,-22
        GETTAB  T2,     ;GET FIRST WORD OF USER NAME
        JRST    EX      ;END
        JUMPE   T2,WHO3         ;SKIP NO NAME
        HRRI    T1,2
        HRL     T1,C
        GETTAB  T1,     ;GET THIS JOBS PROJ.
        JRST    EX
        JUMPE   T1,WHO3         ;SKIP OF NOT LOGGED IN
        JUMPE   T5,WHO2         ;PRINT ALL FOR SOME PROJ.
        TRNN    T5,-1
        HRRI    T1,0    ;KEEP PROJ. ONLY
        CAME    T1,T5   ;CK IF SAME PROJ
        JRST    WHO3    ;NO, BYPASS PRINTING
WHO2:   MOVE    T1,C    ;PREPARE TO PRINT JOB NO.
        PUSHJ   P,RADX10
        MOVEI   T1,[ASCIZ /  /] ;PRINT A COUPLE OF SPACES
        PJOB    T3,
        CAMN    T3,C
        MOVEI   T1,[ASCIZ /* /] ;* FOR THIS USER
        OUTSTR  T1
        HRL     T3,C    ;GET 2ND WORD OF NAME
        HRRI    T3,-21
        GETTAB  T3,     
        JRST    EX
        MOVEI   T1,T2
        PUSHJ   P,TPSIX ;TYPE NAME
        OUTSTR  [BYTE(7) 15,12,0]
WHO3:   AOJA    C,WHO1  ;TO NEXT JOB

;PRINTS PROJECT-PROGRAMMER (ACCOUNT-PROG.) NO
PPN:    MOVEI   T4,[ASCIZ /USER NAME: /]
        PUSHJ   P,PROMPT        ;CK IF PROMPT, THEN TYPE
        PUSHJ   P,GTUNO         ;GET USER NO.
        PUSHJ   P,TPPPN         ;GO TYPE
        JRST    EX
;TAKES NAME AND VERIFIES IF VALID
;RETURNS USER NO. IN T3
GTUNO:  PUSHJ   P,OPUD          ;OPEN USER DIR FILE
        MOVEI   T5,0            ;IN CASE 6 OR LESS CHARS.
        PUSHJ   P,SCANW         ;GET FIRST 6
        JRST    GTU1            ;FOUND A TERMINATOR
        MOVE    T4,ACCUM        ;SAVE FIRST WORD OF NAME
        PUSHJ   P,SCANW
        JRST    GTU2            ;A TERM.CHAR.
        PUSHJ   P,GETCH         ;CK. NEXT CHAR
        JUMPG   CS,SYNERR       ;ERROR, MUST BE TERM.
GTU2:   MOVE    T5,ACCUM        ;2ND WORD OF NAME
        JRST    .+2
GTU1:   MOVE    T4,ACCUM
        PUSHJ   P,LKLUD
        JRST    BADNAM
        MOVE    T3,BUF+LDPPN(T1)
        POPJ    P,0

BADNAM: MOVEI   T1,[ASCIZ /NO USER NAMED: /]
        STRING
        MOVEI   T1,T4
        PUSHJ   P,TPSIX
        JRST    EX

;LOOK UP NAME IN LUD
;EXPECTS NAME IN T4,T5
;FILE MUST BE OPEN
;NO SKIP RETURN IF NOT IN LUD
;T1 RETURNS WITH INDEX INTO BUF
LKLUD:  MOVEM   FL2,SVAC        ;SAVE SOME ACCUM.
        MOVE    FL2,[XWD FL3,SVAC+1]
        BLT     FL2,SVAC+6
        MOVEI   C,0
        MOVE    T1,[555555555555]
        MOVE    T2,[361275431652]
        MOVE    T3,[612754316523]
        PUSHJ   P,RND
        PUSHJ   P,RND
        PUSHJ   P,RND
        PUSHJ   P,RND
        XOR     T2,T3
        XOR     T1,T2
        TLZ     T2,400000
        IDIVI   T2,^D887
        MOVE    T2,T1
SRUS:   PUSHJ   P,RDBLK
        MOVEI   T1,0
SRUSR:  CAME    T2,BUF+LHUN(T1)
        JRST    NUSER
        AOS     0(P)
LKLUDE: MOVE    C,[XWD SVAC,FL2]        ;RESTORE ACCUM.
        BLT     C,C
        POPJ    P,0
NUSER:  SKIPG   T6,BUF+LDPPN(T1)
        JRST    BLKLNK
        MOVE    T6,BUF+LLINK(T1)
        ANDI    T6,177
        ADD     T1,T6
        JRST    SRUSR
BLKLNK: JUMPE   T6,LKLUDE       
        MOVE    T3,T6
        JRST    SRUS

RND:    ADD     T2,T4
        ROTC    T4,-22
        MOVEI   CS,5
RND1:   MOVE    T6,T2(C)
        MUL     T6,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
        ADDM    T7,T3(C)
        AOJE    C,RND2
        MOVNI   C,1
        TRNE    T2,1
        SKIPL   T3
        MOVEI   C,0
        EXCH    T1,T3
RND2:   SOJG    CS,RND1
        POPJ    P,0
TPPPN: HLRZ    T1,T3           ;TYPE NO. IN T3
        MOVE    T4,T3
        PUSHJ   P,OCTTYP
        MOVEI   T1,","          ;TYPE COMMA
        OUTCHR  T1
        HRRZ    T1,T4
        PUSHJ   P,OCTTYP
        POPJ    P,0

OPUD:   INIT    RFL,17          ;OPEN USER.-DIR.
        SIXBIT  /SYS/
        XWD     0,0
        CALLI   12
        MOVE    T1,[SIXBIT /LUD/]
        MOVSI   T2,(SIXBIT /SYS/)
        SETZB   T3,T4
        LOOKUP  RFL,T1
        JRST    SYNERR
        POPJ    P,0

SCANW:  SETZM   ACCUM   ;GATHERS 6 CHARS.
        MOVE    T1,[POINT 6,ACCUM]
        SKIPN   CS,SAVCHR       ;USE SAVED CHAR IF ONE
        JRST    SCW1    ;NO SAVED, GO GET ONE
        SETZM   SAVCHR  ;DONT USE AGAIN
        JRST    .+2     ;DONT GET ANOTHER
SCW1:   PUSHJ   P,GETCH
        JUMPLE  CS,SCW3         ;TERM CHAR
SCW2:   IDPB    CS,T1
        TLNE    T1,770000       ;CK IF 6 CHARS. STORED
        JRST    SCW1            ;NOT YET
        AOS     0(P)            ;SET SKIP RETURN FOR NO TERM CHAR
        POPJ    P,0
SCW3:   CAIE    C,15            ;CAR. RET. FOR TERM.
        CAMN    CS,CTBL+12              ;DAS IST SEHR SCHMART, JA!
        POPJ    P,0
        CAIN    C,":"
        POPJ    P,0
        CAIN    C,";"           ;VALID TERMINATOR
        POPJ    P,0
        CAIN    C,")"
        POPJ    P,0
        SUBI    C,40            ;CHANGE TO 6 BIT
        TRNE    C,100           ;CK NO. OF BITS
        POPJ    P,0             ;MUST BE FUNNY TERM.
        MOVE    CS,C
        JRST    SCW2

WTBLK:  USETO   RFL,1(T3)
        OUTPUT  RFL,HED
        STATZ   RFL,740000
        JRST    SYNERR
        POPJ    P,0

RDBLK:  USETI   RFL,1(T3)
        INPUT   RFL,HED
        STATZ   RFL,760000
        JRST    .+2
        POPJ    P,0
        MOVSI   T1,-200
        SETZM   BUF(T1)
        AOBJN   T1,.-1
        MOVEI   T1,0            ;SW
        POPJ    P,0
DODATE:        DATE    T1,             ;GET DATE FROM MONITOR
        PUSHJ   P,TDATE
        TIMER   T1,             ;RECOVER TIME IN MILLISEC
        PUSHJ   P,PRTIM1
        JRST    EX

TDATE:  IDIVI   T1,^D31         ;GET MONTH
        EXCH    T1,T2           ;SET ASIDE MONTH
        AOS     T1              ;FIX DAY
        MOVE    T3,T1           ;SAVE DAY
        MOVEI   T1,0
        DIVI    T1,^D12         ;SEPARATE MONTH FROM YEAR
        EXCH    T1,T2
        ADDI    T1,MONTH
        STRING
        MOVE    T1,T3
        PUSHJ   P,RADX10        ;TYPE DAY
        MOVEI   T1,[ASCIZ /, 19/]
        STRING
        MOVEI   T1,^D64(T2)     ;YEAR 64 IS 0
        PUSHJ   P,RADX10        ;TYPE YEAR
        MOVEI   T1," "
        OUTCHR  T1            ;TYPE A COUPLE SPACES
        OUTCHR  T1
        POPJ    P,
MONTH:  ASCIZ "JAN "
        ASCIZ "FEB "
        ASCIZ "MAR "
        ASCIZ "APR "
        ASCIZ "MAY "
        ASCIZ "JUN "
        ASCIZ "JUL "
        ASCIZ "AUG "
        ASCIZ "SEP "
        ASCIZ "OCT "
        ASCIZ "NOV "
        ASCIZ "DEC "

SYSNO:  MOVSI   T2,-5   ;5 WORDS
        MOVEI   T1,11   ;DATA BLOCK NUMBER
        HRLI    T1,0(T2)
        GETTAB  T1,     ;MOVE DATA FROM MONITOR
        0
        MOVEM   T1,RUNBLK(T2)   ;ANY GOOD BLOCK GOOD
        AOBJN   T2,SYSNO+1
        MOVEI   T1,RUNBLK
        STRING
        JRST    EX
PRTIM1: IDIVI   T1,^D3600               ;CHANGE JIFFIES TO MINS.
        IDIVI   T1,^D60         ;HRS IN T1, MIN IN T2
        PUSHJ   P,RADX10        ;TYPE HOURS
        MOVEI   T1,":"
        OUTCHR  T1
        MOVEI   T1,"0"          ;PREPARE TO TYPE LEADING 0
        CAIG    T2,11
        OUTCHR  T1
        MOVE    T1,T2
        PUSHJ   P,RADX10        ;TYPE MINUTES
        POPJ    P,
;SET FILE DIRECTORY CONTROLS
FDC:    MOVEI   T6,70           ;STANDARD OPTIONS, PRIVATE
        MOVEI   T1,[ASCIZ /ALLOW ACCOUNT:
/]
        PUSHJ   P,SFDC
        MOVEI   T7,5            ;STANDARD OPTIONS
        OR      T6,T7
        LSH     T6,3
        MOVEI   T1,[ASCIZ /PUBLIC:
/]
        PUSHJ   P,SFDC
        MOVEI   T7,0            ;STANDARD OPTIONS
        OR      T6,T7
        PUSHJ   P,OPMFD
        DPB     T6,[POINT 9,T3,8]
        MOVE    T4,[XWD 1,1]
        RENAME  RFL,T1
        JRST    SYNERR
        JRST    EX

SFDC:   STRING
        MOVEI   T7,0
        MOVEI   T1,[ASCIZ /  SHARABLE? /]
        PUSHJ   P,YN
        TRO     T7,4
        MOVEI   T1,[ASCIZ /  NEW FILES? /]
        PUSHJ   P,YN
        TRO     T7,2
        MOVEI   T1,[ASCIZ /  LISTABLE? /]
        PUSHJ   P,YN
        TRO     T7,1
        JRST    CPOPJ1

;PRINT FILE DIRECTORY CONTROLS
PFDC:   PUSHJ   P,OPMFD
        MOVEI   T1,[ASCIZ /
ACCOUNT: /]
        LSHC    T2,6
        PUSHJ   P,TFDC
        MOVEI   T1,[ASCIZ /
PUBLIC: /]
        LSHC    T2,3
        PUSHJ   P,TFDC
        JRST    EX

TFDC:   TRNN    T2,7
        POPJ    P,
        STRING
        MOVEI   T1,[ASCIZ /SHARABLE /]
        TRNE    T2,4
        STRING
        MOVEI   T1,[ASCIZ /NEW FILES /]
        TRNE    T2,2
        STRING
        MOVEI   T1,[ASCIZ /LISTABLE/]
        TRNE    T2,1
        STRING
        POPJ    P,0

OPMFD:  INIT    RFL,17
        SIXBIT  /DSK/
        0
        HALT
        GETPPN  T1,
        MOVSI   T2,(SIXBIT /UFD/)
        MOVEI   T3,0
        MOVE    T4,[XWD 1,1]
        LOOKUP  RFL,T1
        JRST    SYNERR
        POPJ    P,
;SEND MAIL - PUTS MAIL IN FILE "MBX"
SEND:   SETZM   SVJBFF
        SETZM   NAMCT   ;NAME COUNT
        SETOM   SENDFL ;CAUSE : IS ALWAYS : EXCEPT WHEN IT'S NOT
        MOVEI   T4,[ASCIZ /TO: /]
        PUSHJ   P,PROMPT
SN1:    PUSHJ   P,GTUNO         ;CKS NAME IN LUD,SYNERR IF BAD
        SKIPN   T3,SVJBFF       ;CK FOR SPACE
        HRRZ    T3,JOBFF
        HRRZ    T1,JOBREL
        CAIL    T1,105(T3)
        JRST    .+4             ;STILL ROOM
        MOVEI   T2,2000(T1)     ;GET MORE CORE
        CORE    T2,
        HALT    .               ;SHOULDN'T FAIL TO GET SPACE
        MOVEM   T4,(T3)         ;NAME FROM GTUNO
        MOVEM   T5,1(T3)
        AOS     NAMCT           ;COUNT NAMES
        ADDI    T3,2            ;NEXT LOCATION
        MOVEM   T3,SVJBFF
        CAIE    C,15
        JRST    SN1             ;NOT LAST, GET NEXT NAME
        SETOM   (T3)            ;END OF NAME LIST FLAG
        SETZM   SENDFL
        AOS     SVJBFF
        HRROI   T1,-22          ;GET THIS USERS NAME
        GETTAB  T1,
        HALT    .
        MOVEM   T1,1(T3)
        HRROI   T1,-21          ;LAST 6 CHARS OF NAME
        GETTAB  T1,
        HALT    .
        MOVEM   T1,2(T3)
        GETPPN  T1,             ;GET THIS USERS NO.
        MOVEM   T1,3(T3)
        DATE    T1,
        MOVEM   T1,4(T3)
        TIMER   T1,             ;TIME
        MOVEM   T1,5(T3)
        SETOM   101(T3)         ;SET POSSIBLE OVERFLOW FLAG
        MOVEI   T1,6(T3)        ;LOC. FOR NAME POINTERS
        MOVEM   T1,NAMPT
        MOVE    T1,NAMCT
        ADDI    T3,7(T1)        ;FIND SPACE FOR MAIL AFTER NAME PTRS
        MOVE    T4,T3
        HRLI    T4,(POINT 7,0)  ;BYTE PTR FOR MAIL
        OUTSTR  [ASCIZ /TYPE LETTER:
/]
        INCHWL  T2              ;GET CHAR
        CAIN    T2,"@"          ;CK FOR AT-SIGN
        JRST    SN3             ;NO TEXT
SN1B:   IDPB    T2,T4   
        MOVE    T2,SVJBFF
        MOVE    T2,100(T2)
        CAMN    T2,[-1]
        JRST    SN1A
        OUTSTR  [ASCIZ /
---LETTER TERMINATED HERE---
(TYPE ANOTHER ONE IF YOU WANT TO GO ON)/]
        JRST    SN1C
SN1A:   INCHWL  T2
        CAIN    T2,"D"-100      ;CONTROL D
        JRST    SN1C
        CAIE    T2,"Z"-100
        JRST    SN1B            ;NOT CONTROL Z
SN1C:   MOVEI   T2,0
        IDPB    T2,T4
        JRST    SN2
;GET MAIL FROM FILE FOR MAILBOX
SN3:    PUSHJ   P,GSTRN         ;GET FILE NAME STRING
        PUSHJ   P,SCAN
        PUSHJ   P,GETNAM
        MOVE    T1,JOBFF        ;SAVE JOBFF
        PUSH    P,T1
        MOVE    T1,SVJBFF       ;TO FORCE BUFFERS PAST MAIL STORAGE
        ADDI    T1,110
        MOVEM   T1,JOBFF
        INIT    IFL,1
        SIXBIT  /DSK/
        XWD     0,SBUF
        LOGOUT
        SETZM   SVPPN+1
        LOOKUP  IFL,SVNAM
        JRST    BADFL
SN3A:   SOSG    SBUF+2
        IN      IFL,
        JRST    SN3B
        STATZ   IFL,740000
        JRST    BADFL
        POP     P,T1    ;RESTORE JOBFF
        MOVEM   T1,JOBFF
        JRST    SN2
SN3B:   ILDB    T2,SBUF+1
        SKIPE   T2
        IDPB    T2,T4
        JRST    SN3A
BADFL:  MOVEI   T1,[ASCIZ /BAD FILE/]
        JRST    TPERR
;T7 USED FOR MAIL #, T6 PTR TO NEXT NAME
SN2:    SETZ    T2,             ;FOR END MAIL STRING FLAG
        IDPB    T2,T4
        MOVE    T1,SVJBFF       ;CK IF MAIL TOO LONG
        SETO    T2,
        PUSHJ   P,OPMBX
        PUSHJ   P,FNXML         ;FIND NEXT MAIL NO. (PUTS IN T7)
        MOVE    T6,JOBFF        ;SET LOC. FOR NEXT NAME
SN4:    MOVE    T4,(T6)
        CAMN    T4,[-1]         ;CK FOR END FLAG
        JRST    SN5             ;END OF NAME LIST
        MOVE    T5,1(T6)
        MOVEI   T1,T4
        CALLI   T1,-17
        PUSHJ   P,LKUP          ;LOOK FOR NAME ALREADY IN MBX
        JRST    SN6             ;FOUND NAME
;NAME NOT THERE, RETURNS WITH T2 POINTING TO PKT
        MOVEM   T4,BUF(T2)
        MOVEM   T5,BUF+1(T2)
        HRLZM   T7,BUF+2(T2)
        SETZM   BUF+3(T2)
SN4A:   PUSHJ   P,WTBLK         ;WRITE MAIL BLK
        MOVEM   CS,@NAMPT       ;CS SET BY LKUP WITH MAIL INDEX
        AOS     NAMPT           ;FOR NEXT INDEX
        ADDI    T6,2            ;NEXT NAME
        JRST    SN4
SN5:    SETOM   @NAMPT          ;SET END FLAG FOR SURE
        PUSHJ   P,GMBLK         ;USES MAIL NO. IN T7
;SETS T6 TO CORE LOC AND C TO CURSER POS
        HRLZ    T1,SVJBFF
        HRR     T1,T6
        BLT     T1,77(T6)       ;MOVE MAIL INTO BUFFER
        PUSHJ   P,WTBLK
        JRST    EX
;FOUND NAME, ADD NEW MAIL NO.,T2 PTS TO PKT & NAME
;C HAS CURSER POS, CS HAS INDEX WORD THIS NAME
SN6:    PUSHJ   P,IMN           ;INSERT MAIL NO.
        JRST    SN4A

;INSERTS MAIL NO., EXPECTS NO. IN T7,MAIL INDEX IN CS
;LOC. INDEX IN T2
IMN:    ADDI    T2,2
        MOVEI   T4,4
SN9:    PUSHJ   P,FHOLE         ;FIND HOLE FOR ENTRY
        JRST    SN7             ;FOUND HOLE THIS PKT
        TRZN    T1,400000       ;IS THERE A CHAIN PTR
        JRST    SN8             ;NO, GO SET NEW PTR
        PUSHJ   P,GPKT          ;GET PKT AND SETS T3
        MOVEI   T4,10
        JRST    SN9             ;TO NXT PKT
SN8:    PUSH    P,T1            ;SAVE OLD MAIL NO.
        PUSHJ   P,FEMT          ;FINDS EMPTY PKT
;RETURNS INDEX IN T4
        TRO     T4,400000
        DPB     T4,T5
        PUSHJ   P,WTBLK
        PUSHJ   P,GPKT
        POP     P,T1            ;RECOVER OLD MAIL NO.
        MOVEM   T7,BUF(T2)
        HRLM    T1,BUF(T2)      ;OLD NO.
        SETZM   BUF+1(T2)
        SETZM   BUF+2(T2)
        SETZM   BUF+3(T2)
        JRST    SN7+1

SN7:    DPB     T7,T5
        PUSHJ   P,WTBLK
        POPJ    P,
FHOLE: MOVEI   T5,BUF(T2)
        HRLI    T5,(POINT 18,0)
FHOLE1: ILDB    T1,T5
        SKIPN   T1
        POPJ    P,
        SOJG    T4,FHOLE1
        JRST    CPOPJ1

LKUP:   MOVE    T2,T4
        ADD     T2,T5
        TLZ     T2,400000
        IDIVI   T2,MBXHSH
LKUP4:  AOS     T3              ;SKIPS OVER FIRST BLK
        PUSHJ   P,RDBLK
        MOVEI   T2,0
LKUP3:  SKIPN   BUF(T2)
        JRST    LKUP1
        CAMN    T4,BUF(T2)
        JRST    LKUP2
LKUP5:  ADDI    T2,4
        CAIGE   T2,200
        JRST    LKUP3
        JRST    LKUP4
LKUP1:  AOS     (P)             ;SET SKIP RETURN IF NAME NOT FND
        MOVE    CS,T2
        LSH     CS,11           ;INDEX GOES IN LEFT HALF
        OR      CS,T3           ;CUSOR IN RT HALF
        MOVE    C,CS            ;CURRENT INDEX IN C
        POPJ    P,
LKUP2:  CAME    T5,BUF+1(T2)
        JRST    LKUP5
        JRST    LKUP1+1

OPMBX:  MOVEI   T5,0            ;USED FOR BUSY SW.
        INIT    RFL,17
IFNDEF DEBMAIL,<SIXBIT  /SYS/>
IFDEF DEBMAIL,<SIXBIT /DSK/>
        XWD     0,0
        LOGOUT
        MOVE    T1,['XOBLAM']
        MOVEI   T2,0
        MOVEI   T3,0
        MOVEI   T4,0
        LOOKUP  RFL,T1
        JRST    NOMBX
        MOVEI   T3,0
        MOVEI   T4,0
        ENTER   RFL,T1
        JRST    .+2
        POPJ    P,0
        JUMPE   T5,OPMBX1       ;FIRST BUSY
        JUMPG   T5,OPMBX2       ;LAST BUSY
        MOVEI   T5,5            ;FOR 5 SEC SLEEP
        MOVEI   T1,[ASCIZ /STAND BY, MAIL BOX BUSY
/]
        STRING
OPMBX2: SLEEP   T5,     ;SLEEP
        JRST    OPMBX+1
OPMBX1: SETO    T5,
        MOVEI   T1,3            ;3 SEC SLEEP ON FIRST TRY
        CALLI   T1,31
        JRST    OPMBX+1
TPERR:  STRING
        JRST    EX
FLMBX:  MOVEI   T1,[ASCIZ /MAIL BOX FULL, PSE TELL OPER./]
        JRST    TPERR
NOMBX:  MOVEI   T4,0
        ENTER   RFL,T1
        HALT
        CLOSE   RFL,0
        JRST    OPMBX

CLMBX:  RELEAS  RFL,
        POPJ    P,
;FINDS EMPTY PKT
;USES DIFFERENT BUFFER IF NEC.
;PUTS NEW INDEX IN T1
FEMT:   MOVE    T2,T3           ;FOR POSS. NEW BUF
        MOVEI   T1,0
FEMT2:  SKIPN   T4,BUF(T1)
        JRST    FEMT5
        CAME    T4,[-1]
        JRST    FEMT1
FEMT5:  LSH     T1,11
        OR      T1,T2   ;MERGE CURSER
        MOVE    T4,T1
        POPJ    P,
FEMT1:  ADDI    T1,4
        CAIGE   T1,200
        JRST    FEMT2
FEMT3:  AOS     T2
        PUSHJ   P,RDSBLK
        MOVEI   T1,0            ;START LOOKING SPEC. BUF
FEMT4:  SKIPN   SBUF(T1)
        JRST    FEMT5
        ADDI    T1,4
        CAIGE   T1,200
        JRST    FEMT4
        JRST    FEMT3

RDSBLK: USETI   RFL,1(T2)
        INPUT   RFL,SHED
        STATZ   RFL,760000
        JRST    SYNERR
        POPJ    P,

;FIND NEXT MAIL NO.
FNXML:  MOVEI   T7,1
        PUSHJ   P,GMBLK
        SKIPN   (T6)
        POPJ    P,
        DATE    T1,             ;GET DATE
        SUBI    T1,75           ;OVER 60 DAYS, DELETE
        CAMG    T1,3(T6)
        AOJA    T7,FNXML+1
        PUSHJ   P,CMAIL1
        POPJ    P,

;GET MAIL BLK BY MAIL NO (IN T7)
GMBLK:  MOVE    T3,T7
        LSH     T3,-1
        ADDI    T3,210
        PUSHJ   P,RDBLK
        MOVE    C,T3
        MOVEI   T6,BUF
        TRNE    T7,1
        ADDI    T6,100          ;HI ADR. IF ODD MAIL NO.
        POPJ    P,

;GETS PKT FROM INDEX IN T1, ASSUMES OLD IN C
;RESETS C, RETURNS PTR IN T2
GPKT:   MOVE    T2,T1
        XOR     T2,C
        TRNE    T2,777          ;CK IF NEW IN SAME BLK
        JRST    GPKT1           ;NO, GET NEW BLK
GPKT2:  MOVE    T2,T1
        LSH     T2,-11          ;MAKE PTR
        MOVE    C,T1
        POPJ    P,
GPKT1:  MOVE    T3,T1
        ANDI    T3,777
        PUSHJ   P,RDBLK
        JRST    GPKT2
;GETS ALL OF A USERS MAIL
MAIL:   SETOM   NMLSW#          ;DONT TYPE "NO MAIL" IF LETTER
MAIL1:  PUSHJ   P,OPUD          ;CK FOR BROADCAST LTR
        SETZB   T3,T4
        ENTER   RFL,T1
        MESS    <MAILBOX BUSY, PLEASE TRY LATER>
        PUSHJ   P,GNAM
        PUSHJ   P,LKLUD
        HALT    .
        MOVE    T2,BUF+LBITS(T1)
        TRNE    T2,77000
        JRST    TBL             ;FOUND 1 OR MORE LETTER BITS
        RELEASE RFL,0
        PUSHJ   P,OPMBX
        PUSHJ   P,GNAM          ;GET THIS USERS NAME
        PUSHJ   P,LKUP
        JRST    ML
        MOVEI   T1,[ASCIZ /NO MAIL/]
        SKIPN   NMLSW
        JRST    EX
        JRST    TPERR
ML:     SETZM   BUF(T2)
        SETZM   BUF+1(T2)
        PUSHJ   P,GMLN          ;STACKS ALL MAIL NO'S AND CLEANS
;REMOVE MAIL FROM STACK AND TYPE
ML2:    MOVE    T2,@SBUF
        AOS     SBUF
        JUMPLE  T2,EX           ;DONE IF NEG
        PUSHJ   P,TPML          ;TYPES MAIL IN T2
        SETZM   @NAMPT          ;ZERO THIS INDEX
        MOVE    T6,SVJBFF       ;RECOVER INDEX TO START OF BLK
        ADDI    T6,5            ;ADJ. TO NAME INDEX LIST
        SKIPLE  (T6)
        JRST    ML2A            ;ANOTHER ADDRESSEE,DONT DELETE
        SKIPL   (T6)
        AOJA    T6,.-3          ;BLANK SPOT
        SETZM   @SVJBFF         ;NO ADDRESSEES, DELETE MAIL
ML2A:   PUSHJ   P,WTBLK         ;WRITE MAIL BLOCK
        JRST    ML2

;GET THIS USERS NAME
GNAM:   HRROI   T4,-22
        GETTAB  T4,
        HALT    .
        HRROI   T5,-21
        GETTAB  T5,
        HALT    .
        POPJ    P,
;TYPES A BROADCAST LETTER, REMOVES 1 BIT
TBL:    MOVEI   C,1             ;THE LETTER NO.
        MOVEI   T5,1000         ;BIT FOR LETTER 1
TBL1:   TDZE    T2,T5
        JRST    TBL2            ;FOUND A BIT
        LSH     T5,1            ;TO NEXT LETTER
        AOJA    C,TBL1
TBL2:   MOVEM   T2,BUF+LBITS(T1)
        PUSHJ   P,WTBLK
        RELEASE RFL,0
        PUSHJ   P,OPMBX
        MOVE    T3,C
        LSH     T3,-1
        ADDI    T3,201
        PUSHJ   P,RDBLK
        MOVEI   T5,BUF
        TRNE    C,1
        ADDI    T5,100
        SKIPN   (T5)
        JRST    TBLE            ;EMPTY LETTER SLOT
        DATE    T1,
        CAMGE   T1,4(T5)        ;DONT TYPE EXPIRED LETTER
        JRST    TBL3
TBLE:   RELEASE RFL,0
        JRST    MAIL1
TBL3:   SKIPG   5(T5)           ;CK COUNT, DONE TYPE IF ZERO
        JRST    TBLE
        SOS     5(T5)
        PUSHJ   P,WTBLK
        RELEAS  RFL,0
        MOVE    T1,2(T5)
        PUSHJ   P,TDATE
        OUTCHR  [15]
        OUTCHR  [12]
        MOVEI   T1,12(T5)       ;ADR. OF LETTER
        STRING
        OUTCHR  [15]
        OUTCHR  [12]
        SETZM   NMLSW
        JRST    MAIL1
;TYPES MAIL, EXPECTS MAIL NO. IN T2 AND INDEX IN CS
TPML:   MOVE    T7,T2
        PUSHJ   P,GMBLK
        MOVEM   T6,SVJBFF       ;SAVE ADR OF MAIL FOR POS. DELETE
        SKIPN   (T6)
        POPJ    P,0             ;EMPTY
        ADDI    T6,5
        SKIPGE  (T6)
        POPJ    P,0             ;NO MATCH ON INDEX
        CAME    CS,(T6)
        AOJA    T6,.-3
;FOUND MATCH
        PUSH    P,T3
        PUSH    P,CS
        MOVEI   T1,[ASCIZ /
        FROM /]
        STRING
        MOVE    T1,SVJBFF       ;ADR. OF NAME
        PUSHJ   P,TPSIX
        OUTCHR  [","]
        OUTCHR  [" "]
        MOVE    T2,SVJBFF
        MOVE    T1,3(T2)        ;ADR. OF DATE
        PUSHJ   P,TDATE
        MOVE    T2,SVJBFF
        MOVE    T1,4(T2)
        PUSHJ   P,PRTIM1        ;TYPE TIME
        OUTCHR  [15]
        OUTCHR  [12]
        MOVEM   T6,NAMPT        ;SAVE LOC. OF MATCH
        SKIPL   1(T6)           ;SEARCH FOR END FLAG
        AOJA    T6,.-1
        MOVEI   T1,2(T6)        ;SET ADR. OF MAIL
        STRING
        POP     P,CS
        POP     P,T3
        POPJ    P,0

;STACKS ALL MAIL NOS. AND CLEARS LIST
;EXPECTS LOC. INDEX IN T2, MAIL INDEX IN CS
;IGNORES MAIL NOS. GRTR THAN 777
GMLN:   HRLI    T2,-2           ;SET CTR
        ADDI    T2,2            ;ADJ ADR TO SKIP OVER NAME
        MOVEI   T1,SBUF+1
        MOVEM   T1,SBUF         ;PTR FOR MAIL NO. STACK
        SETZM   SBUF+1          ;END FLAG
GM:     HLRZ    T1,BUF(T2)      ;GET MAIL NO.
        JUMPE   T1,GM1          ;IGNORE BLANKS
        HLLM    T1,BUF(T2)      ;CLEAR FROM BUFFER
        CAIG    T1,777          ;IGNORE LARGE NUMBERS
        PUSHJ   P,GMSTK         ;STACK MAIL NOS.
GM1:    HRRZ    T1,BUF(T2)      ;TRY RIGHT HALF
        JUMPE   T1,GM2
        SETZM   BUF(T2)
        TRZE    T1,400000       ;CK FOR INDEX LINK OR NULL
        JRST    GM3             ;FOUND LINK OR NULL
        CAIG    T1,777
        PUSHJ   P,GMSTK
GM2:    AOBJN   T2,GM   ;TO NEXT WORD
        PUSHJ   P,GBG           ;CLEAR GARBAGE
        PUSHJ   P,WTBLK         ;NO MORE, WRITE OUT BLK
        SETZM   @SBUF           ;END FLAG
        MOVEI   T1,SBUF+1
        MOVEM   T1,SBUF
        POPJ    P,
GM3:    TRNE    T1,200000       ;NULL FLAG
        JRST    GM2             ;IGNORE NULL
        MOVE    T4,T1           ;PREPARE TO TEST OF PKT IN CORE
        XOR     T4,C
        TRNE    T4,777
        JRST    GM4             ;NOT IN CORE,CLEANUP BEFORE WRITE
GM5:    MOVE    T2,T1           ;MAKE ADR. OF NEW PKT
        LSH     T2,-11
        HRLI    T2,-4           ;SET WORD COUNT
        JRST    GM
GM4:    PUSHJ   P,GBG
        PUSHJ   P,WTBLK
        PUSHJ   P,GPKT
        HRLI    T2,-4
        JRST    GM

;PREPARES FOR GARBARGE COLLECTION BY FLAGGING EMPTY CELLS
GBG:    MOVEI   T2,174
        SKIPE   T4,BUF(T2)      ;BACKUP TO FIRST NONZERO
        JRST    GBG1
GBG2:   SUBI    T2,4
        JUMPG   T2,GBG+1
        POPJ    P,0
GBG1:   CAME    T4,[-1]
        JRST    GBG3
        SETZM   BUF(T2)
        JRST    GBG2
GBG3:   SUBI    T2,4
        SKIPGE  T2
        POPJ    P,0
        SKIPN   BUF(T2)
        SETOM   BUF(T2)
        JRST    GBG3
GMSTK:  MOVEM   T1,@SBUF        ;STACK MAIL NOS.
        AOS     SBUF
        POPJ    P,0
;CANCELS MAIL, GIVEN MAIL NO. IN T7
;ALSO REMOVES GARBAGE ON LIST
CMAIL:  PUSHJ   P,GMBLK
CMAIL1: SETZM   (T6)
        JSP     T2,GIXN ;GET INDEX NUMBERS
        PUSHJ   P,WTBLK
        MOVEM   T7,NAMCT        ;SAVE FOR RETURN
CM:     POP     P,T1            ;RECOVER MAIL INDICES
        JUMPE   T1,CM6
        MOVE    CS,T1
        PUSHJ   P,GPKT
        SKIPN   T1,BUF(T2)
        JRST    CM              ;PKT ALREADY EMPTY
        CAMN    T1,[-1]
        JRST    CM
        PUSHJ   P,GMLN          ;GET ALL MAIL NO'S
CM1:    MOVE    T7,@SBUF        ;RECOVER STACKED MAIL NO'S
        AOS     SBUF
        JUMPE   T7,CM3          ;NO MORE, CLEAR MAIL LIST
        CAMN    T7,NAMCT        ;IS THIS THE ONE TO CANCEL?
        JRST    CM1             ;YES, IGNORE
CM2:    MOVE    T1,CS           ;GO BACK TO START
        PUSHJ   P,GPKT
        PUSHJ   P,IMN           ;INSERT MAIL BACK INTO LIST
        MOVE    T7,@SBUF        ;RECOVER ANOTHER MAIL NO.
        AOS     SBUF
        JUMPE   T7,CM   ;DONE INSERTING ON ZERO
        CAMN    T7,NAMCT        ;COULD BE THE ONE BEING CANCELLED
        JRST    .-4
        JRST    CM2             ;GO INSERT
CM3:    MOVE    T1,CS
        PUSHJ   P,GPKT
        SKIPE   T1,BUF+4(T2)    ;IF NEXT ZERO,THAEN ZERO THIS ONE
        SETO    T1,
CM5:    MOVEM   T1,BUF(T2)
        PUSHJ   P,WTBLK
        JRST    CM
CM6:    MOVE    T7,NAMCT        ;RECOVER SAVED MAIL NO.
        POPJ    P,0

GIXN:   ADDI    T6,5            ;GET INDEX NUMBERS
        MOVEI   T1,0
        PUSH    P,T1            ;ZERO END FLAG
        SKIPGE  T1,(T6)
        JRST    (T2)            ;REQUIRES RET. ADR. IN T2
        SKIPE   T1
        AOJA    T6,.-4
        AOJA    T6,.-4          ;SKIP OVER ZERO, DONT PUSH
;POSTMAN, TYPES MAIL NOT REMOVED FROM BOX, ETC.
POST:   MOVEI   T7,1            ;TRIAL MAIL NO.
POST1:  PUSHJ   P,OPMBX
        SETO    T1,
        PUSHJ   P,GMBLK
        JUMPE   T1,EX           ;FLAG SET BY RDBLK ON END
        MOVE    T1,(T6)
        JUMPE   T1,POST1B+1     ;EMPTY MAIL SLOT
        PUSHJ   P,GNAM          ;GET THIS USERS NAME
        TRNE    FL3,SPCOM       ;CK IF SPEC. COMMAND
        JRST    POST5
POST1B: CAME    T4,(T6)         ;CK IF THIS USER
        AOJA    T7,POST1        ;TO NEXT
        CAME    T5,1(T6)
        AOJA    T7,POST1
POST1C: MOVEI   T1,[ASCIZ /
MAIL SENT ON: /]
        STRING
        MOVE    T1,3(T6)
        PUSHJ   P,TDATE
        MOVE    T1,4(T6)
        PUSHJ   P,PRTIM1
        MOVEI   T1,[ASCIZ /
TO: /]
        STRING
        JSP     T2,GIXN         ;GET INDEX NUMBERS
POST3:  POP     P,T1            ;RECOVER MAIL INDEX
        JUMPE   T1,POST4        ;ZERO FOR END OF LIST
        MOVEM   T1,NAMPT        ;SAVE FOR TPMLX
POST3A: PUSHJ   P,GPKT
        MOVEI   T1,BUF(T2)
        PUSHJ   P,TPSIX
        POP     P,T1
        JUMPE   T1,POST4
        OUTCHR  [":"]
        JRST    POST3A
POST4:  PUSHJ   P,CLMBX
        PUSHJ   P,TPOST
        JRST    SYNERR
        AOJA    T7,POST1

POST5:  HRROI   T1,-20
        GETTAB  T1,
        HALT    .
        TRNE    T1,100000       ;OPERATOR LICENSE
        JRST    POST1C
        JRST    POST1B

TPOST:  MOVEI   T1,[ASCIZ /
TYPE MAIL? /]
        PUSHJ   P,YN
        PUSHJ   P,TPMLX
        MOVEI   T1,[ASCIZ /
CANCEL MAIL? /]
        PUSHJ   P,YN
        JRST    .+2
        JRST    CPOPJ1
        PUSHJ   P,OPMBX
        PUSHJ   P,CMAIL
        PUSHJ   P,CLMBX
        JRST    CPOPJ1
TPMLX:  PUSHJ   P,OPMBX
        MOVE    CS,NAMPT        ;RECOVER SAVED INDEX
        PUSHJ   P,TPML+1
        PUSHJ   P,CLMBX
        POPJ    P,
;TEST FOR TYPE OF EDITOR
TEDTEC: MOVE    T1,[SIXBIT "TECO"]
        JRST    TEDIT2
TEDIT:  TRNN    FL3,TYMX
TED10:  SKIPA   T1,[SIXBIT "EDIT10"]
NANED:  MOVE    T1,[SIXBIT /EDITOR/]    ;SET EDITOR
TEDIT2: MOVEM   T1,PCNAM
        POPJ    P,
;FLAGS IN RH OF FL3 FOR EDITING
SQFLG==1        ;/S FOR EDIT10
RFLG==2         ;READ ONLY (/R)

DOEDIT: PUSHJ P,SCAN    ;START ON THE FILE NAME
DOEDT1: PUSHJ P,GETNAM
EDRS:   PUSHJ   P,SCAN
        CAIE    C,"/"
        JRST NOEDSW
        PUSHJ P,SCAN
        TRNN FL,IDF
        JRST SYNERR
        MOVE T1,[XWD -EDSWL,EDSWT]
        PUSHJ P,SWSCAN
        JRST UNRECS
        JFCL
        TRO FL3,@EDSWT2-EDSWT(T1)
        JRST EDRS
NOEDSW: SKIPE SVDEV     ;DEVICE IS ILLEGAL
        JRST    SYNERR
        MOVEI   T3,OUTEDT
        MOVEI   T1," "          ;COMMAND FOR DREDIT
        PUSHJ   P,OUTEDT        ;OUTPUT THE S
        PUSHJ   P,OUTNAM        ;OUTPUT THE NAME & EXT
        MOVE T4,[XWD -EDSBL,EDSBT]
        PUSHJ P,SWBITS
        TRNN FL3,(T2)
        TRNE    FL,CREATF       ;EDIT OR CREATE?
        JRST    DOEDT3          ;CREATE (OR MAKE)
DOEDP:  PUSHJ   P,OUCRLF        ;EDIT (OR TECO) - OUTPUT CRLF
DOEDT2:
        CAIE C,"%"
        JRST NOCOMS
RCOM:   PUSHJ P,GETCH
        TLNN CS,TERMF   ;THE END, GET CRLF AND GO
        CAIN C,"%"      ;START OF ANOTHER COMMAND
        JRST DOEDP      ;THIS WILL PUT OUT CRLF AND READ MORE
        MOVE T1,C       ;PUT IT OUT
        PUSHJ P,OUTEDT
        JRST RCOM
NOCOMS: TLNN CS,TERMF
        JRST SYNERR
IFE TEMP,<      RELEAS  CHNEDT,0        ;END OF COMMAND>
IFN TEMP,<      PUSHJ P,CHKEDT          ;CHECK FOR TMPCOR UUO>
        JRST DONE       ;GO GET IT LOADED

DOEDT3: MOVEI   T1,175          ;OLD ALTMODE
        PUSHJ   P,OUTEDT        ;ENDS CREATE OR MAKE COMMAND
        JRST    DOEDT2

DEFINE TABLE
<SWITCH SEQUENCE,SQFLG
SWITCH READ,RFLG>
TBGEN (EDSWT,EDSWL,EDSWT2)
EDSBT:  SBTS R,RFLG
        SBTS S,SQFLG
EDSBL==.-EDSBT
IFE LSTRSW,<
OUTLST=OUTPIP           ;USE PIP FOR A LISTER
CHNLST=CHNPIP
IFN TEMP,<CHKLST=CHKPIP>>

IFN LSTRSW,<;FLAGS IN LH OF FL3(PERM) AND LH OF FL2(TEMP)
;FOR SPECIAL LISTER SWITCHES
PFLG==1         ;FORTRAN TYPE OUTPUT /FORTRAN
LFLG==2         ;CASE CONTROL /CASE
QFLG==4         ;QUESTION MARK CONTROL /QUESTION
HFLG==10        ;HEADER CONTROL /HEADER
NFLG==20        ;SEQUENCE NUMBER PRINTING /NOSEQUENCE
SFLG==40        ;SKIP CONTROL CHRS. /NOSKIPCONTROL
DFLG==100       ;MULTI LINE /DOUBLE /MULTILINE
CFLG==200       ;COPIES /COPIES
FFLG==400       ;FORMS
LIMSW==1000     ;IMEDIATE MODE SWITCH

;ENTRIES IN NUMBER TABLE
FORMS==1
COPIES==2
LINES==3

ARRAY PTAB,TTAB[LINES+1]
>
PRINT:  SKIPA T2,[SIXBIT /LPT:_/]
TYPR:   MOVE T2,[SIXBIT /TTY:_/]
        TLOA FL3,HFLG
LISTR:  MOVE T2,[SIXBIT /LPT:_/]
        TLO     FL3,QFLG
        MOVE    T3,LSTCSP       ;USE LISTER
        MOVEM   T3,PCNAM        ;...
        MOVEI T3,OUTLST
LISTYP: PUSHJ P,OUTSIX
        MOVSI T2,(SIXBIT /DSK/)
        MOVEM T2,LOKNAM
        TRO     FL,PIPF ;IF IT'S PIP, ALLOW *.MAC, ETC.
LSTLP:  PUSHJ P,SCAN    ;GET NAME
IFN LSTRSW,<LSTLPP:     CAIN C,"/"
        JRST PLSTSW>
        PUSHJ P,GETNAM
        MOVEI T3,OUTLST
        SKIPN T2,SVDEV
        MOVE T2,LOKNAM  ;USE PREV NAME IF NO NEW NAME
        MOVEM   T2,LOKNAM       ;KEEP DTA NAME AROUND FOR MORE FILES
        PUSHJ P,OUTSIX  ;OUTPIT IT
        MOVEI T1,":"    ;COLON AFTER DEV
        PUSHJ P,0(T3)
        PUSHJ P,OUTNAM  ;FILE NAME
IFN LSTRSW,<HLL FL2,FL3 ;SET TEMPS FROM PERMS
        MOVE T1,[XWD PTAB,TTAB]
        BLT T1,TTAB+LINES>
        PUSHJ P,SCAN
IFN LSTRSW,<TSWRT:
        CAIN C,"/"
        JRST TLSTSW
        MOVEI T3,OUTLST
        MOVE T4,[XWD -LSWBTL,LSWBTB]
        PUSHJ P,SWBITS
        TLNN FL2,(T2)
        MOVEI T4,LINES
NXSLC:  TLNN FL2,@SPSTB-1(T4)   ;IS BIT ON
        JRST NOSPC      ;NO
        MOVEI T1,"/"
        PUSHJ P,OUTLST
        MOVE T1,TTAB(T4)
        PUSHJ P,OUTNUM
        HLLZ T2,SPSTB-1(T4)
        PUSHJ P,OUTSIX  ;THE SWITCH
NOSPC:  SOJG T4,NXSLC
        CAIE C,"("
        JRST ENDLST
        SKIPA   ;PASS PAGE INFO
LST1:   PUSHJ P,GETCH
        MOVE T1,C
        PUSHJ P,OUTLST
        CAIE C,")"
        JRST LST1
        PUSHJ P,SCAN
>
ENDLST: CAIN C,","      ;SHOULD BE COMMA OR CR
        JRST [MOVEI T1,","
                PUSHJ P,0(T3)
                JRST LSTLP]
        TLNN CS,TERMF   ;SHOULD BE TERMINATOR
        JRST SYNERR     ;WASNT
ENDLS1: PUSHJ   P,OUCRLF        ;ADD CRLF TO COMMAND
IFE TEMP,<      RELEASE CHNLST,0>
IFN TEMP,<
        PUSHJ   P,CHKLST        ;...>
        JRST DONE1

LSTCSP: IFE LSTRSW,<SIXBIT /PIP/>
        IFN LSTRSW,<SIXBIT /LISTER/>

DEFINE SPMK (A,B)
<<SIXBIT !A!>+B>
SPSTB:  SPMK F,FFLG
        SPMK C,CFLG
        SPMK D,DFLG     ;THIS MUST BE IN THE SAME ORDER AT TTAB

OUTNUM: IDIVI T1,^D10   ;RECOGNIZE THIS??
        HRLM T2,(P)
        SKIPE T1
        PUSHJ P,OUTNUM
        HLRZ T1,(P)
        ADDI T1,"0"
        JRST (T3)
PLSTSW:        PUSHJ P,LSTSW
        TLZ FL3,(T4)
        MOVSS T4
        TLO FL3,(T4)
        MOVEM T2,PTAB(T3)
        JRST LSTLPP

TLSTSW: PUSHJ P,LSTSW
        TLZ FL2,(T4)
        MOVSS T4
        TLO FL2,(T4)
        MOVEM T2,TTAB(T3)
        JRST TSWRT

LSTSW:  PUSHJ P,SCAN
        TRNN FL,IDF
        JRST SYNERR
        MOVE T1,[XWD -LLG,LSTBL]
        PUSHJ P,SWSCAN
        JRST UNRECS
        JFCL
        SETZB T2,T3
        MOVE T4,LSTB2-LSTBL(T1)
        TLNE T4,LIMSW
        JRST (T4)
        JRST SCAN

DEFINE TABLE
<SWITCH FORTRAN,<XWD PFLG,0>
SWITCH NONFORTRAN,PFLG
SWITCH NOCASE,<XWD LFLG,0>
SWITCH CASE,LFLG
SWITCH NOQUESTION,<XWD QFLG,0>
SWITCH QUESTION,QFLG
SWITCH SINGLE,DFLG
SWITCH DOUBLE,<XWD LIMSW,DOUBLE>
SWITCH MULTISPACE,<XWD LIMSW,MULTI>
SWITCH HEADING,HFLG
SWITCH NOHEADING,<XWD HFLG,0>
SWITCH SEQUENCE,NFLG
SWITCH NOSEQUENCE,<XWD NFLG,0>
SWITCH SKIPCONTROL,<XWD SFLG,0>
SWITCH NOSKIPCONTROL,SFLG
SWITCH COPIES,<XWD LIMSW,DCOP>
SWITCH FORMS,<XWD LIMSW,DFRM>
>
TBGEN (LSTBL,LLG,LSTB2)
LSWBTB: SBTS P,PFLG
        SBTS L,LFLG
        SBTS Q,QFLG
        SBTS H,HFLG
        SBTS N,NFLG
        SBTS S,SFLG
LSWBTL==.-LSWBTB
DCOP:  PUSHJ P,STARGS
        TRNN FL,NUMFS
        JRST SYNERR
        MOVSI T4,CFLG
        MOVE T2,NUMAC   ;GET NUMBER
        CAIG T2,1       ;IF NOT AT LEAST 2, TURN OFF FLAG
        MOVEI T4,CFLG
        MOVEI T3,COPIES
        JRST FINPRS

DFRM:   PUSHJ P,STARGS
        TRNN FL,NUMFS
        JRST SYNERR
        MOVSI T4,FFLG
        SKIPN T2,NUMAC  ;NUMBER MUST BE NON-ZERO
        MOVEI T4,FFLG
        MOVEI T3,FORMS
        JRST FINPRS

MULTI:  PUSHJ P,STARGS
        TRNN FL,NUMFS
        JRST SYNERR
        MOVE T2,NUMAC
MULTIC: MOVSI T4,DFLG
        MOVEI T3,LINES
        JRST FINPRS

DOUBLE: MOVEI T2,1
        TRZ FL3,PARFLG
        JRST MULTIC
OUTNAM:        MOVE T2,SVNAM
        PUSHJ P,OUTSIX
        SKIPN T2,SVEXT
        JRST OUTPP
        MOVEI T1,"."
        PUSHJ P,(T3)
        PUSHJ P,OUTSIX
OUTPP:  SKIPN T2,SVPPN
        POPJ P,
        JRST OUTPPN

OUCRLF: MOVEI   T1,15   ;CARRIAGE RETURN
        PUSHJ   P,0(T3)         ;TO CURRENT OUTPUT FILE
        MOVEI   T1,12           ;LINE FEED
        JRST    0(T3)           ;TO OUTPUT FILE
DEFINE ORTNS
<
PROCESS
;ALSO THESE
X LOADER,LOD
X DIRIT,DIR
IFN LSTRSW,<X LIST,LST>
X PIP,PIP
X CREF,CRF
X EDT,EDT
>

DEFINE X (A,B,C,D,E)
<OUT'B: MOVEI T5,NORT   ;SET UP INDEX NUMBER
        TRON FL2,FCH'B  ;CHACK FOR INITED
        PUSHJ P,INITCH  ;AND INIT IF NEEDED
        JRST COMNIT     ;COMMON ROUTINE
IFN TEMP,<
CHK'B:  MOVEI T5,NORT
        JRST COMCHK>
NORT==NORT+1>

NORT==0
        ORTNS

ARRAY BJFF,BFLG[NORT],BOB[NORT*3]

BOB0:   FOR (0,NORT,BOB+XXX*3)
BOB1:   FOR (0,NORT,BOB+1+XXX*3)
BOB2:   FOR (0,NORT,BOB+2+XXX*3)

IFN TEMP,<
DEFINE X(A,B,C,D,E)
<       RELEASE CHN'B,0>
RLTB:   ORTNS>

DEFINE X(A,B,C,D,E)
<       OUT CHN'B,0>
OTAB:   ORTNS

DEFINE X(A,B,C,D,E)
<       ENTER CHN'B,NAME>
ENTB:   ORTNS

DEFINE X(A,B,C,D,E)
<       OPEN CHN'B,NSDOPN>
OPNTB:  ORTNS

DEFINE X(A,B,C,D,E)
<       OUTBUF CHN'B,2>
OBTB:   ORTNS

IFN TEMP,<
DEFINE X(A,B,C,D,E)
<       SETSTS CHN'B,1>
SETS1:  ORTNS

DEFINE X(A,B,C,D,E)
<       SETSTS CHN'B,17>
SETS17: ORTNS
>

DEFINE X(A,B,C,D,E)
<       <SIXBIT /   A/>>
BNAM:   ORTNS
COMNIT:        SOSLE @BOB2(T5) ;ANY MORE SPACE
        JRST BOU1
IFN TEMP,<SKIPN BFLG(T5)        ;DOING TEMPCORE?
        PUSHJ P,INOLD   ;YES, INIT FOR DSK
        SETOM BFLG(T5)  ;AND MARK>
        XCT OTAB(T5)    ;THE OUT
        SKIPA
        JRST OUTER
BOU1:   IDPB T1,@BOB1(T5)
        POPJ P,
IFN TEMP,<
COMCHK: SKIPE BFLG(T5)
        JRST CK1B       ;ALREADY SET TO DSK
        PUSH P,T1
        MOVE T1,BJFF(T5)        ;RECOVER START OF BUFFERS
        ADDI T1,2       ;START OF DATA
        HRRM T1,TMPFIL+1        ;SET UP IOWD
        HRRZ T1,@BOB1(T5)       ;THE BYTE POINTER
        SUB T1,BJFF(T5)
        SUBI T1,2
        MOVNS T1        ;SET UP COUNT
        HRLM T1,TMPFIL+1
        HRLZ T1,BNAM(T5)        ;THE TEMPCORE NAME
        MOVEM T1,TMPFIL
        MOVE T1,[XWD 3,TMPFIL]  ;WRITE OP
        TMPCOR  T1,
        SKIPA   ;ERROR
        JRST    [POP P,T1
                POPJ P,]
        POP P,T1
        PUSHJ P,INOLD   ;SET UP FOR REGULAR OUTPUT
CK1B:   XCT RLTB(T5)    ;DO A RELEASE (WILL WRITE IT OUT)
        POPJ P,>

INITCH: PUSHJ P,CHKRM   ;GET SOME SPACE FOR BUFFERS
        PUSH P,T1
        PUSH P,T2
IFN TEMP,<      MOVE T1,JOBFF
        MOVEM T1,BJFF(T5)
        MOVS T2,BOB0(T5)
        MOVEM T2,INIT1
        XCT OPNTB(T5)
        JRST EX ;CAN NOT GET DSK
        XCT OBTB(T5)    ;OUTBUF
IFN TEMP,<ADDI T1,3
        HRLI T1,440700  ;SET UP BUFFER HEADERS
        MOVEM T1,@BOB1(T5)
        HRLS T1
        ADDI T1,1
        SETZM -1(T1)
        MOVEI T2,176(T1)
        BLT T1,(T2)
        MOVEI T1,200*5
        MOVEM T1,@BOB2(T5)
        MOVSI T1,400000
        ANDCAM T1,@BOB0(T5)
        SETZM BFLG(T5)  ;MARK AS TEMPCORE
        POP P,T2
        POP P,T1
        POPJ P,
INOLD:  PUSH P,T1>
        HLLZ T1,JOBNAM  ;SET UP DSK
        HRR T1,BNAM(T5)
        MOVEM T1,NAME
        MOVSI T1,(SIXBIT /TMP/)
        MOVEM T1,NAME+1
        SETZM NAME+2
        SETZM NAME+3
IFN TEMP,<XCT SETS17(T5)        ;SET MODE TO 17 TO PRESERVE BUFFERS>
        XCT ENTB(T5)    ;ENTER IT
        JRST FIU
IFN TEMP,<XCT SETS1(T5) ;BACK TO MODE 1>
IFE TEMP,<      POP P,T2>
        POP P,T1
        POPJ P,
NONSTD:        AOBJP SVPT,TMNER        ;NOT ENOUGH ROOM TO READ NAME
        PUSHJ P,STARGS  ;WORRY ABOUT ( AROUND ARGS
        PUSHJ P,GETNAM  ;GET A FILE NAME ETC
        PUSHJ P,FINPAR  ;END OF ARGS
        AOS T1,NOSTNM   ;ONE MORE NOW
        CAIL T1,NSDNM
        JRST TMSTD      ;WE DON'T ALLOW THAT MANY
        AOBJP IOPNT,TMSTD       ;NOT ENOGH CHANELS FREE
        SUBI IOPNT,1    ;JUST ONE LESS, NOT ONE MORE IN USE
        MOVE C,NOSTBF(T1)       ;GET A BUFFER AREA
        MOVEM C,INIT1
        PUSH P,T1       ;SAVE NUMBER
        MOVNI T1,-NSDCHN(T1)    ;GET CHANEL NUMBER
        MOVE T5,[OPEN 0,NSDOPN]
        DPB T1,[POINT 4,T5,12]
        POP P,T1        ;GET IT BACK
        XCT T5  ;DO THE OPEN
        JRST DSKNA      ;SORRY
        PUSHJ P,CHKRM
        XCT NINTAB(T1)  ;DO THE INBUF
        MOVE C,SVNAM(SVPT)      ;GET NAME GIVEN
        MOVEM C,NONSNM(T1)      ;AND SAVE IT
        HLLM C,EXTAB+NPROCS(T1) ;AND SAVE AS AN EXTENSION
        MOVEM C,NSDSWS(T1)      ;ALSO AS A SWITCH
        HLRS C  ;SET UP FILE NAME (NO TEMP FILES FOR NON-STANDARD)
        HLL C,JOBNAM
        MOVEM C,LNAM
        SKIPN C,SVDEV(SVPT)
        MOVSI C,(SIXBIT /DSK/)  ;ASSUME DSK NOT SYS
        MOVEM C,NONSDV(T1)      ;SAVE AWAY
        MOVE C,SVEXT(SVPT)
        MOVEM C,NONSXT(T1)
        MOVE C,SVPPN(SVPT)
        MOVEM C,NONSPN(T1)
        SETZM NONUSD(T1)        ;USED YET??
        MOVSI C,(SIXBIT /TMP/)
        MOVEM C,LEXT
        SETZM LEXT+1
        SETZM LPPN
        XCT ENTAB(T1)   ;THE ENTER
        JRST FIU
        SUB SVPT,[XWD 1,1]      ;GET IT BACK TO PROPER PLACE
        AOS NPROC1      ;ONE MOREPROCESSOR
        MOVE T1,NSWTAB(T1)      ;GET THE SWITCH
        IORM T1,CALPRO  ;AND SET IN LIST OF ALL
        HRLZS T1        ;FAKE A SWITCH
        JRST INTSW      ;AND GO

NSWTAB: FOR (1,NSDNM,<CAT(NSDS,\XXX)>)
NOSTBF: FOR (0,NSDNM,<XWD OBUF+3*XXX,0>)
ARRAY OBUF[9]
NINTAB: FOR (NSDCHN,NSDNM,<OUTBUF XXX,2>,-1)
ARRAY NONSNM,NONSDV,NONSXT,NONSPN,NONUSD[NSDNM]
ENTAB:  FOR (NSDCHN,NSDNM,<ENTER XXX,LNAM>,-1)

FOR (1,NSDNM,<CAT(NSDO,\XXX):MOVEI T5,<XXX-1>*3
        SETOM NONUSD+XXX-1
        JRST COMSTD>)
COMSTD:        SOSLE OBUF+2(T5)
        JRST OUT1
        XCT OUTB(T5)
        SKIPA
        JRST OUTER
OUT1:   IDPB T1,OBUF+1(T5)
        POPJ P,

OUTB:   FOR (NSDCHN,NSDNM,<OUT XXX,0
0
0>,-1)

FINPRS: PUSHJ P,SCAN
FINPAR: TRNN FL3,PARFLG ;IS THE PAREN FLAG ON??
        POPJ P, ;NO, ARG NOT SURROUNDED BY ()
        CAIE C,")"      ;SHOULD HAVE ENDED PROPERLY
        JRST SYNERR     ;NO
        JRST SCAN       ;YES, SCAN PAST IT

STARGS: PUSHJ P,SCAN
        CAIN C,"("      ;ARGS SURROUNDED BY PAREN??
        TROA FL3,PARFLG ;YES
        TRZA FL3,PARFLG ;NO
        JRST SCAN       ;SCAN PAST (
        POPJ P,
;DATA STORAGE ASSIGNMENTS

PDP:    IOWD    PDL,PDLB                ;INITIAL PDL POINTER

INTEGER USRPPN,SWBKB,SWBKL,ONAM,OEXT,OPPN,BINNAM
INTEGER PCNAM,SAVPPN,SVSWP,PCNUM,SDAT,STIM,SAVCHR
INTEGER ACCUM,DINPT,DINCT,SVJFF,CORTOP,CORT1,SVRPP,NUMAT
INTEGER SVIND,SVPDL,JOBNAM,BROCNT,LODSP2,LODCT2,LODSP,LODCTR
INTEGER NAMCT,NAMPT,SVJBFF

ARRAY LODSBK,LODSB2[LODSCT/5+1]
ARRAY SWBKS,SVDEV,SVNAM,SVEXT,SVPPN[NFILE],NSDSWS[NSDNM]
ARRAY DDTBUF[^D30],LOOKBF,OPENB[3],PDLB[PDL+1],SWBLK[SWBK+1]
ARRAY ERRBUF[22],BUFTAB,FREBUF[NESTDP+2],IOPD[<NESTDP+2>*3+1]
ARRAY BUF[200],SVAC[20],SBUF[200]
IFN TEMP,<ARRAY TMPFIL[2]>

LVAR

IFN TEMP,<TMPFL1=TMPFIL+1>
DEFINE CODIT (A,B)
<XXZ==.
        B
        LVAR
        ARRAY A[.-XXZ]
        LVAR>

DEFINE X(A,B,C,D,E)
<SIXBIT /B/>

INIDAT: CODIT (RUNBLK,<EXP SYSDEV,0,0,0,0,0>)

        NAME=RUNBLK+1
        LNAM=RUNBLK+1
        LEXT=RUNBLK+2
        LDAT=RUNBLK+3
        LPPN=RUNBLK+4
        INILOW=RUNBLK

        CODIT (EXTAB,<PROCESS
        BLOCK NSDNM>)

        CODIT(LOKINT,1)

        CODIT(LOKNAM,<0
        XWD LOOKBF,LOOKBF>)

        CODIT (CALPRO,ALPROCS)

        CODIT (NPROC1,NPROCS)

        CODIT (DSKLK,<1
        SIXBIT /DSK/
        XWD LOOKBF,LOOKBF>)

        CODIT (FCOMD,<ASCII /@***SVC.TMP
/
        BYTE (7) 177,177>)

        CODIT (FCOMD3,<ASCII /EXEC />)
        CODIT (FCOMD2,<ASCII /@***EDS.TMP
/
        BYTE (7) 177,177>)

        CODIT (CRFRDR,<ASCII /@***CRE.TMP
/
        BYTE (7) 177,177>)

        CODIT (NSDOPN,<EXP 1,SIXBIT /DSK/>)

        CODIT (INIT1,0)

        CODIT (HED,<IOWD 200,BUF
        0>)
        CODIT (SHED,<IOWD 200,SBUF
        0>)
INILEN==.-INIDAT
LOWTOP=RUNBLK-1

        LVAR
        LIT

IFDEF DEBFLAG,<PATCH:BLOCK 300>
        END     STPT
-  