!TITLE: SETTERM
!SUBTTL GARY GERE       VERSION 1       DECEMBER 1972
!
! SETTERM WILL ACCEPT INPUT IN FORM
!
!   -SETTERM INPUT,OUTPUT [HUSH]
!

! TABLES
!TCHR TABLE CONTAINS THE IDENTIFICATION CHARACTERS AND FOR
!EACH IDENTIFICATION CHARACTER THE RESPECTIVE CHARACTERISTICS
!FORMAT IS:  LH=CHARACTERISTICS, RH=ID CHARACTER
!
GLOBAL TABLE TCHR(1:10) [&
OCT 440000000000+$A,&
OCT 444004000000+$C,&
OCT 444146000000+$E,&
OCT 442252000000+$G,&
OCT 226002000000+$B,&
OCT 244004000000+$F,&
OCT 236002000000+$J,&
OCT 244146000000+$N,&
OCT 000000000000+$D,&
0 !0 TERMS IT!]

!TNUM CONTAINS THE BAUD RATE AND ASSOCIATED TYMNET BAUD NUMBER
!FOR EACH AVAILABLE BAUD RATE.  FORMAT IS
!   LH=TYMNET BAUD RATE NUMBER    RH=USER BAUD RATE
!
GLOBAL TABLE TNUM(1:7) [&
(OCT 0000000)+10,&
(OCT 1000000)+15,&
(OCT 2000000)+30,&
(OCT 3000000)+40,&
(OCT 4000000)+60,&
(OCT 5000000)+120,&
0 !0 TERMS IT !]

! THESE EXU CALLS CHEAT BY ASSUMMING THAT SIMPL ARGUMENTD START AT
! REGISTER 1 AND GO UP.  ARGUMENTS ARE PASSED IN REGISTERS AND
! ARE SET UP IN THE EXU CALL SO SIMPL SETS EVERYTHING UP FOR US.
FIND %GETTMC,%HELP,%OUTDATE
DEF RESCAN AS EXU(OCT 51,OCT 10,0)
DEF SETTMC(X) AS EXU(OCT 47,OCT 4,OCT 777763,X)    !SETS TERMINAL CHRS!

GLOBAL STR,CHAR,A,B,MYINP,MYOUT,MYLEFT
GLOBAL %LOOK,%NXCH,%SEARCH,%VAL,%OUTNUM

BEGIN:  IOCS(0) !GET IOCS MOVING - NO IO BUFFERS
        RESCAN          !RESCAN INPUT BUFFER
        STR_GETLINE(TEL)
        WHILE NOT $A<=CHAR_NXCH(@STR)<=$Z THEN NULL
        EIF CHAR=$R THEN DO
          MSG('$PARAMETERS: '); STR_GETLINE(TEL)
          END
        ELSE LOOP DO
          IF CHAR_NCHV STR = CARRET THEN DO
            MSG('$PARAMETERS: '); STR_GETLINE(TEL)
            CHAR_$ 
            END
          WHILE CHAR#$ 
        END

        MYINP_MYOUT_MYLEFT_GETTMC
        MYLEFT BAND_ OCT 777776000000
        MYINP BAND _OCT 1600000
        MYOUT BAND_ OCT 167777
        WHILE CHAR_NXCH(@STR)=$  THEN NULL

! IF $A<=CHAR=$Z THEN DO TABLE SEARCH
        EIF $A<=CHAR<=$Z THEN DO
          A_SEARCH(CHAR) BAND OCT 1600000
          END

! IF A = * THEN USE CURRENT PARAMETERS
        ORIF CHAR=$* THEN A_MYINP

! IF $0<=CHAR<=$9 THEN DO NUMERIC INPUT --- ITS IN DECIMAL
        ORIF $0<=CHAR<=$9 THEN DO
          A_VAL(CHAR,@STR)       !INPUT STUFF
          A_(LOOK(A) SHL 16) BAND OCT 1600000
          END

! HERE WHEN DIDNT LIKE IT
        ELSE DO
          MSG('$COMMAND ERROR AT: '); PUT(CHAR); PUT(CARRET)
          EXIT
          END

! GOT FIRST PARAMETER
        WHILE CHAR_NXCH(@STR)#$, THEN NULL
        CHAR_NXCH(@STR)

! IF $A<=CHAR<=$A THEN DO TABLE SEARCH
        EIF $A<=CHAR<=$Z THEN DO
          B_SEARCH(CHAR) BAND OCT 177777
          END

! IF A = * THEN USE CURRENT PARAMETERS
        ORIF CHAR=$* THEN B_MYOUT

! IF $0<=CHAR<=$9 THEN DO NUMERIC INPUT --- ITS IN DECIMAL
        ORIF $0<=CHAR<=$9 THEN DO
          B_VAL(CHAR,@STR)
          B_(LOOK(B) SHL 13) BAND OCT 177777
          END

! ERROR
        ELSE DO
          MSG('$COMMAND ERROR AT: '); PUT(CHAR); PUT(CARRET)
          EXIT
          END

! NOW SET CHARACTERISTICS
        SETTMC(MYLEFT+B+A)

! NOW CHECK FOR THINGS LIKE THE HUS COMMAND, ETC.
        IF CHAR_NCHV STR#CARRET THEN DO
          IF CHAR_NCHV STR=$H AND CHAR_NCHV STR=$U THEN EXIT
          END
          OUTDATE       !OUTPUT DATE, ETC.

!ALL DONE
        EXIT

! NXCH - RETURNS NEXT CHARACTER OR FAILS
%NXCH(@STR)

LOCAL CHR

        IF CHR_NCHV STR=$? THEN HELP
        IF CHR#CARRET THEN RETURN CHR

! ERROR

        MSG('$COMMAND INCOMPLETE$')
        EXIT

END NXCH

! SEARCH - ARG IS CHAR TO SEARCH ON
%SEARCH(CHR)

LOCAL CNT

        FOR CNT_1 TO 500 DO
          IF TCHR(CNT) BAND OCT 177=CHR THEN RETURN TCHR(CNT) SHR 18
        IF TCHR(CNT)=0 THEN DO

        MSG('$INVALID PARAMETER: '); PUT(CHR); PUT(CARRET)
        EXIT
        END
        END

END SEARCH

! LOOK - ARG IS NUM TO SEARCH ON
%LOOK(NUM)

LOCAL CNT

        FOR CNT_1 TO 500 DO
          IF TNUM(CNT) BAND OCT 7777=NUM THEN RETURN TNUM(CNT) SHR 18
        IF TNUM(CNT)=0 THEN DO

        MSG('$INVALID BAUD RATE$')
        EXIT
        END
        END

END LOOK

! VAL - ARG1 IS FIRST CHAR, @ARG2 IS STRING TO COME FROM
%VAL(ARG1,@ARG2)

LOCAL VLU,CHR

        VLU_0

          PCHV ARG2
          LOOP DO
            IF NOT $0<=CHR_NCHV ARG2<=($0+10) THEN DO
            IF CHR=CARRET OR CHR=$, OR CHR=$  THEN DO
            PCHV ARG2
            RETURN VLU
            END
              MSG('$INVALID NUMBER$')
              EXIT
              END
            VLU_VLU*10+(CHR-$0)
            END

END VAL

        \\->BEGIN

! OUTNUM - GIVEN NUMBER, PRINT'S IT OUT
%OUTNUM(NUM)

LOCAL T(11),K,L

        K_NUM
        LOOP DO
          FOR L_11 BY -1
          [K;T(L)]_K DIVMOD 10
          WHILE K>0
          END

        FOR L_L TO 11 PUT(T(L)+$0)
        RETURN

END OUTNUM
