MODULE KEYCM(DOLLAR,ENTRIES=($KEYCM),FSAVE,TIMING,TIMER=EXTERNAL(SIX12))=

BEGIN

REQUIRE KDEFS.BLI[7,107355];
REQUIRE RDEFS.BLI[7,107355];
REQUIRE UDEFS.BLI[7,107355];
REQUIRE DVDEFS.BLI[7,107355];
UNDECLARE $KEYCM;

MACHOP LSH=#242;

MACRO RSHIFT(X)= (REGISTER Q$$$; Q$$$_X; LSH(Q$$$,-1))&;

GLOBAL ROUTINE $KEYCM(SOB,A,B)=

   BEGIN
   MAP KSOB$ SOB;
   MAP UDYNARY$ A:B;
   LOCAL KDSC$ DSC;
   LOCAL S,ANS;

   DSC_.SOB[K$SDESCR];
   INCR I FROM 0 TO .SOB[K$SNKEY]-1
      DO
         BEGIN
         S_.DSC[K$SSIZE,.I];
         SELECT .DSC[K$SDDTYPE,.I] OF
            NSET
   DDV$1WD: IF (ANS_.A[0]-.B[0]) NEQ 0
               THEN RETURN SIGN(.ANS);

   DDV$2WD: IF (ANS_.A[0]-.B[0]) NEQ 0
               THEN RETURN SIGN(.ANS)
               ELSE IF (ANS_.A[1]-.B[1]) NEQ 0
                  THEN RETURN SIGN(.ANS);

   DDV$STR: INCR N FROM 0 TO .S-1
               DO IF (ANS_RSHIFT(.A[.N])-RSHIFT(.B[.N])) NEQ 0
                  THEN RETURN SIGN(.ANS);
            TESN;
         A_.A+.S;
         B_.B+.S;
         END;
   SOB[K$SDUPLICATES]_1;   % EQUALITY OF KEYS => INSERTING A DUPLICATE %
   RETURN 0;
   END;
END ELUDOM
