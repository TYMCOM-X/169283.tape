;*********
;  PROCEDURE TO CONVERT STRING TO NUMBER.
;  CALL: 1ST CHAR IN R1, BYTE PTR TO 1ST CHAR IN R2
;  PUSHJ P,GIVNUM       WHERE  REGISTERS 0-8 WILL BE USED.
;   RETURN WILL BE NUMBER IN R0. NOSKIP RETURN WILL BE ERROR ON
; SCAN, WITH CHARACTER CAUSING ERROR IN R0,BYTE PTR
; IN R1.
; SKIP RETURN WILL BE INTEGER TYPE NUMBER SCANNED,
; DOUBLE SKIP RETURN WILL BE REAL NUMBER SCANNED, VALUE IN R0.


; REGISTERS
A=0
C=A+1
BP=C+1
N=BP+1
T=N+1
B=T+1
D=B+1
F=D+1
X1=F+1
GIVNUM:  SETZB N,B
        MOVEI D,9      ;WE WILL ONLY CONSIDER 9 DIGITS SIGNIF.
        MOVEI F,(F)     ;NO FLAGS SET TO START, CLEAR LH OF F
        
        CAIN C,"+"      ;CK 1ST CHAR FOR +
        JRST EV1        ; IF SO PASS OVER

        CAIN C,"-"      ; ELSE CK FOR -
        JRST EV2         ; IF NOT -, GO DO OTHER CKS

        TLO F,F.MIN     ;ELSE SET - FLAG

EV1:    ILDB C,BP      ;GET NEXT CHAR
EV2:    CAIG C,"9"     ;DIGIT?
        CAIGE C,"0"
        JRST EV3       ;NO

        TLO F,F.NUM    ;YES, SET DIGIT SEEN FLAG
        JUMPE N,EV2A    ;LEAD ZEROS NOT COUNTED
        SOJG D,EV2A       ;ONLY SIGNIF DIGITS ARE ACCUMULATED
        AOJA B,EV2B        ;OTHER DIGITS COUNTED FOR SCALING

EV2A: IMULI N,^D10
        ADDI N,-60(C)    ; N=N*10+ VALUE(C)
EV2B:   TLNE F,F.DOT    ; DOT SEEN?
        SUBI B,1         ; IF SO, KEEP SCALE FACTOR CORRECT
        JRST EV1

EV3:    CAIE C,"."      ;NOT DIGIT OR SIGN. ISIT DEC. PT?
        JRST EV4        ;NOPE. TRY ELSEWHERE

        TLOE F,F.DOT    ;YES, DEC PT. SET FLAG
        POPJ P,         ; TWO PTS ARENT ALLOWED!

        JRST EV1

EV4:    TLNN F,F.NUM    ;HAVE WE SCANNED A DIGIT?
        POPJ P,          ;THEN WE HAVE GARBAGE!

        CAIE C,"E"      ;HAVE WE AN E?
        JRST EV8        ;NO

        ILDB C,BP       ;WE HAVE AN E, GET NXT CHAR
        CAIN C,"+"  ; POSITIVE EXP?
        JRST EV5        ;YES

        CAIE C,"-"      ; NEG EXP?
        JRST EV6
        TLO F,F.MXP     ; YES. SET FLAG

EV5:    ILDB C,BP       ;NXT CHAR
EV6:    CAIG C,"9"       ;DIGIT?
        CAIGE C,"0"
        POPJ P,         ;IF NOT, BAD EXPONENT

        MOVEI A,-60(C)   ;SAVE 1ST DIG. OF EXP
        ILDB C,BP         ; GET NXT
        CAIG C,"9"        ;IS IT DIGIT?
        CAIGE C,"0"
        JRST EV7        ;NO, 1DIGIT EXP.
        IMULI A,^D10
        ADDI A,-60(C)    ;CALCULATE NUM VALUE FOR EXP

        ILDB C,BP        ;LOOK AHEAD ALWAYS WHEN SCANNING

EV7: TLNE F,F.MXP   ;NEG EXP?
        MOVN A,A     ;YES
        ADD B,A      ;ADD DECIMAL PT SCALE FACTOR

EV8:   JUMPE N,CPJ1   ;NO FURTHER PROCESS IF NUM IS ZERO
EV8A:   MOVE X1,N
        IDIVI X1,^D10    ;FUDGE TO REMOVE TRAIL ZEROS (.1,.100,...ETC)
        JUMPN X2,EV8B
        MOVE N,X1
        AOJA B,EV8A

EV8B:  MOVM A,B     ;NOW TO CK FOR FINITE WORD SIZE FUDGINGS
        CAILE A,MAXE
        POPJ P,      ;TOO LARGE A NUMBER!

        PUSH P,T     ;SAVE REGISTER 
        JFFO N,.+2   ;TAKE CARE OF LEAD ZEROS
        JRST .+4
        LSH N,-11(T)    ;NORMALIZE
        MOVNI T,(T)     ;RH OF T = - RH OF T
        FSC N,244(T)    ;EXPONENT TREATMENT
        POP P,T       ;NOW RESTORE REGISTER
        JOV .+1       ;TURN OFF OVERFLO
EV8C:    CAIGE B,^D15   ;DO SCALING
        JRST EV8D
        SUBI B,^D14
        FMPR N,D1E14
        JRST EV8C

EV8D:   CAML B,[EXP -^D4]
        JRST EV8E
        ADDI B,^D18
        FMPR N,D1EM18
        JRST EV8D

EV8E:  FMPR N,DECTAB(B)
        TLNE F,F.MIN
        MOVN N,N
        JOV CPJ
CPJ1:   AOS P
CP:     POPJ P,       ;GUD RETURN

;*********

; NUMBER,STRING CONVERSION TABLES, CONSTANTS:

D1EM18: OCT 105447113564   ;10^-18

DECFLO:
D1EM4:  OCT 163643334273   ;10^-4
        OCT 167406111565
        OCT 172507534122
        OCT 175631463146
DECTAB: DEC 1.0     ;10^0
        DEC 1.0E1
         DEC 1.0E2
       DEC 1.0E3
       DEC 1.0E4
       DEC 1.0E5
      DEC 1.0E6
      DEC 1.0E7
      DEC 1.0E8
      DEC 1.0E9
      DEC 1.0E10
       DEC 1.0E11
       OCT 250721522451    ;10^12
       OCT 254443023471
D1E14: OCT 257553630410   ;10^14
DECCEI:

MAXE=^D38
DECFIX: EXP 225400000000
FIXCON: EXP 233400000000

;FLAGS FOR CONVERSION ROUTINES:

F.NUM=200000  ;DIGIT SEEN
F.MIN=100000  ;MINUS SEEN
F.MXP=40000  ;MINUS EXPONENT
F.DOT=20000  ;DECIMAL PT SEEN



        END
