C* **********************************************************
C* *                                                        *
C* *      PROCUCT 062-1464-02                               *
C* *              TERMINAL CONTROL SYSTEM                   *
C* *                                                        *
C* *                                                        *
C* *                                                        *
C* *                                                        *
C* *                SOFTWARE DEVELOPMENT                    *
C* *            INFORMATION DISPLAY PRODUCTS                *
C* *                  TEKTRONIX, INC.                       *
C* *                                                        *
C* *                                                        *
C* *            COPYRIGHT 1971  TEKTRONIX, INC.             *
C* *                 ALL RIGHTS RESERVED                    *
C* *                                                        *
C* *                                                        *
C* **********************************************************
C* *
C* *
C* *
C* *
C*      START OF TERMINAL CONTROL SYSTEM GENERAL MODULES
C*
C*
C*      PLEASE NOTE!!!!!!
C*      IN THE FOLLOWING LISTING COMMENTS FOLLOWED
C*      BY AN  *  ARE INSTALLATION COMMENTS.
C*      THEY INDICATE CONTROL CHARACTERS AND TERMINAL
C*      DEPENDENT OPERATIONS OR VARIABLES,
C*      FOR WHICH MODIFICATIONS MAY BE NEEDED ACCORDING TO
C*      THE SYSTEM USED.
C*
C*
C ROUTINE---DA-0010.05-01   FINITT
      SUBROUTINE FINITT(IX,IY)
      CALL MOVABS(IX,IY)
      CALL ANMODE
      STOP
      END
C ROUTINE---DA-0020.05-01   BELL
      SUBROUTINE BELL
C*  OUTPUT (BEL)
      CALL TOUTPT(7)
      RETURN
      END
C ROUTINE---BG-0460.05-01   SUBWIN
C       ROUTINE TO ALTER WINDOW SIZE BUT RETAIN SCALE
C 
       SUBROUTINE SUBWIN(XV,XL,YV,YL)
	COMMON /TKTRNX/ KBAUDR,KERROR,KGRAFL,KHOMEY,KKMODE,
     1 KHORSZ,KVERSZ,KITALC,KSIZEF,KLMRGN,KRMRGN,
     2 KTBLSZ,KHORZT(10),KVERTT(10),
     3 KBEAMX,KBEAMY,KMOVEF,KPCHAR(4),KDASHT,
     4 KMINSX,KMINSY,KMAXSX,KMAXSY,TMINVX,TMINVY,TMAXVX,TMAXVY,
     5 TREALX,TREALY,TIMAGX,TIMAGY,TRCOSF,TRSINF,TRSCAL
C 
      SCALE=(KMAXSX-KMINSX)/(TMAXVX-TMINVX)
      OFF=XV-TMINVX
      IX=KMINSX+OFF*SCALE
      LX=XL*SCALE
      SCALE=(KMAXSY-KMINSY)/(TMAXVY-TMINVY)
      OFF=YV-TMINVY
      IY=KMINSY+OFF*SCALE
      LY=YL*SCALE
      CALL VWINDO(XV,XL,YV,YL)
      CALL SWINDO(IX,LX,IY,LY)
      RETURN
      END
C ROUTINE---BG-0030.05-01   SWINDO
      SUBROUTINE SWINDO(IX,LX,IY,LY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      KMINSX=IX
      KMINSY=IY
      KMAXSX=IX+LX
      KMAXSY=IY+LY
      KGRAFL=0
      RETURN
      END
C ROUTINE---BG-0040.05-01   VWINDO
      SUBROUTINE VWINDO(X,XL,Y,YL)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREAXL, TREAYL, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      TMINVX=X
      TMINVY=Y
      TMAXVX=X+XL
      TMAXVY=Y+YL
      KGRAFL=0
      RETURN
      END
C ROUTINE---BG-0050.05-01   POINTR
      SUBROUTINE POINTR(X,Y)
      CALL REL2AB(X,Y,RX,RY)
      CALL POINTA(RX,RY)
      RETURN
      END
C ROUTINE---BG-0050.05-01   POINTR
      SUBROUTINE POINTR(X,Y)
      CALL REL2AB(X,Y,RX,RY)
      CALL POINTA(RX,RY)
      RETURN
      END
C ROUTINE---BG-0060.05-01   DASHR
      SUBROUTINE DASHR(X,Y,L)
      CALL REL2AB(X,Y,XL,YL)
      CALL DASHA(XL,YL,L)
      RETURN
      END
C ROUTINE---BG-0070.05-01   DASHA
      SUBROUTINE DASHA(X,Y,L)
      CALL LVLCHT
      CALL DSHMOD(L)
      CALL V2ST(1,X,Y)
      RETURN
      END
C ROUTINE---DA-0080.05-01   DSHREL
      SUBROUTINE DSHREL(IX,IY,L)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      JX=IX+KBEAMX
      JY=IY+KBEAMY
      CALL DSHABS(JX,JY,L)
      RETURN
      END
C ROUTINE---DA-0090.05-01   DSHABS
      SUBROUTINE DSHABS(IX,IY,L)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
1      CALL DSHMOD(L)
2      CALL TKDASH(IX,IY)
      RETURN
      END
C ROUTINE---BG-0100.05-01   DRAWR
      SUBROUTINE DRAWR(X,Y)
      CALL REL2AB(X,Y,RX,RY)
      CALL DRAWA(RX,RY)
      RETURN
      END
C ROUTINE---BG-0110.05-01   POINTA
      SUBROUTINE POINTA(X,Y)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      CALL LVLCHT
      IF(KKMODE.EQ.2)GO TO 10
      CALL PNTMOD
10    CALL V2ST(0,X,Y)
      RETURN
      END
C ROUTINE---BG-0120.05-01   DRAWA
      SUBROUTINE DRAWA(X,Y)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      CALL LVLCHT
      IF(KKMODE.NE.1)GO TO 20
      IF(KMOVEF.EQ.1)GO TO 30
      GO TO 10
20    CALL VECMOD
30    CALL MODCHK(KBEAMX,KBEAMY)
10    CALL V2ST(1,X,Y)
      RETURN
      END
C ROUTINE---BG-0130.05-01   MOVER
      SUBROUTINE MOVER(X,Y)
      CALL REL2AB(X,Y,RX,RY)
      CALL MOVEA(RX,RY)
      RETURN
      END
C ROUTINE---BG-0140.05-01   REL2AB
      SUBROUTINE REL2AB(XIN,YIN,XOUT,YOUT)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      CALL LVLCHT
      XOUT=(((XIN*TRCOSF)-(YIN*TRSINF))*TRSCAL)+TIMAGX
      YOUT=(((XIN*TRSINF)+(YIN*TRCOSF))*TRSCAL)+TIMAGY
      RETURN
      END
C ROUTINE---BG-0150.05-01   MOVEA
      SUBROUTINE MOVEA(X,Y)
      CALL LVLCHT
      CALL VECMOD
      CALL V2ST(0,X,Y)
      RETURN
      END
C ROUTINE---BG-0160.05-01   VCURSR
      SUBROUTINE VCURSR(ICHAR,X,Y)
      CALL DCURSR(ICHAR,IXA,IYA)
      CALL REVCOT(IXA,IYA,X,Y)
      RETURN
      END
C ROUTINE---BG-0170.05-01   V2ST
      SUBROUTINE V2ST(I,X,Y)
      DIMENSION BUFIN(4),BFOUT(4)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      EQUIVALENCE (BUFIN(1),XS),(BUFIN(2),YS),(BUFIN(3),XE),
     1(BUFIN(4),YE)
      EQUIVALENCE (BFOUT(1),CXS),(BFOUT(2),CYS),(BFOUT(3),CXE),
     1            (BFOUT(4),CYE)
      XE=X
      YE=Y
      IF(I.EQ.0)GO TO 10
      XS=TIMAGX
      YS=TIMAGY
      CALL CLIPT(BUFIN,BFOUT)
      IF(KERROR.EQ.1)GO TO 110
      IF(CXS.EQ.TREALX.AND.CYS.EQ.TREALY)GO TO 120
      MODE=KKMODE
      CALL VECMOD
      CALL WINCOT(CXS,CYS)
      KKMODE=MODE
      GO TO 120
10    CALL PCLIPT(XE,YE)
      IF(KERROR.EQ.1)GO TO 110
      CXE=XE
      CYE=YE
120   CALL WINCOT(CXE,CYE)
      TREALX=CXE
      TREALY=CYE
110   TIMAGX=X
      TIMAGY=Y
      RETURN
      END
C ROUTINE---BG-0180.05-01   CLIPT
      SUBROUTINE CLIPT(BUFIN,OUTBF)
      DIMENSION  BUFIN(1),OUTBF(1)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      GSTAX=BUFIN(1)
      GSTAY=BUFIN(2)
      GENDX=BUFIN(3)
      GENDY=BUFIN(4)
      IF(GSTAX.GE.TMINVX)GO TO 10
      IF(GENDX.GE.TMINVX)GO TO 20
      GO TO 110
10    IF(GSTAX.LE.TMAXVX)GO TO 20
      IF(GENDX.LE.TMAXVX)GO TO 20
      GO TO 110
20    IF(GSTAY.GE.TMINVY)GO TO 21
      IF(GENDY.GE.TMINVY)GO TO 30
      GO TO 110
21    IF(GSTAY.LE.TMAXVY)GO TO 30
      IF(GENDY.LE.TMAXVY)GO TO 30
      GO TO 110
30    IF(GSTAX.NE.GENDX)GO TO 31
      DSTAX=GSTAX
      DENDX=GSTAX
      CALL PARCLT(GSTAY,GENDY,TMINVY,TMAXVY,DSTAY,DENDY)
      GO TO 120
31    IF(GSTAY.NE.GENDY)GO TO 40
      DSTAY=GSTAY
      DENDY=GSTAY
      CALL PARCLT(GSTAX,GENDX,TMINVX,TMAXVX,DSTAX,DENDX)
      GO TO 120
40    A=GENDX-GSTAX
      B=GENDY-GSTAY
      IF(GSTAX.LT.TMINVX)GO TO 41
      IF(GSTAX.LE.TMAXVX)GO TO 43
      Q=TMAXVX
      GO TO 42
43    IF(GSTAY.GT.TMAXVY)GO TO 140
      IF(GSTAY.LT.TMINVY)GO TO 44
      DSTAX=GSTAX
      DSTAY=GSTAY
      GO TO 150
41    Q=TMINVX
42    DSTAY=GSTAY+((Q-GSTAX)*B/A)
      IF(DSTAY.GT.TMAXVY)GO TO 140
      IF(DSTAY.LT.TMINVY)GO TO 44
      DSTAX=Q
      GO TO 150
44    R=TMINVY
      GO TO 45
140   R=TMAXVY
45    DSTAX=GSTAX+((R-GSTAY)*A/B)
      IF(DSTAX.GT.TMAXVX)GO TO 110
      IF(DSTAX.LT.TMINVX)GO TO 110
      DSTAY=R
150   IF(GENDX.LT.TMINVX)GO TO 50
      IF(GENDX.GT.TMAXVX)GO TO 51
      IF(GENDY.GT.TMAXVY)GO TO 160
      IF(GENDY.LT.TMINVY)GO TO 52
      DENDX=GENDX
      DENDY=GENDY
      GO TO 120
51    Q=TMAXVX
      GO TO 53
50    Q=TMINVX
53    DENDY=GSTAY+((Q-GSTAX)*B/A)
      IF(DENDY.GT.TMAXVY)GO TO 160
      IF(DENDY.LT.TMINVY)GO TO 52
      DENDX=Q
      GO TO 120
52    R=TMINVY
      GO TO 60
160   R=TMAXVY
60    DENDX=GSTAX+((R-GSTAY)*A/B)
      DENDY=R
120   OUTBF(1)=DSTAX
      OUTBF(2)=DSTAY
      OUTBF(3)=DENDX
      OUTBF(4)=DENDY
      KERROR=0
      GO TO 70
110   KERROR=1
70    RETURN
      END
C ROUTINE---BG-0190.05-01   PARCLT
      SUBROUTINE PARCLT(RL1,RL2,RM1,RM2,RN1,RN2)
      IF(RL1.LT.RM1)GO TO 10
      IF(RL1.GT.RM2)GO TO 20
      RN1=RL1
      IF(RL2-RM1)30,40,40
10    RN1=RM1
40    IF(RL2.LE.RM2)GO TO 50
      RN2=RM2
      GO TO 60
50    RN2=RL2
      GO TO 60
20    RN1=RM2
      IF(RL2.GE.RM1)GO TO 50
30    RN2=RM1
60    RETURN
      END
C ROUTINE---BG-0200.05-01   WINCOT
      SUBROUTINE WINCOT(X,Y)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      WXD=TMAXVX-TMINVX
      WYD=TMAXVY-TMINVY
      DISX=KMAXSX-KMINSX
      DISY=KMAXSY-KMINSY
      KDISX=((X-TMINVX)*DISX/WXD)+FLOAT(KMINSX)
      KDISY=((Y-TMINVY)*DISY/WYD)+FLOAT(KMINSY)
      CALL MODCHK(KDISX,KDISY)
      RETURN
      END
C ROUTINE---BG-0210.05-01   LVLCHT
      SUBROUTINE LVLCHT
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KGRAFL.NE.0)GO TO 10
      CALL REVCOT(KBEAMX,KBEAMY,TREALX,TREALY)
      TIMAGX=TREALX
      TIMAGY=TREALY
      KGRAFL=1
10    RETURN
      END
C ROUTINE---BG-0220.05-01   REVCOT
      SUBROUTINE REVCOT(IX,IY,X,Y)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      SXL=KMAXSX-KMINSX
      VXL=TMAXVX-TMINVX
      X=(FLOAT(IX-KMINSX)*VXL/SXL)+TMINVX
      SYL=KMAXSY-KMINSY
      VYL=TMAXVY-TMINVY
      Y=(FLOAT(IY-KMINSY)*VYL/SYL)+TMINVY
      CALL PCLIPT(X,Y)
      RETURN
      END
C ROUTINE---BG-0230.05-01   PCLIPT
      SUBROUTINE PCLIPT(X,Y)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      KERROR=0
      IF(X.LT.TMINVX)GO TO 10
      IF(X.GT.TMAXVX)GO TO 10
      IF(Y.LT.TMINVY)GO TO 10
      IF(Y.LE.TMAXVY)GO TO 20
10    KERROR=1
20    RETURN
      END
C ROUTINE---DA-0240.05-01   PNTREL
      SUBROUTINE PNTREL(IX,IY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      JX=KBEAMX+IX
      JY=KBEAMY+IY
      CALL PNTABS(JX,JY)
      RETURN
      END
C ROUTINE---DA-0250.05-01   PNTABS
      SUBROUTINE PNTABS(IX,IY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KKMODE.EQ.2)GO TO 10
      CALL PNTMOD
10    CALL XYCNVT(IX,IY)
      KGRAFL=0
      RETURN
      END
C ROUTINE---DA-0260.05-01   DRWREL
      SUBROUTINE DRWREL(IX,IY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      JX=KBEAMX+IX
      JY=KBEAMY+IY
      CALL DRWABS(JX,JY)
      RETURN
      END
C ROUTINE---DA-0270.05-01   DRWABS
      SUBROUTINE DRWABS(IX,IY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KKMODE.NE.1)GO TO 20
      IF(KMOVEF.EQ.1)GO TO 30
      GO TO 10
20    CALL VECMOD
30    CALL XYCNVT(KBEAMX,KBEAMY)
10    CALL XYCNVT(IX,IY)
      KGRAFL=0
      RETURN
      END
C ROUTINE---DA-0280.05-01   MOVREL
      SUBROUTINE MOVREL(IX,IY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      JX=KBEAMX+IX
      JY=KBEAMY+IY
      CALL MOVABS(JX,JY)
      RETURN
      END
C ROUTINE---BG-0290.05-01   SETTAB
      SUBROUTINE SETTAB(ITAB,ITBTBL)
      DIMENSION ITBTBL(1)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      KERROR=0
      IF(ITAB.LE.0)GO TO 10
      I=1
20    IF(ITBTBL(I).EQ.ITAB)GO TO 10
      IF(ITAB.LT.ITBTBL(I))GO TO 60
      IF(ITBTBL(I).EQ.0)GO TO 60
      I=I+1
      IF(I.LE.KTBLSZ)GO TO 20
30    KERROR=1
10    RETURN
60    ITEMP=ITBTBL(I)
      ITBTBL(I)=ITAB
70    IF(ITEMP.EQ.0)GO TO 10
      I=I+1
      IF(I.GT.KTBLSZ)GO TO 30
      ITEMPA=ITBTBL(I)
      ITBTBL(I)=ITEMP
      ITEMP=ITEMPA
      GO TO 70
      END
C ROUTINE---BG-0300.05-01   RSTTAB
      SUBROUTINE RSTTAB(ITAB,ITBTBL)
      DIMENSION ITBTBL(1)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(ITAB)10,20,30
30    DO 40 I=1,KTBLSZ
      IF(ITAB.EQ.ITBTBL(I))GO TO 50
40    CONTINUE
      GO TO 10
50    J=KTBLSZ-1
      DO 60 K=I,J
60    ITBTBL(K)=ITBTBL(K+1)
      ITBTBL(KTBLSZ)=0
      GO TO 10
20    DO 21 II=1,KTBLSZ
21    ITBTBL(II)=0
10    RETURN
      END
C ROUTINE---BG-0310.05-01   TABHOR
      SUBROUTINE TABHOR
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      KFLG=0
      DO 10 K=1,KTBLSZ
      IF(KHORZT(K).NE.0)KFLG=1
      IF(KHORZT(K).GT.KBEAMX)GO TO 20
10    CONTINUE
      IF(KFLG.EQ.0)GO TO 30
15    CALL NEWLIN
      GO TO 30
20    IF (KHORZT(K).GE.KRMRGN) GO TO 15
      CALL MOVABS(KHORZT(K),KBEAMY)
      CALL ANMODE
30    RETURN
      END
C ROUTINE---BG-0320.05-01   TABVER
      SUBROUTINE TABVER
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      DO 10 K=1,KTBLSZ
      I=KTBLSZ-K+1
      IF(KVERTT(I)) 10,10,11
11    IF(KVERTT(I).LT.KBEAMY)GO TO 30
10    CONTINUE
      GO TO 20
30    CALL MOVABS(KBEAMX,KVERTT(I))
      CALL ANMODE
20    RETURN
      END
C ROUTINE---DA-0350.05-01   DSHMOD
      SUBROUTINE DSHMOD(L)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      KKMODE=4
      KDASHT=L
      RETURN
      END
C ROUTINE---BG-0360.05-01   MODCHK
      SUBROUTINE MODCHK(IX,IY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KKMODE-4)1,2,1
2      CALL TKDASH(IX,IY)
      GO TO 10
1      CALL XYCNVT(IX,IY)
10      RETURN
      END
C ROUTINE---DA-0370.05-01   TKDASH
      SUBROUTINE TKDASH(IX,IY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      DIMENSION DTABL(10), U(9), WORK(10)
      DATA IDTYPE, U/-1,3.,-5.,8.,-10.,23.,-25.,48.,-50.,0./
      IF(KDASHT)26,25,27
27      IF (IDTYPE-KDASHT)1,2,1
1      ICHAR=KDASHT
      DO 3 NO=1,10
6      IF(ICHAR)4,5,4
4      I=ICHAR-((ICHAR/10)*10)
      ICHAR=ICHAR/10
      IF(I)3,6,3
3      WORK(NO)=U(I)
      NO=11
5      NO=NO-1
      DO 7 I=1,NO
7      DTABL(I)=WORK(NO+1-I)
      IDTYPE=KDASHT
      I=0
      SEGLEN=0
2     IF(NO-1)11,11,10
10    IF(LASTX-KBEAMX)14,13,14
13    IF(LASTY-KBEAMY)14,12,14
14    SEGLEN=0
      I=0
12    X=FLOAT(KBEAMX)
      Y=FLOAT(KBEAMY)
      DX=FLOAT(IX-KBEAMX)
      DY=FLOAT(IY-KBEAMY)
      DIAG=SQRT(DX*DX+DY*DY)
      DIST=DIAG
      IF (SEGLEN-1.5)15,15,17
15      I=MOD(I,NO)+1
      SEGLEN=ABS(DTABL(I))
17      IF(SEGLEN-DIST)20,18,18
20      F=SEGLEN/DIAG
      X=X+DX*F
      Y=Y+DY*F
      IF (DTABL(I))21,21,22
C*   OUTPUT A GS FOR A DARK VECTOR
21      CALL TOUTPT(29)
22      CALL XYCNVT(IFIX(X),IFIX(Y))
      DIST=DIST-SEGLEN
      SEGLEN=0
      GO TO 15
18      IF(DTABL(I))29,29,24
29      CALL TOUTPT(29)
C*   THAT WAS A GS FOR AN INVISIBLE LINE
24      CALL XYCNVT(IX,IY)
      LASTX=IX
      LASTY=IY
      SEGLEN=SEGLEN-DIST
19      RETURN
11      IF(NO)26,26,25
26      CALL TOUTPT(29)
25      CALL XYCNVT(IX,IY)
      NO=1-NO
      GO TO 19
      END
C* **********************************************************
C* *                                                        *
C* *                                                        *
C* *              TERMINAL CONTROL SYSTEM                   *
C* *                                                        *
C* *                                                        *
C* *                                                        *
C* *                                                        *
C* *                SOFTWARE DEVELOPMENT                    *
C* *            INFORMATION DISPLAY PRODUCTS                *
C* *                  TEKTRONIX, INC.                       *
C* *                                                        *
C* *                                                        *
C* *            COPYRIGHT 1971  TEKTRONIX, INC.             *
C* *                 ALL RIGHTS RESERVED                    *
C* *                                                        *
C* *                                                        *
C* **********************************************************
C* *
C* *
C* *
C* *
C* *
C* *
C*      START OF TERMINAL CONTROL SYSTEM 4002A MODULE
C*
C*
C*      PLEASE NOTE!!!!!!
C*      IN THE FOLLOWING LISTING COMMENTS FOLLOWED
C*      BY AN  *  ARE INSTALLATION COMMENTS.
C*      THEY INDICATE CONTROL CHARACTERS AND TERMINAL
C*      DEPENDENT OPERATIONS OR VARIABLES,
C*      FOR WHICH MODIFICATIONS MAY BE NEEDED ACCORDING TO 
C*      THE SYSTEM USED
C*
C*
C ROUTINE---DA-2150.05-01 4002A INITT
      SUBROUTINE INITT(ICPS)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      KBAUDR=ICPS*10
      KMINSX=0
      KMAXSX=1023
      KMINSY=0
      KMAXSY=760
      KHOMEY=740
      KHORSZ=12
      DO 100 I=1,10
      KHORZT(I)=0
100   KVERTT(I)=0
      KITALC=0
      KLMRGN=0
      KRMRGN=1010
      KSIZEF=0
      KTBLSZ=10
      KVERSZ=19
      TMINVX=0.
      TMAXVX=1023.
      TMINVY=0.
      TMAXVY=760.
      TRCOSF=1.
      TRSINF=0.
      TRSCAL=1.
      CALL ANMODE
      CALL NEWPAG
      RETURN
      END
C ROUTINE---DA-2160.05-01 4002A ERASE
      SUBROUTINE ERASE
C*  OUTPUT (CAN) TO ERASE SCREEN
      CALL TOUTPT(24)
      CALL IOWAIT(5)
      RETURN
      END
C ROUTINE---DA-2170.05-01 4002A HDCOPY
      SUBROUTINE HDCOPY
C*  OUTPUT (ETB) TO INITIATE A HARD COPY
      CALL TOUTPT(23)
      CALL IOWAIT(180)
      RETURN
      END
C ROUTINE---DA-2180.05-01 4002A KIN
      FUNCTION KIN(RI)
      KIN=RI*124.
      RETURN
      END
C ROUTINE---DA-2190.05-01 4002A KCM
      FUNCTION KCM(RC)
      KCM=RC*49.25
      RETURN
      END
C ROUTINE---DA-2200.05-01 4002A DCURSR
      SUBROUTINE DCURSR(ICHAR,IX,IY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      DIMENSION SAV(60)
      CALL SVSTAT(SAV)
      CALL ANMODE
C*  OUTPUT (DC1) TO ENABLE GRAPHIC CURSOR
      CALL TOUTPT(17)
      CALL TINPUT(ICHAR)
      K=0
C*  ORDER OF INPUT MUST REFLECT HARDWARE STRAPPING
10    CALL TINPUT(KOOR)
      KOOR=MOD(KOOR,32)*32
      CALL TINPUT(KOOR2)
      KOOR=KOOR+MOD(KOOR2,32)
      K=K+1
      IF(K.EQ.2)GO TO 20
      IX=KOOR
      GO TO 10
20    IY=KOOR
C*  INPUT OF CR OR EOT SHOULD BE ACCEPTED AT THIS POINT
C*  IF NEEDED
       CALL TINPUT(III)
       CALL TINPUT(III)
      CALL RESTAT(SAV)
      RETURN
      END
C ROUTINE---DA-2210.05-01 4002A INCPLT
      SUBROUTINE INCPLT(IONOFF,IDIR,NO)
      DIMENSION KSGN(10)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      DATA KSGN/0,1,1,1,0,-1,-1,-1,0,1/
      IF(KKMODE.EQ.3)GO TO 10
      CALL IPMOD
10    KOUT=MOD(IDIR,8)
      KOUT=KOUT+40+IONOFF*8
      DO 20 J=1,NO
20    CALL TOUTPT(KOUT)
      KBEAMX=KBEAMX+(KSGN(IDIR)*NO)
      KBEAMY=KBEAMY+(KSGN(IDIR+3)*NO)
      RETURN
      END
C ROUTINE---DA-2220.05-01 4002A ANCHO
      SUBROUTINE ANCHO(ICHAR)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KKMODE.EQ.0)GO TO 10
      CALL ANMODE
10    CALL TOUTPT(ICHAR)
      KBEAMX=KBEAMX+((KSIZEF+1)*KHORSZ)
      IF(KBEAMX.LT.KRMRGN)GO TO 20
      CALL NEWLIN
20    RETURN
      END
C ROUTINE---DA-2240.05-01 4002A BAKSP
      SUBROUTINE BAKSP
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KKMODE.EQ.0)GO TO 10
      CALL ANMODE
C*  OUTPUT (BS)
10    CALL TOUTPT(8)
      KBEAMX=KBEAMX-((KSIZEF+1)*KHORSZ)
      IF(KBEAMX.GE.0)GO TO 100
      KBEAMX=0
100   RETURN
      END
C ROUTINE---DA-2250.05-01 4002A HOME
      SUBROUTINE HOME
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KLMRGN.EQ.0)GO TO 10
      CALL MOVABS(KLMRGN,KHOMEY)
      CALL ANMODE
      GO TO 40
10    IF(KKMODE)20,30,20
20    CALL ANMODE
C*  OUTPUT (SOH) CONTROL CHAR AND RESET BEAM PARMETERS
30    CALL TOUTPT(1)
      KBEAMX=0
      KBEAMY=KHOMEY
40    RETURN
      END
C ROUTINE---DA-2260.05-01 4002A NEWPAG
      SUBROUTINE NEWPAG
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KKMODE) 5,6,5
5     CALL ANMODE
C*  OUTPUT (FF) CONTROL CHAR
6     CALL TOUTPT(12)
      CALL IOWAIT(5)
      IF(KLMRGN.EQ.0)GO TO 10
      CALL MOVABS(KLMRGN,KHOMEY)
      CALL ANMODE
      GO TO 20
10    KBEAMX=0
      KBEAMY=KHOMEY
20    RETURN
      END
C ROUTINE---DA-0330.05-01   NEWLIN
      SUBROUTINE NEWLIN
      CALL CARTN
      CALL LINEF
      RETURN
      END
C ROUTINE---DA-0340.05-01   CARTN
      SUBROUTINE CARTN
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KKMODE.EQ.0)GO TO 20
      CALL ANMODE
20    IF(KLMRGN.EQ.0)GO TO 10
      CALL MOVABS(KLMRGN,KBEAMY)
      CALL ANMODE
      GO TO 30
10    KBEAMX=0
C*  OUTPUT (CR)
      CALL TOUTPT(13)
30    RETURN
      END
C ROUTINE---DA-2230.05-01 4002A LINEF
      SUBROUTINE LINEF
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KKMODE.EQ.0)GO TO 10
      CALL ANMODE
C*  OUTPUT (LF)
10    CALL TOUTPT(10)
      KBEAMY=KBEAMY-((KSIZEF+1)*KVERSZ)
      RETURN
      END
C ROUTINE---DA-2280.05-01 4002A ITALIR
      SUBROUTINE ITALIR
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      KITALC=0
      CALL ANMODE
10    RETURN
      END
C ROUTINE---DA-2330.05-01 4002A RESTAT
      SUBROUTINE RESTAT(RARRAY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      DIMENSION RARRAY(55),IB(44),RB(11)
      EQUIVALENCE (IB(1),KBAUDR),(RB(1),TMINVX)
      DO 100 I=1,44
100   IB(I)=RARRAY(I)
      DO 101 I=1,11
101   RB(I)=RARRAY(I+44)
      MOVE=KMOVEF
      MODE=KKMODE+1
      KGL=KGRAFL
      CALL ANMODE
      CALL MOVABS(KBEAMX,KBEAMY)
      GO TO (200,300,400,500,300),MODE
200   CALL ANMODE
      GO TO 600
300   IF(MOVE) 350,600,350
350   CALL VECMOD
      GO TO 600
400   CALL PNTMOD
      GO TO 600
500   CALL IPMOD
600   KGRAFL=KGL
      RETURN
      END
C ROUTINE---DA-2300.05-01 4002A NRMSIZ
      SUBROUTINE NRMSIZ
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      KSIZEF=0
      CALL ANMODE
10    RETURN
      END
C ROUTINE---DA-2310.05-01 4002A ANMODE
      SUBROUTINE ANMODE
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
C*  SET ALPHA MODE OUTPUT (US)
      CALL TOUTPT(31)
      KKMODE=0
      IF(KSIZEF) 10,20,10
10    CALL DBLSIZ
20    IF(KITALC) 30,40,30
30    CALL ITALIC
40    KGRAFL=0
      RETURN
      END
C ROUTINE---DA-2290.05-01 4002A DBLSIZ
      SUBROUTINE DBLSIZ
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
C*  OUTPUT DOUBLE SIZE CONTROL CHAR (EM)
      CALL TOUTPT(25)
      KSIZEF=1
      RETURN
      END
C ROUTINE---DA-2270.05-01 4002A ITALIC
      SUBROUTINE ITALIC
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
C*  OUTPUT ITALICS CONTROL CHAR (SO)
      CALL TOUTPT(14)
      KITALC=1
      RETURN
      END
C ROUTINE---DA-2380.05-01 4002A PNTMOD
      SUBROUTINE PNTMOD
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
C*  CANCEL PREVIOUS MODES OUTPUT (US)
      CALL TOUTPT(31)
      DO 111 II=1,4
111   KPCHAR(II)=-1
C*  OUTPUT(FS) SET POINT PLOT MODE
      KKMODE=2
      CALL TOUTPT(28)
      RETURN
      END
C ROUTINE---DA-2320.05-01 4002A IPMOD
      SUBROUTINE IPMOD
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
C*  CANCEL PREVIOUS MODE OUTPUT (US)
      CALL  TOUTPT(31)
      KKMODE=3
      KGRAFL=0
C*  OUTPUT INCRMENTAL PLOT MODE CHAR. (RS)
      CALL TOUTPT(30)
      RETURN
      END
C ROUTINE---DA-2340.05-01 4002A ENSPM
      SUBROUTINE ENSPM
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
C*  ENTER SCRATCHPAD MODE - OUTPUT 'ESC' 'US'
      CALL TOUTPT(27)
      CALL TOUTPT(31)
      KKMODE=0
      KSIZEF=0
      KITALC=0
      RETURN
      END
C ROUTINE---DA-2350.05-01 4002A CLRSP
      SUBROUTINE CLRSP
C*  EXIT FROM SCRATCHPAD   OUTPUT  'ESC'-'C'
      CALL TOUTPT(27)
      CALL TOUTPT(67)
      RETURN
      END
C ROUTINE---DA-2360.05-01 4002A ENCLM
      SUBROUTINE ENLCM
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
C*  EXIT SCRATCHPAD MODE  OUTPUT 'EOT'
      CALL TOUTPT(4)
      KBEAMX=0
      KGRAFL=-1
      RETURN
      END
C ROUTINE---DA-2370.05-01 4002A EDITSP
      SUBROUTINE EDITSP
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
C*  ENTER SCRATCHPAD EDIT MODE  OUTPUT 'ESC' '?'
      CALL TOUTPT(27)
      CALL TOUTPT(63)
      KGRAFL=-1
      RETURN
      END
C ROUTINE---DA-0380.05-01   MOVABS
      SUBROUTINE MOVABS(IX,IY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      CALL VECMOD
      CALL XYCNVT(IX,IY)
      KGRAFL=0
      RETURN
      END
C ROUTINE---DA-0390.05-01   IOWAIT
      SUBROUTINE IOWAIT(ITIME)
C*  THIS ROUTINE IS USED TO GENERATE DELAYS FOR REMOTE TERMINALS
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KBAUDR.LE.0)GO TO 20
      KOUNT=ITIME*KBAUDR/10
      DO 10 J=1,KOUNT
C*  OUTPUT (SYN) TO INSURE AGAINST LOSS OF OUTPUT WHILE
C*  TERMINAL IS BUSY. (SYN) DOES NOT AFFECT THE TERMINAL.
10    CALL TOUTPT(22)
20    RETURN
      END
C ROUTINE---DA-0410.05-01   VECMOD
      SUBROUTINE VECMOD
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IF(KKMODE.EQ.1)GO TO 10
C*  OUTPUT (US) TO ENTER A/N MODE AND RESET FOR VECTOR MODE
      CALL TOUTPT(31)
      DO 112 II=1,4
112   KPCHAR(II)=-1
      KKMODE=1
C*  OUTPUT (GS) TO ENTER VECTOR MODE
10    CALL TOUTPT(29)
      KMOVEF=1
      RETURN
      END
C ROUTINE---DA-0420.05-01   SVSTAT
      SUBROUTINE SVSTAT(RARRAY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      DIMENSION RARRAY(55),IB(44),RB(11)
      EQUIVALENCE (IB(1),KBAUDR),(RB(1),TMINVX)
      DO 100 I=1,44
100   RARRAY(I)=IB(I)
      DO 101 I=1,11
101   RARRAY(I+44)=RB(I)
      RETURN
      END
C ROUTINE---DA-0430.05-01   CSIZE
      SUBROUTINE CSIZE(IHORZ,IVERT)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      IHORZ=KHORSZ
      IVERT=KVERSZ
      RETURN
      END
C ROUTINE---BG-0440.05-01   RSCALE
      SUBROUTINE RSCALE(SCALE)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
       TRSCAL=SCALE
       RETURN
       END
C ROUTINE---BG-0450.05-01   RROTAT
      SUBROUTINE RROTAT(ANGLE)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
       A=0.0174*ANGLE
       TRCOSF=COS(A)
       TRSINF=SIN(A)
       RETURN
       END
C ROUTINE---DA-2390.05-01 4002A XYCNVT
      SUBROUTINE XYCNVT(IX,IY)
      COMMON /TKTRNX/ KBAUDR, KERROR, KGRAFL, KHOMEY, KKMODE,
     1 KHORSZ, KVERSZ, KITALC, KSIZEF, KLMRGN, KRMRGN,
     2 KTBLSZ, KHORZT(10), KVERTT(10),
     3 KBEAMX, KBEAMY, KMOVEF, KPCHAR(4),KDASHT,
     4 KMINSX, KMINSY, KMAXSX, KMAXSY, TMINVX, TMINVY, TMAXVX, TMAXVY,
     5 TREALX, TREALY, TIMAGX, TIMAGY, TRCOSF, TRSINF, TRSCAL
      KBEAMY=IY
      KBEAMX=IX
      LOX=MOD(IX,32)+64
      IHOX=MOD(IX/32,32)+32
      LOY=MOD(IY,32)+96
      IHOY=MOD(IY/32,32)+32
C*  THE ORDER IN WHICH THE PLOT CHARACTERS ARE
C*  OUTPUT MUST REFLECT THE HARDWARE STRAPPING
      IF(IHOY.NE.KPCHAR(1))GO TO 10
      IF(LOY.NE.KPCHAR(2))GO TO 20
      IF(IHOX.EQ.KPCHAR(3))GO TO 41
      GO TO 30
10    KPCHAR(1)=IHOY
      CALL TOUTPT(IHOY)
20    KPCHAR(2)=LOY
30    CALL TOUTPT(LOY)
      IF(IHOX.EQ.KPCHAR(3))GO TO 40
      KPCHAR(3)=IHOX
      CALL TOUTPT(IHOX)
      GO TO 40
41    IF(LOX.NE.KPCHAR(4))GO TO 40
      IF(KKMODE.EQ.2)GO TO 100
      IF(KMOVEF.EQ.1)GO TO 42
      IF(IFLAG.EQ.0)GO TO 42
      GO TO 100
40    KPCHAR(4)=LOX
42    CALL TOUTPT(LOX)
      IF(KMOVEF.EQ.1)GO TO 50
      IFLAG=1
      GO TO 100
50    IFLAG=0
      KMOVEF=0
100   RETURN
      END
C*
C*
    xf