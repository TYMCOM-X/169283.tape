!***COPYRIGHT 1972, DIGITAL EQUIPEMENT CORP., MAYNARD, MASS.***
!AUTHOR: F.J. INFANTE
GLOBAL BIND CLASV=41;	!5-APR-73
	SWITCHES LIST;
	REGISTER T1=1,T2=2;
	BIND E1=1,E2=2,E3=3,E4=4,E5=5,E6=6,E7=7,E8=8,E9=9,E10=10,E11=11,E20=20;
	EXTERNAL ONEPLIT;
	EXTERNAL POOL[POOLSIZ],GLOBEND,PIN,POUT,COLUMN,PAGE,LINECOUNT,STANUM,STALABL;
	EXTERNAL Q,QON,QOFF,QUEUE,DEQUEUE,LSTOUT,RELOUT,READ,ERROUT,PROGNAME,TBLSEARCH;
	FORWARD
		UNSAVE,
		SAVE,
		INPUT,
		PAGEHEADER,
		CHAR,
		NEWCARD,
		RD,
		CLASSIFIER,
		HASH;
	BIND
		CR=#015,	% CARRIAGE RETURN %
		LF=#012,	% LINE FEED %
		FF=#014,	% FORM FEED %
		EOL=#777777,	% END-OF-LINE %
		EOF=3,		% END-OF-FILE %
		LINEND=1;
GLOBAL ROUTINE CREF=
BEGIN
%
GENERATE CREF OUTPUT SYMBOLS IN LST FILE, CREFLINK CONTAINS PTR TO LINKED
LIST OF PTRS TO SYMBOLS IN CURRENT SOURCE
%
EXTERNAL CREFLINK,LSTOUT;
MAP BASE CREFLINK:VREG;
LOCAL CCHAR[6];
!START CREF INPUT
!
	C _ #177; LSTOUT(); C_ "B"; LSTOUT();
!
!NOW CREF SYMBOLS
!
	CREFLINK _ .CREFLINK<LEFT>;
	DO BEGIN
		VREG _ .CREFLINK[CW0L];
		T2 _ .VREG[IDSYMBOL];
		DECR I FROM 5 TO 0 DO
		(T1 _ 0; LSHC(T1,6); T1 _ .T1+#40;   CCHAR[.I] _ .T1;
				!GET  1 CHAR AND CONVERT TO ASCII
		  IF .T2 EQL 0
		  THEN(
			C _ 1; LSTOUT();  !SYMBOL DEF FOR CREF
			!NOW NUM OF CHARACTERS
			C_6; C _ .C-.I; LSTOUT();
			EXITLOOP
		      );
		);
		DECR I FROM 5 TO 6-.C DO
			  (C _ .CCHAR[.I]; LSTOUT());
	   END WHILE (CREFLINK _ .CREFLINK[CLINK]) NEQ 0;
	!NOW THE END SYMBOL FOR CREF
	C _ #177; LSTOUT(); C _ "C"; LSTOUT();
END;
GLOBAL ROUTINE UNSAVE =
BEGIN
	C_IF .POUT GEQ .PIN THEN -1 ELSE .POOL[POUT_.POUT+1];
	.VREG
END;
ROUTINE SAVE =
%(-----------------------------------------------------------------------------------------------------------------
	SAVES A CHARACTER FOR FUTURE REFERENCE ON MAJOR QUEUE
	INITIALIZE PIN = POUT = POINTER TO FIRST WORD OF CHAIN
	EQUIVALENCE POOL AND POOLPOINTER TO THE SHARED POOL
-----------------------------------------------------------------------------------------------------------------)%
BEGIN
	IF .PIN LEQ .POUT THEN PIN_POUT_-1;
	POOL[PIN_.PIN+1]_.C;
	.VREG
END;
GLOBAL ROUTINE INPUT =
BEGIN
	IF .FLGREG<ENDFILE> THEN C_EOL
		ELSE
		BEGIN
			READ();
			IF .C EQL #32 %^Z% THEN (FLGREG<ENDFILE> _ 1; RETURN C _ EOL);
			IF .FLGREG<LISTING> THEN LSTOUT()
		END
END;
GLOBAL ROUTINE PAGEHEADER =
BEGIN
	MACHOP CALLI=#047,ROTC=#245,MOVEI=#201,HLLZ=#510;
	MACHOP ROT=#241,HLRZ=#554,HRLZ=#514;
	BIND JOBVER=#137;
	ROUTINE DIGITS(BASE)=
	BEGIN
		LOCAL N;
		N_.T2 MOD @BASE;
		IF (T2_.T2/@BASE) NEQ 0 THEN DIGITS(@BASE);
		C_@N+"0";LSTOUT();
		.VREG
	END;
	ROUTINE STRING6=
	BEGIN
		DO (MOVEI(C,0);ROTC(T2,6);MOVEI(C," ",C);LSTOUT()) UNTIL .T2 EQL 0;
		.VREG
	END;
	ROUTINE STRING7=
	BEGIN
		DO (MOVEI(C,0);ROTC(T2,7);LSTOUT()) UNTIL .T2 EQL 0;
		.VREG
	END;
	C_FF;LSTOUT();
!------------------------------------------------------------------------------------------------------------------
!	PROGRAM NAME
!------------------------------------------------------------------------------------------------------------------
	T2_.PROGNAME;STRING6();
!------------------------------------------------------------------------------------------------------------------
!	FILENAME - NOT NECESSARY
!------------------------------------------------------------------------------------------------------------------
	C_"	";LSTOUT();
	IF (T2_@FILENAME(SRC)) NEQ 0 THEN
	BEGIN
		STRING6();
		IF HLLZ(T2,EXTENSION(SRC)) NEQ 0 THEN
		BEGIN
			C_".";LSTOUT();
			STRING6();
		END
	END;
	C _ "	"; LSTOUT();
	T2_'FORTR';STRING7();T2_'AN V.';STRING7();
!------------------------------------------------------------------------------------------------------------------
!	VERSION - DOESN'T CHANGE
!------------------------------------------------------------------------------------------------------------------
	T1_@JOBVER;T2_0;
	ROT(T1,3);ROTC(T1,9);DIGITS(8);T2_0;
	ROTC(T1,6);
	IF .T2 NEQ 0 THEN (MOVEI(C,"@",T2);LSTOUT());
	IF HLRZ(T2,T1) NEQ 0 THEN (C_"(";LSTOUT();DIGITS(8);C_")";LSTOUT());
	HRLZ(T1,T1);ROTC(T1,3);
	IF .T2 NEQ 0 THEN (C_"-";LSTOUT();DIGITS(8));
	!
	!SET IN KA OR KI VERSION AND /OPT IF OPTIMIZED
	!
	IF .FLGREG<KA10> THEN T2 _ ' /KA' ELSE T2 _ ' /KI';
	STRING7();
	IF .FLGREG<OPTIMIZE> THEN (T2 _ '/OPT'; STRING7());
!------------------------------------------------------------------------------------------------------------------
!	DATE - DOESN'T CHANGE
!------------------------------------------------------------------------------------------------------------------
	C_"	";LSTOUT();
	T2_(CALLI(T1,#14) MOD 31)+1;DIGITS(10);
	T2_@(PLIT('-JAN-','-FEB-','-MAR-','-APR-','-MAY-','-JUN-',
		  '-JUL-','-AUG-','-SEP-','-OCT-','-NOV-','-DEC-')
		+(T1_.T1/31) MOD 12);
	STRING7();
	T2_.T1/12 +64;DIGITS(10);C_"	";LSTOUT();
!------------------------------------------------------------------------------------------------------------------
!	TIME - DOESN'T CHANGE
!------------------------------------------------------------------------------------------------------------------
	T2_CALLI(T1,#23)/3600000;DIGITS(10);C_":";LSTOUT();
	T2_(T1_.T1 MOD 3600000)/60000;DIGITS(10);
!------------------------------------------------------------------------------------------------------------------
!	PAGE
!------------------------------------------------------------------------------------------------------------------
	T2_'	PAGE';STRING7();C_" ";LSTOUT();
	T2_.PAGE<18,18>;DIGITS(10);
	IF (T2_.PAGE<0,18>) NEQ 0 THEN (C_"-";LSTOUT();DIGITS(10));
	PAGE_.PAGE+1;LINECOUNT_53;
	T2_'?M?J';STRING7();
	T2_'?M?J?M?J';STRING7();
	.VREG
END;
GLOBAL ROUTINE CHAR =
%(-----------------------------------------------------------------------------------------------------------------
	RETURNS NEXT CHARACTER OF STATEMENT FIELD
	AS SIDE EFFECTS CALCULATES LABEL,STRIPS SEQUENCE
	NUMBERS, AND BRINGS IN NEW CARDS AS NEEDED.
	THE CHARACTER IS ALSO COPIED INTO THE GLOBAL C
-----------------------------------------------------------------------------------------------------------------)%
BEGIN
EXTERNAL CREFLINK,CREF;
	IF  .QON GTR .QOFF THEN C_@Q[QOFF_@QOFF+1]	!A PREVIEWED CHARACTER WAITING IN MINOR QUEUE
		ELSE IF .PIN GTR .POUT THEN C_@POOL[POUT_@POUT+1] ! FROM MAJOR QUEUE
			ELSE
			BEGIN
				!NORMAL CASE
				MACHOP MOVEI=#201,IORM=#436;
				IF .FLGREG<ENDFILE> THEN RETURN C_EOL
				ELSE 
				BEGIN
					READ();
					IF .C EQL CR
					THEN BEGIN
						IF .FLGREG<LISTING>
						THEN BEGIN
							IF .CREFLINK NEQ 0
							THEN (CREF(); C _ CR);
							LSTOUT()
						     END;
						RETURN NEWCARD();
					      END;
					IF .FLGREG<LISTING> THEN LSTOUT();
					IF .C EQL "	" THEN (MOVEI(VREG,7);IORM(VREG,COLUMN));
					IF .C EQL LF THEN NEWCARD()
					   ELSE
						IF (COLUMN_.COLUMN+1) GTR -8
						!PAST END OF STATEMENT FIELD
						THEN(IF .CREFLINK NEQ 0 THEN CREF();
							NEWCARD()
						     );
				END
			END;
	RETURN .C
END;
!******************************************************************************************************************
!
ROUTINE ERRLST(EPTR,ENODE)=
BEGIN	!OUTPUTS MSG POINTED TO BY EPTR

	REGISTER T3=3;
	EXTERNAL LSTOUT;
	LOCAL N,VALUE;
	OWN TTYMSG[20]; BIND TTYPTR= TTYMSG;
	TTYPTR _ (TTYMSG+1)<36,7>;
	N _ 0;
	T1_(PLIT' ----> ?0')<36,7>;
	UNTIL (C_SCANI(T1)) EQL 0 DO (IF .FLGREG<LISTING> THEN LSTOUT(); REPLACEI(TTYPTR,.C));
	WHILE 1 DO
		IF (C_SCANI(EPTR)) LSS " " THEN CASE .C OF SET
	%0:%	EXITLOOP;	!UNDEFINED CHARACTERS IN LAST WORD OF PLIT ARE NULLS
	%"?A":%	BEGIN	!INSERT THE RIGHTMOST FIVE DECIMAL DIGITS SUPPLIED BY THE NEXT PARAMETER
			T1_.(.ENODE<RIGHT>)[N_.N+1];T2_T3_0;
			DECR J FROM 4 TO 0 DO
			BEGIN
				MACHOP IDIVI=#231,ADDI=#271,LSHC=#246;
				IDIVI(T1,10);	!REMAINDER IN T2
				ADDI(T2,"0");	!ASCII-DIGIT_REMAINDER+"0"
				LSHC(T2,-7);	!T3<29,7>_ASCII-DIGIT
			END;
			VALUE _ .T3;	!T3 (C) GETS CLOBBERED BY SCANI BELOW
			T1_VALUE<36,7>;
			DECR J FROM 4 TO 0 DO (C _ SCANI(T1); IF .FLGREG<LISTING> THEN LSTOUT(); REPLACEI(TTYPTR,.C));
		END;
	%"?B":%	BEGIN	!INSERT THE ASCII STRING POINTED TO BY THE NEXT PARAMETER
			T1_(.(.ENODE<RIGHT>)[N_.N+1])<36,7>;
			UNTIL(C_SCANI(T1)) EQL 0 DO ( IF .FLGREG<LISTING> THEN LSTOUT(); REPLACEI(TTYPTR,.C));		END;
	%"?C":%	BEGIN	!INSERT THE SIXBIT NAME POINTED TO BY THE NEXT PARAMETER
			T1_(.(.ENODE<RIGHT>)[N_.N+1])<36,6>;
			DECR X FROM 5 TO 0 DO (C _ SCANI(T1)+" "; IF .FLGREG<LISTING> THEN  LSTOUT(); REPLACEI(TTYPTR,.C))
		END
		TES ELSE (IF .FLGREG<LISTING> THEN LSTOUT(); REPLACEI(TTYPTR,.C));
	C_#15; %CR%  IF .FLGREG<LISTING> THEN LSTOUT(); REPLACEI(TTYPTR,.C);
	C_#12; %LF%  IF .FLGREG<LISTING> THEN LSTOUT(); REPLACEI(TTYPTR,.C);
	C_0; REPLACEI(TTYPTR,.C);
	IF NOT .FLGREG<NOERRORS>
	  THEN IF NOT .FLGREG<TTYDEV> THEN OUTSTR( (TTYMSG+1) );
END; !OF ERRLST
GLOBAL ROUTINE NEWCARD = 
BEGIN
	ROUTINE SEQUENCE =
	BEGIN
		IF .FLGREG<ENDFILE> THEN RETURN .VREG ELSE
		IF @@BUFPNT(SRC) THEN	!IF SEQUENCE NUMBER (BIT35=1) THEN
		BEGIN
			MACHOP IMULI=#221,ADDI=#271;
			T2_0;LSTOUT();
			DECR N FROM 4 TO 0 DO	!IGNORE 5 DIGITS
			BEGIN
				IMULI(T2,10);ADDI(T2,-"0",C);
				INPUT()
			END;
			INPUT();	!IGNORE TRAILING TAB
			STANUM_.T2	!STATEMENT NUMBER _ SEQUENCE NUMBER
		END
		ELSE
		BEGIN
			REGISTER CX;
			MACHOP ROT=#241,AOS=#350;
			AOS(T1,STANUM);
			IF .FLGREG<LISTING> THEN IF NOT .FLGREG<CROSSREF> THEN
			BEGIN
				CX_.C;
				DECR T FROM 4 TO 0 DO
				BEGIN
					MACHOP IDIVI=#231,MOVEI=#201,ROTC=#245;
					IDIVI(T1,10);
					MOVEI(T2,"0",T2);
					ROTC(T2,-7)
				END;
				DECR T FROM 4 TO 0 DO (ROT(C,7);LSTOUT());
				C_"	";LSTOUT();C_.CX;LSTOUT()
			END;
		END;
		.VREG
	END;
	ROUTINE NEWPAGE =
	BEGIN
		REGISTER CX;
		MACHOP MOVEI=#201,CAIE=#302;
		LOCAL T2SAV,PTR;
		EXTERNAL ERRLINK,SAVSPACE,LSTOUT;
		DO CX_READ() WHILE .CX EQL 0;
		IF .CX EQL #32 %^Z% THEN (FLGREG<ENDFILE>_1; CX _ EOL);
		IF .FLGREG<LISTING> THEN
			IF .C EQL FF THEN (PAGEHEADER();DO CX_READ() WHILE .CX EQL 0)
				ELSE IF (LINECOUNT_.LINECOUNT-1) EQL 0 THEN PAGEHEADER();
		UNTIL (PTR_.ERRLINK<RIGHT>) EQL 0 DO
		BEGIN
			T2_ (.(.PTR)<LEFT>)<36,7>;
			ERRLINK_@@ERRLINK;
				IF (LINECOUNT_.LINECOUNT-1) EQL 0 THEN PAGEHEADER();
				ERRLST(.T2,.PTR);
			SAVSPACE(3,.PTR<RIGHT>)
		END;
		C_.CX;
		.VREG
	END;
	ROUTINE IGNORE =
	BEGIN
		UNTIL .C EQL LF DO (INPUT(); IF .C EQL EOL THEN RETURN);

		NEWPAGE();SEQUENCE();
		VREG_-1
	END;
	EXTERNAL CSAVE,LINES;
	STALABL_0;
%(-----------------------------------------------------------------------------------------------------------------
	IF COLUMN NEQ 0 THEN EAT THRU CARD SEQUENCE NUMBER
	ELSE START AT FIRST CHARACTER
-----------------------------------------------------------------------------------------------------------------)%
	IF .COLUMN LEQ 0 THEN
	BEGIN
		UNTIL .C EQL LF DO (INPUT(); IF .C EQL EOL THEN RETURN);
	END;
	WHILE .C EQL #14 %FORMFEED% DO INPUT();	!IGNORE ALL FF
	IF .FLGREG<EOP> THEN RETURN(C_EOL);
	NEWPAGE();SEQUENCE();
	!WHILE COMMENT CHARACTER IN COLUMN 1
	WHILE 1 DO
	BEGIN
		VREG_0;
		IF .C EQL " " THEN EXITLOOP
			ELSE IF .C EQL "	" THEN EXITLOOP;  !TAB
	 	IF .C EQL "C" THEN IGNORE()
			ELSE IF .C EQL "D" THEN (IF .FLGREG<INCLUDE> THEN (C_" ";EXITLOOP) ELSE IGNORE())
			  ELSE IF .C EQL "$" THEN IGNORE()
			    ELSE IF .C EQL "*" THEN IGNORE()
			     ELSE IF .C EQL "/" THEN IGNORE()
				ELSE IF .C EQL CR %CR% THEN IGNORE() ELSE EXITLOOP;
		.VREG
	END;
	DECR COL FROM 4 TO 0 DO
	BEGIN
		MACHOP MOVE=#200,IMULI=#221,ADDI=#271,MOVEM=#202;
		IF .FLGREG<ENDFILE> THEN RETURN C_EOL;
		IF .C EQL "	" THEN
		BEGIN
			INPUT();
			IF DIGIT(C) THEN (INPUT();COLUMN_-80+7;CSAVE_0)
				ELSE (CSAVE_.C;COLUMN_-80+6;C_EOL);
			RETURN .C
		END;
		IF DIGIT(C) THEN
			(T1 _ .STALABL; T1 _.T1*10; T1 _ .T1-#60+.C; STALABL _ .T1)
			ELSE IF .C NEQ " " THEN
				BEGIN
					ENTRY[0]_@ISN;
					IF .C LSS " " THEN ENTRY[1]_.C^22+'^@'+ENTRY[1]<0,0>
						ELSE ENTRY[1]_.C^29+ENTRY[1]<0,0>;
					ERROUT(E7)
				END;
		INPUT();
		IF .C EQL CR THEN (DO (INPUT(); IF .C EQL EOL THEN RETURN)
					UNTIL .C EQL LF;
				   CSAVE _ 0;
				   RETURN (C _ EOL)
				  );
	END;
	CSAVE_0;
	IF .C NEQ "0" THEN
		IF .C NEQ " " THEN
			IF .C NEQ "	" THEN (INPUT();COLUMN_-80+7;RETURN .C);
	COLUMN_-80+6;C_EOL;
	RETURN .C
END;
!******************************************************************************************************************
GLOBAL ROUTINE RD =
%(-----------------------------------------------------------------------------------------------------------------
	CALL NEWCARD TO INITIALIZE. HANDLES CONTINUATION CARDS, SAVES ALL
	CHARACTERS READ, ALWAYS RETURNS THE CHARACTER IN REGISTER C,
	IGNORES BLANK INPUT WORDS.
-----------------------------------------------------------------------------------------------------------------)%
BEGIN
	EXTERNAL CREFLINK;
	MACHOP MOVEI=#201,IORM=#436;
	LABEL RD1;
	RD1: IF .FLGREG<ENDFILE> THEN RETURN C_EOL
	ELSE
	BEGIN
		READ();
		IF .C EQL #32 %^Z% THEN (FLGREG<ENDFILE> _ 1; IF .CREFLINK NEQ 0 THEN CREF(); RETURN C _ EOL);
		IF .C EQL CR
		THEN BEGIN
			IF .FLGREG<LISTING>
			THEN BEGIN
				IF .CREFLINK NEQ 0
				THEN (CREF(); C _ CR);
				LSTOUT()
			     END;
			NEWCARD();
			LEAVE RD1
		      END;
		IF .FLGREG<LISTING> THEN LSTOUT();
		IF .C EQL "	" THEN (MOVEI(VREG,7);IORM(VREG,COLUMN));
		!INCREMENT COLUMN TO NEXT TAB COLUMN
		IF .C EQL LF THEN NEWCARD()
			   ELSE
				IF (COLUMN_.COLUMN+1) GTR -8
				THEN( IF .CREFLINK NEQ 0 THEN CREF();
					NEWCARD();
				    );
	END;
	SAVE()
END;
!******************************************************************************************************************
EXTERNAL ARITHIF,STATEFUNC,DOLOOP,ASSIGNMENT;
GLOBAL ROUTINE CLASSIFIER =
%(-----------------------------------------------------------------------------------------------------------------
	RETURNS NAME (ADDRESS) OF PROPER STATEMENT ROUTINE
-----------------------------------------------------------------------------------------------------------------)%
BEGIN
	MACRO IGNOREBLANKS =
	BEGIN
		DO RD() WHILE .C LEQ " ";
		.C
	END$;
	ROUTINE NAMESCAN =
	BEGIN
		DO DO DO IGNOREBLANKS WHILE ALPHA(C) WHILE DIGIT(C) WHILE LCALPHA(C);
		.VREG
	END;
	REGISTER R1;
	ROUTINE CONST =
	BEGIN
		MACHOP IMULI=#221,ADDI=#271;
		DO (IMULI(R1,10);ADDI(R1,0,VREG);IGNOREBLANKS) WHILE DIGIT(C);
		.VREG
	END;
	LOCAL PARENLEVEL,ZLE;	!ZLE MEANS ZERO LEVEL EQUALS
	LABEL CLAS1;
	EXTERNAL CSAVE,ERROUT;
	PARENLEVEL_ZLE_0;
	IF .CSAVE EQL 0 THEN IGNOREBLANKS  ELSE (C_.CSAVE;CSAVE_0;SAVE());
	IF .C LEQ #40 THEN IGNOREBLANKS; !IF NON PRINTING CHAR MOST LIKELY A TAB OR BLANK
CLAS1:	DO
	BEGIN
		IF .C EQL -1 THEN
			IF .ZLE NEQ 0 THEN RETURN ASSIGNMENT ELSE RETURN HASH();
		WHILE DIGIT(C) DO
		BEGIN
			R1_0;CONST();
			IF .C EQL "H" THEN DECR I FROM .R1 TO 0 DO RD()
				ELSE IF .C EQL "X" THEN RD() ELSE EXITLOOP;
		END;
		IF ALPHA(C) THEN NAMESCAN();
		IF LCALPHA(C) THEN NAMESCAN();
		IF .C EQL "'" THEN DO(DO ( RD(); IF .C EQL EOL THEN LEAVE CLAS1;) UNTIL .C EQL "'")UNTIL( RD(); .C)  NEQ "'";
		IF .C EQL "[" THEN PARENLEVEL_@PARENLEVEL+1;
		IF .C EQL "]" THEN PARENLEVEL_@PARENLEVEL-1;
		IF .C EQL ")" THEN IF (PARENLEVEL_.PARENLEVEL-1) EQL 0 THEN
		    BEGIN
			IGNOREBLANKS;
			IF DIGIT(C) THEN RETURN ARITHIF;
			IF .C EQL "[" THEN RETURN STATEFUNC;
			IF .C EQL "=" THEN
				IF .ZLE NEQ 0 THEN RETURN ASSIGNMENT
					ELSE RETURN STATEFUNC;
			IF .ZLE EQL 0 THEN RETURN HASH()
		    END;
		IF .C EQL "(" THEN PARENLEVEL_.PARENLEVEL+1;
		IF .PARENLEVEL EQL 0 THEN
			IF .C EQL "=" THEN ZLE_1
				ELSE IF .C EQL "[" THEN RETURN ASSIGNMENT
					ELSE IF .ZLE EQL 0 THEN RETURN HASH()
						ELSE IF .C EQL "," THEN RETURN DOLOOP;
		IF .PARENLEVEL LSS 0 THEN RETURN ERROUT(E9);	!UNMATCHED RIGHT PAREN
		IF .C EQL EOL THEN EXITLOOP;
		IF .C EQL ";" THEN EXITLOOP; !THE OTHER END OF LINE
		IF .PIN GEQ POOLSIZ-100 THEN LEAVE CLAS1; !RUN OUT OF POOL TRY TO CLASSIFY ANYWAY
		IGNOREBLANKS;
	END
	UNTIL .C EQL EOL;
	IF .ZLE NEQ 0 THEN ASSIGNMENT ELSE HASH()
END;
EXTERNAL
!******************************************************************************************************************
!
!THE NUMBER IN COMMENTS IS THE STATEMENTS LOCATION IN THE HASH TABLE
!
%  1%	PUNCSTA,
%  3%	DATASTA,
%  4%	PROTSTA,
%  8%	PRINSTA,
% 10%	OUTPSTA,
% 16%	SUBRSTA,
% 17%	SUBSSTA,
% 18%	OPENSTA,
% 19%	INTESTA,
% 29%	LOGISTA,
% 30%	IMPLSTA,
% 32%	GLOBSTA,
% 34%	FINDSTA,
% 38%	CALLSTA,
% 39%	REWISTA,
% 43%	INPUSTA,
% 45%	RERESTA,
% 49%	GOTOSTA,
% 51%	DIMESTA,
% 53%	PAUSSTA,
% 54%	LOGICALIF,
% 56%	DOUBSTA,
% 57%	RETUSTA,
% 58%	ENDSTA,
% 59%	FORMSTA,
% 60%	INCLSTA,
% 63%	BKSPST,
% 64%	ENTRSTA,
% 65%	EQUISTA,
% 67%	DECOSTA,
% 71%	NAMESTA,
% 73%	ACCESTA,
% 75%	BLOCSTA,
% 78%	READSTA,
% 79%	UNLOSTA,
% 81%	FUNCSTA,
% 83%	CLOSSTA,
% 84%	ENDFSTA,
% 86%	REALSTA,
% 88%	SKIPSTA,
% 90%	WRITSTA,
% 91%	EXTESTA,
% 93%	COMMSTA,
% 95%	ENCOSTA,
% 96%	COMPSTA,
% 98%	CONTSTA,
%109%	ASSISTA,
%113%	TYPESTA,
%114%	STOPSTA,
%119%	BYTESTA,
%121%	PROGSTA;
GLOBAL ROUTINE HASH =
!------------------------------------------------------------------------------------------------------------------
!DEVELOPS HASH CODE FOR STATEMENT IDENTIFICATION IN CLASSIFIER.
!CALLED BY ROUTINE CLASSIFIER- NO ARGUMENTS. RETURNS THE NAME OF THE STATEMENT ROUTINE
!FOR THE STATEMENT CURRENTLY BEING PARSED.
!
!THE FOLLOWING IS THE TABLE OF UNIQUE FIRST LETTERS FOR ALL THE
!STATEMENTS IN THE FORTRAN LANGUAGE, FOLLOWED BY THE CORRESPONDING STATEMENT ROUTINE.
!THE FIRST 4 LETTERS OF EACH STATEMENT ARE RIGHT JUSTIFIED WITH THE REMAINDER OF THE WORD SET TO ZERO.
!------------------------------------------------------------------------------------------------------------------
BEGIN
!THIS HASH TABLE WAS CREATED BY THE FORTRAN PROGRAM HASHGEN.F4.
MACRO	STEP=( -2)$;
BIND
VECTOR	CLASLIST=PLIT(
%  0%	0,
%  1%	PLIT("PUNC",PUNCSTA),
%  2%	0,
%  3%	PLIT("DATA",DATASTA),
%  4%	PLIT("PROT",PROTSTA),
%  5%	0,
%  6%	0,
%  7%	0,
%  8%	0,	!PLIT("PRIN",PRINSTA),	!CONFLICTS WITH ENTRY OUTP
%  9%	0,
% 10%	PLIT("PRIN",PRINSTA),	!PLIT(",OUTPSTA),
% 11%	0,
% 12%	0,
% 13%	0,
% 14%	0,
% 15%	0,
% 16%	PLIT("SUBR",SUBRSTA),
% 17%	PLIT("SUBS",SUBSSTA),
% 18%	PLIT("OPEN",OPENSTA),
% 19%	PLIT("INTE",INTESTA),
% 20%	0,
% 21%	0,
% 22%	0,
% 23%	0,
% 24%	0,
% 25%	0,
% 26%	0,
% 27%	0,
% 28%	0,
% 29%	PLIT("LOGI",LOGISTA),
% 30%	PLIT("IMPL",IMPLSTA),
% 31%	0,
% 32%	PLIT("GLOB",GLOBSTA),	!CONFLICTS WITH ENTRY FIND
% 33%	0,
% 34%	PLIT("FIND",FINDSTA),
% 35%	0,
% 36%	0,
% 37%	0,
% 38%	PLIT("CALL",CALLSTA),
% 39%	PLIT("REWI",REWISTA),
% 40%	0,
% 41%	0,
% 42%	0,
% 43%	PLIT("INPU",INPUSTA),
% 44%	0,
% 45%	PLIT("RERE",RERESTA),
% 46%	0,
% 47%	0,
% 48%	0,
% 49%	PLIT("GOTO",GOTOSTA),
% 50%	0,
% 51%	PLIT("DIME",DIMESTA),	!CONFLICTS WITH ENTRY PAUS
% 52%	0,
% 53%	PLIT("PAUS",PAUSSTA),
% 54%	PLIT("IF",LOGICALIF),
% 55%	0,
% 56%	PLIT("DOUB",DOUBSTA),	!CONFLICTS WITH ENTRY END
% 57%	PLIT("RETU",RETUSTA),
% 58%	PLIT("END",ENDSTA),
% 59%	PLIT("FORM",FORMSTA),
% 60%	PLIT("INCL",INCLSTA),
% 61%	0,
% 62%	0,
% 63%	PLIT("BACK",BKSPST),
% 64%	PLIT("ENTR",ENTRSTA),
% 65%	PLIT("EQUI",EQUISTA),
% 66%	0,
% 67%	PLIT("DECO",DECOSTA),
% 68%	0,
% 69%	0,
% 70%	0,
% 71%	PLIT("NAME",NAMESTA),
% 72%	0,
% 73%	PLIT("ACCE",ACCESTA),
% 74%	0,
% 75%	PLIT("BLOC",BLOCSTA),
% 76%	0,
% 77%	0,
% 78%	PLIT("READ",READSTA),
% 79%	PLIT("UNLO",UNLOSTA),
% 80%	0,
% 81%	PLIT("FUNC",FUNCSTA),
% 82%	0,
% 83%	PLIT("CLOS",CLOSSTA),
% 84%	PLIT("ENDF",ENDFSTA),
% 85%	0,
% 86%	PLIT("REAL",REALSTA),
% 87%	0,
% 88%	PLIT("SKIP",SKIPSTA),	!CONFLICTS WITH ENTRY WRIT
% 89%	0,
% 90%	PLIT("WRIT",WRITSTA),
% 91%	PLIT("EXTE",EXTESTA),
% 92%	0,
% 93%	PLIT("COMM",COMMSTA),
% 94%	0,
% 95%	PLIT("ENCO",ENCOSTA),
% 96%	PLIT("COMP",COMPSTA),
% 97%	0,
% 98%	PLIT("CONT",CONTSTA),
% 99%	0,
%100%	0,
%101%	0,
%102%	0,
%103%	0,
%104%	0,
%105%	0,
%106%	0,
%107%	0,
%108%	0,
%109%	PLIT("ASSI",ASSISTA),
%110%	0,
%111%	0,
%112%	0,
%113%	PLIT("TYPE",TYPESTA),
%114%	PLIT("STOP",STOPSTA),
%115%	0,
%116%	0,
%117%	0,
%118%	0,
%119%	PLIT("BYTE",BYTESTA),
%120%	0,
%121%	PLIT("PROG",PROGSTA),
%122%	0,
%123%	0,
%124%	0,
%125%	0,
%126%	0,
%127%	0,
%128%	0,
%129%	0,
	0);
	MACHOP LSH=#242,ADDI=#271;
	REGISTER R1,R2;
	R1 _ 0;
	!--------------------------------------------------------------------------------------------------------
	!IF THE CLASSIFIER READ IN AN ENTIRE LINE THEN THE FIRST CHARACTER
	!OF THE NEXT LINE IS ON THE MINOR QUEUE WHERE IT WILL CAUSE TROUBLE
	!----------------------------------------------------------------------------------------------------------
	DECR I FROM 3 TO 0 DO
	BEGIN
		SIGCHAR;
		IF NOT ALPHA(C) THEN IF NOT LCALPHA(C) THEN EXITLOOP
					ELSE C _.C -#40; !CONVERT TO UPPER CASE
		LSH(R1,7); ADDI(R1,0,C);
	END;
	R2 _ .R1 MOD 130;
	IF .R1 EQL @@CLASLIST[.R2]
		THEN VREG_ @(@CLASLIST[.R2]+1)
		ELSE
			IF .R1 EQL @@CLASLIST[.R2+STEP]
				THEN VREG_ @(@CLASLIST[.R2+STEP]+1)
				ELSE VREG_ 0;
	.VREG
END;
GLOBAL ROUTINE CHROUT=!LIST CHR ON LST OR TTY DEVICES OR BOTH
BEGIN
EXTERNAL TTYOUT,LSTOUT;
	IF .FLGREG<LISTING> THEN LSTOUT();
	IF NOT .FLGREG<TTYDEV>  THEN OUTCHR(C); !IF 0 THEN LIST DEVICE IS TTY OR LPT:
	.VREG
END;
GLOBAL ROUTINE ZOUTMSG(PTR)=
BEGIN
	IF NOT .FLGREG<TTYDEV>  THEN OUTSTR(.PTR); !O/P TO TTY
	IF .FLGREG<LISTING> THEN
	BEGIN
	PTR_(.PTR-1)<0,7>;
	UNTIL (CHR_SCANI(PTR)) EQL 0 DO LSTOUT();
	END;
	.VREG
END;
ROUTINE CRLF=
BEGIN
	C_#15; CHROUT(); C _ #12; CHROUT();
END;
GLOBAL ROUTINE EOUTDEC(VALUE)= !OUTPUT DECIMAL VALUE TO LST OR TTY OR BOTH
BEGIN
LOCAL Z[6];
	INCR I FROM 0 TO 5 DO
	BEGIN
		Z[.I]_.VALUE MOD 10;
		VALUE _ .VALUE/10;
		IF .VALUE EQL 0 THEN
		(UNTIL .I LSS 0 DO 
			(CHR_.Z[.I]+#60; CHROUT(); I_.I-1);
		 RETURN
		);
	END;
END;
GLOBAL ROUTINE LABLCHECK=
BEGIN
%
ROUTINE SCANS THE LABEL TABLE AND OUTPUTS A LIST OF UNDEFINED
LABELS IT ENCOUNTERS.
%
EXTERNAL LABTBL,LSTOUT,EOUTDEC,ZOUTMSG;
LOCAL BASE PTR, UNDEFLABEL,COUNT;
COUNT _ 0;
UNDEFLABEL _ 0;
	DECR I FROM LASIZ-1 TO 0 DO
	BEGIN
		IF (PTR _ .LABTBL[.I]) NEQ 0
		THEN
		 BEGIN
		  DO BEGIN
			IF .PTR[SNHDR] EQL 0
			THEN IF .PTR[SNUMBER] LEQ 99999
			   THEN (!ERROR UNDEFINED LABEL
				IF .FLGREG<LISTING>
				THEN
				BEGIN
				 IF .UNDEFLABEL EQL 0 THEN ZOUTMSG(PLIT '?M?J?M?JUNDEFINED LABELS?M?J?M?J?0');
				UNDEFLABEL _ .PTR[SNUMBER];
				EOUTDEC(.UNDEFLABEL);
				IF (COUNT _.COUNT+1) GTR 3
				THEN (COUNT _ 0; CRLF();)
				ELSE (C_#11; %TAB% CHROUT(););
				END;
				GLOBEND _ .GLOBEND+1; !INCREMENT # OF ERRORS
				FLGREG<ERRSW> _ 1; !SET FLAG FOR FATAL ERRORS
			    );
		     END WHILE (PTR _ .PTR[CLINK]) NEQ 0;
		 END
	END;
END; !OF LABLCHECK
GLOBAL ROUTINE UNDOLABEL= !UNTERMINATED DO LABELS
BEGIN
%
ROUTINE IS CALLED BY CLASSIFIER MODULE AFTER END OF PROGRAM
FLAG IS SEEN AND SOME UNTERMINATED DO LOOPS ARE KNOWN TO EXIST.
ROUTINE THEN FOLLOWS CHAIN OF UNTERMINATD DO'S AND OUTPUTS THE LOOP TERMINAL LABEL
MISSING AND THE LINE NUMBER OF THE DO STATEMENT IN WHICH IT WAS FOUND
%
EXTERNAL LASDOLABEL,ERROUT,ENTRY,ZOUTMSG; MAP BASE LASDOLABEL;
LOCAL BASE PTR;
MACRO E46=46$;
	FLGREG<ERRSW> _ 1;
	IF NOT .FLGREG<LISTING> THEN RETURN;
	ZOUTMSG(PLIT'?M?J?M?JUNTERMINATED DO LOOPS?M?J?M?J?0');
	DO
	BEGIN
		PTR _ .LASDOLABEL<RIGHT>; PTR _ .PTR[SNDOLNK];
		ZOUTMSG(PLIT'LINE: ');
		PTR_.(.PTR)<LEFT>;	!PTR TO DO NODE
		 EOUTDEC(.PTR[SRCISN]); CRLF();
		GLOBEND _ .GLOBEND+1;	!INCREMENT ERROR COUNT
	END UNTIL (LASDOLABEL _ @(.LASDOLABEL<LEFT>)) EQL 0;
END; !OF UNDOLABEL
GLOBAL ROUTINE DOCHECK=
BEGIN
%T1 CONTAINS APOINTER TO A STATEMENT LABEL NODE
 ADJUST THE DO NEST LEVEL COUNT AND CHECK FOR ILLEGAL NESTING
 WE GET HERE ONLY IF THE LABEL WAS IN A DO STATEMENT SEEN ALREADY
%
EXTERNAL LASDOLABEL,DONESTLEVEL,DOINDEX;
MAP BASE T1: LASDOLABEL;
!
DONESTLEVEL _ .DONESTLEVEL - .T1[SNDOLVL]; !SUBTRACT THE NEST LEVEL OF THE LABEL
IF .T1 NEQ .LASDOLABEL<RIGHT>
	THEN	!INCORRECT DO LOOP NESTING
	(LOCAL BASE SRCPT;
	 SRCPT _ .LASDOLABEL[SNDOLNK]; !THE LINK PTR TO THE LAST DO NODE SEEN
	SRCPT _ .(.SRCPT)<LEFT>; !GET PTR TO SOURCE NODE
	 ENTRY[1] _ .SRCPT[SRCISN]; ! THE ISN OF THE LAST DO SEEN
	ERROUT(29)	!ILLEGALLY NESTED DO
	)
	!
	!ELSE UNSTACK NESTED LABELS OF DO'S THAT HAVE THE SAME TERMINAL LABEL
	!
	ELSE DO LASDOLABEL _ @(.LASDOLABEL<LEFT>) UNTIL .T1 NEQ .LASDOLABEL<RIGHT>;
	IF .LASDOLABEL NEQ 0
	THEN(LOCAL BASE SRCPT;
		SRCPT _ .LASDOLABEL[SNDOLNK];
		SRCPT _ .(.SRCPT)<LEFT>;
		DOINDEX _ .SRCPT[DOSYM];	!NEW CURRENT DO INDEX
	    )
	ELSE DOINDEX _ 0;
END;
MACHOP POPJ=#263,BLT=#251;
MACRO CORE(X) = BEGIN
			CALLI(X,#11);
		     END$;
MACRO MTIME(X)=
	BEGIN
	 CALLI(X,#23)
	END$;
MACRO INTEGER=16$,REAL=20$;
EXTERNAL LOWLOC,SEGINCORE,ILABIX,PHAZCONTROL %()%,ERRLINK,SP,TMPCNT[4],FREELIST,LSAVE,LEXL,LEXEMEGEN %()%;
ROUTINE GLOBI1=	!INITIALIZE GLOBAL AREA
			!CALLED BY GLOBINIT AND BY MAIN ROUTINE CLAS.F
BEGIN
	EXTERNAL ENTRY,SYMTYPE,TBLSEARCH,NAME,ONEPLIT;
	ILABIX _ 100000;	!INITIAL TEMPORTRY LABEL VALUE
	TYPTAB["A"-"A"]_REAL;
	T1<LEFT>_TYPTAB["A"-"A"];T1<RIGHT>_.T1<LEFT>+1;
	BLT(T1,TYPTAB["Z"-"A"]);
	T1<LEFT>_TYPTAB["I"-"A"];T1<RIGHT>_.T1<LEFT>+1;
	TYPTAB["I"-"A"]_INTEGER;
	BLT(T1,TYPTAB["N"-"A"]);
	PAGE_1^18;PROGNAME_SIXBIT'MAIN.';
	IF .FLGREG<LISTING> THEN PAGEHEADER();
	SPACEFREE_@JOBREL-@JOBFF;
	SEGINCORE_1;FLGREG<FIRSTSTA>_1;
	FLGREG _ <BTTMSTFL> _ 1;	!BOTTOMMOST SUBROUTINE FLAG
	LOWLOC _ 1;

END;	!OF GLOBI1
ROUTINE GLOBINIT=		!INITIALIZE ALL GLOBALS
BEGIN
	!FILE GLOBL.BLI HAS ALL THE GLOBALS DEFINED

	SYMTBL[0]_ 0;	!THE FIRST GLOBAL IN GLOBL.BLI
	VREG<LEFT>_ SYMTBL<0,0>; VREG<RIGHT> _ SYMTBL[1]<0,0>; !SET BLT POINTER
	BLT(VREG,GLOBEND);	!ZERO THE GLOBAL AREA
	(.JOBFF)<FULL> _ 0;
	VREG<LEFT> _ .JOBFF; VREG<RIGHT> _ .JOBFF+1;
	BLT(VREG,.JOBREL);
	GLOBI1();	!SET GLOBALS TO ZERO AND RESET VALUES
	!
END;	!OF ROUTINE GLOBINIT
%(-----------------------------------------------------------------------------------------------------------------
	CLASSIFIES AND PARSES EACH STATEMENT
-----------------------------------------------------------------------------------------------------------------)%
UNTIL .FLGREG<ENDFILE> DO
BEGIN
	EXTERNAL JOBFFSAV,ENDSTA,WARNOUT;
	EXTERNAL PHAZSTAT, OT, PSTATBLK,LASDOLABEL,ADJCALL;
	PHAZSTAT(1,0);		!GET PHASE 1 START TIMES
	GLOBINIT();	!INITIALIZE GLOBALTABLES
	PIN_POUT_QON_QOFF_-1;
	IF .FLGREG<EOP> THEN FLGREG<EOP> _ 0
	ELSE (COLUMN _ 1; NEWCARD()); !GET FIRST CARD OF FILE
	JOBFFSAV _ .JOBFF;	!SAVING THE STATE OF THE LOW SEG
	!CREATE A CONSTANT NODE FOR ONE
	NAME_CONTAB; SYMTYPE_INTEGER; ENTRY[1]_ 1;
	ONEPLIT _ TBLSEARCH(); !PTR TO CONSTANT ONE  IN ONEPLIT
	UNTIL .FLGREG<EOP>
	DO
	BEGIN
		IF .FLGREG<ENDFILE> THEN EXITLOOP;
		ISN_.STANUM;
		IF .STALABL NEQ 0 THEN
		BEGIN
			MAP BASE T1:T2;
			NAME_LABTAB;ENTRY[0]_@STALABL;
			T1_LABLOFSTATEMENT_TBLSEARCH();
			IF (T2_.T1[SNHDR]) NEQ 0 THEN
				(ENTRY[0]_@ISN;ENTRY[1]_.T1[SNUMBER];ENTRY[2]_.T2[SRCISN];ERROUT(E20));
			IF .T1[SNDOLVL] NEQ 0 THEN DOCHECK(); !CHECK FOR ILLEGAL NESTING AND ADJUST DO NEST LEVEL
		END ELSE LABLOFSTATEMENT_0;
		SP_-1; LSAVE _ 0;	!INITIALIZING FOR CORRECT STATEMENT TO STATEMENT TRANSITION
		IF (T2_CLASSIFIER()) LEQ 0 
			THEN 
			(ENTRY[0]_@ISN;ERROUT(E10);PIN _ POUT _ -1;
			 IF NOT  .FLGREG<ENDFILE> THEN NEWCARD()
				ELSE FLGREG<EOP> _ 1;
			)
			ELSE IF (@T2)() LSS 0 THEN
				BEGIN
					!IF .LSAVE NEQ 0 THEN LSAVE_0 ELSE LEXL_LEXEMEGEN();
					LSAVE _ 0;
					STALABL_0;
					UNTIL .LEXL<LEFT> EQL LINEND DO (LEXL_LEXEMEGEN())
				END;
		FLGREG<FIRSTSTA>_0; !NO LONGER FIRST STATEMENT NOW
		IF .STANUM EQL .ISN THEN STALABL _ 0; !FOR MULTIPLE STATEMENTS PER LINE
	END;
	BEGIN MAP BASE SORCPTR;
	IF .SORCPTR[SRCID] NEQ ENDID
	  THEN IF NOT .FLGREG<ERRSW> THEN (WARNOUT(69);
						C _ EOL; ENDSTA()
					   );
	END;
	PHAZSTAT(1,1);		!GET PHASE 1 FINISH TIMES
	LABLCHECK();	!CHECK FOR UNDEFINED LABELS
	IF .LASDOLABEL NEQ 0 THEN UNDOLABEL(); !OUTPUT LIST OF UNTERMINATED DO LOOPS
	IF  NOT .FLGREG<ERRSW>
		THEN IF NOT .FLGREG<SYNONLY> THEN
		  BEGIN
			LOCAL REG0SAV;
			 REG0SAV_.FLGREG;
			ADJCALL();	!INSERT CALLS FOR ADJUSTABLE DIMENSIONS
			PHAZCONTROL();
			FLGREG_.REG0SAV;
		  END;
	IF .ERRLINK<RIGHT> NEQ 0
	THEN(LOCAL BASE PTR;
		UNTIL (PTR_.ERRLINK<RIGHT>) EQL 0 DO
		BEGIN
			T2_ (.(.PTR)<LEFT>)<36,7>;
			ERRLINK_@@ERRLINK;
				ERRLST(.T2,.PTR);
		END;
	     );
	IF .FLGREG<ERRSW> THEN (FLGREG<FATALERR> _ 1;FLGREG<ERRSW> _ 0);	!SET GLOBAL ERRORS FLAG FOR "?" GENERATION ON TTY
	T2 _ PROGNAME<36,6>;
	CRLF();
	DECR I FROM 5 TO 0 DO (C_SCANI(T2)+#40; CHROUT());
	C _  #11; CHROUT();
	EOUTDEC(.GLOBEND);
	ZOUTMSG(PLIT' ERRORS DETECTED?M?J');
	IF .FLGREG<STATFLG> THEN OT();	!OUTPUT STATISTICS IF DESIRED
	DECR I FROM 20*4-1 TO 0 DO PSTATBLK[.I]_0;	!CLEAR TIME DATA AREA
	!
	!RESET SIZE OF LOWSEG
	BEGIN MACHOP JFCL=#255;
	!
	JOBFF _ VREG _ .JOBFFSAV<RIGHT>;
	CORE(VREG);
	JFCL(0,0);
	END;
END;
IF .FLGREG<FATALERR> THEN OUTSTR(PLIT'?M?J???M?J?0'); !CRLF ? CRLF
POPJ(SREG,0);
END ELUDOM
   sa?Qè