; SUBROUTINE CMACBV (NNR,WTPTR,BPTR(0),KVEH,ICELL(IPPOP),IBRC(0),
;    IBPT(0),ICELL(IPSING),NISS,IVRESC,IPRSW,IBNO,NWPERB,NWACC,
;    IVMEAS(1),IWMEAS(1),NOTV)
; CHEAP UNITAB - ADD TO BASE/VEHICLE SUMS FOR NNR RESPONDENTS
;
; 0. NNR=# RESPONDENTS TO PROCESS
; 1. WTPTR=WEIGHTS POP POINTER
; 2. BPTR=BASE POP POINTERS - UNIVERSE IS BASE 0
; 3. KVEH=# VEHICLES
; 4. ICELL(IPPOP)=LOC OF FIRST VEHICLE POP POINTER-1
; 5. IBRC=BASE RESPONDENT COUNTS
; 6. IBPT=BASE PROJECTED TOTALS
; 7. ICELL(IPSING)=LOC OF FIRST SINGLE-1
;10. NISS=TOTAL # ISSUES SURVEYED
;11. IVRESC=1 IF RESPONSE COUNTS REQUESTED, 0 IF NOT
;12. IPRSW=1 IF PAIRS REQUESTED, 0 IF NOT
;13. IBNO=# BASES (EXCLUDING UNIVERSE)
;14. NWPERB=# WORDS PER BASE FOR SINGLES, RESPONSE COUNTS & PAIRS
;15. NWACC=TOTAL # OF SUCH WORDS FOR ALL BASES PLUS PER-RESPONDENT
;      INCREMENT
;16. IVMEAS(J)=TOTAL #ISSUES SURVEYED FOR VEHICLE J
;17 IWMEAS(J)=#ISSUES SURVEYED PER WEEK FOR VEHICLE J
;20. NOTV=1 IF ALL VEHICLES ARE 2-ISSUE, 0 OTHERWISE
;
;
        TITLE   CMACBV
        ENTRY   CMACBV
;
        IWM=4
        SPRLOC=5
        WT=6
        KVEH=7
        WEEK1=10
        WEEK2=11
        IVRESC=13
        NWPERB=14
        W1=1
        W2=2
        W3=3
        W4=0
        W5=12
;
CMACBV: 0
        MOVE    W1,@0(16)
        MOVEM   W1,NNR
        MOVE    KVEH,@3(16)
; COPY BASE POP POINTERS FROM CALLING PROGRAM AND ZERO OUT RESPONDENT
;    COUNTS AND PROJECTED TOTALS
        MOVE    W1,@13(16)     ;# OF BASES
CMA10:  HRRZ    W2,2(16)
        ADD     W2,W1
        MOVE    W3,0(W2)
        MOVEM   W3,BPTR(W1)
        SETZM   0,IBRC(W1)
        SETZM   0,IBPT(W1)
        SOJGE   W1,CMA10
        JUMPG   KVEH,CMA50
; NO VEHICLES IN THIS RUN
CMA20:  ILDB    WT,@1(16)     ;POP RESPONDENT WEIGHT
        MOVE    W1,@13(16)    ;#BASES
CMA30:  ILDB    W2,BPTR(W1)
        JUMPE   W2,CMA40
; THIS BASE TRUE
        AOS     0,IBRC(W1)
        ADDM    WT,IBPT(W1)
CMA40:  SOJGE   W1,CMA30
        SOSE    0,NNR        ;SKIP AFTER FINISHING NNR RESPONDENTS
        JUMPA   0,CMA20
        JUMPA   0,CMA370
; VEHICLE(S) INCLUDED IN THIS RUN
CMA50:  MOVE    IVRESC,@11(16)
        MOVE    W1,4(16)     ;LOC OF ICELL(IPPOP)
        HRRM    W1,CMA80
        HRRM    W1,CMA120
        HRRM    W1,CMA190
        HRRM    W1,CMA420
        SUBI    W1,1
        HRRM    W1,CMA110     ;LOC OF ICELL(IPPOP-1)
        HRRM    W1,CMA180
        HRRM    W1,CMA480
        HRRM    W1,CMA510
        MOVE    W1,7(16)     ;LOC OF ICELL(IPSING)
        HRRM    W1,CMA125
        HRRM    W1,CMA130
        HRRM    W1,CMA140
        HRRM    W1,CMA200
        HRRM    W1,CMA210
        HRRM    W1,CMA220
        HRRM    W1,CMA350
        HRRM    W1,CMA405
        HRRM    W1,CMA430
        HRRM    W1,CMA475
        HRRM    W1,CMA490
        HRRM    W1,CMA520
        JUMPE   IVRESC,CMA55
        ADD     W1,KVEH     ;LOC OF ICELL(IPRESC)
        HRRM    W1,CMA150
        HRRM    W1,CMA230
        HRRM    W1,CMA240
        HRRM    W1,CMA455
        HRRM    W1,CMA540
CMA55:  ADD     W1,KVEH     ;LOC OF ICELL(IPPAIR)
        HRRM    W1,SPRLOC
        MOVE    NWPERB,@14(16)
        HRRZ    W1,7(16)
        ADD     W1,@15(16)
        SUB     W1,NWPERB
        HRRM    W1,LASTB
        HRRZ    W1,16(16)     ;LOC OF IVMEAS(1)
        SUBI    W1,1
        HRRM    W1,CMA410
        HRRZ    W1,17(16)     ;LOC OF IWMEAS(1)
        SUBI    W1,1
        HRRM    W1,CMA470
        HRRM    W1,CMA600
        HRRM    W1,CMA610
; MAIN LOOP OVER NNR RESPONDENTS
CMA60:  ILDB    WT,@1(16)     ;POP RESPONDENT WEIGHT
        SETZ    W4,
        MOVE    W1,@13(16)    ;#BASES
CMA70:  ILDB    W2,BPTR(W1)
        MOVEM   W2,BASEYN(W1)
        JUMPE   W2,CMA75   ;JUMP IF BASE IS FALSE
        AOS     0,IBRC(W1) ;ADD TO RESPONDENT COUNT
        ADDM    WT,IBPT(W1)   ;ADD TO PROJECTED TOTAL
        ADD     W4,W2
CMA75:  SOJGE   W1,CMA70
        SUB     W4,BASEYN
        JUMPG   W4,CMA100     ;JUMP IF 1+ BASES OTHER THAN UNIV. TRUE
; ALL BASES FALSE - POP & IGNORE VEHICLES
        MOVE    W1,@10(16)     ;TOTAL #ISSUES
CMA80:  ILDB    W2,.-.(W1)    ;POP ALL ISSUES
        SOJG    W1,CMA80
        JUMPA   0,CMA365
; AT LEAST ONE BASE TRUE - PROCESS VEHICLES
CMA100: SKIPN   0,@20(16)     ;SKIP IF ALL VEHICLES ARE 2-ISSUE
        JUMPA   0,CMA400
        SKIPE   0,@12(16)     ;SKIP IF NO PAIRS REQUESTED
        JUMPA   0,CMA170
; SINGLES ONLY
        MOVE    W1,@10(16)     ;TOTAL #ISSUES
        MOVE    W2,KVEH
CMA110: ILDB    WEEK1,.-.(W1)
CMA120: ILDB    WEEK2,.-.(W1)
CMA125: SETZM   0,.-.(W2)     ;CLEAR SINGLE
        CAIE    WEEK1,0
CMA130: ADDM    WT,.-.(W2)    ;ADD TO SINGLE
        CAIE    WEEK2,0
CMA140: ADDM    WT,.-.(W2)
        JUMPE   IVRESC,CMA160
; RESPONSE COUNTS
        ADD     WEEK1,WEEK2
CMA150: MOVEM   WEEK1,.-.(W2)  ;STORE RESPONSE COUNT
CMA160: SUBI    W1,2
        SOJG    W2,CMA110      ;JUMP IF MORE VEHICLES LEFT
        JUMPA   0,CMA315
; SINGLES AND PAIRS
CMA170: MOVEI   W1,2           ;W1 POINTS TO ISSUE POP POINTERS
        MOVEI   W2,1           ;W2 POINTS TO VEHICLES
        HRR     W4,SPRLOC
        HRRM    W4,CMA260
CMA180: ILDB    WEEK1,.-.(W1)
CMA190: ILDB    WEEK2,.-.(W1)
        MOVEM   WEEK1,IWK1-1(W2)
        MOVEM   WEEK2,IWK2-1(W2)
CMA200: SETZM   0,.-.(W2)      ;CLEAR SINGLE
        CAIE    WEEK1,0
CMA210: ADDM    WT,.-.(W2)
        CAIE    WEEK2,0
CMA220: ADDM    WT,.-.(W2)
        JUMPE   IVRESC,CMA250
; RESPONSE COUNTS
CMA230: MOVEM   WEEK1,.-.(W2)
CMA240: ADDM    WEEK2,.-.(W2)
; PAIRS AND SELF-PAIRS
CMA250: MOVE    W3,W2
        HRRZ    W4,CMA260      ;SET UP PAIR STORAGE ADDRESSES
        ADD     W4,W3
        SUBI    W4,1
        HRRM    W4,CMA260
        HRRM    W4,CMA270
        HRRM    W4,CMA280
        HRRM    W4,CMA290
        HRRM    W4,CMA300
; SELF-PAIR
CMA260: SETZM   0,.-.(W3)      ;CLEAR SELF-PAIR
        MOVE    W4,WEEK1
        IOR     W4,WEEK2
        CAIE    W4,0
CMA270: MOVEM   WT,.-.(W3)     ;STORE SELF-PAIR
        SOJE    W3,CMA310      ;JUMP IF FIRST VEHICLE
; PAIRS (OTHER THAN SELF-PAIR)
CMA280: SETZM   0,.-.(W3)      ;CLEAR PAIR
        MOVE    W4,WEEK1       ;WK1(J)
        IOR     W4,IWK1-1(W3)  ;WK1(K)
        CAIE    W4,0
CMA290: MOVEM   WT,.-.(W3)
        MOVE    W4,WEEK2       ;WK2(J)
        IOR     W4,IWK2-1(W3)  ;WK2(K)
        CAIE    W4,0
CMA300: ADDM    WT,.-.(W3)
        SOJG    W3,CMA280      ;JUMP IF MORE OTHER VEHICLES LEFT
CMA310: ADDI    W1,2
        ADDI    W2,1
        CAMG    W2,KVEH
        JUMPA   0,CMA180
; FINISHED VEHICLES - ADD TO RUNNING SUMS FOR ALL TRUE BASES
CMA315: MOVE    W1,@13(16)     ;#BASES
        MOVE    W2,LASTB
CMA320: SKIPN   0,BASEYN(W1)
        JUMPA   0,CMA360
; BASE IS TRUE
        MOVE    W3,NWPERB
        HRRM    W2,CMA355
CMA350: MOVE    W4,.-.(W3)
CMA355: ADDM    W4,.-.(W3)
        SOJG    W3,CMA350
CMA360: SUB     W2,NWPERB
        SOJG    W1,CMA320
; FINISHED THIS RESPONDENT
CMA365: SOSE    0,NNR          ;SKIP AFTER FINISHING NNR RESPONDENTS
        JUMPA   0,CMA60
; FINISHED NNR RESPONDENTS
CMA370: MOVE    W1,@13(16)     ;#BASES
CMA380: HRRZ    W2,5(16)       ;UPDATE RESP COUNTS IN CALLING PROG
        ADD     W2,W1
        MOVE    W3,IBRC(W1)
        ADDM    W3,0(W2)
        HRRZ    W2,6(16)       ;UPDATE PROJ TOTALS IN CALLING PROG
        ADD     W2,W1
        MOVE    W3,IBPT(W1)
        ADDM    W3,0(W2)
        SOJGE   W1,CMA380
        JRA     16,21(16)
;
;
;
; PROCESS RUNS WITH SOME VEHICLES NOT 2-ISSUE (E.G. DAYTIME TV)
CMA400: SKIPE   0,@12(16)     ;SKIP IF PAIRS NOT REQUESTED
        JUMPA   0,CMA460
; SINGLES ONLY
        MOVE    W1,@10(16)     ;TOTAL #ISSUES
        MOVE    W2,KVEH
CMA405: SETZM   0,.-.(W2)      ;CLEAR SINGLE
CMA410: MOVE    W3,.-.(W2)     ;IVMEAS(J)
        SETZ    W4,            ;CLEAR RESPONSE COUNTER
CMA420: ILDB    WEEK1,.-.(W1)
        JUMPE   WEEK1,CMA450
CMA430: ADDM    WT,.-.(W2)     ;ADD TO SINGLE
        ADDI    W4,1           ;ADD TO RESPONSE COUNTER
CMA450: SUBI    W1,1           ;POINT TO NEXT ISSUE
        SOJG    W3,CMA420      ;JUMP IF VEHICLE NOT FINISHED
        CAIE    IVRESC,0
CMA455: MOVEM   W4,.-.(W2)     ;STORE RESPONSE COUNT
        SOJG    W2,CMA405      ;JUMP IF MORE VEHICLES LEFT
        JUMPA   0,CMA315
; SINGLES AND PAIRS
CMA460: MOVEI   W1,2           ;W1 POINTS TO ISSUE POP POINTERS
        MOVEI   W2,1           ;W2 POINTS TO VEHICLES
        HRR     W4,SPRLOC      ;SET UP PAIR STORAGE ADDRESSES
        HRRM    W4,CMA560
CMA470: MOVE    W3,.-.(W2)     ;IWMEAS(J)
        MOVEM   W3,IWM
        SETZM   0,IWK1-1(W2)   ;CLEAR WEEKLY EXPOSURE COUNTS
        SETZM   0,IWK2-1(W2)
CMA475: SETZM   0,.-.(W2)      ;CLEAR SINGLE
        SETZ    W5,            ;CLEAR UNWEIGHTED SELF-PAIR COUNTER
CMA480: ILDB    WEEK1,.-.(W1)  ;POP WEEK1 ISSUES
        JUMPE   WEEK1,CMA500
        AOS     0,IWK1-1(W2)
CMA490: ADDM    WT,.-.(W2)     ;ADD TO SINGLE
CMA500: ADD     W1,IWM         ;POINT TO WEEK2
CMA510: ILDB    WEEK2,.-.(W1)
        SUB     W1,IWM
        IOR     WEEK1,WEEK2
        ADD     W5,WEEK1       ;ADD TO SELF-PAIR
        JUMPE   WEEK2,CMA530
        AOS     0,IWK2-1(W2)
CMA520: ADDM    WT,.-.(W2)     ;ADD TO SINGLE
CMA530: ADDI    W1,1           ;POINT TO NEXT ISSUE
        SOJG    W3,CMA480      ;JUMP IF VEHICLE NOT FINISHED
        ADD     W1,IWM         ;POINT TO FIRST ISSUE OF NEXT VEHICLE
        JUMPE   IVRESC,CMA550
; RESPONSE COUNTS
        MOVE    W3,IWK1-1(W2)
        ADD     W3,IWK2-1(W2)
CMA540: MOVEM   W3,.-.(W2)     ;STORE RESPONSE COUNT
; PAIRS AND SELF-PAIR
CMA550: MOVE    W3,W2
        HRRZ    W4,CMA560      ;SET UP PAIR STORAGE ADDRESSES
        ADD     W4,W3
        SUBI    W4,1
        HRRM    W4,CMA560
        HRRM    W4,CMA570
        HRRM    W4,CMA620
; SELF-PAIR
CMA560: SETZM   0,.-.(W3)      ;CLEAR SELF-PAIR
        MOVE    W4,W5
        JUMPE   W4,CMA580      ;JUMP IF SELF-PAIR IS ZERO
        IMUL    W4,WT
CMA570: MOVEM   W4,.-.(W3)     ;STORE SELF-PAIR
CMA580: SOJE    W3,CMA630      ;JUMP IF THIS IS FIRST VEHICLE
; PAIRS OTHER THAN SELF-PAIR
CMA590: MOVE    W5,W4          ;UNWEIGHTED SELF-PAIR
        ADD     W5,IWK1-1(W3)  ;ADD VEHICLE K EXPOSURE COUNTS
        ADD     W5,IWK2-1(W3)
        JUMPE   W5,CMA620      ;PAIR(J,K)=0 IF W5=0
        MOVE    W5,IWK1-1(W2)  ;WK1(J)
        ADD     W5,IWK2-1(W2)  ;WK2(J)
CMA600: IMUL    W5,.-.(W3)     ;IWMEAS(K)
        MOVE    WEEK1,IWK1-1(W3)   ;WK1(K)
        ADD     WEEK1,IWK2-1(W3)   ;WK2(K)
CMA610: IMUL    WEEK1,.-.(W2)  ;IWMEAS(J)
        ADD     W5,WEEK1
        MOVE    WEEK1,IWK1-1(W2)   ;WK1(J)
        IMUL    WEEK1,IWK1-1(W3)   ;WK1(K)
        SUB     W5,WEEK1
        MOVE    WEEK2,IWK2-1(W2)   ;WK2(J)
        IMUL    WEEK2,IWK2-1(W3)   ;WK2(K)
        SUB     W5,WEEK2
        IMUL    W5,WT
CMA620: MOVEM   W5,.-.(W3)     ;STORE PAIR
        SOJG    W3,CMA590      ;JUMP IF MORE OTHER VEHICLES LEFT
CMA630: ADDI    W2,1           ;POINT TO NEXT VEHICLE
        CAMG    W2,KVEH
        JUMPA   0,CMA470       ;JUMP IF MORE VEHICLES LEFT
        JUMPA   0,CMA315       ;FINISHED VEHICLES FOR THIS RESP.
;
;
;
BPTR:   BLOCK   ^D7
BASEYN: BLOCK   ^D7
IBRC:   BLOCK   ^D7
IBPT:   BLOCK   ^D7
IWK1:   BLOCK   ^D25
IWK2:   BLOCK   ^D25
LASTB:  0
NNR:    0
        END
 