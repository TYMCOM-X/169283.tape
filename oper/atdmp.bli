MODULE ATDMP (DOLLAR,ENTRIES=($ATDMP),FSAVE,TIMING,
   TIMER=EXTERNAL(SIX12))=BEGIN
!VERSION A4
!LAST UPDATE, AUG. 1,  1974, 1525, BY V.E.VAN VLEAR

REQUIRE UDEFS.BLI[7,107355];
REQUIRE ADEFS.BLI[7,107355];
REQUIRE GDEFS.BLI[7,107355];
REQUIRE TDEFS.BLI[7,107355];

UNDECLARE $ATDMP;


MACRO
  WCI(N)=$UOCHR(N)&,
  WMSG(N)=$UAPSZ($USPTR(N))&,
  DVAL(N)=$UAPN(.TPTR[N],10,1)&,
  QVAL(N)=$UAPN(.TPTR[N],8,1)&;

!SPECIAL CHAR. TABLE, STARTS WITH TOKEN VALUE 310
BIND SPCTB=PLIT(
  TV$LPAREN,"(",
  TV$RPAREN,")",
  TV$STAR,  "*",
  TV$PLUS,  "+",
  TV$COMMA, ",",
  TV$MINUS, "-",
  TV$SLASH, "/",
  TV$COLON, ":",
  0);

!EXPANDS TOKEN AND OUTPUTS CHARS. TO UOUT LINE
!TPTR IS PTR. TO TOKEN
GLOBAL ROUTINE $ATDMP(TPTR)=
BEGIN

MAP A$ TPTR;
LOCAL P1,T1,VAL,GTKT$ NODE;

VAL_.TPTR[A$ID];                !GET TOKEN ID VALUE
NODE_$GFIND(.VAL);
IF .NODE EQL -1 THEN
  BEGIN
  WMSG ('ID: ');
  QVAL (A$ID)
  END
ELSE
  BEGIN
  $UAPSZ($USPTR(' TV$'));
  $UAPSC (NODE[G$TMNEM],7)
  END;

WMSG (' CID: ');
!OUTPUTS SPECIAL CHAR. FOR CID IF CID FLAG IS TRUE
!OTHERWISE OUTPUTS LETTER F
IF (T1_.TPTR[A$CID]) EQL AV$TRUE
THEN
  BEGIN
  T1_0;                 !FOR TABLE INDEX
  UNTIL .SPCTB[.T1] EQL 0 OR .SPCTB[.T1] EQL .VAL
  DO T1_.T1+2;
  IF .SPCTB[.T1] EQL 0 THEN
    BEGIN
    WCI("T");
    $UAPN(.VAL,8,1);
    WCI (" ")
    END
  ELSE
    WCI(.SPCTB[.T1+1])
  END
ELSE
  WCI ("F");

!OUTPUTS "-ER-" IF "ERFL" IS TRUE
IF .TPTR[A$ERFL] EQL AV$TRUE THEN
  BEGIN
  WCI("-"); WCI("E"); WCI("R"); WCI("-")
  END;

!OUTPUTS "-RW-" FOR RESERVED WORDS
IF .TPTR[A$RSW] EQL AV$TRUE THEN
  BEGIN
  WCI("-"); WCI("R"); WCI("W"); WCI("-")
  END;

WMSG (' CP: ');
DVAL (A$CP);

WMSG (' LN: ');
DVAL (A$LN);

WMSG (' SF: ');
QVAL (A$SF);

WMSG (' STP: ');
QVAL (A$STP);

WMSG (' SBID: ');
QVAL (A$SBID);

END;
END ELUDOM
