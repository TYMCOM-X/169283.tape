C
C     ..................................................................
C
C        SUBROUTINE DLBVP
C
C        PURPOSE
C           TO SOLVE A LINEAR BOUNDARY VALUE PROBLEM, WHICH CONSISTS OF
C           A SYSTEM OF NDIM LINEAR FIRST ORDER DIFFERENTIAL EQUATIONS
C                  DY/DX=A(X)*Y(X)+F(X)
C           AND NDIM LINEAR BOUNDARY CONDITIONS
C                  B*Y(XL)+C*Y(XU)=R.
C
C        USAGE
C           CALL DLBVP (PRMT,B,C,R,Y,DERY,NDIM,IHLF,AFCT,FCT,DFCT,OUTP,
C                      AUX,A)
C           PARAMETERS AFCT,FCT,DFCT,OUTP REQUIRE AN EXTERNAL STATEMENT.
C
C        DESCRIPTION OF PARAMETERS
C           PRMT   - DOUBLE PRECISION INPUT AND OUTPUT VECTOR WITH
C                    DIMENSION GREATER THAN OR EQUAL TO 5, WHICH
C                    SPECIFIES THE PARAMETERS OF THE INTERVAL AND OF
C                    ACCURACY AND WHICH SERVES FOR COMMUNICATION BETWEEN
C                    OUTPUT SUBROUTINE (FURNISHED BY THE USER) AND
C                    SUBROUTINE DLBVP. EXCEPT PRMT(5) THE COMPONENTS
C                    ARE NOT DESTROYED BY SUBROUTINE DLBVP AND THEY ARE
C           PRMT(1)- LOWER BOUND XL OF THE INTERVAL (INPUT),
C           PRMT(1)- UPPER BOUND XU OF THE INTERVAL (INPUT),
C           PRMT(3)- INITIAL INCREMENT OF THE INDEPENDENT VARIABLE
C                    (INPUT),
C           PRMT(4)- UPPER ERROR BOUND (INPUT). IF RELATIVE ERROR IS
C                    GREATER THAN PRMT(4), INCREMENT GETS HALVED.
C                    IF INCREMENT IS LESS THAN PRMT(3) AND RELATIVE
C                    ERROR LESS THAN PRMT(4)/50, INCREMENT GETS DOUBLED.
C                    THE USER MAY CHANGE PRMT(4) BY MEANS OF HIS
C                    OUTPUT SUBROUTINE.
C           PRMT(5)- NO INPUT PARAMETER. SUBROUTINE DLBVP INITIALIZES
C                    PRMT(5)=0. IF THE USER WANTS TO TERMINATE
C                    SUBROUTINE DLBVP AT ANY OUTPUT POINT, HE HAS TO
C                    CHANGE PRMT(5) TO NON-ZERO BY MEANS OF SUBROUTINE
C                    OUTP. FURTHER COMPONENTS OF VECTOR PRMT ARE
C                    FEASIBLE IF ITS DIMENSION IS DEFINED GREATER
C                    THAN 5. HOWEVER SUBROUTINE DLBVP DOES NOT REQUIRE
C                    AND CHANGE THEM. NEVERTHELESS THEY MAY BE USEFUL
C                    FOR HANDING RESULT VALUES TO THE MAIN PROGRAM
C                    (CALLING DLBVP) WHICH ARE OBTAINED BY SPECIAL
C                    MANIPULATIONS WITH OUTPUT DATA IN SUBROUTINE OUTP.
C           B      - DOUBLE PRECISION NDIM BY NDIM INPUT MATRIX
C                    (DESTROYED). IT IS THE COEFFICIENT MATRIX OF Y(XL)
C                    IN THE BOUNDARY CONDITIONS.
C           C      - DOUBLE PRECISION NDIM BY NDIM INPUT MATRIX
C                    (POSSIBLY DESTROYED). IT IS THE COEFFICIENT MATRIX
C                    OF Y(XU) IN THE BOUNDARY CONDITIONS.
C           R      - DOUBLE PRECISION INPUT VECTOR WITH DIMENSION NDIM
C                    (DESTROYED). IT SPECIFIES THE RIGHT HAND SIDE OF
C                    THE BOUNDARY CONDITIONS.
C           Y      - DOUBLE PRECISION AUXILIARY VECTOR WITH
C                    DIMENSION NDIM. IT IS USED AS STORAGE LOCATION
C                    FOR THE RESULTING VALUES OF DEPENDENT VARIABLES
C                    COMPUTED AT INTERMEDIATE POINTS X.
C           DERY   - DOUBLE PRECISION INPUT VECTOR OF ERROR WEIGHTS
C                    (DESTROYED). ITS MAXIMAL COMPONENT SHOULD BE
C                    EQUAL TO 1. LATERON DERY IS THE VECTOR OF
C                    DERIVATIVES, WHICH BELONG TO FUNCTION VALUES Y AT
C                    INTERMEDIATE POINTS X.
C           NDIM   - AN INPUT VALUE, WHICH SPECIFIES THE NUMBER OF
C                    DIFFERENTIAL EQUATIONS IN THE SYSTEM.
C           IHLF   - AN OUTPUT VALUE, WHICH SPECIFIES THE NUMBER OF
C                    BISECTIONS OF THE INITIAL INCREMENT. IF IHLF GETS
C                    GREATER THAN 10, SUBROUTINE DLBVP RETURNS WITH
C                    ERROR MESSAGE IHLF=11 INTO MAIN PROGRAM.
C                    ERROR MESSAGE IHLF=12 OR IHLF=13 APPEARS IN CASE
C                    PRMT(3)=0 OR IN CASE SIGN(PRMT(3)).NE.SIGN(PRMT(2)-
C                    PRMT(1)) RESPECTIVELY. FINALLY ERROR MESSAGE
C                    IHLF=14 INDICATES, THAT THERE IS NO SOLUTION OR
C                    THAT THERE ARE MORE THAN ONE SOLUTION OF THE
C                    PROBLEM.
C                    A NEGATIVE VALUE OF IHLF HANDED TO SUBROUTINE OUTP
C                    TOGETHER WITH INITIAL VALUES OF FINALLY GENERATED
C                    INITIAL VALUE PROBLEM INDICATES, THAT THERE WAS
C                    POSSIBLE LOSS OF SIGNIFICANCE IN THE SOLUTION OF
C                    THE SYSTEM OF SIMULTANEOUS LINEAR EQUATIONS FOR
C                    THESE INITIAL VALUES. THE ABSOLUTE VALUE OF IHLF
C                    SHOWS, AFTER WHICH ELIMINATION STEP OF GAUSS
C                    ALGORITHM POSSIBLE LOSS OF SIGNIFICANCE WAS
C                    DETECTED.
C           AFCT   - THE NAME OF AN EXTERNAL SUBROUTINE USED. IT
C                    COMPUTES THE COEFFICIENT MATRIX A OF VECTOR Y ON
C                    THE RIGHT HAND SIDE OF THE SYSTEM OF DIFFERENTIAL
C                    EQUATIONS FOR A GIVEN X-VALUE. ITS PARAMETER LIST
C                    MUST BE X,A. SUBROUTINE AFCT SHOULD NOT DESTROY X.
C           FCT    - THE NAME OF AN EXTERNAL SUBROUTINE USED. IT
C                    COMPUTES VECTOR F (INHOMOGENEOUS PART OF THE
C                    RIGHT HAND SIDE OF THE SYSTEM OF DIFFERENTIAL
C                    EQUATIONS) FOR A GIVEN X-VALUE. ITS PARAMETER LIST
C                    MUST BE X,F. SUBROUTINE FCT SHOULD NOT DESTROY X.
C           DFCT   - THE NAME OF AN EXTERNAL SUBROUTINE USED. IT
C                    COMPUTES VECTOR DF (DERIVATIVE OF THE INHOMOGENEOUS
C                    PART ON THE RIGHT HAND SIDE OF THE SYSTEM OF
C                    DIFFERENTIAL EQUATIONS) FOR A GIVEN X-VALUE. ITS
C                    PARAMETER LIST MUST BE X,DF. SUBROUTINE DFCT
C                    SHOULD NOT DESTROY X.
C           OUTP   - THE NAME OF AN EXTERNAL OUTPUT SUBROUTINE USED.
C                    ITS PARAMETER LIST MUST BE X,Y,DERY,IHLF,NDIM,PRMT.
C                    NONE OF THESE PARAMETERS (EXCEPT, IF NECESSARY,
C                    PRMT(4),PRMT(5),...) SHOULD BE CHANGED BY
C                    SUBROUTINE OUTP. IF PRMT(5) IS CHANGED TO NON-ZERO,
C                    SUBROUTINE DLBVP IS TERMINATED.
C           AUX    - DOUBLE PRECISION AUXILIARY STORAGE ARRAY WITH 20
C                    ROWS AND NDIM COLUMNS.
C           A      - DOUBLE PRECISION NDIM BY NDIM MATRIX, WHICH IS USED
C                    AS AUXILIARY STORAGE ARRAY.
C
C        REMARKS
C           THE PROCEDURE TERMINATES AND RETURNS TO CALLING PROGRAM, IF
C           (1) MORE THAN 10 BISECTIONS OF THE INITIAL INCREMENT ARE
C               NECESSARY TO GET SATISFACTORY ACCURACY (ERROR MESSAGE
C               IHLF=11),
C           (2) INITIAL INCREMENT IS EQUAL TO 0 OR IF IT HAS WRONG SIGN
C               (ERROR MESSAGES IHLF=12 OR IHLF=13),
C           (3) THERE IS NO OR MORE THAN ONE SOLUTION OF THE PROBLEM
C               (ERROR MESSAGE IHLF=14),
C           (4) THE WHOLE INTEGRATION INTERVAL IS WORKED THROUGH,
C           (5) SUBROUTINE OUTP HAS CHANGED PRMT(5) TO NON-ZERO.
C
C        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
C           SUBROUTINE DGELG     SYSTEM OF LINEAR EQUATIONS.
C           THE EXTERNAL SUBROUTINES AFCT(X,A), FCT(X,F), DFCT(X,DF),
C           AND OUTP(X,Y,DERY,IHLF,NDIM,PRMT) MUST BE FURNISHED
C           BY THE USER.
C
C        METHOD
C           EVALUATION IS DONE USING THE METHOD OF ADJOINT EQUATIONS.
C           HAMMINGS FOURTH ORDER MODIFIED PREDICTOR-CORRECTOR METHOD
C           IS USED TO SOLVE THE ADJOINT INITIAL VALUE PROBLEMS AND FI-
C           NALLY TO SOLVE THE GENERATED INITIAL VALUE PROBLEM FOR Y(X).
C           THE INITIAL INCREMENT PRMT(3) IS AUTOMATICALLY ADJUSTED.
C           FOR COMPUTATION OF INTEGRAL SUM, A FOURTH ORDER HERMITEAN
C           INTEGRATION FORMULA IS USED.
C           FOR REFERENCE, SEE
C           (1) LANCE, NUMERICAL METHODS FOR HIGH SPEED COMPUTERS,
C               ILIFFE, LONDON, 1960, PP.64-67.
C           (2) RALSTON/WILF, MATHEMATICAL METHODS FOR DIGITAL
C               COMPUTERS, WILEY, NEW YORK/LONDON, 1960, PP.95-109.
C           (3) RALSTON, RUNGE-KUTTA METHODS WITH MINIMUM ERROR BOUNDS,
C               MTAC, VOL.16, ISS.80 (1962), PP.431-437.
C           (4) ZURMUEHL, PRAKTISCHE MATHEMATIK FUER INGENIEURE UND
C               PHYSIKER, SPRINGER, BERLIN/GOETTINGEN/HEIDELBERG, 1963,
C               PP.227-232.
C
C     ..................................................................
C
     0SUBROUTINE DLBVP(PRMT,B,C,R,Y,DERY,NDIM,IHLF,AFCT,FCT,DFCT,OUTP,
     1AUX,A)
C
C
      DIMENSION PRMT(1),B(1),C(1),R(1),Y(1),DERY(1),AUX(20,1),A(1)
      DOUBLE PRECISION PRMT,B,C,R,Y,DERY,AUX,A,H,X,Z,GL,HS,GU,SUM,
     1DGL,DGU,XST,XEND,DELT
C
C     ERROR TEST
      IF(PRMT(3)*(PRMT(2)-PRMT(1)))2,1,3
    1 IHLF=12
      RETURN
    2 IHLF=13
      RETURN
C
C     SEARCH FOR ZERO-COLUMNS IN MATRICES B AND C
    3 KK=-NDIM
      IB=0
      IC=0
      DO 7 K=1,NDIM
      AUX(15,K)=DERY(K)
      AUX(1,K)=1.D0
      AUX(17,K)=1.D0
      KK=KK+NDIM
      DO 4 I=1,NDIM
      II=KK+I
      IF(B(II))5,4,5
    4 CONTINUE
      IB=IB+1
      AUX(1,K)=0.D0
    5 DO 6 I=1,NDIM
      II=KK+I
      IF(C(II))7,6,7
    6 CONTINUE
      IC=IC+1
      AUX(17,K)=0.D0
    7 CONTINUE
C
C     DETERMINATION OF LOWER AND UPPER BOUND
      IF(IC-IB)8,11,11
    8 H=PRMT(2)
      PRMT(2)=PRMT(1)
      PRMT(1)=H
      PRMT(3)=-PRMT(3)
      DO 9 I=1,NDIM
    9 AUX(17,I)=AUX(1,I)
      II=NDIM*NDIM
      DO 10 I=1,II
      H=B(I)
      B(I)=C(I)
   10 C(I)=H
C
C     PREPARATIONS FOR CONSTRUCTION OF ADJOINT INITIAL VALUE PROBLEMS
   11 X=PRMT(2)
      CALL FCT(X,Y)
      CALL DFCT(X,DERY)
      DO 12 I=1,NDIM
      AUX(18,I)=Y(I)
   12 AUX(19,I)=DERY(I)
C
C     POSSIBLE BREAK-POINT FOR LINKAGE
C
C     THE FOLLOWING PART OF SUBROUTINE DLBVP UNTIL NEXT BREAK-POINT FOR
C     LINKAGE HAS TO REMAIN IN CORE DURING THE WHOLE REST OF THE
C     COMPUTATIONS
C
C     START LOOP FOR GENERATING ADJOINT INITIAL VALUE PROBLEMS
      K=0
      KK=0
  100 K=K+1
      IF(AUX(17,K))108,108,101
C
C     INITIALIZATION OF ADJOINT INITIAL VALUE PROBLEM
  101 X=PRMT(2)
      CALL AFCT(X,A)
      SUM=0.D0
      GL=AUX(18,K)
      DGL=AUX(19,K)
      II=K
      DO 104 I=1,NDIM
      H=-A(II)
      DERY(I)=H
      AUX(20,I)=R(I)
      Y(I)=0.D0
      IF(I-K)103,102,103
  102 Y(I)=1.D0
  103 DGL=DGL+H*AUX(18,I)
  104 II=II+NDIM
      XEND=PRMT(1)
      H=.0625D0*(XEND-X)
      ISW=0
      GOTO 400
C     THIS IS BRANCH TO ADJOINT LINEAR INITIAL VALUE PROBLEM
C
C     THIS IS RETURN FROM ADJOINT LINEAR INITIAL VALUE PROBLEM
  105 IF(IHLF-10)106,106,117
C
C     UPDATING OF COEFFICIENT MATRIX B AND VECTOR R
  106 DO 107 I=1,NDIM
      KK=KK+1
      H=C(KK)
      R(I)=AUX(20,I)+H*SUM
      II=I
      DO 107 J=1,NDIM
      B(II)=B(II)+H*Y(J)
  107 II=II+NDIM
      GOTO 109
  108 KK=KK+NDIM
  109 IF(K-NDIM)100,110,110
C
C
C     GENERATION OF LAST INITIAL VALUE PROBLEM
  110 EPS=PRMT(4)
      CALL DGELG(R,B,NDIM,1,EPS,I)
      IF(I)111,112,112
  111 IHLF=14
      RETURN
C
  112 PRMT(5)=0.D0
      IHLF=-I
      X=PRMT(1)
      XEND=PRMT(2)
      H=PRMT(3)
      DO 113 I=1,NDIM
  113 Y(I)=R(I)
      ISW=1
  114 ISW2=12
      GOTO 200
  115 ISW3=-1
      GOTO 300
  116 IF(IHLF)400,400,117
C     THIS WAS BRANCH INTO INITIAL VALUE PROBLEM
C
C     THIS IS RETURN FROM INITIAL VALUE PROBLEM
  117 RETURN
C
C     THIS PART OF LINEAR BOUNDARY VALUE PROBLEM COMPUTES THE RIGHT
C     HAND SIDE DERY OF THE SYSTEM OF ADJOINT LINEAR DIFFERENTIAL
C     EQUATIONS (IN CASE ISW=0) OR OF THE GIVEN SYSTEM (IN CASE ISW=1).
  200 CALL AFCT(X,A)
      IF(ISW)201,201,205
C
C     ADJOINT SYSTEM
  201 LL=0
      DO 203 M=1,NDIM
      HS=0.D0
      DO 202 L=1,NDIM
      LL=LL+1
  202 HS=HS-A(LL)*Y(L)
  203 DERY(M)=HS
  204 GOTO(502,504,506,407,415,418,608,617,632,634,421,115),ISW2
C
C     GIVEN SYSTEM
  205 CALL FCT(X,DERY)
      DO 207 M=1,NDIM
      LL=M-NDIM
      HS=0.D0
      DO 206 L=1,NDIM
      LL=LL+NDIM
  206 HS=HS+A(LL)*Y(L)
  207 DERY(M)=HS+DERY(M)
      GOTO 204
C
C     THIS PART OF LINEAR BOUNDARY VALUE PROBLEM COMPUTES THE VALUE OF
C     INTEGRAL SUM, WHICH IS A PART OF THE OUTPUT OF ADJOINT INITIAL
C     VALUE PROBLEM (IN CASE ISW=0) OR RECORDS RESULT VALUES OF THE
C     FINAL INITIAL VALUE PROBLEM (IN CASE ISW=1).
  300 IF(ISW)301,301,305
C
C     ADJOINT PROBLEM
  301 CALL FCT(X,R)
      GU=0.D0
      DGU=0.D0
      DO 302 L=1,NDIM
      GU=GU+Y(L)*R(L)
  302 DGU=DGU+DERY(L)*R(L)
      CALL DFCT(X,R)
      DO 303 L=1,NDIM
  303 DGU=DGU+Y(L)*R(L)
      SUM=SUM+.5D0*H*((GL+GU)+.16666666666666667D0*H*(DGL-DGU))
      GL=GU
      DGL=DGU
  304 IF(ISW3)116,422,618
C
C     GIVEN PROBLEM
  305 CALL OUTP(X,Y,DERY,IHLF,NDIM,PRMT)
      IF(PRMT(5))117,304,117
C
C     POSSIBLE BREAK-POINT FOR LINKAGE
C
C     THE FOLLOWING PART OF SUBROUTINE DLBVP SOLVES IN CASE ISW=0 THE
C     ADJOINT INITIAL VALUE PROBLEM. IT COMPUTES INTEGRAL SUM AND
C     THE VECTOR Y OF DEPENDENT VARIABLES AT THE LOWER BOUND PRMT(1).
C     IN CASE ISW=1 IT SOLVES FINALLY GENERATED INITIAL VALUE PROBLEM.
  400 N=1
      XST=X
      IHLF=0
      DO 401 I=1,NDIM
      AUX(16,I)=0.D0
      AUX(1,I)=Y(I)
  401 AUX(8,I)=DERY(I)
      ISW1=1
      GOTO 500
C
  402 X=X+H
      DO 403 I=1,NDIM
  403 AUX(2,I)=Y(I)
C
C     INCREMENT H IS TESTED BY MEANS OF BISECTION
  404 IHLF=IHLF+1
      X=X-H
      DO 405 I=1,NDIM
  405 AUX(4,I)=AUX(2,I)
      H=.5D0*H
      N=1
      ISW1=2
      GOTO 500
C
  406 X=X+H
      ISW2=4
      GOTO 200
  407 N=2
      DO 408 I=1,NDIM
      AUX(2,I)=Y(I)
  408 AUX(9,I)=DERY(I)
      ISW1=3
      GOTO 500
C
C     TEST ON SATISFACTORY ACCURACY
  409 DO 414 I=1,NDIM
      Z=DABS(Y(I))
      IF(Z-1.D0)410,411,411
  410 Z=1.D0
  411 DELT=.066666666666666667D0*DABS(Y(I)-AUX(4,I))
      IF(ISW)413,413,412
  412 DELT=AUX(15,I)*DELT
  413 IF(DELT-Z*PRMT(4))414,414,429
  414 CONTINUE
C
C     SATISFACTORY ACCURACY AFTER LESS THAN 11 BISECTIONS
      X=X+H
      ISW2=5
      GOTO 200
  415 DO 416 I=1,NDIM
      AUX(3,I)=Y(I)
  416 AUX(10,I)=DERY(I)
      N=3
      ISW1=4
      GOTO 500
C
  417 N=1
      X=X+H
      ISW2=6
      GOTO 200
  418 X=XST
      DO 419 I=1,NDIM
      AUX(11,I)=DERY(I)
  4190Y(I)=AUX(1,I)+H*(.375D0*AUX(8,I)+.7916666666666667D0*AUX(9,I)
     1-.20833333333333333D0*AUX(10,I)+.041666666666666667D0*DERY(I))
  420 X=X+H
      N=N+1
      ISW2=11
      GOTO 200
  421 ISW3=0
      GOTO 300
  422 IF(N-4)423,600,600
  423 DO 424 I=1,NDIM
      AUX(N,I)=Y(I)
  424 AUX(N+7,I)=DERY(I)
      IF(N-3)425,427,600
C
  425 DO 426 I=1,NDIM
      DELT=AUX(9,I)+AUX(9,I)
      DELT=DELT+DELT
  426 Y(I)=AUX(1,I)+.33333333333333333D0*H*(AUX(8,I)+DELT+AUX(10,I))
      GOTO 420
C
  427 DO 428 I=1,NDIM
      DELT=AUX(9,I)+AUX(10,I)
      DELT=DELT+DELT+DELT
  428 Y(I)=AUX(1,I)+.375D0*H*(AUX(8,I)+DELT+AUX(11,I))
      GOTO 420
C
C     NO SATISFACTORY ACCURACY. H MUST BE HALVED.
  429 IF(IHLF-10)404,430,430
C
C     NO SATISFACTORY ACCURACY AFTER 10 BISECTIONS. ERROR MESSAGE.
  430 IHLF=11
      X=X+H
      IF(ISW)105,105,114
C
C     THIS PART OF LINEAR INITIAL VALUE PROBLEM COMPUTES
C     STARTING VALUES BY MEANS OF RUNGE-KUTTA METHOD.
  500 Z=X
      DO 501 I=1,NDIM
      X=H*AUX(N+7,I)
      AUX(5,I)=X
  501 Y(I)=AUX(N,I)+.4D0*X
C
      X=Z+.4D0*H
      ISW2=1
      GOTO 200
  502 DO 503 I=1,NDIM
      X=H*DERY(I)
      AUX(6,I)=X
  503 Y(I)=AUX(N,I)+.29697760924775360D0*AUX(5,I)+.15875964497103583D0*X
C
      X=Z+.45573725421878943D0*H
      ISW2=2
      GOTO 200
  504 DO 505 I=1,NDIM
      X=H*DERY(I)
      AUX(7,I)=X
  505 Y(I)=AUX(N,I)+.21810038822592047D0*AUX(5,I)-3.0509651486929308D0*
     1AUX(6,I)+3.8328647604670103D0*X
C
      X=Z+H
      ISW2=3
      GOTO 200
  506 DO 507 I=1,NDIM
  5070Y(I)=AUX(N,I)+.17476028226269037D0*AUX(5,I)-.55148066287873294D0*
     1AUX(6,I)+1.2055355993965235D0*AUX(7,I)+.17118478121951903D0*
     2H*DERY(I)
      X=Z
      GOTO(402,406,409,417),ISW1
C
C     POSSIBLE BREAK-POINT FOR LINKAGE
C
C     STARTING VALUES ARE COMPUTED.
C     NOW START HAMMINGS MODIFIED PREDICTOR-CORRECTOR METHOD.
  600 ISTEP=3
  601 IF(N-8)604,602,604
C
C     N=8 CAUSES THE ROWS OF AUX TO CHANGE THEIR STORAGE LOCATIONS
  602 DO 603 N=2,7
      DO 603 I=1,NDIM
      AUX(N-1,I)=AUX(N,I)
  603 AUX(N+6,I)=AUX(N+7,I)
      N=7
C
C     N LESS THAN 8 CAUSES N+1 TO GET N
  604 N=N+1
C
C     COMPUTATION OF NEXT VECTOR Y
      DO 605 I=1,NDIM
      AUX(N-1,I)=Y(I)
  605 AUX(N+6,I)=DERY(I)
      X=X+H
  606 ISTEP=ISTEP+1
      DO 607 I=1,NDIM
     0DELT=AUX(N-4,I)+1.3333333333333333D0*H*(AUX(N+6,I)+AUX(N+6,I)-
     1AUX(N+5,I)+AUX(N+4,I)+AUX(N+4,I))
      Y(I)=DELT-.9256198347107438D0*AUX(16,I)
  607 AUX(16,I)=DELT
C     PREDICTOR IS NOW GENERATED IN ROW 16 OF AUX, MODIFIED PREDICTOR
C     IS GENERATED IN Y. DELT MEANS AN AUXILIARY STORAGE.
C
      ISW2=7
      GOTO 200
C     DERIVATIVE OF MODIFIED PREDICTOR IS GENERATED IN DERY.
C
  608 DO 609 I=1,NDIM
     0DELT=.125D0*(9.D0*AUX(N-1,I)-AUX(N-3,I)+3.D0*H*(DERY(I)+AUX(N+6,I)
     1+AUX(N+6,I)-AUX(N+5,I)))
      AUX(16,I)=AUX(16,I)-DELT
  609 Y(I)=DELT+.07438016528925620D0*AUX(16,I)
C
C     TEST WHETHER H MUST BE HALVED OR DOUBLED
      DELT=0.D0
      DO 616 I=1,NDIM
      Z=DABS(Y(I))
      IF(Z-1.D0)610,611,611
  610 Z=1.D0
  611 Z=DABS(AUX(16,I))/Z
      IF(ISW)613,613,612
  612 Z=AUX(15,I)*Z
  613 IF(Z-PRMT(4))614,614,628
  614 IF(DELT-Z)615,616,616
  615 DELT=Z
  616 CONTINUE
C
C     H MUST NOT BE HALVED. THAT MEANS Y(I) ARE GOOD.
      ISW2=8
      GOTO 200
  617 ISW3=1
      GOTO 300
  618 IF(H*(X-XEND))619,621,621
  619 IF(DABS(X-XEND)-.1D0*DABS(H))621,620,620
  620 IF(DELT-.02D0*PRMT(4))622,622,601
  621 IF(ISW)105,105,117
C
 H COULD BE DOUBLED IF ALL NECESSARY PRECEEDING VALUES ARE
C     AVAILABLE.
  622 IF(IHLF)601,601,623
  623 IF(N-7)601,624,624
  624 IF(ISTEP-4)601,625,625
  625 IMOD=ISTEP/2
      IF(ISTEP-IMOD-IMOD)601,626,601
  626 H=H+H
      IHLF=IHLF-1
      ISTEP=0
      DO 627 I=1,NDIM
      AUX(N-1,I)=AUX(N-2,I)
      AUX(N-2,I)=AUX(N-4,I)
      AUX(N-3,I)=AUX(N-6,I)
      AUX(N+6,I)=AUX(N+5,I)
      AUX(N+5,I)=AUX(N+3,I)
      AUX(N+4,I)=AUX(N+1,I)
      DELT=AUX(N+6,I)+AUX(N+5,I)
      DELT=DELT+DELT+DELT
  6270AUX(16,I)=8.962962962962963D0*(Y(I)-AUX(N-3,I))
     1-3.3611111111111111D0*H*(DERY(I)+DELT+AUX(N+4,I))
      GOTO 601
C
C     H MUST BE HALVED
  628 IHLF=IHLF+1
      IF(IHLF-10)630,630,629
  629 IF(ISW)105,105,114
  630 H=.5D0*H
      ISTEP=0
      DO 631 I=1,NDIM
     0Y(I)=.390625D-2*(8.D1*AUX(N-1,I)+135.D0*AUX(N-2,I)+4.D1*AUX(N-3,I)
     1+AUX(N-4,I))-.1171875D0*(AUX(N+6,I)-6.D0*AUX(N+5,I)-AUX(N+4,I))*H
     0AUX(N-4,I)=.390625D-2*(12.D0*AUX(N-1,I)+135.D0*AUX(N-2,I)+
     1108.D0*AUX(N-3,I)+AUX(N-4,I))-.0234375D0*(AUX(N+6,I)+
     218.D0*AUX(N+5,I)-9.D0*AUX(N+4,I))*H
      AUX(N-3,I)=AUX(N-2,I)
  631 AUX(N+4,I)=AUX(N+5,I)
      DELT=X-H
      X=DELT-(H+H)
      ISW2=9
      GOTO 200
  632 DO 633 I=1,NDIM
      AUX(N-2,I)=Y(I)
      AUX(N+5,I)=DERY(I)
  633 Y(I)=AUX(N-4,I)
      X=X-(H+H)
      ISW2=10
      GOTO 200
  634 X=DELT
      DO 635 I=1,NDIM
      DELT=AUX(N+5,I)+AUX(N+4,I)
      DELT=DELT+DELT+DELT
     0AUX(16,I)=8.962962962962963D0*(AUX(N-1,I)-Y(I))
     1-3.3611111111111111D0*H*(AUX(N+6,I)+DELT+DERY(I))
  635 AUX(N+3,I)=DERY(I)
      GOTO 606
C     END OF INITIAL VALUE PROBLEM
      END
    s@7¨