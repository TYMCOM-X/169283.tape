TITLE PASSIN - GET PASSWORD AND CIPHER

	EXTERN PASSW

	ENTRY PASSIN



TTY==1

INF=1

F=0
A=1
B=2
COUNT=3
CH=4
INDEX=5
CTLV1=6
CTLV11=7
XT1=10
XT2=11
XT3=12
XT4=13
XT5=14

PASSIN:	MOVEM 0,ACBLK
	MOVE 0,[XWD 1,ACBLK+1]
	BLT 0,ACBLK+16
	SETZ F,
	INIT TTY,200	;INIT TTY, NO ECHO
	SIXBIT /TTY/
	0,TTYI
	CALLI 12
	INBUF TTY,2
AGN:	TTCALL 3,[ASCIZ/
ENTER PASSWORD: /]
	MOVE XT1,WORD1
	MOVE XT3,XT1
	MOVE XT2,WORD2
	MOVE XT4,XT2
	MOVE XT5,XT2
	SETZ CTLV1,
CPHR1A: SETZ COUNT,
	MOVEI CTLV11,24
	JSR RN
	SOJGE CTLV11,.-1
	JSR CHRIN
	JRST CPHR3	;CR TYPED
	ADD XT1,CH
	JRST CPHR1A+1

CPHR3: CAIG COUNT,6
	JRST TOOSMALL
	ADD XT3,XT4
	TDZ XT3,MASK
	ADD XT1,XT2
	TDZ XT1,MASK
	TROE F,INF	;FIRST TIME THRU?
	JRST CHKC	;NO - COMPARE 2 CIPHERS
	MOVEM XT1,PASSW	;YES - SAVE CIPHER
	MOVEM XT3,PASSW+1
	TRO F,INF
	TTCALL 3,[ASCIZ/
ENTER IT AGAIN: /]
	JRST AGN+1

TOOSMALL: TTCALL 3,[ASCIZ/
PASSWORD MUST CONTAIN A MINIMUM OF SIX CHARACTERS
TO PROVIDE MAXIMUM SECURITY.
/]
	JRST AGN

CHKC: CAMN XT1,PASSW
	CAME XT3,PASSW+1
	JRST NOMATCH
	CLOSE TTY,
	RELEASE TTY,
	MOVE 16,[XWD ACBLK,0]
	BLT 16,16
	JRST @17

NOMATCH: TRZ F,INF
	TTCALL 3,[ASCIZ/
THE TWO PASSWORDS DO NOT MATCH.
/]
	JRST AGN

BADCHR: TTCALL 3,[ASCIZ/
ILLEGAL CHAGACTER.
/]
	JRST AGN

RN:	0
	MOVE INDEX,CTLV1
	MOVE A,XT1(INDEX)
	TLNE A,40
	XOR A,[XWD 777700,0]
	MULI A,6065
	DIV A,[XWD 37,777761]
	TDZ A,MASK
	TDZ B,MASK
	MOVE A,B
	ADD A,XT2(INDEX)
	TDZ A,MASK
	MOVE B,A
	TDZ A,MASK2
	MOVE CTLV1,A
	MOVE A,XT1(INDEX)
	MOVEM B,XT1(INDEX)
	MOVEM A,XT1+1(INDEX)
	EXCH XT5,XT1
	JRST @RN

CHRIN: 0
	SOSG TTYI+2
	INPUT 1,
	ILDB CH,TTYI+1
	JUMPE CH,.-3
	CAIN CH,15
	JRST @CHRIN
	CAIGE CH,40
	JRST CHRIN+1
	AOJ COUNT,
	AOS ,CHRIN
	CAIN CH,73
	JRST BADCHR
	SUBI CH,40
	JRST @CHRIN

TTYI: BLOCK 3
WORD1: XWD 27,657537
WORD2: XWD 55,555555
MASK: XWD 777700,000000
MASK2: XWD 777777,777774
ACBLK:	BLOCK 17
	END
