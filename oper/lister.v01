;PROGRAM TO DO LISTING WITH HEADING AND ? CHR CONVERT
DEFINE  VERSION(P,V),<TITLE LISTER RELEASE P'.'V'.
        LOC 137
        XWD     P,V     ;OUR PATCH AND DIGITEC VERSION NUMBER
        RELOC>

        VERSION \1,\114
;ACS

P=17
C=16    ;TTY INPUT CHRS
CS=15   ;STATUS BITS
CF=14   ;FILE INPUT STATUS
CL=13   ;FINLE INPUT CHRS
CCNT=12 ;CHARACTER COUNT IN OUTPUT LINE

T5=5
T4=4
T3=3    ;TEMPS
T2=2
T1=1
FL=0    ;FLAGS

;FLAGS

TTYSW==1        ;OUTPUT ON TTY
LISTSW==2       ;CURRENTLY PRINTING
NEEDHD==4       ;WE NEED TO PRINT A HEADING LINE
IDF==10 ;SCAN SAW AN IDENT
NUMF==20        ;SCAN SAW A NUMBER
TERMF==40       ;SCAN SAW A TERMINATOR (LF, OR ALT)
ANYPR==100      ;WE HAVE DONE SOME PRINTING
PGIND==200      ;SAYS PAGE SPECIFICATIONS SEEN
SWFLG==1000     ;WE ARE SCANNING A SWITCH SO DIGIT-LET IS NOT AN ID
RPGF==2000      ;WE ARE READING A COMMAND FILE
TEMPF==4000     ;THE COMMAND FILE IS A TEMPCOR ONE
TTYTOP==10000   ;TTY IS AT TOP OF FORM

;LEFT HALF FLAGS
CR96==1         ;OUTPUTTING ON 96 CHR DEVICE
NOHED==2        ;WE DO NOT WANT HEADINGS
LOWTOUP==4      ;CONVERT LOWER CASE TO UPPER CASE
FORTRN==10      ;SPECIAL CARRAIGE CONTROL CHRS
C64==20         ;IGNORE ? TYPE CHRS
NOSEQ==40       ;DO NOT PRINT SEQUENCE NUMBERS
SKIPOK==100     ;SPECIAL PRINTER CONTROL CHRS SHOULD GO THROUGH
TTYFM==200      ;THIS TTY CAN DO FORM FEEDS
STREXT==400     ;WE HAVE SEEN A * FOR EXTENSION
STRNAM==1000    ;WE HAVE SEEN A * FOR FILE NAME
STRINP==STREXT!STRNAM   ;PROCESSING * REQUEST
LPTDSK==2000    ;GOING TO PRINTR
SECINI==4000    ;HAVE PRINTED ON LPT-PRINTR REINIT
STRSET==10000   ;HAVE MOVED REST OF TEXT INTO CORE FOR *
DTA==20000      ;UFD DEVICE IS A DECTAPE
;DEVICES

TTY=1
INF=2
OUTF=3
CMND==4 ;RPG COMMANDS
CMND2==5;FOR DELETING RPG FILE
UFD==6  ;UFD READING DEVICE

EXTERNAL JOBFF,JOBREL,JOBSA

%LPP==^D58
LPTLIN==^D132   ;LENGTH OF LINE ON LPT
TTYLIN==^D72    ;LENGTH OF LINE ON TTY

STANSW==0       ;CONTROLS PPNS ETC
TEMPSW==1
TMPCOR==44      ;TEMPCORE UUO

MOD35==1B<14+18>        ;THIS IS A MODEL 35 WITH FORM FEEDS
CTLFBT==1B<13+18>       ;CONTROL F ON TTY (MODEL 37)
SYSNUM==20      ;NUMBER OF ELEMENT FOR SYSTEM NUMBER
CNFTBL==11      ;TABLE WITH CONFIG INFO
DVTTY==10       ;BIT TO SAY TTY IN DEVCHR
DVLPT==40000    ;BIT TO SAY LPT IN DEVCHR
DVDSK==200000   ;DEVICE IS A DSK
DIRBT==4        ;DEVICE HAS A DIRECTORY
DTABT==100      ;DEVICE IS A DTA
CALPPN==24      ;GET PRJPRG NUMBER
DEFINE  ERR(T),<JRST[OUTSTR [ASCIZ "T"]
                EXIT]>
HISEG
STPT:   TDZA FL,FL      ;SET TO 0
        MOVEI FL,RPGF   ;IN RPG MODE IF STARTED HERE
        RESET
        MOVE T1,[XWD HIDATA,INOPN]
        BLT T1,INOPN+LHDAT-1
        JUMPN FL,INRPG  ;SET UP RPG IF FLAG ON
        INIT TTY,1
        SIXBIT /TTY/
        TIBUF
        EXIT
        INBUF TTY,2
RPGRET: MOVE T1,JOBFF
        MOVEM T1,SVJFF#
        MOVE P,[IOWD 40,PDL]
RDOFL:
        TLZ FL,CR96!TTYFM!LPTDSK!STRINP!SECINI!STRSET   ;WE DON'T KNOW YET
        TRNN FL,RPGF    ;IF NOT IN RPG MODE
        OUTSTR  [ASCIZ /
*/]             ;PUT OUT *
        SETZM INPTR#    ;DO NOT READ IN CORE STUFF
        SETZM SAVCHR    ;CLEAR OUT SCANNER
        PUSHJ P,GETNAM  ;READ A FILE NAME FOR OUTPUT
        TLNE    FL,STRINP       ;NO * PERMITTED IN OUTPUT NAME
        ERR     <* NOT ALLOWED IN OUTPUT NAME>
        CAIE C,"_"      ;MUST FOLLOW
        ERR     <_ DOES NOT FOLLOW FILE NAME>
        SKIPN T1,SVDEV  ;DID HE GIVE A DEVICE
        MOVSI T1,'DSK'          ;NO USE DSK
        MOVEM T1,OUTDEV ;PUT IN OPEN
        DEVCHR  T1,     ;GET DEVCHRS
        MOVEI T3,LPTLIN ;SET UP FOR LINE LENGTH
        TLNE T1,DVTTY   ;IS IT A TTY
        JRST    [MOVNI T1,1
                TTCALL 6,T1     ;GET LINE CHARACTERISTICS
                TLNE T1,CTLFBT  ;IS IT A 37
                TLO FL,CR96
                TLNE T1,MOD35   ;CAN IT FORM
                TLO FL,TTYFM    ;YES, REMEMBER IT
                MOVEI T3,TTYLIN ;SET LINE LENGTH
                TRO FL,TTYSW    ;AND A TTY
                JRST OPNOUT]    ;GO
        TLNN T1,DVLPT   ;IS IT AN LPT
        JRST OPNOUT     ;NO
        TLNE T1,DVDSK   ;IF ALSO A DSK
        TLO FL,LPTDSK   ;THEN GOING TO PRINTR
        MOVE T1,[XWD SYSNUM,CNFTBL]
        GETTAB  T1,     ;GET SYSTEM NUMBER
        CAIN T1,^D53    ;IF 53
        TLO FL,CR96     ;ASSUM 96 CHR LPT FOR NOW
OPNOUT: MOVEM T3,LNLNG# ;FOR OVERRUN COUNTING
        OPEN OUTF,OUTOPN        ;OPEN IT
        JRST ODEVNA     ;NOT THERE
        ENTER OUTF,NAMBLK       ;AND NAME
        JRST OFNA       ;LOSE
        MOVEI T1,OUTBLK
        EXCH T1,JOBFF   ;SET UP OUTPUT BUFFERS
        OUTBUF OUTF,2   ;SET BUFFERS
        MOVEM T1,JOBFF  ;RESET JOBFF
RDINFL: TLNE FL,STRINP  ;IF DOING * REQUEST
        JRST    [SOS LINESP     ;SINCE WE WILL AOS
                JRST NOCHNG]    ;DO NOT RESET FLAGS
        SETZM COPIES#   ;INITIALIZE THE STATE OF THINGS
        SETZM FORMS#
        SETZM LINESP#
        TLZ FL,FORTRN!NOHED!LOWTOUP!C64!NOSEQ!SKIPOK
        TRZ FL,LISTSW!PGIND!ANYPR!NEEDHD!TTYTOP ;IN CASE NXTNAM FINDS NOTHING
NOCHNG: PUSHJ P,NXTNAM  ;GET ANOTHER NAME
        TRZ FL,LISTSW!PGIND!ANYPR!NEEDHD!TTYTOP
        SETZM STRTPG#   ;STARTING PAGE
        MOVSI T1,377777
        MOVEM T1,ENDPG# ;AND ENDING PAGE
        TLNE FL,LPTDSK  ;IF TO PRINTR
        TLON FL,SECINI  ;AND THIS IS AT LEAST SECOND TIME
        JRST NORINT
        RELEASE OUTF,0  ;RE-INIT
        OPEN OUTF,OUTOPN
        JRST ODEVNA
        MOVEI T1,OUTBLK
        EXCH T1,JOBFF
        OUTBUF OUTF,2
        MOVEM T1,JOBFF
NORINT: TLNN FL,STRSET  ;HAVE WE MOVED THE REST OF THE TEXT?
        JRST TRYSW      ;NO, JUST PROCEED
RSTPT:  MOVE T1,INP1    ;SET UP POINTERS SO WE READ IT
        MOVEM T1,INPTR
        PUSHJ P,SCAN    ;TO GET THINGS STARTED
TRYSW: CAIE C,"/"
        JRST NOSW
        MOVEI T2,1
        TRO FL,SWFLG
        PUSHJ P,SCAN    ;LOOK FOR NUMBER
        TRZ FL,SWFLG
        TRNE FL,NUMF    ;IF IT IS
        PUSHJ P,SCAN    ;SCAN AGAIN FOR LETTER
        MOVE T1,ACCUM   ;NUMBER IN T2 LETTER? IN T1
        TRNN FL,IDF
        JRST CMDERR     ;LOSE IF NOT LETTER
        MOVSI T3,-SWLNG ;GET TABLE LENGTH
        CAME T1,SWTAB(T3)
        AOBJN T3,.-1
        JUMPGE T3,CMDERR        ;NONE FOUND
        XCT EXTAB(T3)   ;DO IT
        PUSHJ P,SCAN
        JRST TRYSW      ;LOOK FOR ANOTHER
NOSW:   TLNE FL,STRINP  ;IF DOING STAR REQUEST
        TLNE FL,STRSET  ;AND STRING NOT MOVED YET
        SKIPA
        JRST SETSTR     ;THEN MOVE IT
        CAIE C,"("      ;WERE ANY SPECIFIED
        JRST NOPG
        TRO FL,PGIND
        PUSHJ P,SCAN    ;YES, GET ONE
        CAIE T2,0       ;PAGE 0 IS ILLEGAL BECAUSE IT WILL CAUSE TROUBLE
        TRNN FL,NUMF    ;SHOULD BE A NUMBER
        JRST CMDERR
        MOVEM T2,STRTPG
        MOVEM T2,ENDPG  ;IN CASE ONLY ONE
        PUSHJ P,SCAN
        CAIE C,":"      ;MORE??
        JRST NOPG
        PUSHJ P,SCAN
        CAML T2,STRTPG  ;END PAGE MUST BE >= STARTPAGE
        TRNN FL,NUMF
        JRST CMDERR
        MOVEM T2,ENDPG
        PUSHJ P,SCAN
DEFINE TABLE
<X D,<MOVEM T2,LINESP>
X P,<TLO FL,FORTRN>
X H,<TLO FL,NOHED>
X L,<TLO FL,LOWTOUP>
X Q,<TLO FL,C64>
X N,<TLO FL,NOSEQ>
X S,<TLO FL,SKIPOK>
X C,<MOVEM T2,COPIES>
X F,<MOVEM T2,FORMS>
>
NOPG:  SKIPN NAMBLK    ;MUST GIVE A NAME
        JRST NONAM
        SKIPN T1,SVDEV  ;DEVICE?
        MOVSI T1,'DSK'
        MOVEM T1,INDEV
        OPEN INF,INOPN
        JRST IDEVNA
        LOOKUP INF,NAMBLK
        JRST IFNA
        MOVEI T1,INBLK
        EXCH T1,JOBFF
        INBUF INF,2
        MOVEM T1,JOBFF  ;SET UP INPUT BUFFERS
        MOVEI T1,1
        AOS LINESP      ;NO, SET TO 1
        MOVEM T1,LOGPG# ;SET UP CURRENT PAGE NUMBER
        CAMG T1,ENDPG   ;SHOULD WE BE PRINTING
        CAMGE T1,STRTPG
        JRST SKPON
        TRO FL,NEEDHD   ;IF NO PGMK WILL BE FIRST SEEN
        PUSHJ P,GOLST
SKPON:  MOVE T3,[POINT 7,HDBUF] ;SET UP HEADER BUFFER
        MOVEI T4,PUTR   ;PLACE TO PUT CHRS
        MOVE T2,NAMBLK  ;PUT IN NAME
        PUSHJ P,OUTSIX
        HLLZ T2,NAMBLK+1        ;GET EXT
        JUMPE T2,NOEXT
        MOVEI T1,"."
        IDPB T1,T3      ;PUT IN BUFFER
        PUSHJ P,OUTSIX
NOEXT:  MOVEI T2,5      ;5 SPACES
        MOVEI T1," "
        IDPB T1,T3
        SOJG T2,.-1
        DATE    T1,     ;GET DATE
        IDIVI T1,^D31   ;LEAVE DAY IN T2
        PUSH P,T1       ;SAVE MOUNTH AND YEAR
        MOVEI T1,1(T2)
        PUSHJ P,DECPR   ;PRINT DAY
        MOVEI T1,"-"
        IDPB T1,T3
        POP P,T1
        IDIVI T1,^D12   ;LEAVE MONTH IN T2 YR IN T1
        PUSH P,T1
        MOVE T2,MONTAB(T2)      ;GET MONTH
        PUSHJ P,OUTSIX  ;PRINT IT
        MOVEI T1,"-"
        IDPB T1,T3
        POP P,T1        ;THE YEAR
        ADDI T1,^D64
        PUSHJ P,DECPR
        MOVEI T1," "
        IDPB T1,T3
        MSTIME  T1,     ;GET TIME
        IDIVI T1,^D60000        ;GET IN MINUTES
        IDIVI T1,^D60   ;NOW HOURS
        PUSH P,T2       ;SAVE MINUTES
        PUSHJ P,DECPR
        MOVEI T1,":"
        IDPB T1,T3
        POP P,T1
        MOVEI T2,"0"    ;PUT IN A ZERO
        CAIG T1,^D9     ;IF LESS THAN 10
        IDPB T2,T3
        PUSHJ P,DECPR
        MOVEI T1,15     ;FINISH OFF
        IDPB T1,T3
        MOVEI T1,12
        IDPB T1,T3
        IDPB T1,T3
        MOVEI T1,0
        IDPB T1,T3
       SETZM LSTCNT#   ;LINE LOCATION SO WILL PUT OUT A HEADER
        SETZM PHYSPG#
        MOVE CCNT,LNLNG         ;SET UP FOR A LINE
        SETZM CNTR              ;NOT REPLAYING A FAKE PAGE MARK
        TRNN    FL,LISTSW       ;NO LIST, NO HEADING. THAT MAKES SENCE
        JRST    LSTLP
        PUSHJ   P,PRNTHD
        PUSHJ   P,LGNCH
        JRST    RDEOF           ;SHOULD RATHER SAY "FILE EMPTY"
        TLNE    FL,FORTRN
        CAIE    CL,"1"          ;TOP OF FORM IN FORTRAN
        CAIN    CL,"L"-100      ;CONTROL SHIT L OTHERWISE
        SKIPA
        JRST    LSTLP1
LSTLP:  PUSHJ P,LGNCH   ;GET A CHR
        JRST RDEOF      ;END OF FILE
LSTLP1: PUSHJ   P,LOUCH   ;PUT IT OUT
        JRST LSTLP      ;AND GO BACK FOR MORE

LGNCH:  SKIPE CNTR#     ;CHECK SPECIAL COUNTER FOR ALMOST PAGE MARKS
        JRST    [SOSG CNTR      ;LAST CHR?
                JRST WASPAG     ;GO PICK IT UP
                ILDB CL,PNTR    ;GET CHARACTER
                JRST CPOPJ1]    ;AND RETURN
LGNC1:  SOSG INPBUF+2   ;ANY THERE?
        JRST RDIN       ;NO, READ SOME MORE
DNRD:   IBP INPBUF+1    ;ADVANCE POINTER
        MOVE CF,@INPBUF+1       ;GET THAT WORD
        CAMN CF,PGMK    ;CHECK FOR PAGE MARK
        JRST ISPAG      ;AND TREAT IS SPECAILLY
        TLNE FL,NOSEQ   ;IS SUPRESSING SEQUENCE NUMS
        TRZN CF,1       ;OR IF THIS IS NOT ONE
        JRST WASPAG     ;GO GET CHR
        MOVEM CF,@INPBUF+1      ;PUT IT BACK WITH BIT OFF
        REPEAT 5,<        PUSHJ P,LGNCH ;AND SKIP THOSE CHRS
                POPJ P, ;OUT OF CHRS RETURN>
        JRST LGNCH      ;GET THE NEXT ONE
WASPAG: LDB CL,INPBUF+1 ;GET THE CHR
        JUMPE CL,LGNCH  ;IGNORE NULLS
CPOPJ1: AOS(P)          ;SKIP RETURN
        POPJ P,

RDIN:   INPUT INF,0
        STATZ INF,20000
        POPJ P,         ;EOF IS NON-SKIP RETURN
        STATO INF,740000
        JRST DNRD       ;GOT IT
        OUTSTR  [ASCIZ /
**ERROR ON INPUT FILE**/]
        EXIT            ;INPUT ERROR

ISPAG:  MOVEI CF,1      ;TURN OFF THE BIT
        ANDCAM CF,@INPBUF+1
        MOVEI CF,6      ;SET UP IN CASE NOT A PAGE MARK
        MOVEM CF,CNTR
        MOVE CF,[POINT 7,REPLAY]        ;POINT AT CORRECT TEXT
        MOVEM CF,PNTR#
        REPEAT 5,<        PUSHJ P,LGNC1
        POPJ P,>        ;SKIP THE SPACES
        CAIE CL,15      ;STILL LOOK LIKE A PAGE MARK?
        JRST LGNCH      ;NO
        AOS CNTR        ;ONE MORE CHR
        PUSHJ P,LGNC1
        POPJ P,
        CAIE CL,15      ;STILL?
        JRST LGNCH
        AOS CNTR
        PUSHJ P,LGNC1
        POPJ P,
        CAIE CL,14      ;FINALLY
        JRST LGNCH      ;NOT QUITE A PAGE MARK
        SETZM CNTR      ;IT WAS, FORGET THIS JUNK
        MOVEI CL,200    ;SPECIAL CHR FOR PAGE MARK
        SETZM PHYSPG    ;0 PHYSICAL PAGE
        AOS CF,LOGPG    ;AND ADVANCE LOGICAL PAGE
TRYNOW: CAML CF,STRTPG
        PUSHJ P,GOLST
        CAMG CF,ENDPG   ;PAST ENDING
        JRST CPOPJ1
        TRZ FL,LISTSW   ;NO LISTING
        CAIN C,")"
        POPJ P, ;END OF SPEC. PRETEND EOF
        CAIE C,","
        JRST CMDERR     ;LOSE LOSE
        PUSHJ P,SCAN    ;GET NEXT PAGE NUMBER
        TRNN FL,NUMF
        JRST CMDERR
        MOVEM T2,ENDPG
        MOVEM T2,STRTPG
        PUSHJ P,SCAN    ;GET DELIMITER
        CAIE C,":"
        JRST TRYNOW     ;CHECK RANGE NOW
        PUSHJ P,SCAN
        CAML T2,STRTPG
        TRNN FL,NUMF    ;SHOULD BE ANOTHER NUMBER
        JRST CMDERR
        MOVEM T2,ENDPG
        PUSHJ P,SCAN
        JRST TRYNOW

DEFINE  TEXT(T),<IRP T,<
        SIXBIT  /T/>>

BINEXT: TEXT    <REL,LOW,HGH,BIN,SAV,SHR>

REPLAY: BYTE (7) 40,40,40,40,40,15,15,14

RDEOF:  TRNN FL,ANYPR   ;DID WE PRINT ANYTHING
        JRST RDEOF1     ;NO
        TRNN FL,TTYSW   ;IF A TTY
        TRNN FL,NEEDHD  ;OR NOT AT END OF PAGE
        PUSHJ P,HEAD1A  ;DO THE FORM FEED STUFF
RDEOF1: MOVE P,[IOWD 40,PDL]    ;JUST TO GET BACK IN SYNC
        TLNE FL,STRINP  ;IF DOING STAR REQUEST
        JRST RELINF     ;JUST LET GO AND PROCEED
        TRNE FL,TERMF   ;IF TERMINATOR
        JRST CKTR2      ;THEN CHECK THE STATE OF THINGS
        TRNN FL,PGIND   ;ARE WE READING PAGES??
        JRST CKTR3      ;NO, SHOULD FIND A COMMA OR TERM
RDEOFR: CAIN C,")"      ;ARE WE ATE END OF PAGE SPEC
        JRST SCNCOM     ;NO
        TRNE FL,TERMF   ;THIS IS WHEN WE SCANN FOR ENDING )
        JRST CMDERR     ;OUT OF STUFF TOO SOON
        PUSHJ P,SCAN
        JRST RDEOFR     ;AND TRY AGAIN
SCNCOM: PUSHJ P,SCAN
CKTR3:  CAIE C,","      ;IF A COMMA
        JRST CKTRM
RELINF: RELEASE INF,0
        JRST RDINFL
CKTRM:  TRNN FL,TERMF   ;CHECK TERMINATOR
        JRST CMDERR     ;THAT WAS THE LAST CHANCE
CKTR2:  RELEASE OUTF,0
        RELEASE INF,0   ;JUST IN CASE
        JRST RDOFL      ;GET A NEW OUTPUT FILE
GOLST: TRON FL,LISTSW!ANYPR    ;IF JUST STARTING
        TLNN FL,LPTDSK  ;AND TO PRINTR
        POPJ P,
        PUSH P,CL
        MOVEI CL,177    ;SET SPECIAL INFORMATION
        PUSHJ P,POCHR
        MOVEI CL,1
        PUSHJ P,POCHR
        SKIPN T1,FORMS  ;DOES HE WANT SPECIAL FORMS?
        JRST NOFORM
        MOVEI CL,2      ;CODE IS 2
        PUSHJ P,OUTCOD  ;DUMP IT
NOFORM: SKIPN T1,COPIES ;DOES HE WANT COPIES
        JRST NOCOPS
        MOVEI CL,3
        PUSHJ P,OUTCOD
NOCOPS: MOVE T3,INDEV   ;GET DEVICE
        DEVCHR  T3,
        TLNN T3,DIRBT   ;IF A DIRECTORY DEVICE
        JRST NODIR
        LDB T1,[POINT 23,SVEXT+1,35]    ;GET TIME AND DATE
        MOVEI CL,4      ;CODE IS 4
        PUSHJ P,OUTCOD
NODIR:  MOVE T1,SVNAM
        MOVEI CL,5      ;CODE FOR NAME IS 5 AND ALWAYS THERE
        PUSHJ P,OUTCOD
        HLLZ T1,SVEXT
        JUMPE T1,GNOEXT ;EXTENSION?
        MOVEI CL,6      ;CODE OF 6
        PUSHJ P,OUTCOD
GNOEXT: TLNN T3,DIRBT   ;IS DEVICE A DSK??
        JRST NONDSK     ;NO
        SKIPN T1,INPPN  ;GET PPN
        GETPPN  T1,     ;OR HIS IF NONE GIVEN
        MOVEI CL,7      ;CODE IS 7
        PUSHJ P,OUTCOD
NONDSK: MOVEI CL,10
        PUSHJ P,POCHR   ;END CODE
        POP P,CL        ;RESTORE CHR
        POPJ P,         ;AND GO

OUTCOD: PUSHJ P,POCHR   ;DUMP CODE BITS
OUTC1:  MOVEI T2,0      ;CONVERT TO OCTAL
        LSHC T1,-3
        ROT T2,3
        HRLM T2,(P)
        SKIPE T1
        PUSHJ P,OUTC1
        HLRZ CL,(P)
        ADDI CL,30      ;PUT OUT AS C30-37
        JRST POCHR
LOUCH: TRNN FL,LISTSW  ;ARE WE LISTING??
        POPJ P,         ;GO AWAY
        MOVE CF,CTBL(CL)        ;GET THE DISPATCH ADRESS
        XCT ACTAB(CF)   ;AND GO

ACTAB:  REPEAT 3,<        JRST CONCTL>  ;FORM CONTROL CHRS
        REPEAT 5,<        JRST SKPCTL>  ;SOMETIMES FORM CONTROL
        POPJ P, ;NULL
        JRST QCHR       ;PRINT WITH ?
        JRST TAB        ;A TAB
        JRST CRTN               ;A CRTN
        JRST ONECHR     ;JUST PRINT IT
        JRST QMARK      ;A ?
        JRST Q96CHR     ;PRINT WITH ? IF NOT 96 CHR OUTPUT DEF
        JRST LOWER      ;A LOWER CASE LETTER
        JRST PGMKR      ;A PAGE MARK CHARACTER

SKPCTL: TLNN FL,SKIPOK!FORTRN   ;DO WE WANT THESE TO GO THROUGH
        JRST QCHR       ;NO PRINT THE ? EQUIVALENTS
CONCTL: TRNE FL,NEEDHD  ;DO WE WANT A HEADING PRINTED?
        PUSHJ P,PRNTHD  ;YES, PRINT ONE AND RESET THINGS
        MOVE T2,LSTCNT  ;FOR FORM FEED
        XCT ACT2(CF)    ;FURTHER DISPATCH
        JRST MULLF      ;SIMULATE FORM CONTROL

ACT2:   JRST LF
        MOVEI T3,VTBSP  ;SET UP FOR SPECIAL SPACING
        JRST FORM
        MOVEI T3,DLESP
        MOVEI T3,DC1SP
        MOVEI T3,DC2SP
        JRST DC3        ;VERY SPECIAL HANDELING
        MOVEI T3,DC4SP

VTBSP==<%LPP+2>/3
DLESP==<%LPP+2>/2
DC1SP==<%LPP+2>/^D30
DC2SP==<%LPP+2>/^D20
DC4SP==<%LPP+2>/6

CR1:    MOVEI CL,15     ;PUT OUT A SPECIAL CR
ONECHR: TRNE FL,NEEDHD  ;IS A HEADING WANTED?
        PUSHJ P,PRNTHD
        SOJL CCNT,INSCR ;SHOULD WE PUT IN A CRLF?
POCHR:
ONECH2: SOSG OBUF+2
        JRST DOOUT
DNOUT:  IDPB CL,OBUF+1
CPOPJ:  POPJ P,

DOOUT:  OUTPUT OUTF,0
        STATO OUTF,740000
        JRST DNOUT
        OUTSTR  [ASCIZ /
*****OUTPUT DEVICE ERROR***/]
        EXIT
LOWER: TLNE FL,CR96    ;IS IT 96 CHR OUTPUT
        JRST ONECHR     ;JUST PRINT IT
        TLNN FL,C64!LOWTOU      ;IF CONVERTING OR NO ? EFFECTS
        JRST QCHR       ;ITS SPECIAL ELSE PRINT WITH ?
        SUBI CL,40      ;CONVERT TO UPPER CASE
        JRST ONECHR     ;AND PRINT

DC3:    MOVEI CL,15     ;PRINT ONE OF THESE
        PUSHJ P,ONECH2
        MOVE CCNT,LNLNG
        MOVEI CL,23     ;AND ONE OF THESE
        TRNE FL,TTYSW
        MOVEI CL,12     ;BUT LF IF ON TTY
        PUSHJ P,ONECH2
        CAIE CL,13      ;ALSO ASSUME CAN DO VTAB
        SOS T2,LSTCNT   K LINE NUMBER
        CAMGE T2,[-5]   ;AND IF VERRRRY SMALL
        TRO FL,NEEDHD!TTYTOP    ;PRINT A HEADER NEXT TIME
        POPJ P,

MULLF:  MOVE T1,LSTCNT
        IDIV T1,T3      ;FIND OUT HOW MANY LINES WE GET
        SKIPN T2
        MOVE T2,T3      ;IF 0 THE WHOLE BUNCH
FORM:   MOVE CCNT,LNLNG ;RESET FOR A CR
        SKIPG LSTCNT    ;IF NUMBER IS NEG OR 0
        JRST HEADGO     ;PRINT A HEADER
        CAIE CL,13
        CAIN CL,14      ;IF FORM FEED
        TLNN FL,TTYFM!NOHED     ;AND TTY CAN, THEN DO ONE
        TRNN FL,TTYSW   ;IF TTY
        JRST LPTMUL     ;FAKE IT ELSE JUST PRINT
        PUSHJ P,MULTLN  ;THAT MANY
        AOJA CCNT,CR1   ;FAKE A  CR

LPTMUL: PUSHJ P,ONECH2  ;PRINT THAT CHR
        MOVNS T2        ;AND UPDATE LINE COUNT
        ADDB T2,LSTCNT
        JUMPLE T2,PRNTH0        ;PRINT A HEADER TTY AT TOP OF FORM
        POPJ P,

LF:     MOVE CCNT,LNLNG
        JUMPLE T2,HEADGO
        CAMLE T2,LINESP ;MULTIPLE SPACING NUMBER
        MOVE T2,LINESP  ;USE SMALL ER OF THAT AND LINES LEFT
MULTLN: PUSHJ P,LFD1    ;A SINGLE LINE FEED
        SOJG T2,.-1
        POPJ P, ;DONE

LFD1:   MOVEI CL,12
        PUSHJ P,ONECHP  ;ADD TO CCNT AND PRINT
        SOSG LSTCNT     ;CHECK FOR OUT OF ROOM
        TRO FL,NEEDHD   ;AND JUST SET FLAG
        POPJ P,

PGMKR:  TRNN FL,TTYSW
        TRNN FL,NEEDHD  ;IF NEEDED ANYWAY
        JRST HEAD1      ;THIS GIVE A FORM AND HEADER
        JRST PRNTHD     ;ALREADY THERE JUST HEADER

INSCR:  PUSH P,CL       ;SAVE THE CHARACTER
        PUSHJ P,DC3     ;AND AN EVERY LINE CODE
        POP P,CL        ;GET BACK
        JRST ONECHR     ;AND GO

HEADGO: MOVEI CL,15     ;PRINT CR,FL
        PUSHJ P,ONECH2
        MOVEI CL,12
        PUSHJ P,ONECH2
        SOS LSTCNT      ;IN CASE THIS IS A TTY
        TRO FL,NEEDHD   ;AND SET FLAG. LPT WILL BE THERE, TTY WILL GO THERE
        POPJ P,
QCHR:  TLNE FL,C64     ;SHOULD WE IGNORE THESE?
        POPJ P,         ;CONSIDER IT DONE
QCHR1:  MOVEI CL,"?"    ;PRINT THE ?
        PUSHJ P,ONECHR
        LDB CL,[POINT 7,CF,17]  ;GET ALT CHR
        JRST ONECHR

TAB:    SUB CCNT,LNLNG  ;ADJUST SO WE DON'T CARE WHAT IT WAS
        TRZN CCNT,7     ;MOVE TO A MULTIPLE OF 8
        SUBI CCNT,10
        ADD CCNT,LNLNG
ONECHP: AOJA CCNT,ONECHR        ;GO PRINT (CORRECT FO SOJL)

CRTN:   TLNE FL,FORTRN
        JRST FORTCR     ;SPECIAL IN FORTRAN MODE
CR2:    MOVE CCNT,LNLNG ;RESET COUNT
        JRST ONECH2     ;AND JUST PRINT

QMARK:  TLNE FL,C64     ;IF IGNORING ? CHRS
        JRST ONECHR     ;JUST PRINT IT
        JRST QCHR1      ;ELSE PRINT 2 OF THEM

Q96CHR: TLNN FL,CR96    ;IS IT A 96 CHR PRINTER
        JRST QCHR       ;NO PRINT A ? CHR
        JRST ONECHR     ;YES, JUST PRINT IT

DEFINE MONTHS (A)
<IRP A <SIXBIT /A/>>

MONTAB: MONTHS <JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC>

FORTCR: PUSHJ P,CR2     ;PUT OUT THE CR
        REPEAT 2,<
        PUSHJ   P,LGNCH
        JRST RDEOF>     ;SKIP THE LFD AND GET CONTROL CHR
        CAIL CL,"*"
        CAILE CL,"3"    ;IF OUT OF RANGE
        JRST    [MOVEI CL,12    ;PRINT A LFD    SPACE GOES HERE TOO
                JRST LOUCH]
LOUCH2: XCT FCCL-"*"(CL)        ;THE CONTROL TABLE
        JRST LOUCH      ;MOST JUST LOAD A CHR
FCCL:   MOVEI CL,23     ;*
        POPJ P,         ;+  DOES NOTHING BUT THE CR
        MOVEI CL,21     ;,
        JRST FORMIN     ;-
        MOVEI CL,22     ;.
        MOVEI CL,24     ;/
        JRST FORZRO     ;0
        MOVEI CL,14     ;1
        MOVEI CL,20     ;2
        MOVEI CL,13     ;3

FORMIN: MOVEI CL,12     ;THREE LFDS
        PUSHJ P,LOUCH
FORZRO: MOVEI CL,12     ;TWO LFDS
        PUSHJ P,LOUCH
        MOVEI CL,12
        JRST LOUCH
HEAD1: PUSHJ P,HEAD1A
PRNTHD: TRNN FL,TTYTOP  ;IF TTY AT TOP
        TRNN FL,TTYSW           ;OR NOT A TTY
        SKIPA           ;THEN SKIP
        PUSHJ P,HEAD1A  ;ELSE GET TO TOP OF FORM
PRNTH0: TLNE FL,NOHED   ;DO WE WANT TO NOT PRINT?
        JRST NOOUTH     ;YES, SKIP THIS CODE
        PUSH P,CL       ;SAVE IT
        MOVEI CL,15
        PUSHJ P,POCHR   ;GET TO LEFT MARGIN
        MOVE T1,[POINT 7,[ASCIZ /PAGE /]]
        PUSHJ P,OUTASC  ;SET UP
        MOVE T1,LOGPG   ;PRINT OUT THE PAGE NUMBER
        MOVEI T4,POCHR  ;SET TO CALL THE OUTPUT ROUTINE
        MOVEI T5,^D26   ;MANY SPACES FOR LPT
        TRNE FL,TTYSW   ;IS IT TTY
        MOVEI T5,^D10   ;USE FEWER ON TTY
        PUSHJ P,DECPR   ;THE PRINT ROUTINE
        MOVEI CL,"-"    ;AND SEPERATE
        PUSHJ P,POCHR
        AOS T1,PHYSPG   ;NEXT PHYSICAL PAGE
        PUSHJ P,DECPR
        TRZE T5,7       ;CONVERT TO NUMBER OF TABS
        ADDI T5,10
        LSH T5,-3
        JUMPE T5,DNTAB
SPOLP:  MOVEI CL,11
        PUSHJ P,POCHR
        SOJG T5,SPOLP
DNTAB:  MOVE T1,[POINT 7,HDBUF]
        PUSHJ P,OUTASC  ;PRINT ASCII FOR HEADER
        POP P,CL        ;GET CHR BACK
        SKIPA T1,[%LPP] ;RESET LSTCNT
NOOUTH: MOVEI T1,%LPP+2 ;GET 2 FREE LINES IF NO HEADING
        MOVEM T1,LSTCNT
        MOVE CCNT,LNLNG ;SAY WE DID A CR
        TRZ FL,NEEDHD!TTYTOP    ;DON'T NEED ONE ANY MORE
        POPJ P,         ;ALL DONE

HEAD1A: MOVEM CL,SVCL#
        TRO FL,TTYTOP
        TLNE FL,NOHED
        JRST    [MOVEI T2,2
                PUSHJ P,MULTLN
                JRST CPOPJL]
        TLNN FL,TTYFM   ;TTY CAN DO FORM FEED
        TRNN FL,TTYSW   ;IF NOT TTY
        JRST LPTH1      ;PRINT AFORM FEED
        MOVE T2,LSTCNT
        ADDI T2,3
        JUMPLE T2,HEAD1B        ;IN CASE OF 23'S
        PUSHJ P,HEAD1C
        MOVE T1,[POINT 7,[ASCIZ /--/]]
        PUSHJ P,OUTASC
HEAD1B: ADDI T2,3       ;REST OF THE LINE FEEDS
        MOVEI CL,15
        PUSHJ P,ONECH2
        JUMPLE T2,CPOPJL        ;JUST IN CASE
HEAD1C: MOVEI CL,12
        PUSHJ P,ONECH2
        SOJG T2,.-1
CPOPJL: MOVE CL,SVCL
        POPJ P,
LPTH1:  MOVEI CL,14
        PUSHJ P,ONECH2  ;DO A FORM FEED
        JRST CPOPJL

PUTR:   IDPB CL,T3      ;PUT CHRS IN BUFFER
        POPJ P,

OUTSIX: MOVEI T1,0
        LSHC T1,6       ;GET A CHR
        MOVEI CL,40(T1)
        PUSHJ P,(T4)    ;PUT IT OUT
        JUMPN T2,OUTSIX
        POPJ P,

DECPR:  IDIVI T1,^D10
        HRLM T2,(P)
        SKIPE T1
        PUSHJ P,DECPR
        HLRZ CL,(P)
        ADDI CL,"0"
        SOJA T5,(T4)

OUTASC:        ILDB CL,T1      ;GET NEXT CHR
        JUMPE CL,CPOPJ
        PUSHJ P,POCHR
        JRST OUTASC

CMDERR: OUTSTR  [ASCIZ /

****COMMAND ERROR***/]
        EXIT
IDEVNA: MOVE T2,INDEV
CDEVNA: OUTSTR  [ASCIZ /
DEVICE NOT AVAILABLE - /]
        MOVEI T4,PUTR
        MOVE T3,[POINT 7,ERRBUF]
        PUSHJ P,OUTSIX
COMERR: MOVEI T1,15
        IDPB T1,T3
        MOVEI T1,12
        IDPB T1,T3
        MOVEI T1,0
        IDPB T1,T3
        OUTSTR  ERRBUF
        EXIT

ODEVNA: MOVE T2,OUTDEV
        JRST CDEVNA

IFNA:   MOVEI T1,[ASCIZ /
CAN NOT FIND FILE - /]
COMFIL: DDTOUT  T1,
        MOVEI T4,PUTR
        MOVE T3,[POINT 7,ERRBUF]
        MOVE T2,NAMBLK
        PUSHJ P,OUTSIX
        HLLZ T2,NAMBLK+1
        JUMPE T2,COMERR
        MOVEI CF,"."
        IDPB CF,T3
        PUSHJ P,OUTSIX
        JRST COMERR

OFNA:   MOVEI T1,[ASCIZ /
CAN NOT ENTER FILE - /]
        JRST COMFIL
PGMK:  <ASCIZ /     />+1
NONAM:  OUTSTR  [ASCIZ /
INPUT FILE MUST HAVE NAME
/]
        EXIT
INRPG:
IFN TEMPSW,<
        HRRZ T1,JOBFF
        MOVEI T2,-1(T1)
        HRLI T2,-200
        MOVEM T2,TMPBF+1
        MOVSI T2,'LIS'
        MOVEM T2,TMPBF
        HRRM T1,CIBUF+1
        ADDI T1,200     ;CHECK TO SEE IF ENOUGH CORE
        CAMLE T1,JOBREL
        JRST    [IORI T1,1777
                CORE    T1,
                JRST STPT       ;GIVE UP
                JRST .+1]
        MOVE T1,[XWD 2,TMPBF]
        TMPCOR  T1,     ;GET THE FILE
        JRST RPGDSK     ;NO, TRY DSK
        ADDM T1,JOBFF   ;REMEMBER NEW END OF WORLD
        IMULI T1,5      ;GET NUMBER OF CHRS
        ADDI T1,1       ;FOR SOSG PROBLEMS
        MOVEM T1,CIBUF+2
        MOVSI T1,(POINT 7,0)    ;SET INPUT POINTER
        HLLM T1,CIBUF+1
        TRO FL,TEMPF    ;SET TEMP CORE FLAG
        JRST RPGRET     ;GO>
RPGDSK: INIT CMND,1     ;GET THE COMMAND DEVICE
        SIXBIT /DSK/
        CIBUF
        JRST STPT       ;GIVE UP IF NOT THERE
        PJOB    T1,     ;JOB NUMBER
        MOVEI T4,3      ;NUMBER OF DIGITS
INRPG1: IDIVI T1,12     ;CONVERT TO DECIMAL
        ADDI T2,20      ; USING T1,T2,T3, WHICH MUST BE
        LSHC T2,-6      ; CONTIGUOUS
        SOJG T4,INRPG1
        HRRI T3,'LIS'           ;REST OF COMMAND FILE NAME
        MOVEM T3,BLNKN  ;TO LOOKUP BLOCK
        MOVSI T3,'TMP'          ;EXTENTION
        MOVEM T3,BLNKN+1
        SETZM BLNKN+3   ;STANDRAD [P,P]
        LOOKUP CMND,BLNKN       ;SEE IF IT IS THERE FROM COMPIL
        SKIPA           ;NO, SO TRY FOR USER-SUPPLIED FILE
        JRST INRPG2     ;FOUND FILE .TMP
        LOOKUP CMND,RPGN        ;GET THE FILE
        JRST STPT
        JRST RPGRET     ;DO NOT DELETE THE USER'S COMMAND FILE
INRPG2: INIT CMND2,16   ;SECOND DEVICE IS FOR DELETING
        SIXBIT /DSK/
        0
        JRST STPT
        LOOKUP CMND2,BLNKN
        JRST STPT
        SETZM BLNKN
        SETZM BLNKN+1
        SETZM BLNKN+3
        RENAME CMND2,BLNKN
        JFCL
        RELEASE CMND2,0 ;NOW WE HAVE GOTTEN RID OF IT
        JRST RPGRET
RRPGN:  SIXBIT /QQLIST/
        SIXBIT /RPG/
        EXP 0,0
GETNAM:SETZM SVNAM     ;ZERO OUT CELLS IN CASE NOTHING
        SETZM SVEXT     ;GETS PUT THERE
        SETZM SVEXT+1
        SETZM SVPPN
        SETZM SVDEV
        SETZM INPPN#
        PUSHJ P,SCAN
        CAIN C,"*"      ;IF *
STRNM:  JRST    [TLO FL,STRNAM  ;ASSUME NAME AND FLAG IT
                PUSHJ P,SCAN    ;SCAN PAST
                JRST NODEV1]    ;AND PROCEED
        TRNN FL,IDF             ;WAS THE THING SCANNED AN IDENT
        JRST CMDERR     ;NO, LOSE
        PUSH P,ACCUM
        PUSHJ P,SCAN    ;CHECK FOR EXT OR PPN
        CAIE C,":"      ;IS IT A DEVICE NAME
        JRST NODEV      ;NO
        POP P,SVDEV#    ;WE WERE HIDING IT IN THE STACK
        PUSHJ P,SCAN    ;AND GET NEXT
        CAIN C,"*"
        JRST @STRNM
        TRNN FL,IDF     ;MUST BE AN IDENT
        POPJ P,
        PUSH P,ACCUM
        PUSHJ P,SCAN
NODEV:  POP P,SVNAM
NODEV1: CAIN C,"["      ;IS IT PPN
        JRST GETPPN
        CAIE C,"."      ;NO, EXT?
        POPJ P,         ;NEITHER, RETURN
        PUSHJ P,SCAN    ;GET EXT
        CAIN C,"*"
        JRST    [TLO FL,STREXT
                JRST GSEXT]
        TRNN FL,IDF
        JRST CMDERR
        MOVE T1,ACCUM
        HLLZM T1,SVEXT
GSEXT:  PUSHJ P,SCAN
        CAIE C,"["      ;CHECK FOR PPN AGAIN
        POPJ P,
GETPPN: PUSHJ P,SCAN
        TRNN FL,IDF
        JRST CMDERR
        MOVE T1,ACCUM
        PUSHJ P,RJUST   ;THIS NEED TO BE RIGHT JUSTIFIED
        HRLM T1,SVPPN   ;STORE LEFT HALF
        HRLM T1,INPPN   ;SAVE IT HERE TOO
        PUSHJ P,SCAN
        CAIE C,","
        JRST CMDERR
        PUSHJ P,SCAN
        TRNN FL,IDF
        JRST CMDERR
        MOVE T1,ACCUM
        PUSHJ P,RJUST
        HRRM T1,SVPPN
        HRRM T1,INPPN
        PUSHJ P,SCAN
        CAIE C,"]"
        JRST CMDERR
        JRST SCAN       ;ALL DONE

IFN STANSW,<
RJUST:  TRNE T1,77
        POPJ P, ;GET IT OVER THERE
        LSH T1,-6
        JRST RJUST>

IFE STANSW,<
RJUST:  MOVE T3,T1      ;CONVERT SIXBIT TO OCTAL
        MOVEI T1,0
CONVOC: MOVEI T2,0
        LSHC T2,6
        CAIL T2,"0"-40
        CAILE T2,"7"-40
        JRST CMDERR
        LSH T1,3
        IORI T1,40-"0"(T2)
        JUMPN T3,CONVOC
        POPJ P,>
SETSTR: TRNE FL,IDF!NUMF        ;NEITHER OF THESE SHOULD BE HERE
        JRST CMDERR
        MOVE T1,JOBFF   ;GET AMOUNT OF ROOM FOR THIS STUFF
        MOVE T1,JOBREL
        SUB T2,T1
        IMULI T2,5
        HRLI T1,(POINT 7,0)     ;TURN INTO BYTE POINTER
        MOVEM T1,INP1#  ;WE HAVE LEFT 1 WORD NOT COUNTED
        CAIE C,"("      ;IF A PAGE LIST
        JRST PUT1
PUTLP:  SOJLE T2,MOCO   ;OUT OF CORE
NXTPUT: IDPB C,T1       ;PUT IT AWAY
        TLNE CF,TERMF   ;CHECK FOR TERMINATION
        JRST CMDERR     ;ONE SHOULD NOT BE HERE
        CAIN C,")"      ;DONE??
        JRST PUT2       ;YES
        PUSHJ P,GNCH
        JRST PUTLP      ;MOVE MORE
PUT2:   PUSHJ P,GNCH
PUT1:   TLNE CF,IGNOR   ;SHOULD WE IGNORE THIS ONE??
        JRST PUT2       ;YES
        IDPB C,T1       ;PUT IN STRING
        MOVEI C,0
        IDPB C,T1       ;AND SET END
        TLO FL,STRSET   ;FLAG IT AS DONE
        JRST RSTPT      ;GO GET STARTED READING IT

MOCO:   MOVE T3,JOBREL
        ADDI T3,2000
        CORE    T3,
        JRST NOCOR
        ADDI T2,5*2000  ;MORE CHRS CAN NOW GO IN
        JRST NXTPUT

NOCOR:  OUTSTR  [ASCIZ /
***NOT ENOUGH CORE***/]
        EXIT

UFDER:  OUTSTR  [ASCIZ /
**ERROR READING DIRECTORY**/]
        EXIT
NONDV:  OUTSTR  [ASCIZ /
**DEVICE MUST HAVE A DIRECTORY**/]
        EXIT
UFDNR:  OUTSTR  [ASCIZ /
**CANNOT READ DIRECTORY**/]
        EXIT
UFDNA:  MOVE T2,UFDDEV
        JRST CDEVNA
NXTNAM:        TLNN FL,STRINP  ;IF NOT ALREADY DOING ONE
        JRST CKSTAR     ;THEN LOOK FOR A NEW ONE
RDUFD:  PUSHJ P,UFDNAM  ;GET ANOTHER NAME
        JRST UFDEOF     ;RAN OUT OF UFD
        JUMPE T1,RDUFD  ;NAME IS 0, TRY AGAIN
        TLNN FL,STRNAM  ;IF A WILD FIELD
        CAMN T1,SVSTNM  ;OR A MATCH
        SKIPA           ;ALL IS GO
        JRST RDUFD      ;TRY AGAIN
        HLLZS T2        ;GET RID OF POINTER
        TLNN FL,STREXT
        CAMN T2,SVSTXT  ;SAME FOR EXTENSION
        SKIPA
        JRST RDUFD
        MOVEM T1,SVNAM
        MOVEM T2,SVEXT
        TLNN    FL,STREXT
        JRST    RDU1
        MOVE    T1,[XWD -6,BINEXT]
        CAMN    T2,(T1)
        JRST    RDUFD
        AOBJN   T1,.-2
RDU1:   MOVE    T1,INPPN
        MOVEM T1,SVPPN  ;RESET PPN
        POPJ P,         ;GOT ONE
UFDEOF: RELEASE UFD,0   ;LET GO OF UFD
        TLZ FL,STRINP!STRSET    ;TURN OFF FLAGS
        MOVE T1,SVJFF
        MOVEM T1,JOBFF  ;RESET JOBFF
        JRST RDEOF1     ;AND PRETEND EOF WAS SEEN

UFDNAM: TLNE FL,DTA     ;IF THIS IS A DEC TAPE
        JRST DTAUFD     ;GO GET IT SPECIALLY
        PUSHJ P,UFDWD
        POPJ P,         ;OUT OF UFD
        JUMPE T3,UFDNAM
        MOVE T1,T3      ;GET FIRST WORD OF PAIR
        PUSHJ P,UFDWD
        POPJ P,
        MOVE T2,T3
        PUSHJ P,UFDWD
        POPJ P,
        PUSHJ P,UFDWD
        POPJ P,
UFDWD:  SOSLE UFDIB+2   ;IS THERE MORE??
        JRST OKUFD      ;YES, PICK IT UP
        INPUT UFD,0
        STATZ UFD,20000 ;IS IT EOF
        POPJ P,
        STATZ UFD,760000        ;OR ERROR
        JRST UFDER
OKUFD:  ILDB T3,UFDIB+1
        AOS(P)  ;SKIP RETURN
        POPJ P,

DTAUFD: MOVE T1,UFDPTR  ;PICK UP POINTER
        AOBJP T1,CPOPJ  ;IF DONE, RETURN
        MOVEM T1,UFDPTR ;SAVE UPDATED VERSION
        HLLZ T2,^D104(T1)       ;GET EXTENSION
        MOVE T1,^D82(T1)        ;AND NAME
        AOS(P)
        POPJ P,

CKSTAR: PUSHJ P,GETNAM  ;GET A NAME
        TLNN FL,STRINP  ;IF A * IN IT
        POPJ P,
        TLZ FL,DTA      ;NOT A DECTAPE
        MOVE T1,SVNAM
        MOVEM T1,SVSTNM#
        MOVE T1,SVEXT
        MOVEM T1,SVSTXT#        ;SAVE INFO
        SKIPN T1,SVPPN
        GETPPN  T1,     ;WE REALLY NEED HIS PPN
        MOVEM T1,INPPN
        MOVEM T1,SVNAM  ;SET UP FOR READING UFD
        SKIPN T1,SVDEV
        MOVSI T1,'DSK'
        MOVEM T1,UFDDEV ;SAVE DEVICE
        DEVCHR  T1,     ;GET BITS
        TLNN T1,DIRBT
        JRST NONDV      ;NON-DIRECTORY DEVICE NOT LEGAL
        TLNE T1,DTABT   ;IS IT A DTA
        JRST GETDTA     ;SET IT UP
        OPEN UFD,UFDOPN
        JRST UFDNA
        MOVE T1,[XWD 1,1]
        MOVEM T1,SVPPN
        MOVSI T1,'UFD'
        MOVEM T1,SVEXT
        LOOKUP UFD,SVNAM
        JRST UFDNR
        INBUF UFD,1
        JRST NXTNAM     ;GO GET FIRST ACCEPTABLE NAME

GETDTA: TLO FL,DTA
        OPEN UFD,UFDOPN
        JRST UFDNA
        USETI UFD,^D100 ;SET TO READ DIRECTORY
        INPUT UFD,0
        STATZ UFD,760000
        JRST UFDER
        MOVSI T1,-^D23  ;NUMBER OF POSSIBLE FILES+1
        HRR T1,UFDIB+1
        MOVEM T1,UFDPTR#
        JRST NXTNAM     ;GO READ



;A CHARACTER TABLE FOR USE ON TYPE IN AND TYPE OUT

;FLAGS USED IN CHARACTER TABLE

OPF==400000             ;THIS IS A SPECIAL CHARACTER
SNUMF==100000   ;THIS IS PART OF A NUMBER
IGNOR==200000   ;IGNORE THIS CHARACTER
TERM==40000             ;THIS IS A TERMINATOR

;THIS CHARACTER TABLE IS USED FOR SCANNING AND DISPATCHING
;WHEN PRINTING

        DEFINE CHRS (FLAGS,SIXBT,ALT,DISP)
<BYTE (5) <FLAGS>_-^D13 (6) SIXBT (7) ALT (18) DISP>
        DEFINE MCHR     (A,B,C,D)
<IRPC C <CHRS A,B,"C",D>>

;DISPATCH ADDRESSES
GO==10  ;POPJ
GQCH==11        ;TO QCHR
GTAB==12        ;TO TAB
GLF==0  ;TO LF
GVTB==1         ;TO VTAB
GFORM==2        ;TO FORM
GCR==13         ;TO CR
GDLE==3         ;TO DLE
GDC1==4         ;TO DC1
GDC2==5         ;TO DC2
GDC3==6         ;TO DC3
GDC4==7         ;TO DC4
GONE==14        ;TO ONECHR
GQMK==15        ;TO QMARK
G96==16         ;TO Q96CHR
GLOW==17        ;TO LOWER
GPG==20         ;TO PAGE MARK CODE

CTBL:   CHRS OPF!IGNOR,0,0,GO
        CHRS OPF,0,"!",GQCH
        CHRS OPF,0,42,GQCH
        MCHR OPF,0,#$%&'(,GQCH
        CHRS OPF!IGNOR,0,0,GTAB
        CHRS OPF!TERM,0,0,GLF
        CHRS OPF,0,0,GVTB
        CHRS OPF,0,0,GFORM
        CHRS OPF!IGNOR,0,0,GCR
        MCHR OPF,0,)*,GQCH
        CHRS OPF,0,"+",GDLE
        CHRS OPF,0,\",",GDC1
        CHRS OPF,0,"-",GDC2
        CHRS OPF,0,".",GDC3
        CHRS OPF,0,"/",GDC4
        MCHR OPF,0,012964=<>78,GQCH
SXBS:   CHRS OPF!IGNOR,0,0,GONE
        REPEAT 57-40,<        CHRS OPF,.-SXBS,0,GONE>
        REPEAT 71-57,<        CHRS SNUMF,.-SXBS,0,GONE>
        REPEAT 76-71,<        CHRS OPF,.-SXBS,0,GONE>
        CHRS OPF,.-SXBS,"?",GQMK
        CHRS OPF,.-SXBS,0,GONE
        REPEAT 132-100,<        CHRS 0,.-SXBS,0,GONE>
        REPEAT 137-132,<        CHRS OPF,.-SXBS,0,GONE>
        CHRS OPF,0,100,G96
        REPEAT 172-140,<        CHRS 0,.-SXBS-40,.-SXBS,GLOW>
        MCHR OPF,0,[:,G96
        CHRS OPF!TERM,0,"]",G96
        MCHR OPF,0,3\,GQCH
        CHRS OPF,0,0,GPG
SCAN:  TRZ FL,TERMF!NUMF!IDF   ;RESET FLAGS
        SKIPE C,SAVCHR# ;CHECK TO SEE IF WE LEFT ONE LAST TIME
        SKIPA CS,CTBL(C)        ;GET BITS
SL10:   PUSHJ P,GNCH    ;GET A CHR
        TLNE CS,IGNOR   ;SHOULD WE IGNORE IT
        JRST SL10       ;YES
        JUMPL CS,SL1    ;SPECIAL CHARACTER?
        MOVE T3,[POINT 6,ACCUM] ;SET TO SAVE IDENT
        SETZM ACCUM#
        TLNE CS,SNUMF   ;CHECK FOR NUMBER
        JRST SNUM1      ;AND GO RACING OFF TO NUMBER ROUTINE
SL2P:   TRO FL,IDF      ;IT IS AN IDENT
SL2:    LDB CS,[POINT 6,CS,10]  ;GET SIXBIT
        TLNE T3,770000  ;HAVE WE STORED ENOUGH?
        IDPB CS,T3      ;NO, STORE ANOTHER (RH OF CHR TABLE HAS SIXBIT)
        PUSHJ P,GNCH    ;CONTINUE
        JUMPG CS,SL2    ;CHECK FOR ANOTHER NUMBER OR LETTER
SOK1:   MOVEM C,SAVCHR  ;SAVE THE CHARACTER (MUST BE A SPECIAL CHR)
        MOVEI C,0       ;ZERO IN C FOR NUMBERS AND IDNETS
        POPJ P,

SL1:
        TLNE CS,TERM    ;CHECK FOR TERMINATOR
        TRO FL,TERMF    ;AND SET FLAG
        ANDI C,377      ;GET RID OF EXTRA BITS
        SETZM SAVCHR    ;ZERO SAVCHR FOR LATER
        POPJ P,         ;NO RETURN

SNUM1:  SETZB T1,T2     ;SET NUMBER ACCUMS TO 0
SN1A:   LDB CS,[POINT 6,CS,10]
        TLNE T3,770000  ;WILL STORE THE SIXBIT FOR FILE NAMES
        IDPB CS,T3      ;BUT ONLY IF LESS THAN 6
SN1B:   LSH T1,7        ;ACCUMULATE ASCII IN T1
        IOR T1,C
        IMULI T2,^D10   ;DECIMAL IN T2
        ADDI T2,-"0"(C)
        PUSHJ P,GNCH    ;GET NEXT AND CONTINUE
        JUMPLE CS,SOK2  ;CHECK FOR END OF NUMBER
       TLNE CS,SNUMF   ;CHECK FOR NUMBER
        JRST SN1A       ;CONTINUE SCANNING NUMBER
        TRNN FL,SWFLG   ;IF IN SWITCH MODE
        JRST SL2P       ;WE WOULD NOT GO HERE TO ACT LIKE AN IDENT
SOK2:   TRO FL,NUMF!IDF ;IT WAS A NUMBER
        LSH T1,1        ;CONVERT TO LINE NUMBER
        IOR T1,[<ASCIZ /00000/>+1]
        JRST SOK1       ;SAVE DELIM AND RETURN

GNCH:   SKIPE INPTR     ;IF SOMETHING IS SET UP
        JRST    [ILDB C,INPTR   ;GET CHR
                JUMPN C,GNCH2   ;0 IS END
                SETZM INPTR     ;SO SAY NO MORE
                JRST GNCH]      ;AND TRY REGULAR WAY
        TRNE FL,RPGF
        JRST RPGIN
GNCH1:  SOSG TIBUF+2
        INPUT TTY,0     ;GET MORE
        ILDB C,TIBUF+1
        JUMPE C,GNCH1
GNCH2:  MOVE CS,CTBL(C) ;GET STATUS
        POPJ P,

RPGIN:  SOSG CIBUF+2    ;SOME LEFT?
        JRST DORPGI
DNRPGI: ILDB C,CIBUF+1  ;GET ONE
        JUMPE C,RPGIN   ;IGNORE NULLS
        MOVE CS,CTBL(C)
        POPJ P,
DORPGI: IFN TEMPSW,<TRNE FL,TEMPF       ;IS IT A TEMPCORE FILE??
        EXIT    1,      ;YES, EXIT>
        INPUT CMND,0
        STATO CMND,760000
        JRST DNRPGI     ;NO ERRORS
        STATO CMND,20000        ;WAS IT EOF
        OUTSTR  [ASCIZ /

***ERROR READING COMMAND FILE***
/]
        EXIT    1,

SWTAB:  DEFINE X(A,B)
<<SIXBIT /A/>>
        TABLE
SWLNG==.-SWTAB
EXTAB:  DEFINE X(A,B)
<B>
        TABLE
ARRAY INBLK,OUTBLK[203*2+1]
ARRAY UFDIB,OBUF[3]
ARRAY INPBUF[3]
ARRAY TIBUF[3]
ARRAY HDBUF[7]
IFN TEMPSW,<ARRAY TMPBF[2]>
ARRAY NAMBLK[4]
ARRAY ERRBUF[10]
ARRAY BLNKN[4]
ARRAY CIBUF[3]

ARRAY PDL[41]
ARRAY RPGN[4]
        DEFINE DATA (NM,DAT)
<%A%==.
        DAT
        LVAR
        ARRAY NM[.-%A%]
        LVAR>
HIDATA: DATA INOPN,1
        DATA INDEV,<EXP 0,INPBUF>
        DATA UFDOPN,14
        DATA UFDDEV,<EXP 0,UFDIB>
        DATA OUTOPN,1
        DATA OUTDEV,<0
        XWD OBUF,0>
LHDAT==.-HIDATA
LVAR
SVNAM=NAMBLK
SVEXT=NAMBLK+1
SVPPN=NAMBLK+3
        LIT
        END STPT
  *@y´