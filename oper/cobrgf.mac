TITLE COBRGF FOR COBRG VERSION 1	
SUBTTL ROUTINE TO AID COBRG

;COPYRIGHT 1970 -- DIGITAL EQUIPMENT CORP., MAYNARD, MASS.


	ENTRY NUMJOB	;PICK UP JOB NUMBER
ENTRY FORMN	;FOR N-CARD
ENTRY FORMB	;FOR B-CARD
ENTRY FORMH1	;FOR FIRST H-CARD
ENTRY FORMH2	;FOR SECOND H-CARD
ENTRY FORMA	;FOR A-CARD
ENTRY FORMT	;FOR T-CARD
ENTRY FORML	;FOR L-CARD
ENTRY FORMI	;FOR I-CARD
ENTRY FORMO	;FOR O-CARD

;ACCUMULATORS

SW=0		;FLAGS
IP=1		;BYTE-POINTER TO INPUT FIELD
OP=2		;BYTE-POINTER TO OUTPUT FIELD
XP=3		;BYTE-POINTER TO LAST BYTE IN INPUT FIELD
CH=4		;INPUT CHARACTER
CT=5		;COUNTER
TEMP=6		;TEMP

TA=12		;TEMP
TB=13		;TEMP
TC=14		;TEMP
TD=15		;TEMP
TE=16		;TEMP

PP=17		;PUSH-DOWN POINTER


;FLAGS FOR SWITCH WORD

REGET==1B18	;GET PREVIOUS CHARACTER AGAIN
	SUBTTL GET CURRENT JOB NUMBER

;ENTER BY:
;	PUSHJ	PP,NUMJOB
;	ARG	0,<LOCATION TO WHICH JOB-NUMBER RETURNED>

NUMJOB:	CALLI	TA,30		;PJOB
	AOS	TB,(PP)		;BUMP RETURN PC
	MOVEM	TA,@-1(TB)	;RETURN JOB NUMBER
	POPJ	PP,
	SUBTTL FORMAT INPUT RECORDS

;FORMAT N-CARD

FORMN:	PUSHJ	PP,SETARG

	MOVEI	TA,6		;PROG-ID
	PUSHJ	PP,GETTXT
	MOVEI	TA,3		;BLOCKING FACTOR
	PUSHJ	PP,GETDEC
	MOVEI	TA,6		;INPUT DEVICE
	PUSHJ	PP,GETTXT
	MOVEI	TA,6		;OUTPUT DEVICE
	PUSHJ	PP,GETTXT
	MOVEI	TA,^D9		;INPUT VALUE-OF-ID
	PUSHJ	PP,GETTXT
	MOVEI	TA,^D9		;OUTPUT VALUE-OF-ID
	PUSHJ	PP,GETTXT
	MOVEI	TA,^D9		;DATE-WRITTEN
	PUSHJ	PP,GETTXT
	MOVEI	TA,^D20		;AUTHOR
	JRST	GETTXT

;FORMAT B-CARD

FORMB:	PUSHJ	PP,SETARG

	MOVEI	TA,1		;LEVEL
	PUSHJ	PP,GETTXT

	MOVEI	CT,3		;FIRST
FORMB1:	PUSHJ	PP,GETONE	;  THREE
	SOJG	CT,FORMB1	;  SPACINGS

	MOVEI	TA,1		;FOURTH SPACING
	PUSHJ	PP,GETTXT

	MOVEI	TA,3		;SIZE OF BREAK FIELD
	PUSHJ	PP,GETDEC
	MOVEI	TA,^D24		;NAME OF BREAK FIELD
	JRST	GETTXT
;FORMAT FIRST H-CARD

FORMH1:	PUSHJ	PP,SETARG

	MOVEI	TA,1		;PAGE COUNTER SIZE
	PUSHJ	PP,GETTXT
	MOVEI	TA,3		;PAGE COUNTER LOCATION
	PUSHJ	PP,GETDEC
	PUSHJ	PP,GETONE	;HEADER SPACING
	MOVEI	TA,1		;HEADER SKIPPING
	PUSHJ	PP,GETTXT
	MOVEI	TA,^D66+1	;HEADING COLS-1-60
	PUSHJ	PP,GETXT2
	MOVEI	TA,^D10		;RESETS
	JRST	GETTXT


;FORMAT SECOND H-CARD

FORMH2:	PUSHJ	PP,SETARG

	MOVEI	TA,^D66+1	;HEADING COLUMNS 67-132
	JRST	GETXT2


;FORMAT A-CARD

FORMA:	PUSHJ	PP,SETARG

	MOVEI	TA,1		;AC NUMBER
	PUSHJ	PP,GETTXT
	MOVEI	TA,2		;AC SIZE
	PUSHJ	PP,GETDEC
	MOVEI	TA,2		;AC DECIMAL PLACES
	PUSHJ	PP,GETDEC
	MOVEI	TA,^D24		;INPUT NAME
	PUSHJ	PP,GETTXT
	MOVEI	TA,^D24		;OUTPUT NAME
	JRST	GETTXT
;FORMAT T-CARD

FORMT:	PUSHJ	PP,SETARG

	MOVEI	TA,1		;AC NUMBER
	PUSHJ	PP,GETTXT
	MOVEI	TA,^D24		;OUTPUT NAME
	PUSHJ	PP,GETTXT
	MOVEI	TA,^D11		;LEVELS
	JRST	GETTXT


;FORMAT L-CARD

FORML:	PUSHJ	PP,SETARG

	MOVEI	TA,1		;WHEN TO LIST
	PUSHJ	PP,GETTXT
	MOVEI	TA,^D24		;INPUT NAME
	PUSHJ	PP,GETTXT
	MOVEI	TA,^D24		;OUTPUT NAME
	JRST	GETTXT

;FORMAT I-CARD OR O-CARD

FORMI:
FORMO:	PUSHJ	PP,SETARG

	PUSHJ	PP,GETKAR	;GET FIRST CHARACTER
	CAIE	CH," "		;IF SPACE OR
	CAIN	CH,11		;  TAB,
	JRST	FORMO2		;  OK
	CAIE	CH,"-"		;IF '-' OR
	CAIN	CH,"*"		;  '*',
	JRST	FORMO2		;  OK

	MOVEI	CH," "		;INSERT SPACE
	IORI	SW,REGET	;WILL GET THAT CHARACTER AGAIN

FORMO2:	IDPB	CH,OP
	MOVEI	TA,^D79+1	;NOW GET OTHERS
	FORMO3:	PUSHJ	PP,GETKAR
	CAIN	CH,15
	MOVEI	CH," "
	IDPB	CH,OP
	SOJG	TA,FORMO3
	POPJ	PP,
;GET TEXTUAL DATA, WHICH MUST BE FOLLOWED BY COMMA OR END-OF-LINE
;ENTER WITH SIZE OF FIELD IN TA

GETTXT:	PUSHJ	PP,GETKAR	;GET A CHARACTER
	CAIN	CH," "		;SKIP LEADING
	JRST	GETTXT		;  SPACES

GETXT1:	CAIE	CH,15		;END-OF-LINE?
	CAIN	CH,","		;  OR COMMA?
	JRST	GETXT3		;YES

	IDPB	CH,OP		;NO--STASH IT
GETXT2:	PUSHJ	PP,GETKAR	;GET NEXT ONE
	SOJG	TA,GETXT1	;LOOP UNTIL END-OF-FIELD

	CAIE	CH,15		;DID WE END WITH END-OF-LINE OR
	CAIN	CH,","		;  COMMA?
	POPJ	PP,		;YES--RETURN
	JRST	GETXT2		;NO--SKIP TO COMMA OR END-OF-LINE

GETXT3:	MOVEI	CH," "		;PAD FIELD WITH
	IDPB	CH,OP		;  SPACES
	SOJG	TA,GETXT3

	POPJ	PP,

;GET A SINGLE TEXTUAL CHARACTER, WHICH NEED NOT BE FOLLOWED BY COMMA.

GETONE:	PUSHJ	PP,GETKAR	;GET NEXT CHARACTER
	CAIN	CH,15		;CONVERT END-OF LINE TO
	MOVEI	CH," "		;  SPACE
	IDPB	CH,OP		;STASH IT

	PUSHJ	PP,GETKAR	;GET NEXT CHARACTER
	CAIE	CH,","		;IF NOT COMMA
	IORI	SW,REGET	;  GET IT AGAIN LATER

	POPJ	PP,
;GET DECIMAL DATA
;ENTER WITH DESIRED SIZE OF FIELD IN TA (NOT > 5).

GETDEC:	PUSHJ	PP,GETKAR	;GET NEXT CHARACTER
	CAIN	CH," "		;IGNORE LEADING
	JRST	GETDEC		;  SPACES

	MOVE	TC,TA		;SAVE TA
	MOVE	TD,[POINT 7,TEMP]; SET UP BYTE-POINTER TO SCRATCH

GTDEC1:	CAIE	CH,15		;END OF
	CAIN	CH,","		;  FIELD?
	JRST	GTDEC2		;YES

	CAIG	CH,"9"		;NO--DIGIT?
	CAIGE	CH,"0"
	JRST	GTDEC2		;NO

	IDPB	CH,TD		;YES--STASH IN TEMP

	PUSHJ	PP,GETKAR	;GET NEXT CHARACTER
	SOJG	TC,GTDEC1	;LOOP UNTIL FIELD SCANNED

GTDEC2:	JUMPLE	TC,GTDEC3	;COMPLETE FIELD?
	MOVEI	CH,"0"		;NO--
	IDPB	CH,OP		;  STASH
	SUBI	TC,1		;  LEADING
	SOJA	TA,GTDEC2	;  ZEROES

GTDEC3:	JUMPLE	TA,GTDEC5	;ANYTHING SEEN?
	MOVE	TD,[POINT 7,TEMP]; YES

GTDEC4:	ILDB	CH,TD		;COPY TEMP
	IDPB	CH,OP
	SOJG	TA,GTDEC4

GTDEC5:	IORI	SW,REGET	;SCAN
	JRST	GETXT2		;  UNTIL TERMINATOR
;SET UP ARGUEMENT LIST

SETARG:	MOVEI	TA,2		;SET TA TO
	EXCH	TA,-1(PP)	;  FIRST ARGUEMENT, AND
	ADDM	TA,-1(PP)	;  RESET RETURN LOCATION

	MOVE	IP,@0(TA)	;GET BYTE-POINTER TO INPUT FIELD
	MOVE	OP,@1(TA)	; "       "          OUTPUT FIELD
	MOVE	XP,@0(TA)	;CREATE BYTE-POINTER TO
	ADDI	XP,^D16		;  LAST
	JUMPGE	XP,STARG1	;  BYTE
	SUBI	XP,1		;  IN
	TLZ	XP,77B23	;  INPUT
	TLO	XP,1B23		;  FIELD

STARG1:	MOVEI	SW,0		;CLEAR SWITCHES

	PUSHJ	PP,GETKAR	;GET FIRST CHARACTER
	PUSHJ	PP,GETKAR	;GET POSSIBLE COMMA
	CAIE	CH,","		;WAS IT A COMMA?
	IORI	SW,REGET	;NO

	POPJ	PP,


;GET A CHARACTER FROM INPUT FIELD

GETKAR:	TRZE	SW,REGET	;SHOULD WE REGET A CHARACTER?
	JRST	GTKAR1		;YES

	CAMN	IP,XP		;NO--DONE WITH FIELD?
	JRST	GTKAR2		;YES

	ILDB	CH,IP
	POPJ	PP,

GTKAR1:	LDB	CH,IP		;GET PREVIOUS CHARACTER AGAIN
	POPJ	PP,

GTKAR2:	MOVEI	CH,15
	DPB	CH,IP
	POPJ	PP,


	END
