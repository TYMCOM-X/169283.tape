C	COPYRIGHT C 1972 BY CORE & CODE, INC.
	SUBROUTINE MN3ZZZ
C
C THIS ROUTINE IS THE MAIN PROGRAM FOR THE PROFILE/USER'S MODEL
C LINK OF PROFILE.  THE ROUTINE IS CALLED BY A DUMMY MAIN PROFILE/USER
C MAIN PROGRAM WHICH IS WRITTEN IN MACHINE LANGUAGE TO AVOID FORSE
C AUTOMATICALLY RESETTING ALL IO DEVICES.  THIS ROUTINE WILL
C PERFORM THREE FUNCTIONS: (1) INITIALLY DUMP A CHAIN FILE OF THE
C USER'S MODEL TO BE USED BY THE PROFILE SYSTEM DURING EXECUTION,
C (2) EXECUTE THE USER'S MODEL WHEN THE MODEL IS RUN, (3) DUMP A SAV
C FILE OF THE USER'S MODEL FOR LOADING AT A LATER DATE.
C
C
	COMMON /C18ZZZ/ IAGRUS(2),IOCTGR,IOCTUS
	COMMON /C17ZZZ/ LASLNK,LNKCOD,NJOBFF,INJFF,IRJFF,
     +  IRUNID,IRNAME(2),IREXT(2),IDNAME(2),IDEXT(2),KRAVE,KRISK,KROI,
     +  NEWJFF,NAMTP2(2),NEXTP2(2),NAMLK2(2),NEXLK2(2),NAMLK4(2),
     +  NEXLK4(2),NAMLK5(2),NEXLK5(2)
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C04ZZZ/ INPUT(320),INPOS,INSIZE,INCONT
	DIMENSION NAMRUN(2),NEXRUN(2)
	EQUIVALENCE (KRAVE,RAVE),(KRISK,RISK),(KROI,ROI)
	EXTERNAL RUNZZZ
	LNKOLD=LASLNK
	LASLNK=3
	IF(LNKOLD.NE.1)GO TO 400
	CALL SETZZZ(3)
	CALL PR2ZZZ(NAMRUN,NEXRUN)
	CALL TMPZZZ(NAMTP2(1))
	CALL JFFZZZ(NEWJFF)
	CALL C76ZZZ(ICHNM,NAMTP2(1),NAMTP2(2))
	CALL C76ZZZ(ICHNE,NEXTP2(1),NEXTP2(2))
	CALL OPMZZZ(2,ICHNM,ICHNE,0,0,0,IOK)
	CALL CNSZZZ(NEWJFF)
	CALL CLMZZZ(0)
	CALL C76ZZZ(ICHNM,NAMLK2(1),NAMLK2(2))
	CALL C76ZZZ(ICHNE,NEXLK2(1),NEXLK2(2))
	CALL CHNZZZ(0,ICHNM,ICHNE,IOCTGR,IOCTUS,-(NEWJFF-1))
400	IF(LNKCOD.EQ.2) GO TO 900
	CALL JFFZZZ(NJOBFF)
	CALL CR2ZZZ(RAVE,NPER*NOROW+NTRIAL*(MAXPRO+1)+NPER*MAXPFR
     +  +IRJFF-NJOBFF,IOK)
	IF(IOK.EQ.0)GO TO 410
	CALL SPKZZZ(13HOUT OF MEMORY,13,1,0,1)
	GO TO 910
410	KRAVE=KRAVE+IRJFF-NJOBFF
	KRISK=KRAVE+NPER*NOROW
	KROI=KRISK+NTRIAL*(MAXPRO+1)
	NUMPRO=MAXPRO+1
	CALL CR3ZZZ(3,9,RUNZZZ,RAVE,RISK,ROI,NPER,NOROW,NTRIAL,NUMPRO,
     +  NPER,MAXPFR)
	GO TO 910
900	CALL PSVZZZ
910	CALL C76ZZZ(ICHNM,NAMLK2(1),NAMLK2(2))
	CALL C76ZZZ(ICHNE,NEXLK2(1),NEXLK2(2))
	CALL CHNZZZ(0,ICHNM,ICHNE,IOCTGR,IOCTUS,INJFF-1)
	END
C
C
C
	SUBROUTINE RUNZZZ(RAVE,RISK,ROI,IRAVE,JRAVE,IRISK,JRISK,
     +  IROI,JROI)
C
C THIS ROUTINE CONTROLS THE SAMPLING OF ALL DATA VARIABLES, THE
C INITIALIZATION OF ALL SYSTEM DATA AND REPORT VARIABLES, THE
C CALLING OF THE TRIAL AND PERIOD SUBROUTINES, THE ROUTINES TO
C TABULATE INFORMATION GENERATED, THE ROUTINES TO PERFORM THE
C APPROPRIATE STATISTICS, AND THE PRINT ROUTINE.
C
C RAVE=STANDARD REPORT MATRIX
C RISK=RISK PROFILE MATRIX
C ROI=ROI MATRIX
C IRAVE=CURRENT STANDARD REPORT I INDEX MAXIMUM (NUMBER OF PERIODS)
C JRAVE=CURRENT STANDARD REPORT J INDEX MAXIMUM (NUMBER OF ROWS)
C IRISK=CURRENT RISK I INDEX MAXIMUM (NUMBER OF TRIALS)
C JRISK=CURRENT RISK J INDEX MAXIMUM (NUMBER OF PROFILES)
C IROI=CURRENT ROI I INDEX MAXIMUM (NUMBER OF PERIODS)
C JROI=CURRENT ROI J INDEX MAXIMUM (NUMBER OF LINES IN ROI REPORT)
C
	COMMON /C18ZZZ/ IAGRUS(2),IOCTGR,IOCTUS
	COMMON /C17ZZZ/ LASLNK,LNKCOD,NJOBFF,INJFF,IRJFF,
     +  IRUNID,IRNAME(2),IREXT(2),IDNAME(2),IDEXT(2),KRAVE,KRISK,KROI,
     +  NEWJFF,NAMTP2(2),NEXTP2(2),NAMLK2(2),NEXLK2(2),NAMLK4(2),
     +  NEXLK4(2),NAMLK5(2),NEXLK5(2)
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	COMMON /C02ZZZ/ IERREX,NOTRND,NOMULT,KSEED,NOCOPY,ITMPDT,
     +  NAMTP3(2),NEXTP3(2),ISTRON,IOLDTR,IOLDPR,IOLDRW,IBINON
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C15ZZZ/ ITIMON,ICONN(4),ICOMP(4)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C13ZZZ/ RMEAN(9),RDEV(9)
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	DIMENSION RISK(IRISK,JRISK),RAVE(IRAVE,JRAVE),ROI(IROI,JROI)
	EXTERNAL SM1ZZZ,SM2ZZZ,SM3ZZZ,SM4ZZZ,SM5ZZZ
	EXTERNAL SM6ZZZ,SM7ZZZ,M6DZZZ,SDUZZZ,ST6ZZZ,ST7ZZZ
	IF(ITIMON.EQ.2)CALL SETZZZ(1)
	IF(LNKCOD.NE.1)GO TO 1700
	IOUNIT=IPOUT
	IBINON=0
	KILRUN=0
	KILTRI=0
	KILPER=0
	PVRATE=RATE(1)
	FVRATE=RATE(2)
	ISEED=KSEED
	DO 100 I=1,IRISK
	DO 100 J=1,JRISK
100	RISK(I,J)=0.0
	DO 200 I=1,MAXVAR
200	D(I)=0.0
	DO 300 I=1,IRAVE
	DO 300 J=1,JRAVE
300	RAVE(I,J)=0.0
	LASPER=1
	NOTRND=0
	NOMULT=0
	CALL DSNZZZ(SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,ST6ZZZ,ST7ZZZ,1)
	IF(IERREX.LT.0)RETURN
	DO 1200 ITRIAL=1,NTRIAL
	IPER=1
	CALL DSNZZZ(SM1ZZZ,SM2ZZZ,SM3ZZZ,SM4ZZZ,SM5ZZZ,SDUZZZ,SDUZZZ,1)
	IF(NOTRND.LE.0)GO TO 350
	CALL DSNZZZ(SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,M6DZZZ,SDUZZZ,1)
	CALL DSNZZZ(SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,SM6ZZZ,SDUZZZ,1)
350	IF(NOMULT.GT.0)CALL DSNZZZ(SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,
     +  SDUZZZ,SM7ZZZ,1)
	DO 400 I=1,MAXROW
400	R(I)=0.0
	DO 500 I=1,IROI
	DO 500 J=1,JROI
500	ROI(I,J)=0.0
	PVVAL=0.0
	FVVAL=0.0
	CALL SETUP
	IF(KILRUN.EQ.1)RETURN
	IF(IBINON.EQ.1)GO TO 1700
	DO 900 IPER=1,NPER
	IF(IPER.EQ.1)GO TO 600
	CALL DSNZZZ(SDUZZZ,SM2ZZZ,SM3ZZZ,SM4ZZZ,SM5ZZZ,SDUZZZ,SDUZZZ,2)
	IF(NOTRND.GT.0)CALL DSNZZZ(SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,
     +  SM6ZZZ,SDUZZZ,2)
	IF(NOMULT.GT.0)CALL DSNZZZ(SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,SDUZZZ,
     +  SDUZZZ,SM7ZZZ,2)
600	CALL RULES
	IF(KILRUN.EQ.1)RETURN
	IF(IBINON.EQ.1)GO TO 1700
	DO 700 J=1,JRAVE
700	RAVE(IPER,J)=RAVE(IPER,J)+R(J)
	DO 800 I=1,MAXRSK
	IF(IRSKON(I).LT.1)GO TO 800
	IF(IRSKCL(I).NE.IPER)GO TO 800
	IF(IRSKRW(I).LE.JRAVE)RISK(ITRIAL,I+3)=R(IRSKRW(I))
800	CONTINUE
	IF(IPFRON(1).GE.1)CALL PVCZZZ(PVVAL,JRAVE)
	IF(IPFRON(2).GE.1)CALL FVCZZZ(FVVAL,JRAVE)
	IF(IPFRON(3).LE.0)GO TO 830
	DO 820 J=1,JROI
	IF(IPFRLN(J,3).EQ.0)GO TO 830
	IF(IPFRLN(J,3).LE.JRAVE)GO TO 810
	ROI(IPER,J)=0.0
	GO TO 820
810	ROI(IPER,J)=R(IPFRLN(J,3))
820	CONTINUE
830	IF(KILPER.EQ.1)GO TO 1000
900	CONTINUE
	IPER=NPER
1000	LASPER=MAX0(IPER,LASPER)
	IF(IPFRON(1).GE.1)RISK(ITRIAL,1)=PVVAL
	IF(IPFRON(2).GE.1)RISK(ITRIAL,2)=FVVAL
	IF(IPFRON(3).LT.1)GO TO 1100
	CALL RIGZZZ(ROIVAL,ROI,IROI,JROI)
	RISK(ITRIAL,3)=ROIVAL
1100	IF(KILTRI.EQ.1)GO TO 1300
1200	CONTINUE
	ITRIAL=NTRIAL
1300	TRIALS=FLOAT(MAX0(ITRIAL,1))
	IPER=LASPER
	DO 1400 I=1,IPER
	DO 1400 J=1,JRAVE
1400	RAVE(I,J)=RAVE(I,J)/TRIALS
	IF(IPFRON(3).LT.1)GO TO 1700
	DO 1500 J=1,JROI
	IF(IPFRLN(J,3).EQ.0)GO TO 1600
	IF(IPFRLN(J,3).LE.JRAVE)GO TO 1420
	DO 1410	I=1,IPER
1410	ROI(I,J)=0.0
	GO TO 1500
1420	DO 1430 I=1,IPER
1430	ROI(I,J)=RAVE(I,IPFRLN(J,3))
1500	CONTINUE
1600	CALL RIGZZZ(ROIVAL,ROI,IROI,JROI)
	RATE(3)=ROIVAL
1700	CALL C76ZZZ(ICHNO,NAMLK4(1),NAMLK4(2))
	CALL C76ZZZ(ICHNE,NEXLK4(1),NEXLK4(2))
	CALL CHNZZZ(0,ICHNO,ICHNE,IOCTGR,IOCTUS,INJFF-1)
	END
C
C
C
	SUBROUTINE DSNZZZ(SUB1,SUB2,SUB3,SUB4,SUB5,SUB6,SUB7,ISAMP)
C
C THIS ROUTINE WILL SCAN THE DATA VARIABLES CURRENTLY LOADED INTO
C THE DVAL (MVAL) VECTOR AND CALL THE APPROPRIATE COMPUTATION ROUTINE
C DEPENDING UPON THE TYPE OF EACH DATA VARIABLE.
C
C SUB1=EXTERNAL SUBROUTINE NAME FOR CONSATNT DATA
C SUB2=EXTERNAL SUBROUTINE NAME FOR VECTOR DATA
C SUB3=EXTERNAL SUBROUTINE NAME FOR DISCRETE DATA
C SUB4=EXTERNAL SUBROUTINE NAME FOR CONTINUOUS DATA
C SUB5=EXTERNAL SUBROUTINE NAME FOR NORMAL DATA
C SUB6=EXTERNAL SUBROUTINE NAME FOR TREND DATA
C SUB7=EXTERNAL SUBROUTINE NAME FOR MULTIVARIATE DATA
C ISAMP=FLAG TO CHECK SAMPLE CODE BEFORE CALLING (1=PER TRIAL, 2=PER
C	PERIOD)
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	COMMON /C02ZZZ/ IERREX,NOTRND,NOMULT,KSEED,NOCOPY,ITMPDT,
     +  NAMTP3(2),NEXTP3(2),ISTRON,IOLDTR,IOLDPR,IOLDRW,IBINON
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	IERREX=1
	IF(LASPT.LT.4)RETURN
	DO 800 I=4,LASPT,3
	IDTYPE=MVAL(I+1)
	GO TO (100,200,300,400,500,600,700),IDTYPE
100	CALL SUB1(I)
	GO TO 800
200	CALL SUB2(I)
	GO TO 800
300	IF(MVAL(MVAL(I-1)-1).GE.ISAMP)CALL SUB3(I)
	GO TO 800
400	IF(MVAL(MVAL(I-1)-1).GE.ISAMP)CALL SUB4(I)
	GO TO 800
500	IF(MVAL(MVAL(I-1)-1).GE.ISAMP)CALL SUB5(I)
	GO TO 800
600	CALL SUB6(I)
	GO TO 800
700	IF(MVAL(MVAL(I-1)-1).GE.ISAMP)CALL SUB7(I)
800	CONTINUE
	RETURN
	END
C
C
C
	SUBROUTINE SM1ZZZ(IPTLOC)
C
C THIS ROUTINE INSERTS A VALUE FOR A CONSTANT DATA VARIABLE INTO
C THE D VECTOR.
C
C IPTLOC=LOCATION IN DVAL (MVAL) VECTOR OF POINTER WORD SET ASSOCIATED
C        WITH THE DATA VARIABLE
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	D(MVAL(IPTLOC))=DVAL(MVAL(IPTLOC-1)-1)
	RETURN
	END
C
C
C
	SUBROUTINE SM2ZZZ(IPTLOC)
C
C THIS ROUTINE INSERTS A VALUE FOR A VECTOR DATA VARIABLE INTO THE
C D VECTOR DEPENDING UPON THE CURRENT VALUE OF IPER.
C
C IPTLOC=LOCATION IN DVAL (MVAL) VECTOR OF POINTER WORD SET ASSOCIATED
C        WITH THE DATA VARIABLE
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	LOCDAT=MVAL(IPTLOC-1)-1
	NOVAL=MAX0(IPER,1)
	NOVAL=MIN0(NOVAL,MVAL(LOCDAT))
	D(MVAL(IPTLOC))=DVAL(LOCDAT-NOVAL)
	RETURN
	END
C
C
C
	SUBROUTINE SM3ZZZ(IPTLOC)
C
C THIS ROUTINE INSERTS A RANDOMLY SAMPLED VALUE FOR A DISCRETE
C PROBABILITY DISTRIBUTION DATA VARIABLE INTO THE D VECTOR.
C
C IPTLOC=LOCATION IN DVAL (MVAL) VECTOR OF POINTER WORD SET ASSOCIATED
C        WITH THE DATA VARIABLE
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	D(MVAL(IPTLOC))=DSCZZZ(RNDZZZ(IDUMMY),MVAL(IPTLOC-1)-1)
	RETURN
	END
C
C
C
	SUBROUTINE SM4ZZZ(IPTLOC)
C
C THIS ROUTINE INSERTS A RANDOMLY SAMPLED VALUE FOR A CONTINUOUS
C PROBABILITY DISTRIBUTION DATA VARIABLE INTO THE D VECTOR.
C
C IPTLOC=LOCATION IN DVAL (MVAL) VECTOR OF POINTER WORD SET ASSOCIATED
C        WITH THE DATA VARIABLE
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	D(MVAL(IPTLOC))=CNTZZZ(RNDZZZ(IDUMMY),MVAL(IPTLOC-1)-1)
	RETURN
	END
C
C
C
	SUBROUTINE SM5ZZZ(IPTLOC)
C
C THIS ROUTINE INSERTS A RANDOMLY SAMPLED VALUE FOR A NORMAL PROBABILITY
C DISTRIBUTION DATA VARIABLE INTO THE D VECTOR.
C
C IPTLOC=LOCATION IN DVAL (MVAL) VECTOR OF POINTER WORD SET ASSOCIATED
C        WITH THE DATA VARIABLE
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	LOCDAT=MVAL(IPTLOC-1)-1
	D(MVAL(IPTLOC))=RNMZZZ(DVAL(LOCDAT-1),DVAL(LOCDAT-2))
	RETURN
	END
C
C
C
	SUBROUTINE SM6ZZZ(IPTLOC)
C
C THIS ROUTINE INSERTS A VALUE FOR A TREND DATA VARIABLE INTO THE D
C VECTOR.
C
C IPTLOC=LOCATION IN DVAL (MVAL) VECTOR OF POINTER WORD SET ASSOCIATED
C        WITH THE DATA VARIABLE
C
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION MVAL(3000),IPFLT(2)
	EQUIVALENCE (DVAL(1),MVAL(1))
	EQUIVALENCE (IPF,IPFLT(1)),(IPL,IPFLT(2))
	LOCDAT=MVAL(IPTLOC-1)-1
	NOPAIR=MVAL(LOCDAT-4)
	DO 30 I=1,2
	IPFLT(I)=MVAL(LOCDAT-(I-1)*2)/10000
	IF(IPFLT(I))10,20,30
10	IPFLT(I)=INT(D(-IPFLT(I))+.5)
	IF(IPFLT(I).LT.1)IPFLT(I)=1
	IF(IPFLT(I).GT.MAXCOL)IPFLT(I)=NPER
	GO TO 30
20	IPFLT(I)=MAX0(1,(I-1)*NPER)
30	CONTINUE
	KPER=MAX0(IPER,1)
	IF(KPER.LT.IPF)GO TO 500
	IF(KPER.GT.IPL)GO TO 500
	DF=D(MOD(IABS(MVAL(LOCDAT)),10000))
	DL=D(MOD(IABS(MVAL(LOCDAT-2)),10000))
	DS=XINZZZ(DF,IPF,DL,IPL,KPER)
	IF(NOPAIR.GT.0)GO TO 100
50	D(MVAL(IPTLOC))=DS
	RETURN
100	NOSTOP=(NOPAIR-1)*2
	NOSTAR=0
110	IF(MVAL(LOCDAT-5-NOSTAR).GE.IPF)GO TO 120
	NOSTAR=NOSTAR+2
	IF(NOSTAR.LE.NOSTOP)GO TO 110
	GO TO 50
120	IF(MVAL(LOCDAT-5-NOSTOP).LE.IPL)GO TO 130
	NOSTOP=NOSTOP-2
	IF(NOSTOP.GE.NOSTAR)GO TO 120
	GO TO 50
130	DFM=DVAL(LOCDAT-1)
	DLM=DVAL(LOCDAT-3)
	DSM=XINZZZ(DFM,IPF,DLM,IPL,KPER)
	NOCHK=NOSTAR
200	IF(MVAL(LOCDAT-5-NOCHK).GE.KPER)GO TO 300
	NOCHK=NOCHK+2
	IF(NOCHK.LE.NOSTOP)GO TO 200
	D(MVAL(IPTLOC))=XINZZZ(DVAL(LOCDAT-4-NOCHK),MVAL(LOCDAT-3-
     +  NOCHK),DLM,IPL,KPER)+(DS-DSM)
	RETURN
300	IF(NOCHK.GT.NOSTAR)GO TO 400
	D(MVAL(IPTLOC))=XINZZZ(DFM,IPF,DVAL(LOCDAT-6-NOCHK),MVAL(LOCDAT-
     +  5-NOCHK),KPER)+(DS-DSM)
	RETURN
400	D(MVAL(IPTLOC))=XINZZZ(DVAL(LOCDAT-4-NOCHK),MVAL(LOCDAT-3-
     +  NOCHK),DVAL(LOCDAT-6-NOCHK),MVAL(LOCDAT-5-NOCHK),KPER)+(DS-DSM)
	RETURN
500	IF(NOPAIR.GT.0)GO TO 600
550	D(MVAL(IPTLOC))=0.0
	RETURN
600	NOSTOP=(NOPAIR-1)*2
	NOSTAR=0
610	IF(MVAL(LOCDAT-5-NOSTAR).EQ.KPER)GO TO 620
	NOSTAR=NOSTAR+2
	IF(NOSTAR.LE.NOSTOP)GO TO 610
	GO TO 550
620	D(MVAL(IPTLOC))=DVAL(LOCDAT-6-NOSTAR)
	RETURN
	END
C
C
C
	SUBROUTINE SM7ZZZ(IPTLOC)
C
C THIS ROUTINE INSERTS A VALUE FOR A MULTIVARIATE PROBABILITY DISTRI-
C BUTION DATA VARIABLE INTO THE D VECTOR.  THE EQUATION IS OF THE FORM:
C
C	VALDEP=(A)+(B)*(VALIND)+(NORMALLY DISTRIBUTED MODIFIER)
C
C THE NORMALLY DISTRIBUTED MODIFIER HAS A MEAN=0, AND A VARIANCE=THE
C COMBINED VARIANCES OF A, B, E, AND THE COVAR AS FOLLOWS:
C
C	COMBINED VAR=(VAR A)+(VALIND)**2*(VAR B)+2*(VALIND)*(COVAR)+(E)
C
C THE STANDARD DEVIATION OF THE NORMALLY DISTRIBUTED MODIFIER IS SIMPLY
C THE SQUARE ROOT OF THE COMBINED VARIANCE.
C
C IPTLOC=LOCATION IN DVAL (MVAL) VECTOR OF POINTER WORD SET ASSOCIATED
C        WITH THE DATA VARIABLE
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	LOCDAT=MVAL(IPTLOC-1)-1
	VALIND=D(MVAL(LOCDAT-1))
	D(MVAL(IPTLOC))=DVAL(LOCDAT-2)+DVAL(LOCDAT-4)*VALIND+
     +  RNMZZZ(0.0,SQRT(ABS(DVAL(LOCDAT-3)+VALIND**2*DVAL(LOCDAT-5)+
     +  2.0*VALIND*DVAL(LOCDAT-7)+DVAL(LOCDAT-6))))
	RETURN
	END
C
C
C
	SUBROUTINE M6DZZZ(IPTLOC)
C
C THIS ROUTINE SAMPLES THE TWO TREND VARIABLE END DISTRIBUTIONS TO
C INSURE THAT THEY WERE SAMPLED DEPENDENTLY.
C
C IPTLOC=LOCATION IN DVAL (MVAL) VECTOR OF POINTER WORD SET ASSOCIATED
C        WITH THE DATA VARIABLE
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	LOCDAT=MVAL(IPTLOC-1)-1
	JSEED=ISEED
	CALL SAMPLE(MOD(IABS(MVAL(LOCDAT)),10000))
	ISEED=JSEED
	CALL SAMPLE(MOD(IABS(MVAL(LOCDAT-2)),10000))
	RETURN
	END
C
C
C
	SUBROUTINE ST6ZZZ(IPTLOC)
C
C THIS ROUTINE SETS UP THE REMAINING INFORMATION ASSOCIATED WITH A
C TREND VARIABLE IN THE DVAL (MVAL) VECTOR PRIOR TO SAMPLING.
C
	COMMON /C02ZZZ/ IERREX,NOTRND,NOMULT,KSEED,NOCOPY,ITMPDT,
     +  NAMTP3(2),NEXTP3(2),ISTRON,IOLDTR,IOLDPR,IOLDRW,IBINON
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	LOCDAT=MVAL(IPTLOC-1)-1
	DO 600 I=1,3,2
	LOCASS=LDZZZ(MVAL(IPTLOC),MOD(IABS(MVAL(LOCDAT-I+1)),10000))
	IF(LOCASS.LT.0)GO TO 600
	ITPASS=MVAL(LOCASS+1)
	GO TO (100,500,200,300,400,500,500),ITPASS
100	DVAL(LOCDAT-I)=DVAL(MVAL(LOCASS-1)-1)
	GO TO 600
200	DVAL(LOCDAT-I)=DSCZZZ(.50,MVAL(LOCASS-1)-1)
	GO TO 600
300	DVAL(LOCDAT-I)=CNTZZZ(.50,MVAL(LOCASS-1)-1)
	GO TO 600
400	DVAL(LOCDAT-I)=DVAL(MVAL(LOCASS-1)-2)
	GO TO 600
500	CALL BADZZZ(MVAL(IPTLOC),MVAL(LOCASS))
600	CONTINUE
	NOTRND=NOTRND+1
	RETURN
	END
C
C
C
	SUBROUTINE ST7ZZZ(IPTLOC)
C
C THIS ROUTINE CHECKS TO INSURE THAT THE VARIABLE REFERENCED BY A
C MULTIVARIATE DATA VARIABLE EXISTS AND IS A LEGAL TYPE.
C
C IPTLOC=LOCATION IN DVAL (MVAL) VECTOR OF POINTER WORD SET ASSOCIATED
C        WITH THE DATA VARIABLE
C
	COMMON /C02ZZZ/ IERREX,NOTRND,NOMULT,KSEED,NOCOPY,ITMPDT,
     +  NAMTP3(2),NEXTP3(2),ISTRON,IOLDTR,IOLDPR,IOLDRW,IBINON
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	DIMENSION 3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	LOCASS=LDZZZ(MVAL(IPTLOC),MVAL(MVAL(IPTLOC-1)-2))
	IF(LOCASS.LT.0)RETURN
	IF(MVAL(LOCASS+1).EQ.7)CALL BADZZZ(MVAL(IPTLOC),MVAL(LOCASS))
	NOMULT=NOMULT+1
	RETURN
	END
C
C
C
	FUNCTION LDZZZ(IDNO,IDWANT)
C
C THIS FUNCTION WILL LOCATE A GIVEN DATA VARIABLE IN THE POINTER
C SECTION OF THE DVAL (MVAL) VECTOR.
C
C IDNO=DATA VARIABLE NUMBER OF VARIABLE MAKING THE REFERENCE
C IDWAT=DATA VARIABLE NUMBER OF VARIABLE BEING REFERENCED
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	IF(LASPT.LT.4)GO TO 200
	DO 100 LDZZZ=4,LASPT,3
	IF(MVAL(LDZZZ)-IDWANT)100,300,200
100	CONTINUE
200	LDZZZ=-1
	CALL BADZZZ(IDNO,IDWANT)
300	RETURN
	END
C
C
C
	FUNCTION DSCZZZ(TSTPRB,ISTART)
C
C THIS ROUTINE SEARCHS DOWN THE DVAL (MVAL) VECTOR FOR THE POINT
C WHERE A DISCRETE PROBABILITY DISTRIBUTION DATA VARIABLE HAS A
C PROBABILITY GREATER THAN OR EQUAL TO THE PASSED IN PROBABILITY.
C
C TSTPRB=PROBABILITY TESTING FOR
C ISTART=LOCATION OF DATA VARIABLE VALUES IN DVAL
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	TOTPRB=0.0
	DO 100 I=1,39,2
	TOTPRB=TOTPRB+DVAL(ISTART-I)
	IF(TOTPRB.GE.TSTPRB)GO TO 200
	IF(TOTPRB.GE..995)GO TO 200
100	CONTINUE
200	DSCZZZ=DVAL(ISTART-I-1)
	RETURN
	END
C
C
C
	FUNCTION CNTZZZ(TSTPRB,ISTART)
C
C THIS ROUTINE SEARCHS DOWN THE DVAL (MVAL) VECTOR FOR THE POINT
C WHERE A CONTINUOUS PROBABILITY DISTRIBUTION DATA VARIABLE HAS A
C PROBABILITY GREATER THAN OR EQUAL TO THE PASSED IN PROBABILITY,
C AND THEN INTERPOLATES A VALUE FOR THE DATA VARIABLE.
C
C TSTPRB=PROBABILITY TESTING FOR
C ISTART=LOCATION OF DATA VARIABLE VALUES IN DVAL
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	DO 100 I=1,39,2
	IF(DVAL(ISTART-I).GE.TSTPRB)GO TO 200
	IF(DVAL(ISTART-I).GT..949)GO TO 200
100	CONTINUE
200	I=MAX0(I,3)
	CNTZZZ=DVAL(ISTART-I-1)-(DVAL(ISTART-I)-TSTPRB)/(DVAL(ISTART-I)-
     +  DVAL(ISTART-I+2))*(DVAL(ISTART-I-1)-DVAL(ISTART-I+1))
	RETURN
	END
C
C
C
	FUNCTION XINZZZ(V1,I1,V2,I2,INOW)
C
C THIS ROUTINE WILL CALCULATED AN INTERPOLATED VALUE BETWEEN TWO POINTS
C LOCATED AT V1 (TIME=I1) AND V2 (TIME=I2) ASSUMING A STRAIGHT LINE
C INTERPOLATION AND TIME=INOW.
C
	IF(I2.EQ.I1)GO TO 10
	XINZZZ=V1+(V2-V1)*FLOAT(INOW-I1)/FLOAT(I2-I1)
	RETURN
10	XINZZZ=V1
	RETURN
	END
C
C
C
	FUNCTION RNMZZZ(XMN,DEV)
C
C THIS ROUTINE WILL CALCULATED A RANDOMLY SAMPLED NORMAL PROBABILITY
C DISTRIBUTION VALUE WITH A MEAN=XMN AND A STANDARD DEVIATION=DEV
C UTILIZING THE ASSUMPTION THAT THE MEAN OF 12 VALUES SAMPLED FROM
C A UNIFORMLY DISTRIBUTED POPULATION IS A NORMALLY DISTRIBUTED QUANTITY.
C
	TSTPRB=0.0
	DO 100 I=1,12
100	TSTPRB=TSTPRB+RNDZZZ(IDUMMY)
	RNMZZZ=XMN+(TSTPRB-6.0)*DEV
	RETURN
	END
C
C
C
	SUBROUTINE PVCZZZ(CUMVAL,JRAVE)
C
C THIS ROUTINE WILL TAKE THE CURRENT VALUES IN THE R VECTOR FOR THE
C ROWS WHICH ARE TO BE DISCOUNTED, AND WILL PERFORM THAT OPERATION,
C ACCUMULATING THE RESULT INTO CUMVAL, WHICH IS RETURNED.
C
C CUMVAL=ACCUMULATED TOTAL PRESENT VALUE
C JRAVE=CURRENT STANDARD REPORT J INDEX MAXIMUM VALUE (NUMBER OF ROWS)
C
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	IF(TIME(1).LT.0.0)GO TO 100
	XTIME=TIME(1)
	GO TO 200
100	XTIME=D(INT(-TIME(1)+.5))
	IF(XTIME.LT.0.0)XTIME=0.0
200	DO 300 I=1,MAXPFR
	IF(IPFRLN(I,1).GT.JRAVE)GO TO 300
	IF(IPFRLN(I,1).EQ.0)GO TO 400
	CALL GRFZZZ(I,1,PVRATE,XTIME,FACTOR)
	CUMVAL=CUMVAL+R(IPFRLN(I,1))*FACTOR/(1.0+PVRATE)**(FLOAT(IPER)*
     +  XTIME)
300	CONTINUE
400	RETURN
	END
C
C
C
	SUBROUTINE FVCZZZ(CUMVAL,JRAVE)
C
C THIS ROUTINE WILL TAKE THE CURRENT VALUES IN THE R VECTOR FOR THE
C ROWS WHICH ARE TO BE COMPOUNDED, AND WILL PERFORM THAT OPERATION,
C ACCUMULATING THE RESULT INTO CUMVAL, WHICH IS RETURNED.
C
C CUMVAL=ACCUMULATED TOTAL PRESENT VALUE
C JRAVE=CURRENT STANDARD REPORT J INDEX MAXIMUM VALUE (NUMBER OF ROWS)
C
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	IF(TIME(2).LT.0.0)GO TO 100
	XTIME=TIME(2)
	GO TO 200
100	XTIME=D(INT(-TIME(2)+.5))
	IF(XTIME.LT.0.0)XTIME=0.0
200	DO 300 I=1,MAXPFR
	IF(IPFRLN(I,2).GT.JRAVE)GO TO 300
	IF(IPFRLN(I,2).EQ.0)GO TO 400
	CALL GRFZZZ(I,2,FVRATE,XTIME,FACTOR)
	CUMVAL=CUMVAL+R(IPFRLN(I,2))*FACTOR*(1.0+FVRATE)**
     +  (FLOAT(NPER-IPER)*XTIME)
300	CONTINUE
400	RETURN
	END
C
C
C
	SUBROUTINE RIGZZZ(ROIVAL,ROI,IROI,JROI)
C
C THIS ROUTINE WILL DISCOUNT THE CURRENT VALUES IN THE ROI MATRIX
C ON A TRIAL AND ERROR BASIS UNTIL IT CONVERGES ON A DISCOUNT RATE
C WHICH DISCOUNTS THE VALUES TO A PRESENT VALUE WHICH IS VERY CLOSE
C TO ZERO.  THE LIMITS OF THE SEARCH ARE 0% (.0) AND 1000% (10.0),
C AND THE CONVERGENCE PROCEEDS BY HALVING THE DIFFERENCE BETWEEN THE
C HIGH SIDE RATE AND THE LOW SIDE RATE.
C
C ROIVAL=THE BREAKEVEN ROI RATE WHICH IS RETURNED
C ROI=ROI MATRIX
C IROI=CURRENT ROI I INDEX MAXIMUM (NUMBER OF PERIODS)
C JROI=CURRENT ROI J INDEX MAXIMUM (NUMBER OF LINES IN ROI REPORT)
C
	COMMON /C13ZZZ/ RMEAN(9),RDEV(9)
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	COMMON /C14ZZZ/ MAXDAT,MAXTMP,MAXROW,MAXCOL,MAXTRI,
     +  MAXRSK,MAXPRO,  MAXPFR,IWDSIZ,MAXOUT,MAXIN ,MAXTIT,
     +  MAXTWD,MAXTCH,  MAXCWD,MAXCCH,MAXRWD,MAXRCH,MSTDAL,
     +  MAXROP,MPFRAL,  MAXRAL,MAXVAR,MAXDWD,MAXEXP,EXPMAX,
     +  MINEXP,EXPMIN,  NOCONT,LENAME,MAXCPP,ICOLWD,LENEXT,
     +  MAXCOP
	COMMON /PROZZZ/ ISEED,NTRIAL,ITRIAL,NPER,IPER,PVRATE,FVRATE,
     +  R(100),D(500),KILRUN,KILTRI,KILPER,IOUNIT,DFILE(2),RFILE(2)
	DIMENSION ROI(IROI,JROI)
	DO 10 J=1,JROI
	IF(IPFRLN(J,3).EQ.0)GO TO 20
	DO 10 I=1,IPER
	IF(ROI(I,J).LT.0.0)GO TO 30
10	CONTINUE
20	ROIVAL=1000.0
	RETURN
30	RTPLS1=1.0
	ADDON=1.0
	ITOPOK=0
	PERTIM=TIME(3)
	IF(PERTIM.GT.0.0)GO TO 100
	PERTIM=D(INT(-PERTIM+.5))
	IF(PERTIM.LT.0.0)PERTIM=0.0
100	PVTOT=0.0
	DO 400 J=1,JROI
	IF(IPFRLN(J,3).EQ.0)GO TO 500
	CALL GRFZZZ(J,3,RTPLS1-1.0,PERTIM,FACTOR)
	PERNOW=0.0
	DO 400 I=1,IPER
	PERNOW=PERNOW+PERTIM
400	PVTOT=PVTOT+ROI(I,J)*FACTOR/RTPLS1**PERNOW
500	IF(PVTOT)600,900,700
600	IF(RTPLS1.LE.1.0)GO TO 900
	RTPLS1=RTPLS1-ADDON
	ITOPOK=1
	GO TO 800
700	IF(RTPLS1.GT.10.999)GO TO 900
	RTPLS1=RTPLS1+ADDON
	IF(ITOPOK.EQ.0)GO TO 100
800	ADDON=ADDON*.5
	IF(ADDON.GT..0005)GO TO 100
900	ROIVAL=(RTPLS1-1.0)*100.0
	RETURN
	END
C
C
C
	SUBROUTINE GRFZZZ(JDSCLN,IRTYPE,ERATE,XTIME,FACTOR)
C
C THIS ROUTINE CALCULATES THE FACTOR BY WHICH CASH FLOWS MUST BE
C INCREASED (IF CONTINUOUS OR BEGINNING-OF-YEAR DISCRETE) TO EQUAL
C END-OF-YEAR DISCRETE FLOWS.  IF END-OF-YEAR, THE FACTOR IS 1.0
C I.E. NO CHANGE.
C
C JDSCLN=ROW NUMBER OF CASH FLOWS IN IDSCLN
C IRTYPE=REPORT TYPE (1=PV, 2=FV, 3=ROI)
C ERATE=EFFECTIVE ANNUAL RATE (DECIMAL FORM)
C XTIME=TIME PERIOD BETWEEN CASH FLOWS
C FACTOR=FACTOR RETURNED TO MULTIPLY CASH FLOWS BY.
C
	COMMON /C10ZZZ/ ITITLE(16,4),ICOLUM(2,52),IROW1(3,100),
     +  ISTDON,ISTDWD,ISTDLN(152),TIME(3),
     +  IPFRON(3),RATE(3),IPFRLN(5,3),IPFRIN(3),
     +  IRSKON(6),IRSKRW(6),IRSKCL(6),IRSKIN(6),
     +  NOROW,NOTIT,IDSCLN(5,3),IOPAGE,IODATE
	IF(IDSCLN(JDSCLN,IRTYPE))100,200,300
100	RATEN=ALOG(ERATE+1.0)
	XFACT=RATEN*XTIME
	IF(XFACT.LE.0.0)GO TO 200
	FACTOR=(EXP(XFACT)-1.0)/XFACT
	RETURN
200	FACTOR=1.0
	RETURN
300	FACTOR=(ERATE+1.0)**XTIME
	RETURN
	END
C
C
C
	SUBROUTINE BADZZZ(IDNO,IDWANT)
C
C THIS ROUTINE CONTROLS PRINTING THE ERROR MESSAGE THAT A DATA
C VARIABLE HAS ILLEGALLY REFERENCED ANOTHER DATA VARIABLE.
C
C IDNO=DATA VARIABLE NUMBER MAKING REFERENCE
C IDWANT=DATA VARIABLE NUMBER BEING REFERENCED
C
	COMMON /C02ZZZ/ IERREX,NOTRND,NOMULT,KSEED,NOCOPY,ITMPDT,
     +  NAMTP3(2),NEXTP3(2),ISTRON,IOLDTR,IOLDPR,IOLDRW,IBINON
	COMMON /C04ZZZ/ INPUT(320),INPOS,INSIZE,INCONT
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	CALL SPKZZZ(11HINPUT ERROR,11,1,0,1)
	CALL LODZZZ(ITTYO,IDNO,2,1)
	CALL OUTZZZ(2,22H ILLEGALLY REFERENCES ,1,22,3,0,ITTYO)
	CALL LODZZZ(ITTYO,IDWANT,3,0)
	CALL WZZZ(ITTYO,0)
	IERREX=-1
	RETURN
	END
C
C
C
	SUBROUTINE SDUZZZ(I)
C
C DUMMY SUBROUTINE USED TO REPLACE A DATA TYPE CALCULATION SUBROUTINE
C WHEN NO CALCULATIONS ARE REQUIRED.
C
C I=DUMMY POINTER WORD LOCATION
C
	RETURN
	END
C
C
C
	SUBROUTINE SAMPLE(IDNO)
C
C THIS ROUTINE WILL CONTROL SAMPLING OF ONE DATA VARIABLE WITH THE
C RESULTING VALUE INSERTED IN THE D VECTOR.
C
C IDNO=NUMBER OF DATA VARIABLE TO BE SAMPLED
C
	COMMON /C16ZZZ/ DVAL(3000),DTEMP(105),IDQTY,LASPT,LASDAT,
     +  NAMTYP(21)
	DIMENSION MVAL(3000)
	EQUIVALENCE (DVAL(1),MVAL(1))
	IF(LASPT.LT.4)RETURN
	DO 100 I=4,LASPT,3
	IF(MVAL(I)-IDNO)100,300,200
100	CONTINUE
200	RETURN
300	IDTYPE=MVAL(I+1)
	GO TO (310,320,330,340,350,360,370),IDTYPE
310	CALL SM1ZZZ(I)
	RETURN
320	CALL SM2ZZZ(I)
	RETURN
330	CALL SM3ZZZ(I)
	RETURN
340	CALL SM4ZZZ(I)
	RETURN
350	CALL SM5ZZZ(I)
	RETURN
360	CALL SM6ZZZ(I)
	RETURN
370	CALL SM7ZZZ(I)
	RETURN
	END
C
C
C
	SUBROUTINE PSVZZZ
C
C THIS ROUTINE WILL CONTROL THE SAVING OF A CORE IMAGE (FILE.SAV)
C OF THE CURRENT PROFILE MODEL SO THAT THE MODEL NEED NOT BE LOADED
C AGAIN BUT CAN BE EXECUTED DIRECTLY FROM THE DUMMY MAIN PROFILE
C PROGRAM WHEN ASKED "RULES FILE NAME?"  THE CORE IMAGE IS ALWAYS
C SAVED ON A FILE WITH THE EXTENSION ".SAV" AND ALL OPEN LOGICAL UNITS
C ARE CLOSED PRIOR TO THE SAVING OPERATION.
C
	COMMON /C02ZZZ/ IERREX,NOTRND,NOMULT,KSEED,NOCOPY,ITMPDT,
     +  NAMTP3(2),NEXTP3(2),ISTRON,IOLDTR,IOLDPR,IOLDRW,IBINON
	COMMON /C17ZZZ/ LASLNK,LNKCOD,NJOBFF,INJFF,IRJFF,
     +  IRUNID,IRNAME(2),IREXT(2),IDNAME(2),IDEXT(2),KRAVE,KRISK,KROI,
     +  NEWJFF,NAMTP2(2),NEXTP2(2),NAMLK2(2),NEXLK2(2),NAMLK4(2),
     +  NEXLK4(2),NAMLK5(2),NEXLK5(2)
	COMMON /C05ZZZ/ ITTYI,ITTYO,IOFILE,IPOUT,IRECNO(2),ISTRIP(2),
     +  IROUTE,IRTOPN,IFFPOS,IOCMD,IOCMDN(2),IOCMDX(2),ICMDOP,ITTYSK,
     +  ITTYOC,ITTYOH,ICMDGR,ICMDUS,ISYSGR,ISYSUS
	DIMENSION NAME(2),NAMEXT(2),NAMSAV(2)
	DATA NAMSAV/5HSAV  ,5H     /
	CALL ASKZZZ(11HFILE NAME? ,11,1,1)
	CALL GETZZZ(NAME,NAMEXT,NGROUP,NUSER)
	IF(NAMZZZ(NAME,NAMSAV).LE.0)RETURN
	CALL C76ZZZ(NEW6,NAME(1),NAME(2))
	CALL C76ZZZ(ISAV,NAMSAV(1),NAMSAV(2))
	CALL OPMZZZ(2,NEW6,ISAV,NGROUP,NUSER,0,IOK)
	IF(IOK.LT.0)GO TO 10
	IF(IRTOPN.EQ.1)CALL CLOZZZ(IROUTE)
	IRTOPN=0
	IPOUT=ITTYO
	KOLDTR=IOLDTR
	IOLDTR=0
	KCMDOP=ICMDOP
	ICMDOP=0
	ITTYSK=0
	LASLNK=0
	LNKCOD=0
	CALL SVMZZZ(NEWJFF)
	CALL CLMZZZ(0)
	IOLDTR=KOLDTR
	ICMDOP=KCMDOP
	ITTYSK=ICMDOP
	LASLNK=3
	LNKCOD=2
	RETURN
10	CALL SPKZZZ(15HCAN'T OPEN FILE,15,1,0,1)
	RETURN
	END
   Kos±