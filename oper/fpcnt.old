MODULE FPCNT(DOLLAR,ENTRIES=($FPCNT),FSAVE,TIMING,
  TIMER=EXTERNAL(SIX12))=BEGIN
%VERSION F1     %
%LAST UPDATE, MAR. 26,1975, BY V.E. VAN VLEAR   %
REQUIRE FPDEFS.BLI[7,107355];

UNDECLARE $FPCNT;


%FORMAT PICTURE COUNTER, COUNTS TOTAL FIELD REQUIREMENT %
GLOBAL ROUTINE $FPCNT(FW)=
BEGIN

MAP FP$I FW;

LOCAL CT;

IF .FW[FP$TYPE] EQL FPV$NUM THEN
  BEGIN
  CT:=.FW[FP$XI]+.FW[FP$FI];
  IF .FW[FP$SP4] EQL FPV$TRUE THEN
    CT:=.CT+((.CT-1)/3);                 %ADD COUNT FOR COMMAS   %
  CT:=.CT+.FW[FP$DI]+.FW[FP$EI];         %DECIMAL FIELDS %
  IF .FW[FP$SP1] EQL FPV$TRUE OR .FW[FP$FS] EQL FPV$DOLLAR THEN
    CT:=.CT+1;                           %FOR DOLLAR SIGN        %
  IF .FW[FP$SP2] EQL FPV$TRUE THEN
    CT:=.CT+1;                           %FOR DECIMAL POINT      %
  IF .FW[FP$SIGN] NEQ FPV$NSC THEN
    IF .FW[FP$SIGN] EQL FPV$PAREN OR
       .FW[FP$SIGN] EQL FPV$CR OR
       .FW[FP$SIGN] EQL FPV$DB THEN
          CT:=.CT+2
       ELSE
          CT:=.CT+1
  END
ELSE
  CT:=.FW[FP$C];

.CT

END;

END ELUDOM
