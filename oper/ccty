C     PROGRAM /SCTY/.  MEGASYSTEMS 940. (FROM DDI)  7/30/70
C     THIS PROGRAM APPORTIONS A BUDGET AMONG A,B,C, AND D
C  COUNTIES.  FOR NIGHT, DAY AND FRINGE BUDGET THE APPORTIONMENT
C  USES NATIONAL-AVERAGE FIGURES.  FOR SPOT BUDGET THE APPORTION-
C  MENT IS DONE SEPARATELY FOR EACH SPECIFIED MARKET.
C     TYMSHARE, MODIFIED 8/28/69 FROM DNC XFOS.
C     COEFFICIENTS UPDATED 9/25/69 PER JW INFO OF 9/9/69.
C     MODIFIED FOR PREMIUM FRINGE, 8/5/70.
      DIMENSION M(4,206),MD(206),PCT(206),IF(4),IS(4),E(4)
      JFIX(R,I)=IFIX(R*FLOAT(I)+.5)
      NEW=0
      WRITE(5,100)
100   FORMAT(///15X,'PLAN ANALYSIS BY COUNTY SIZE'///)
1     WRITE(5,101)
101   FORMAT(////'TYPE THE TOTAL NIGHT, DAY AND FRINGE DOLLARS'
     1 ' IN NETWORK'/'T.V. USING THIS PLAN : ')
      READ(5,102)INIGHT,IDAY,IFRING
102   FORMAT(3I9/)
      WRITE(5,104)
104   FORMAT(/'HOW MANY SPOT MARKETS WILL YOU SPECIFY ? ')
      READ(5,103)NM
103   FORMAT(I4/)
C     TO DESIGNATE A MARKET NUMBER WITHIN THIS PROGRAM REFER TO
C  FILE /LEVER/ AND USE THE SEQUENCE NUMBER, BASED ON THE ORDER
C  IN WHICH MARKET ARE NAMED ON FILE /LEVER/.
      IF(NEW-1)4,8,4
4     OPEN(2,'LEVER3',INPUT)
      ILO=1
      IHI=10
      II=0
      DO 702 IT=1,21
      IF(IT-21)6,5,6
5     IHI=6
6     DO 7 I=ILO,IHI
      II=II+1
7     READ(2,107)(M(J,II),J=1,4),PCT(II)
      WRITE(5,108)
108   FORMAT(1H )
702   CONTINUE
107   FORMAT(42X,4I6/F7.4/)
      CLOSE(2)
      NEW=1
8     DO 10 I=1,206
10    MD(I)=0
      PCTOT=0.
      DO 11 I=1,4
11    IS(I)=0
13    WRITE(5,109)
109   FORMAT(/'WILL YOU INPUT THEM FROM A PUNCHED TAPE ? ')
      GO TO (15,14,13),NOYES(IA)
14    WRITE(5,110)
110   FORMAT(/'USE A SEGMENTED TAPE, I.E., A TAPE WITH AN 8-INCH '
     1 'BLOCK OF RUBOUTS'/'SEPARATING SUCCESSIVE BLOCKS OF ENTRIES.'
     2 ' TURN OFF THE TAPE READER'/'AS SOON AS RUBOUTS EMERGE FROM '
     3 'THE READING HEAD AND TURN IT BACK ON'/'WHEN THE PRINTING '
     4 'ECHOS CATCH UP.   NOW TURN IT ON.'//)
      GO TO 16
15    WRITE(5,111)
111   FORMAT('TYPE THEM IN.'//)
16    DO 18 I=1,NM
      READ(5,112)L,MD(L)
18    PCTOT=PCTOT+PCT(L)
112   FORMAT(2I9/)
C     CALCULATE DOLLARS BY COUNTY SIZE FOR THE SPOT BUDGET
      DO 51 I=1,4
51    E(I)=0.
      DO 59 I=1,206
      IF(MD(I))59,59,54
C     MD(I) IS DISREGARDED IF NEGATIVE
54    MTOT=0
      DO 55 J=1,4
55    MTOT=MTOT+M(J,I)
      RM=1./FLOAT(MTOT)
      DO 57 J=1,4
57    E(J)=E(J)+FLOAT(M(J,I))*RM*FLOAT(MD(I))
59    CONTINUE
      WRITE(5,113)NM,PCTOT
113   FORMAT(///'THESE',I4,' MARKETS COVER',F7.3,'% U.S. H.H.'//)
      III=0
      WRITE(5,114)
114   FORMAT(/////28X'DOLLARS BY COUNTY SIZE'///
     123X'A'7X'B'7X'C'7X'D'/
     2 20X4('COUNTY'2X),6X'TOTAL'/
     3'_ U.S. H.H.'11X'41.0'4X'28.0'4X'18.0'4X'13.0'8X'100.0'///)
      IF (IDAY) 21,21,20
20    IF(1)=JFIX(0.35,IDAY)
      IF(2)=JFIX(0.30,IDAY)
      IF(3)=JFIX(0.21,IDAY)
      IF(4)=JFIX(0.14,IDAY)
      II=IF(1)+IF(2)+IF(3)+IF(4)
      WRITE(5,115)(IF(I),I=1,4),II
115   FORMAT('DAY NETWORK       'I8,3I8,I13///)
      DO 24 I=1,4
24    IS(I)=IS(I)+IF(I)
      III=II
21    IF (INIGHT) 23,23,22
22    IF(1)=JFIX(0.37,INIGHT)
      IF(2)=JFIX(0.29,INIGHT)
      IF(3)=JFIX(0.19,INIGHT)
      IF(4)=JFIX(0.15,INIGHT)
      II=IF(1)+IF(2)+IF(3)+IF(4)
      WRITE(5,116)(IF(I),I=1,4),II
116   FORMAT('NIGHT NETWORK     'I8,3I8,I13///)
      DO 25 I=1,4
25    IS(I)=IS(I)+IF(I)
      III=III+II
23    IF (IFRING) 27,27,26
26    IF(1)=JFIX(0.50,IFRING)
      IF(2)=JFIX(0.27,IFRING)
      IF(3)=JFIX(0.16,IFRING)
      IF(4)=JFIX(0.07,IFRING)
      II=IF(1)+IF(2)+IF(3)+IF(4)
      WRITE(5,118)(IF(I),I=1,4),II
      III=III+II
118   FORMAT('FRINGE NETWORK    '4I8,I13///)
      DO 31 I=1,4
31    IS(I)=IS(I)+IF(I)
27    II=0
      DO 32 I=1,4
      IF(I)=IFIX(E(I)+0.5)
      IS(I)=IS(I)+IF(I)
32    II=II+IF(I)
      III=III+II
      WRITE(5,119)(IF(I),I=1,4),II,(IS(I),I=1,4),III
119   FORMAT('SPOT T.V.         '4I8,I13/
     1 20X'------'3(2X'------'),7X'------'/
     2'    TOTAL         '4I8,I13///)
      FF=FLOAT(III)
      DO 33 I=1,4
33    E(I)=100.0*FLOAT(IS(I))/FF
      WRITE(5,120)(E(I),I=1,4)
120   FORMAT('_ DOLLARS         '4F8.2,8X'100.0'///)
      IF(1)=IFIX(100.0*E(1)/41.0+0.5)
      IF(2)=IFIX(100.0*E(2)/28.0+0.5)
      IF(3)=IFIX(100.0*E(3)/18.0+0.5)
      IF(4)=IFIX(100.0*E(4)/13.0+0.5)
      WRITE(5,121)(IF(I),I=1,4)
121   FORMAT('DOLLAR INDEX      '4I8,10X'100'//////)
70    WRITE(5,171)
171   FORMAT('DO YOU WANT TO DO ANOTHER ANALYSIS NOW ? ')
      GO TO (73,1,70),NOYES(IA)
73    STOP
      END
C
      FUNCTION NOYES(IA)
      READ(5,101)IA
101   FORMAT(A1/)
      NOYES=2
      IF(IA-1HY)2,3,2
2     NOYES=1
      IF(IA-1HN)4,5,4
4     NOYES=3
      WRITE(5,102)
102   FORMAT(' INVALID REPLY. '//)
      RETURN
3     WRITE(5,113)
113   FORMAT('(YES)'/)
      RETURN
5     WRITE(5,114)
114   FORMAT('(NO)'/)
      RETURN
      END
 
