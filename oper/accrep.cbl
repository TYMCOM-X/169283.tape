IDENTIFICATION DIVISION.
PROGRAM-ID. ACCTS.
AUTHOR. ROZ.
INSTALLATION. NORTH LA.
DATE-WRITTEN. 4-8-73.
DATE-COMPILED. 4-8-73.
SECURITY. NONE.
REMARKS. ACCOUNT TRACKING REPORT.
ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
SOURCE-COMPUTER. PDP-10.
OBJECT-COMPUTER. PDP-10.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
        SELECT ACCTS ASSIGN TO DSK
        RECORDING MODE IS ASCII.
        SELECT TEMP ASSIGN TO DSK,DSK,DSK
        RECORDING MODE IS ASCII.
        SELECT ACCT-REPORT ASSIGN TO DSK
        RECORDING MODE IS ASCII.
DATA DIVISION.
FILE SECTION.
FD ACCTS
        VALUE OF IDENTIFICATION IS "ACCNTSDAT".
01 ACCT-REC.
        02 SALESMAN, PIC X(8).
        02 ANALYST, PIC X(8).
        02 SERVICE, PIC XX.
        02 KODE, PIC X.
        02 MONTH, PIC S999V9 OCCURS 6 TIMES.
        02 MTD, PIC S999V9.
        02 BASE, PIC S999V9.
        02 ACCOUNT, PIC X(20).
SD TEMP.
01 SORT-REC.
        02 SALESMAN, PIC X(8).
        02 ANALYST, PIC X(8).
        02 SERVICE, PIC XX.
        02 KODE, PIC X.
        02 MONTHLY-DATA, PIC X(32).
        02 ACCOUNT, PIC X(20).
FD ACCT-REPORT
        VALUE OF IDENTIFICATION IS 'SALESMREP'
        REPORTS ARE SALESMAN-REPORT,ANALYST-REPORT.
01 REPORT-LINE PIC X(120).
WORKING-STORAGE SECTION.
77 HIRSCH-QUOTA, PIC S999V9.
77 CANNON-QUOTA, PIC S999V9.
77  POLEN-QUOTA, PIC S999V9.
77  BARON-QUOTA, PIC S999V9.
77   OTTI-QUOTA, PIC S999V9.
77    ROZ-QUOTA, PIC S999V9.
77      LAST-SALESMAN, PIC X(8).
77 MONTH1, PIC S999V9.
77 MONTH2, PIC S999V9.
77 MONTH3, PIC S999V9.
77 MONTH4, PIC S999V9.
77 MONTH5, PIC S999V9.
77 MONTH6, PIC S999V9.
77      MONTH-HEADER1, PIC 9(4).
77      MONTH-HEADER2, PIC 9(4).
77      MONTH-HEADER3, PIC 9(4).
77      MONTH-HEADER4, PIC 9(4).
77      MONTH-HEADER5, PIC 9(4).
77      MONTH-HEADER6, PIC 9(4).
77      ICOUNT, PIC 9(2).
77 QUOTA, PIC S999V9.
REPORT SECTION.
RD SALESMAN-REPORT CONTROL IS  FINAL,SALESMAN OF ACCT-REC
        PAGE 58 HEADING 1 FIRST DETAIL 5
        LAST DETAIL 55 FOOTING 56.
01 TYPE PH LINE 1 NEXT GROUP PLUS 2 PIC X.
01 TYPE CH SALESMAN OF ACCT-REC NEXT GROUP PLUS 1.
        02 COLUMN 20 LINE PLUS 1 PIC X(30)
         VALUE 'SALESMAN DETAIL REPORT'.
        02 COLUMN 25 LINE PLUS 1 PIC X(15) VALUE 'HOURS PER DAY'.
         02 COLUMN 3 LINE PLUS 3 PIC X(8) SOURCE SALESMAN OF ACCT-REC.
        02 COLUMN 30 LINE PLUS 2 PIC Z(5) SOURCE MONTH-HEADER1.
        02 COLUMN 38 PIC Z(5) SOURCE MONTH-HEADER2.
        02 COLUMN 46 PIC Z(5) SOURCE MONTH-HEADER3.
        02 COLUMN 54 PIC Z(5) SOURCE MONTH-HEADER4.
        02 COLUMN 62 PIC Z(5) SOURCE MONTH-HEADER5.
        02 COLUMN 70 PIC Z(5) SOURCE MONTH-HEADER6.
        02 COLUMN 81 PIC X(3) VALUE 'MTD'.
        02 COLUMN 88 PIC X(4) VALUE 'BASE'.
01 DETAIL-LINE-SALESMAN TYPE DETAIL.
        02 COLUMN 01 LINE PLUS 1 PIC X(20) SOURCE ACCOUNT OF ACCT-REC.
        02 COLUMN 25 PIC XX SOURCE SERVICE OF ACCT-REC.
        02 COLUMN 30 PIC ZZZZ.9 SOURCE MONTH1.
        02 COLUMN 38 PIC ZZZZ.9 SOURCE MONTH2.
        02 COLUMN 46 PIC ZZZZ.9 SOURCE MONTH3.
        02 COLUMN 54 PIC ZZZZ.9 SOURCE MONTH4.
        02 COLUMN 62 PIC ZZZZ.9 SOURCE MONTH5.
        02 COLUMN 70 PIC ZZZZ.9 SOURCE MONTH6.
        02 COLUMN 78 PIC ZZZZ.9 SOURCE MTD.
        02 COLUMN 86 PIC ZZZZ.9 SOURCE BASE.
01 TYPE CF SALESMAN OF ACCT-REC LINE PLUS 1 NEXT GROUP NEXT PAGE.
        02 COLUMN 01 PIC X(20) VALUE 'TOTAL'.
        02 COLUMN 30 PIC ZZZZ.9 SUM MONTH1.
        02 COLUMN 38 PIC ZZZZ.9 SUM MONTH2.
        02 COLUMN 46 PIC ZZZZ.9 SUM MONTH3.
        02 COLUMN 54 PIC ZZZZ.9 SUM MONTH4.
        02 COLUMN 62 PIC ZZZZ.9 SUM MONTH5.
        02 COLUMN 70 PIC ZZZZ.9 SUM MONTH6.
        02 COLUMN 78 PIC ZZZZ.9 SUM MTD.
        02 COLUMN 86 PIC ZZZZ.9 SUM BASE.
        02 COLUMN 86 LINE PLUS 1 PIC ZZZZ.9 SOURCE QUOTA.
        02 COLUMN 86 LINE PLUS 1 PIC ZZZZ.9 SUM BASE,QUOTA.
RD ANALYST-REPORT CONTROL IS  FINAL,ANALYST OF ACCT-REC
        PAGE 58 HEADING 1 FIRST DETAIL 5
        LAST DETAIL 55 FOOTING 56.
01 TYPE PH LINE 1 NEXT GROUP PLUS 2 PIC X.
01 TYPE CH ANALYST OF ACCT-REC NEXT GROUP PLUS 1.
        02 COLUMN 20 LINE PLUS 1 PIC X(30)
         VALUE 'ANALYST DETAIL REPORT'.
        02 COLUMN 25 LINE PLUS 1 PIC X(15) VALUE 'HOURS PER DAY'.
         02 COLUMN 3 LINE PLUS 3 PIC X(8) SOURCE ANALYST OF ACCT-REC.
        02 COLUMN 30 LINE PLUS 2 PIC Z(5) SOURCE MONTH-HEADER1.
        02 COLUMN 38 PIC Z(5) SOURCE MONTH-HEADER2.
        02 COLUMN 46 PIC Z(5) SOURCE MONTH-HEADER3.
        02 COLUMN 54 PIC Z(5) SOURCE MONTH-HEADER4.
        02 COLUMN 62 PIC Z(5) SOURCE MONTH-HEADER5.
        02 COLUMN 70 PIC Z(5) SOURCE MONTH-HEADER6.
        02 COLUMN 81 PIC X(3) VALUE 'MTD'.
        02 COLUMN 88 PIC X(4) VALUE 'BASE'.
01 DETAIL-LINE-ANALYST TYPE DETAIL.
        02 COLUMN 01 LINE PLUS 1 PIC X(20) SOURCE ACCOUNT OF ACCT-REC.
        02 COLUMN 25 PIC XX SOURCE SERVICE OF ACCT-REC.
        02 COLUMN 30 PIC ZZZZ.9 SOURCE MONTH1.
        02 COLUMN 38 PIC ZZZZ.9 SOURCE MONTH2.
        02 COLUMN 46 PIC ZZZZ.9 SOURCE MONTH3.
        02 COLUMN 54 PIC ZZZZ.9 SOURCE MONTH4.
        02 COLUMN 62 PIC ZZZZ.9 SOURCE MONTH5.
        02 COLUMN 70 PIC ZZZZ.9 SOURCE MONTH6.
        02 COLUMN 78 PIC ZZZZ.9 SOURCE MTD.
        02 COLUMN 86 PIC ZZZZ.9 SOURCE BASE.
01 TYPE CF ANALYST OF ACCT-REC LINE PLUS 1 NEXT GROUP NEXT PAGE.
        02 COLUMN 01 PIC X(20) VALUE 'TOTAL'.
        02 COLUMN 30 PIC ZZZZ.9 SUM MONTH1.
        02 COLUMN 38 PIC ZZZZ.9 SUM MONTH2.
        02 COLUMN 46 PIC ZZZZ.9 SUM MONTH3.
        02 COLUMN 54 PIC ZZZZ.9 SUM MONTH4.
        02 COLUMN 62 PIC ZZZZ.9 SUM MONTH5.
        02 COLUMN 70 PIC ZZZZ.9 SUM MONTH6.
        02 COLUMN 78 PIC ZZZZ.9 SUM MTD.
        02 COLUMN 86 PIC ZZZZ.9 SUM BASE.
        02 COLUMN 86 LINE PLUS 1 PIC ZZZZ.9 SOURCE QUOTA.
        02 COLUMN 86 LINE PLUS 1 PIC ZZZZ.9 SUM BASE,QUOTA.
PROCEDURE DIVISION.
MONTH-HEADER-SETUP.
        DISPLAY ' INPUT CURRENT MONTH (AS: XX): ' WITH NO ADVANCING.
        ACCEPT ICOUNT.
        SUBTRACT 1 FROM ICOUNT.
        IF ICOUNT EQUALS ZERO MOVE 12 TO ICOUNT.
        MOVE ICOUNT TO MONTH-HEADER6.
        SUBTRACT 1 FROM ICOUNT.
        IF ICOUNT EQUALS ZERO MOVE 12 TO ICOUNT.
        MOVE ICOUNT TO MONTH-HEADER5.
        SUBTRACT 1 FROM ICOUNT.
        IF ICOUNT EQUALS ZERO MOVE 12 TO ICOUNT.
        MOVE ICOUNT TO MONTH-HEADER4.
        SUBTRACT 1 FROM ICOUNT.
        IF ICOUNT EQUALS ZERO MOVE 12 TO ICOUNT.
        MOVE ICOUNT TO MONTH-HEADER3.
        SUBTRACT 1 FROM ICOUNT.
        IF ICOUNT EQUALS ZERO MOVE 12 TO ICOUNT.
        MOVE ICOUNT TO MONTH-HEADER2.
        SUBTRACT 1 FROM ICOUNT.
        IF ICOUNT EQUALS ZERO MOVE 12 TO ICOUNT.
        MOVE ICOUNT TO MONTH-HEADER1.
QUOTA-INPUT.
        DISPLAY 'INPUT QUOTAS.'.
        DISPLAY 'CANNON: ' WITH NO ADVANCING.
        ACCEPT CANNON-QUOTA.
        DISPLAY 'HIRSCH: ' WITH NO ADVANCING.
        ACCEPT HIRSCH-QUOTA.
        DISPLAY ' POLEN: ' WITH NO ADVANCING.
        ACCEPT POLEN-QUOTA.
        DISPLAY ' BARON: ' WITH NO ADVANCING.
        ACCEPT BARON-QUOTA.
        DISPLAY '  OTTI: ' WITH NO ADVANCING.
        ACCEPT OTTI-QUOTA.
        DISPLAY '   ROZ: ' WITH NO ADVANCING.
        ACCEPT ROZ-QUOTA.
PGR1.
        SORT TEMP ON ASCENDING SALESMAN OF ACCT-REC,
                ACCOUNT OF ACCT-REC,KODE OF ACCT-REC
                USING ACCTS
                GIVING ACCTS.
PGR2.
        OPEN OUTPUT ACCT-REPORT.
        INITIATE SALESMAN-REPORT.
        OPEN INPUT ACCTS.
PGR3.
        READ ACCTS AT END GO TO PGR4.
        MOVE MONTH(1) TO MONTH1
        MOVE MONTH(2) TO MONTH2
        MOVE MONTH(3) TO MONTH3
        MOVE MONTH(4) TO MONTH4
        MOVE MONTH(5) TO MONTH5
        MOVE MONTH(6) TO MONTH6
        IF LAST-SALESMAN EQUALS 'CANNON'
          MOVE CANNON-QUOTA TO QUOTA.
        IF LAST-SALESMAN EQUALS 'HIRSCH'
          MOVE HIRSCH-QUOTA TO QUOTA.
        IF LAST-SALESMAN EQUALS 'POLEN'
          MOVE POLEN-QUOTA TO QUOTA.
        GENERATE DETAIL-LINE-SALESMAN.
        MOVE SALESMAN OF ACCT-REC TO LAST-SALESMAN.
        GO TO PGR3.
PGR4.
        TERMINATE SALESMAN-REPORT.
        CLOSE ACCTS.
PGR5.
        SORT TEMP ON ASCENDING ANALYST OF ACCT-REC,
                ACCOUNT OF ACCT-REC,KODE OF ACCT-REC
                USING ACCTS
                GIVING ACCTS.
PGR6.
        INITIATE ANALYST-REPORT.
        OPEN INPUT ACCTS.
PGR7.
        READ ACCTS AT END GO TO PGR8.
        MOVE MONTH(1) TO MONTH1
        MOVE MONTH(2) TO MONTH2
        MOVE MONTH(3) TO MONTH3
        MOVE MONTH(4) TO MONTH4
        MOVE MONTH(5) TO MONTH5
        MOVE MONTH(6) TO MONTH6
        IF LAST-SALESMAN EQUALS 'BARON'
          MOVE BARON-QUOTA TO QUOTA.
        IF LAST-SALESMAN EQUALS 'OTTI'
          MOVE OTTI-QUOTA TO QUOTA.
        IF LAST-SALESMAN EQUALS 'ROZ'
          MOVE ROZ-QUOTA TO QUOTA.
        GENERATE DETAIL-LINE-ANALYST.
        MOVE ANALYST OF ACCT-REC TO LAST-SALESMAN.
        GO TO PGR7.
PGR8.
        TERMINATE ANALYST-REPORT.
        CLOSE ACCTS.
        CLOSE ACCT-REPORT.
        STOP RUN.
   