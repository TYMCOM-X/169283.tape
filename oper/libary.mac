TITLE	LIBARY VERSION 2B		
SUBTTL	COBOL LIBRARY MAINTENANCE PROGRAM		AL BLACKINGTON/CAM	

EDIT==007000            ;DEC-EDT=007,TYM-REL=000
VERSION==002001         ;DEC-VER=002.02,TYM-REL=001

;COPYRIGHT 1970,1971,1972 DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.


	TWOSEG		;TWO-SEGMENT PROGRAM

	LOC	137
	XWD	VERSION,EDIT

	DEFINE ERRORA (X),<
	PUSHJ	PP,TYPEQA
	TTCALL	3,[ASCIZ "'X'
"]
	>

	DEFINE ERRORB (X),<
	PUSHJ	PP,TYPEQB
	TTCALL	3,[ASCIZ "'X'
"]
	>

	SALL

	RELOC	400000
START:	JSP	TA,INITTY	;INITIALIZE TTY

START1:	HLRZ	TA,JOBSA	;SAVE ENOUGH
	ADDI	TA,406		; ROOM FOR TWO
	MOVEM	TA,SAVJFF	;  DISK BUFFERS FOR COMMAND FILE
	HRRM	TA,JOBFF

	MOVE	PP,PPOINT	;SET UP PUSH-DOWN POINTER
	MOVE	TA,[XWD IMPVAL,LOWIMP]	;SET UP IMPURE AREA
	BLT	TA,HIIMP

	SETZM	LOCLR		;CLEAR SOME IMPURE AREA
	MOVE	TA,[XWD LOCLR,LOCLR+1]
	BLT	TA,HICLR

	PUSHJ	PP,DATIME	;SET UP DATE AND TIME FOR HEADER
	PUSHJ	PP,GETDEV

	TRNE	SW,LSWICH	;IS THIS "/L" FUNCTION?
	JRST	LSTFIL		;YES

	CALLI	TA,30		;GET JOB NUMBER
	PUSHJ	PP,CONVBD
	HLLM	TC,TOFNAM
	HLLM	TC,TODNAM
	HLLM	TC,TIDNAM
	SETZB	TA,TOEXTN
	OPEN	TOF,TOFDAT
	JRST	NODSK
	OPEN	TOD,TODDAT
	JRST	NODSK
	TRNE	SW,NOINP	;ANY INPUT FILE?
	JRST	START7		;NO
;SKIP OVER ROUGH TABLE

	OPEN	TID,TIDDAT
	JRST	NODSK

	MOVE	TA,RUFPTR
START2:	PUSHJ	PP,GETOIF
	MOVEM	WD,0(TA)
	AOBJN	TA,START2

;COPY INPUT DIRECTORY TO DISK

	SETZM	TIDNAM+3
	SETZM	TIDNAM+2
	ENTER	TID,TIDNAM
	JRST	NOEDSK
	MOVE	TA,JOBFF
	MOVEM	TA,TIDBUF
	OUTBUF	TID,2

START3:	PUSHJ	PP,GETOIF
	PUSHJ	PP,PUTTID
	AOJE	WD,START4
	PUSHJ	PP,GETOIF
	PUSHJ	PP,PUTTID
	JRST	START3

START4:	CLOSE	TID,

START5:	SKIPN	TA,TIDBUF
	MOVE	TA,JOBFF
	MOVEM	TA,TIDBUF
	MOVEM	TA,JOBFF
	INBUF	TID,1

	SETZM	TIDNAM+3

	LOOKUP	TID,TIDNAM
	JRST	NOLDSK
	PUSHJ	PP,COPYL3

;SET UP OUTPUT SCRATCH FILES


START7:	SETZM	TODNAM+3
	SETZM	TODNAM+2
	ENTER	TOD,TODNAM
	JRST	NOEDSK
	MOVE	TA,JOBFF
	MOVEM	TA,TODBUF
	OUTBUF	TOD,2

	SETZM	TOFNAM+3
	SETZM	TOFNAM+2
	ENTER	TOF,TOFNAM
	JRST	NOEDSK
	MOVE	TA,JOBFF
	MOVEM	TA,TOFBUF
	OUTBUF	TOF,2

	SETOM	RUFTAB
	MOVE	TA,[XWD RUFTAB,RUFTAB+1]
	BLT	TA,RUFTAB+177

	SETZM	RUFCTR

	MOVEI	TA,RUFTAB
	MOVEM	TA,RUFLOC

	MOVEI	TA,200
	MOVEM	TA,OFFSET
	MOVEM	TA,FINCTR

	SETZM	TOFCTR
	SETZM	OUTNAM
;GET ANOTHER MAJOR COMMAND FROM TTY

NEWCOM:	MOVE	PP,PPOINT
	PUSHJ	PP,GETCOM

NEWCM1:	ANDCMI	SW,DOLIST	;TURN OFF "LIST THIS PROG" FLAG

	PUSHJ	PP,TSTCOM	;SEE IF THIS IS A VALID COMMAND
	JUMPN	TA,(TA)		;IF IT IS--GO TO APPROPRIATE ROUTINE

	ERRORA	<IMPROPER COMMAND>
	JRST	NEWCOM

;FIND NEXT MAJOR COMMAND IN CCL FILE AFTER ERROR

SKPCOM:	TRNN	SW,NOTTY	;CCL COMMANDS?
	JRST	NEWCOM		;NO

SKPCM1:	MOVE	PP,PPOINT
	PUSHJ	PP,GETCOM
	ANDCMI	SW,DOLIST
	PUSHJ	PP,TSTCOM
	JUMPN	TA,(TA)		;GO EXECUTE VALID COMMAND
SKPCM2:	PUSHJ	PP,GETTY	;SKIP TO END OF INVALID LINE
	CAIE	CH,12
	CAIN	CH,14
	JRST	SKPCM1		;IGNORE INVALID COMMAND
	JRST	SKPCM2
;CORRECT AN EXISTING PROGRAM

CORECT:	PUSHJ	PP,SEARCH	;FIND INPUT PROGRAM
	JRST	SKPCOM		;IT HAS BEEN PASSED
	JRST	NOPROG		;IT DOESN'T EXIST

	SKIPE	OFFDEV		;ANY 'OFF-LINE' FILE?
	JRST	DELET1		;YES--ERROR

	PUSHJ	PP,PUTNNM	;PUT NAME IN OUTPUT DIRECTORY

	SETOM	OUTLIN
COREC1:	PUSHJ	PP,GETIRD	;GET A COMMAND
	JRST	COREC4		;ONE OF THE MAJOR ONES

;CHECK CODE
	LDB	TA,TTYC1
	CAIN	TA,"I"-40
	JRST	COREC5
	CAIN	TA,"D"-40
	JRST	CREC20
	CAIN	TA,"R"-40
	JRST	CREC15

	PUSHJ	PP,BADCOM
	JRST	COREC1

COREC4:	TRZE	SW,ENDP
	JRST	COR4A
	PUSHJ	PP,COPYL
	JRST	COREC4
COR4A:	MOVNI	WD,1
	PUSHJ	PP,PUTTOF
	JRST	NEWCM1


;INSERT A LINE

COREC5:	CAMG	LN,OUTLIN
	JRST	CREC23

	TRNE	SW,ENDP		;INPUT PROGRAM FINISHED?
	JRST	COREC6		;YES

	CAMN	LN,INLIN	;NO--EQUAL TO INPUT LINE?
	JRST	CREC24		;YES--ERROR

	CAMG	LN,INLIN	;NO--GREATER?
	JRST	COREC6		;NO--INSERT HERE

	PUSHJ	PP,COPYL	;YES--COPY INPUT LINE
	JRST	COREC5		;LOOP

COREC6:	PUSHJ	PP,PUTLIN
	JRST	COREC1
;CORRECT AN EXISTING PROGRAM (CONT'D)

;REPLACE A LINE

CREC15:	CAML	LN,INLIN	;BEFORE CURRENT INPUT LINE?
	CAMG	LN,OUTLIN	;NO--AFTER LAST OUTPUT LINE?
	JRST	CREC23		;NO--ERROR

	TRNE	SW,ENDP		;INPUT PROGRAM FINISHED?
	JRST	CREC22		;YES--ERROR

	CAMN	LN,INLIN	;EQUAL TO INPUT LINE?
	JRST	CREC16		;YES

	PUSHJ	PP,COPYL	;NO--COPY INPUT LINE
	JRST	CREC15		;LOOP

CREC16:	PUSHJ	PP,PASSL	;SKIP OVER INPUT LINE
	PUSHJ	PP,PUTLIN	;COPY LINE TO OUTPUT
	JRST	COREC1

;DELETE A LINE

CREC20:	CAML	LN,INLIN	;BEFORE CURRENT INPUT LINE?
	CAMG	LN,OUTLIN	;NO--AFTER LAST OUTPUT LINE?
	JRST	CREC23		;NO--ERROR

	TRNE	SW,ENDP
	JRST	CREC22

	CAMN	LN,INLIN
	JRST	CREC21

	PUSHJ	PP,COPYL
	JRST	CREC20

CREC21:	PUSHJ	PP,PASSL
	JRST	CREC29

CREC22:	ERRORB	<THAT LINE DOES NOT EXIST>
	JRST	CREC29

CREC23:	ERRORB	<THAT LINE HAS BEEN PASSED>
	JRST	CREC29

CREC24:	ERRORB	<THAT LINE ALREADY EXISTS>

CREC29:	MOVEI	CH," "
	PUSHJ	PP,SKPTTY
	JRST	COREC1
;INSERT A NEW PROGRAM

INSERT:	TRO	SW,INSRTF	;NEW FILE OPEN FOR INSERTING
	PUSHJ	PP,SEARCH
	JRST	SKPCOM
	JRST	INSRT0

	ERRORA	<THAT PROGRAM ALREADY EXISTS>
	JRST	SKPCOM

INSRT0:	PUSHJ	PP,SETOFF	;SET UP ANY 'OFF-LINE' FILE
	JRST	SKPCOM		;TROUBLE--FORGET THE WHOLE THING

INSRT1:	PUSHJ	PP,PUTNNM	;PUT NAME IN OUTPUT DIRECTORY

	SETOM	OUTLIN

	SKIPE	OFFDEV		;ANY 'OFF-LINE' FILE?
	JRST	INSR20		;YES

INSRT6:	PUSHJ	PP,GETIRD	;GET COMMAND
	JRST	INSRT8		;IT IS ONE OF THE MAJOR ONES

	LDB	TA,TTYC1
	CAIE	TA,"I"-40
	JRST	INSR10

	CAMG	LN,OUTLIN
	JRST	INSRT9

	PUSHJ	PP,PUTLIN
	TRZ	SW,INSRTF	;SOMETHING INSERTED
	JRST	INSRT6

INSRT8:	TRNE	SW,INSRTF	;ANYTHING INSERTED IN OPENED FILE?
	JRST	INSR21		;NO
	MOVNI	WD,1
	PUSHJ	PP,PUTTOF
	JRST	NEWCM1

INSRT9:	ERRORB	<THAT LINE HAS BEEN PASSED>
	JRST	INSR19

INSR10:	ERRORB	<ONLY INSERTS ARE ALLOWED>

INSR19:	MOVEI	CH," "
	PUSHJ	PP,SKPTTY
	JRST	INSRT6
;COPY PROGRAM FROM 'OFF-LINE' FILE

INSR20:	PUSHJ	PP,COPOFF
	MOVNI	WD,1
	PUSHJ	PP,PUTTOF
	JRST	NEWCOM

INSR21:	ERRORB	<NOTHING INSERTED IN PREVIOUSLY OPENED FILE>
	JRST	INSR19
;DELETE A PROGRAM

DELETE:	PUSHJ	PP,SEARCH	;FIND THE PROGRAM
	JRST	SKPCOM
	JRST	NOPROG		;IT DOESN'T EXIST
	SKIPE	OFFDEV		;ANY 'OFF-LINE' FILE?
	JRST	DELET1		;YES--ERROR

	PUSHJ	PP,PASSP	;SKIP OVER THE PROGRAM

	JRST	NEWCOM

DELET1:	ERRORA	<FILE NAME NOT ALLOWED WITH THIS COMMAND>
	JRST	SKPCOM

;REPLACE A PROGRAM

REPLAC:	PUSHJ	PP,SEARCH	;FIND THE PROGRAM
	JRST	SKPCOM
	JRST	NOPROG		;IT DOESN'T EXIST

	PUSHJ	PP,SETOFF	;SET UP ANY 'OFF-LINE' FILE
	JRST	SKPCOM		;ERROR--QUIT

	PUSHJ	PP,PASSP	;SKIP OVER THE PROGRAM
	JRST	INSRT1
;EXTRACT A PROGRAM

XTRACT:	PUSHJ	PP,SEARCH	;FIND THE PROGRAM
	JRST	SKPCOM		;IT HAS BEEN PASSED
	JRST	NOPROG		;IT DOESN'T EXIST

	SKIPN	OFFDEV		;ANY 'OFF-LINE' FILE?
	JRST	XTRAC8		;NO--ERROR

	SKIPN	TA,OFFBUF	;YES--SET IT UP
	HRRZ	TA,JOBFF
	MOVEM	TA,OFFBUF
	HRRM	TA,JOBFF

	MOVEI	TA,0		;ASCII MODE
	MOVE	TB,OFFDEV
	MOVSI	TC,OFFBH

	OPEN	OFF,TA
	JRST	NOOFFD

	OUTBUF	OFF,2

	MOVE	TE,[XWD OFFNAM,TA]
	BLT	TE,TD
	ENTER	OFF,TA
	JRST	NOEOFF

	PUSHJ	PP,PUTNNM	;PUT NAME IN OUTPUT DIRECTORY
	SETOM	OUTLIN

	SKIPA	WD,INLIN

XTRAC3:	PUSHJ	PP,GETOIF	;GET NEXT WORD FROM INPUT
	PUSHJ	PP,PUTTOF

	TRNN	WD,1		;IF NOT A LINE NUMBER
	JRST	XTRAC5		;  IT GOES TO EXTRACTION FILE

	AOJE	WD,XTRAC4	;IF END OF PROG, WE ARE ALMOST DONE
	TRNE	SW,RENUM	;ARE WE NUMBERING?
	PUSHJ	PP,OFFNUM	;YES--PUT OUT SEQUENCE NUMBER
	JRST	XTRAC3		;  AND LOOP

XTRAC4:	IORI	SW,ENDP		;SET 'END OF PROGRAM'
	JRST	XTRAC9		;FINISH UP
;EXTRACT A PROGRAM (CONT'D)

XTRAC5:	MOVE	TA,[POINT 7,WD]	;WRITE
XTRAC6:	ILDB	CH,TA		;  WORD
	JUMPE	CH,XTRA6B	;  ONTO
	CAIE	CH,12		;  EXTRACTION
	CAIN	CH,14		;  FILE
	JRST	XTRAC7		;  *
XTRA6A:	PUSHJ	PP,PUTOFF	;  *
XTRA6B:	TLNE	TA,760000	;  *
	JRST	XTRAC6		;  *

	JRST	XTRAC3		;BACK FOR ANOTHER WORD

XTRAC7:	PUSH	PP,CH
	MOVEI	CH,15
	PUSHJ	PP,PUTOFF
	POP	PP,CH
	JRST	XTRA6A

XTRAC8:	ERRORA	<NEED OUTPUT FILE FOR EXTRACTION>
	JRST	SKPCOM

XTRAC9:	CLOSE	OFF,		;CLOSE OUT
	STATO	OFF,740000	;IF NO ERROR,
	JRST	XTRA9A		;  SKIP THE ERROR TYPEOUT

	ERRORA	<OUTPUT ERROR WHEN EXTRACTION FILE CLOSED>
XTRA9A:	RELEASE	OFF,
	PUSHJ	PP,COPYL3
	JRST	NEWCOM
;RESTART MAINTENANCE.
;TEMPORARY OUTPUTS BECOME INPUTS.

RESTRT:	LDB	CH,TTYIB+1
	CAIE	CH,12
	JRST	REST10

	ANDCMI	SW,REGET

	PUSHJ	PP,CLOSOT

	TRNN	SW,NOINP
	JRST	REST1
	OPEN	TID,TIDDAT
	JRST	REST9
	MOVE	TA,TIDNAM
	MOVSI	TB,(SIXBIT "TMP")
	SETZB	TC,TD
	LOOKUP	TID,TA
	JRST	REST7

REST1:	CLOSE	TID,
	SETZB	TA,TB
	SETZB	TC,TD
	RENAME	TID,TA
	JRST	REST9

REST2:	MOVE	TA,TIDNAM
	MOVSI	TB,(SIXBIT "TMP")
	SETZB	TC,TD
	RENAME	TOD,TA
	JRST	REST9

	TRNN	SW,NOINP
	JRST	REST3
	MOVEI	TA,14
	MOVSI	TB,(SIXBIT "DSK")
	MOVEI	TC,OIFIB
	OPEN	OIF,TA
	JRST	REST9
	SKIPN	TA,OIFBUF
	MOVE	TA,JOBFF
	MOVEM	TA,OIFBUF
	MOVEM	TA,JOBFF
	INBUF	OIF,2
;RESTART MAINTENANCE (CONT'D)

REST3:	CLOSE	OIF,
	MOVE	TA,TOFNAM	;LOOK FOR FILE 'JJJS04.TMP'
	HRRI	TA,(SIXBIT "S04")
	MOVSI	TB,(SIXBIT "TMP")
	SETZB	TC,TD
	LOOKUP	OIF,TA
	JRST	REST8		;IT DOESN'T EXIST (OR POSSIBLY TROUBLE)

	CLOSE	OIF,		;OK--NOW DELETE IT
	SETZB	TA,TB
	SETZB	TC,TD
	RENAME	OIF,TA
	JRST	REST9		;TROUBLE

REST4:	MOVE	TA,TOFNAM	;RENAME 'TOF' TO BE 'JJJS04.TMP'
	HRRI	TA,(SIXBIT "S04")
	MOVSI	TB,(SIXBIT "TMP")
	SETZB	TC,TD
	RENAME	TOF,TA
	JRST	REST9		;TROUBLE

	SETZB	TC,TD		;OK--NOW OPEN THAT FILE FOR INPUT
	LOOKUP	OIF,TA
	JRST	REST9

	ANDCMI	SW,ENDF!NOINP!ENDP
	IORI	SW,HAVS04	;REMEMBER THE RESTART
	JRST	START5

REST7:	HRRZS	TB
	JUMPE	TB,REST2
	JRST	REST9

REST8:	HRRZS	TB
	JUMPE	TB,REST4


REST9:	TTCALL	3,[ASCIZ "
?TROUBLE RENAMING FILES
"]
	CALLI	12

REST10:	PUSHJ	PP,BADCOM
	JRST	NEWCOM
;"END" COMMAND HAS BEEN TYPED.

ALLDUN:	PUSHJ	PP,CLOSOT

	MOVE	TA,TODBUF
	MOVEM	TA,JOBFF
	INBUF	TOD,2
	MOVE	TA,TOFBUF
	MOVEM	TA,JOBFF
	INBUF	TOF,2

	PUSHJ	PP,SETBAK	;SET UP OUTPUT

	MOVE	TA,RUFPTR

ALDUN2:	MOVE	WD,0(TA)
	PUSHJ	PP,PUTFOF
	AOBJN	TA,ALDUN2

	SETZM	TODNAM+2
	LOOKUP	TOD,TODNAM
	JRST	NOLDSK

ALDUN3:	PUSHJ	PP,GETTOD
	PUSHJ	PP,PUTFOF
	AOJE	WD,ALDUN4

	PUSHJ	PP,GETTOD
	ADD	WD,OFFSET
	PUSHJ	PP,PUTFOF

	JRST	ALDUN3

ALDUN4:	SETZM	TOFNAM+3
	LOOKUP	TOF,TOFNAM
	JRST	NOLDSK

ALDUN5:	PUSHJ	PP,GETTOF
	JRST	ALDUN6
	PUSHJ	PP,PUTFOF
	JRST	ALDUN5
;CLOSE OUT ALL FILES, AND DELETE ALL SCRATCH FILES

ALDUN6:	MOVEI	TA,0		;FILE-NAME FOR 'DELETE'

	CLOSE	TOD,
	RENAME	TOD,TA
	JFCL
	RELEASE	TOD,

	CLOSE	TOF,
	RENAME	TOF,TA
	JFCL
	RELEASE	TOF,

	TRNN	SW,NOINP	;ANY INPUT FILE?
	JRST	ALDN6A		;YES
	TRNN	SW,HAVS04	;NO--READING SCRATCH?
	JRST	ALDUN7		;NO
ALDN6A:	CLOSE	TID,
	RENAME	TID,TA
	JFCL
	RELEASE	TID,

	TRZN	SW,HAVS04	;WERE WE READING SCRATCH?
	JRST	ALDUN7		;NO
	CLOSE	OIF,		;YES--
	RENAME	OIF,TA		;DELETE IT
	JFCL

ALDUN7:	RELEASE	OIF,

	SKIPN	FOFDAT+1	;ANY FOF FILE?
	JRST	ALDUN8		;NO
	CLOSE	FOF,
	STATZ	FOF,760000
	TTCALL	3,[ASCIZ "?ERROR WHILE CLOSING OUTPUT FILE
"]
	RELEASE	FOF,

ALDUN8:	RELEASE	LST,

	TRNE	SW,ENDCOM	;END OF NON-TTY COMMAND FILE?
	JRST	START		;YES--START AT RESET
	JRST	START1		;NO--KEEP COMMAND DEVICE
;LIST THE ENTIRE LIBRARY FILE

LSTFIL:	ENTER	LST,LSTNAM	;ENTER LISTING FILE
	JRST	CNTLST		;CANNOT--ERROR

	MOVEI	FT,2

LSTF3:	PUSHJ	PP,GETFT

	MOVE	PN,[XWD -^D64,FINTAB]

LSTF4:	MOVE	TA,(PN)
	AOJE	TA,ALDUN7

	SUBI	TA,1
	MOVEM	TA,OUTNAM
	MOVE	TA,1(PN)
	MOVEM	TA,OUTNAM+1
	PUSHJ	PP,LSTHDG

	MOVE	TA,1(PN)
	AND	TA,[XWD 77,-1]
	LSHC	TA,-7
	USETI	OIF,1(TA)
	IN	OIF,
	AOSA	OIFIB+2
	JRST	LSTERA

	MOVEI	TA,0
	LSHC	TA,7
	ADDM	TA,OIFIB+1
	MOVNS	TA
	ADDM	TA,OIFIB+2
;LIST THE LIBRARY (CONT'D)

LSTF6:	PUSHJ	PP,GETOIF
	TRNN	WD,1
	JRST	TRBL1

	MOVE	TA,WD
	AOJE	TA,LSTF10

	LSH	WD,-1
	PUSHJ	PP,LINOUT

LSTF7:	PUSHJ	PP,GETOIF
	TRNE	WD,1
	JRST	TRBL2

	MOVE	TA,[POINT 7,WD]
LSTF8:	ILDB	CH,TA
	CAIN	CH,12		;IS CHARACTER A LINE-FEED?
	JRST	LSTF9		;YES

	PUSHJ	PP,PUTKAR
	TLNE	TA,760000	;IS WORD NOW EMPTY?
	JRST	LSTF8		;NO
	JRST	LSTF7		;YES--GET ANOTHER

LSTF9:	MOVE	TA,CH		;SAVE END-LINE CHARACTER
	MOVEI	CH,15
	PUSHJ	PP,PUTKAR
	MOVE	CH,TA		;PUT OUT
	PUSHJ	PP,PUTKAR	;  END-LINE CHARACTER
	JRST	LSTF6


LSTF10:	ADDI	PN,1
	AOBJN	PN,LSTF4
	JRST	LSTF3
;PRINT OUT LINE NUMBER

LINOUT:	MOVE	TA,WD
	MOVEI	TC,0
	MOVEI	TD,6

LINOT1:	IDIVI	TA,^D10
	ADDI	TB,"0"
	LSHC	TB,-6
	SOJG	TD,LINOT1

LINOT2:	MOVEI	TB,0
	LSHC	TB,6
	MOVEI	CH,(TB)
	PUSHJ	PP,PUTKAR
	JUMPN	TC,LINOT2

	POPJ	PP,
;GET ONE BLOCK OF FINE-TABLE

GETFT:	USETI	OIF,(FT)
	IN	OIF,
	SKIPA
	JRST	DIRERA

	MOVS	TA,OIFIB+1
	HRRI	TA,FINTAB-1
	BLT	TA,FINTAB+177

	ADDI	FT,1

	POPJ	PP,


;PUT A SINGLE CHARACTER OUT ONTO LISTING FILE

PUTKAR:	SOSG	LSTBH+2
	JRST	PUTK2
PUTK1:	IDPB	CH,LSTBH+1
	POPJ	PP,

PUTK2:	OUT	LST,
	JRST	PUTK1
	JRST	LSTERA


;PUT A MESSAGE ONTO LISTING FILE

PUTMES:	ILDB	CH,TE
	JUMPE	CH,CPOPJ
	PUSHJ	PP,PUTKAR
	JRST	PUTMES
;GET A COMMAND FROM TTY

GETCOM:	TRNE	SW,ENDCOM	;COMMAND FILE EMPTY?
	JRST	GTCOM8		;YES

	CAIN	CH,"@"
	JRST	GTCM10

	MOVE	CP,[POINT 7,COMSAV]

	TRNN	SW,NOTTY
	TTCALL	3,[ASCIZ "*"]
	MOVE	TA,[POINT 6,COMWRD]
	SETZM	COMWRD
	SETZM	COMWRD+1

GTCOM1:	PUSHJ	PP,GETTY
	TRNE	SW,ENDCOM
	JRST	GTCOM8

	CAIN	CH,"@"
	JRST	GTCM10

	CAIN	CH,12
	JRST	GTCOM2

	CAIGE	CH,140
	CAIGE	CH,40
	JRST	GTCOM7

	CAIN	CH," "
	JRST	GTCOM2
	CAIE	CH,"*"
	CAIN	CH,"-"
	JRST	GTCOM3

	SUBI	CH,40
	IDPB	CH,TA
	CAME	TA,[POINT 6,COMWRD+1,35]
	JRST	GTCOM1
	JRST	GTCOM9

GTCOM2:	CAMN	TA,[POINT 6,COMWRD]
	JRST	GETCOM

GTCOM3:	IORI	SW,REGET
	POPJ	PP,

GTCOM7:	CAIN	CH,11
	JRST	GTCOM2

	CAIE	CH,12
	JRST	GTCOM9
	MOVE	TA,COMWRD
	CAMN	TA,[SIXBIT "END"]
	POPJ	PP,
GTCOM8:	MOVE	TA,[SIXBIT "END"]
	MOVEM	TA,COMWRD
	POPJ	PP,

GTCOM9:	PUSHJ	PP,BADCOM
	JRST	GETCOM



;'@' SEEN IN COMMAND

GTCM10:	CAME	TA,[POINT 6,COMWRD]
	JRST	GTCM11
	SETZB	DN,FN
	SETZB	EX,PJ
	PUSHJ	PP,GTDEV8	;SCAN FOR 'DEV:FILE.EXT[P,P]'
	JFCL			;IGNORE ERROR (MESSAGE WAS TYPED)
	JRST	GETCOM		;GO BACK FOR ANOTHER COMMAND

GTCM11:	PUSHJ	PP,BADCOM
	JRST	GETCOM
;SEE IF THE COMMAND IS ONE OF THE MAJOR ONES.
;IF SO, RETURN WITH TA CONTAINING THE ADDRESS OF THE
;   APPROPRIATE ROUTINE

TSTCOM:	MOVSI	CH,LSTSIZ
	MOVE	TA,COMWRD

TSTCM1:	CAME	TA,LIST1(CH)
	AOBJN	CH,TSTCM1
	JUMPGE	CH,TSTCM2
	SKIPA	TA,LIST2(CH)

TSTCM2:	MOVEI	TA,0
	POPJ	PP,

LIST1:	SIXBIT	"CORREC"
	SIXBIT	"REPLAC"
	SIXBIT	"INSERT"
	SIXBIT	"DELETE"
	SIXBIT	"RESTAR"
	SIXBIT	"EXTRAC"
	SIXBIT	"END"

LIST2:	EXP	CORECT
	EXP	REPLAC
	EXP	INSERT
	EXP	DELETE
	EXP	RESTRT
	EXP	XTRACT
	EXP	ALLDUN

LSTSIZ==LIST2-.
;GET AN "I", "R" OR "D" COMMAND, AND RETURN TO CALL+2.
;IF NONE OF THESE, CHECK TO SEE IF ONE OF THE MAJOR COMMANDS TYPED. IF SO --
;	RETURN TO CALL+1.
;IF ERROR, PUT OUT THE MESSAGE AND TRY AGAIN.

GETIRD:	PUSHJ	PP,GETCOM

	PUSHJ	PP,TSTCOM	;SEE IF IT IS ONE OF THE MAJOR COMMANDS
	JUMPN	TA,CPOPJ	;IF IT IS--LEAVE

	LDB	CH,TTYC1
	CAIE	CH,"I"-40
	CAIN	CH,"R"-40
	JRST	GTIRD1
	CAIE	CH,"D"-40
	JRST	GTIRD7

GTIRD1:	MOVE	TA,TTYC1
	ILDB	CH,TA
	CAIG	CH,"9"-40
	CAIGE	CH,"0"-40
	JRST	GTIRD3

	MOVEI	LN,-"0"+40(CH)
	MOVEI	TC,6

GTIRD2:	ILDB	CH,TA
	CAIG	CH,"9"-40
	CAIGE	CH,"0"-40
	JRST	GTIRD5

	IMULI	LN,^D10
	ADDI	LN,-"0"+40(CH)
	SOJG	TC,GTIRD2

GTIRD3:	TTCALL	3,[ASCIZ "
?IMPROPER LINE NUMBER"]
GTIRD4:	PUSHJ	PP,SKPTTY
	JRST	GETIRD

GTIRD5:	JUMPN	CH,GTIRD3
	LSH	LN,1
	IORI	LN,1
	MOVEM	LN,COMLIN
	AOS	(PP)
	POPJ	PP,

GTIRD7:	PUSHJ	PP,BADCOM
	PUSHJ	PP,SKPTTY
	JRST	GETIRD
;COPY A LINE FROM INPUT FILE TO OUTPUT FILE

COPYL:	MOVEI	WD,^D10*2
	TRNN	SW,RENUM
COPYL0:	SKIPA	WD,INLIN
	ADDB	WD,RENLIN
	MOVEM	WD,OUTLIN	;THAT'S NEW OUTPUT LINE
COPYL1:	PUSHJ	PP,PUTTOF	;WRITE IT

	PUSHJ	PP,GETOIF	;GET INPUT WORD
	TRNN	WD,1B35		;IS IT A LINE NUMBER?
	JRST	COPYL1		;NO - LOOP

COPYL2:	MOVEM	WD,INLIN	;YES -- THAT'S NEW INPUT LINE
	AOJN	WD,CPOPJ	;WAS IT MINUS 1?
	IORI	SW,ENDP		;YES -- SET "END OF PROGRAM"

COPYL3:	PUSHJ	PP,GETTID	;GET PROGRAM NAME
	AOJE	WD,COPYL5	;ANYTHING THERE?
	SUBI	WD,1		;YES
	MOVEM	WD,INNAM
	PUSHJ	PP,GETTID
	AND	WD,[XWD 777700,0]
	MOVEM	WD,INNAM+1

	PUSHJ	PP,GETOIF	;GET NEXT LINE NUMBER
	MOVEM	WD,INLIN	;SAVE LINE NUMBER
	POPJ	PP,

COPYL5:	IORI	SW,ENDF		;NO MORE PROGRAMS--SET "END-FILE"
	POPJ	PP,

;PASS OVER ONE LINE OF INPUT

PASSL:	PUSHJ	PP,GETOIF	;COPY WORDS UNTIL
	TRNN	WD,1B35		;	1-BIT SEEN
	JRST	PASSL

	JRST	COPYL2
;COPY A PROGRAM FROM INPUT TO OUTPUT

COPYP:	TRZE	SW,ENDP		;END OF PROGRAM SEEN?
	JRST	COPYP1		;YES

	PUSHJ	PP,COPYL0	;NO--COPY ONE LINE
	JRST	COPYP		;LOOP

COPYP1:	MOVNI	WD,1		;WRITE OUT LAST LINE NUMBER
	JRST	PUTTOF


;PASS OVER A PROGRAM

PASSP:	PUSHJ	PP,PASSL
	TRZN	SW,ENDP
	JRST	PASSP

	POPJ	PP,
;SEARCH FOR THE PROGRAM SPECIFIED BY "NEWNAM"
;IF AT END OF INPUT FILE, PUT OUT ERROR MESSAGE AND
;	RETURN TO CALL+1.
;IF THE PROGRAM HAS BEEN PASSED, PUT OUT ERROR MESSAGE AND
;	RETURN TO CALL+1.
;IF THE PROGRAM DOESN'T EXIST, RETURN TO CALL+2.
;RETURN TO CALL+3 IF THE PROGRAM IS FOUND.

SEARCH:	ANDCMI	SW,FSRC		;TURN OFF "WE HAVE MOVED INPUT"
	PUSHJ	PP,GETNAM	;GET PROGRAM NAME
	POPJ	PP,		;ERROR-QUIT

SERCH1:	TRNE	SW,ENDF		;END OF INPUT?
	JRST	SERCH4		;YES -- ERROR
	MOVEI	TB,INNAM
	PUSHJ	PP,COMPAR
	JRST	SERCH4		;NEW NAME LESS THAN NEXT INPUT
	JRST	SERCH2		;NEW NAME GREATER THAN NEXT INPUT
	JRST	CPOPJ2		;NEW NAME EQUAL TO NEXT INPUT

SERCH2:	PUSHJ	PP,PUTINM	;PUT NAME IN OUTPUT DIRECTORY
	PUSHJ	PP,COPYP	;COPY THIS PROGRAM
	IORI	SW,FSRC		;SET "WE HAVE MOVED INPUT"
	ANDCMI	SW,ENDP		;INSURE "END-OF-PROGRAM" IS RESET
	JRST	SERCH1		;LOOP

SERCH3:	CAML	TA,INNAM	;ARE WE BEYOND THE DESIRED PROGRAM?
	JRST	SERCH2

SERCH4:	TRNE	SW,FSRC		;YES -- WAS INPUT DONE?
	JRST	CPOPJ1		;YES -- PROGRAM DOESN'T EXIST

	MOVEI	TB,OUTNAM	;NO--IS NEW NAME GREATER THAN LAST WRITTEN?
	PUSHJ	PP,COMPAR
	JRST	SERCH5		;LESS--ERROR
	JRST	CPOPJ1		;GREATER--OK

SERCH5:	ERRORA	<PROGRAM, IF IT EXISTS, HAS BEEN PASSED>
	POPJ	PP,


CPOPJ2:	AOS	(PP)
CPOPJ1:	AOS	(PP)
CPOPJ:	POPJ	PP,
;SCAN COMMAND TO SET UP INPUT AND OUTPUT FILES

GETDEV:	TRNE	SW,ENDCOM	;AT END OF COMMAND FILE?
	JRST	START		;YES--GO BACK TO TTY

	MOVE	CP,[POINT 7,COMSAV]

	ANDI	SW,NOTTY	;CLEAR ALL SWITCHES EXCEPT "COMMANDS NOT ON TTY"
	MOVE	TA,SA;RESET JOBFF
	MOVEM	TA,JOBFF

	TRNN	SW,NOTTY	;TYPE "*" IF COMMAND FILE NOT TTY
	TTCALL	3,[ASCIZ "
*"]
	PUSHJ	PP,GETTY	;IS THERE A
	CAIN	CH,12		;  LINE-FEED WAITING?
	JRST	GETDEV		;YES--FORGET IT

	IORI	SW,REGET	;NO--SET UP TO REGET CHARACTER

GTDEV0:	PUSHJ	PP,GTDEV6	;SCAN ONE FILE DESCRIPTOR
	CAIN	CH,"/"
	PUSHJ	PP,GTDEV7
	CAIN	CH,"@"
	JRST	GTDV0A

	SKIPN	DN
	MOVSI	DN,(SIXBIT "DSK")

	MOVE	TA,DN		;GET
	CALLI	TA,4		;  CHARACTERISTICS
	JUMPE	TA,NOTDEV	;IF ZERO--NOT A DEVICE
	MOVEM	TA,ODEVCH	;SAVE CHARACTERISTICS

	MOVEM	DN,FOFDAT+1
	MOVEM	FN,FOFNAM
	MOVEM	EX,FOFNAM+1
	MOVEM	PJ,FOFNAM+3
	JRST	GTDEV1

GTDV0A:	PUSHJ	PP,GTDEV8
	JRST	START1		;ERROR
	JRST	GTDEV0
;SET UP INPUT AND OUTPUT FILES (CONT'D).

;NOW DO LISTING FILE

GTDEV1:	CAIE	CH,","		;IS THERE A LISTING FILE?
	JRST	GTDV1B		;NO

GTDV1A:	PUSHJ	PP,GTDEV6	;YES--SCAN IT
	CAIN	CH,"/"
	PUSHJ	PP,GTDEV7
	CAIN	CH,"@"
	JRST	GTDV1C

	SKIPN	DN
	MOVSI	DN,(SIXBIT "DSK")
	MOVE	TA,DN
	CALLI	TA,4
	JUMPE	TA,NOTDEV
	MOVEM	DN,LSTDAT+1

	SKIPN	FN
	MOVE	FN,[SIXBIT "LIBARY"]
	MOVEM	FN,LSTNAM

	SKIPN	EX
	MOVSI	EX,(SIXBIT "LST")
	MOVEM	EX,LSTNAM+1
	MOVEM	PJ,LSTNAM+3

	OPEN	LST,LSTDAT
	JRST	NOTAV

	OUTBUF	LST,2

	ENTER	LST,LSTNAM
	JRST	CNTLST

	IORI	SW,LISTIT

	MOVE	TA,LSTDAT+1
	CALLI	TA,$GETCH
	TLNE	TA,10000
	IORI	SW,LSTTY

GTDV1B:	CAIE	CH,"="
	CAIN	CH,"_"
	JRST	GTDEV2
	PUSHJ	PP,BADCOM
	JRST	GOBACK

GTDV1C:	PUSHJ	PP,GTDEV8
	JRST	START1		;ERROR
	JRST	GTDV1A
;SET UP INPUT AND OUTPUT FILES  (CONT'D).

;NOW DO INPUT FILE

GTDEV2:	PUSHJ	PP,GTDEV6
	CAIN	CH,"/"
	PUSHJ	PP,GTDEV7
	CAIN	CH,"@"
	JRST	GTDV4A

	CAIE	CH,12
	JRST	GTDV8C

	JUMPN	DN,GTDEV3
	JUMPN	FN,GTDV2A
	JUMPN	EX,GTDV2A
	JUMPN	PJ,GTDV2A

	IORI	SW,ENDF!NOINP
	SETZM	INDEV
	TRNE	SW,LSWICH
	JRST	GTDV8C
	JRST	GTDV5B

GTDV2A:	MOVSI	DN,(SIXBIT "DSK")

GTDEV3:	MOVE	TA,DN
	CALLI	TA,4
	JUMPE	TA,NOTDEV
	TRNN	TA,1B23		;BINARY DEVICE?
	JRST	NOTBIN		;NO--ERROR

	TLNN	TA,4
	JRST	GTDEV4
	IORI	SW,INDIR
	JUMPE	FN,NONAME

GTDEV4:	TRNE	SW,OUTDIR
	SKIPE	FOFNAM
	JRST	GTDEV5

	JUMPE	FN,NONAME
	MOVEM	FN,FOFNAM
	JRST	GTDEV5

GTDV4A:	PUSHJ	PP,GTDEV8
	JRST	START1
	JRST	GTDEV2
;SET UP INPUT AND OUTPUT FILES (CONT'D)
;DOING INPUT FILE (CONT'D)

GTDEV5:	MOVEM	DN,INDEV
	MOVEM	DN,OIFDAT+1
	MOVEM	FN,OIFNAM
	MOVEM	FN,INFIL

	SKIPN	EX
	MOVSI	EX,(SIXBIT "LIB")
	HLLZM	EX,OIFNAM+1
	HLLZM	EX,INEXT
	MOVEM	PJ,OIFNAM+3
	MOVEM	PJ,INPP

	OPEN	OIF,OIFDAT
	JRST	NOTAV

	SETZM	OIFNAM+2
	MOVE	TE,[XWD OIFNAM,TA]
	BLT	TE,TD
	MOVE	TE,JOBFF
	MOVEM	TE,OIFBUF
	ADDI	TE,406

	MOVEI	TF,1
	TRNN	SW,LSWICH
	MOVEI	TF,2
	INBUF	OIF,(TF)

	MOVEM	TE,JOBFF

	LOOKUP	OIF,TA
	JRST	NOFIND
;SET UP I/O FILES  (CONT'D)

;INITIALIZE OUTPUT DEVICE

GTDV5B:	MOVE	TA,ODEVCH
	TLNE	TA,4
	IORI	SW,OUTDIR

	SKIPN	TB,FOFNAM
	MOVE	TB,OIFNAM
	JUMPN	TB,.+3
	TRNE	SW,OUTDIR
	JRST	NONAME
	MOVEM	TB,FOFNAM

	TRNE	SW,LSWICH	;ARE WE DOING "/L"?
	JRST	GTDV5C		;YES

	TRNN	TA,1B23		;BINARY DEVICE?
	JRST	NOTBIN		;NO--ERROR

	SKIPN	EX,FOFNAM+1	;ANY EXTENSION?
	MOVSI	EX,(SIXBIT "LIB")	;NO--USE LIB
	MOVEM	EX,FOFNAM+1

	MOVE	DN,FOFDAT+1
	OPEN	FOF,FOFDAT
	JRST	NOTAV
	OUTBUF	FOF,2
	POPJ	PP,

GTDV5C:	TRNE	SW,LISTIT	;ARE THERE TWO OUTPUT FILES?
	JRST	TUMANY		;YES
	SKIPN	EX,FOFNAM+1
	MOVSI	EX,(SIXBIT "LST")
	MOVEM	EX,FOFNAM+1
	MOVE	TA,[XWD FOFNAM,LSTNAM]
	BLT	TA,LSTNAM+3
	MOVE	DN,FOFDAT+1
	MOVEM	DN,LSTDAT+1
	SETZM	FOFDAT+1
	OPEN	LST,LSTDAT
	JRST	NOTAV
	OUTBUF	LST,2

	CALLI	DN,$GETCH
	TLNE	DN,10000
	IORI	SW,LSTTY
	POPJ	PP,
;SCAN 'DEV:FILE.EXT[P,P]

GTDEV6:	MOVEM	CP,SAVECP	;SAVE POINTER TO COMMAND SAVER
	SETZB	DN,FN
	SETZB	EX,PJ

	PUSHJ	PP,GETSIX
	CAIE	CH,":"
	JRST	GTDV6B
	MOVE	DN,TA
	PUSHJ	PP,GETSIX

GTDV6B:	MOVE	FN,TA
	CAIE	CH,"."
	JRST	GTDV6C
	PUSHJ	PP,GETSIX
	HLLZ	EX,TA

GTDV6C:	CAIE	CH,"["
	POPJ	PP,

	PUSHJ	PP,GETOCT
	HRLZ	PJ,TA
	CAIE	CH,","
	JRST	BADPP
	PUSHJ	PP,GETOCT
	HRR	PJ,TA
	CAIE	CH,"]"
	JRST	BADPP

	JRST	GETTY


;"/" SEEN -- CHECK FOR 'L' SWITCH

GTDEV7:	PUSHJ	PP,GETTY
	CAIE	CH,"L"
	JRST	GTDV7C
	IORI	SW,LSWICH
	JRST	GETTY

GTDV7C:	ERRORA	<L IS THE ONLY LEGAL SWITCH>
	JRST	GOBACK

;IMPROPER PROJ-PROG NUMBER

BADPP:	ERRORA	<IMPROPER PROJECT-PROGRAMMER NUMBER>
	JRST	GOBACK
;TERMINATOR IS "@", SET UP NEW COMMAND FILE

GTDEV8:	JUMPN	DN,GTDV8A
	JUMPN	FN,GTDV8A
	JUMPN	EX,GTDV8A
	JUMPN	PJ,GTDV8A
	PUSHJ	PP,GTDEV6
	JRST	GTDV8B

GTDV8A:	PUSHJ	PP,GETTY
GTDV8B:	CAIE	CH,12
	JRST	GTDV8C

	RELEASE	TTY,		;THROW AWAY OLD COMMAND DEVICE
	SKIPN	DN
	MOVSI	DN,(SIXBIT "DSK")
	MOVEI	TA,0
	MOVE	TB,DN
	MOVEI	TC,TTYIB
	OPEN	TTY,TA
	JRST	NOTAV

	MOVE	TA,FN
	MOVE	TB,EX
	MOVEI	TC,0
	MOVE	TD,PJ
	JUMPN	TB,GTDV8E
	MOVSI	TB,(SIXBIT 'CCL')
	LOOKUP	TTY,TA
	SKIPA
	JRST	GTDV8F

	MOVE	TB,EX
	MOVE	TD,PJ

GTDV8E:	LOOKUP	TTY,TA
	JRST	NOCOM
GTDV8F:	IORI	SW,NOTTY

	HLRZ	TA,JOBSA
	EXCH	TA,JOBFF
	INBUF	TTY,2
	MOVEM	TA,JOBFF

	ANDCMI	SW,ENDCOM
	MOVE	CP,SAVECP	;RESET POINTER TO COMMAND SAVER
	AOS	(PP)		;SKIP RETURN
	POPJ	PP,

GTDV8C:	PUSHJ	PP,BADCOM
	PUSH	PP,JOBFF
	JSP	TA,INITTY	;GET TTY FOR DEVICE
	POP	PP,JOBFF
	POPJ	PP,
;ERRORS DETECTED WHILE SCANNING FILE COMMAND

CNTFOF:	ERRORA	<CANNOT ENTER OUTPUT FILE>
	JRST	GOBACK

CNTLST:	ERRORA	<CANNOT ENTER LISTING FILE>
GOBACK:	PUSHJ	PP,SKPTTY
	JRST	START


NOTDEV:	ERRORA	<IMPROPER DEVICE>
	JRST	GOBACK
NOTBIN:	ERRORA	<INPUT AND OUTPUT DEVICES MUST BE BINARY>
	JRST	GOBACK
NONAME:	ERRORA	<DIRECTORY DEVICES REQUIRE FILE NAMES>
	JRST	GOBACK
NOFIND:	ERRORA	<CANNOT FIND INPUT FILE>
	JRST	GOBACK

NOTAV:	MOVE	TA,[POINT 6,DN]
	MOVE	TB,[POINT 7,COMWRD]

NOTAV1:	ILDB	TC,TA
	JUMPE	TC,NOTAV2
	ADDI	TC,40
	IDPB	TC,TB
	TLNE	TA,770000
	JRST	NOTAV1

NOTAV2:	MOVEI	TC,0
	IDPB	TC,TB

	PUSHJ	PP,TYPEQB
	TTCALL	3,[ASCIZ /"/]
	TTCALL	3,COMWRD
	TTCALL	3,[ASCIZ /" NOT AVAILABLE
/]
	JRST	GOBACK
NOCOM:	ERRORA	<CANNOT FIND COMMAND FILE>
	JRST	GOBACK

BADSW:	ERRORA	<IMPROPER SWITCH>
	JRST	GOBACK

TUMANY:	ERRORA	<ONLY ONE OUTPUT FILE ALLOWED WITH /L>
	JRST	GOBACK
;INITIALIZE TTY.
;ENTER WITH 'JSP TA,INITTY'

INITTY:	MOVEI	SW,0		;CLEAR SWITCHES
	CALLI	0		;RESET DEVICES
	INIT	TTY,1		;ASCII-LINE MODE
	SIXBIT	'TTY'
	XWD	0,TTYIB
	JRST	CANTTY		;NO TTY????

	INBUF	TTY,2
	JRST	(TA)

CANTTY:	TTCALL	3,[ASCIZ "?CANNOT INIT TTY
"]
	CALLI	12
;TYPE OUT '<C.R.>?'
;IF COMMAND FILE IS NOT ON TTY, TYPE OUT LATEST COMMAND.

TYPEQA:	TTCALL	3,[ASCIZ "
?"]
	TRNN	SW,NOTTY
	POPJ	PP,

	JRST	TYPEQ4


;TYPE OUT '<C.R.>?'
;IF INPUT IS NOT FROM TTY, TYPE OUT LATEST LIBRARY NAME FOLLOWED BY
;	PREVIOUS COMMAND.

TYPEQB:	TTCALL	3,[ASCIZ "
?"]
	TRNN	SW,NOTTY
	POPJ	PP,

	MOVE	TA,[POINT 6,LASTPG]
TYPEQ2:	ILDB	TB,TA
	JUMPE	TB,TYPEQ3
	ADDI	TB,40
	CAIN	TB,":"
	MOVEI	TB,"-"
	TTCALL	1,TB
	JRST	TYPEQ2

TYPEQ3:	TTCALL	3,[ASCIZ " -- "]
TYPEQ4:	MOVEI	TB,0
	IDPB	TB,CP
	TTCALL	3,COMSAV
	TTCALL	3,[ASCIZ "
"]
	MOVE	CP,[POINT 7,COMSAV]
	POPJ	PP,
;GET INPUT AND OUTPUT DEVICES (CONT'D).

;READ NEXT SIXBIT NAME FROM COMMAND STRING INTO TA.

GETSIX:	MOVEI	TA,0
	MOVE	TB,[POINT 6,TA]

GETSX1:	PUSHJ	PP,GETTY
	CAIG	CH,"Z"
	CAIGE	CH,"A"
	JRST	GETSX3

GETSX2:	SUBI	CH,40
	TLNE	TB,770000
	IDPB	CH,TB
	JRST	GETSX1

GETSX3:	CAIG	CH,"9"
	CAIGE	CH,"0"
	POPJ	PP,
	JRST	GETSX2

;GET NEXT OCTAL NUMBER FROM COMMAND STRING INTO TA

GETOCT:	MOVEI	TA,0

GETOC1:	PUSHJ	PP,GETTY
	CAIG	CH,"7"
	CAIGE	CH,"0"
	POPJ	PP,

	LSH	TA,3
	IORI	TA,-"0"(CH)
	CAIG	TA,-1
	JRST	GETOC1
	POPJ	PP,
;GET NAME OF PROGRAM FROM TTY

GETNAM:	SETZM	OFFDEV
	ANDCMI	SW,RENUM
	SETZM	NEWNAM
	SETZM	NEWNAM+1
	MOVE	TA,[POINT 6,NEWNAM]

GTNAM0:	PUSHJ	PP,GETTY	;PASS OVER INTERVENING SPACES
	CAIE	CH," "
	CAIN	CH,11
	JRST	GTNAM0

	SKIPA
GTNAM1:	PUSHJ	PP,GETTY
	CAIG	CH,"Z"
	CAIGE	CH,"A"
	JRST	GTNAM3

GTNAM2:	CAIN	CH,"-"
	MOVEI	CH,":"
	SUBI	CH,40
	CAME	TA,[POINT 6,NEWNAM+1,11]
	IDPB	CH,TA
	JRST	GTNAM1

GTNAM3:	CAIG	CH,"9"
	CAIGE	CH,"0"
	CAIN	CH,"-"
	JRST	GTNAM2
GTNM3A:	CAIN	CH,"/"
	JRST	GTNAM7

GTNM3B:	CAIN	CH,12
	JRST	GTNAM5

	CAIE	CH,","
	JRST	GTNM3C
	SKIPE	OFFDEV
	JRST	GTNM3D
	PUSHJ	PP,GTNM10
	JRST	GTNM3A

GTNM3C:ERRORA	<IMPROPER CHARACTER IN NAME>
	JRST	SKPTTY
GTNM3D:	ERRORA	<ONLY ONE FILE ALLOWED>
	PUSHJ	PP,SKPTTY
	JRST	GTNAM9
;GET NAME OF PROGRAM FROM COMMAND FILE (CONT'D)

GTNAM5:	SKIPE	NEWNAM
	JRST	GTNAM9
	ERRORA	<NO NAME SPECIFIED>
	POPJ	PP,

;SWITCH SEEN

GTNAM7:	PUSHJ	PP,GETTY
	CAIE	CH,"N"
	JRST	GTNAM8

	MOVEI	CH,1
	MOVEM	CH,RENLIN
	IORI	SW,RENUM

	PUSHJ	PP,GETTY
	JRST	GTNM3B
GTNAM8:	TTCALL	3,[ASCIZ "
?BAD SWITCH
"]
	JRST	SKPTTY

GTNAM9:	MOVE	CH,[XWD NEWNAM,LASTPG]
	BLT	CH,LASTPG+1
	AOS	(PP)
	POPJ	PP,


;COMMA SEEN -- SCAN FILE NAME

GTNM10:	PUSHJ	PP,GTDEV6
	SKIPN	DN
	MOVSI	DN,(SIXBIT "DSK")
	MOVEM	DN,OFFDEV
	MOVEM	FN,OFFNAM
	HLLZM	EX,OFFNAM+1
	MOVEM	PJ,OFFNAM+3
	POPJ	PP,
;CONVERT JOB NUMBER IN "TA" TO DECIMAL

CONVBD:	MOVEI	TD,3

CONVB1:	IDIVI	TA,^D10
	ADDI	TB,"0"-40
	LSHC	TB,-6
	SOJG	TD,CONVB1

	POPJ	PP,
;COMPARE "NEWNAM" AGAINST TWO WORDS WHOSE ADDRESS IS IN TB.
;EXIT TI CALL+1 IF NEWNAM<(TB)
;EXIT TO CALL+2 IF NEWNAM>(TB)
;EXIT TO CALL+3 IF NEWNAM=(TB)

COMPAR:	SKIPL	TC,NEWNAM
	JRST	COMP3
	SKIPL	(TB)
	JRST	CPOPJ1

COMP1:	CAMN	TC,(TB)
	JRST	COMP2
	CAMG	TC,(TB)
	JRST	CPOPJ
	JRST	CPOPJ1

COMP2:	HLRZ	TC,NEWNAM+1
	HLRZ	TD,1(TB)
	LSH	TC,-6
	LSH	TD,-6
	CAMN	TC,TD
	JRST	CPOPJ2
	CAMG	TC,TD
	JRST	CPOPJ
	JRST	CPOPJ1

COMP3:	SKIPL	(TB)
	JRST	COMP1
	JRST	CPOPJ
;COPY A LINE FROM TTY TO OUTPUT BUFFER

PUTLIN:	MOVEI	WD,^D10*2
	TRNN	SW,RENUM
	SKIPA	WD,COMLIN
	ADDB	WD,RENLIN
	MOVEM	WD,OUTLIN

PUTLI1:	PUSHJ	PP,PUTTOF
	MOVEI	WD,0
	MOVE	TB,[POINT 7,WD]

PUTLI2:	PUSHJ	PP,GETTY

	IDPB	CH,TB

	CAIN	CH,12
	JRST	PUTTOF

	TLNE	TB,760000
	JRST	PUTLI2
	JRST	PUTLI1
;PUT INPUT NAME INTO OUTPUT DIRECTORY

PUTINM:	MOVE	TA,INNAM
	MOVEM	TA,OUTNAM
	MOVE	TA,INNAM+1
	MOVEM	TA,OUTNAM+1
	JRST	PUTNAM


;PUT NEW NAME INTO OUTPUT DIRECTORY

PUTNNM:	MOVE	TA,NEWNAM
	MOVEM	TA,OUTNAM
	MOVE	TA,NEWNAM+1
	MOVEM	TA,OUTNAM+1

	TRNN	SW,LISTIT	;ANY LISTING FILE?
	JRST	PUTNAM		;NO

	IORI	SW,DOLIST	;YES--SET "LIST THIS PROGRAM" FLAG
	PUSHJ	PP,LSTHDG	;PUT OUT HEADING LINE
;PUT OUTPUT NAME INTO DIRECTORY.
;IF THIS IS THE FIRST WORD OF A DIRECTORY BLOCK, PUT ENTRY INTO ROUGH TABLE.

PUTNAM:	MOVE	TC,TOFCTR	;IS FILE TOO LARGE?
	TLNE	TC,777700
	JRST	PUTNM6		;YES

	MOVE	TB,OUTNAM+1	;NO
	AND	TB,[XWD 777700,0]
	IOR	TB,TC

	MOVE	TA,PRGCTR	;ARE WE ABOUT TO WRITE FIRST WORD OF A BLOCK?
	TRNE	TA,77
	JRST	PUTNM2		;NO

	MOVE	TA,RUFCTR	;YES--IS ROUGH TABLE FULL?
	CAILE	TA,176
	JRST	PUTNM5		;YES

	MOVE	TC,OUTNAM
	MOVEM	TC,RUFTAB(TA)

	MOVE	TC,OUTNAM+1
	AND	TC,[XWD 777700,0]
	IOR	TC,FINCTR
	MOVEM	TC,RUFTAB+1(TA)

	MOVEI	TC,200
	ADDM	TC,FINCTR

	MOVEI	WD,2
	ADDM	WD,RUFCTR

PUTNM2:	AOS	PRGCTR
	MOVEI	WD,2
	ADDM	WD,OFFSET
	MOVE	WD,OUTNAM
	PUSHJ	PP,PUTTOD
	MOVE	WD,TB
	JRST	PUTTOD

;ROUGH TABLE IS FULL

PUTNM5:	ERRORA	<TOO MANY PROGRAMS>
	JRST	KILLIM

;FILE TOO LARGE

PUTNM6:	ERRORA	<FILE TOO LARGE>
	JRST	KILLIM
;GET A CHARACTER FROM COMMAND FILE.
;PRINTER-CONTROL CHARACTERS (EXCEPT FORM-FEED) AND ALTMODES ARE CONVERTED
;	TO LINE-FEEDS.
;CARRIAGE-RETURNS AND NULLS ARE IGNORED.

GETTY:	TRNE	SW,ENDCOM	;COMMAND FILE EMPTY?
	JRST	GETY2A		;YES

	TRZE	SW,REGET	;REGET LAST CHARACTER?
	JRST	GETTY4		;YES

	SOSG	TTYIB+2		;NO--GET NEXT ONE
	JRST	GETTY5
GETTY0:	ILDB	CH,TTYIB+1

GETTY1:	CAIN	CH,15
	JRST	GETTY

	CAIN	CH,32
	JRST	GETY5A
	JUMPE	CH,GETTY

	CAIE	CH,175
	CAIN	CH,176
	JRST	GETTY2
	CAIN	CH,33
	JRST	GETTY2
	CAIE	CH,13
	CAIN	CH,14
	JRST	GETY2A
	CAIG	CH,24
	CAIGE	CH,20
	JRST	GETTY7

GETTY2:	TRNN	SW,NOTTY
	TTCALL	3,[ASCIZ "
"]
GETY2A:	MOVEI	CH,12
	DPB	CH,TTYIB+1
	POPJ	PP,

GETTY4:	LDB	CH,TTYIB+1
	JRST	GETTY1
;GET CHARACTER FROM COMMAND FILE  (CONT'D).

;GET ANOTHER BUFFER FULL

GETTY5:	IN	TTY,
	JRST	GETTY0

	GETSTS	TTY,CH		;END-OF-FILE?
	TRNN	CH,1B22
	JRST	GETTY6		;NO
GETY5A:	IORI	SW,ENDCOM	;YES
	JRST	GETY2A


GETTY6:	TTCALL	3,[ASCIZ "
?READ ERROR ON COMMAND FILE
"]
	JRST	START

;PUT CHARACTER INTO COMMAND STREAM SAVER

GETTY7:	CAME	CP,[POINT 7,COMSAV+8,27]
	IDPB	CH,CP
	POPJ	PP,
;DISPLAY "IMPROPER COMMAND" AND SKIP TO END OF LINE

BADCOM:	ERRORB	<IMPROPER COMMAND>

;SKIP THE REST OF THE TTY INPUT LINE

SKPTTY:	IORI	SW,REGET
SKPTT1:	PUSHJ	PP,GETTY
	CAIE	CH,12
	JRST	SKPTT1
	POPJ	PP,
;WRITE CONTENTS OF "WD" ONTO LISTING FILE

PUTLST:	TRNN	WD,1		;SEQUENCE NUMBER?
	JRST	PUTL6		;NO

	SKIPGE	TE,WD		;YES-- GET LINE NUMBER (BITS 0-34)
	POPJ	PP,		;LINE NUMBER WAS NEGATIVE--FORGET IT

	LSH	TE,-1
	MOVE	TG,[XWD -6,DECIML]

PUTL1:	IDIV	TE,(TG)
	MOVEI	CH,"0"(TE)
	PUSHJ	PP,PUTL2
	MOVE	TE,TE+1
	AOBJN	TG,PUTL1

	MOVEI	CH,11

PUTL2:	SOSG	LSTBH+2
	JRST	PUTL4
PUTL3:	IDPB	CH,LSTBH+1
	POPJ	PP,

PUTL4:	OUT	LST,
	JRST	PUTL3
	JRST	LSTERA

PUTL6:	SKIPA	TE,[POINT 7,WD]
PUTL7:	PUSHJ	PP,PUTL2
PUTL8:	TLNN	TE,760000
	POPJ	PP,

	ILDB	CH,TE
	JUMPE	CH,PUTL8
	CAIE	CH,12
	CAIN	CH,14
	SKIPA
	JRST	PUTL7

	MOVEI	CH,15
	PUSHJ	PP,PUTL2
	LDB	CH,TE

	MOVEI	TE,LINPAG
	CAIN	CH,14
	MOVEM	TE,LINECT

	JRST	PUTL2
;WRITE OUT HEADING LINE ONTO LISTING FILE

LSTHDG:	TRNE	SW,LSTTY	;IS LISTING ON CONSOLE?
	JRST	LSTHD5		;YES

	MOVE	CH,LINECT
	CAIN	CH,LINPAG
	JRST	LSTHD1

	MOVEI	CH,15
	PUSHJ	PP,PUTL2
	MOVEI	CH,14
	PUSHJ	PP,PUTL2

LSTHD1:	MOVE	TE,[POINT 6,OUTNAM]

LSTHD2:	ILDB	CH,TE
	JUMPE	CH,LSTHD3
	ADDI	CH,40
	CAIN	CH,":"
	MOVEI	CH,"-"
	PUSHJ	PP,PUTL2
	MOVEI	CH," "
	PUSHJ	PP,PUTL2
	JRST	LSTHD2

LSTHD3:	SKIPA	TE,[POINT 7,HEADER]

LSTHD4:	PUSHJ	PP,PUTL2
	ILDB	CH,TE
	JUMPN	CH,LSTHD4

	MOVEI	CH,LINPAG-1
	MOVEM	CH,LINECT

	POPJ	PP,

LSTHD5:	MOVE	TE,[POINT 7,[ASCIZ "

*** PROGRAM "]]
	PUSHJ	PP,PUTMES

	MOVE	TE,[POINT 6,OUTNAM]

LSTF5:	ILDB	CH,TE
	ADDI	CH,40
	CAIN	CH,":"
	MOVEI	CH,"-"
	PUSHJ	PP,PUTKAR
	CAME	TE,[POINT 6,OUTNAM+1,11]
	JRST	LSTF5

	MOVE	TE,[POINT 7,[ASCIZ "

"]]
	PUSHJ	PP,PUTMES

	HRLOI	CH,377777
	MOVEM	CH,LINECT

	POPJ	PP,
;PUT DATE AND TIME INTO HEADING LINE

DATIME:	CALLI	TE,14		;GET DATE
	IDIVI	TE,^D31*^D12	;TE=YEAR
	IDIVI	TF,^D31		;TF=MONTH,TG=DAY

	ADDI	TG,1
	PUSHJ	PP,DATIM9
	TRNN	TG,3600
	TRZ	TG,4000
	DPB	TG,[POINT 14,DATE,34]

	MOVE	TG,MOTABL(TF)
	DPB	TG,[POINT 21,DATE+1,27]

	MOVEI	TG,^D64(TE)
	PUSHJ	PP,DATIM9
	DPB	TG,[POINT 14,DATE+2,13]

	CALLI	TF,23		;GET TIME IN MILLISECONDS
	IDIVI	TF,^D1000*^D60	;CONVERT TO MINUTES
	IDIVI	TF,^D60
	PUSHJ	PP,DATIM9
	DPB	TG,[POINT 14,TIME,34]
	MOVE	TG,TF
	PUSHJ	PP,DATIM9
	DPB	TG,[POINT 14,TIME,13]

	POPJ	PP,

DATIM9:	IDIVI	TG,^D10
	LSH	TG,7
	ADDI	TG,14060(TH)
	POPJ	PP,
;OUTPUT ROUTINES

;PUT A WORD ONTO TEMPORARY OUTPUT FILE

PUTTOD:	SOSG	TODOB+2
	JRST	PTOD2
PTOD1:	IDPB	WD,TODOB+1
	POPJ	PP,

PTOD2:	OUT	TOD,
	JRST	PTOD1
	JRST	SCROE

;PUT A WORD ONTO TEMPORARY INPUT DIRECTORY

PUTTID:	SOSG	TIDOB+2
	JRST	PTID2
PTID1:	IDPB	WD,TIDOB+1
	POPJ	PP,

PTID2:	OUT	TID,
	JRST	PTID1
	JRST	SCROE

;PUT A WORD ONTO FINAL OUTPUT FILE

PUTFOF:	SOSG	FOFOB+2
	JRST	PFOF2
PFOF1:	IDPB	WD,FOFOB+1
	POPJ	PP,

PFOF2:	OUT	FOF,
	JRST	PFOF1
	JRST	SCROE

;PUT A WORD ONTO TEMPORARY OUTPUT FILE

PUTTOF:	AOS	TOFCTR

	SOSG	TOFOB+2
	JRST	PTOF2
PTOF1:	IDPB	WD,TOFOB+1
	TRNE	SW,DOLIST
	PUSHJ	PP,PUTLST
	POPJ	PP,

PTOF2:	OUT	TOF,
	JRST	PTOF1
	JRST	SCROE

;INPUT ROUTINES

;GET A WORD FROM TEMPORARY OUTPUT DIRECTORY

GETTOD:	SOSG	TODIB+2
	JRST	GTOD2
GTOD1:	ILDB	WD,TODIB+1
	POPJ	PP,

GTOD2:	IN	TOD,
	JRST	GTOD1
	JRST	SCRIE

;GET A WORD FROM TEMPORARY INPUT DIRECTORY

GETTID:	TRNE	SW,ENDF	;ANY MORE INPUT FILE?
	JRST	GOIF3		;NO

	SOSG	TIDIB+2
	JRST	GTID2
GTID1:	ILDB	WD,TIDIB+1
	POPJ	PP,

GTID2:	IN	TID,
	JRST	GTID1
	JRST	SCRIE

;GET A WORD FROM TEMPORARY OUTPUT FILE

GETTOF:	SOSG	TOFIB+2
	JRST	GTOF2
GTOF1:	ILDB	WD,TOFIB+1
	AOS	(PP)
	POPJ	PP,

GTOF2:	IN	TOF,
	JRST	GTOF1
	GETSTS	TOF,WD
	TRNE	WD,740000
	JRST	SCRIE
	POPJ	PP,
;INPUT ROUTINES  (CONT'D).

;GET A WORD FROM OLD INPUT FILE

GETOIF:	TRNE	SW,ENDF	;ANY MORE INPUT FILE?
	JRST	GOIF3		;NO

	SOSG	OIFIB+2
	JRST	GOIF2
GOIF1:	ILDB	WD,OIFIB+1
	POPJ	PP,

GOIF2:	IN	OIF,
	JRST	GOIF1
	JRST	INPERA


GOIF3:	MOVNI	WD,1
	POPJ	PP,
;SET UP ANY 'OFF-LINE' FILE FOR INPUT

SETOFF:	SKIPN	OFFDEV
	JRST	CPOPJ1

	SKIPN	TA,OFFBUF
	HRRZ	TA,JOBFF
	HRRM	TA,JOBFF
	MOVEM	TA,OFFBUF
	MOVEI	TA,0
	MOVE	TB,OFFDEV
	MOVEI	TC,OFFBH
	OPEN	OFF,TA
	JRST	NOOFFD

	INBUF	OFF,2

	MOVE	TE,[XWD OFFNAM,TA]
	BLT	TE,TD
	LOOKUP	OFF,TA
	JRST	NOOFFL
	AOS	(PP)
	POPJ	PP,

NOOFFD:	TTCALL	3,[ASCIZ "?DEVICE DOES NOT EXIST
"]
	POPJ	PP,

NOOFFL:	TTCALL	3,[ASCIZ "?CANNOT FIND FILE
"]
	POPJ	PP,
;COPY PROGRAM FROM 'OFF-LINE' FILE

COPOFF:	MOVEI	TA,1		;SET LINE # TO ZERO
	MOVEM	TA,OUTLIN

	PUSHJ	PP,GETOFF	;GET A CHARACTER
	JUMPE	CH,CPOPJ	;IF END-OF-FILE -- QUIT

COPF01:	MOVE	TA,@OFFBH+1	;ANY SEQ #?
	TRNN	TA,1
	JRST	COPF06		;NO

COPF02:	TRNN	SW,RENUM	;YES--ARE WE RE-NUMBERING?
	JRST	COPF03		;NO
	MOVNI	TA,5		;YES--SKIP PAST SEQ #
	ADDM	TA,@OFFBH+2
	AOS	OFFBH+1
	MOVSI	WD,1B18		;DUMMY # < ANYTHING
	JRST	COPF05

COPF03:	MOVEI	WD,0
	MOVEI	TB,5
COPF04:	IMULI	WD,^D10
	ADDI	WD,-"0"(CH)
	PUSHJ	PP,GETOFF
	SOJG	TB,COPF04
	LSH	WD,1
	IORI	WD,1

COPF05:	PUSHJ	PP,GETOFF	;GET FIRST DATA CHARACTER
	MOVEI	TA,1		;CLEAR "SEQ" FLAG
	ANDCMI	TA,@OFFBH+1	;  IN THAT WORD
	CAMLE	WD,OUTLIN
	JRST	COPF07

COPF06:	MOVEI	WD,^D10*2
	ADD	WD,OUTLIN

COPF07:	MOVEM	WD,OUTLIN
;COPY 'OFF-LINE' FILE (CONT'D)

COPF08:	PUSH	PP,CH
	PUSHJ	PP,PUTTOF	;PUT OUT ONE WORD
	POP	PP,CH
	MOVE	TB,[POINT 7,WD]	;RESET
	MOVEI	WD,0		;  WORD
	JUMPE	CH,COPF10

COPF09:	IDPB	CH,TB		;STASH CHARACTER
	CAIN	CH,12		;END OF LINE?
	JRST	COPF11

	PUSHJ	PP,GETOFF	;NO--GET ANOTHER
	MOVE	TA,@OFFBH+1	;SEQ #?
	TRNE	TA,1
	JRST	COPF10		;YES

	TLNE	TB,760000	;NO--IS OUTPUT WORD FULL?
	JRST	COPF09		;NO
	JRST	COPF08		;YES

COPF10:	MOVEI	TA,12		;STASH END-OF-LINE
	IDPB	TA,TB
	SKIPA

COPF11:	PUSHJ	PP,GETOFF	;GET CHARACTER AFTER LINE-FEED
COPF12:	PUSH	PP,CH
	PUSHJ	PP,PUTTOF	;PUT OUT FINAL WORD FOR LINE
	POP	PP,CH
	JUMPN	CH,COPF01	;LOOP UNTIL END-FILE

	POPJ	PP,
;GET CHARACTER FROM 'OFF-LINE' FILE

GETOFF:	SOSG	OFFBH+2
	JRST	GETOF3
GETOF1:	ILDB	CH,OFFBH+1
	CAILE	CH,37
	POPJ	PP,

	JUMPE	CH,GETOFF
	CAIN	CH,15
	JRST	GETOF2

	CAIG	CH,24
	CAIGE	CH,12
	POPJ	PP,
	CAILE	CH,14
	CAIL	CH,20
	JRST	GETOFF
	POPJ	PP,

GETOF2:	MOVEI	CH,12
	POPJ	PP,

GETOF3:	IN	OFF,
	JRST	GETOF1
	STATZ	OFF,740000
	TTCALL	3,[ASCIZ "
?READ ERROR ON INSERTION FILE
"]
	MOVEI	CH,0
	RELEASE	OFF,
	POPJ	PP,
;PUT LINE NUMBER INTO 'OFF-LINE' FILE

OFFNUM:	MOVE	TA,OFFBH+1	;MAKE
	TLNN	TA,760000	;  SURE
	JRST	OFFNM1		;  WE
	MOVEI	CH,0		;  ARE
	PUSHJ	PP,PUTOFF	;  AT
	JRST	OFFNUM		;  WORD BOUNDARY

OFFNM1:	SOS	TA,WD		;GET LINE NUMBER BACK
	LSH	TA,-1		;SHIFT OFF BIT 35
	MOVSI	TC,1B18		;GET READY TO SHIFT IN A NUMBER

OFFNM2:	IDIVI	TA,^D10		;GET LOW-OERDER DIGIT INTO TB
	ADDI	TB,"0"		;CONVERT TO ASCII
	LSHC	TB,-7		;SHIFT IT INTO TC
	TRNN	TC,1		;IF WE DON'T HAVE FIVE DIGITS,
	JRST	OFFNM2		;  LOOP

	MOVE	TA,[POINT 7,TC]
OFFNM3:	ILDB	CH,TA		;GET HIGH-ORDER DIGIT
	PUSHJ	PP,PUTOFF	;WRITE IT OUT
	TLNE	TA,760000	;LOOP UNTIL
	JRST	OFFNM3		;  FIVE DIGITS OUT

	MOVEI	TA,1		;SET
	IORM	TA,OFFBH+1	;  BIT 35

	MOVEI	CH,11		;PUT OUT TAB AND
	JRST	PUTOFF		;  RETURN

;PUT A CHARACTER INTO 'OFF-LINE' FILE

PUTOFF:	SOSG	OFFBH+2		;IF BUFFER IS FULL,
	JRST	PUTOF2		;  WRITE IT OUT
PUTOF1:	IDPB	CH,OFFBH+1	;STASH CHARACTER
	POPJ	PP,		;EXIT

PUTOF2:	OUT	OFF,		;WRITE OUT A BUFFER FULL
	JRST	PUTOF1		;NO ERRORS--ALL IS WELL

	ERRORA	<OUTPUT ERROR ON EXTRACTION FILE>
	JRST	KILLIM		;TOUGH
;CLOSE OUT TEMPORARY OUTPUT FILES AFTER COPYING REST OF INPUT

CLOSOT:	TRNE	SW,ENDF		;END OF INPUT SEEN?

	JRST	CLSOT1

	ANDCMI	SW,ENDP
	PUSHJ	PP,PUTINM

	PUSHJ	PP,COPYP
	JRST	CLOSOT

CLSOT1:	MOVNI	WD,1
	PUSHJ	PP,PUTTOD

	AOS	OFFSET

	CLOSE	TOD,		;CLOSE OUTPUT DIRECTORY
	CLOSE	TOF,		;CLOSE OUTPUT PROGRAMS

	POPJ	PP,
;CREATE "BAK" FILE IF NECESSARY.
;SET UP OUTPUT FILE.

SETBAK:	TRNN	SW,NOINP	;ANY INPUT DEVICE?
	TRNN	SW,INDIR	;YES--DIRECTORY DEVICE?
	JRST	SETBK4		;NO

	MOVE	TA,OIFNAM	;SAME NAME FOR
	CAME	TA,FOFNAM	; INPUT AND OUTPUT?
	JRST	SETBK4		;NO

	HLLZ	TA,OIFNAM+1	;YES--SAME EXTENSION?
	HLLZ	TB,FOFNAM+1
	CAME	TA,TB
	JRST	SETBK4		;NO

	MOVE	TA,OIFNAM+3
	CAME	TA,FOFNAM+3
	JRST	SETBK4

;INPUT FILE SHOULD BE CHANGED TO HAVE EXTENSION "BAK"

	MOVE	TA,OIFNAM	;ANY BACKUP FILE ALREADY?
	MOVSI	TB,(SIXBIT "BAK")
	MOVEI	TC,0
	MOVE	TD,OIFNAM+3
	LOOKUP	TOD,TA
	JRST	SETBK5		;NO

	CLOSE	TOD,		;YES--DELETE IT
	SETZB	TA,TB
	SETZB	TC,TD
	RENAME	TOD,TA
	JRST	SETBK6		;COULDN'T DELETE

SETBK2:	MOVE	TA,OIFNAM	;RENAME INPUT FILE
	HLLZ	TB,OIFNAM+1
	MOVEI	TC,0
	MOVE	TD,OIFNAM+3
	LOOKUP	TOD,TA
	JRST	SETBK9		;CAN'T FIND IT NOW

	CLOSE	TOD,
	MOVSI	TB,(SIXBIT "BAK")
	MOVE	TD,OIFNAM+3
	RENAME	TOD,TA
	JRST	SETBK9

;NOW SET UP OUTPUT FILE

SETBK4:	ENTER	FOF,FOFNAM
	JRST	CNTFOF

	POPJ	PP,
;RENAME INPUT FILE TO "BAK" (CONT'D)

SETBK5:	HRRZS	TB		;COULDN'T FIND "BAK"--IS IT THERE?
	JUMPE	TB,SETBK2	;IF JUMP--IT REALLY WASN'T THERE

SETBK6:	TTCALL	3,[ASCIZ /
?CANNOT DELETE "BAK" FILE/]

SETBK7:	TTCALL	3,[ASCIZ "
WRITING "]

	MOVE	TA,[POINT 6,TIDNAM]
SETBK8:	ILDB	TB,TA
	ADDI	TB,40
	TTCALL	1,TB
	CAME	TA,[POINT 6,TIDNAM,17]
	JRST	SETBK8

	TTCALL	3,[ASCIZ "LIB.LIB AS OUTPUT FILE
"]

	MOVE	TA,TIDNAM
	HRRI	TA,(SIXBIT "LIB")
	MOVEM	TA,FOFNAM
	MOVSI	TA,(SIXBIT "LIB")
	MOVEM	TA,FOFNAM+1
	JRST	SETBK4

SETBK9:	TTCALL	3,[ASCIZ /
?CANNOT RENAME INPUT TO "BAK"/]
	JRST	SETBK7
;ERROR ROUTINES

DIRERA:	TTCALL	3,[ASCIZ "?ERROR READING TABLE
"]
	JRST	KILL2

TRBL1:	TTCALL	3,[ASCIZ "?FIRST WORD NOT LINE NUMBER
"]
	JRST	KILL2

TRBL2:	TTCALL	3,[ASCIZ "?IMBEDDED LINE NUMBER
"]
	JRST	KILL2

NODSK:	TTCALL	3,[ASCIZ "
?DSK IS NOT A DEVICE"]
	JRST	KILLIM

NOEDSK:	TTCALL	3,[ASCIZ "
?CAN'T ENTER A SCRATCH FILE ON DSK"]
	JRST	KILLIM

NOEOFF:	ERRORA	<CANNOT ENTER EXTRACTION FILE>
	JRST	KILLIM

NOLDSK:	TTCALL	3,[ASCIZ "
?CAN'T FIND A SCRATCH FILE ON DSK"]
	JRST	KILLIM

NOPROG:	ERRORA	<THAT PROGRAM NOT FOUND>
	JRST	NEWCOM

SCROE:	TTCALL	3,[ASCIZ "
?OUTPUT ERROR ON SCRATCH FILE"]
	JRST	KILLIM

SCRIE:	TTCALL	3,[ASCIZ "
?READ ERROR ON SCRATCH FILE"]
	JRST	KILLIM

LSTERA:	TTCALL	3,[ASCIZ "
?OUTPUT ERROR ON LISTING FILE
"]
	JRST	KILLIM

INPERA:	TTCALL	3,[ASCIZ "
?ERROR READING INPUT FILE"]

KILLIM:	CALLI	0		;THROW AWAY ALL OUTPUT FILES
KILL2:	CALLI	12		;GIVE UP
;ACCUMULATORS

SW=0			;SWITCH REGISTER
TA=1			;TEMPORARY
TB=2			;TEMPORARY
TC=3			;TEMPORARY
TD=4			;TEMPORARY
TE=5			;TEMPORARY
TF=6			;TEMPORARY
TG=7			;TEMPORARY
TH=10			;TEMPORARY

DN=11			;DEVICE NAME FROM "GETDEV"
FN=12			;FILE-NAME FROM "GETDEV"
EX=13			;EXTENSION FROM "GETDEV"
PJ=14			;PROJ-PROG NUMBER FROM "GETDEV"

CP==TH			;POINTER TO 'COMSAV' AREA
PN==DN			;POINTER TO FINE TABLE (/L ONLY)
FT==FN			;RELATIVE ADDRESS OF FINE TABLE (/L ONLY)

WD=14			;I/O WORD
LN=15			;LINE NUMBER
CH=16			;TTY CHARACTER
PP=17			;PUSH-DOWN POINTER

;I/O CHANNELS

TTY==1			;TELETYPE
TOD==2			;TEMPORARY OUTPUT DIRECTORY
TOF==3			;TEMPORARY OUTPUT FILE
TID==4			;TEMPORARY INPUT DIRECTORY
OIF==5			;ORIGINAL INPUT FILE
FOF==6			;FINAL OUTPUT FILE
LST==7			;LISTING FILE
OFF==10		;'OFF-LINE' FILE

;MISCELLANEOUS

RUFPTR:	IOWD	200,RUFTAB+1
LINPAG==^D54			;LINES PER PRINTED PAGE (EXCLUDING HEADING)
$GETCH==4	;CALLI CODE FOR 'DEVCHR'
;SWITCH REGISTER FLAGS

ENDP==1B18		;END OF INPUT PROGRAM SEEN
ENDF==1B19		;END OF OUTPUT PROGRAM SEEN
FSRC==1B20		;INPUT FILE HAS BEEN MOVED
REGET==1B21		;GET PREVIOUS CHARACTER FROM TTY
INDIR==1B22		;INPUT FILE IS A DIRECTORY DEVICE
NOINP==1B23		;THERE IS NO INPUT FILE
RENUM==1B24		;RENUMBER THE OUTPUT PROGRAM
OUTDIR==1B25		;OUTPUT DEVICE HAS A DIRECTORY
LISTIT==1B26		;THERE IS A LISTING FILE
DOLIST==1B27		;LIST THIS FILE AS IT IS WRITTEN
NOTTY==1B28		;COMMAND FILE IS NOT TTY
LSWICH==1B29		;WE ARE DOING LISTING OF ENTIRE FILE ONLY
ENDCOM==1B30		;NO MORE COMMANDS FROM COMMAND FILE
HAVS04==1B31		;WE ARE READING 'JJJS04' AS INPUT
LSTTY==1B32		;LISTING IS ON CONSOLE
INSRTF==1B33		;INSERT OPENED, NOTHING INSERTED


PPOINT:	IOWD	10,PPTABL
TTYC1:	POINT	6,COMWRD,5

DECIML:	DEC 100000
	DEC 10000
	DEC 1000
	DEC 100
	DEC 10
	DEC 1

;TABLE OF MONTHS

MOTABL:	"JAN"
	"FEB"
	"MAR"
	"APR"
	"MAY"
	"JUN"
	"JUL"
	"AUG"
	"SEP"
	"OCT"
	"NOV"
	"DEC"
;THESE ARE THE VALUES THAT ARE JAMMED INTO IMPURE AREA BETWEEN
;  LOWIMP AND HIIMP

IMPVAL:	ASCII	"		COBOL LIBRARY"
	ASCIZ	"		 XX-XXX-XX   XX:XX

"
	OCT	14		;TOF DATA
	SIXBIT	"DSK"
	XWD	TOFOB,TOFIB
	SIXBIT	"   S01"
	SIXBIT	"TMP"
	OCT	0
	OCT	0

	OCT	14		;TOD DATA
	SIXBIT	"DSK"
	XWD	TODOB,TODIB
	SIXBIT	"   S02"
	SIXBIT	"TMP"
	OCT	0
	OCT	0

	OCT	14		;TID DATA
	SIXBIT	"DSK"
	XWD	TIDOB,TIDIB
	SIXBIT	"   S03"
	SIXBIT	"TMP"
	OCT	0
	OCT	0

	OCT	14		;FOF DATA
	OCT	0
	XWD	FOFOB,0
	OCT	0
	OCT	0
	OCT	0
	OCT	0

	OCT	14		;OIF DATA
	OCT	0
	XWD	0,OIFIB
	OCT	0
	OCT	0
	OCT	0
	OCT	0

	OCT	0		;OFF DATA
	OCT	0
	OCT	0
	OCT	0
	OCT	0

	OCT	0		;LST DATA
	OCT	0
	XWD	LSTBH,0

ENDPUR==.		;END OF PURE SEGMENT--LITERALS WILL START HERE
;IMPURE AREA

	RELOC	0

LOWIMP==.

HEADER:	BLOCK	3
DATE:	BLOCK	3
TIME:	BLOCK	2

TOFDAT:	BLOCK	3
TOFNAM:	BLOCK	4

TODDAT:	BLOCK	3
TODNAM:	BLOCK	4

TIDDAT:	BLOCK	3
TIDNAM:	BLOCK	4

FOFDAT:	BLOCK	3
FOFNAM:	BLOCK	4

OIFDAT:	BLOCK	3
OIFNAM:	BLOCK	4

OFFDEV:	BLOCK	1
OFFNAM:	BLOCK	4

LSTDAT:	BLOCK	3
LSTNAM:	BLOCK	4

HIIMP==LSTNAM-1

ODEVCH:	BLOCK	1	;CHARACTERISTICS OF FIRST OUTPUT DEVICE
TTYIB:	BLOCK	3
LSTBH:	BLOCK	3
TOFOB:	BLOCK	3
TOFIB:	BLOCK	3
TODOB:	BLOCK	3
TODIB:	BLOCK	3
TIDOB:	BLOCK	3
TIDIB:	BLOCK	3
FOFOB:	BLOCK	3
OIFIB:	BLOCK	3
OFFBH:	BLOCK	3
LOCLR==.

TOFBUF:	BLOCK	1
TODBUF:	BLOCK	1
TIDBUF:	BLOCK	1
OIFBUF:	BLOCK	1
OFFBUF:	BLOCK	1

LASTPG:	BLOCK	2	;LATEST LIBRARY NAME TYPED

HICLR==.-1
SAVJFF:	BLOCK	1	;SAVE JOBFF AFTER SETTING UP TTY
INDEV:	BLOCK	1	;INPUT DEVICE
INFIL:	BLOCK	1	;INPUT FILE-NAME
INEXT:	BLOCK	1	;INPUT FILE-EXTENSION
INPP:	BLOCK	1		;INPUT PROJ-PROG NUMBER
TOEXTN:	BLOCK	1	;CURRENT TEMPORARY EXTENSION, IN BINARY

PPTABL:	BLOCK	10	;PUSH-DOWN LIST
COMLIN:	BLOCK	1	;LINE NUMBER TYPED WITH "I", "R" OR "D"
COMWRD:	BLOCK	2
NEWNAM:	BLOCK	2
RUFCTR:	BLOCK	1
RUFLOC:	BLOCK	1
RUFTAB:	BLOCK	200	;ROUGH TABLE

INNAM:	BLOCK	2	;CURRENT INPUT PROGRAM
OUTNAM:	BLOCK	2	;CURRENT OUTPUT PROGRAM
INLIN:	BLOCK	1	;CURRENT INPUT LINE NUMBER
OUTLIN:	BLOCK	1	;CURRENT OUTPUT LINE NUMBER

OFFSET:	BLOCK	1	;AMOUNT NEEDED TO OFFSET CHAINS
TOFCTR:	BLOCK	1	;COUNT OF WORDS WRITTEN INTO TEMP OUTPUT FILE
PRGCTR:	BLOCK	1	;NUMBER OF PROGRAMS IN THE FILE
FINCTR:	BLOCK	1
RENLIN:	BLOCK	1	;OUTPUT LINE NUMBER (IF RENUMBERING)
LINECT:	BLOCK	1	;LINES LEFT ON PRINTER PAGE
COMSAV:	BLOCK	^D9	;SAVE COMMAND STREAM
SAVECP:	BLOCK	1	;SAVE 'CP' DURING '@' COMMAND

	BLOCK	1	;USED WHEN BLT'ING TO FINTAB
FINTAB:	BLOCK	200	;HOLDS FINE TABLE WHEN DOING /L

EXTERNAL JOBFF,JOBSA

	RELOC ENDPUR


	END	START
   -B—