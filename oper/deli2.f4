C
C     ..................................................................
C
C        SUBROUTINE DELI2
C
C        PURPOSE
C           COMPUTES THE GENERALIZED ELLIPTIC INTEGRAL OF SECOND KIND
C
C        USAGE
C           CALL DELI2(R,X,CK,A,B)
C
C        DESCRIPTION OF PARAMETERS
C           R     - RESULT VALUE IN DOUBLE PRECISION
C           X     - UPPER INTEGRATION BOUND (ARGUMENT OF ELLIPTIC
C                   INTEGRAL OF SECOND KIND) IN DOUBLE PRECISION
C           CK    - COMPLEMENTARY MODULUS IN DOUBLE PRECISION
C           A     - DOUBLE PRECISION CONSTANT TERM IN NUMERATOR
C           B     - DOUBLE PRECISION QUATRATIC TERM IN NUMERATOR
C
C        REMARKS
C           DOUBLE PRECISION MODULUS K = DSQRT(1.D0-CK*CK).
C           SPECIAL CASES OF THE GENERALIZED ELLIPTIC INTEGRAL OF
C           SECOND KIND ARE
C           F(DATAN(X),K) OBTAINED WITH A=1.D0, B=1.D0
C           E(DATAN(X),K) OBTAINED WITH A=1.D0, B=CK*CK
C           B(DATAN(X),K) OBTAINED WITH A=1.D0, B=0.D0
C           D(DATAN(X),K) OBTAINED WITH A=0.D0, B=1.D0.
C
C        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
C           NONE
C
C        METHOD
C           DEFINITION
C           R=INTEGRAL((A+B*T*T)/(SQRT((1+T*T)*(1+(CK*T)**2))*(1+T*T)),
C                  SUMMED OVER T FROM 0 TO X).
C           EQUIVALENT IS THE DEFINITION
C           R=INTEGRAL((A+(B-A)*(SIN(T))**2)/SQRT(1-(K*SIN(T))**2),
C                  SUMMED OVER T FROM 0 TO ATAN(X)).
C           EVALUATION
C           LANDENS TRANSFORMATION IS USED FOR CALCULATION.
C           REFERENCE
C           R. BULIRSCH, NUMERICAL CALCULATION OF ELLIPTIC INTEGRALS AND
C                  ELLIPTIC FUNCTIONS
C                  HANDBOOK SERIES OF SPECIAL FUNCTIONS
C                  NUMERISCHE MATHEMATIK VOL. 7, 1965, PP. 78-90.
C
C     ..................................................................
C
      SUBROUTINE DELI2(R,X,CK,A,B)
C
      DOUBLE PRECISION R,X,A,B,AN,AA,ANG,AANG,PIM,PIMA,ARI,AARI
      DOUBLE PRECISION GEO,SGEO,C,D,P,CK
C
C        TEST ARGUMENT
C
      IF(X)2,1,2
    1 R=0.D0
      RETURN
C
C        TEST MODULUS
C
    2 C=0.D0
      D=0.5D0
      IF(CK)7,3,7
    3 R=DSQRT(1.D0+X*X)
      R=(A-B)*DABS(X)/R+B*DLOG(DABS(X)+R)
    4 R=R+C*(A-B)
C
C        TEST SIGN OF ARGUMENT
C
      IF(X)5,6,6
    5 R=-R
    6 RETURN
C
C        INITIALIZATION
C
    7 AN=(B+A)*0.5D0
      AA=A
      R=B
      ANG=DABS(1.D0/X)
      PIM=0.D0
      ISI=0
      ARI=1.D0
      GEO=DABS(CK)
C
C        LANDEN TRANSFORMATION
C
    8 R=AA*GEO+R
      SGEO=ARI*GEO
      AA=AN
      AARI=ARI
C
C        ARITHMETIC MEAN
C
      ARI=GEO+ARI
C
C        SUM OF SINE VALUES
C
      AN=(R/ARI+AA)*0.5D0
      AANG=DABS(ANG)
      ANG=-SGEO/ANG+ANG
      PIMA=PIM
      IF(ANG)10,9,11
C
C        REPLACE 0 BY SMALL VALUE
C
    9 ANG=-1.D-17*AANG
   10 PIM=PIM+3.1415926535897932
      ISI=ISI+1
   11 AANG=ARI*ARI+ANG*ANG
      P=D/DSQRT(AANG)
      IF(ISI-4)13,12,12
   12 ISI=ISI-4
   13 IF(ISI-2)15,14,14
   14 P=-P
   15 C=C+P
      D=D*(AARI-GEO)*0.5D0/ARI
      IF(DABS(AARI-GEO)-1.D-9*AARI)17,17,16
   16 SGEO=DSQRT(SGEO)
C
C        GEOMETRIC MEAN
C
      GEO=SGEO+SGEO
      PIM=PIM+PIMA
      ISI=ISI+ISI
      GOTO 8
C
C        ACCURACY WAS SUFFICIENT
C
   17 R=(DATAN(ARI/ANG)+PIM)*AN/ARI
      C=C+D*ANG/AANG
      GOTO 4
      END
