1 L=0
2 W=0
3 R1=0
4 P1=0
5 Q1=0
6 M3=0
7 M4=0
8 P2=0
9 T1=0
10 T2=0
110    REMARKABLE PROGRAM BY L. CRAM , L. GOODIE , AND D. HIBBARD 
120    PRINT "DO YOU WANT DESCRIPTIONS (0=YES, 1=NO)";
130    INPUT Z
140    FOR U=1 TO 6 
150   PRINT
160   NEXT U 
170   IF Z=1 THEN 400
180   PRINT "THIS IS A CIVIL WAR SIMULATION."
190   PRINT "TO PLAY, TYPE A RESPONSE WHEN THE COMPUTER ASKS." 
200   PRINT "REMEMBER THAT ALL FACTORS ARE INTERRELATED AND THAT YOUR" 
210   PRINT "RESPONSES COULD CHANGE HISTORY. FACTS AND FIGURES USED ARE" 
220   PRINT "BASED ON THE ACTUAL OCCURENCE. MOST BATTLES TEND TO RESULT" 
230   PRINT "AS THEY DID IN THE CIVIL WAR, BUT IT ALL DEPENDS ON YOU!!"
240   PRINT
250   PRINT "THE OBJECT OF THE GAME IS TO WIN AS MANY BATTLES AS POSSIBLE" 
260   PRINT
270   PRINT "YOUR CHOICES FOR DEFENSIVE STRATEGY ARE:" 
280   PRINT "       (1) ARTILLERY ATTACK"
290   PRINT "       (2) FORTIFICATION AGAINST FRONTAL ATTACK"
300   PRINT "       (3) FORTIFICATION AGAINST FLANKING MANUEVERS"
310   PRINT "       (4) FALLING BACK"
320   PRINT "YOUR CHOICES FOR OFFENSIVE STRATEGY ARE:" 
330   PRINT "       (1) ARTILLERY ATTACK"
340   PRINT "       (2) FRONTAL ATTACK"
350   PRINT "       (3) FLANKING MANUEVERS"
360   PRINT "       (4) ENCIRCLEMENT"
370   PRINT "YOU MAY SURRENDER BY TYPING A '5' FOR YOUR STRATEGY." 
380   PRINT
390   PRINT "YOU ARE THE CONFEDERACY.       GOOD LUCK!"
400  READ M1,M2,C1,C2,M,A,U 
410  LET I1=10+(L-W)*2
420  LET I2=10+(W-L)*2
430  LET D1=100*INT((M1*(100-I1)/2000)*(1+(R1-Q1)/(R1+1))+0.5)
440  LET D2=100*INT(M2*(100-I2)/2000+0.5) 
450  LET F1=5*M1/6
460  LET A1=Z 
470  FOR U=1 TO 4 
480  PRINT
490  NEXT U 
500  PRINT "THIS IS THE BATTLE OF ";
510  GOSUB 1510
520  PRINT " ","CONFEDERACY","  UNION"
530  PRINT "MEN"," ";INT(M1*(1+(P1-T1)/(M3+1)))," ";
540  PRINT INT(M2*(1+(P2-T2)/(M4+1))) 
550  PRINT "MONEY","$";D1,"$";D2
560  PRINT "INFLATION"," ";I1+15;"%"," ";I2;"%" 
570  PRINT
580  PRINT "HOW MUCH DO YOU WISH TO SPEND FOR FOOD";
590  INPUT F
600  IF F<0 THEN 1460
610  PRINT "HOW MUCH DO YOU WISH TO SPEND FOR SALARIES";
620  INPUT S
630  IF S<0 THEN 1460
640  PRINT "HOW MUCH DO YOU WISH TO SPEND FOR AMMUNITION";
650  INPUT B
660  IF B<0 THEN 1460
670  PRINT
680  IF F+S+B<=D1 THEN 710
690 PRINT "THINK AGAIN!  YOU HAVE ONLY $";D1
700  GOTO 570 
710  LET O=((2*F^2+S^2)/F1^2+1) 
720  IF O<10 THEN 750 
730  PRINT "MORALE IS HIGH" 
740  GOTO 790 
750  IF O<5 THEN 780
760  PRINT "MORALE IS FAIR" 
770  GOTO 790 
780  PRINT "MORALE IS POOR" 
790  IF M<>3 THEN 820 
800  PRINT "YOU ARE ON THE OFFENSIVE" 
810  GOTO 860 
820  IF M<>1 THEN 850 
830  PRINT "YOU ARE ON THE DEFENSIVE" 
840  GOTO 860 
850  PRINT "BOTH SIDES ARE ON THE OFFENSIVE"
860  PRINT
870  PRINT "YOUR STEGY"; 
880  INPUT Y
890  IF Y=5 THEN 2360 
900  IF ABS(Y-3)<3 THEN 930 
910  PRINT "YOU JERK!  USE THE OTHER SET OF STRATEGIES!!" 
920  GOTO 860 
930  PRINT
940  PRINT " ","CONFEDERACY","UNION"
950  LET C5=(2*C1/5)*(1+1/(2*(ABS(INT(4*RND(1)+1)-Y)+1))) 
960  LET C5=INT(C5*(1+1/O)*(1.28+F1/(B+1))+0.5) 
970  IF C5+100/O<M1*(1+(P1-T1)/(M3+1)) THEN 1020 
980  LET C5=INT(13*M1/20*(1+(P1-T1)/(M3+1)))
990  LET E=7*C5/13
1000  LET U=1
1010  GOTO 1030 
1020  LET E=100/O
1030  PRINT "CASUALTIES",C5,INT(17*C2*C1/(C5*20)+0.5)
1040  PRINT "DESERTIONS",INT(E),INT(5*O) 
1050  PRINT
1060  IF C5-C1>=0 THEN 1090 
1070 PRINT "YOUR CASUALTIES WERE ";INT(100*(C1-C5)/C1+0.5);" % MORE THAN"
1080  GOTO 1100 
1090 PRINT "YOUR CASUALTIES WERE ";INT(100*(C5-C1)/C1+0.5);" % MORE THAN"
1100  PRINT "THE ACTUAL CASUALITIES AT ";
1110  LET A1=1 
1120  GO SUB1510
1130  IF U=1 THEN 1150
1140  IF C5+E<17*C2*C1/(C5*20)+5*O THEN 1180
1150  PRINT "YOU LOSE "; 
1160  LET L=L+1
1170  GOTO 1200 
1180  PRINT "YOU WIN ";
1190  LET W=W+1
1200  GOSUB 1510
1210  IF W=8 THEN 2380 
1220  LET T1=T1+C5+E 
1230  LET T2=T2+17*C2*C1/(C5*20)+5*O 
1240  LET P1=P1+C1 
1250  LET P2=P2+C2 
1260  LET Q1=Q1+(F+S+B)
1270  LET R1=R1+M1*(100-I1)/20 
1280  LETM3=M3+M1
1290  LET M4=M4+M2 
1300  IF A=14 THEN 2390
1310  GOTO 400 
1320  DATA 18000,18500,1967,2708,1,1,0 
1330  DATA 40000,44894,10699,13047,3,2,0 
1340  DATA 95000,115000,20614,15849,3,3,0
1350  DATA 54000,63000,10000,14000,2,4,0 
1360  DATA 40000,50000,10000,12000,3,5,0 
1370  DATA 75000,120000,5377,12653,1,6,0 
1380  DATA 38000,45000,11000,12000,1,7,0 
1390  DATA 32000,90000,13000,17197,2,8,0 
1400  DATA 50000,70000,12000,19000,1,9,0 
1410  DATA 72500,85000,20000,23000,3,10,0
1420  DATA 66000,60000,18000,16000,2,11,0
1430  DATA 37000,60000,6700,5800,2,12,0
1440  DATA 62000,110000,17723,18000,2,13,0 
1450  DATA 65000,100000,8500,3700,1,14,0 
1460  PRINT "GO TO JAIL."
1470  PRINT "GO DIRECTLY TO JAIL." 
1480  PRINT "DO NOT PASS GO."
1490  PRINT "DO NOT COLLECT $200"
1500  GOTO 470 
1510  IF A<>1 THEN 1580 
1520  PRINT "BULL RUN" 
1530  IF A1=1 THEN 2340
1540 PRINT"JULY 21,1861    GEN. BEAUREGARD COMMANDING THE SOUTH MET THE" 
1550 PRINT"UNION FORCES WITH GEN MCDOWELL IN A PREMATURE BATTLE AT BULL" 
1560 PRINT"RUN.  GEN. JACKSON HELPED PUSH BACK THE UNION ATTACK."
1570  GO TO 2340 
1580  IF A<>2 THEN 1640 
1590  PRINT "SHILOH" 
1600  IF A1=1 THEN 2340
1610 PRINT"APRIL 6-7,1862    THE CONFEDERATE SURPRISE ATTACK AT SHILOH"
1620 PRINT"FAILED DUE TO POOR ORGANIZATION." 
1630  GO TO 2340 
1640  IF A<>3 THEN 1710 
1650  PRINT "SEVEN DAYS" 
1660  IF A1=1 THEN 2340
1670 PRINT"JUNE 25-JULY 1,1862    GENERAL LEE (CSA) UPHELD THE OFFENSIVE"
1680 PRINT"THROUGHOUT THE BATTLE AND FORCED GEN. MCCLELLAN AND THE UNION"
1690 PRINT"FORCES AWAY FROM RICHMOND." 
1700  GO TO 2340 
1710  IF A<>4 THEN 1770
1720  PRINT "THE SECOND BULL RUN"
1730  IF A1=1 THEN 2340
1740 PRINT"AUG 29-30,1862    THE COMBINED CONFEDERATE FORCES UNDER LEE AND"
1750 PRINT"JACKSON DROVE THE UNION FORCES BACK INTO WASHINGTON." 
1760  GO TO 2340 
1770 IF A<>5 THEN 1830
1780 PRINT "ANTIETAM" 
1790 IF A1=1 THEN 2340
1800PRINT"SEPT 17,1862    THE SOUTH FAILED TO INCORPORATE MARYLAND INTO"
1810PRINT"THE CONFEDERACY." 
1820 GO TO 2340 
1830 IF A<>6 THEN 1890
1840 PRINT "FREDERICKSBURG" 
1850 IF A1=1 THEN 2340
1860PRINT"DEC 13,1862 THE CONFEDERACY UNDER LEE SUCESSFULLY REPULSED" 
1870PRINT"AN ATTACK BY THE UNION UNDER GEN. BURNSIDE."
1880 GOTO 2340
1890 IF A <>7 THEN 1940 
1900 PRINT "MURFREESBORO" 
1910 IF A1=1 THEN 2340
1920PRINT"DEC 31,1862    THE SOUTH UNDER GEN. BRAGG WON A CLOSE BATTLE" 
1930 GOTO 2340
1940 IF A<>8 THEN 2000
1950 PRINT "CHANCELLORSVILLE" 
1960 IF A1=1 THEN 2340
1970PRINT"MAY 1-6,1863    THE SOUTH HAD A COSTLY VICTORY AND LOST ONE"
1980PRINT"OF THEIR OUTSTANDING GENERALS, 'STONEWALL' JACKSON."
1990 GOTO 2340
2000 IF A<>9 THEN 2060
2010 PRINT "VICKSBURG"
2020 IF A1=1 THEN 2340
2030PRINT"JULY 4,1863    VICKSBURG WAS A COSTLY DEFEAT FOR THE SOUTH" 
2040PRINT"BECAUSE IT GAVE THE UNION ACCESS TO THE MISSISSIPPI." 
2050 GOTO 2340
2060 IF A<>10 THEN 2120 
2070 PRINT "GETTYSBURG" 
2080 IF A1=1 THEN 2340
2090PRINT"JUNE 30,1863    A SOUTHERN MISTAKE BY GEN. LEE AT GETTYSBURG" 
2100PRINT"COST THEM ONE OF THE MOST CRUCIAL BATTLES OF THR WAR."
2110 GOTO 2340
2120 IF A<>11 THEN 2180 
2130 PRINT "CHICKAMAUGA"
2140 IF A1=1 THEN 2340
2150PRINT"NOV 25,1863    AFTER THE SOUTH HAD SIEGED GEN. ROSENCRANS'" 
2160PRINT"ARMY FOR THREE MONTHS, GEN. GRANT BROKE THE SIEGE." 
2170 GOTO 2340
2180 IF A<>12 THEN 2240 
2190 PRINT "CHATTANOOGA"
2200 IF A1=1 THEN 2340
2210PRINT"SEPT 15,1863    CONFUSION IN A FOREST NEAR CHICKAMAUGA LED" 
2220PRINT"TO A COSTLY SOUTHERN VICTORY."
2230 GOTO 2340
2240 IF A<>13 THEN 2300 
2250 PRINT "SPOTSYLVANIA" 
2260 IF A1=1 THEN 2340
2270PRINT"MAY 5,1864    GRANT'S PLAN TO KEEP LEE ISOLATED BEGAN TO FAIL"
2280PRINT"HERE, AND CONTINUED AT COLD HARBOR AND PETERSBURG." 
2290 GOTO 2340
2300 PRINT "ATLANTA"
2310 IF A1=1 THEN 2340
2320PRINT"AUGUST, 1864   SHERMAN AND THREE VETERAN ARMIES CONVERGED ON" 
2330PRINT"ATLANTA AND DEALT THE DEATH BLOW TO THE CONFEDERACY." 
2340 PRINT
2350 RETURN 
2360 PRINT "THE CONFEDERACY HAS SURRENDERED"
2370 GOTO 2390
2380 PRINT "THE UNION HAS SURRENDERED"
2390 PRINT
2400 PRINT "YOU HAVE WON ";W;" BATTLES AND LOST ";L;" BATTLES."
2410 IF Y=5 THEN 2450 
2420 IF W<=L THEN 2450
2430 PRINT "THE CONFEDERACY HAS WON THE WAR"
2440 STOP 
2450 PRINT "THE UNION HAS WON THE WAR"
2460 END
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                           