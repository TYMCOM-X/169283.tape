MODULE KRENM(DOLLAR,ENTRIES=($KRENM),FSAVE,TIMING,TIMER=EXTERNAL(SIX12))=
BEGIN

REQUIRE KDEFS.BLI[7,107355];
REQUIRE CDEFS.BLI[7,107355];

UNDECLARE $KRENM;

GLOBAL ROUTINE $KRENM(FCB)=
  BEGIN

  MAP KFCB$ FCB;

  BIND RENAMUUO=#055, RELEASEUUO=#071;
  MACHOP XCT=#256;
  MACRO MAKEOP(OP,REG,ADDR)=(OP^27+REG^23+(ADDR)<0,0>)&;

  LOCAL INST;

  IF .FCB[K$TERM] OR .FCB[K$SIMCH]
    THEN (.FCB[K$EXCEPT])(.FCB,KV$ILIOP)
    ELSE BEGIN
      FCB[K$EXISTS] := SIXBIT 'RENAME';
      IF .FCB[K$CLOSER] NEQ 0
        THEN (.FCB[K$CLOSER])(.FCB);
      INST := MAKEOP(RENAMUUO,.FCB[K$CHNL],.FCB[K$FIDPTR]);
      IFSKIP XCT(0,INST)
        THEN BEGIN
          INST := MAKEOP(RELEASEUUO,.FCB[K$CHNL],0);
          XCT(0,INST);
          % REMOVE FROM FCB CHAIN IF THERE %
           IF NOT (.FCB[K$LCHAIN] EQL 0 ) THEN
               IF NOT (.FCB[K$RCHAIN] EQL 0 ) THEN
                BEGIN
                FCB[K$PRCHAIN] _ .FCB[K$RCHAIN];
                FCB[K$NLCHAIN] _ .FCB[K$LCHAIN]
                END;
          $CFREE(.FCB[K$FIDPTR]);
          $CFREE(.FCB);
          1  END
        ELSE BEGIN
          FCB[K$EXISTS] := KV$RENFL;
          0  END
      END
  END;

END ELUDOM
