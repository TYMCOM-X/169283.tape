TITLE	DATE	V.32(433)	TODAY'S DATE	FORTRAN IV
SUBTTL	1-JUNE-72	/KK/DMN


;THIS SUBROUTINE PUTS TODAY'S DATE INTO A
;DIMENSIONED TWO-WORD ARRAY.

;THE DATE WILL BE IN THE FORM:
;	17-Aug-66

;THE ROUTINE IS CALLED IN THE FOLLOWING MANNER:
;	JSA	16,DATE
;	ARG	A

;"DATE" OBTAINS THE DATE FROM THE MONITOR IN THE FORM:
;	( (YEAR-1964)*12 + (MONTH-1) )*31 + (DAY-1)

	ENTRY	DATE

	SIXBIT/DATE/
DATE:	0			;ENTRY TO DATE ROUTINE.
	MOVEI	1,@(16)		;GET ADDRESS OF 2 WORD ARRAY
	MOVEM	2,0(1)		;SAVE THE CONTENTS OF AC 2.
	MOVEM	3,1(1)		;SAVE THE CONTENTS OF AC 3.
	CALLI	1,14		;GET THE DATE FROM THE MONITOR.
	IDIVI	1,^D31		;DIV. BY 31 TO OBTAIN THE DAY-1.
	ADDI	2,1		;TO OBTAIN THE DAY.
	IDIVI	2,^D10		;CONVERT INTO TWO DEC. DIGITS.
	SKIPN	2		;IS THE DAY < 10?
	MOVNI	2,20		;YES, OUTPUT BLANK.
	MOVEI	0,"0"(2)	;GET FIRST DIGIT
	LSH	0,7		;MAKE SPACE
	ADDI	0,"0"(3)	;ADD IN 2ND DIGIT
	IDIVI	1,^D12		;TO OBTAIN THE MONTH
	EXCH	1,2		;SAVE YEAR IN 2
	MOVE	1,TABLE(1)	;GET MONTH IN 1
	LSHC	0,3*7		;LEFT JUSTIFY 0 & 1
	LSH	0,1		;0 = ASCII /DD-MO/
				;1 = ASCII /N-/
	MOVEI	2,^D64(2)	;GET THE YEAR
	IDIVI	2,^D10		;CONVERT INTO TWO DEC. DIGITS
	ADDI	2,"0"		;MAKE ASCII
	ADDI	3,"0"
	LSH	2,2*7+1		;SHIFT TO CHAR 3
	LSH	3,7+1		;SHIFT TO CHAR 4
	ADD	3,2		;ADD IN TO 3
	ADD	3,1		;SO LOW WORD IS IN 3
	MOVE	2,0		;PUT HIGH ORDER RESULT IN 2
	MOVEI	1,@(16)		;USER ADDRESS
	EXCH	2,0(1)		;RESTORE 2
	EXCH	3,1(1)		;AND 3 WHILE STORING RESULT
	JRA	16,1(16)	;RETURN.

;SUB1 CONVERTS THE DAY AND THE YEAR INTO ASCII CHARS, AND
;DEPOSITS THEM IN THE TWO WORD ARRAY.

SUB1:	ADDI	2,60		;CONVERT FIRST DIGIT TO ASCII
	IDPB	2,0		;DEPOSIT FIRST DIGIT
	ADDI	3,60		;CONVERT SECOND DIGIT TO ASCII
TABLE:	ASCII	/-Jan-/
	ASCII	/-Feb-/
	ASCII	/-Mar-/
	ASCII	/-Apr-/
	ASCII	/-May-/
	ASCII	/-Jun-/
	ASCII	/-Jul-/
	ASCII	/-Aug-/
	ASCII	/-Sep-/
	ASCII	/-Oct-/
	ASCII	/-Nov-/
	ASCII	/-Dec-/

	PRGEND
