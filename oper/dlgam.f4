C
C     ..................................................................
C
C        SUBROUTINE DLGAM
C
C        PURPOSE
C           COMPUTES THE DOUBLE PRECISION NATURAL LOGARITHM OF THE
C           GAMMA FUNCTION OF A GIVEN DOUBLE PRECISION ARGUMENT.
C
C        USAGE
C           CALL DLGAM(XX,DLNG,IER)
C
C        DESCRIPTION OF PARAMETERS
C           XX   - THE DOUBLE PRECISION ARGUMENT FOR THE LOG GAMMA
C                  FUNCTION.
C           DLNG - THE RESULTANT DOUBLE PRECISION LOG GAMMA FUNCTION
C                  VALUE.
C           IER  - RESULTANT ERROR CODE WHERE
C                  IER= 0----NO ERROR.
C                  IER=-1----XX IS WITHIN 10**(-9) OF BEING ZERO OR XX
C                            IS NEGATIVE.  DLNG IS SET TO -1.OD75.
C                  IER=+1----XX IS GREATER THAN 10**70. DLNG IS SET TO
C                            +1.OD75.
C
C        REMARKS
C           NONE
C
C        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
C           NONE
C
C        METHOD
C           THE EULER-MCLAURIN EXPANSION TO THE SEVENTH DERIVATIVE TERM
C           IS USED, AS GIVEN BY M. ABRAMOWITZ AND I.A. STEGUN,
C           'HANDBOOK OF MATHEMATICAL FUNCTIONS', U. S. DEPARTMENT OF
C           COMMERCE, NATIONAL BUREAU OF STANDARDS APPLIED MATHEMATICS
C           SERIES, 1966, EQUATION 6.1.41.
C
C     ..................................................................
C
      SUBROUTINE DLGAM(XX,DLNG,IER)
      DOUBLE PRECISION XX,ZZ,TERM,RZ2,DLNG
      IER=0
      ZZ=XX
      IF(XX-1.D10) 2,2,1
    1 IF(XX-1.D38) 8,9,9
C
C        SEE IF XX IS NEAR ZERO OR NEGATIVE
C
    2 IF(XX-1.D-9) 3,3,4
    3 IER=-1
      DLNG=-1.D38
      GO TO 10
C
C        XX GREATER THAN ZERO AND LESS THAN OR EQUAL TO 1.D+10
C
    4 TERM=1.D0
    5 IF(ZZ-18.D0) 6,6,7
    6 TERM=TERM*ZZ
      ZZ=ZZ+1.D0
      GO TO 5
    7 RZ2=1.D0/ZZ**2
      DLNG =(ZZ-0.5D0)*DLOG(ZZ)-ZZ +0.9189385332046727 -DLOG(TERM)+
     1(1.D0/ZZ)*(.8333333333333333D-1 -(RZ2*(.2777777777777777D-2 +(RZ2*
     2(.7936507936507936D-3 -(RZ2*(.5952380952380952D-3)))))))
      GO TO 10
C
C        XX GREATER THAN 1.D+10 AND LESS THAN 1.D+70
C
    8 DLNG=ZZ*(DLOG(ZZ)-1.D0)
      GO TO 10
C
C        XX GREATER THAN OR EQUAL TO 1.D+70
C
    9 IER=+1
      DLNG=1.D38
   10 RETURN
      END
