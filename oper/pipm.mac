	TITLE BATPIP  BY RON COLCERNIAN
	ENTRY BATPIP

	;WRITTEN TO RENAME A FILE "PIP.DAT" TO 000PIP.TMP AND
;TO START SYS:PIP TO EXECUTE THE FILE

BATPIP:	0
	SETZM	NEWNAM+2	;ZERO OUT NAME BLOCK
	SETZM	NEWNAM+3	;"    "    "   "
	SETZM	OLDNAM+2	;"    "    "   "
	SETZM	OLDNAM+3	;"    "    "   "
	CALLI	1,30		;GET JOB NUMBER
	IDIVI	1,^D10		;DIVIDE BY TEN
	MOVE	4,PNTR2		;GET BYTE POINTER
	MOVE	6,NEWNAM	;GET NAME TEMPLATE
	MOVEI	5,20		;GET A SIXBIT ZERO
	IDPB	5,4		;PUT A LEADING ZERO IN JOB NUM
	ADDI	1,20		;SIXBIT-TISE TENS
	IDPB	1,4		;DEPOSIT IN NAME
	ADDI	2,20		;SIXBIT-TISE UNITS
	IDPB	2,4		;DEPOSIT IN NAME
	MOVEM	6,NEWNAM	;PUT PIP NAME IN TABLE
	INIT	0,17		;USE CHANNEL 0 IN DUMP MODE
	SIXBIT/DSK/		;DISK ONLY
	0			;NO BUFFERS
	HALT			;NO DISK AVAILABLE
X:      LOOKUP  0,NEWNAM        ;IS 000PIP.TMP THERE?
        JRST    NOTHR           ;NO.
        RENAME  0,PIP+2         ;YES, DELETE IT
        JRST    .+1             ;TAKE A CHANCE
NOTHR:  LOOKUP	0,OLDNAM	;FIND PIP.DAT
	JRA	16,0(16)	;RETURN AND MAKE FILE
	RENAME	0,NEWNAM	;RENAME PIP FILE
	JRST	RENERR		;ERROR
	CLOSE	0,		;CLOSE CHANNEL 0
	MOVSI	1,1		;ADR +1
	HRRI	1,PIP		;RUN UUO TABLE
	CALLI	1,35		;UP-AWAY WITH PIP
	TTCALL	3,[ASCIZ/
ERROR IN BATCH - LINKAGE TO PIP ERROR
/]
	JRST	DONE		;DONE
RENERR:	TTCALL 3,[ASCIZ/
ERROR IN BATCH - RENAMEING FILE PIP.DAT
/]
	JRST	DONE		;DONE
PIP:	SIXBIT/SYS/
	SIXBIT/PIP/
	0
	0
	0
	0
OLDNAM:	SIXBIT/PIP/
	SIXBIT/DAT/
	0
	0
NEWNAM:	SIXBIT/000PIP/
	SIXBIT/TMP/
	0
	0
PNTR2:	POINT	6,6
DONE:	END
