TITLE CREAD   - READ DAILY BILLING FILES FROM  USER NAME SYS TEST.DAT THRU TEST.D77,RENAME, AND WRITE FILE
;MODIFIED DEC 15,1971,N. LATHAM FOR CUPERTINO WW MONITOR
;STORAGE IS COLLECTED FROM UFD'S IN DSKSTR ROUTINE

;FIX TO WRITE TTY #'S AS BINARY FOR LEVEL D 3/1/71 JNM
;ADDITION TO HANDLE NEW OFF RECORD DATA: N.LATHAM,6/71
F=0	;FOR FLAGS
A=1	;A - D ARE GENERAL REGISTERS
B=2
C=3
D=4
AC=5
POPPTR=5
DATPTR=6
F1=6		;1 IN RH, THIS IS AN ON RECORD,-1 IN LF IF OFF RECORD
EXTPTR=7
BP=10	;BYTE POINTER
E=10	;TEMP STORAGE DURING SORT ROUTINE
DAT=11
WD=11
WD1=12
T=12
EXT=12
Z=12
N=13	;FOR NUMBERS
N1=N+1
TRYCTR=14	;COUNT FOR TIMES TO TRY ACCESS OF BILLING FILE
R=15	;RADIX
CH=16	;CHARACTERS
P=17	;PUSHDOWN STACK

;MISC.
EOF=1B22
;CHANNELS
UFD=1
MFD=2
OUTPT=3
;DEVICE CHANNELS

INP=2
OUT=3

CONFIG==0
CNFTBL==11
LICTAB==-20
OPRLIC==100000	;OPERATOR LICENSE PRIVILEGE BIT
CBEGIN==100
IODEND==20000
ENDF==1
ONBIT==1	;FLAG BIT FOR ON RECORD
OFBIT==4	;FLAG BIT FOR OFF RECORD
HKBIT==2	;FLAG BIT FOR CHECKPOINT RECORD
DATEBIT==10
EOFBIT==20
LEGALTYP==7	;MASK FOR FLAG BITS OF RECORD TYPES  CURRENTLY PROCESSED IN THIS PROGRAM
JOBAUN==-23	;GETTAB FOR AUN THIS JOB
ENTSIZ==^D30	;MAXIMUM NO. WORDS IN ONE TEST.DAT RECORD

;CALLI DEFINITIONS

RESET=0
EXIT=12
SLEEP=31
;LOGOUT RECORD SIZE IN WORDS. DO NOT MODIFY WITHOUT
;LOOKING AT CODE AT NOACCT:  ALSO GBIL2.F4 MUST BE CHANGED TO HANDLE 3RD RECORD
QLENGT=^D14

INTERN JOBVER
	JOBVER=137
	LOC	JOBVER
	XWD	0,24	;VERSION NO.

RELOC



	PAGE
	EXTERNAL FORSE., GBIL2

ENTRY CREAD
CREAD:	SETZB	F,F1		;ENTER HERE FOR CREAD,.+2 FOR XREAD
	TROA	F,CBEGIN	;SET FLAG FOR CREAD START
	SETZB	F,F1
	RESET.	00,0	;FORTRAN RESET
	CALLI	RESET		;RESETS EVERYTHING
	HRROI	A,LICTAB
	GETTAB	A,
	MOVE	A,0
	TLNN	A,OPRLIC
	JRST [TTCALL	3,[ASCIZ/YOU MUST SHOW YOUR OPERATOR'S LICENSE.
/]
	CALLI	12]
	HRROI	A,JOBAUN	;GETTAB, THIS JOB AUN
	GETTAB	A,
	JFCL
	CAME	A,[XWD 1,44355]	;MUST BE UNDER BILLING10
	CAMN	A,[XWD 6,10064]	; OR NATALIE
	JRST	DATEIN		;OKAY
	TTCALL	3,[ASCIZ/
YOU MUST LOG IN UNDER BILLING10
/]
	CALLI 12
DATEIN:	MOVE	P,PDP		;INITIALIZE PUSHDOWN LIST
	SETZ	Z,
	MOVEI	AC,MSG1
	CALL	AC,[SIXBIT/DDTOUT/]
	TTCALL	11,0		;CLEAR THE TTY BUFFER
	MOVE	AC,[XWD	-6,0]
	MOVE	A,[POINT 6,T]
LOOOP:	MOVE	BP,[XWD 070700,MSG2(AC)]
	TTCALL	4,WD
	IDPB	WD,BP
	SUBI	WD,40
	IDPB	WD,A
	AOBJN	AC,LOOOP
	CAMGE	T,[SIXBIT/010171/]
	JRST DATEIN
	CAMLE	T,[SIXBIT/123199/]
	JRST DATEIN
GETSIT:	TRNN	F,CBEGIN
	JRST XSITE	;XREAD
	HRLZI	C,-5	;NO. WORDS IN CONGIF FOR SYSTEM NAME
	MOVE	A,[XWD CONFIG, CNFTBL]
	GETTAB	A,
NOSITE:	JRST	[TTCALL	3,[ASCIZ/
CANNOT FIND SYSTEM NAME. NOTIFY S.Q.A. RUN ABORTED./]
	CALLI EXIT]
	MOVE	BP,[POINT 7, A]
	MOVEI	D,5	;WDAR PER WORD
GETONE:	ILDB	WD,BP
	CAIN	WD,63
	JRST NXNUM
	SOJG	D,GETONE
	JRST	NOSITE
NXNUM:	SOJLE	D,NOSITE
	ILDB	WD,BP
	CAIL WD,61
	CAIL	WD,66
	JRST NXNUM
	JRST STORAGE

XSITE:	TTCALL	11,0
	MOVEI	A,DIREC2	;ASK FOR SITE
	CALL	A,[SIXBIT/DDTOUT/]
	TTCALL	0,WD
	CAIE	WD,63
	JRST XSITE
	TTCALL	0,WD
	CAIL	WD,61	;ASC 1
	CAIL	WD,66
	JRST XSITE


STORAG:	MOVEM	WD,MSG2(AC)	;SAVE THE SIATE
	TRNE	F,CBEGIN	;CREAD? OR XREAD
	PUSHJ	P,DSKSTR	;CREAD. DO DISC STORAGE
	MOVSI	A,(SIXBIT/SYS/)	;FOR CREAD
	TRNN	F,CBEGIN
	MOVSI	A,(SIXBIT/DSK/)	;FOR XREAD
	MOVEM	A,OPENBK+1
	OPEN	INP,OPENBK	;SYS IF CREAD,DSK IF XREAD FOR TEST.DAT
	JRST	ERRDSK		;IF NOT AVAILABLE, EXIT
	INBUF	INP,1		;SET UP 1 INPUT BUFFER ON THIS CHANNEL
	MOVE DATPTR,[IOWD 100,DATLST]
	MOVE EXTPTR,[IOWD 100,EXTLST]
	MOVEI B,(SIXBIT /DAT/)		;SAVE IN RIGHT 18 BITS

;******N. LATHAM, MOD. TO MERGE MULTIPLE TEST FILES BY TIME,6/70
NEXT:	MOVE A,[SIXBIT /TEST/]
	MOVEM B,EXTENS
	MOVSS B			;SET UP IN LEFT 18 BITS FOR LOOKUP
	SETZB C,D
	LOOKUP INP,A
	JUMPA .+4		;FILE NOT FOUND. TRY NEXT
	AND C,[XWD 37,-1]	;SAVE TIME AND DATE ONLY
	PUSH DATPTR,C
	PUSH EXTPTR,EXTENS	;SAVE RIGHT JUSTIFIED EXTENSION
	MOVE B,EXTENS	;RESTORE TO RIGHT HALF
	CAIN B,(SIXBIT /DAT/)		;WAS IT DAT?
	MOVEI B, (SIXBIT /D00/)	;YES
	CAIN B,(SIXBIT/D77/)
	JRST SORT
	ADDI B,1
	TRNN B,7
	ADDI B,100-10
	JRST NEXT

SORT:	MOVEM EXTPTR,POPPTR
NEXTA:	HRRZ DAT,DATPTR
	HRRZ EXT,EXTPTR
	POP DATPTR,A		;TAKE OFF TOP, THEN DECREMENT ADDRESS
	POP EXTPTR,C
	HLRO E,DATPTR
	ADDI E,77
	SKIPGE E	;HAS LEFT HALF GONE TO -1 MEANING LIST DONE?
	JRST DONE		;YES. SO EXIT SORT
NEXTE:	SUBI DAT,1
	SUBI EXT,1
	MOVE B,@DAT
	MOVE D,@EXT
	MOVEM A,15
	ANDI 15,7777	;SAVE BITS CONTAINING DATE ONLY
	MOVEM B,16
	ANDI 16,7777
	CAMLE 15,16
	JRST EXCH
	CAME 15,16
	JRST NOEXCH
	CAMG A,B	;COMPARE TIMES
	JRST NOEXCH

EXCH:	EXCH C,D
	EXCH A,B

NOEXCH:	MOVEM B,@DAT
	MOVEM D,@EXT
	SUB E,[1]		;DECREMENT INDEX
	SKIPL E
	JRST NEXTE
	HRRZ DAT,DATPTR
	ADDI DAT,1
	MOVEM A,@DAT
	HRRZ EXT,EXTPTR
	ADDI EXT,1
	MOVEM C,@EXT
	JRST NEXTA
;*****END MERGE MOD.

DONE:	MOVE EXTPTR,POPPTR	;RESET PTR FOR POPPING
	INIT	OUT,0		;INITIALIZE OUTPUT DEVICE,ASCII MODE
	SIXBIT	/DSK/	
	XWD	OBUF,0		;OUTPUT ONLY
	JRST	DERROR		;IF NOT AVAILABLE,EXIT
	OUTBUF	OUT,1		;SET UP 1 OUTPUT BUFFER ON THIS CHANNEL
	MOVE	A,[SIXBIT/DAILY/]	;FILENAME FOR DAILY BILLING
	MOVSI	B,(SIXBIT/DAT/)	;EXTENSION
	SETZB	C,D
	ENTER	OUT,A
	JRST	DERROR

;***MORE OF MERGE MOD
	HLRZ 15,EXTPTR
	CAIN 15,-100
	JRST NOFILE
INEXT:	HLRZ 15,EXTPTR
	CAIN 15,-100	;HAS LAST FILE BEEN RENAMED?
	JRST LAST		;YES. ALSO PROCESSED, SO EXIT
	POP EXTPTR,B		;EXTENSION STORED IN RIGHT HALF OF @EXTPTR
	HRLZM B, EXTENS		;SAVE EXTENSION IN LEFT HALF OF EXTENS
;*****END OF MERGE MOD
	MOVE TRYCTR,^D20	;NO. OF TIMES TO TRY IF BUSY
TRYGIN:	MOVE A,[SIXBIT/TEST/]
	HLLZ B,EXTENS
	SETZB C,D
	LOOKUP	INP,A
	JRST	RERROR
	MOVE A,[SIXBIT/TEST/]
	MOVE	N1,[XWD 140600,A]
	MOVE	AC,[XWD -2,2]
SETUP:	MOVE	BP,[XWD 070700,MSG2(AC)]	;GET DAY OF MONTH
	ILDB	CH,BP
	SUBI	CH,40
	IDPB	CH,N1
	AOBJN	AC,SETUP
	MOVEM	A,FILNAM	;SAVE THIS FILNAME
	HLLZ B,EXTENS
	SETZB C,D
	RENAME INP,A
	SKIPA		;ERROR RETURN
	JRST DUMP		;FILE RENAMED
	HRRZ N,B	;ERROR CHCKING ROUTINE
	CAIE N,B	;ERROR CODE 3 MEANS FILE BEING USED. ANY OTHER ERROR
	JRST RERROR
	MOVEI N,1
	CALLI N, SLEEP
	SOSG TRYCTR
	JRST RERROR	;CANNOT ACCESS FILE
	JRST TRYGIN	;TRY AGAIN

DUMP:	SETZM	F1	;ZERO RECORD TYPE FLAG
	MOVE A,FILNAM	;GET TEST FILE NAME
	HLLZ B,EXTENS
	SETZB C,D
	LOOKUP INP,	A
	JRST RERROR
	JRST	DATE		;GO OUTPUT THE DATE RECORD


DUMP04:	SETZM	ENTRY			;ZERO OUT THE BLOCK
	TRZ	F1,LEGALTYP+DATEBIT+EOFBIT		;GET NEW RECORD
	MOVE	A,[XWD ENTRY,ENTRY+1]	;ENTRY,ENTRY+ENTSIZ-1
	BLT	A,ENTRY+ENTSIZ-1
	PUSHJ	 P,RDDSK		;GO READ FIRST WORD OF RECORD
	MOVEM	WD,ENTRY	;INSERT IT IN ENTRY (JOB NO.,TTY,SIZE OF RECORD)
	ANDI	WD,77		;GET SIZE OF RECORD
	JUMPE	WD,INEXT	;DONE CURRENT FILE
	CAIL	WD,ENTSIZ		;NO,OTHER ERROR?(MAX. # WORDS PER RECORD)
ERRREC:	JRST [TTCALL 4, BUF6
	JRST 	DUMP04]		;ILLEGAL RECORD, TRY AGAIN
	MOVNS	WD		;SET UP POINTER COUNTER IN A
	HRLZ	A,WD		;NO. OF WDS IN RECORD IN LEFT HALF
	JRST 	DUMP03		;INDEX IN RIGHT HALF
DUMP02:	PUSHJ	P,RDDSK		;GO GET NEXT WORD OF RECORD
	MOVEM	WD,ENTRY(A)	;PLACE WORD IN CORRECT PLACE IN TABLE
DUMP03:	AOBJN	A,DUMP02	;INCREMENT POINTER AND COUNTER
	MOVE	A,ENTRY+1
	HLRZ	B,ENTRY+1	;THE PROJECT (GAN) HALF
	CAIN	B,3674		;AND MAYBE MAC10
	JRST DUMP04		;YES, SO DUMP IT
			;HAVE WE GO ALL THE WORDS IN THIS RECORD??

DUMP05:	LDB	A,[POINT 9,ENTRY,8]	;GET FIRST BYTE
	MOVSI	WD,373737
	CAIN	A,377
	JRST	DUMP04

CHKTYP:	CAIN	A,200	;HK RECORD?
	TRO	F1,HKBIT	;YES
	CAIN	A,140	;OFF RECORD?
	TRO	F1,OFBIT	;YES
	CAIN	A,100	;ON RECORD?
	TRO	F1,ONBIT	;YES
	TRNN	F1,LEGALTYP	;MUST BE EITHER ON,OFF, OR HK?
	JRST	DUMP04	;IT ISN'T; GET NEW ENTRY

ONOFF:	TRNE	F1,HKBIT		;HK?
	HRRZI	WD,(SIXBIT/ HK/)	;HK RECORD
	TRNE	F1,OFBIT		;OFF RECORD?
	HRRZI	WD,(SIXBIT/ OF/)	;OFF RECORD
	TRNE	F1,ONBIT			;ON RECORD?
	HRRZI	WD,(SIXBIT/ ON/)		;ON RECORD
	MOVE	BP,[XWD 140600,WD]		;SET UP BP FOR EVERYBODY
	PUSHJ	P,SIXBT+1		;CONVERT TO ASCII AND OUPUT

JOBN:	LDB	N,[POINT 9,ENTRY,17]	;RETRIEVE THE JOB NUMBER
	MOVEI	AC,^D9
	PUSHJ	P,DECPRT		;CONVERT AND OUTPUT

LINE:	MOVE	N,ENTRY	;RETRIEVE LINE NUMBER
	ANDI	N,001700	;MASK OUT ANY OTHER BITS
	IDIVI	N,100
	MOVEI	AC,3		;THREE PLACES
	PUSHJ	P,OCTPRT	;PRINT TTY NUMBER IN OCTAL

PPN:	MOVE	WD,ENTRY+1	;RETRIEVE PROJECT NUMBER
	PUSHJ	P,SQZIT	;CONVRT TO SIXBIT
	PUSHJ	P,SIXBT
DTIME:	LDB	A,[POINT 24,ENTRY+2,35]	;GET DAYTIME IN TICS
	IMULI	A,^D50
	IDIVI	A,3
	PUSHJ	P,TIMOT1
	SETZ	A,
	TRNN	F1,ONBIT		;SKIP IF ON RECORD AND WE WANT TO WRITE ZERO

RTIME:	MOVE	A,ENTRY+3	;TRU
	ADDI	A,^D50		;ROUND UP TO NEAREST 100TH
	IDIVI	A,^D10000	; DIVIDE BY 10 TO THE 4TH
	MOVEI	AC,^D4
	MOVE	N,A
	PUSHJ	P,DECPRT
	PUSHJ	P,DOT
	IDIVI	B,^D100
	MOVE	N,B		;NOW 2 POSITION REMAINDER
	MOVEI	AC,^D10		;2 PLACES
	PUSHJ	P,DECPRT
	SETZ	Z,

KCT:	SETZ	N,	;(NO LONGER USED)
	MOVEI	AC,1		;11 POSITIONS FOR KCT
	PUSHJ	P,DECPRT

DSKBLK:	SETZ	N,		;(NO LONGER USED)
	MOVEI	AC,6		;ALOW 6 PLACES
	PUSHJ	P,DECPRT	;OUTPUT NO. OF DSK BLOCKS

DATESV:	LDB A,[POINT 12,ENTRY+2,11]	;GET DATE FROM TEST.DAT
	IDIVI A,^D31
	MOVEI N,1(B)
	MOVEI AC,^D10		;ALLOW 2 PLACES
	PUSHJ P, DECPRT
AGAIN:	PUSHJ	P,CRLF	;OUTPUT CARRIAGE RETURN
	TRNE	F1,DATEBIT+EOFBIT	;ALL DONE IF DATE OR EOF RECORD
	JRST CONTUE			;YES, WAS DATE OR EOF

	TRNE	F1,OFBIT	;OFF RECORD?
	JRST	NOACCT		;YES
	TRNN	F1,ONBIT	;CONTINUE IF ON
	JRST 	CONTUE		;ALL DONE
	MOVE	BP,[XWD 440700,ENTRY+3]
	MOVEI	C,^D12	;12 CHARACTERS IN PROJECT CODE
NXCHAR:	ILDB	CH,BP
	CAIGE	CH,40
	MOVEI	CH,40	;DUMP ALL CONTROL CHAR.
	CAILE	CH,137	;AND LOWER CASE
	MOVEI	CH,40
	PUSHJ	P,OUCH
	SOJG	C,NXCHAR	;DONE?
	PUSHJ	P,CRLF		;YES
	JRST	CONTUE		;ALL DONE ON RECORD

		;****ADD OFF RECORD WORDS,N.LATHAM,6/71
NOACCT:	MOVSI	C,-QLENGT	;# WORDS TO BE OUTPUT IN OFF REC.,LESS ONE
	HRRI	C,7		;INDEXING FACTOR, 7 WORDS ALREADY PROCESSED
	HRREI	B,-6	;# WORDS PER RECORD(LINE)
	MOVE	N,ENTRY+6	;,USETI'S
	SETZ	AC,		;ALLOW 12 PLACES
	PUSHJ	P,DECPRT
	MOVE	N,ENTRY+7	; LOOKUP AND ENTER'S
	SKIPA
OFF1:	MOVE	N,ENTRY(C)	;GET A WORD
	SETZ	AC,		;ALLOW 12 PLACES AGAIN
	PUSHJ	P,DECPRT
	AOJL	B,OFF2		;TIME FOR CR YET?
	PUSHJ	P,CRLF		;YES, OUTPUT IT
	HRREI	B,-7		;AND RESET TO COUNTER
OFF2:	AOBJN	C,OFF1		;AND ARE WE DONE ALL WORDS YET?
	CAME	B,[-7]		;DID WE JUST DO CRLF?
	PUSHJ	P,CRLF			;NO, SO DO ONE TO FINISH RECORD
CONTUE:	TRNN	F,ENDF	;ARE WE CONE?
	JRST DUMP04	;NO,GET NEXT GROUP OF RECORDS
	AOSE ENDFLG	;WAS THIS THE LAST INPUT FILE?
	JRST INEXT	;NO. GET NEXT BILLING FILE, RENAME, AND PROCESS
	MOVEI AC,BUF2	;PRINT FINAL MSG
	PUSHJ	P, OUTEX	;EXIT FROM READIN
	MOVEM	F,ENDFLG	;SAVE AC F FROM FORTRAN
	JSA	16,GBIL2	;RUN GBIL2 AND RETURN
	TTCALL	3,[ASCIZ/
PART II SUCCESSFULLY COMPLETED.
/]
	MOVE	F,ENDFLG	;RESTORE F
	CALLI EXIT


SIXBT:	MOVE	BP,[XWD 440600,WD]	;SET UP BYTE POINTER
	ILDB	CH,BP		;LOAD BYTE
	ADDI	CH,40		;CONVERT TO ASCII
	PUSHJ	P,OUCH		;GO TO OUTPUT CHARACTER
	TLNE	BP,770000	;ARE WE DONE?
	JRST	SIXBT+1		;NO,GO DO NEXT BYTE
	POPJ	P,0		;YES,RETURN

DECPRT:	MOVEI	R,12	;RADIX
	CAIG	N,0		;IS IT ZERO?
	JRST	ZEROUT		;YES, GO OUTPUT IT
CHECKD:	CAMLE	N,TABD(AC)
	JRST	RDXPRT
	PUSHJ	P,SPACE
	ADDI	AC,1
	JRST	CHECKD

OCTPRT:	MOVEI	R,10
CHECKO:	CAMLE	N,TABO(AC)	;DO WE NEED A SPACE??
	JRST RDXPRT		;NO,GO  CONVERT
	CAMN	N,TABO(AC)
	JUMPE	N,RDXPRT	;PRINT IF VALUE IS 0 AND TABO REFERENCE IS ALSO 0
	PUSHJ	P,SPACE		;YES,GO OUTPUT ONE
	ADDI	AC,1		;INCREMENT INDEX
	JRST CHECKO		;GO CHECK AGAIN


PRNT:
RDXPRT:	MOVMS	N		;GET MAGNITUDE
	IDIVI	N,(R)
	HRLM	N1,0(P)
	SKIPE	N
	PUSHJ 	P,RDXPRT
	HLRZ	CH,0(P)
	ADDI	CH,"0"
	CAIN	Z,0
	JRST	OUCH
	CAIN	Z,1
	POPJ	P,0
	SUBI	Z,1
OUCH:	SOSG	 OBUF+2		;BUFFER FULL?
	OUTPUT	OUT,0		;YES, OUTPUT IT
	IDPB	CH,OBUF+1	;NO, INCREMENT POINTER
	POPJ	P,0		;RETURN

	PAGE
DATOUT:	MOVEI	AC,^D10		;PRINT 2 PLACES
	PUSHJ	P,DECPRT
	POPJ	P,0		;RETURN
TIMOT1:	IDIV	A,[EXP ^D60000*^D60]
	MOVE	N,A		;HOURS ARE IN N
	PUSHJ	P,DATOUT		;OUTPUT HOURS
	MOVE	A,B		;GET REMAINDER IN A
	IDIVI	A,^D60000	;DIVIDE TO GET MINUTES
	MOVE	N,A
	PUSHJ	P,DATOUT	;OUTPUT MINUTES
	POPJ	P,0
TIMOT2:	MOVE	N,B		;GET REMAINDER
	IDIVI	N,^D1000	;DIVIDE TO GET SECONDS
	MOVEI	AC,^D10
	PUSHJ	P,DECPRT
	POPJ	P,0

MINUS:	CAIN AC,^D10		;ENOUGH SPACES?
	JRST .+4
	PUSHJ P, SPACE
	ADDI AC,1
	JRST MINUS
	MOVEI CH,55	;PRINT '-'
	PUSHJ P, OUCH
	MOVEI CH, 61	;PRINT '1',UNLESS N TURNS OUT TO BE -2
	CAIN N,7776		;IS IT MINUS 2?
	MOVEI CH,62	;YES SO CHANGE CH ACCORDINGLY
	PUSHJ P, OUCH
	POPJ P,0


ZEROUT:	CAIN	AC,^D11		;ENOUGH SPACES?
	JRST	.+4	;YES, PRINT ZERO
	PUSHJ	P,SPACE
	ADDI	AC,1
	JRST ZEROUT
	MOVEI	CH,60	;OUTPUT ZERO
	PUSHJ	P,OUCH
	POPJ	P,0
DOT:	MOVEI	CH,56	;PERIOD
	JRST 	OUCH	;RETURNS TO CALLER

CRLF:	MOVEI	CH,15	;CR
	PUSHJ	P,OUCH
	MOVEI	CH,12	;LF
	JRST OUCH	;OUTPUT AND RETURNS FROM THERE

DATE:	TRO	F1,ONBIT+DATEBIT	;DATE RECORD LOOKS LIKE ON RECORD
	SETZM	ENTRY
	MOVE	A,[XWD ENTRY,ENTRY+1]
	BLT	A,ENTRY+ENTSIZ-1
	HRRZI	WD,(SIXBIT/ DA/)
	MOVE BP,[XWD 140600,WD]
	PUSHJ P,SIXBT+1	;OUTPUT'DA'
	MOVE	CH,MSG2+6	;THE SITE
	PUSHJ	P,OUCH
	MOVEI	AC,0
	PUSHJ P, DTOUT+1
	JRST DATE3
DATE2:	MOVE	BP,[XWD 140600,WD]
	PUSHJ	P,SIXBT+1
	MOVEI	AC,0
	PUSHJ	P,DTOUT
DATE3:	PUSHJ	P,DTOUT
	SETZM	WD
	MOVE	BP,[XWD 220600,WD]
	PUSHJ	P,SIXBT+1
	PUSHJ	P,DTOUT
	JRST DTIME
DTOUT:	PUSHJ	P,SPACE
	ADDI	AC,1
	MOVE	BP,[XWD 070700,MSG2-1(AC)]
	ILDB	CH,BP
	PUSHJ	P,OUCH
	TRNE	AC,1	;SKIPS EVERY OTHER TIME
	JRST	DTOUT+1
	POPJ	P,
SPACE:	MOVEI	CH,40
	JRST OUCH

RDDSK:	SOSLE	DSKBUF+2	;DECREMENT BYTE COUNTER - IS BUFFER EMPTY?
	JRST	DSKOK		;NO,GET NEXT BYTE
	INPUT	INP,0		;YES, REFILL BUFFER
	STATZ	INP,760000	;CHECK FOR ERRORS
	JRST	END

DSKOK:	ILDB	WD,DSKBUF+1	;GET NEXT BYTE
	POPJ	P,

END:	TRO	F,ENDF	;SET FOR ONE CMORE
	JRST DATE

LAST:	TRO	F1,EOFBIT+ONBIT	;EOF LOOKS LIKE ON RECCRD FIRST LINE
	SETZM ENTRY
	MOVE A, [XWD ENTRY, ENTRY+1]
	BLT A, ENTRY+ENTSIZ-1
	HRRZI WD,(SIXBIT/ XX/)
	TRO	F,ENDF
	SETOM ENDFLG
	JRST DATE2		;WRITE EOF RECORD

OUTEX:	CALL	AC,[SIXBIT/DDTOUT/]
	RELEASE	OUT,0
	RELEASE	INP,0
	POPJ	P,0


RERROR:	MOVEI	AC, BUF1
	PUSHJ	P, OUTEX
	CALLI EXIT
DERROR:	MOVEI AC,BUF5
	PUSHJ	P, OUTEX
	CALLI EXIT
ERRDSK:	MOVEI AC, BUF7
	PUSHJ	P, OUTEX
	CALLI EXIT
NOFILE:	MOVEI AC,BUF4
	PUSHJ	P, OUTEX
	CALLI	EXIT
	PAGE
;STORAGE

DIREC2:ASCIZ/TYPE SITE WHERE THIS FACT.SYS WAS CREATED
AS 2 DIGITS (31 - 35): /
MSG1:	ASCIZ/TYPE DATE OF BILLING AS "MMDDYY"
/
SORTBK:	SIXBIT/SYS/
	SIXBIT/SORT/
	SIXBIT/SAV/
	EXP	0,0,0
	0
ENDFLG:	0	;FLAG TO MARK PROCESSING OF LAST FILE
EXTENS:	0		;TEMP STORAGE FOR EXTENSION
MSG2:	BLOCK	21
OBUF:	BLOCK	3		;SPACE FOR OUTPUT BUFFER HEADER
DSKBUF:	BLOCK	3		;SPACE FOR INPUT BUFFER HEADER
OPENBK:	14	;BINARY
	SIXBIT/FOO/
	XWD	0,DSKBUF
FILNAM:	0	;SAVE TODAY'S TEST FILE NAME HERE
DATLST:	BLOCK 100
EXTLST:	BLOCK 100
TABD:	^D99999999999	;(AC=0) = D12 PLACES
	^D9999999999	;(AC=1) = D11 PLACES
	^D999999999	;(AC=2) = D10 PLACES

	^D99999999	;(AC=3) =D9 PLACES
	^D9999999	;(AC=4) = D8 PLACES
	^D999999		;(AC=5) = D7 PLACES
	^D99999	;(AC=6), 6 PLACES
	^D9999	;(AC=7),5 PLACES
	^D999	;(AC=8),4 PLACES
	^D99	;(AC=9),3 PLACES
	^D9	;(AC=10), 2 PLACES
	0	;END OF TABLE


TABO:	77777
	7777
	777
	77
	7
	0
BUF1:	ASCIZ/
CANNOT RENAME OR LOOKUP TEST FILE.  DID YOU ENTER CORRECT DATE?
IF NOT TYPE ^C AND START AGAIN. NOTIFY S.Q.A. IF THE PROBLEM
CONTINUES.

/
BUF2:ASCIZ/
PART I SUCCESSFULLY COMPLETED.  CONTINUING WITH PART II...
 /
BUF4:	ASCIZ/
NO TEST FILE WAS FOUND.  NOTIFY S.Q.A.  CREAD ABORTED./
BUF5:	ASCIZ/
DAILY.DAT INIT OR ENTER FAILED.  NOTIFY S.Q.A. RUN ABORTED.
/
BUF6:	ASCIZ/
IN FACT.SYS, RECORD SIZE IS ILLEGAL.  NOTIFY S.Q.A. RUN CONTINUES.
/
BUF7:	ASCIZ/
DSK BUFFER CANNOT BE OPENED ON CHANNEL. NOTIFY S.Q.A.  RUN ABORTED.
/
ENTRY:	BLOCK	ENTSIZ


;SUBTTL DSKSTR - WRITES AN ASCII FFILE OF DSK STORAGE USING THE
;MAXIMUM DAILY STORAGE FOUND IN UFD. THEN REPLACES THIS VALUE IN
;THE UFD WITH ACTUAL DSK STORG.
; N. LATHAM, DECEMBER 1971


DSKSTR:	INIT	OUTPT,0
	SIXBIT/DSK/
	XWD	OBUF,0
	JRST FILERR
	MOVE	A,[SIXBIT/STORG/]
	MOVE	C,MSG2+6		;GET SITE
	SUBI	C,40		;CONVERT TO 6BIT
	DPB	C,[POINT 6,A,29]
	MOVSI	B,(SIXBIT/DAT/)
	SETZB	C,D
	ENTER	OUTPT,A
	JRST FILERR

	INIT	MFD,14
	SIXBIT/SYS/
	EXP MFDBF
	JRST	FILERR
	MOVE	A,UFDPP
	MOVSI	B,(SIXBIT/UFD/)
	MOVEI	C,0
	MOVE	D,UFDPP
GETMFD:	LOOKUP	MFD,A
	JRST  FILERR

	INIT	UFD,14
	SIXBIT/SYS/
	XWD	0,UFDBF
	JRST FILERR
PRTDAT:	PUSHJ	P,SPACE
	PUSHJ	P,SPACE
	PUSHJ	P,SPACE
	HRLZI	AC,-6
NXTDAT:	MOVE	BP,[XWD 070700,MSG2(AC)]
	ILDB	CH,BP
	PUSHJ	P,OUCH
	AOBJN	AC,NXTDAT
	PUSHJ	P,CRLF
READMF:	PUSHJ	P,READM
	JRST	DONEIT
	JUMPE	WD,READMF
	MOVEM	WD,UFDBLK+2	;PROG/PROG.#
	MOVE	WD,UFDPP
	MOVEM	WD,UFDBLK+1
	PUSHJ	P,READM
	JRST DONEIT
	HLRZS	WD
	CAIE	WD,(SIXBIT/UFD/)
	JRST READMF
	HRLZM	WD,UFDBLK+3	;SET UP EXT.
	MOVEI	N,32
	MOVEM	N,UFDBLK	;NO. ARG FOR LOOKUP
GETUFD:	LOOKUP	UFD,UFDBLK
	JRST NOUFD
	PUSHJ	P,SPACE
	MOVE	WD,UFDBLK+2
	PUSHJ	P,SQZIT		;CONVERT TO SIXBIT
	PUSHJ	P,SIXBT	;OUTPT THE USER NAME
	PUSHJ	P,SPACE
	MOVE	CH,MSG2+6		;GET SITE
	PUSHJ	P,OUCH
	MOVE	N,UFDBLK+25	;MAXIMUM STOR.TODAY
	SETZ	AC,	;FOR 9 PLACES
	PUSHJ	P,DECPR2	;PRINT IT
	PUSHJ	P,CRLF
	PUSHJ	P,READM
	JFCL
	PUSHJ	P,READM
	JFCL
	PUSHJ	P,READM
	JFCL
	JRST RETURN	;TEMP FOR DEBUG.
UPDATE:	MOVE	N,UFDBLK+25
	MOVEM	N,UFDBLK+24
	RENAME	UFD,UFDBLK
	JRST  FILERR
RETURN:	JRST READMF

DECPR2:	MOVEI	R,12	;RADIX
	CAIG	N,0
	JRST	ZERO
	CAMLE	N,TABDD(AC)
	JRST RDXPRT
	PUSHJ	P,SPACE
	ADDI	AC,1
	JRST	.-4

ZERO:	CAIN	AC,^D8	;NO.PLACES-1
	JRST	.+4
	PUSHJ	P,SPACE
	ADDI	AC,1
	JRST	ZERO
	MOVEI	CH,60
	JRST OUCH

READM:	SOSG	MFDBF+2
	INPUT	MFD,0
	ILDB	WD,MFDBF+1
	STATO	MFD,EOF
	AOS	(P)
	POPJ	P,0

SQZIT:	PUSHJ	P,.+1
	MOVE	A,WD
	MOVEI	WD1,3
	HRRZ	B,A
SQEEZ:	IDIVI	B,50
	EXCH	B,C
	CAIL	B,13
	ADDI	B,41-13-20
	ADDI	B,20
	CAIN	B,32
	MOVEI	B,0
	ROTC	A,-6
	MOVE	B,C
	SOJG	WD1,SQEEZ
	MOVE	WD,A
	POPJ	P,0



FILERR:	TTCALL	3,[ASCIZ/ERROR IN OPEN OR LOOKUP OF A FILE
/]
	CALLI	12

NOUFD:	TTCALL	3,[ASCIZ/CAN'T FIND UFD
/]
	JRST READMF

DONEIT:	PUSHJ	P,SPACE
	MOVE	WD,[SIXBIT/ZZZZZZ/]
	PUSHJ	P,SIXBT
	PUSHJ	P,CRLF
	CLOSE OUTPT,
	STATZ	OUTPT,740000
	JRST	CLSERR
	RELEASE MFD,
	RELEASE UFD,
	RELEASE OUTPT,
	TTCALL	3,[ASCIZ/
STORAGE RUN COMD.  CONTINUING WITH PART I...
/]
	POPJ	P,0

CLSERR:	TTCALL	3,[ASCIZ /ERROR CLOSING STORAGE FILE.  NOTIFY PROGRAMMER.
/]
	CALLI 12

DEFINE MONMAC(A)<	IRP A,<A>>
MONTAB:	MONMAC<1,2,3,4,5,6,7,^D8,^D9,^D10,^D11,^D12>
TABDD:	^D99999999
	^D9999999
	^D999999
	^D99999
	^D9999
	^D999
	^D99
	^D9
	0
MFDBF:	BLOCK 3
UFDBF:	BLOCK 3
UFDBLK:	BLOCK 34
PDP:	XWD -20,.
	BLOCK 20
UFDPP:	XWD 1,1
	END CREAD
 Q t¾