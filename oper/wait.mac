TITLE	WAIT - HACK PROGRAM TO WAIT FOR A FILE

A0=0
A1=1
A2=2
A3=3
A4=4
A5=5
A6=6
A7=7
A10=10
A11=11
A12=12
A13=13
A14=14
A15=15
A16=16
SP=17

	SLEEP=31
	CORE=11


	MAJVER=1		; MAJOR VERSION
	EDITNO=12		; EDIT NUMBER

	LOC	137
	EXP MAJVER*1B11+EDITNO
	RELOC

	PURGE	MAJVER,EDITNO




	EXTERN	.JBFF
	EXTERN	%INIT,INPT,RELDEV,CLOSEFILE,OPENFILE,TRANSFILE,SELIN
	EXTERN	SELOUT,OUTPT,ERRCON,WRITE,INSYMBOL,OUTSYMBOL,CORGET
	EXTERN	GETLIN

	TWOSEG	400000
START:	RESET
	MOVEI	A2,^D40		; STACK SIZE
	JSP	A1,%INIT	; INITIALIZE STACK AND PROCEDURE

BEGIN:	MOVEI	A11,^D1800	; SET UPPER TIME LIMIT TO 30 MINUTES
	MOVEI	A1,1		; CHANNEL 1
	MOVEI	A2,0		; ASCII MODE
	MOVSI	A3,'DSK'
	SETZB	A4,A5		; DEFAULT BUFFERS
	PUSHJ	SP,INPT		; TRY TO OPEN THE FILE
	JRST	ERROR

LOOK:	MOVEI	A1,ENTMSG
	PUSHJ	SP,WRITE
	JRST	ERROR
	SETZB	A2,A3
	MOVEI	A6,6		; COUNT
	MOVEI	A7,A2		; GET FILENAME INTO A2
	TLO	A7,440600	; SIXBIT BP

LOOP:	PUSHJ	SP,GETLIN	; GO GET TTY CHARACTER
	JFCL		; ERROR RETURN
	JRST	ERROR	; (EOF) ERROR RETURN
	CAIN	A13,^D10	; LINEFEED?
	JRST	NAMOUT		; YES
	CAIN	A13,"."		; DOT?
	JRST	EXTGET
	JUMPLE	A6,LOOP		; SCAN?
	SUBI	A13,40		; MAKE SIXBIT
	IDPB	A13,A7
	SOJA	A6,LOOP

EXTGET:	MOVEI	A7,A3
	TLO	A7,440600
	MOVEI	A6,3
	JRST	LOOP

NAMOUT:	OUTSTR	WTMSG		; WAITING FOR
	MOVE	A5,A2		; RELOCATE FILENAME
	MOVEI	A1,A5
	HRLI	A1,1		; SIXBIT
	MOVEI	A6,0
	PUSHJ	SP,WRITE	; WRITE FILENAME
	JRST	ERROR
	OUTSTR	DOT
	MOVEI	A1,A3		; EXTENSION
	HRLI	A1,1
	PUSHJ	SP,WRITE
	JRST	ERROR
	OUTSTR	CRLF

LOKFIL:	MOVEI	A1,1		; CHANNEL
	SETZB	A4,A5		; MY OWN PPN
	PUSHJ	SP,OPENFILE
	JRST	SLPQUE		; FILE NOT FOUND
	MOVEI	A1,FNDMSG
	PUSHJ	SP,WRITE
	JRST	ERROR
	MOVEI	A2,^D30		; TIME OUT FOR 3 MINUTES
	MOVEI	A1,6

TIM:	CALLI	A2,SLEEP
	SOJG	A1,TIM		; DONE?
	EXIT

SLPQUE:	MOVEI	A4,^D30		; SLEEP FOR 30 SECONDS
	SUB	A11,A4		; SUBTRACT TIME
	CAIG	A11,0		; SPUN OUR WHEELS ENOUGH?
	JRST	LNGWAT		; YES, BOMB OUT!
	CALLI	A4,SLEEP
	JRST	LOKFIL		; TRY AGAIN

LNGWAT:	OUTSTR	LNGMSG
	EXIT

LNGMSG:	ASCIZ	/
? FILE NOT FOUND AFTER WAITING 30 MINUTES ...
/
ENTMSG:	ASCIZ	/ENTER FILENAME: /
FNDMSG:	ASCIZ	/FILE FOUND - TIMING OUT FOR 3 MINUTES
/


WTMSG:	ASCIZ	/WAITING FOR /
DOT:	ASCIZ	/./
CRLF:	ASCIZ	/
/
ERROR:	PUSHJ	SP,ERRCON
	EXIT
	END	START
